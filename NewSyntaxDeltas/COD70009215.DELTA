OBJECT Codeunit 70009215 G/L Source Name Assisted Setup
{
  OBJECT-PROPERTIES
  {
    Date=20170222D;
    Time=205432T;
    Version List=GLSN10.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Setup@1000000000 : Record "G/L Source Name Setup";
      GLSourceNamesTxt@1000000001 : TextConst 'ENU=Set up G/L Source Names';
      HelpResource@1000000002 : Record "G/L Source Name Help Resource";
      RequiredPermissionMissingErr@70009200 : TextConst 'ENU=You have not been granted required access rights to start the Assisted Setup.\\The Assisted Setup for G/L Source Names is about assigning the required permissions to users.  To be able to assign permissions you need to be granted either the SUPER og SECURITY permission set.';

    PROCEDURE VerifyUserAccess@8();
    VAR
      AccessControl@70009200 : Record "Access Control";
    BEGIN
      with AccessControl do begin
        SETRANGE("User Security ID",USERSECURITYID);
        SETFILTER("Role ID",'%1|%2','SUPER','SECURITY');
        if ISEMPTY then
          ERROR(RequiredPermissionMissingErr);
      end;
    END;

    [EventSubscriber(Table,1808,OnRegisterAssistedSetup,"",Skip)]
    LOCAL PROCEDURE OnRegisterAssistedSetup@1(VAR TempAggregatedAssistedSetup@70009200 : TEMPORARY Record "Aggregated Assisted Setup");
    BEGIN
      if not Setup.WRITEPERMISSION then exit;
      if not HelpResource.WRITEPERMISSION then exit;
      InitializeSetup;
      AddToAssistedSetup(TempAggregatedAssistedSetup);
    END;

    [EventSubscriber(Table,1808,OnUpdateAssistedSetupStatus,"",Skip)]
    LOCAL PROCEDURE OnUpdateAssistedSetupStatus@2(VAR TempAggregatedAssistedSetup@70009200 : TEMPORARY Record "Aggregated Assisted Setup");
    BEGIN
      Setup.GET;
      with TempAggregatedAssistedSetup do
        SetStatus(TempAggregatedAssistedSetup,PAGE::"G/L Source Name Setup Wizard",Setup.Status);
    END;

    LOCAL PROCEDURE InitializeSetup@4();
    BEGIN
      with Setup do
        if ISEMPTY then begin
          INIT;
          INSERT;
        end else
          GET;

      with HelpResource do
        InitializeResources;
    END;

    LOCAL PROCEDURE AddToAssistedSetup@6(VAR TempAggregatedAssistedSetup@70009200 : TEMPORARY Record "Aggregated Assisted Setup");
    VAR
      TempBlob@70009201 : Record TempBlob;
      GLSourceNameIcon@70009203 : Codeunit "G/L Source Name Icon 240x240";
      InStr@70009202 : InStream;
    BEGIN
      with TempAggregatedAssistedSetup do begin
        GLSourceNameIcon.GetIcon(TempBlob);
        TempBlob.Blob.CREATEINSTREAM(InStr);
        InsertAssistedSetupIcon(HelpResource.Get240PXIconCode,InStr);

        AddExtensionAssistedSetup(
          PAGE::"G/L Source Name Setup Wizard",
          GLSourceNamesTxt,
          true,
          Setup.RECORDID,
          Setup.Status,
          HelpResource.Get240PXIconCode);
      end;
    END;

    BEGIN
    END.
  }
}

