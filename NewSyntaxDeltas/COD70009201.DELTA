OBJECT Codeunit 70009201 G/L Source Name Mgt
{
  OBJECT-PROPERTIES
  {
    Date=20170225D;
    Time=091848T;
    Version List=GLSN10.0;
  }
  PROPERTIES
  {
    Permissions=TableData "G/L Source Name"=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Window@70013401 : Dialog;
      SourceType@70013400 : '" ",Customer,Vendor,"Bank Account","Fixed Asset"';
      ProcessInfo@70013402 : TextConst 'ENU=Populating G/L Source Names...';
      RequiredPermissionMissingErr@1000000000 : TextConst 'ENU=Required permissions for table "%1" is missing.  Can''t refresh lookup data.';
      ProcessCompleted@70009200 : TextConst 'ENU=G/L Source Names lookup table update finished';

    PROCEDURE AddSource@1035(SourceType@1020 : Option;SourceNo@1021 : Code[20];SourceName@1022 : Text[50]);
    VAR
      GLSourceName@1023 : Record "G/L Source Name";
    BEGIN
      with GLSourceName do begin
        if not WRITEPERMISSION then exit;
        if GET(SourceType,SourceNo) then
          UpdateSource(SourceType,SourceNo,SourceName)
        else begin
          INIT;
          "Source Type" := SourceType;
          "Source No." := SourceNo;
          "Source Name" := SourceName;
          INSERT;
        end;
      end;
    END;

    PROCEDURE RemoveSource@1036(SourceType@1022 : Option;SourceNo@1021 : Code[20]);
    VAR
      GLSourceName@1023 : Record "G/L Source Name";
    BEGIN
      with GLSourceName do begin
        if not WRITEPERMISSION then exit;
        if GET(SourceType,SourceNo) then
          DELETE;
      end;
    END;

    PROCEDURE UpdateSource@1037(SourceType@1022 : Option;SourceNo@1021 : Code[20];SourceName@1020 : Text[50]);
    VAR
      GLSourceName@1023 : Record "G/L Source Name";
    BEGIN
      with GLSourceName do begin
        if not WRITEPERMISSION then exit;
        if GET(SourceType,SourceNo) then begin
          "Source Type" := SourceType;
          "Source No." := SourceNo;
          "Source Name" := SourceName;
          MODIFY;
        end else
          AddSource(SourceType,SourceNo,SourceName);
      end;
    END;

    PROCEDURE Refresh@1000000000(HideMessage@70009200 : Boolean);
    VAR
      GLSourceName@1000000004 : Record "G/L Source Name";
      Customer@1000000003 : Record Customer;
      Vendor@1000000002 : Record Vendor;
      BankAccount@1000000001 : Record "Bank Account";
      FixedAsset@1000000000 : Record "Fixed Asset";
    BEGIN
      with GLSourceName do
        if not WRITEPERMISSION then
          ERROR(RequiredPermissionMissingErr);
      with Customer do
        if not READPERMISSION then
          ERROR(RequiredPermissionMissingErr);
      with Vendor do
        if not READPERMISSION then
          ERROR(RequiredPermissionMissingErr);
      with BankAccount do
        if not READPERMISSION then
          ERROR(RequiredPermissionMissingErr);
      with FixedAsset do
        if not READPERMISSION then
          ERROR(RequiredPermissionMissingErr);
      GLSourceName.DELETEALL;
      PopulateSourceTable;
      if not HideMessage then
        MESSAGE(ProcessCompleted);
    END;

    PROCEDURE PopulateSourceTable@1042();
    VAR
      GLSourceName@1020 : Record "G/L Source Name";
      Customer@1021 : Record Customer;
      Vendor@1022 : Record Vendor;
      BankAccount@1023 : Record "Bank Account";
      FixedAsset@1024 : Record "Fixed Asset";
    BEGIN
      if GLSourceName.WRITEPERMISSION and
        Customer.READPERMISSION and
        Vendor.READPERMISSION and
        BankAccount.READPERMISSION and
        FixedAsset.READPERMISSION
      then begin
        if not GLSourceName.ISEMPTY then exit;
        if Customer.ISEMPTY and Vendor.ISEMPTY and BankAccount.ISEMPTY and FixedAsset.ISEMPTY then exit;
        Window.OPEN(ProcessInfo);
        AddSourceTable(SourceType::Customer,Customer);
        AddSourceTable(SourceType::Vendor,Vendor);
        AddSourceTable(SourceType::"Bank Account",BankAccount);
        AddSourceTable(SourceType::"Fixed Asset",FixedAsset);
        Window.CLOSE;
      end;
    END;

    PROCEDURE AddSourceTable@1044(SourceType@1021 : Option;RecVariant@1020 : Variant);
    VAR
      RecRef@1022 : RecordRef;
      NoField@1024 : FieldRef;
      NameField@1023 : FieldRef;
    BEGIN
      with RecRef do begin
        GETTABLE(RecVariant);
        if FINDSET then repeat
          NoField := FIELD(1);
          NameField := FIELD(2);
          AddSource(SourceType,FORMAT(NoField.VALUE),FORMAT(NameField.VALUE));
        until NEXT = 0;
      end;
    END;

    BEGIN
    END.
  }
}

