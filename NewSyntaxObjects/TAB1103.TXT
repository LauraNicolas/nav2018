OBJECT Table 1103 Cost Type
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=;
    DataCaptionFields="No.",Name;
    OnInsert=BEGIN
               Modified;
             END;

    OnModify=BEGIN
               Modified;
             END;

    OnDelete=VAR
               GLAccount@1000 : Record "G/L Account";
               CostEntry@1001 : Record "Cost Entry";
               CostBudgetEntry@1002 : Record "Cost Budget Entry";
             BEGIN
               // Message if balance  <> 0
               if Type = Type::"Cost Type" then begin
                 CALCFIELDS(Balance);
                 TESTFIELD(Balance,0);
               end;

               // Error if movement in not closed fiscal year
               CostEntry.SETRANGE("Cost Type No.","No.");
               AccPeriod.SETRANGE(Closed,false);
               if AccPeriod.FINDFIRST then
                 CostEntry.SETFILTER("Posting Date",'>=%1',AccPeriod."Starting Date");
               if not CostEntry.ISEMPTY then
                 ERROR(Text000);

               // Renumber to entries to no. 0
               CostEntry.RESET;
               CostEntry.SETCURRENTKEY("Cost Type No.");
               CostEntry.SETRANGE("Cost Type No.","No.");
               CostEntry.MODIFYALL("Cost Type No.",'');

               CostBudgetEntry.SETCURRENTKEY("Budget Name","Cost Type No.");
               CostBudgetEntry.SETRANGE("Cost Type No.","No.");
               CostBudgetEntry.DELETEALL;

               GLAccount.SETRANGE("Cost Type No.","No.");
               GLAccount.MODIFYALL("Cost Type No.",'');
             END;

    OnRename=BEGIN
               "Modified Date" := TODAY;
             END;

    CaptionML=ENU=Cost Type;
    LookupPageID="Chart of Cost Types";
    DrillDownPageID="Chart of Cost Types";
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;AltSearchField=Search Name;
                                                   CaptionML=ENU=No.;
                                                   NotBlank=true }
    { 2   ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                "Search Name" := Name;
                                                              END;

                                                   CaptionML=ENU=Name }
    { 3   ;   ;Search Name         ;Code50        ;CaptionML=ENU=Search Name }
    { 4   ;   ;Type                ;Option        ;OnValidate=VAR
                                                                CostEntry@1000 : Record "Cost Entry";
                                                                CostBudgetEntry@1001 : Record "Cost Budget Entry";
                                                              BEGIN
                                                                // Blocked if <> account
                                                                if Type <> xRec.Type then
                                                                  Blocked := Type <> Type::"Cost Type";

                                                                // CHange only if no entries or budget
                                                                if Blocked and not xRec.Blocked then begin
                                                                  CostEntry.SETRANGE("Cost Type No.","No.");
                                                                  if not CostEntry.ISEMPTY then
                                                                    ERROR(Text001,"No.",CostEntry.TABLECAPTION);
                                                                  CostBudgetEntry.SETRANGE("Cost Type No.","No.");
                                                                  if not CostBudgetEntry.ISEMPTY then
                                                                    ERROR(Text001,"No.",CostBudgetEntry.TABLECAPTION);
                                                                end;

                                                                Totaling := '';
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Cost Type,Heading,Total,Begin-Total,End-Total;
                                                   OptionString="Cost Type",Heading,Total,"Begin-Total","End-Total" }
    { 6   ;   ;Cost Center Code    ;Code20        ;TableRelation="Cost Center";
                                                   CaptionML=ENU=Cost Center Code }
    { 7   ;   ;Cost Object Code    ;Code20        ;TableRelation="Cost Object";
                                                   CaptionML=ENU=Cost Object Code }
    { 10  ;   ;Combine Entries     ;Option        ;CaptionML=ENU=Combine Entries;
                                                   OptionCaptionML=ENU=None,Day,Month;
                                                   OptionString=None,Day,Month }
    { 13  ;   ;Blocked             ;Boolean       ;CaptionML=ENU=Blocked }
    { 17  ;   ;New Page            ;Boolean       ;CaptionML=ENU=New Page }
    { 18  ;   ;Blank Line          ;Boolean       ;CaptionML=ENU=Blank Line;
                                                   MinValue=false;
                                                   BlankZero=true }
    { 19  ;   ;Indentation         ;Integer       ;CaptionML=ENU=Indentation;
                                                   MinValue=0 }
    { 20  ;   ;Comment             ;Text50        ;CaptionML=ENU=Comment }
    { 22  ;   ;Cost Classification ;Option        ;CaptionML=ENU=Cost Classification;
                                                   OptionCaptionML=ENU=" ,Fixed,Variable,Step Variable";
                                                   OptionString=" ",Fixed,Variable,"Step Variable" }
    { 23  ;   ;Fixed Share         ;Text30        ;CaptionML=ENU=Fixed Share }
    { 26  ;   ;Modified Date       ;Date          ;CaptionML=ENU=Modified Date;
                                                   Editable=false }
    { 27  ;   ;Modified By         ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit "User Management";
                                                              ModifiedBy@1001 : Code[50];
                                                            BEGIN
                                                              ModifiedBy := "Modified By";
                                                              UserMgt.LookupUserID(ModifiedBy);
                                                            END;

                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Modified By;
                                                   Editable=false }
    { 28  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
    { 29  ;   ;Cost Center Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Center";
                                                   CaptionML=ENU=Cost Center Filter }
    { 30  ;   ;Cost Object Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Object";
                                                   CaptionML=ENU=Cost Object Filter }
    { 31  ;   ;Balance at Date     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE ("Cost Type No."=FIELD("No."),
                                                                                              "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                              "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                              "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                              "Posting Date"=FIELD(UPPERLIMIT("Date Filter"))));
                                                   CaptionML=ENU=Balance at Date;
                                                   BlankZero=true;
                                                   Editable=false }
    { 32  ;   ;Net Change          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE ("Cost Type No."=FIELD("No."),
                                                                                              "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                              "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                              "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                              "Posting Date"=FIELD("Date Filter")));
                                                   CaptionML=ENU=Net Change;
                                                   BlankZero=true;
                                                   Editable=false }
    { 33  ;   ;Budget Amount       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Budget Entry".Amount WHERE ("Cost Type No."=FIELD("No."),
                                                                                                     "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                                     "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                                     "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                                     Date=FIELD("Date Filter"),
                                                                                                     "Budget Name"=FIELD("Budget Filter")));
                                                   CaptionML=ENU=Budget Amount;
                                                   BlankZero=true }
    { 34  ;   ;Totaling            ;Text250       ;TableRelation="Cost Type";
                                                   OnValidate=BEGIN
                                                                if not (Type in [Type::Total,Type::"End-Total"]) then
                                                                  FIELDERROR(Type);

                                                                CALCFIELDS("Net Change");
                                                              END;

                                                   OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              if LookupCostTypeFilter(SelectionFilter) then
                                                                VALIDATE(Totaling,COPYSTR(SelectionFilter,1,MAXSTRLEN(Totaling)));
                                                            END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Totaling }
    { 35  ;   ;Budget Filter       ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Budget Name";
                                                   CaptionML=ENU=Budget Filter }
    { 36  ;   ;Balance             ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE ("Cost Type No."=FIELD("No."),
                                                                                              "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                              "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                              "Cost Object Code"=FIELD("Cost Object Filter")));
                                                   CaptionML=ENU=Balance;
                                                   BlankZero=true;
                                                   Editable=false }
    { 37  ;   ;Budget at Date      ;Decimal       ;CaptionML=ENU=Budget at Date;
                                                   Editable=false }
    { 40  ;   ;G/L Account Range   ;Text50        ;TableRelation="G/L Account";
                                                   OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              if LookupGLAccFilter(SelectionFilter) then
                                                                VALIDATE("G/L Account Range",COPYSTR(SelectionFilter,1,MAXSTRLEN("G/L Account Range")));
                                                            END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=G/L Account Range }
    { 47  ;   ;Debit Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry"."Debit Amount" WHERE ("Cost Type No."=FIELD("No."),
                                                                                                      "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                                      "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                                      "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                                      "Posting Date"=FIELD("Date Filter")));
                                                   CaptionML=ENU=Debit Amount }
    { 48  ;   ;Credit Amount       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry"."Credit Amount" WHERE ("Cost Type No."=FIELD("No."),
                                                                                                       "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                                       "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                                       "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                                       "Posting Date"=FIELD("Date Filter")));
                                                   CaptionML=ENU=Credit Amount }
    { 51  ;   ;Balance to Allocate ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE ("Cost Type No."=FIELD("No."),
                                                                                              "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                              "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                              Allocated=CONST(false),
                                                                                              "Posting Date"=FIELD("Date Filter")));
                                                   CaptionML=ENU=Balance to Allocate;
                                                   BlankZero=true;
                                                   Editable=false }
    { 60  ;   ;Budget Debit Amount ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Budget Entry".Amount WHERE ("Cost Type No."=FIELD("No."),
                                                                                                     "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                                     "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                                     "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                                     Date=FIELD("Date Filter"),
                                                                                                     "Budget Name"=FIELD("Budget Filter")));
                                                   CaptionML=ENU=Budget Debit Amount;
                                                   BlankNumbers=BlankNegAndZero }
    { 72  ;   ;Budget Credit Amount;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Cost Budget Entry".Amount WHERE ("Cost Type No."=FIELD("No."),
                                                                                                      "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                                      "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                                      "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                                      Date=FIELD("Date Filter"),
                                                                                                      "Budget Name"=FIELD("Budget Filter")));
                                                   CaptionML=ENU=Budget Credit Amount;
                                                   BlankNumbers=BlankNegAndZero }
    { 73  ;   ;Add. Currency Net Change;Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry"."Additional-Currency Amount" WHERE ("Cost Type No."=FIELD("No."),
                                                                                                                    "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                                                    "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                                                    "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                                                    "Posting Date"=FIELD("Date Filter")));
                                                   CaptionML=ENU=Add. Currency Net Change;
                                                   BlankZero=true;
                                                   Editable=false }
    { 74  ;   ;Add. Currency Balance at Date;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry"."Additional-Currency Amount" WHERE ("Cost Type No."=FIELD("No."),
                                                                                                                    "Cost Type No."=FIELD(FILTER(Totaling)),
                                                                                                                    "Cost Center Code"=FIELD("Cost Center Filter"),
                                                                                                                    "Cost Object Code"=FIELD("Cost Object Filter"),
                                                                                                                    "Posting Date"=FIELD(UPPERLIMIT("Date Filter"))));
                                                   CaptionML=ENU=Add. Currency Balance at Date }
  }
  KEYS
  {
    {    ;"No."                                   ;Clustered=true }
    {    ;Type                                     }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;"No.",Name,Type                          }
  }
  CODE
  {
    VAR
      AccPeriod@1002 : Record "Accounting Period";
      Text000@1003 : TextConst 'ENU=You cannot delete a cost type with entries in an open fiscal year.';
      Text001@1004 : TextConst 'ENU=You cannot change cost type %1. There are %2 associated with it.';

    LOCAL PROCEDURE Modified@1();
    BEGIN
      "Modified Date" := TODAY;
      "Modified By" := USERID;
    END;

    [Internal]
    PROCEDURE LookupGLAccFilter@2(VAR Text@1000 : Text) : Boolean;
    VAR
      GLAccList@1001 : Page "G/L Account List";
    BEGIN
      GLAccList.LOOKUPMODE(true);
      if GLAccList.RUNMODAL = ACTION::LookupOK then begin
        Text := GLAccList.GetSelectionFilter;
        exit(true);
      end;
      exit(false)
    END;

    [Internal]
    PROCEDURE LookupCostTypeFilter@3(VAR Text@1000 : Text) : Boolean;
    VAR
      CostTypeList@1001 : Page "Cost Type List";
    BEGIN
      CostTypeList.LOOKUPMODE(true);
      if CostTypeList.RUNMODAL = ACTION::LookupOK then begin
        Text := CostTypeList.GetSelectionFilter;
        exit(true);
      end;
      exit(false)
    END;

    BEGIN
    END.
  }
}

