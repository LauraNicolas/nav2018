OBJECT Table 5311 Outlook Synch. Dependency
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields="Synch. Entity Code";
    OnInsert=BEGIN
               CheckUserSetup;

               if ISNULLGUID("Record GUID") then
                 "Record GUID" := CREATEGUID;

               TESTFIELD("Table Relation");
             END;

    OnDelete=BEGIN
               CheckUserSetup;

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;
             END;

    OnRename=BEGIN
               CheckUserSetup;

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;
               Condition := '';
               "Table Relation" := '';
             END;

    CaptionML=ENU=Outlook Synch. Dependency;
    PasteIsValid=false;
    LookupPageID="Outlook Synch. Dependencies";
    DrillDownPageID="Outlook Synch. Dependencies";
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity Element"."Synch. Entity Code";
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Element No.");
                                                              END;

                                                   CaptionML=ENU=Synch. Entity Code;
                                                   NotBlank=true }
    { 2   ;   ;Element No.         ;Integer       ;CaptionML=ENU=Element No. }
    { 3   ;   ;Depend. Synch. Entity Code;Code10  ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                if "Synch. Entity Code" = "Depend. Synch. Entity Code" then
                                                                  ERROR(Text001,"Synch. Entity Code");

                                                                LoopCheck("Depend. Synch. Entity Code","Synch. Entity Code");

                                                                CALCFIELDS(Description);
                                                              END;

                                                   CaptionML=ENU=Depend. Synch. Entity Code }
    { 4   ;   ;Description         ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Outlook Synch. Entity".Description WHERE (Code=FIELD("Depend. Synch. Entity Code")));
                                                   CaptionML=ENU=Description;
                                                   Editable=false }
    { 5   ;   ;Condition           ;Text250       ;CaptionML=ENU=Condition;
                                                   Editable=false }
    { 6   ;   ;Table Relation      ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Table Relation");
                                                              END;

                                                   CaptionML=ENU=Table Relation;
                                                   Editable=false }
    { 7   ;   ;Record GUID         ;GUID          ;CaptionML=ENU=Record GUID;
                                                   Editable=false }
    { 8   ;   ;Depend. Synch. Entity Tab. No.;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Outlook Synch. Entity"."Table No." WHERE (Code=FIELD("Depend. Synch. Entity Code")));
                                                   CaptionML=ENU=Depend. Synch. Entity Tab. No.;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Synch. Entity Code","Element No.","Depend. Synch. Entity Code";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=The selected entity cannot be the same as the %1 entity.';
      Text002@1001 : TextConst 'ENU=You cannot add this entity because it is already setup as a dependency for one or more of its own dependencies.';
      OSynchFilter@1002 : Record "Outlook Synch. Filter";
      Text003@1003 : TextConst 'ENU=You cannot change this dependency for the %1 collection of the %2 entity because it is set up for synchronization.';

    [External]
    PROCEDURE LoopCheck@21(DependSynchEntityCode@1003 : Code[10];SynchEntityCode@1000 : Code[10]);
    VAR
      OSynchDependency@1002 : Record "Outlook Synch. Dependency";
    BEGIN
      OSynchDependency.RESET;
      OSynchDependency.SETRANGE("Synch. Entity Code",DependSynchEntityCode);
      OSynchDependency.SETRANGE("Depend. Synch. Entity Code",SynchEntityCode);
      if OSynchDependency.FIND('-') then
        ERROR(Text002);

      OSynchDependency.SETRANGE("Depend. Synch. Entity Code");
      if OSynchDependency.FIND('-') then
        repeat
          if OSynchDependency."Depend. Synch. Entity Code" = "Synch. Entity Code" then
            ERROR(Text002);

          LoopCheck(OSynchDependency."Depend. Synch. Entity Code",OSynchDependency."Synch. Entity Code");
        until OSynchDependency.NEXT = 0;
    END;

    [External]
    PROCEDURE CheckUserSetup@1();
    VAR
      OSynchEntityElement@1002 : Record "Outlook Synch. Entity Element";
      OSynchUserSetup@1000 : Record "Outlook Synch. User Setup";
      OSynchSetupDetail@1001 : Record "Outlook Synch. Setup Detail";
    BEGIN
      OSynchUserSetup.RESET;
      OSynchUserSetup.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      if not OSynchUserSetup.FIND('-') then
        exit;

      repeat
        OSynchUserSetup.CALCFIELDS("No. of Elements");
        if OSynchUserSetup."No. of Elements" > 0 then
          if OSynchSetupDetail.GET(OSynchUserSetup."User ID","Synch. Entity Code","Element No.") then begin
            OSynchEntityElement.GET("Synch. Entity Code","Element No.");
            ERROR(
              Text003,
              OSynchEntityElement."Outlook Collection",
              OSynchEntityElement."Synch. Entity Code");
          end;
      until OSynchUserSetup.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

