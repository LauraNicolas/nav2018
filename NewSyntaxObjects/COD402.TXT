OBJECT Codeunit 402 Purchase Header Apply
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="Purchase Header";
    OnRun=BEGIN
            PurchHeader.COPY(Rec);
            with PurchHeader do begin
              PayToVendorNo := "Pay-to Vendor No." ;
              VendLedgEntry.SETCURRENTKEY("Vendor No.",Open);
              VendLedgEntry.SETRANGE("Vendor No.",PayToVendorNo);
              VendLedgEntry.SETRANGE(Open,true);
              if "Applies-to ID" = '' then
                "Applies-to ID" := "No.";
              if "Applies-to ID" = '' then
                ERROR(
                  Text000,
                  FIELDCAPTION("No."),FIELDCAPTION("Applies-to ID"));
              ApplyVendEntries.SetPurch(PurchHeader,VendLedgEntry,FIELDNO("Applies-to ID"));
              ApplyVendEntries.SETRECORD(VendLedgEntry);
              ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
              ApplyVendEntries.LOOKUPMODE(true);
              OK := ApplyVendEntries.RUNMODAL = ACTION::LookupOK;
              CLEAR(ApplyVendEntries);
              if not OK then
                exit;
              VendLedgEntry.RESET;
              VendLedgEntry.SETCURRENTKEY("Vendor No.",Open);
              VendLedgEntry.SETRANGE("Vendor No.",PayToVendorNo);
              VendLedgEntry.SETRANGE(Open,true);
              VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
              if VendLedgEntry.FINDFIRST then begin
                "Applies-to Doc. Type" := 0;
                "Applies-to Doc. No." := '';
              end else
                "Applies-to ID" := '';

              MODIFY;
            end;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1 or %2.';
      PurchHeader@1001 : Record "Purchase Header";
      VendLedgEntry@1002 : Record "Vendor Ledger Entry";
      ApplyVendEntries@1003 : Page "Apply Vendor Entries";
      PayToVendorNo@1004 : Code[20];
      OK@1005 : Boolean;

    BEGIN
    END.
  }
}

