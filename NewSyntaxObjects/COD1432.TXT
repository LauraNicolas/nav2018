OBJECT Codeunit 1432 Net Promoter Score Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DisplayDataTxt@1003 : TextConst '@@@={Locked};ENU="display/financials/?puid=%1"';
      RenderDataTxt@1004 : TextConst '@@@={Locked};ENU="render/financials/?culture=%1&puid=%2"';

    [Internal]
    PROCEDURE ShowNpsDialog@1() : Boolean;
    VAR
      NetPromoterScoreSetup@1002 : Record "Net Promoter Score Setup";
    BEGIN
      if not ShouldBePrompted then
        exit(false);

      if NetPromoterScoreSetup.GetApiUrl = '' then
        exit(false);

      DisableRequestSending;

      COMMIT;
      PAGE.RUNMODAL(PAGE::"Net Promoter Score");
      exit(true);
    END;

    LOCAL PROCEDURE ShouldBePrompted@4() : Boolean;
    VAR
      NetPromoterScore@1001 : Record "Net Promoter Score";
      DisplayUrl@1004 : Text;
      RenderUrl@1000 : Text;
      Response@1010 : Text;
      Display@1006 : Boolean;
    BEGIN
      if not NetPromoterScore.ShouldSendRequest then
        exit(false);

      if not IsNpsSupported then
        exit(false);

      DisplayUrl := GetDisplayUrl;
      if DisplayUrl = '' then
        exit(false);

      RenderUrl := GetRenderUrl;
      if RenderUrl = '' then
        exit(false);

      if not ExecuteWebRequest(DisplayUrl,Response) then
        exit(false);

      if not TryGetDisplayProperty(Response,Display) then
        exit(false);

      exit(Display);
    END;

    [External]
    PROCEDURE IsNpsSupported@10() : Boolean;
    VAR
      PermissionManager@1002 : Codeunit "Permission Manager";
    BEGIN
      if not GUIALLOWED then
        exit(false);

      if CURRENTCLIENTTYPE in [CLIENTTYPE::Phone,CLIENTTYPE::Tablet] then
        exit(false);

      if not PermissionManager.SoftwareAsAService then
        exit(false);

      if PermissionManager.IsSandboxConfiguration then
        exit(false);

      if GetApiUrl = '' then
        exit(false);

      exit(true);
    END;

    PROCEDURE TestConnection@5(BaseUrl@1001 : Text;VAR Response@1000 : Text;VAR ErrorMessage@1002 : Text) : Boolean;
    VAR
      TestUrl@1003 : Text;
      Success@1004 : Boolean;
    BEGIN
      TestUrl := GetTestUrl(BaseUrl);
      Success := ExecuteWebRequest(TestUrl,Response);
      if not Success then
        ErrorMessage := GETLASTERRORTEXT;
      exit(Success);
    END;

    [TryFunction]
    [External]
    PROCEDURE ExecuteWebRequest@3(Url@1006 : Text;VAR Response@1004 : Text);
    VAR
      TempBlob@1005 : TEMPORARY Record TempBlob;
      HttpWebRequestMgt@1002 : Codeunit "Http Web Request Mgt.";
      HttpStatusCode@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      Headers@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
      ResponseStream@1003 : InStream;
    BEGIN
      TempBlob.INIT;
      TempBlob.Blob.CREATEINSTREAM(ResponseStream);
      HttpWebRequestMgt.Initialize(Url);
      HttpWebRequestMgt.DisableUI;
      HttpWebRequestMgt.SetReturnType('application/json');
      HttpWebRequestMgt.AddHeader('Accept-Encoding','utf-8');
      HttpWebRequestMgt.SetMethod('GET');
      if not HttpWebRequestMgt.GetResponse(ResponseStream,HttpStatusCode,Headers) then
        ERROR(GETLASTERRORTEXT);
      ResponseStream.READTEXT(Response);
    END;

    [Internal]
    PROCEDURE GetPuid@9() : Text;
    VAR
      IdentityManagement@1000 : Codeunit "Identity Management";
      Puid@1001 : Text;
      Guid@1002 : GUID;
    BEGIN
      Puid := IdentityManagement.GetObjectId(USERSECURITYID);
      if not EVALUATE(Guid,Puid) then
        Puid := '';
      exit(Puid);
    END;

    [External]
    PROCEDURE GetDisplayUrl@12() : Text;
    VAR
      Data@1001 : Text;
      BaseUrl@1002 : Text;
      FullUrl@1000 : Text;
    BEGIN
      Data := GetDisplayData;
      BaseUrl := GetApiUrl;
      FullUrl := GetFullUrl(BaseUrl,Data);
      exit(FullUrl);
    END;

    [External]
    PROCEDURE GetRenderUrl@13() : Text;
    VAR
      Data@1002 : Text;
      BaseUrl@1001 : Text;
      FullUrl@1000 : Text;
    BEGIN
      Data := GetRenderData;
      BaseUrl := GetApiUrl;
      FullUrl := GetFullUrl(BaseUrl,Data);
      exit(FullUrl);
    END;

    [External]
    PROCEDURE GetTestUrl@27(BaseUrl@1000 : Text) : Text;
    VAR
      Data@1001 : Text;
      FullUrl@1002 : Text;
    BEGIN
      Data := GetTestData;
      FullUrl := GetFullUrl(BaseUrl,Data);
      exit(FullUrl);
    END;

    LOCAL PROCEDURE GetFullUrl@7(BaseUrl@1001 : Text;Data@1003 : Text) : Text;
    VAR
      FullUrl@1002 : Text;
    BEGIN
      if (BaseUrl <> '') and (Data <> '') then
        FullUrl := STRSUBSTNO('%1%2',BaseUrl,Data);
      exit(FullUrl);
    END;

    LOCAL PROCEDURE GetApiUrl@2() : Text;
    VAR
      NetPromoterScoreSetup@1000 : Record "Net Promoter Score Setup";
      Url@1001 : Text;
    BEGIN
      Url := NetPromoterScoreSetup.GetApiUrl;
      exit(Url);
    END;

    LOCAL PROCEDURE GetTestData@15() : Text;
    VAR
      Data@1001 : Text;
    BEGIN
      Data := STRSUBSTNO(DisplayDataTxt,'');
      exit(Data);
    END;

    LOCAL PROCEDURE GetDisplayData@18() : Text;
    VAR
      Puid@1000 : Text;
      Data@1001 : Text;
    BEGIN
      Puid := GetPuid;
      Data := STRSUBSTNO(DisplayDataTxt,Puid);
      exit(Data);
    END;

    LOCAL PROCEDURE GetRenderData@17() : Text;
    VAR
      TypeHelper@1003 : Codeunit "Type Helper";
      Culture@1002 : Text;
      Puid@1000 : Text;
      Data@1001 : Text;
    BEGIN
      Culture := LOWERCASE(TypeHelper.LanguageIDToCultureName(GLOBALLANGUAGE));
      Puid := GetPuid;
      Data := STRSUBSTNO(RenderDataTxt,Culture,Puid);
      exit(Data);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryGetDisplayProperty@25(Data@1000 : Text;VAR DisplayProperty@1002 : Boolean);
    VAR
      JSONManagement@1003 : Codeunit "JSON Management";
      JObject@1001 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JObject";
    BEGIN
      JSONManagement.InitializeObject(Data);
      JSONManagement.GetJSONObject(JObject);
      JSONManagement.GetBoolPropertyValueFromJObjectByName(JObject,'display',DisplayProperty);
    END;

    [TryFunction]
    [External]
    PROCEDURE TryGetMessageType@35(Data@1000 : Text;VAR MessageType@1002 : Text);
    VAR
      JSONManagement@1003 : Codeunit "JSON Management";
      JObject@1001 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JObject";
    BEGIN
      JSONManagement.InitializeObject(Data);
      JSONManagement.GetJSONObject(JObject);
      JSONManagement.GetStringPropertyValueFromJObjectByName(JObject,'msgType',MessageType);
    END;

    [EventSubscriber(Codeunit,1,OnAfterCompanyOpen)]
    LOCAL PROCEDURE OnAfterCompanyUpdateRequestSendingStatus@6();
    VAR
      NetPromoterScore@1000 : Record "Net Promoter Score";
    BEGIN
      NetPromoterScore.UpdateRequestSendingStatus;
    END;

    [EventSubscriber(Codeunit,2,OnCompanyInitialize)]
    [External]
    PROCEDURE OnCompanyInitializeInvalidateCache@11();
    VAR
      NetPromoterScoreSetup@1000 : Record "Net Promoter Score Setup";
    BEGIN
      if NetPromoterScoreSetup.GET then begin
        NetPromoterScoreSetup."Expire Time" := CURRENTDATETIME;
        NetPromoterScoreSetup.MODIFY;
      end;
    END;

    [External]
    PROCEDURE DisableRequestSending@8();
    VAR
      NetPromoterScore@1001 : Record "Net Promoter Score";
    BEGIN
      NetPromoterScore.DisableRequestSending;
    END;

    BEGIN
    END.
  }
}

