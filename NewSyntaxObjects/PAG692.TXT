OBJECT Page 692 Import Style Sheet
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Import Style Sheet;
    DataCaptionExpr=AllObjWithCaption."Object Caption";
    PageType=StandardDialog;
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General }

    { 5   ;2   ;Field     ;
                Name=FileName;
                CaptionML=ENU=Style Sheet;
                ApplicationArea=#Advanced;
                SourceExpr=FileName;
                OnValidate=VAR
                             TempBlob@1001 : Record TempBlob;
                           BEGIN
                             if not FileMgt.ClientFileExists(FileName) then
                               ERROR(Text002,FileName);

                             FileMgt.BLOBImport(TempBlob,FileName);
                             StyleSheet."Style Sheet" := TempBlob.Blob;
                           END;

                OnAssistEdit=BEGIN
                               LookupStyleSheet;
                             END;
                              }

    { 2   ;2   ;Field     ;
                Name=Description;
                CaptionML=ENU=Name;
                ApplicationArea=#Advanced;
                SourceExpr=Description;
                OnValidate=BEGIN
                             StyleSheet.Name := Description;
                           END;
                            }

    { 8   ;2   ;Field     ;
                Name=ApplicationName;
                CaptionML=ENU=Send-to Program;
                ApplicationArea=#Advanced;
                SourceExpr=ApplicationName;
                OnValidate=BEGIN
                             FindApplication;
                           END;

                OnLookup=BEGIN
                           LookupApplication;
                         END;
                          }

  }
  CODE
  {
    VAR
      SendToProgram@1002 : Record "Send-To Program";
      StyleSheet@1006 : Record "Style Sheet";
      AllObjWithCaption@1010 : Record AllObjWithCaption;
      FileMgt@1011 : Codeunit "File Management";
      FileName@1000 : Text;
      Description@1003 : Text[1024];
      ApplicationName@1001 : Text[250];
      ObjType@1004 : Integer;
      ObjectID@1005 : Integer;
      Text001@1007 : TextConst 'ENU=No %1 found.';
      Text002@1008 : TextConst 'ENU=The file %1 could not be found.';

    [External]
    PROCEDURE SetObjectID@2(NewObjectType@1000 : Integer;NewObjectID@1001 : Integer;NewApplicationID@1002 : GUID);
    BEGIN
      if not SendToProgram.GET(NewApplicationID) then
        SendToProgram.FINDFIRST;
      ApplicationName := SendToProgram.Name;
      ObjType := NewObjectType;
      ObjectID := NewObjectID;
      if not AllObjWithCaption.GET(AllObjWithCaption."Object Type"::Page,ObjectID) then
        AllObjWithCaption.INIT;
      StyleSheet.INIT;
      StyleSheet."Style Sheet ID" := CREATEGUID;
      StyleSheet."Object ID" := ObjectID;
      StyleSheet."Object Type" := ObjType;
      StyleSheet."Program ID" := SendToProgram."Program ID";
      StyleSheet.Date := TODAY;
    END;

    [Internal]
    PROCEDURE GetStyleSheet@1(VAR ReturnStyleSheet@1000 : Record "Style Sheet");
    BEGIN
      ReturnStyleSheet := StyleSheet;
    END;

    LOCAL PROCEDURE LookupApplication@5();
    VAR
      SendToPrograms@1000 : Page "Send-to Programs";
    BEGIN
      SendToPrograms.LOOKUPMODE := true;
      SendToPrograms.SETRECORD(SendToProgram);
      if SendToPrograms.RUNMODAL = ACTION::LookupOK then begin
        SendToPrograms.GETRECORD(SendToProgram);
        StyleSheet."Program ID" := SendToProgram."Program ID";
        ApplicationName := SendToProgram.Name;
      end;
    END;

    LOCAL PROCEDURE FindApplication@6();
    BEGIN
      SendToProgram.RESET;
      if ApplicationName = '' then begin
        SendToProgram.FINDFIRST;
        ApplicationName := SendToProgram.Name;
        exit;
      end;
      SendToProgram.SETRANGE(Name,ApplicationName);
      if SendToProgram.FINDFIRST then begin
        ApplicationName := SendToProgram.Name;
        exit;
      end;
      SendToProgram.SETFILTER(Name,'@*' + ApplicationName + '*');
      if SendToProgram.FINDFIRST then begin
        ApplicationName := SendToProgram.Name;
        exit;
      end;
      ERROR(Text001,SendToProgram.TABLECAPTION);
    END;

    LOCAL PROCEDURE LookupStyleSheet@8();
    VAR
      TempBlob@1001 : Record TempBlob;
      PathLength@1000 : Integer;
    BEGIN
      FileName := FileMgt.BLOBImport(TempBlob,'*.xsl*');

      if FileName = '' then begin
        CLEAR(StyleSheet."Style Sheet");
        CLEAR(Description);
        exit;
      end;
      PathLength := STRLEN(FileMgt.GetDirectoryName(FileName));
      Description := COPYSTR(FileName,PathLength + 2);
      StyleSheet.Name := Description;
      StyleSheet."Style Sheet" := TempBlob.Blob;
    END;

    BEGIN
    END.
  }
}

