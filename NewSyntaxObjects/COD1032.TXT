OBJECT Codeunit 1032 Job Planning Line-Reserve
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Reservation Entry"=rimd,
                TableData "Planning Assignment"=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Reserved quantity cannot be greater than %1.';
      Text002@1001 : TextConst '@@@=starts with "Planning Date";ENU=must be filled in when a quantity is reserved';
      Text004@1003 : TextConst '@@@=starts with some field name;ENU=must not be changed when a quantity is reserved';
      Text005@1004 : TextConst 'ENU=Codeunit is not initialized correctly.';
      CreateReservEntry@1005 : Codeunit "Create Reserv. Entry";
      ReservEngineMgt@1006 : Codeunit "Reservation Engine Mgt.";
      ReservMgt@1007 : Codeunit "Reservation Management";
      SetFromType@1009 : '" ",Sales,"Requisition Line",Purchase,"Item Journal","BOM Journal","Item Ledger Entry",Service,Job';
      SetFromSubtype@1010 : Integer;
      SetFromID@1011 : Code[20];
      SetFromBatchName@1012 : Code[10];
      SetFromProdOrderLine@1013 : Integer;
      SetFromRefNo@1014 : Integer;
      SetFromVariantCode@1015 : Code[10];
      SetFromLocationCode@1016 : Code[10];
      SetFromSerialNo@1018 : Code[20];
      SetFromLotNo@1019 : Code[20];
      SetFromQtyPerUOM@1020 : Decimal;

    [External]
    PROCEDURE CreateReservation@1(JobPlanningLine@1000 : Record "Job Planning Line";Description@1001 : Text[50];ExpectedReceiptDate@1002 : Date;Quantity@1008 : Decimal;QuantityBase@1003 : Decimal;ForSerialNo@1005 : Code[20];ForLotNo@1004 : Code[20]);
    VAR
      PlanningDate@1007 : Date;
      SignFactor@1006 : Integer;
    BEGIN
      if SetFromType = 0 then
        ERROR(Text005);
      JobPlanningLine.TESTFIELD(Type,JobPlanningLine.Type::Item);
      JobPlanningLine.TESTFIELD("No.");
      JobPlanningLine.TESTFIELD("Planning Date");

      JobPlanningLine.CALCFIELDS("Reserved Qty. (Base)");
      if ABS(JobPlanningLine."Remaining Qty. (Base)") < ABS(JobPlanningLine."Reserved Qty. (Base)") + QuantityBase then
        ERROR(
          Text000,
          ABS(JobPlanningLine."Remaining Qty. (Base)") - ABS(JobPlanningLine."Reserved Qty. (Base)"));

      JobPlanningLine.TESTFIELD("Variant Code",SetFromVariantCode);
      JobPlanningLine.TESTFIELD("Location Code",SetFromLocationCode);

      SignFactor := -1;

      if QuantityBase * SignFactor < 0 then
        PlanningDate := JobPlanningLine."Planning Date"
      else begin
        PlanningDate := ExpectedReceiptDate;
        ExpectedReceiptDate := JobPlanningLine."Planning Date";
      end;

      CreateReservEntry.CreateReservEntryFor(
        DATABASE::"Job Planning Line",JobPlanningLine.Status,
        JobPlanningLine."Job No.",'',0,JobPlanningLine."Job Contract Entry No.",JobPlanningLine."Qty. per Unit of Measure",
        Quantity,QuantityBase,ForSerialNo,ForLotNo);
      CreateReservEntry.CreateReservEntryFrom(
        SetFromType,SetFromSubtype,SetFromID,SetFromBatchName,SetFromProdOrderLine,SetFromRefNo,
        SetFromQtyPerUOM,SetFromSerialNo,SetFromLotNo);
      CreateReservEntry.CreateReservEntry(
        JobPlanningLine."No.",JobPlanningLine."Variant Code",JobPlanningLine."Location Code",
        Description,ExpectedReceiptDate,PlanningDate);

      SetFromType := 0;
    END;

    LOCAL PROCEDURE CreateBindingReservation@10(JobPlanningLine@1000 : Record "Job Planning Line";Description@1001 : Text[50];ExpectedReceiptDate@1002 : Date;Quantity@1008 : Decimal;QuantityBase@1003 : Decimal);
    BEGIN
      CreateReservation(JobPlanningLine,Description,ExpectedReceiptDate,Quantity,QuantityBase,'','');
    END;

    [External]
    PROCEDURE CreateReservationSetFrom@7(TrackingSpecificationFrom@1000 : Record "Tracking Specification");
    BEGIN
      with TrackingSpecificationFrom do begin
        SetFromType := "Source Type";
        SetFromSubtype := "Source Subtype";
        SetFromID := "Source ID";
        SetFromBatchName := "Source Batch Name";
        SetFromProdOrderLine := "Source Prod. Order Line";
        SetFromRefNo := "Source Ref. No.";
        SetFromVariantCode := "Variant Code";
        SetFromLocationCode := "Location Code";
        SetFromSerialNo := "Serial No.";
        SetFromLotNo := "Lot No.";
        SetFromQtyPerUOM := "Qty. per Unit of Measure";
      end;
    END;

    [External]
    PROCEDURE SetBinding@8(Binding@1000 : '" ","Order-to-Order"');
    BEGIN
      CreateReservEntry.SetBinding(Binding);
    END;

    [External]
    PROCEDURE FilterReservFor@12(VAR FilterReservEntry@1000 : Record "Reservation Entry";JobPlanningLine@1001 : Record "Job Planning Line");
    BEGIN
      FilterReservEntry.SetSourceFilter(
        DATABASE::"Job Planning Line",JobPlanningLine.Status,JobPlanningLine."Job No.",JobPlanningLine."Job Contract Entry No.",false);
      FilterReservEntry.SetSourceFilter2('',0);
    END;

    [External]
    PROCEDURE ReservQuantity@21(JobPlanningLine@1001 : Record "Job Planning Line";VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1002 : Decimal);
    BEGIN
      case JobPlanningLine.Status of
        JobPlanningLine.Status::Planning,
        JobPlanningLine.Status::Quote,
        JobPlanningLine.Status::Order,
        JobPlanningLine.Status::Completed:
          begin
            QtyToReserve := JobPlanningLine."Remaining Qty.";
            QtyToReserveBase := JobPlanningLine."Remaining Qty. (Base)";
          end;
      end;
    END;

    [External]
    PROCEDURE Caption@30(JobPlanningLine@1001 : Record "Job Planning Line") CaptionText@1000 : Text[80];
    BEGIN
      CaptionText :=
        STRSUBSTNO('%1 %2 %3',JobPlanningLine.Status,JobPlanningLine."Job No.",JobPlanningLine."No.");
    END;

    [External]
    PROCEDURE FindReservEntry@4(JobPlanningLine@1000 : Record "Job Planning Line";VAR ReservEntry@1001 : Record "Reservation Entry") : Boolean;
    BEGIN
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,false);
      FilterReservFor(ReservEntry,JobPlanningLine);
      exit(ReservEntry.FINDLAST);
    END;

    [External]
    PROCEDURE VerifyChange@59(VAR NewJobPlanningLine@1000 : Record "Job Planning Line";VAR OldJobPlanningLine@1001 : Record "Job Planning Line");
    VAR
      JobPlanningLine@1005 : Record "Job Planning Line";
      ReservEntry@1002 : Record "Reservation Entry";
      ShowError@1003 : Boolean;
      HasError@1004 : Boolean;
    BEGIN
      if (NewJobPlanningLine.Type <> NewJobPlanningLine.Type::Item) and (OldJobPlanningLine.Type <> OldJobPlanningLine.Type::Item) then
        exit;
      if NewJobPlanningLine."Job Contract Entry No." = 0 then
        if not JobPlanningLine.GET(
             NewJobPlanningLine."Job No.",
             NewJobPlanningLine."Job Task No.",
             NewJobPlanningLine."Line No.")
        then
          exit;

      NewJobPlanningLine.CALCFIELDS("Reserved Qty. (Base)");
      ShowError := NewJobPlanningLine."Reserved Qty. (Base)" <> 0;

      if NewJobPlanningLine."Usage Link" <> OldJobPlanningLine."Usage Link" then begin
        if ShowError then
          NewJobPlanningLine.FIELDERROR("Usage Link",Text004);
        HasError := true;
      end;

      if (NewJobPlanningLine."Planning Date" = 0D) and (OldJobPlanningLine."Planning Date" <> 0D) then begin
        if ShowError then
          NewJobPlanningLine.FIELDERROR("Planning Date",Text002);
        HasError := true;
      end;

      if NewJobPlanningLine."No." <> OldJobPlanningLine."No." then begin
        if ShowError then
          NewJobPlanningLine.FIELDERROR("No.",Text004);
        HasError := true;
      end;

      if NewJobPlanningLine."Variant Code" <> OldJobPlanningLine."Variant Code" then begin
        if ShowError then
          NewJobPlanningLine.FIELDERROR("Variant Code",Text004);
        HasError := true;
      end;

      if NewJobPlanningLine."Location Code" <> OldJobPlanningLine."Location Code" then begin
        if ShowError then
          NewJobPlanningLine.FIELDERROR("Location Code",Text004);
        HasError := true;
      end;

      if NewJobPlanningLine."Bin Code" <> OldJobPlanningLine."Bin Code" then begin
        if ShowError then
          NewJobPlanningLine.FIELDERROR("Bin Code",Text004);
        HasError := true;
      end;

      if NewJobPlanningLine."Line No." <> OldJobPlanningLine."Line No." then
        HasError := true;

      if NewJobPlanningLine.Type <> OldJobPlanningLine.Type then begin
        if ShowError then
          NewJobPlanningLine.FIELDERROR(Type,Text004);
        HasError := true;
      end;

      if HasError then
        if (NewJobPlanningLine."No." <> OldJobPlanningLine."No.") or
           FindReservEntry(NewJobPlanningLine,ReservEntry)
        then begin
          if (NewJobPlanningLine."No." <> OldJobPlanningLine."No.") or (NewJobPlanningLine.Type <> OldJobPlanningLine.Type) then begin
            ReservMgt.SetJobPlanningLine(OldJobPlanningLine);
            ReservMgt.DeleteReservEntries(true,0);
            ReservMgt.SetJobPlanningLine(NewJobPlanningLine);
          end else begin
            ReservMgt.SetJobPlanningLine(NewJobPlanningLine);
            ReservMgt.DeleteReservEntries(true,0);
          end;
          ReservMgt.AutoTrack(NewJobPlanningLine."Remaining Qty. (Base)");
        end;

      if HasError or (NewJobPlanningLine."Planning Date" <> OldJobPlanningLine."Planning Date")
      then begin
        AssignForPlanning(NewJobPlanningLine);
        if (NewJobPlanningLine."No." <> OldJobPlanningLine."No.") or
           (NewJobPlanningLine."Variant Code" <> OldJobPlanningLine."Variant Code") or
           (NewJobPlanningLine."Location Code" <> OldJobPlanningLine."Location Code")
        then
          AssignForPlanning(OldJobPlanningLine);
      end;
    END;

    [External]
    PROCEDURE VerifyQuantity@2(VAR NewJobPlanningLine@1000 : Record "Job Planning Line";VAR OldJobPlanningLine@1001 : Record "Job Planning Line");
    VAR
      JobPlanningLine@1002 : Record "Job Planning Line";
    BEGIN
      with NewJobPlanningLine do begin
        if Type <> Type::Item then
          exit;
        if Status = OldJobPlanningLine.Status then
          if "Line No." = OldJobPlanningLine."Line No." then
            if "Quantity (Base)" = OldJobPlanningLine."Quantity (Base)" then
              exit;
        if "Line No." = 0 then
          if not JobPlanningLine.GET("Job No.","Job Task No.","Line No.") then
            exit;
        ReservMgt.SetJobPlanningLine(NewJobPlanningLine);
        if "Qty. per Unit of Measure" <> OldJobPlanningLine."Qty. per Unit of Measure" then
          ReservMgt.ModifyUnitOfMeasure;
        if "Remaining Qty. (Base)" * OldJobPlanningLine."Remaining Qty. (Base)" < 0 then
          ReservMgt.DeleteReservEntries(true,0)
        else
          ReservMgt.DeleteReservEntries(false,"Remaining Qty. (Base)");
        ReservMgt.ClearSurplus;
        ReservMgt.AutoTrack("Remaining Qty. (Base)");
        AssignForPlanning(NewJobPlanningLine);
      end;
    END;

    [External]
    PROCEDURE TransferJobLineToItemJnlLine@35(VAR JobPlanningLine@1000 : Record "Job Planning Line";VAR NewItemJnlLine@1001 : Record "Item Journal Line";TransferQty@1002 : Decimal) : Decimal;
    VAR
      OldReservEntry@1003 : Record "Reservation Entry";
      ItemTrackingFilterIsSet@1009 : Boolean;
      EndLoop@1008 : Boolean;
      TrackedQty@1005 : Decimal;
      UnTrackedQty@1007 : Decimal;
      xTransferQty@1006 : Decimal;
    BEGIN
      if not FindReservEntry(JobPlanningLine,OldReservEntry) then
        exit(TransferQty);

      // Store initial values
      OldReservEntry.CALCSUMS("Quantity (Base)");
      TrackedQty := -OldReservEntry."Quantity (Base)";
      xTransferQty := TransferQty;

      OldReservEntry.Lock;

      // Handle Item Tracking on job planning line:
      CLEAR(CreateReservEntry);
      if NewItemJnlLine."Entry Type" = NewItemJnlLine."Entry Type"::"Negative Adjmt." then
        if NewItemJnlLine.TrackingExists then begin
          CreateReservEntry.SetNewSerialLotNo(NewItemJnlLine."Serial No.",NewItemJnlLine."Lot No.");
          // Try to match against Item Tracking on the job planning line:
          OldReservEntry.SetTrackingFilterFromItemJnlLine(NewItemJnlLine);
          if OldReservEntry.ISEMPTY then
            OldReservEntry.ClearTrackingFilter
          else
            ItemTrackingFilterIsSet := true;
        end;

      NewItemJnlLine.TestItemFields(JobPlanningLine."No.",JobPlanningLine."Variant Code",JobPlanningLine."Location Code");

      if TransferQty = 0 then
        exit;

      if ReservEngineMgt.InitRecordSet2(OldReservEntry,NewItemJnlLine."Serial No.",NewItemJnlLine."Lot No.") then
        repeat
          OldReservEntry.TestItemFields(JobPlanningLine."No.",JobPlanningLine."Variant Code",JobPlanningLine."Location Code");

          TransferQty :=
            CreateReservEntry.TransferReservEntry(DATABASE::"Item Journal Line",
              NewItemJnlLine."Entry Type",NewItemJnlLine."Journal Template Name",NewItemJnlLine."Journal Batch Name",0,
              NewItemJnlLine."Line No.",NewItemJnlLine."Qty. per Unit of Measure",OldReservEntry,TransferQty);

          if ReservEngineMgt.NEXTRecord(OldReservEntry) = 0 then
            if ItemTrackingFilterIsSet then begin
              OldReservEntry.ClearTrackingFilter;
              ItemTrackingFilterIsSet := false;
              EndLoop := not ReservEngineMgt.InitRecordSet(OldReservEntry);
            end else
              EndLoop := true;
        until EndLoop or (TransferQty = 0);

      // Handle remaining transfer quantity
      if TransferQty <> 0 then begin
        TrackedQty -= (xTransferQty - TransferQty);
        UnTrackedQty := JobPlanningLine."Remaining Qty. (Base)" - TrackedQty;
        if TransferQty > UnTrackedQty then begin
          ReservMgt.SetJobPlanningLine(JobPlanningLine);
          ReservMgt.DeleteReservEntries(false,JobPlanningLine."Remaining Qty. (Base)");
        end;
      end;
      exit(TransferQty);
    END;

    [External]
    PROCEDURE DeleteLine@3(VAR JobPlanningLine@1000 : Record "Job Planning Line");
    BEGIN
      with JobPlanningLine do begin
        ReservMgt.SetJobPlanningLine(JobPlanningLine);
        ReservMgt.DeleteReservEntries(true,0);
        CALCFIELDS("Reserved Qty. (Base)");
        AssignForPlanning(JobPlanningLine);
      end;
    END;

    LOCAL PROCEDURE AssignForPlanning@5(VAR JobPlanningLine@1000 : Record "Job Planning Line");
    VAR
      PlanningAssignment@1001 : Record "Planning Assignment";
    BEGIN
      with JobPlanningLine do begin
        if Status <> Status::Order then
          exit;
        if Type <> Type::Item then
          exit;
        if "No." <> '' then
          PlanningAssignment.ChkAssignOne("No.","Variant Code","Location Code","Planning Date");
      end;
    END;

    [External]
    PROCEDURE BindToPurchase@6(JobPlanningLine@1000 : Record "Job Planning Line";PurchLine@1001 : Record "Purchase Line";ReservQty@1002 : Decimal;ReservQtyBase@1003 : Decimal);
    VAR
      TrackingSpecification@1004 : Record "Tracking Specification";
      ReservationEntry@1005 : Record "Reservation Entry";
    BEGIN
      SetBinding(ReservationEntry.Binding::"Order-to-Order");
      TrackingSpecification.InitTrackingSpecification2(
        DATABASE::"Purchase Line",PurchLine."Document Type",PurchLine."Document No.",'',0,PurchLine."Line No.",
        PurchLine."Variant Code",PurchLine."Location Code",PurchLine."Qty. per Unit of Measure");
      CreateReservationSetFrom(TrackingSpecification);
      CreateBindingReservation(JobPlanningLine,PurchLine.Description,PurchLine."Expected Receipt Date",ReservQty,ReservQtyBase);
    END;

    [External]
    PROCEDURE BindToRequisition@9(JobPlanningLine@1003 : Record "Job Planning Line";ReqLine@1002 : Record "Requisition Line";ReservQty@1001 : Decimal;ReservQtyBase@1000 : Decimal);
    VAR
      TrackingSpecification@1005 : Record "Tracking Specification";
      ReservationEntry@1004 : Record "Reservation Entry";
    BEGIN
      SetBinding(ReservationEntry.Binding::"Order-to-Order");
      TrackingSpecification.InitTrackingSpecification2(
        DATABASE::"Requisition Line",
        0,ReqLine."Worksheet Template Name",ReqLine."Journal Batch Name",0,ReqLine."Line No.",
        ReqLine."Variant Code",ReqLine."Location Code",ReqLine."Qty. per Unit of Measure");
      CreateReservationSetFrom(TrackingSpecification);
      CreateBindingReservation(JobPlanningLine,ReqLine.Description,ReqLine."Due Date",ReservQty,ReservQtyBase);
    END;

    BEGIN
    END.
  }
}

