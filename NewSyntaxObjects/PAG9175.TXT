OBJECT Page 9175 Connect Online
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Connect Online;
    PageType=CardPart;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Field     ;
                Name=Connect Online;
                CaptionML=ENU=Connect Online;
                ToolTipML=ENU="Specifies whether you can click to open a website and read the full content. ";
                ApplicationArea=#Advanced;
                SourceExpr=Parameters;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.DynamicsOnlineConnect;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      CurrentRole@1000 : Code[30];
      CurrentRoleID@1001 : Integer;
      CurrentLanguageID@1002 : Integer;
      Text000@1003 : TextConst '@@@="version=";ENU="version="';
      Text001@1004 : TextConst '@@@="locale=";ENU="locale="';
      Text002@1005 : TextConst '@@@="role=";ENU="role="';
      Text003@1006 : TextConst '@@@="roleid=";ENU="roleid="';
      Text004@1007 : TextConst '@@@="Serial=";ENU="Serial="';

    LOCAL PROCEDURE Parameters@26() : Text[350];
    BEGIN
      InitCurrentRoleValues;

      exit(Add(Version) + Add(Locale) + Add(Role) + Add(RoleID) + Add(Serial));
    END;

    LOCAL PROCEDURE Add@23(Parameter@1000 : Text[200]) : Text[200];
    BEGIN
      exit(Parameter + Separator);
    END;

    LOCAL PROCEDURE Version@1() : Text[170];
    VAR
      ApplicationManagement@1000 : Codeunit ApplicationManagement;
    BEGIN
      exit(Text000 + FORMAT(ApplicationManagement.ApplicationVersion + ':' + ApplicationManagement.ApplicationBuild,0,XMLFormat));
    END;

    LOCAL PROCEDURE Locale@2() : Text[30];
    BEGIN
      // Windows Language ID
      exit(Text001 + FORMAT(CurrentLanguageID,0,XMLFormat));
    END;

    LOCAL PROCEDURE Role@4() : Text[40];
    BEGIN
      // Profile ID (Any text entered in Profile ID)
      exit(Text002 + FORMAT(DELCHR(CurrentRole,'=',Separator),0,XMLFormat));
    END;

    LOCAL PROCEDURE RoleID@7() : Text[30];
    BEGIN
      // Role Center ID (Page ID)
      exit(Text003 + FORMAT(CurrentRoleID,0,XMLFormat));
    END;

    LOCAL PROCEDURE Serial@6() : Text[80];
    BEGIN
      // License ID
      exit(Text004 + FORMAT(SERIALNUMBER,0,XMLFormat));
    END;

    LOCAL PROCEDURE Separator@15() : Text[1];
    BEGIN
      exit(';');
    END;

    LOCAL PROCEDURE XMLFormat@5() : Integer;
    BEGIN
      exit(9);
    END;

    LOCAL PROCEDURE InitCurrentRoleValues@9();
    VAR
      UserPersonalization@1001 : Record "User Personalization";
      Profile@1002 : Record Profile;
      ApplicationManagement@1000 : Codeunit ApplicationManagement;
    BEGIN
      CurrentLanguageID := GLOBALLANGUAGE;
      CurrentRoleID := ApplicationManagement.DefaultRoleCenter;
      CurrentRole := FORMAT(CurrentRoleID);

      Profile.SETRANGE("Default Role Center",true);
      if Profile.FINDFIRST then begin
        CurrentRoleID := Profile."Role Center ID";
        CurrentRole := Profile."Profile ID";
      end;

      UserPersonalization.SETFILTER("User ID",'%1',UserFilter);
      if UserPersonalization.FINDFIRST then begin
        if (UserPersonalization."Language ID" <> 0) and (UserPersonalization."Language ID" <> CurrentLanguageID) then
          CurrentLanguageID := UserPersonalization."Language ID";
        if UserPersonalization."Profile ID" <> '' then begin
          CurrentRole := UserPersonalization."Profile ID";
          if Profile.GET(UserPersonalization."Profile ID") then
            CurrentRoleID := Profile."Role Center ID";
        end;
      end;
    END;

    LOCAL PROCEDURE UserFilter@8() : Text[80];
    BEGIN
      exit('*\' + USERID);
    END;

    BEGIN
    END.
  }
}

