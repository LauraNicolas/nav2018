OBJECT Codeunit 226 CustEntry-Apply Posted Entries
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Cust. Ledger Entry";
    EventSubscriberInstance=Manual;
    Permissions=TableData "Cust. Ledger Entry"=rimd;
    OnRun=BEGIN
            if PreviewMode then
              case RunOptionPreviewContext of
                RunOptionPreview::Apply:
                  Apply(Rec,DocumentNoPreviewContext,ApplicationDatePreviewContext);
                RunOptionPreview::Unapply:
                  PostUnApplyCustomer(DetailedCustLedgEntryPreviewContext,DocumentNoPreviewContext,ApplicationDatePreviewContext);
              end
            else
              Apply(Rec,"Document No.",0D);
          END;

  }
  CODE
  {
    VAR
      PostingApplicationMsg@1000 : TextConst 'ENU=Posting application...';
      MustNotBeBeforeErr@1001 : TextConst 'ENU=The Posting Date entered must not be before the Posting Date on the Cust. Ledger Entry.';
      NoEntriesAppliedErr@1003 : TextConst '@@@=%1 - Caption of "Applies to ID" field of Gen. Journal Line;ENU=Cannot post because you did not specify which entry to apply. You must specify an entry in the %1 field for one or more open entries.';
      UnapplyPostedAfterThisEntryErr@1018 : TextConst 'ENU=Before you can unapply this entry, you must first unapply all application entries that were posted after this entry.';
      NoApplicationEntryErr@1017 : TextConst 'ENU=Cust. Ledger Entry No. %1 does not have an application entry.';
      UnapplyingMsg@1015 : TextConst 'ENU=Unapplying and posting...';
      UnapplyAllPostedAfterThisEntryErr@1019 : TextConst 'ENU=Before you can unapply this entry, you must first unapply all application entries in Cust. Ledger Entry No. %1 that were posted after this entry.';
      NotAllowedPostingDatesErr@1021 : TextConst 'ENU=Posting date is not within the range of allowed posting dates.';
      LatestEntryMustBeAnApplicationErr@1024 : TextConst 'ENU=The latest Transaction No. must be an application in Cust. Ledger Entry No. %1.';
      CannotUnapplyExchRateErr@1023 : TextConst 'ENU=You cannot unapply the entry with the posting date %1, because the exchange rate for the additional reporting currency has been changed.';
      CannotUnapplyInReversalErr@1026 : TextConst 'ENU=You cannot unapply Cust. Ledger Entry No. %1 because the entry is part of a reversal.';
      CannotApplyClosedEntriesErr@1102601000 : TextConst 'ENU=One or more of the entries that you selected is closed. You cannot apply closed entries.';
      DetailedCustLedgEntryPreviewContext@1008 : Record "Detailed Cust. Ledg. Entry";
      ApplicationDatePreviewContext@1004 : Date;
      DocumentNoPreviewContext@1005 : Code[20];
      RunOptionPreview@1007 : 'Apply,Unapply';
      RunOptionPreviewContext@1009 : 'Apply,Unapply';
      PreviewMode@1002 : Boolean;

    [Internal]
    PROCEDURE Apply@17(CustLedgEntry@1000 : Record "Cust. Ledger Entry";DocumentNo@1001 : Code[20];ApplicationDate@1002 : Date);
    VAR
      PaymentToleranceMgt@1003 : Codeunit "Payment Tolerance Management";
    BEGIN
      with CustLedgEntry do begin
        PaymentToleranceMgt.ApplyPostedEntriesMode;
        if not PreviewMode then
          if not PaymentToleranceMgt.PmtTolCust(CustLedgEntry) then
            exit;
        GET("Entry No.");

        if ApplicationDate = 0D then
          ApplicationDate := GetApplicationDate(CustLedgEntry)
        else
          if ApplicationDate < GetApplicationDate(CustLedgEntry) then
            ERROR(MustNotBeBeforeErr);

        if DocumentNo = '' then
          DocumentNo := "Document No.";

        CustPostApplyCustLedgEntry(CustLedgEntry,DocumentNo,ApplicationDate);
      end;
    END;

    [External]
    PROCEDURE GetApplicationDate@14(CustLedgEntry@1000 : Record "Cust. Ledger Entry") ApplicationDate : Date;
    VAR
      ApplyToCustLedgEntry@1001 : Record "Cust. Ledger Entry";
    BEGIN
      with CustLedgEntry do begin
        ApplicationDate := 0D;
        ApplyToCustLedgEntry.SETCURRENTKEY("Customer No.","Applies-to ID");
        ApplyToCustLedgEntry.SETRANGE("Customer No.","Customer No.");
        ApplyToCustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
        ApplyToCustLedgEntry.FINDSET;
        repeat
          if ApplyToCustLedgEntry."Posting Date" > ApplicationDate then
            ApplicationDate := ApplyToCustLedgEntry."Posting Date";
        until ApplyToCustLedgEntry.NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CustPostApplyCustLedgEntry@12(CustLedgEntry@1000 : Record "Cust. Ledger Entry";DocumentNo@1002 : Code[20];ApplicationDate@1003 : Date);
    VAR
      SourceCodeSetup@1004 : Record "Source Code Setup";
      GenJnlLine@1005 : Record "Gen. Journal Line";
      UpdateAnalysisView@1001 : Codeunit "Update Analysis View";
      GenJnlPostLine@1006 : Codeunit "Gen. Jnl.-Post Line";
      GenJnlPostPreview@1011 : Codeunit "Gen. Jnl.-Post Preview";
      Window@1007 : Dialog;
      EntryNoBeforeApplication@1008 : Integer;
      EntryNoAfterApplication@1009 : Integer;
    BEGIN
      with CustLedgEntry do begin
        Window.OPEN(PostingApplicationMsg);

        SourceCodeSetup.GET;

        GenJnlLine.INIT;
        GenJnlLine."Document No." := DocumentNo;
        GenJnlLine."Posting Date" := ApplicationDate;
        GenJnlLine."Document Date" := GenJnlLine."Posting Date";
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::Customer;
        GenJnlLine."Account No." := "Customer No.";
        CALCFIELDS("Debit Amount","Credit Amount","Debit Amount (LCY)","Credit Amount (LCY)");
        GenJnlLine.Correction :=
          ("Debit Amount" < 0) or ("Credit Amount" < 0) or
          ("Debit Amount (LCY)" < 0) or ("Credit Amount (LCY)" < 0);
        GenJnlLine.CopyCustLedgEntry(CustLedgEntry);
        GenJnlLine."Source Code" := SourceCodeSetup."Sales Entry Application";
        GenJnlLine."System-Created Entry" := true;

        EntryNoBeforeApplication := FindLastApplDtldCustLedgEntry;

        OnBeforePostApplyCustLedgEntry(GenJnlLine,CustLedgEntry);
        GenJnlPostLine.CustPostApplyCustLedgEntry(GenJnlLine,CustLedgEntry);

        EntryNoAfterApplication := FindLastApplDtldCustLedgEntry;
        if EntryNoAfterApplication = EntryNoBeforeApplication then
          ERROR(STRSUBSTNO(NoEntriesAppliedErr,GenJnlLine.FIELDCAPTION("Applies-to ID")));

        if PreviewMode then
          GenJnlPostPreview.ThrowError;

        COMMIT;
        Window.CLOSE;
        UpdateAnalysisView.UpdateAll(0,true);
      end;
    END;

    LOCAL PROCEDURE FindLastApplDtldCustLedgEntry@1() : Integer;
    VAR
      DtldCustLedgEntry@1000 : Record "Detailed Cust. Ledg. Entry";
    BEGIN
      DtldCustLedgEntry.LOCKTABLE;
      if DtldCustLedgEntry.FINDLAST then
        exit(DtldCustLedgEntry."Entry No.");

      exit(0);
    END;

    LOCAL PROCEDURE FindLastApplEntry@2(CustLedgEntryNo@1002 : Integer) : Integer;
    VAR
      DtldCustLedgEntry@1001 : Record "Detailed Cust. Ledg. Entry";
      ApplicationEntryNo@1000 : Integer;
    BEGIN
      with DtldCustLedgEntry do begin
        SETCURRENTKEY("Cust. Ledger Entry No.","Entry Type");
        SETRANGE("Cust. Ledger Entry No.",CustLedgEntryNo);
        SETRANGE("Entry Type","Entry Type"::Application);
        SETRANGE(Unapplied,false);
        ApplicationEntryNo := 0;
        if FIND('-') then
          repeat
            if "Entry No." > ApplicationEntryNo then
              ApplicationEntryNo := "Entry No.";
          until NEXT = 0;
      end;
      exit(ApplicationEntryNo);
    END;

    LOCAL PROCEDURE FindLastTransactionNo@6(CustLedgEntryNo@1002 : Integer) : Integer;
    VAR
      DtldCustLedgEntry@1001 : Record "Detailed Cust. Ledg. Entry";
      LastTransactionNo@1000 : Integer;
    BEGIN
      with DtldCustLedgEntry do begin
        SETCURRENTKEY("Cust. Ledger Entry No.","Entry Type");
        SETRANGE("Cust. Ledger Entry No.",CustLedgEntryNo);
        SETRANGE(Unapplied,false);
        SETFILTER("Entry Type",'<>%1&<>%2',"Entry Type"::"Unrealized Loss","Entry Type"::"Unrealized Gain");
        LastTransactionNo := 0;
        if FINDSET then
          repeat
            if LastTransactionNo < "Transaction No." then
              LastTransactionNo := "Transaction No.";
          until NEXT = 0;
      end;
      exit(LastTransactionNo);
    END;

    [External]
    PROCEDURE UnApplyDtldCustLedgEntry@3(DtldCustLedgEntry@1000 : Record "Detailed Cust. Ledg. Entry");
    VAR
      ApplicationEntryNo@1001 : Integer;
    BEGIN
      DtldCustLedgEntry.TESTFIELD("Entry Type",DtldCustLedgEntry."Entry Type"::Application);
      DtldCustLedgEntry.TESTFIELD(Unapplied,false);
      ApplicationEntryNo := FindLastApplEntry(DtldCustLedgEntry."Cust. Ledger Entry No.");

      if DtldCustLedgEntry."Entry No." <> ApplicationEntryNo then
        ERROR(UnapplyPostedAfterThisEntryErr);
      CheckReversal(DtldCustLedgEntry."Cust. Ledger Entry No.");
      UnApplyCustomer(DtldCustLedgEntry);
    END;

    [External]
    PROCEDURE UnApplyCustLedgEntry@7(CustLedgEntryNo@1000 : Integer);
    VAR
      DtldCustLedgEntry@1002 : Record "Detailed Cust. Ledg. Entry";
      ApplicationEntryNo@1001 : Integer;
    BEGIN
      CheckReversal(CustLedgEntryNo);
      ApplicationEntryNo := FindLastApplEntry(CustLedgEntryNo);
      if ApplicationEntryNo = 0 then
        ERROR(NoApplicationEntryErr,CustLedgEntryNo);
      DtldCustLedgEntry.GET(ApplicationEntryNo);
      UnApplyCustomer(DtldCustLedgEntry);
    END;

    LOCAL PROCEDURE UnApplyCustomer@19(DtldCustLedgEntry@1000000000 : Record "Detailed Cust. Ledg. Entry");
    VAR
      UnapplyCustEntries@1000 : Page "Unapply Customer Entries";
    BEGIN
      with DtldCustLedgEntry do begin
        TESTFIELD("Entry Type","Entry Type"::Application);
        TESTFIELD(Unapplied,false);
        UnapplyCustEntries.SetDtldCustLedgEntry("Entry No.");
        UnapplyCustEntries.LOOKUPMODE(true);
        UnapplyCustEntries.RUNMODAL;
      end;
    END;

    [Internal]
    PROCEDURE PostUnApplyCustomer@4(DtldCustLedgEntry2@1007 : Record "Detailed Cust. Ledg. Entry";DocNo@1000 : Code[20];PostingDate@1001 : Date);
    BEGIN
      PostUnApplyCustomerCommit(DtldCustLedgEntry2,DocNo,PostingDate,true);
    END;

    [Internal]
    PROCEDURE PostUnApplyCustomerCommit@22(DtldCustLedgEntry2@1007 : Record "Detailed Cust. Ledg. Entry";DocNo@1000 : Code[20];PostingDate@1001 : Date;CommitChanges@1020 : Boolean);
    VAR
      GLEntry@1019 : Record "G/L Entry";
      CustLedgEntry@1018 : Record "Cust. Ledger Entry";
      SourceCodeSetup@1017 : Record "Source Code Setup";
      GenJnlLine@1016 : Record "Gen. Journal Line";
      DtldCustLedgEntry@1014 : Record "Detailed Cust. Ledg. Entry";
      DateComprReg@1005 : Record "Date Compr. Register";
      GenJnlPostLine@1002 : Codeunit "Gen. Jnl.-Post Line";
      GenJnlPostPreview@1008 : Codeunit "Gen. Jnl.-Post Preview";
      Window@1012 : Dialog;
      LastTransactionNo@1003 : Integer;
      AddCurrChecked@1004 : Boolean;
      MaxPostingDate@1006 : Date;
    BEGIN
      MaxPostingDate := 0D;
      GLEntry.LOCKTABLE;
      DtldCustLedgEntry.LOCKTABLE;
      CustLedgEntry.LOCKTABLE;
      CustLedgEntry.GET(DtldCustLedgEntry2."Cust. Ledger Entry No.");
      CheckPostingDate(PostingDate,MaxPostingDate);
      if PostingDate < DtldCustLedgEntry2."Posting Date" then
        ERROR(MustNotBeBeforeErr);
      if DtldCustLedgEntry2."Transaction No." = 0 then begin
        DtldCustLedgEntry.SETCURRENTKEY("Application No.","Customer No.","Entry Type");
        DtldCustLedgEntry.SETRANGE("Application No.",DtldCustLedgEntry2."Application No.");
      end else begin
        DtldCustLedgEntry.SETCURRENTKEY("Transaction No.","Customer No.","Entry Type");
        DtldCustLedgEntry.SETRANGE("Transaction No.",DtldCustLedgEntry2."Transaction No.");
      end;
      DtldCustLedgEntry.SETRANGE("Customer No.",DtldCustLedgEntry2."Customer No.");
      DtldCustLedgEntry.SETFILTER("Entry Type",'<>%1',DtldCustLedgEntry."Entry Type"::"Initial Entry");
      DtldCustLedgEntry.SETRANGE(Unapplied,false);
      if DtldCustLedgEntry.FIND('-') then
        repeat
          if not AddCurrChecked then begin
            CheckAdditionalCurrency(PostingDate,DtldCustLedgEntry."Posting Date");
            AddCurrChecked := true;
          end;
          CheckReversal(DtldCustLedgEntry."Cust. Ledger Entry No.");
          if DtldCustLedgEntry."Transaction No." <> 0 then begin
            if DtldCustLedgEntry."Entry Type" = DtldCustLedgEntry."Entry Type"::Application then begin
              LastTransactionNo :=
                FindLastApplTransactionEntry(DtldCustLedgEntry."Cust. Ledger Entry No.");
              if (LastTransactionNo <> 0) and (LastTransactionNo <> DtldCustLedgEntry."Transaction No.") then
                ERROR(UnapplyAllPostedAfterThisEntryErr,DtldCustLedgEntry."Cust. Ledger Entry No.");
            end;
            LastTransactionNo := FindLastTransactionNo(DtldCustLedgEntry."Cust. Ledger Entry No.");
            if (LastTransactionNo <> 0) and (LastTransactionNo <> DtldCustLedgEntry."Transaction No.") then
              ERROR(LatestEntryMustBeAnApplicationErr,DtldCustLedgEntry."Cust. Ledger Entry No.");
          end;
        until DtldCustLedgEntry.NEXT = 0;

      DateComprReg.CheckMaxDateCompressed(MaxPostingDate,0);

      with DtldCustLedgEntry2 do begin
        SourceCodeSetup.GET;
        CustLedgEntry.GET("Cust. Ledger Entry No.");
        GenJnlLine."Document No." := DocNo;
        GenJnlLine."Posting Date" := PostingDate;
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::Customer;
        GenJnlLine."Account No." := "Customer No.";
        GenJnlLine.Correction := true;
        GenJnlLine.CopyCustLedgEntry(CustLedgEntry);
        GenJnlLine."Source Code" := SourceCodeSetup."Unapplied Sales Entry Appln.";
        GenJnlLine."Source Currency Code" := "Currency Code";
        GenJnlLine."System-Created Entry" := true;
        Window.OPEN(UnapplyingMsg);
        OnBeforePostUnapplyCustLedgEntry(GenJnlLine,CustLedgEntry,DtldCustLedgEntry2);
        GenJnlPostLine.UnapplyCustLedgEntry(GenJnlLine,DtldCustLedgEntry2);

        if PreviewMode then
          GenJnlPostPreview.ThrowError;

        if CommitChanges then
          COMMIT;
        Window.CLOSE;
      end;
    END;

    LOCAL PROCEDURE CheckPostingDate@5(PostingDate@1001 : Date;VAR MaxPostingDate@1005 : Date);
    VAR
      GenJnlCheckLine@1000 : Codeunit "Gen. Jnl.-Check Line";
    BEGIN
      if GenJnlCheckLine.DateNotAllowed(PostingDate) then
        ERROR(NotAllowedPostingDatesErr);

      if PostingDate > MaxPostingDate then
        MaxPostingDate := PostingDate;
    END;

    LOCAL PROCEDURE CheckAdditionalCurrency@8(OldPostingDate@1000 : Date;NewPostingDate@1001 : Date);
    VAR
      GLSetup@1002 : Record "General Ledger Setup";
      CurrExchRate@1003 : Record "Currency Exchange Rate";
    BEGIN
      if OldPostingDate = NewPostingDate then
        exit;
      GLSetup.GET;
      if GLSetup."Additional Reporting Currency" <> '' then
        if CurrExchRate.ExchangeRate(OldPostingDate,GLSetup."Additional Reporting Currency") <>
           CurrExchRate.ExchangeRate(NewPostingDate,GLSetup."Additional Reporting Currency")
        then
          ERROR(CannotUnapplyExchRateErr,NewPostingDate);
    END;

    LOCAL PROCEDURE CheckReversal@9(CustLedgEntryNo@1000 : Integer);
    VAR
      CustLedgEntry@1001 : Record "Cust. Ledger Entry";
    BEGIN
      CustLedgEntry.GET(CustLedgEntryNo);
      if CustLedgEntry.Reversed then
        ERROR(CannotUnapplyInReversalErr,CustLedgEntryNo);
    END;

    [External]
    PROCEDURE ApplyCustEntryFormEntry@10(VAR ApplyingCustLedgEntry@1000 : Record "Cust. Ledger Entry");
    VAR
      CustLedgEntry@1002 : Record "Cust. Ledger Entry";
      ApplyCustEntries@1001 : Page "Apply Customer Entries";
      CustEntryApplID@1004 : Code[50];
    BEGIN
      if not ApplyingCustLedgEntry.Open then
        ERROR(CannotApplyClosedEntriesErr);

      CustEntryApplID := USERID;
      if CustEntryApplID = '' then
        CustEntryApplID := '***';
      if ApplyingCustLedgEntry."Remaining Amount" = 0 then
        ApplyingCustLedgEntry.CALCFIELDS("Remaining Amount");

      ApplyingCustLedgEntry."Applying Entry" := true;
      ApplyingCustLedgEntry."Applies-to ID" := CustEntryApplID;
      ApplyingCustLedgEntry."Amount to Apply" := ApplyingCustLedgEntry."Remaining Amount";
      CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",ApplyingCustLedgEntry);
      COMMIT;

      CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
      CustLedgEntry.SETRANGE("Customer No.",ApplyingCustLedgEntry."Customer No.");
      CustLedgEntry.SETRANGE(Open,true);
      if CustLedgEntry.FINDFIRST then begin
        ApplyCustEntries.SetCustLedgEntry(ApplyingCustLedgEntry);
        ApplyCustEntries.SETRECORD(CustLedgEntry);
        ApplyCustEntries.SETTABLEVIEW(CustLedgEntry);
        ApplyCustEntries.RUNMODAL;
        CLEAR(ApplyCustEntries);
        ApplyingCustLedgEntry."Applying Entry" := false;
        ApplyingCustLedgEntry."Applies-to ID" := '';
        ApplyingCustLedgEntry."Amount to Apply" := 0;
      end;
    END;

    LOCAL PROCEDURE FindLastApplTransactionEntry@11(CustLedgEntryNo@1000 : Integer) : Integer;
    VAR
      DtldCustLedgEntry@1001 : Record "Detailed Cust. Ledg. Entry";
      LastTransactionNo@1002 : Integer;
    BEGIN
      DtldCustLedgEntry.SETCURRENTKEY("Cust. Ledger Entry No.","Entry Type");
      DtldCustLedgEntry.SETRANGE("Cust. Ledger Entry No.",CustLedgEntryNo);
      DtldCustLedgEntry.SETRANGE("Entry Type",DtldCustLedgEntry."Entry Type"::Application);
      LastTransactionNo := 0;
      if DtldCustLedgEntry.FIND('-') then
        repeat
          if (DtldCustLedgEntry."Transaction No." > LastTransactionNo) and not DtldCustLedgEntry.Unapplied then
            LastTransactionNo := DtldCustLedgEntry."Transaction No.";
        until DtldCustLedgEntry.NEXT = 0;
      exit(LastTransactionNo);
    END;

    [Internal]
    PROCEDURE PreviewApply@13(CustLedgEntry@1003 : Record "Cust. Ledger Entry";DocumentNo@1002 : Code[20];ApplicationDate@1001 : Date);
    VAR
      GenJnlPostPreview@1004 : Codeunit "Gen. Jnl.-Post Preview";
      CustEntryApplyPostedEntries@1000 : Codeunit "CustEntry-Apply Posted Entries";
    BEGIN
      BINDSUBSCRIPTION(CustEntryApplyPostedEntries);
      CustEntryApplyPostedEntries.SetApplyContext(ApplicationDate,DocumentNo);
      GenJnlPostPreview.Preview(CustEntryApplyPostedEntries,CustLedgEntry);
    END;

    [Internal]
    PROCEDURE PreviewUnapply@15(DetailedCustLedgEntry@1002 : Record "Detailed Cust. Ledg. Entry";DocumentNo@1001 : Code[20];ApplicationDate@1000 : Date);
    VAR
      CustLedgEntry@1005 : Record "Cust. Ledger Entry";
      GenJnlPostPreview@1003 : Codeunit "Gen. Jnl.-Post Preview";
      CustEntryApplyPostedEntries@1004 : Codeunit "CustEntry-Apply Posted Entries";
    BEGIN
      BINDSUBSCRIPTION(CustEntryApplyPostedEntries);
      CustEntryApplyPostedEntries.SetUnapplyContext(DetailedCustLedgEntry,ApplicationDate,DocumentNo);
      GenJnlPostPreview.Preview(CustEntryApplyPostedEntries,CustLedgEntry);
    END;

    [External]
    PROCEDURE SetApplyContext@16(ApplicationDate@1000 : Date;DocumentNo@1001 : Code[20]);
    BEGIN
      ApplicationDatePreviewContext := ApplicationDate;
      DocumentNoPreviewContext := DocumentNo;
      RunOptionPreviewContext := RunOptionPreview::Apply;
    END;

    [External]
    PROCEDURE SetUnapplyContext@28(VAR DetailedCustLedgEntry@1002 : Record "Detailed Cust. Ledg. Entry";ApplicationDate@1000 : Date;DocumentNo@1001 : Code[20]);
    BEGIN
      ApplicationDatePreviewContext := ApplicationDate;
      DocumentNoPreviewContext := DocumentNo;
      DetailedCustLedgEntryPreviewContext := DetailedCustLedgEntry;
      RunOptionPreviewContext := RunOptionPreview::Unapply;
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnPreviewRun@21(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1003 : Variant);
    VAR
      CustEntryApplyPostedEntries@1002 : Codeunit "CustEntry-Apply Posted Entries";
    BEGIN
      CustEntryApplyPostedEntries := Subscriber;
      PreviewMode := true;
      Result := CustEntryApplyPostedEntries.RUN(RecVar);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostApplyCustLedgEntry@18(VAR GenJournalLine@1000 : Record "Gen. Journal Line";CustLedgerEntry@1001 : Record "Cust. Ledger Entry");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostUnapplyCustLedgEntry@20(VAR GenJournalLine@1000 : Record "Gen. Journal Line";CustLedgerEntry@1001 : Record "Cust. Ledger Entry";DetailedCustLedgEntry@1002 : Record "Detailed Cust. Ledg. Entry");
    BEGIN
    END;

    BEGIN
    END.
  }
}

