OBJECT Table 99000829 Planning Component
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ReservePlanningComponent.VerifyQuantity(Rec,xRec);

               ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
               ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");

               GetReqLine;
               "Planning Line Origin" := ReqLine."Planning Line Origin";
               if "Planning Line Origin" <> "Planning Line Origin"::"Order Planning" then
                 TESTFIELD("Worksheet Template Name");

               "Due Date" := ReqLine."Starting Date";
             END;

    OnModify=BEGIN
               ReservePlanningComponent.VerifyChange(Rec,xRec);
             END;

    OnDelete=BEGIN
               ReservePlanningComponent.DeleteLine(Rec);

               CALCFIELDS("Reserved Qty. (Base)");
               TESTFIELD("Reserved Qty. (Base)",0);
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=ENU=Planning Component;
    LookupPageID="Planning Component List";
    DrillDownPageID="Planning Component List";
  }
  FIELDS
  {
    { 1   ;   ;Worksheet Template Name;Code10     ;TableRelation="Req. Wksh. Template";
                                                   CaptionML=ENU=Worksheet Template Name }
    { 2   ;   ;Worksheet Batch Name;Code10        ;TableRelation="Requisition Wksh. Name".Name WHERE ("Worksheet Template Name"=FIELD("Worksheet Template Name"));
                                                   CaptionML=ENU=Worksheet Batch Name }
    { 3   ;   ;Worksheet Line No.  ;Integer       ;TableRelation="Requisition Line"."Line No." WHERE ("Worksheet Template Name"=FIELD("Worksheet Template Name"),
                                                                                                      "Journal Batch Name"=FIELD("Worksheet Batch Name"));
                                                   CaptionML=ENU=Worksheet Line No. }
    { 5   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   BlankZero=true }
    { 11  ;   ;Item No.            ;Code20        ;TableRelation=Item WHERE (Type=CONST(Inventory));
                                                   OnValidate=BEGIN
                                                                ReservePlanningComponent.VerifyChange(Rec,xRec);
                                                                CALCFIELDS("Reserved Qty. (Base)");
                                                                TESTFIELD("Reserved Qty. (Base)",0);

                                                                if "Item No." = '' then begin
                                                                  "Dimension Set ID" := 0;
                                                                  "Shortcut Dimension 1 Code" := '';
                                                                  "Shortcut Dimension 2 Code" := '';
                                                                  exit;
                                                                end;

                                                                GetItem;
                                                                Description := Item.Description;
                                                                VALIDATE("Unit of Measure Code",Item."Base Unit of Measure");
                                                                GetUpdateFromSKU;
                                                                CreateDim(DATABASE::Item,"Item No.");
                                                              END;

                                                   CaptionML=ENU=Item No. }
    { 12  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 13  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE ("Item No."=FIELD("Item No."));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Item No.");

                                                                GetItem;
                                                                GetGLSetup;

                                                                "Unit Cost" := Item."Unit Cost";

                                                                if "Unit of Measure Code" <> '' then begin
                                                                  "Qty. per Unit of Measure" := UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code");
                                                                  "Unit Cost" :=
                                                                    ROUND(
                                                                      Item."Unit Cost" * "Qty. per Unit of Measure",
                                                                      GLSetup."Unit-Amount Rounding Precision");
                                                                end else
                                                                  "Qty. per Unit of Measure" := 1;

                                                                "Indirect Cost %" := ROUND(Item."Indirect Cost %" * "Qty. per Unit of Measure",0.00001);

                                                                "Overhead Rate" :=
                                                                  ROUND(Item."Overhead Rate" * "Qty. per Unit of Measure",
                                                                    GLSetup."Unit-Amount Rounding Precision");

                                                                "Direct Unit Cost" :=
                                                                  ROUND(
                                                                    ("Unit Cost" - "Overhead Rate") / (1 + "Indirect Cost %" / 100),
                                                                    GLSetup."Unit-Amount Rounding Precision");

                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=ENU=Unit of Measure Code }
    { 14  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 15  ;   ;Position            ;Code10        ;CaptionML=ENU=Position }
    { 16  ;   ;Position 2          ;Code10        ;CaptionML=ENU=Position 2 }
    { 17  ;   ;Position 3          ;Code10        ;CaptionML=ENU=Position 3 }
    { 18  ;   ;Lead-Time Offset    ;DateFormula   ;CaptionML=ENU=Lead-Time Offset }
    { 19  ;   ;Routing Link Code   ;Code10        ;TableRelation="Routing Link";
                                                   OnValidate=VAR
                                                                PlanningRtngLine@1001 : Record "Planning Routing Line";
                                                              BEGIN
                                                                VALIDATE("Expected Quantity",Quantity * PlanningNeeds);

                                                                "Due Date" := ReqLine."Starting Date";
                                                                "Due Time" := ReqLine."Starting Time";
                                                                if "Routing Link Code" <> '' then begin
                                                                  PlanningRtngLine.SETRANGE("Worksheet Template Name","Worksheet Template Name");
                                                                  PlanningRtngLine.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
                                                                  PlanningRtngLine.SETRANGE("Worksheet Line No.","Worksheet Line No.");
                                                                  PlanningRtngLine.SETRANGE("Routing Link Code","Routing Link Code");
                                                                  if PlanningRtngLine.FINDFIRST then begin
                                                                    "Due Date" := PlanningRtngLine."Starting Date";
                                                                    "Due Time" := ReqLine."Starting Time";
                                                                  end;
                                                                end;
                                                                if FORMAT("Lead-Time Offset") <> '' then begin
                                                                  if "Due Date" = 0D then
                                                                    "Due Date" := ReqLine."Ending Date";
                                                                  "Due Date" :=
                                                                    "Due Date" -
                                                                    (CALCDATE("Lead-Time Offset",WORKDATE) - WORKDATE);
                                                                  "Due Time" := 000000T;
                                                                end;
                                                                VALIDATE("Due Date");
                                                              END;

                                                   CaptionML=ENU=Routing Link Code }
    { 20  ;   ;Scrap %             ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Expected Quantity",Quantity * PlanningNeeds);
                                                              END;

                                                   CaptionML=ENU=Scrap %;
                                                   DecimalPlaces=0:5;
                                                   MaxValue=100;
                                                   BlankNumbers=BlankNeg }
    { 21  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE ("Item No."=FIELD("Item No."));
                                                   OnValidate=BEGIN
                                                                ReservePlanningComponent.VerifyChange(Rec,xRec);
                                                                CALCFIELDS("Reserved Qty. (Base)");
                                                                TESTFIELD("Reserved Qty. (Base)",0);
                                                                GetUpdateFromSKU;
                                                              END;

                                                   CaptionML=ENU=Variant Code }
    { 25  ;   ;Expected Quantity   ;Decimal       ;OnValidate=BEGIN
                                                                if Item.GET("Item No.") and ("Ref. Order Type" <> "Ref. Order Type"::Assembly) then
                                                                  if Item."Rounding Precision" > 0 then
                                                                    "Expected Quantity" := ROUND("Expected Quantity",Item."Rounding Precision",'>');
                                                                "Expected Quantity (Base)" := ROUND("Expected Quantity" * "Qty. per Unit of Measure",0.00001);
                                                                "Net Quantity (Base)" := "Expected Quantity (Base)" - "Original Expected Qty. (Base)";

                                                                ReservePlanningComponent.VerifyQuantity(Rec,xRec);

                                                                "Cost Amount" := ROUND("Expected Quantity" * "Unit Cost");
                                                                "Overhead Amount" :=
                                                                  ROUND(
                                                                    "Expected Quantity" *
                                                                    (("Direct Unit Cost" * "Indirect Cost %" / 100) + "Overhead Rate"));
                                                                "Direct Cost Amount" := ROUND("Expected Quantity" * "Direct Unit Cost");
                                                              END;

                                                   CaptionML=ENU=Expected Quantity;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 28  ;   ;Flushing Method     ;Option        ;CaptionML=ENU=Flushing Method;
                                                   OptionCaptionML=ENU=Manual,Forward,Backward,Pick + Forward,Pick + Backward;
                                                   OptionString=Manual,Forward,Backward,"Pick + Forward","Pick + Backward" }
    { 30  ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE ("Use As In-Transit"=CONST(false));
                                                   OnValidate=BEGIN
                                                                ReservePlanningComponent.VerifyChange(Rec,xRec);
                                                                GetUpdateFromSKU;
                                                                GetDefaultBin;
                                                              END;

                                                   CaptionML=ENU=Location Code }
    { 31  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 32  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 33  ;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE ("Location Code"=FIELD("Location Code"));
                                                   OnValidate=BEGIN
                                                                ReservePlanningComponent.VerifyChange(Rec,xRec);
                                                              END;

                                                   CaptionML=ENU=Bin Code }
    { 35  ;   ;Supplied-by Line No.;Integer       ;TableRelation="Requisition Line"."Line No." WHERE ("Worksheet Template Name"=FIELD("Worksheet Template Name"),
                                                                                                      "Journal Batch Name"=FIELD("Worksheet Batch Name"),
                                                                                                      "Line No."=FIELD("Supplied-by Line No."));
                                                   CaptionML=ENU=Supplied-by Line No. }
    { 36  ;   ;Planning Level Code ;Integer       ;CaptionML=ENU=Planning Level Code;
                                                   Editable=false }
    { 37  ;   ;Ref. Order Status   ;Option        ;CaptionML=ENU=Ref. Order Status;
                                                   OptionCaptionML=ENU=Simulated,Planned,Firm Planned,Released;
                                                   OptionString=Simulated,Planned,"Firm Planned",Released }
    { 38  ;   ;Ref. Order No.      ;Code20        ;CaptionML=ENU=Ref. Order No. }
    { 39  ;   ;Ref. Order Line No. ;Integer       ;CaptionML=ENU=Ref. Order Line No. }
    { 40  ;   ;Length              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=ENU=Length;
                                                   DecimalPlaces=0:5 }
    { 41  ;   ;Width               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=ENU=Width;
                                                   DecimalPlaces=0:5 }
    { 42  ;   ;Weight              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=ENU=Weight;
                                                   DecimalPlaces=0:5 }
    { 43  ;   ;Depth               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=ENU=Depth;
                                                   DecimalPlaces=0:5 }
    { 44  ;   ;Calculation Formula ;Option        ;OnValidate=BEGIN
                                                                case "Calculation Formula" of
                                                                  "Calculation Formula"::" ":
                                                                    Quantity := "Quantity per";
                                                                  "Calculation Formula"::Length:
                                                                    Quantity := ROUND(Length * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width":
                                                                    Quantity := ROUND(Length * Width * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width * Depth":
                                                                    Quantity := ROUND(Length * Width * Depth * "Quantity per",0.00001);
                                                                  "Calculation Formula"::Weight:
                                                                    Quantity := ROUND(Weight * "Quantity per",0.00001);
                                                                end;
                                                                "Quantity (Base)" := Quantity * "Qty. per Unit of Measure";
                                                                VALIDATE("Expected Quantity",Quantity * PlanningNeeds);
                                                              END;

                                                   CaptionML=ENU=Calculation Formula;
                                                   OptionCaptionML=ENU=" ,Length,Length * Width,Length * Width * Depth,Weight";
                                                   OptionString=" ",Length,"Length * Width","Length * Width * Depth",Weight }
    { 45  ;   ;Quantity per        ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=ENU=Quantity per;
                                                   DecimalPlaces=0:5 }
    { 46  ;   ;Ref. Order Type     ;Option        ;CaptionML=ENU=Ref. Order Type;
                                                   OptionCaptionML=ENU=" ,Purchase,Prod. Order,Transfer,Assembly";
                                                   OptionString=" ",Purchase,"Prod. Order",Transfer,Assembly;
                                                   Editable=false }
    { 50  ;   ;Unit Cost           ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Item No.");

                                                                GetItem;
                                                                GetGLSetup;

                                                                if Item."Costing Method" = Item."Costing Method"::Standard then begin
                                                                  if CurrFieldNo = FIELDNO("Unit Cost") then
                                                                    ERROR(
                                                                      Text001,
                                                                      FIELDCAPTION("Unit Cost"),Item.FIELDCAPTION("Costing Method"),Item."Costing Method");

                                                                  "Unit Cost" :=
                                                                    ROUND(Item."Unit Cost" * "Qty. per Unit of Measure");
                                                                  "Indirect Cost %" :=
                                                                    ROUND(Item."Indirect Cost %" * "Qty. per Unit of Measure",0.00001);
                                                                  "Overhead Rate" :=
                                                                    ROUND(Item."Overhead Rate" * "Qty. per Unit of Measure",
                                                                      GLSetup."Unit-Amount Rounding Precision");
                                                                  "Direct Unit Cost" :=
                                                                    ROUND(("Unit Cost" - "Overhead Rate") / (1 + "Indirect Cost %" / 100),
                                                                      GLSetup."Unit-Amount Rounding Precision");
                                                                end;

                                                                VALIDATE("Expected Quantity");
                                                              END;

                                                   CaptionML=ENU=Unit Cost;
                                                   AutoFormatType=1 }
    { 51  ;   ;Cost Amount         ;Decimal       ;CaptionML=ENU=Cost Amount;
                                                   AutoFormatType=2 }
    { 52  ;   ;Due Date            ;Date          ;OnValidate=VAR
                                                                CheckDateConflict@1000 : Codeunit "Reservation-Check Date Confl.";
                                                              BEGIN
                                                                CheckDateConflict.PlanningComponentCheck(Rec,CurrFieldNo <> 0);
                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=ENU=Due Date }
    { 53  ;   ;Due Time            ;Time          ;OnValidate=BEGIN
                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=ENU=Due Time }
    { 55  ;   ;Direct Unit Cost    ;Decimal       ;CaptionML=ENU=Direct Unit Cost;
                                                   DecimalPlaces=2:5 }
    { 56  ;   ;Indirect Cost %     ;Decimal       ;OnValidate=BEGIN
                                                                "Direct Unit Cost" :=
                                                                  ROUND("Unit Cost" / (1 + "Indirect Cost %" / 100) - "Overhead Rate");
                                                              END;

                                                   CaptionML=ENU=Indirect Cost %;
                                                   DecimalPlaces=0:5 }
    { 57  ;   ;Overhead Rate       ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=ENU=Overhead Rate;
                                                   DecimalPlaces=0:5 }
    { 58  ;   ;Direct Cost Amount  ;Decimal       ;CaptionML=ENU=Direct Cost Amount;
                                                   DecimalPlaces=2:2 }
    { 59  ;   ;Overhead Amount     ;Decimal       ;CaptionML=ENU=Overhead Amount;
                                                   DecimalPlaces=2:2 }
    { 60  ;   ;Qty. per Unit of Measure;Decimal   ;CaptionML=ENU=Qty. per Unit of Measure;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 62  ;   ;Quantity (Base)     ;Decimal       ;CaptionML=ENU=Quantity (Base);
                                                   DecimalPlaces=0:5 }
    { 63  ;   ;Reserved Qty. (Base);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry"."Quantity (Base)" WHERE ("Source ID"=FIELD("Worksheet Template Name"),
                                                                                                                 "Source Ref. No."=FIELD("Line No."),
                                                                                                                 "Source Type"=CONST(99000829),
                                                                                                                 "Source Subtype"=CONST("0"),
                                                                                                                 "Source Batch Name"=FIELD("Worksheet Batch Name"),
                                                                                                                 "Source Prod. Order Line"=FIELD("Worksheet Line No."),
                                                                                                                 "Reservation Status"=CONST(Reservation)));
                                                   CaptionML=ENU=Reserved Qty. (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 71  ;   ;Reserved Quantity   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry".Quantity WHERE ("Source ID"=FIELD("Worksheet Template Name"),
                                                                                                        "Source Ref. No."=FIELD("Line No."),
                                                                                                        "Source Type"=CONST(99000829),
                                                                                                        "Source Subtype"=CONST("0"),
                                                                                                        "Source Batch Name"=FIELD("Worksheet Batch Name"),
                                                                                                        "Source Prod. Order Line"=FIELD("Worksheet Line No."),
                                                                                                        "Reservation Status"=CONST(Reservation)));
                                                   CaptionML=ENU=Reserved Quantity;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 73  ;   ;Expected Quantity (Base);Decimal   ;CaptionML=ENU=Expected Quantity (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 74  ;   ;Original Expected Qty. (Base);Decimal;
                                                   CaptionML=ENU=Original Expected Qty. (Base);
                                                   DecimalPlaces=0:5;
                                                   BlankZero=true;
                                                   Editable=false }
    { 75  ;   ;Net Quantity (Base) ;Decimal       ;CaptionML=ENU=Net Quantity (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 76  ;   ;Due Date-Time       ;DateTime      ;OnValidate=BEGIN
                                                                "Due Date" := DT2DATE("Due Date-Time");
                                                                "Due Time" := DT2TIME("Due Date-Time");
                                                                VALIDATE("Due Date");
                                                              END;

                                                   CaptionML=ENU=Due Date-Time }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
    { 99000875;;Critical           ;Boolean       ;CaptionML=ENU=Critical }
    { 99000915;;Planning Line Origin;Option       ;CaptionML=ENU=Planning Line Origin;
                                                   OptionCaptionML=ENU=" ,Action Message,Planning,Order Planning";
                                                   OptionString=" ","Action Message",Planning,"Order Planning";
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.","Line No.";
                                                   SumIndexFields="Cost Amount";
                                                   MaintainSIFTIndex=false;
                                                   Clustered=true }
    {    ;"Item No.","Variant Code","Location Code","Due Date","Planning Line Origin";
                                                   SumIndexFields="Expected Quantity (Base)","Cost Amount";
                                                   MaintainSIFTIndex=false }
    { false;"Item No.","Variant Code","Location Code","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code","Planning Line Origin","Due Date";
                                                   SumIndexFields="Expected Quantity (Base)","Cost Amount";
                                                   MaintainSQLIndex=false;
                                                   MaintainSIFTIndex=false }
    {    ;"Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.","Item No.";
                                                   MaintainSQLIndex=false }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot rename a %1.';
      Text001@1001 : TextConst 'ENU=You cannot change %1 when %2 is %3.';
      Item@1003 : Record Item;
      ReservEntry@1004 : Record "Reservation Entry";
      GLSetup@1018 : Record "General Ledger Setup";
      ReqLine@1011 : Record "Requisition Line";
      Location@1006 : Record Location;
      ReservEngineMgt@1007 : Codeunit "Reservation Engine Mgt.";
      ReservePlanningComponent@1008 : Codeunit "Plng. Component-Reserve";
      UOMMgt@1009 : Codeunit "Unit of Measure Management";
      DimMgt@1019 : Codeunit DimensionManagement;
      Reservation@1002 : Page Reservation;
      GLSetupRead@1005 : Boolean;

    [External]
    PROCEDURE Caption@5() : Text[100];
    VAR
      ReqWkshName@1000 : Record "Requisition Wksh. Name";
      ReqLine@1001 : Record "Requisition Line";
    BEGIN
      if GETFILTERS = '' then
        exit('');

      if not ReqWkshName.GET("Worksheet Template Name","Worksheet Batch Name") then
        exit('');

      if not ReqLine.GET("Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.") then
        CLEAR(ReqLine);

      exit(
        STRSUBSTNO('%1 %2 %3 %4 %5',
          "Worksheet Batch Name",ReqWkshName.Description,ReqLine.Type,ReqLine."No.",ReqLine.Description));
    END;

    LOCAL PROCEDURE PlanningNeeds@1() : Decimal;
    VAR
      PlanningRtngLine@1001 : Record "Planning Routing Line";
    BEGIN
      GetReqLine;

      "Due Date" := ReqLine."Starting Date";

      PlanningRtngLine.RESET;
      PlanningRtngLine.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.","Worksheet Line No.");
      if "Routing Link Code" <> '' then
        PlanningRtngLine.SETRANGE("Routing Link Code","Routing Link Code");
      if PlanningRtngLine.FINDFIRST then
        exit(
          ReqLine.Quantity *
          (1 + ReqLine."Scrap %" / 100) *
          (1 + PlanningRtngLine."Scrap Factor % (Accumulated)") *
          (1 + "Scrap %" / 100) +
          PlanningRtngLine."Fixed Scrap Qty. (Accum.)");

      exit(ReqLine.Quantity * (1 + ReqLine."Scrap %" / 100) * (1 + "Scrap %" / 100));
    END;

    [External]
    PROCEDURE ShowReservation@8();
    BEGIN
      TESTFIELD("Item No.");
      CLEAR(Reservation);
      Reservation.SetPlanningComponent(Rec);
      Reservation.RUNMODAL;
    END;

    [External]
    PROCEDURE ShowReservationEntries@21(Modal@1000 : Boolean);
    BEGIN
      TESTFIELD("Item No.");
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,true);
      ReservePlanningComponent.FilterReservFor(ReservEntry,Rec);
      if Modal then
        PAGE.RUNMODAL(PAGE::"Reservation Entries",ReservEntry)
      else
        PAGE.RUN(PAGE::"Reservation Entries",ReservEntry);
    END;

    [External]
    PROCEDURE TransferFromComponent@3(VAR ProdOrderComp@1000 : Record "Prod. Order Component");
    BEGIN
      "Ref. Order Type" := "Ref. Order Type"::"Prod. Order";
      "Ref. Order Status" := ProdOrderComp.Status;
      "Ref. Order No." := ProdOrderComp."Prod. Order No.";
      "Ref. Order Line No." := ProdOrderComp."Prod. Order Line No.";
      "Line No." := ProdOrderComp."Line No.";
      "Item No." := ProdOrderComp."Item No.";
      Description := ProdOrderComp.Description;
      "Unit of Measure Code" := ProdOrderComp."Unit of Measure Code";
      "Quantity per" := ProdOrderComp."Quantity per";
      Quantity := ProdOrderComp.Quantity;
      Position := ProdOrderComp.Position;
      "Position 2" := ProdOrderComp."Position 2";
      "Position 3" := ProdOrderComp."Position 3";
      "Lead-Time Offset" := ProdOrderComp."Lead-Time Offset";
      "Routing Link Code" := ProdOrderComp."Routing Link Code";
      "Scrap %" := ProdOrderComp."Scrap %";
      "Variant Code" := ProdOrderComp."Variant Code";
      "Expected Quantity" := ProdOrderComp."Expected Quantity";
      "Location Code" := ProdOrderComp."Location Code";
      "Dimension Set ID" := ProdOrderComp."Dimension Set ID";
      "Shortcut Dimension 1 Code" := ProdOrderComp."Shortcut Dimension 1 Code";
      "Shortcut Dimension 2 Code" := ProdOrderComp."Shortcut Dimension 2 Code";
      "Bin Code" := ProdOrderComp."Bin Code";
      Length := ProdOrderComp.Length;
      Width := ProdOrderComp.Width;
      Weight := ProdOrderComp.Weight;
      Depth := ProdOrderComp.Depth;
      "Calculation Formula" := ProdOrderComp."Calculation Formula";
      "Planning Level Code" := ProdOrderComp."Planning Level Code";
      "Unit Cost" := ProdOrderComp."Unit Cost";
      "Cost Amount" := ProdOrderComp."Cost Amount";
      "Due Date" := ProdOrderComp."Due Date";
      "Direct Unit Cost" := ProdOrderComp."Direct Unit Cost";
      "Indirect Cost %" := ProdOrderComp."Indirect Cost %";
      "Overhead Rate" := ProdOrderComp."Overhead Rate";
      "Direct Cost Amount" := ProdOrderComp."Direct Cost Amount";
      "Overhead Amount" := ProdOrderComp."Overhead Amount";
      "Qty. per Unit of Measure" := ProdOrderComp."Qty. per Unit of Measure";
      "Quantity (Base)" := ProdOrderComp."Quantity (Base)";
      "Expected Quantity (Base)" := ProdOrderComp."Expected Qty. (Base)";
      "Original Expected Qty. (Base)" := ProdOrderComp."Expected Qty. (Base)";
      UpdateDatetime;
    END;

    [External]
    PROCEDURE TransferFromAsmLine@11(VAR AsmLine@1000 : Record "Assembly Line");
    BEGIN
      "Ref. Order Type" := "Ref. Order Type"::Assembly;
      "Ref. Order Status" := AsmLine."Document Type";
      "Ref. Order No." := AsmLine."Document No.";
      "Ref. Order Line No." := AsmLine."Line No.";
      "Line No." := AsmLine."Line No.";
      "Item No." := AsmLine."No.";
      Description := COPYSTR(AsmLine.Description,1,MAXSTRLEN(Description));
      "Unit of Measure Code" := AsmLine."Unit of Measure Code";
      "Quantity per" := AsmLine."Quantity per";
      Quantity := AsmLine."Quantity per";
      "Lead-Time Offset" := AsmLine."Lead-Time Offset";
      Position := AsmLine.Position;
      "Position 2" := AsmLine."Position 2";
      "Position 3" := AsmLine."Position 3";
      "Variant Code" := AsmLine."Variant Code";
      "Expected Quantity" := AsmLine.Quantity;
      "Location Code" := AsmLine."Location Code";
      "Dimension Set ID" := AsmLine."Dimension Set ID";
      "Shortcut Dimension 1 Code" := AsmLine."Shortcut Dimension 1 Code";
      "Shortcut Dimension 2 Code" := AsmLine."Shortcut Dimension 2 Code";
      "Bin Code" := AsmLine."Bin Code";
      "Unit Cost" := AsmLine."Unit Cost";
      "Cost Amount" := AsmLine."Cost Amount";
      "Due Date" := AsmLine."Due Date";
      "Qty. per Unit of Measure" := AsmLine."Qty. per Unit of Measure";
      "Quantity (Base)" := AsmLine."Quantity per";
      "Expected Quantity (Base)" := AsmLine."Quantity (Base)";
      "Original Expected Qty. (Base)" := AsmLine."Quantity (Base)";
      UpdateDatetime;
    END;

    LOCAL PROCEDURE GetUpdateFromSKU@4();
    VAR
      SKU@1000 : Record "Stockkeeping Unit";
      GetPlanningParameters@1001 : Codeunit "Planning-Get Parameters";
    BEGIN
      GetPlanningParameters.AtSKU(SKU,"Item No.","Variant Code","Location Code");
      VALIDATE("Flushing Method",SKU."Flushing Method");
    END;

    [External]
    PROCEDURE BlockDynamicTracking@17(SetBlock@1000 : Boolean);
    BEGIN
      ReservePlanningComponent.Block(SetBlock);
    END;

    LOCAL PROCEDURE UpdateDatetime@20();
    BEGIN
      "Due Date-Time" := CREATEDATETIME("Due Date","Due Time");
    END;

    [External]
    PROCEDURE OpenItemTrackingLines@6500();
    BEGIN
      if "Item No." <> '' then
        ReservePlanningComponent.CallItemTracking(Rec);
    END;

    LOCAL PROCEDURE CreateDim@12(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      TableID@1003 : ARRAY [10] OF Integer;
      No@1004 : ARRAY [10] OF Code[20];
      DimensionSetIDArr@1005 : ARRAY [10] OF Integer;
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      OnAfterCreateDimTableIDs(Rec,CurrFieldNo,TableID,No);

      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      GetReqLine;
      DimensionSetIDArr[1] :=
        DimMgt.GetRecDefaultDimID(Rec,CurrFieldNo,TableID,No,'',"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0);
      DimensionSetIDArr[2] := ReqLine."Dimension Set ID";
      "Dimension Set ID" :=
        DimMgt.GetCombinedDimensionSetID(DimensionSetIDArr,"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@9(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    LOCAL PROCEDURE GetGLSetup@14();
    BEGIN
      if not GLSetupRead then
        GLSetup.GET;
      GLSetupRead := true;
    END;

    LOCAL PROCEDURE GetItem@2();
    BEGIN
      if "Item No." <> Item."No." then
        Item.GET("Item No.");
    END;

    LOCAL PROCEDURE GetReqLine@6();
    BEGIN
      ReqLine.GET("Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.");
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10]);
    BEGIN
      if LocationCode = '' then
        CLEAR(Location)
      else
        if Location.Code <> LocationCode then
          Location.GET(LocationCode);
    END;

    [External]
    PROCEDURE GetDefaultBin@50();
    BEGIN
      if (Quantity * xRec.Quantity > 0) and
         ("Item No." = xRec."Item No.") and
         ("Location Code" = xRec."Location Code") and
         ("Variant Code" = xRec."Variant Code")
      then
        exit;

      "Bin Code" := '';
      if ("Location Code" <> '') and ("Item No." <> '') then
        VALIDATE("Bin Code",GetToBin);
    END;

    LOCAL PROCEDURE FilterLinesWithItemToPlan@70(VAR Item@1000 : Record Item);
    BEGIN
      RESET;
      SETCURRENTKEY("Item No.");
      SETRANGE("Item No.",Item."No.");
      SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      SETFILTER("Due Date",Item.GETFILTER("Date Filter"));
      Item.COPYFILTER("Global Dimension 1 Filter","Shortcut Dimension 1 Code");
      Item.COPYFILTER("Global Dimension 2 Filter","Shortcut Dimension 2 Code");
      SETFILTER("Quantity (Base)",'<>0');
    END;

    [External]
    PROCEDURE FindLinesWithItemToPlan@68(VAR Item@1000 : Record Item) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item);
      exit(FIND('-'));
    END;

    [External]
    PROCEDURE FindCurrForecastName@40(VAR ForecastName@1001 : Code[10]) : Boolean;
    VAR
      UntrackedPlngElement@1000 : Record "Untracked Planning Element";
    BEGIN
      UntrackedPlngElement.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      UntrackedPlngElement.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
      UntrackedPlngElement.SETRANGE("Item No.","Item No.");
      UntrackedPlngElement.SETRANGE("Source Type",DATABASE::"Production Forecast Entry");
      if UntrackedPlngElement.FINDFIRST then begin
        ForecastName := COPYSTR(UntrackedPlngElement."Source ID",1,10);
        exit(true);
      end;
    END;

    [External]
    PROCEDURE ShowDimensions@7();
    BEGIN
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet2(
          "Dimension Set ID",
          STRSUBSTNO(
            '%1 %2 %3',"Worksheet Template Name","Worksheet Batch Name",
            "Worksheet Line No."),
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    LOCAL PROCEDURE GetToBin@15() BinCode : Code[20];
    BEGIN
      GetLocation("Location Code");
      GetReqLine;
      BinCode := GetRefOrderTypeBin;
      if BinCode <> '' then
        exit;
      exit(GetWMSDefaultBin);
    END;

    LOCAL PROCEDURE GetRefOrderTypeBin@28() BinCode : Code[20];
    BEGIN
      case ReqLine."Ref. Order Type" of
        ReqLine."Ref. Order Type"::"Prod. Order":
          begin
            if "Location Code" = ReqLine."Location Code" then
              BinCode := GetLastOperationFromBin;
            if BinCode <> '' then
              exit(BinCode);
            BinCode := GetFlushingMethodBin;
          end;
        ReqLine."Ref. Order Type"::Assembly:
          BinCode := Location."To-Assembly Bin Code";
      end;
    END;

    LOCAL PROCEDURE GetLastOperationFromBin@13() : Code[20];
    VAR
      WMSManagement@1002 : Codeunit "WMS Management";
    BEGIN
      exit(
        WMSManagement.GetLastOperationFromBinCode(
          ReqLine."Routing No.",
          ReqLine."Routing Version Code",
          "Location Code",
          true,
          "Flushing Method"));
    END;

    LOCAL PROCEDURE GetFlushingMethodBin@23() : Code[20];
    BEGIN
      case "Flushing Method" of
        "Flushing Method"::Manual,
        "Flushing Method"::"Pick + Forward",
        "Flushing Method"::"Pick + Backward":
          exit(Location."To-Production Bin Code");
        "Flushing Method"::Forward,
        "Flushing Method"::Backward:
          exit(Location."Open Shop Floor Bin Code");
      end;
    END;

    LOCAL PROCEDURE GetWMSDefaultBin@25() : Code[20];
    VAR
      WMSManagement@1002 : Codeunit "WMS Management";
      BinCode@1000 : Code[20];
    BEGIN
      if Location."Bin Mandatory" and not Location."Directed Put-away and Pick" then
        WMSManagement.GetDefaultBin("Item No.","Variant Code","Location Code",BinCode);
      exit(BinCode);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateDimTableIDs@138(VAR PlanningComponent@1000 : Record "Planning Component";FieldNo@1001 : Integer;TableID@1003 : ARRAY [10] OF Integer;No@1002 : ARRAY [10] OF Code[20]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

