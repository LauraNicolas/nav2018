OBJECT Table 6661 Return Receipt Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Item Ledger Entry"=r,
                TableData "Value Entry"=r;
    OnDelete=VAR
               SalesDocLineComments@1000 : Record "Sales Comment Line";
             BEGIN
               SalesDocLineComments.SETRANGE("Document Type",SalesDocLineComments."Document Type"::"Posted Return Receipt");
               SalesDocLineComments.SETRANGE("No.","Document No.");
               SalesDocLineComments.SETRANGE("Document Line No.","Line No.");
               if not SalesDocLineComments.ISEMPTY then
                 SalesDocLineComments.DELETEALL;
             END;

    CaptionML=ENU=Return Receipt Line;
    LookupPageID="Posted Return Receipt Lines";
  }
  FIELDS
  {
    { 2   ;   ;Sell-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Sell-to Customer No.;
                                                   Editable=false }
    { 3   ;   ;Document No.        ;Code20        ;TableRelation="Return Receipt Header";
                                                   CaptionML=ENU=Document No. }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 5   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)";
                                                   OptionString=" ","G/L Account",Item,Resource,"Fixed Asset","Charge (Item)" }
    { 6   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST("Fixed Asset")) "Fixed Asset"
                                                                 ELSE IF (Type=CONST("Charge (Item)")) "Item Charge";
                                                   CaptionML=ENU=No. }
    { 7   ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE ("Use As In-Transit"=CONST(false));
                                                   CaptionML=ENU=Location Code }
    { 8   ;   ;Posting Group       ;Code20        ;TableRelation=IF (Type=CONST(Item)) "Inventory Posting Group"
                                                                 ELSE IF (Type=CONST("Fixed Asset")) "FA Posting Group";
                                                   CaptionML=ENU=Posting Group;
                                                   Editable=false }
    { 10  ;   ;Shipment Date       ;Date          ;CaptionML=ENU=Shipment Date }
    { 11  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 12  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2 }
    { 13  ;   ;Unit of Measure     ;Text10        ;CaptionML=ENU=Unit of Measure }
    { 15  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5 }
    { 22  ;   ;Unit Price          ;Decimal       ;CaptionML=ENU=Unit Price;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 23  ;   ;Unit Cost (LCY)     ;Decimal       ;CaptionML=ENU=Unit Cost (LCY);
                                                   AutoFormatType=2 }
    { 25  ;   ;VAT %               ;Decimal       ;CaptionML=ENU=VAT %;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 27  ;   ;Line Discount %     ;Decimal       ;CaptionML=ENU=Line Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 32  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Allow Invoice Disc. }
    { 34  ;   ;Gross Weight        ;Decimal       ;CaptionML=ENU=Gross Weight;
                                                   DecimalPlaces=0:5 }
    { 35  ;   ;Net Weight          ;Decimal       ;CaptionML=ENU=Net Weight;
                                                   DecimalPlaces=0:5 }
    { 36  ;   ;Units per Parcel    ;Decimal       ;CaptionML=ENU=Units per Parcel;
                                                   DecimalPlaces=0:5 }
    { 37  ;   ;Unit Volume         ;Decimal       ;CaptionML=ENU=Unit Volume;
                                                   DecimalPlaces=0:5 }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Appl.-to Item Entry }
    { 39  ;   ;Item Rcpt. Entry No.;Integer       ;CaptionML=ENU=Item Rcpt. Entry No. }
    { 40  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1));
                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 41  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2));
                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 42  ;   ;Customer Price Group;Code10        ;TableRelation="Customer Price Group";
                                                   CaptionML=ENU=Customer Price Group }
    { 45  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Job No. }
    { 52  ;   ;Work Type Code      ;Code10        ;TableRelation="Work Type";
                                                   CaptionML=ENU=Work Type Code }
    { 61  ;   ;Quantity Invoiced   ;Decimal       ;CaptionML=ENU=Quantity Invoiced;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 68  ;   ;Bill-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Bill-to Customer No.;
                                                   Editable=false }
    { 74  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 75  ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=ENU=Gen. Prod. Posting Group }
    { 77  ;   ;VAT Calculation Type;Option        ;CaptionML=ENU=VAT Calculation Type;
                                                   OptionCaptionML=ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   OptionString="Normal VAT","Reverse Charge VAT","Full VAT","Sales Tax" }
    { 78  ;   ;Transaction Type    ;Code10        ;TableRelation="Transaction Type";
                                                   CaptionML=ENU=Transaction Type }
    { 79  ;   ;Transport Method    ;Code10        ;TableRelation="Transport Method";
                                                   CaptionML=ENU=Transport Method }
    { 80  ;   ;Attached to Line No.;Integer       ;TableRelation="Return Receipt Line"."Line No." WHERE ("Document No."=FIELD("Document No."));
                                                   CaptionML=ENU=Attached to Line No. }
    { 81  ;   ;Exit Point          ;Code10        ;TableRelation="Entry/Exit Point";
                                                   CaptionML=ENU=Exit Point }
    { 82  ;   ;Area                ;Code10        ;TableRelation=Area;
                                                   CaptionML=ENU=Area }
    { 83  ;   ;Transaction Specification;Code10   ;TableRelation="Transaction Specification";
                                                   CaptionML=ENU=Transaction Specification }
    { 85  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=ENU=Tax Area Code }
    { 86  ;   ;Tax Liable          ;Boolean       ;CaptionML=ENU=Tax Liable }
    { 87  ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   CaptionML=ENU=Tax Group Code }
    { 89  ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 90  ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=ENU=VAT Prod. Posting Group }
    { 91  ;   ;Currency Code       ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Return Receipt Header"."Currency Code" WHERE ("No."=FIELD("Document No.")));
                                                   CaptionML=ENU=Currency Code;
                                                   Editable=false }
    { 97  ;   ;Blanket Order No.   ;Code20        ;TableRelation="Sales Header"."No." WHERE ("Document Type"=CONST("Blanket Order"));
                                                   TestTableRelation=false;
                                                   AccessByPermission=TableData "Return Receipt Header"=R;
                                                   CaptionML=ENU=Blanket Order No. }
    { 98  ;   ;Blanket Order Line No.;Integer     ;TableRelation="Sales Line"."Line No." WHERE ("Document Type"=CONST("Blanket Order"),
                                                                                                "Document No."=FIELD("Blanket Order No."));
                                                   TestTableRelation=false;
                                                   AccessByPermission=TableData "Return Receipt Header"=R;
                                                   CaptionML=ENU=Blanket Order Line No. }
    { 99  ;   ;VAT Base Amount     ;Decimal       ;CaptionML=ENU=VAT Base Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 100 ;   ;Unit Cost           ;Decimal       ;CaptionML=ENU=Unit Cost;
                                                   Editable=false;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 131 ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE ("Item No."=FIELD("No."));
                                                   CaptionML=ENU=Variant Code }
    { 5403;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE ("Location Code"=FIELD("Location Code"),
                                                                                 "Item Filter"=FIELD("No."),
                                                                                 "Variant Filter"=FIELD("Variant Code"));
                                                   CaptionML=ENU=Bin Code }
    { 5404;   ;Qty. per Unit of Measure;Decimal   ;CaptionML=ENU=Qty. per Unit of Measure;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5407;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE ("Item No."=FIELD("No."))
                                                                 ELSE "Unit of Measure";
                                                   CaptionML=ENU=Unit of Measure Code }
    { 5415;   ;Quantity (Base)     ;Decimal       ;CaptionML=ENU=Quantity (Base);
                                                   DecimalPlaces=0:5 }
    { 5461;   ;Qty. Invoiced (Base);Decimal       ;CaptionML=ENU=Qty. Invoiced (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5600;   ;FA Posting Date     ;Date          ;CaptionML=ENU=FA Posting Date }
    { 5602;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   CaptionML=ENU=Depreciation Book Code }
    { 5605;   ;Depr. until FA Posting Date;Boolean;CaptionML=ENU=Depr. until FA Posting Date }
    { 5612;   ;Duplicate in Depreciation Book;Code10;
                                                   TableRelation="Depreciation Book";
                                                   CaptionML=ENU=Duplicate in Depreciation Book }
    { 5613;   ;Use Duplication List;Boolean       ;CaptionML=ENU=Use Duplication List }
    { 5700;   ;Responsibility Center;Code10       ;TableRelation="Responsibility Center";
                                                   ValidateTableRelation=true;
                                                   CaptionML=ENU=Responsibility Center }
    { 5705;   ;Cross-Reference No. ;Code20        ;AccessByPermission=TableData "Item Cross Reference"=R;
                                                   CaptionML=ENU=Cross-Reference No. }
    { 5706;   ;Unit of Measure (Cross Ref.);Code10;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE ("Item No."=FIELD("No."));
                                                   CaptionML=ENU=Unit of Measure (Cross Ref.) }
    { 5707;   ;Cross-Reference Type;Option        ;CaptionML=ENU=Cross-Reference Type;
                                                   OptionCaptionML=ENU=" ,Customer,Vendor,Bar Code";
                                                   OptionString=" ",Customer,Vendor,"Bar Code" }
    { 5708;   ;Cross-Reference Type No.;Code30    ;CaptionML=ENU=Cross-Reference Type No. }
    { 5709;   ;Item Category Code  ;Code20        ;TableRelation=IF (Type=CONST(Item)) "Item Category";
                                                   CaptionML=ENU=Item Category Code }
    { 5710;   ;Nonstock            ;Boolean       ;CaptionML=ENU=Nonstock }
    { 5711;   ;Purchasing Code     ;Code10        ;TableRelation=Purchasing;
                                                   CaptionML=ENU=Purchasing Code }
    { 5712;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE ("Item Category Code"=FIELD("Item Category Code"));
                                                   ValidateTableRelation=false;
                                                   CaptionML=ENU=Product Group Code }
    { 5805;   ;Return Qty. Rcd. Not Invd.;Decimal ;CaptionML=ENU=Return Qty. Rcd. Not Invd.;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5811;   ;Appl.-from Item Entry;Integer      ;AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Appl.-from Item Entry;
                                                   MinValue=0 }
    { 5812;   ;Item Charge Base Amount;Decimal    ;CaptionML=ENU=Item Charge Base Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 5817;   ;Correction          ;Boolean       ;CaptionML=ENU=Correction;
                                                   Editable=false }
    { 6602;   ;Return Order No.    ;Code20        ;CaptionML=ENU=Return Order No.;
                                                   Editable=false }
    { 6603;   ;Return Order Line No.;Integer      ;CaptionML=ENU=Return Order Line No.;
                                                   Editable=false }
    { 6608;   ;Return Reason Code  ;Code10        ;TableRelation="Return Reason";
                                                   CaptionML=ENU=Return Reason Code }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Allow Line Disc. }
    { 7002;   ;Customer Disc. Group;Code20        ;TableRelation="Customer Discount Group";
                                                   CaptionML=ENU=Customer Disc. Group }
  }
  KEYS
  {
    {    ;"Document No.","Line No."               ;Clustered=true }
    {    ;"Return Order No.","Return Order Line No." }
    {    ;"Blanket Order No.","Blanket Order Line No." }
    {    ;"Item Rcpt. Entry No."                   }
    {    ;"Bill-to Customer No."                   }
    {    ;"Sell-to Customer No."                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Currency@1000 : Record Currency;
      ReturnRcptHeader@1005 : Record "Return Receipt Header";
      Text000@1003 : TextConst 'ENU=Return Receipt No. %1:';
      Text001@1002 : TextConst 'ENU=The program cannot find this purchase line.';
      CurrencyRead@1004 : Boolean;

    [External]
    PROCEDURE GetCurrencyCode@1() : Code[10];
    BEGIN
      if "Document No." = ReturnRcptHeader."No." then
        exit(ReturnRcptHeader."Currency Code");
      if ReturnRcptHeader.GET("Document No.") then
        exit(ReturnRcptHeader."Currency Code");
      exit('');
    END;

    [External]
    PROCEDURE ShowDimensions@25();
    VAR
      DimMgt@1000 : Codeunit DimensionManagement;
    BEGIN
      DimMgt.ShowDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2 %3',TABLECAPTION,"Document No.","Line No."));
    END;

    [External]
    PROCEDURE ShowItemTrackingLines@3();
    VAR
      ItemTrackingDocMgt@1000 : Codeunit "Item Tracking Doc. Management";
    BEGIN
      ItemTrackingDocMgt.ShowItemTrackingForShptRcptLine(DATABASE::"Return Receipt Line",0,"Document No.",'',0,"Line No.");
    END;

    [External]
    PROCEDURE InsertInvLineFromRetRcptLine@2(VAR SalesLine@1000 : Record "Sales Line");
    VAR
      SalesHeader@1011 : Record "Sales Header";
      SalesHeader2@1012 : Record "Sales Header";
      SalesOrderHeader@1010 : Record "Sales Header";
      SalesOrderLine@1005 : Record "Sales Line";
      TempSalesLine@1002 : TEMPORARY Record "Sales Line";
      SalesSetup@1020 : Record "Sales & Receivables Setup";
      TransferOldExtLines@1007 : Codeunit "Transfer Old Ext. Text Lines";
      ItemTrackingMgt@1008 : Codeunit "Item Tracking Management";
      ExtTextLine@1006 : Boolean;
      NextLineNo@1001 : Integer;
    BEGIN
      SETRANGE("Document No.","Document No.");

      TempSalesLine := SalesLine;
      if SalesLine.FIND('+') then
        NextLineNo := SalesLine."Line No." + 10000
      else
        NextLineNo := 10000;

      if SalesHeader."No." <> TempSalesLine."Document No." then
        SalesHeader.GET(TempSalesLine."Document Type",TempSalesLine."Document No.");

      if SalesLine."Return Receipt No." <> "Document No." then begin
        SalesLine.INIT;
        SalesLine."Line No." := NextLineNo;
        SalesLine."Document Type" := TempSalesLine."Document Type";
        SalesLine."Document No." := TempSalesLine."Document No.";
        SalesLine.Description := STRSUBSTNO(Text000,"Document No.");
        SalesLine.INSERT;
        NextLineNo := NextLineNo + 10000;
      end;

      TransferOldExtLines.ClearLineNumbers;
      SalesSetup.GET;
      repeat
        ExtTextLine := (TransferOldExtLines.GetNewLineNumber("Attached to Line No.") <> 0);

        if not SalesOrderLine.GET(
             SalesOrderLine."Document Type"::"Return Order","Return Order No.","Return Order Line No.")
        then begin
          if ExtTextLine then begin
            SalesOrderLine.INIT;
            SalesOrderLine."Line No." := "Return Order Line No.";
            SalesOrderLine.Description := Description;
            SalesOrderLine."Description 2" := "Description 2";
          end  else
            ERROR(Text001);
        end else begin
          if (SalesHeader2."Document Type" <> SalesOrderLine."Document Type"::"Return Order") or
             (SalesHeader2."No." <> SalesOrderLine."Document No.")
          then
            SalesHeader2.GET(SalesOrderLine."Document Type"::"Return Order","Return Order No.");

          InitCurrency("Currency Code");

          if SalesHeader."Prices Including VAT" then begin
            if not SalesHeader2."Prices Including VAT" then
              SalesOrderLine."Unit Price" :=
                ROUND(
                  SalesOrderLine."Unit Price" * (1 + SalesOrderLine."VAT %" / 100),
                  Currency."Unit-Amount Rounding Precision");
          end else begin
            if SalesHeader2."Prices Including VAT" then
              SalesOrderLine."Unit Price" :=
                ROUND(
                  SalesOrderLine."Unit Price" / (1 + SalesOrderLine."VAT %" / 100),
                  Currency."Unit-Amount Rounding Precision");
          end;
        end;
        SalesLine := SalesOrderLine;
        SalesLine."Line No." := NextLineNo;
        SalesLine."Document Type" := TempSalesLine."Document Type";
        SalesLine."Document No." := TempSalesLine."Document No.";
        SalesLine."Variant Code" := "Variant Code";
        SalesLine."Location Code" := "Location Code";
        SalesLine."Return Reason Code" := "Return Reason Code";
        SalesLine."Quantity (Base)" := 0;
        SalesLine.Quantity := 0;
        SalesLine."Outstanding Qty. (Base)" := 0;
        SalesLine."Outstanding Quantity" := 0;
        SalesLine."Return Qty. Received" := 0;
        SalesLine."Return Qty. Received (Base)" := 0;
        SalesLine."Quantity Invoiced" := 0;
        SalesLine."Qty. Invoiced (Base)" := 0;
        SalesLine."Drop Shipment" := false;
        SalesLine."Return Receipt No." := "Document No.";
        SalesLine."Return Receipt Line No." := "Line No.";
        SalesLine."Appl.-to Item Entry" := 0;
        SalesLine."Appl.-from Item Entry" := 0;

        if not ExtTextLine then begin
          SalesLine.VALIDATE(Quantity,Quantity - "Quantity Invoiced");
          SalesLine.VALIDATE("Unit Price",SalesOrderLine."Unit Price");
          SalesLine."Allow Line Disc." := SalesOrderLine."Allow Line Disc.";
          SalesLine."Allow Invoice Disc." := SalesOrderLine."Allow Invoice Disc.";
          SalesLine.VALIDATE("Line Discount %",SalesOrderLine."Line Discount %");
          if SalesOrderLine.Quantity = 0 then
            SalesLine.VALIDATE("Inv. Discount Amount",0)
          else
            SalesLine.VALIDATE(
              "Inv. Discount Amount",
              ROUND(
                SalesOrderLine."Inv. Discount Amount" * SalesLine.Quantity / SalesOrderLine.Quantity,
                Currency."Amount Rounding Precision"));
        end;
        SalesLine."Attached to Line No." :=
          TransferOldExtLines.TransferExtendedText(
            SalesOrderLine."Line No.",
            NextLineNo,
            "Attached to Line No.");
        SalesLine."Shortcut Dimension 1 Code" := SalesOrderLine."Shortcut Dimension 1 Code";
        SalesLine."Shortcut Dimension 2 Code" := SalesOrderLine."Shortcut Dimension 2 Code";
        SalesLine."Dimension Set ID" := SalesOrderLine."Dimension Set ID";
        SalesLine.INSERT;

        ItemTrackingMgt.CopyHandledItemTrkgToInvLine(SalesOrderLine,SalesLine);

        NextLineNo := NextLineNo + 10000;
        if "Attached to Line No." = 0 then
          SETRANGE("Attached to Line No.","Line No.");

      until (NEXT = 0) or ("Attached to Line No." = 0);

      if SalesOrderHeader.GET(SalesOrderHeader."Document Type"::Order,"Return Order No.") then begin
        SalesOrderHeader."Get Shipment Used" := true;
        SalesOrderHeader.MODIFY;
      end;
    END;

    LOCAL PROCEDURE GetSalesCrMemoLines@4(VAR TempSalesCrMemoLine@1000 : TEMPORARY Record "Sales Cr.Memo Line");
    VAR
      SalesCrMemoLine@1003 : Record "Sales Cr.Memo Line";
      ItemLedgEntry@1002 : Record "Item Ledger Entry";
      ValueEntry@1001 : Record "Value Entry";
    BEGIN
      TempSalesCrMemoLine.RESET;
      TempSalesCrMemoLine.DELETEALL;

      if Type <> Type::Item then
        exit;

      FilterPstdDocLnItemLedgEntries(ItemLedgEntry);
      ItemLedgEntry.SETFILTER("Invoiced Quantity",'<>0');
      if ItemLedgEntry.FINDSET then begin
        ValueEntry.SETCURRENTKEY("Item Ledger Entry No.","Entry Type");
        ValueEntry.SETRANGE("Entry Type",ValueEntry."Entry Type"::"Direct Cost");
        ValueEntry.SETFILTER("Invoiced Quantity",'<>0');
        repeat
          ValueEntry.SETRANGE("Item Ledger Entry No.",ItemLedgEntry."Entry No.");
          if ValueEntry.FINDSET then
            repeat
              if ValueEntry."Document Type" = ValueEntry."Document Type"::"Sales Credit Memo" then
                if SalesCrMemoLine.GET(ValueEntry."Document No.",ValueEntry."Document Line No.") then begin
                  TempSalesCrMemoLine.INIT;
                  TempSalesCrMemoLine := SalesCrMemoLine;
                  if TempSalesCrMemoLine.INSERT then;
                end;
            until ValueEntry.NEXT = 0;
        until ItemLedgEntry.NEXT = 0;
      end;
    END;

    [External]
    PROCEDURE FilterPstdDocLnItemLedgEntries@6(VAR ItemLedgEntry@1000 : Record "Item Ledger Entry");
    BEGIN
      ItemLedgEntry.RESET;
      ItemLedgEntry.SETCURRENTKEY("Document No.");
      ItemLedgEntry.SETRANGE("Document No.","Document No.");
      ItemLedgEntry.SETRANGE("Document Type",ItemLedgEntry."Document Type"::"Sales Return Receipt");
      ItemLedgEntry.SETRANGE("Document Line No.","Line No.");
    END;

    [External]
    PROCEDURE ShowItemSalesCrMemoLines@9();
    VAR
      TempSalesCrMemoLine@1000 : TEMPORARY Record "Sales Cr.Memo Line";
    BEGIN
      if Type = Type::Item then begin
        GetSalesCrMemoLines(TempSalesCrMemoLine);
        PAGE.RUNMODAL(0,TempSalesCrMemoLine);
      end;
    END;

    LOCAL PROCEDURE InitCurrency@7(CurrencyCode@1001 : Code[10]);
    BEGIN
      if (Currency.Code = CurrencyCode) and CurrencyRead then
        exit;

      if CurrencyCode <> '' then
        Currency.GET(CurrencyCode)
      else
        Currency.InitRoundingPrecision;
      CurrencyRead := true;
    END;

    [External]
    PROCEDURE ShowLineComments@5();
    VAR
      SalesCommentLine@1000 : Record "Sales Comment Line";
    BEGIN
      SalesCommentLine.ShowComments(SalesCommentLine."Document Type"::"Posted Return Receipt","Document No.","Line No.");
    END;

    [External]
    PROCEDURE InitFromSalesLine@10(ReturnRcptHeader@1001 : Record "Return Receipt Header";SalesLine@1002 : Record "Sales Line");
    BEGIN
      INIT;
      TRANSFERFIELDS(SalesLine);
      if ("No." = '') and (Type in [Type::"G/L Account"..Type::"Charge (Item)"]) then
        Type := Type::" ";
      "Posting Date" := ReturnRcptHeader."Posting Date";
      "Document No." := ReturnRcptHeader."No.";
      Quantity := SalesLine."Return Qty. to Receive";
      "Quantity (Base)" := SalesLine."Return Qty. to Receive (Base)";
      if ABS(SalesLine."Qty. to Invoice") > ABS(SalesLine."Return Qty. to Receive") then begin
        "Quantity Invoiced" := SalesLine."Return Qty. to Receive";
        "Qty. Invoiced (Base)" := SalesLine."Return Qty. to Receive (Base)";
      end else begin
        "Quantity Invoiced" := SalesLine."Qty. to Invoice";
        "Qty. Invoiced (Base)" := SalesLine."Qty. to Invoice (Base)";
      end;
      "Return Qty. Rcd. Not Invd." :=
        Quantity - "Quantity Invoiced";
      if SalesLine."Document Type" = SalesLine."Document Type"::"Return Order" then begin
        "Return Order No." := SalesLine."Document No.";
        "Return Order Line No." := SalesLine."Line No.";
      end;

      OnAfterInitFromSalesLine(ReturnRcptHeader,SalesLine,Rec);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitFromSalesLine@8(ReturnRcptHeader@1000 : Record "Return Receipt Header";SalesLine@1001 : Record "Sales Line";VAR ReturnRcptLine@1002 : Record "Return Receipt Line");
    BEGIN
    END;

    BEGIN
    END.
  }
}

