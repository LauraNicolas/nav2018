OBJECT Codeunit 130402 CAL Command Line Test Runner
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Subtype=TestRunner;
    TestIsolation=Codeunit;
    OnRun=VAR
            CALTestEnabledCodeunit@1000 : Record "CAL Test Enabled Codeunit";
            CALTestResult@1001 : Record "CAL Test Result";
            CodeCoverageMgt@1003 : Codeunit "Code Coverage Mgt.";
            CALTestMgt@1004 : Codeunit "CAL Test Management";
          BEGIN
            TestRunNo := CALTestResult.LastTestRunNo + 1;
            CompanyWorkDate := WORKDATE;

            if CALTestEnabledCodeunit.FINDSET then
              repeat
                if CALTestMgt.DoesTestCodeunitExist(CALTestEnabledCodeunit."Test Codeunit ID") then begin
                  CodeCoverageMgt.Start(true);
                  CODEUNIT.RUN(CALTestEnabledCodeunit."Test Codeunit ID");
                  CodeCoverageMgt.Stop;
                  CALTestMgt.ExtendTestCoverage(CALTestEnabledCodeunit."Test Codeunit ID");
                end;
              until CALTestEnabledCodeunit.NEXT = 0
          END;

  }
  CODE
  {
    VAR
      LibraryRandom@1002 : Codeunit "Library - Random";
      TestRunNo@1001 : Integer;
      CompanyWorkDate@1000 : Date;

    [Internal]
    PROCEDURE OnBeforeTestRun@1(CodeunitID@1000 : Integer;CodeunitName@1001 : Text[30];FunctionName@1002 : Text[128];FunctionTestPermissions@1003 : TestPermissions) : Boolean;
    VAR
      CALTestResult@1004 : Record "CAL Test Result";
    BEGIN
      if FunctionName = '' then
        exit(true);

      LibraryRandom.SetSeed(1);
      APPLICATIONAREA('');

      WORKDATE := CompanyWorkDate;

      CLEARLASTERROR;
      CALTestResult.Initialize(TestRunNo,CodeunitID,FunctionName,CURRENTDATETIME);
      exit(true)
    END;

    [Internal]
    PROCEDURE OnAfterTestRun@2(CodeunitID@1002 : Integer;CodeunitName@1001 : Text[30];FunctionName@1000 : Text[128];FunctionTestPermissions@1005 : TestPermissions;Success@1003 : Boolean);
    VAR
      CALTestResult@1004 : Record "CAL Test Result";
    BEGIN
      if FunctionName = '' then
        exit;

      CALTestResult.FINDLAST;
      CALTestResult.Update(Success,CURRENTDATETIME);

      APPLICATIONAREA('');
    END;

    BEGIN
    END.
  }
}

