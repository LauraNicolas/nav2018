OBJECT Codeunit 418 User Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "G/L Entry"=rm,
                TableData "Cust. Ledger Entry"=rm,
                TableData "Vendor Ledger Entry"=rm,
                TableData "G/L Register"=rm,
                TableData "Item Register"=rm,
                TableData "G/L Budget Entry"=rm,
                TableData "Sales Shipment Header"=rm,
                TableData "Sales Invoice Header"=rm,
                TableData "Sales Cr.Memo Header"=rm,
                TableData "Purch. Rcpt. Header"=rm,
                TableData "Purch. Inv. Header"=rm,
                TableData "Purch. Cr. Memo Hdr."=rm,
                TableData "Job Ledger Entry"=rm,
                TableData "Res. Ledger Entry"=rm,
                TableData "Resource Register"=rm,
                TableData "Job Register"=rm,
                TableData "VAT Entry"=rm,
                TableData "Bank Account Ledger Entry"=rm,
                TableData "Check Ledger Entry"=rm,
                TableData "Phys. Inventory Ledger Entry"=rm,
                TableData "Issued Reminder Header"=rm,
                TableData "Reminder/Fin. Charge Entry"=rm,
                TableData "Issued Fin. Charge Memo Header"=rm,
                TableData "Reservation Entry"=rm,
                TableData "Item Application Entry"=rm,
                TableData "Detailed Cust. Ledg. Entry"=rm,
                TableData "Detailed Vendor Ledg. Entry"=rm,
                TableData "Change Log Entry"=rm,
                TableData "Approval Entry"=rm,
                TableData "Approval Comment Line"=rm,
                TableData "Posted Approval Entry"=rm,
                TableData "Posted Approval Comment Line"=rm,
                TableData "Posted Assembly Header"=rm,
                TableData "Cost Entry"=rm,
                TableData "Cost Register"=rm,
                TableData "Cost Budget Entry"=rm,
                TableData "Cost Budget Register"=rm,
                TableData "Interaction Log Entry"=rm,
                TableData "Campaign Entry"=rm,
                TableData "FA Ledger Entry"=rm,
                TableData "FA Register"=rm,
                TableData "Maintenance Ledger Entry"=rm,
                TableData "Ins. Coverage Ledger Entry"=rm,
                TableData "Insurance Register"=rm,
                TableData "Value Entry"=rm,
                TableData "Service Ledger Entry"=rm,
                TableData "Service Register"=rm,
                TableData "Contract Gain/Loss Entry"=rm,
                TableData "Filed Service Contract Header"=rm,
                TableData "Service Shipment Header"=rm,
                TableData "Service Invoice Header"=rm,
                TableData "Service Cr.Memo Header"=rm,
                TableData "Return Shipment Header"=rm,
                TableData "Return Receipt Header"=rm,
                TableData "Item Budget Entry"=rm,
                TableData "Warehouse Entry"=rm,
                TableData "Warehouse Register"=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The user name %1 does not exist.';
      Text001@1002 : TextConst 'ENU=You are renaming an existing user. This will also update all related records. Are you sure that you want to rename the user?';
      Text002@1001 : TextConst 'ENU=The account %1 already exists.';
      Text003@1003 : TextConst 'ENU=You do not have permissions for this action.';

    [External]
    PROCEDURE ValidateUserID@6(UserName@1000 : Code[50]);
    VAR
      User@1001 : Record User;
    BEGIN
      if UserName <> '' then begin
        User.SETCURRENTKEY("User Name");
        User.SETRANGE("User Name",UserName);
        if not User.FINDFIRST then begin
          User.RESET;
          if not User.ISEMPTY then
            ERROR(Text000,UserName);
        end;
      end;
    END;

    [External]
    PROCEDURE LookupUserID@2(VAR UserName@1000 : Code[50]);
    VAR
      SID@1002 : GUID;
    BEGIN
      LookupUser(UserName,SID);
    END;

    [External]
    PROCEDURE LookupUser@3(VAR UserName@1000 : Code[50];VAR SID@1001 : GUID) : Boolean;
    VAR
      User@1002 : Record User;
    BEGIN
      User.RESET;
      User.SETCURRENTKEY("User Name");
      User."User Name" := UserName;
      if User.FIND('=><') then;
      if PAGE.RUNMODAL(PAGE::Users,User) = ACTION::LookupOK then begin
        UserName := User."User Name";
        SID := User."User Security ID";
        exit(true);
      end;

      exit(false);
    END;

    [Internal]
    PROCEDURE ValidateUserName@4(NewUser@1007 : Record User;OldUser@1008 : Record User;WindowsUserName@1005 : Text);
    VAR
      User@1000 : Record User;
    BEGIN
      if NewUser."User Name" <> OldUser."User Name" then begin
        User.SETRANGE("User Name",NewUser."User Name");
        User.SETFILTER("User Security ID",'<>%1',OldUser."User Security ID");
        if User.FINDFIRST then
          ERROR(Text002,NewUser."User Name");

        if NewUser."Windows Security ID" <> '' then
          NewUser.TESTFIELD("User Name",WindowsUserName);

        if OldUser."User Name" <> '' then
          if CONFIRM(Text001,false) then
            RenameUser(OldUser."User Name",NewUser."User Name")
          else
            ERROR('');
      end;
    END;

    LOCAL PROCEDURE IsPrimaryKeyField@30(TableID@1005 : Integer;FieldID@1006 : Integer;VAR NumberOfPrimaryKeyFields@1004 : Integer) : Boolean;
    VAR
      ConfigValidateMgt@1000 : Codeunit "Config. Validate Management";
      RecRef@1003 : RecordRef;
      KeyRef@1002 : KeyRef;
    BEGIN
      RecRef.OPEN(TableID);
      KeyRef := RecRef.KEYINDEX(1);
      NumberOfPrimaryKeyFields := KeyRef.FIELDCOUNT;
      exit(ConfigValidateMgt.IsKeyField(TableID,FieldID));
    END;

    LOCAL PROCEDURE RenameRecord@8(VAR RecRef@1000 : RecordRef;TableNo@1004 : Integer;NumberOfPrimaryKeyFields@1001 : Integer;UserName@1003 : Code[50];Company@1014 : Text[30]);
    VAR
      UserTimeRegister@1005 : Record "User Time Register";
      PrinterSelection@1006 : Record "Printer Selection";
      SelectedDimension@1007 : Record "Selected Dimension";
      OutlookSynchUserSetup@1008 : Record "Outlook Synch. User Setup";
      FAJournalSetup@1009 : Record "FA Journal Setup";
      AnalysisSelectedDimension@1010 : Record "Analysis Selected Dimension";
      WarehouseEmployee@1002 : Record "Warehouse Employee";
      MyCustomer@1011 : Record "My Customer";
      MyVendor@1012 : Record "My Vendor";
      MyItem@1013 : Record "My Item";
      MyAccount@1017 : Record "My Account";
      CueSetup@1015 : Record "Cue Setup";
      ApplicationAreaSetup@1016 : Record "Application Area Setup";
      MyJob@1018 : Record "My Job";
      MyTimeSheets@1019 : Record "My Time Sheets";
    BEGIN
      if NumberOfPrimaryKeyFields = 1 then
        RecRef.RENAME(UserName)
      else
        case TableNo of
          DATABASE::"User Time Register":
            begin
              UserTimeRegister.CHANGECOMPANY(Company);
              RecRef.SETTABLE(UserTimeRegister);
              UserTimeRegister.RENAME(UserName,UserTimeRegister.Date);
            end;
          DATABASE::"Printer Selection":
            begin
              RecRef.SETTABLE(PrinterSelection);
              PrinterSelection.RENAME(UserName,PrinterSelection."Report ID");
            end;
          DATABASE::"Selected Dimension":
            begin
              SelectedDimension.CHANGECOMPANY(Company);
              RecRef.SETTABLE(SelectedDimension);
              SelectedDimension.RENAME(UserName,SelectedDimension."Object Type",SelectedDimension."Object ID",
                SelectedDimension."Analysis View Code",SelectedDimension."Dimension Code");
            end;
          DATABASE::"Outlook Synch. User Setup":
            begin
              OutlookSynchUserSetup.CHANGECOMPANY(Company);
              RecRef.SETTABLE(OutlookSynchUserSetup);
              OutlookSynchUserSetup.RENAME(UserName,OutlookSynchUserSetup."Synch. Entity Code");
            end;
          DATABASE::"FA Journal Setup":
            begin
              FAJournalSetup.CHANGECOMPANY(Company);
              RecRef.SETTABLE(FAJournalSetup);
              FAJournalSetup.RENAME(FAJournalSetup."Depreciation Book Code",UserName);
            end;
          DATABASE::"Analysis Selected Dimension":
            begin
              AnalysisSelectedDimension.CHANGECOMPANY(Company);
              RecRef.SETTABLE(AnalysisSelectedDimension);
              AnalysisSelectedDimension.RENAME(UserName,AnalysisSelectedDimension."Object Type",AnalysisSelectedDimension."Object ID",
                AnalysisSelectedDimension."Analysis Area",AnalysisSelectedDimension."Analysis View Code",
                AnalysisSelectedDimension."Dimension Code");
            end;
          DATABASE::"Cue Setup":
            begin
              CueSetup.CHANGECOMPANY(Company);
              RecRef.SETTABLE(CueSetup);
              CueSetup.RENAME(UserName,CueSetup."Table ID",CueSetup."Field No.");
            end;
          DATABASE::"Warehouse Employee":
            begin
              WarehouseEmployee.CHANGECOMPANY(Company);
              RecRef.SETTABLE(WarehouseEmployee);
              WarehouseEmployee.RENAME(UserName,WarehouseEmployee."Location Code");
            end;
          DATABASE::"My Customer":
            begin
              MyCustomer.CHANGECOMPANY(Company);
              RecRef.SETTABLE(MyCustomer);
              MyCustomer.RENAME(UserName,MyCustomer."Customer No.");
            end;
          DATABASE::"My Vendor":
            begin
              MyVendor.CHANGECOMPANY(Company);
              RecRef.SETTABLE(MyVendor);
              MyVendor.RENAME(UserName,MyVendor."Vendor No.");
            end;
          DATABASE::"My Item":
            begin
              MyItem.CHANGECOMPANY(Company);
              RecRef.SETTABLE(MyItem);
              MyItem.RENAME(UserName,MyItem."Item No.");
            end;
          DATABASE::"My Account":
            begin
              MyAccount.CHANGECOMPANY(Company);
              RecRef.SETTABLE(MyAccount);
              MyAccount.RENAME(UserName,MyAccount."Account No.");
            end;
          DATABASE::"Application Area Setup":
            begin
              ApplicationAreaSetup.CHANGECOMPANY(Company);
              RecRef.SETTABLE(ApplicationAreaSetup);
              ApplicationAreaSetup.RENAME('','',UserName);
            end;
          DATABASE::"My Job":
            begin
              MyJob.CHANGECOMPANY(Company);
              RecRef.SETTABLE(MyJob);
              MyJob.RENAME(UserName,MyJob."Job No.");
            end;
          DATABASE::"My Time Sheets":
            begin
              MyTimeSheets.CHANGECOMPANY(Company);
              RecRef.SETTABLE(MyTimeSheets);
              MyTimeSheets.RENAME(UserName,MyTimeSheets."Time Sheet No.");
            end;
        end;
      OnAfterRenameRecord(RecRef,TableNo,NumberOfPrimaryKeyFields,UserName,Company);
    END;

    LOCAL PROCEDURE RenameUser@5(OldUserName@1000 : Code[50];NewUserName@1001 : Code[50]);
    VAR
      User@1008 : Record User;
      Field@1007 : Record Field;
      TableInformation@1006 : Record "Table Information";
      Company@1009 : Record Company;
      RecRef@1005 : RecordRef;
      FieldRef@1004 : FieldRef;
      FieldRef2@1003 : FieldRef;
      NumberOfPrimaryKeyFields@1002 : Integer;
    BEGIN
      Field.SETRANGE(RelationTableNo,DATABASE::User);
      Field.SETRANGE(RelationFieldNo,User.FIELDNO("User Name"));
      if Field.FINDSET then
        repeat
          Company.FINDSET;
          repeat
            RecRef.OPEN(Field.TableNo,false,Company.Name);
            if RecRef.READPERMISSION then begin
              FieldRef := RecRef.FIELD(Field."No.");
              FieldRef.SETRANGE(OldUserName);
              if RecRef.FINDSET(true) then
                repeat
                  if IsPrimaryKeyField(Field.TableNo,Field."No.",NumberOfPrimaryKeyFields) then
                    RenameRecord(RecRef,Field.TableNo,NumberOfPrimaryKeyFields,NewUserName,Company.Name)
                  else begin
                    FieldRef2 := RecRef.FIELD(Field."No.");
                    FieldRef2.VALUE := NewUserName;
                    RecRef.MODIFY;
                  end;
                until RecRef.NEXT = 0;
            end else begin
              TableInformation.SETFILTER("Company Name",'%1|%2','',Company.Name);
              TableInformation.SETRANGE("Table No.",Field.TableNo);
              TableInformation.FINDFIRST;
              if TableInformation."No. of Records" > 0 then
                ERROR(Text003);
            end;
            RecRef.CLOSE;
          until Company.NEXT = 0;
        until Field.NEXT = 0;
    END;

    [Integration]
    [External]
    PROCEDURE OnAfterRenameRecord@1(VAR RecRef@1004 : RecordRef;TableNo@1003 : Integer;NumberOfPrimaryKeyFields@1002 : Integer;UserName@1001 : Code[50];Company@1000 : Text[30]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

