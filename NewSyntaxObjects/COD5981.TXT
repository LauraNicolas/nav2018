OBJECT Codeunit 5981 Service-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Service Line";
    EventSubscriberInstance=Manual;
    OnRun=BEGIN
            Code(Rec,ServiceHeaderPreviewContext);
          END;

  }
  CODE
  {
    VAR
      ShipInvoiceConsumeQst@1000 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice,Ship and &Consume';
      PostConfirmQst@1001 : TextConst '@@@="%1 = Document Type";ENU=Do you want to post the %1?';
      ServiceHeaderPreviewContext@1003 : Record "Service Header";
      Selection@1005 : Integer;
      PreviewMode@1002 : Boolean;
      CancelErr@1006 : TextConst 'ENU=The preview has been canceled.';
      NothingToPostErr@1007 : TextConst 'ENU=There is nothing to post.';

    LOCAL PROCEDURE Code@1(VAR PassedServLine@1003 : Record "Service Line";VAR PassedServiceHeader@1005 : Record "Service Header");
    VAR
      ServicePost@1004 : Codeunit "Service-Post";
      Ship@1000 : Boolean;
      Consume@1001 : Boolean;
      Invoice@1002 : Boolean;
      HideDialog@1006 : Boolean;
    BEGIN
      if not PassedServiceHeader.FIND then
        ERROR(NothingToPostErr);

      OnBeforeConfirmServPost(PassedServiceHeader,HideDialog,Ship,Consume,Invoice);
      with PassedServiceHeader do begin
        if not HideDialog then
          case "Document Type" of
            "Document Type"::Order:
              begin
                Selection := STRMENU(ShipInvoiceConsumeQst,3);
                if Selection = 0 then begin
                  if PreviewMode then
                    ERROR(CancelErr);
                  exit;
                end;
                Ship := Selection in [1,3,4];
                Consume := Selection in [4];
                Invoice := Selection in [2,3];
              end
            else
              if not PreviewMode then
                if not CONFIRM(PostConfirmQst,false,"Document Type") then
                  exit;
          end;

        ServicePost.SetPreviewMode(PreviewMode);
        ServicePost.PostWithLines(PassedServiceHeader,PassedServLine,Ship,Consume,Invoice);

        if not PreviewMode then
          COMMIT;
      end;
    END;

    [Internal]
    PROCEDURE PostDocument@2(VAR ServiceHeaderSource@1000 : Record "Service Header");
    VAR
      DummyServLine@1001 : TEMPORARY Record "Service Line";
    BEGIN
      PostDocumentWithLines(ServiceHeaderSource,DummyServLine);
    END;

    [External]
    PROCEDURE PostDocumentWithLines@3(VAR ServiceHeaderSource@1000 : Record "Service Header";VAR PassedServLine@1001 : Record "Service Line");
    VAR
      ServiceHeader@1002 : Record "Service Header";
    BEGIN
      ServiceHeader.COPY(ServiceHeaderSource);
      Code(PassedServLine,ServiceHeader);
      ServiceHeaderSource := ServiceHeader;
    END;

    [Internal]
    PROCEDURE PreviewDocument@4(VAR ServHeader@1000 : Record "Service Header");
    VAR
      TempServLine@1001 : TEMPORARY Record "Service Line";
      GenJnlPostPreview@1003 : Codeunit "Gen. Jnl.-Post Preview";
      ServicePostYesNo@1002 : Codeunit "Service-Post (Yes/No)";
    BEGIN
      BINDSUBSCRIPTION(ServicePostYesNo);
      ServicePostYesNo.SetPreviewContext(ServHeader);
      GenJnlPostPreview.Preview(ServicePostYesNo,TempServLine);
    END;

    [Internal]
    PROCEDURE PreviewDocumentWithLines@5(VAR ServHeader@1000 : Record "Service Header";VAR PassedServLine@1004 : Record "Service Line");
    VAR
      GenJnlPostPreview@1003 : Codeunit "Gen. Jnl.-Post Preview";
      ServicePostYesNo@1001 : Codeunit "Service-Post (Yes/No)";
    BEGIN
      BINDSUBSCRIPTION(ServicePostYesNo);
      ServicePostYesNo.SetPreviewContext(ServHeader);
      GenJnlPostPreview.Preview(ServicePostYesNo,PassedServLine);
    END;

    [External]
    PROCEDURE SetPreviewContext@7(VAR ServiceHeader@1000 : Record "Service Header");
    BEGIN
      ServiceHeaderPreviewContext.COPY(ServiceHeader);
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnRunPreview@6(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1002 : Variant);
    VAR
      ServicePostYesNo@1003 : Codeunit "Service-Post (Yes/No)";
    BEGIN
      ServicePostYesNo := Subscriber;
      PreviewMode := true;
      Result := ServicePostYesNo.RUN(RecVar);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeConfirmServPost@8(VAR ServiceHeader@1000 : Record "Service Header";VAR HideDialog@1001 : Boolean;VAR Ship@1004 : Boolean;VAR Consume@1003 : Boolean;VAR Invoice@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

