OBJECT Page 346 Item Picture
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Item Picture;
    InsertAllowed=false;
    DeleteAllowed=false;
    LinksAllowed=false;
    SourceTable=Item;
    PageType=CardPart;
    OnOpenPage=BEGIN
                 CameraAvailable := CameraProvider.IsAvailable;
                 if CameraAvailable then
                   CameraProvider := CameraProvider.Create;
               END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditableOnPictureActions;
                         END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      Name=ActionContainer2;
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      Name=TakePicture;
                      CaptionML=ENU=Take;
                      ToolTipML=ENU=Activate the camera on the device.;
                      ApplicationArea=#All;
                      Promoted=true;
                      Visible=CameraAvailable AND (HideActions = FALSE);
                      PromotedIsBig=true;
                      Image=Camera;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 TakeNewPicture;
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=ImportPicture;
                      CaptionML=ENU=Import;
                      ToolTipML=ENU=Import a picture file.;
                      ApplicationArea=#All;
                      Visible=HideActions = FALSE;
                      Image=Import;
                      OnAction=BEGIN
                                 ImportFromDevice;
                               END;
                                }
      { 6       ;1   ;Action    ;
                      Name=ExportFile;
                      CaptionML=ENU=Export;
                      ToolTipML=ENU=Export the picture to a file.;
                      ApplicationArea=#All;
                      Visible=(CameraAvailable = FALSE) AND (HideActions = FALSE);
                      Enabled=DeleteExportEnabled;
                      Image=Export;
                      OnAction=VAR
                                 NameValueBuffer@1002 : Record "Name/Value Buffer";
                                 TempNameValueBuffer@1004 : TEMPORARY Record "Name/Value Buffer";
                                 FileManagement@1001 : Codeunit "File Management";
                                 ToFile@1003 : Text;
                                 ExportPath@1005 : Text;
                               BEGIN
                                 TESTFIELD("No.");
                                 TESTFIELD(Description);

                                 NameValueBuffer.DELETEALL;
                                 ExportPath := TEMPORARYPATH + "No." + FORMAT(Picture.MEDIAID);
                                 Picture.EXPORTFILE(ExportPath);
                                 FileManagement.GetServerDirectoryFilesList(TempNameValueBuffer,TEMPORARYPATH);
                                 TempNameValueBuffer.SETFILTER(Name,STRSUBSTNO('%1*',ExportPath));
                                 TempNameValueBuffer.FINDFIRST;
                                 ToFile := STRSUBSTNO('%1 %2.jpg',"No.",CONVERTSTR(Description,'"/\','___'));
                                 DOWNLOAD(TempNameValueBuffer.Name,DownloadImageTxt,'','',ToFile);
                                 if FileManagement.DeleteServerFile(TempNameValueBuffer.Name) then;
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=DeletePicture;
                      CaptionML=ENU=Delete;
                      ToolTipML=ENU=Delete the record.;
                      ApplicationArea=#All;
                      Visible=HideActions = FALSE;
                      Enabled=DeleteExportEnabled;
                      Image=Delete;
                      OnAction=BEGIN
                                 DeleteItemPicture;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Field     ;
                Name=Picture;
                ToolTipML=ENU=Specifies the picture that has been inserted for the item.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Picture;
                ImplicitType=MediaSet;
                ShowCaption=false }

  }
  CODE
  {
    VAR
      CameraProvider@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraProvider" WITHEVENTS RUNONCLIENT;
      CameraAvailable@1002 : Boolean;
      OverrideImageQst@1007 : TextConst 'ENU=The existing picture will be replaced. Do you want to continue?';
      DeleteImageQst@1003 : TextConst 'ENU=Are you sure you want to delete the picture?';
      SelectPictureTxt@1001 : TextConst 'ENU=Select a picture to upload';
      DeleteExportEnabled@1005 : Boolean;
      DownloadImageTxt@1006 : TextConst 'ENU=Download image';
      HideActions@1008 : Boolean;

    [External]
    PROCEDURE TakeNewPicture@2();
    VAR
      CameraOptions@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
    BEGIN
      FIND;
      TESTFIELD("No.");
      TESTFIELD(Description);

      if not CameraAvailable then
        exit;

      CameraOptions := CameraOptions.CameraOptions;
      CameraOptions.Quality := 50;
      CameraProvider.RequestPictureAsync(CameraOptions);
    END;

    [External]
    PROCEDURE ImportFromDevice@4();
    VAR
      FileManagement@1002 : Codeunit "File Management";
      FileName@1001 : Text;
      ClientFileName@1000 : Text;
    BEGIN
      FIND;
      TESTFIELD("No.");
      TESTFIELD(Description);

      if Picture.COUNT > 0 then
        if not CONFIRM(OverrideImageQst) then
          ERROR('');

      ClientFileName := '';
      FileName := FileManagement.UploadFile(SelectPictureTxt,ClientFileName);
      if FileName = '' then
        ERROR('');

      CLEAR(Picture);
      Picture.IMPORTFILE(FileName,ClientFileName);
      if not INSERT(true) then
        MODIFY(true);

      if FileManagement.DeleteServerFile(FileName) then;
    END;

    LOCAL PROCEDURE SetEditableOnPictureActions@1();
    BEGIN
      DeleteExportEnabled := Picture.COUNT <> 0;
    END;

    [External]
    PROCEDURE IsCameraAvailable@3() : Boolean;
    BEGIN
      exit(CameraProvider.IsAvailable);
    END;

    [External]
    PROCEDURE SetHideActions@1010();
    BEGIN
      HideActions := true;
    END;

    PROCEDURE DeleteItemPicture@5();
    BEGIN
      TESTFIELD("No.");

      if not CONFIRM(DeleteImageQst) then
        exit;

      CLEAR(Picture);
      MODIFY(true);
    END;

    EVENT CameraProvider@1000::PictureAvailable@10(PictureName@1001 : Text;PictureFilePath@1000 : Text);
    VAR
      File@1003 : File;
      Instream@1004 : InStream;
    BEGIN
      if (PictureName = '') or (PictureFilePath = '') then
        exit;

      if Picture.COUNT > 0 then
        if not CONFIRM(OverrideImageQst) then begin
          if ERASE(PictureFilePath) then;
          exit;
        end;

      File.OPEN(PictureFilePath);
      File.CREATEINSTREAM(Instream);

      CLEAR(Picture);
      Picture.IMPORTSTREAM(Instream,PictureName);
      if not MODIFY(true) then
        INSERT(true);

      File.CLOSE;
      if ERASE(PictureFilePath) then;
    END;

    BEGIN
    END.
  }
}

