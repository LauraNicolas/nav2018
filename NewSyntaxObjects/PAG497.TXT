OBJECT Page 497 Reservation Entries
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Reservation Entries;
    InsertAllowed=false;
    DeleteAllowed=false;
    SourceTable="Reservation Entry";
    DataCaptionExpr=TextCaption;
    PageType=List;
    OnModifyRecord=BEGIN
                     ReservEngineMgt.ModifyReservEntry(xRec,"Quantity (Base)",Description,true);
                     exit(false);
                   END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      Name=ActionContainer1900000004;
                      ActionContainerType=ActionItems }
      { 61      ;1   ;ActionGroup;
                      Name=F&unctions;
                      CaptionML=ENU=F&unctions;
                      Image=Action }
      { 64      ;2   ;Action    ;
                      Name=CancelReservation;
                      AccessByPermission=TableData Item=R;
                      CaptionML=ENU=Cancel Reservation;
                      ToolTipML=ENU=Cancel the selected reservation entry.;
                      ApplicationArea=#Advanced;
                      Image=Cancel;
                      OnAction=VAR
                                 ReservEntry@1001 : Record "Reservation Entry";
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ReservEntry);
                                 if ReservEntry.FIND('-') then
                                   repeat
                                     ReservEntry.TESTFIELD("Reservation Status","Reservation Status"::Reservation);
                                     ReservEntry.TESTFIELD("Disallow Cancellation",false);
                                     if CONFIRM(
                                          Text001,false,ReservEntry."Quantity (Base)",
                                          ReservEntry."Item No.",ReservEngineMgt.CreateForText(Rec),
                                          ReservEngineMgt.CreateFromText(Rec))
                                     then begin
                                       ReservEngineMgt.CancelReservation(ReservEntry);
                                       COMMIT;
                                     end;
                                   until ReservEntry.NEXT = 0;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 30  ;2   ;Field     ;
                Name=Reservation Status;
                ToolTipML=ENU=Specifies the status of the reservation.;
                ApplicationArea=#Advanced;
                SourceExpr="Reservation Status";
                Editable=FALSE;
                ImplicitType=Option }

    { 4   ;2   ;Field     ;
                Name=Item No.;
                ToolTipML=ENU=Specifies the number of the item that has been reserved in this entry.;
                ApplicationArea=#Advanced;
                SourceExpr="Item No.";
                Editable=FALSE;
                ImplicitType=Code20 }

    { 47  ;2   ;Field     ;
                Name=Variant Code;
                ToolTipML=ENU=Specifies the variant of the item on the line.;
                ApplicationArea=#Advanced;
                SourceExpr="Variant Code";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Code10 }

    { 41  ;2   ;Field     ;
                Name=Location Code;
                ToolTipML=ENU=Specifies the Location of the items that have been reserved in the entry.;
                ApplicationArea=#Advanced;
                SourceExpr="Location Code";
                Editable=FALSE;
                ImplicitType=Code10 }

    { 58  ;2   ;Field     ;
                Name=Serial No.;
                ToolTipML=ENU=Specifies the serial number of the item that is being handled on the document line.;
                ApplicationArea=#ItemTracking;
                SourceExpr="Serial No.";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Code20 }

    { 43  ;2   ;Field     ;
                Name=Lot No.;
                ToolTipML=ENU=Specifies the lot number of the item that is being handled with the associated document line.;
                ApplicationArea=#ItemTracking;
                SourceExpr="Lot No.";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Code20 }

    { 44  ;2   ;Field     ;
                Name=Expected Receipt Date;
                ToolTipML=ENU=Specifies the date on which the reserved items are expected to enter inventory.;
                ApplicationArea=#Advanced;
                SourceExpr="Expected Receipt Date";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Date }

    { 49  ;2   ;Field     ;
                Name=Shipment Date;
                ToolTipML=ENU=Specifies when items on the document are shipped or were shipped. A shipment date is usually calculated from a requested delivery date plus lead time.;
                ApplicationArea=#Advanced;
                SourceExpr="Shipment Date";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Date }

    { 26  ;2   ;Field     ;
                Name=Quantity (Base);
                ToolTipML=ENU=Specifies the quantity of the item that has been reserved in the entry.;
                ApplicationArea=#Advanced;
                SourceExpr="Quantity (Base)";
                OnValidate=BEGIN
                             ReservEngineMgt.ModifyReservEntry(xRec,"Quantity (Base)",Description,false);
                             QuantityBaseOnAfterValidate;
                           END;

                ImplicitType=Decimal }

    { 53  ;2   ;Field     ;
                Name=Reserved For;
                CaptionML=ENU=Reserved For;
                ToolTipML=ENU=Specifies which line or entry the items are reserved for.;
                ApplicationArea=#Advanced;
                SourceExpr=ReservEngineMgt.CreateForText(Rec);
                Editable=FALSE;
                OnLookup=BEGIN
                           LookupReservedFor;
                         END;
                          }

    { 51  ;2   ;Field     ;
                Name=ReservedFrom;
                CaptionML=ENU=Reserved From;
                ToolTipML=ENU=Specifies which line or entry the items are reserved from.;
                ApplicationArea=#Advanced;
                SourceExpr=ReservEngineMgt.CreateFromText(Rec);
                Editable=FALSE;
                OnLookup=BEGIN
                           LookupReservedFrom;
                         END;
                          }

    { 34  ;2   ;Field     ;
                Name=Description;
                ToolTipML=ENU=Specifies a description of the reservation entry.;
                ApplicationArea=#Advanced;
                SourceExpr=Description;
                Visible=FALSE;
                ImplicitType=Text50 }

    { 6   ;2   ;Field     ;
                Name=Source Type;
                ToolTipML=ENU=Specifies for which source type the reservation entry is related to.;
                ApplicationArea=#Advanced;
                SourceExpr="Source Type";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Integer }

    { 8   ;2   ;Field     ;
                Name=Source Subtype;
                ToolTipML=ENU=Specifies which source subtype the reservation entry is related to.;
                ApplicationArea=#Advanced;
                SourceExpr="Source Subtype";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Option }

    { 10  ;2   ;Field     ;
                Name=Source ID;
                ToolTipML=ENU=Specifies which source ID the reservation entry is related to.;
                ApplicationArea=#Advanced;
                SourceExpr="Source ID";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Code20 }

    { 12  ;2   ;Field     ;
                Name=Source Batch Name;
                ToolTipML=ENU=Specifies the journal batch name if the reservation entry is related to a journal or requisition line.;
                ApplicationArea=#Advanced;
                SourceExpr="Source Batch Name";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Code10 }

    { 14  ;2   ;Field     ;
                Name=Source Ref. No.;
                ToolTipML=ENU=Specifies a reference number for the line, which the reservation entry is related to.;
                ApplicationArea=#Advanced;
                SourceExpr="Source Ref. No.";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Integer }

    { 2   ;2   ;Field     ;
                Name=Entry No.;
                ToolTipML=ENU=Specifies the entry number of the reservation entry.;
                ApplicationArea=#Advanced;
                SourceExpr="Entry No.";
                Editable=FALSE;
                ImplicitType=Integer }

    { 32  ;2   ;Field     ;
                Name=Creation Date;
                ToolTipML=ENU=Specifies the date on which the entry was created.;
                ApplicationArea=#Advanced;
                SourceExpr="Creation Date";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Date }

    { 36  ;2   ;Field     ;
                Name=Transferred from Entry No.;
                ToolTipML=ENU=Specifies a value when the order tracking entry is for the quantity that remains on a document line after a partial posting.;
                ApplicationArea=#Advanced;
                SourceExpr="Transferred from Entry No.";
                Editable=FALSE;
                ImplicitType=Integer }

    { 1900000007;0;Container;
                Name=Container1900000007;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Name=Part1900383207;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Name=Part1905767507;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Cancel reservation of %1 of item number %2, reserved for %3 from %4?';
      ReservEngineMgt@1015 : Codeunit "Reservation Engine Mgt.";

    LOCAL PROCEDURE LookupReservedFor@2();
    VAR
      ReservEntry@1000 : Record "Reservation Entry";
    BEGIN
      ReservEntry.GET("Entry No.",false);
      LookupReserved(ReservEntry);
    END;

    LOCAL PROCEDURE LookupReservedFrom@3();
    VAR
      ReservEntry@1000 : Record "Reservation Entry";
    BEGIN
      ReservEntry.GET("Entry No.",true);
      LookupReserved(ReservEntry);
    END;

    LOCAL PROCEDURE LookupReserved@1(ReservEntry@1000 : Record "Reservation Entry");
    VAR
      SalesLine@1003 : Record "Sales Line";
      ReqLine@1004 : Record "Requisition Line";
      PurchLine@1005 : Record "Purchase Line";
      ItemJnlLine@1006 : Record "Item Journal Line";
      ItemLedgEntry@1008 : Record "Item Ledger Entry";
      ProdOrderLine@1009 : Record "Prod. Order Line";
      ProdOrderComp@1010 : Record "Prod. Order Component";
      PlanningComponent@1011 : Record "Planning Component";
      ServLine@1012 : Record "Service Line";
      JobPlanningLine@1016 : Record "Job Planning Line";
      TransLine@1013 : Record "Transfer Line";
      AssemblyHeader@1017 : Record "Assembly Header";
      AssemblyLine@1018 : Record "Assembly Line";
    BEGIN
      with ReservEntry do
        case "Source Type" of
          DATABASE::"Sales Line":
            begin
              SalesLine.RESET;
              SalesLine.SETRANGE("Document Type","Source Subtype");
              SalesLine.SETRANGE("Document No.","Source ID");
              SalesLine.SETRANGE("Line No.","Source Ref. No.");
              PAGE.RUNMODAL(PAGE::"Sales Lines",SalesLine);
            end;
          DATABASE::"Requisition Line":
            begin
              ReqLine.RESET;
              ReqLine.SETRANGE("Worksheet Template Name","Source ID");
              ReqLine.SETRANGE("Journal Batch Name","Source Batch Name");
              ReqLine.SETRANGE("Line No.","Source Ref. No.");
              PAGE.RUNMODAL(PAGE::"Requisition Lines",ReqLine);
            end;
          DATABASE::"Purchase Line":
            begin
              PurchLine.RESET;
              PurchLine.SETRANGE("Document Type","Source Subtype");
              PurchLine.SETRANGE("Document No.","Source ID");
              PurchLine.SETRANGE("Line No.","Source Ref. No.");
              PAGE.RUNMODAL(PAGE::"Purchase Lines",PurchLine);
            end;
          DATABASE::"Item Journal Line":
            begin
              ItemJnlLine.RESET;
              ItemJnlLine.SETRANGE("Journal Template Name","Source ID");
              ItemJnlLine.SETRANGE("Journal Batch Name","Source Batch Name");
              ItemJnlLine.SETRANGE("Line No.","Source Ref. No.");
              ItemJnlLine.SETRANGE("Entry Type","Source Subtype");
              PAGE.RUNMODAL(PAGE::"Item Journal Lines",ItemJnlLine);
            end;
          DATABASE::"Item Ledger Entry":
            begin
              ItemLedgEntry.RESET;
              ItemLedgEntry.SETRANGE("Entry No.","Source Ref. No.");
              PAGE.RUNMODAL(0,ItemLedgEntry);
            end;
          DATABASE::"Prod. Order Line":
            begin
              ProdOrderLine.RESET;
              ProdOrderLine.SETRANGE(Status,"Source Subtype");
              ProdOrderLine.SETRANGE("Prod. Order No.","Source ID");
              ProdOrderLine.SETRANGE("Line No.","Source Prod. Order Line");
              PAGE.RUNMODAL(0,ProdOrderLine);
            end;
          DATABASE::"Prod. Order Component":
            begin
              ProdOrderComp.RESET;
              ProdOrderComp.SETRANGE(Status,"Source Subtype");
              ProdOrderComp.SETRANGE("Prod. Order No.","Source ID");
              ProdOrderComp.SETRANGE("Prod. Order Line No.","Source Prod. Order Line");
              ProdOrderComp.SETRANGE("Line No.","Source Ref. No.");
              PAGE.RUNMODAL(0,ProdOrderComp);
            end;
          DATABASE::"Planning Component":
            begin
              PlanningComponent.RESET;
              PlanningComponent.SETRANGE("Worksheet Template Name","Source ID");
              PlanningComponent.SETRANGE("Worksheet Batch Name","Source Batch Name");
              PlanningComponent.SETRANGE("Worksheet Line No.","Source Prod. Order Line");
              PlanningComponent.SETRANGE("Line No.","Source Ref. No.");
              PAGE.RUNMODAL(0,PlanningComponent);
            end;
          DATABASE::"Transfer Line":
            begin
              TransLine.RESET;
              TransLine.SETRANGE("Document No.","Source ID");
              TransLine.SETRANGE("Line No.","Source Ref. No.");
              TransLine.SETRANGE("Derived From Line No.","Source Prod. Order Line");
              PAGE.RUNMODAL(0,TransLine);
            end;
          DATABASE::"Service Line":
            begin
              ServLine.SETRANGE("Document Type","Source Subtype");
              ServLine.SETRANGE("Document No.","Source ID");
              ServLine.SETRANGE("Line No.","Source Ref. No.");
              PAGE.RUNMODAL(0,ServLine);
            end;
          DATABASE::"Job Planning Line":
            begin
              JobPlanningLine.SETRANGE(Status,"Source Subtype");
              JobPlanningLine.SETRANGE("Job No.","Source ID");
              JobPlanningLine.SETRANGE("Job Contract Entry No.","Source Ref. No.");
              PAGE.RUNMODAL(0,JobPlanningLine);
            end;
          DATABASE::"Assembly Header":
            begin
              AssemblyHeader.SETRANGE("Document Type","Source Subtype");
              AssemblyHeader.SETRANGE("No.","Source ID");
              PAGE.RUNMODAL(0,AssemblyHeader);
            end;
          DATABASE::"Assembly Line":
            begin
              AssemblyLine.SETRANGE("Document Type","Source Subtype");
              AssemblyLine.SETRANGE("Document No.","Source ID");
              AssemblyLine.SETRANGE("Line No.","Source Ref. No.");
              PAGE.RUNMODAL(0,AssemblyLine);
            end;
        end;
    END;

    LOCAL PROCEDURE QuantityBaseOnAfterValidate@19029188();
    BEGIN
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

