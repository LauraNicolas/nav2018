OBJECT Codeunit 782 Opportunity Chart Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [Internal]
    PROCEDURE DrillDown@7(VAR BusinessChartBuffer@1000 : Record "Business Chart Buffer";Period@1002 : Record Date;OpportunityStatus@1003 : Option);
    VAR
      Opportunity@1005 : Record Opportunity;
      SalespersonPurchaser@1001 : Record "Salesperson/Purchaser";
      SalesPersonName@1004 : Variant;
    BEGIN
      BusinessChartBuffer.GetXValue(BusinessChartBuffer."Drill-Down X Index",SalesPersonName);
      SalespersonPurchaser.SETRANGE(Name,SalesPersonName);
      SalespersonPurchaser.FINDFIRST;
      Opportunity.SETRANGE("Salesperson Code",SalespersonPurchaser.Code);
      Opportunity.Status := OpportunityStatus;
      Opportunity.SETRANGE(Status,Opportunity.Status);
      case Opportunity.Status of
        Opportunity.Status::"Not Started",
        Opportunity.Status::"In Progress":
          Opportunity.SETRANGE("Creation Date",0D,Period."Period End");
        Opportunity.Status::Won,
        Opportunity.Status::Lost:
          Opportunity.SETRANGE("Date Closed",Period."Period Start",Period."Period End");
      end;
      PAGE.RUN(PAGE::"Opportunity List",Opportunity);
    END;

    LOCAL PROCEDURE GetOppCount@8(Period@1003 : Record Date;SalesPersonCode@1001 : Code[20];OpportunityStatus@1002 : Option) : Integer;
    VAR
      Opportunity@1000 : Record Opportunity;
    BEGIN
      Opportunity.SETRANGE("Salesperson Code",SalesPersonCode);
      Opportunity.Status := OpportunityStatus;
      Opportunity.SETRANGE(Status,Opportunity.Status);
      case Opportunity.Status of
        Opportunity.Status::"Not Started",
        Opportunity.Status::"In Progress":
          Opportunity.SETRANGE("Creation Date",0D,Period."Period End");
        Opportunity.Status::Won,
        Opportunity.Status::Lost:
          Opportunity.SETRANGE("Date Closed",Period."Period Start",Period."Period End");
      end;
      exit(Opportunity.COUNT);
    END;

    [External]
    PROCEDURE SetDefaultOppStatus@5(VAR Opportunity@1000 : Record Opportunity);
    BEGIN
      Opportunity.Status := Opportunity.Status::Won;
    END;

    [Internal]
    PROCEDURE SetDefaultPeriod@2(VAR Period@1000 : Record Date);
    BEGIN
      Period."Period Type" := Period."Period Type"::Month;
      Period."Period Start" := CALCDATE('<-CM>',WORKDATE);
      Period."Period End" := CALCDATE('<CM>',WORKDATE);
    END;

    [Internal]
    PROCEDURE SetNextPeriod@4(VAR Period@1000 : Record Date);
    BEGIN
      case Period."Period Type" of
        Period."Period Type"::Date:
          Period."Period Start" := CALCDATE('<+1D>',Period."Period Start");
        Period."Period Type"::Week:
          Period."Period Start" := CALCDATE('<+1W>',Period."Period Start");
        Period."Period Type"::Month:
          Period."Period Start" := CALCDATE('<+1M>',Period."Period Start");
        Period."Period Type"::Quarter:
          Period."Period Start" := CALCDATE('<+1Q>',Period."Period Start");
        Period."Period Type"::Year:
          Period."Period Start" := CALCDATE('<+1Y>',Period."Period Start");
      end;
      SetPeriodRange(Period);
    END;

    [Internal]
    PROCEDURE SetPrevPeriod@6(VAR Period@1000 : Record Date);
    BEGIN
      case Period."Period Type" of
        Period."Period Type"::Date:
          Period."Period Start" := CALCDATE('<-1D>',Period."Period Start");
        Period."Period Type"::Week:
          Period."Period Start" := CALCDATE('<-1W>',Period."Period Start");
        Period."Period Type"::Month:
          Period."Period Start" := CALCDATE('<-1M>',Period."Period Start");
        Period."Period Type"::Quarter:
          Period."Period Start" := CALCDATE('<-1Q>',Period."Period Start");
        Period."Period Type"::Year:
          Period."Period Start" := CALCDATE('<-1Y>',Period."Period Start");
      end;
      SetPeriodRange(Period);
    END;

    [Internal]
    PROCEDURE SetPeriodRange@1(VAR Period@1000 : Record Date);
    BEGIN
      case Period."Period Type" of
        Period."Period Type"::Date:
          begin
            Period."Period Start" := Period."Period Start";
            Period."Period End" := Period."Period Start";
          end;
        Period."Period Type"::Week:
          begin
            Period."Period Start" := CALCDATE('<-CW>',Period."Period Start");
            Period."Period End" := CALCDATE('<CW>',Period."Period Start");
          end;
        Period."Period Type"::Month:
          begin
            Period."Period Start" := CALCDATE('<-CM>',Period."Period Start");
            Period."Period End" := CALCDATE('<CM>',Period."Period Start");
          end;
        Period."Period Type"::Quarter:
          begin
            Period."Period Start" := CALCDATE('<-CQ>',Period."Period Start");
            Period."Period End" := CALCDATE('<CQ>',Period."Period Start");
          end;
        Period."Period Type"::Year:
          begin
            Period."Period Start" := CALCDATE('<-CY>',Period."Period Start");
            Period."Period End" := CALCDATE('<CY>',Period."Period Start");
          end;
      end;
    END;

    [Internal]
    PROCEDURE UpdateData@3(VAR BusinessChartBuffer@1000 : Record "Business Chart Buffer";Period@1006 : Record Date;OpportunityStatus@1004 : Option);
    VAR
      SalespersonPurchaser@1001 : Record "Salesperson/Purchaser";
      I@1002 : Integer;
      OppCount@1003 : Integer;
    BEGIN
      with BusinessChartBuffer do begin
        Initialize;
        AddMeasure(SalespersonPurchaser.FIELDCAPTION("No. of Opportunities"),1,"Data Type"::Integer,"Chart Type"::Pie);
        SetXAxis(SalespersonPurchaser.TABLECAPTION,"Data Type"::String);
        if SalespersonPurchaser.FINDSET then
          repeat
            OppCount := GetOppCount(Period,SalespersonPurchaser.Code,OpportunityStatus);
            if OppCount <> 0 then begin
              I += 1;
              AddColumn(SalespersonPurchaser.Name);
              SetValueByIndex(0,I - 1,OppCount);
            end;
          until SalespersonPurchaser.NEXT = 0;
      end;
    END;

    BEGIN
    END.
  }
}

