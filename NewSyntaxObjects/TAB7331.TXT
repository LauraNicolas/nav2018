OBJECT Table 7331 Whse. Internal Put-away Header
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               WhseSetup.GET;
               if "No." = '' then begin
                 WhseSetup.TESTFIELD("Whse. Internal Put-away Nos.");
                 NoSeriesMgt.InitSeries(
                   WhseSetup."Whse. Internal Put-away Nos.",xRec."No. Series",0D,"No.","No. Series");
               end;
             END;

    OnDelete=BEGIN
               TESTFIELD(Status,Status::Open);
               DeleteRelatedLines;
             END;

    OnRename=BEGIN
               ERROR(Text004,TABLECAPTION);
             END;

    CaptionML=ENU=Whse. Internal Put-away Header;
    LookupPageID="Whse. Internal Put-away List";
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                WhseSetup.GET;
                                                                if "No." <> xRec."No." then begin
                                                                  NoSeriesMgt.TestManual(WhseSetup."Whse. Internal Put-away Nos.");
                                                                  "No. Series" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 2   ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE ("Use As In-Transit"=CONST(false));
                                                   OnValidate=BEGIN
                                                                if not WmsManagement.LocationIsAllowed("Location Code") then
                                                                  ERROR(Text006,FIELDCAPTION("Location Code"),"Location Code");

                                                                CheckPutawayRequired("Location Code");
                                                                if "Location Code" <> '' then begin
                                                                  Location.GET("Location Code");
                                                                  Location.TESTFIELD("Directed Put-away and Pick");
                                                                end;
                                                                if "Location Code" <> xRec."Location Code" then begin
                                                                  "From Zone Code" := '';
                                                                  "From Bin Code" := '';
                                                                  WhseInternalPutAwayLine.SETRANGE("No.","No.");
                                                                  if WhseInternalPutAwayLine.FIND('-') then
                                                                    ERROR(
                                                                      Text005,
                                                                      FIELDCAPTION("Location Code"));
                                                                end;
                                                                if USERID <> '' then begin
                                                                  FILTERGROUP := 2;
                                                                  SETRANGE("Location Code","Location Code");
                                                                  FILTERGROUP := 0;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Location Code }
    { 3   ;   ;Assigned User ID    ;Code50        ;TableRelation="Warehouse Employee" WHERE ("Location Code"=FIELD("Location Code"));
                                                   OnValidate=BEGIN
                                                                if "Assigned User ID" <> '' then begin
                                                                  "Assignment Date" := TODAY;
                                                                  "Assignment Time" := TIME;
                                                                end else begin
                                                                  "Assignment Date" := 0D;
                                                                  "Assignment Time" := 000000T;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Assigned User ID }
    { 4   ;   ;Assignment Date     ;Date          ;CaptionML=ENU=Assignment Date;
                                                   Editable=false }
    { 5   ;   ;Assignment Time     ;Time          ;CaptionML=ENU=Assignment Time;
                                                   Editable=false }
    { 6   ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series }
    { 7   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Warehouse Comment Line" WHERE ("Table Name"=CONST("Internal Put-away"),
                                                                                                     Type=CONST(" "),
                                                                                                     "No."=FIELD("No.")));
                                                   CaptionML=ENU=Comment;
                                                   Editable=false }
    { 8   ;   ;From Bin Code       ;Code20        ;TableRelation=IF ("From Zone Code"=FILTER('')) Bin.Code WHERE ("Location Code"=FIELD("Location Code"))
                                                                 ELSE IF ("From Zone Code"=FILTER(<>'')) Bin.Code WHERE ("Location Code"=FIELD("Location Code"),
                                                                                                                         "Zone Code"=FIELD("From Zone Code"));
                                                   OnValidate=VAR
                                                                BinType@1000 : Record "Bin Type";
                                                              BEGIN
                                                                if xRec."From Bin Code" <> "From Bin Code" then begin
                                                                  if "From Bin Code" <> '' then begin
                                                                    GetLocation("Location Code");
                                                                    Location.TESTFIELD("Bin Mandatory");
                                                                    if "From Bin Code" = Location."Adjustment Bin Code" then
                                                                      FIELDERROR(
                                                                        "From Bin Code",
                                                                        STRSUBSTNO(
                                                                          Text001,Location.FIELDCAPTION("Adjustment Bin Code"),
                                                                          Location.TABLECAPTION));

                                                                    Bin.GET("Location Code","From Bin Code");
                                                                    if Bin."Bin Type Code" <> '' then
                                                                      if BinType.GET(Bin."Bin Type Code") then
                                                                        BinType.TESTFIELD(Receive,false);

                                                                    "From Zone Code" := Bin."Zone Code";
                                                                  end;
                                                                  MessageIfIntPutawayLinesExist(FIELDCAPTION("From Bin Code"));
                                                                end;
                                                              END;

                                                   CaptionML=ENU=From Bin Code }
    { 9   ;   ;From Zone Code      ;Code10        ;TableRelation=Zone.Code WHERE ("Location Code"=FIELD("Location Code"));
                                                   OnValidate=BEGIN
                                                                if "From Zone Code" <> xRec."From Zone Code" then begin
                                                                  GetLocation("Location Code");
                                                                  if "From Zone Code" <> '' then
                                                                    Location.TESTFIELD("Directed Put-away and Pick");
                                                                  "From Bin Code" := '';
                                                                  MessageIfIntPutawayLinesExist(FIELDCAPTION("From Zone Code"));
                                                                end;
                                                              END;

                                                   CaptionML=ENU=From Zone Code }
    { 10  ;   ;Due Date            ;Date          ;OnValidate=BEGIN
                                                                MessageIfIntPutawayLinesExist(FIELDCAPTION("Due Date"));
                                                              END;

                                                   CaptionML=ENU=Due Date }
    { 11  ;   ;Document Status     ;Option        ;OnValidate=VAR
                                                                WhsePutAwayRqst@1000 : Record "Whse. Put-away Request";
                                                              BEGIN
                                                                if "Document Status" <> xRec."Document Status" then begin
                                                                  WhsePutAwayRqst.SETRANGE("Document Type",WhsePutAwayRqst."Document Type"::"Internal Put-away");
                                                                  WhsePutAwayRqst.SETRANGE("Document No.","No.");
                                                                  WhsePutAwayRqst.MODIFYALL(
                                                                    "Completely Put Away","Document Status" = "Document Status"::"Completely Put Away");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Document Status;
                                                   OptionCaptionML=ENU=" ,Partially Put Away,Completely Put Away";
                                                   OptionString=" ","Partially Put Away","Completely Put Away";
                                                   Editable=false }
    { 12  ;   ;Sorting Method      ;Option        ;OnValidate=BEGIN
                                                                if "Sorting Method" <> xRec."Sorting Method" then
                                                                  SortWhseDoc;
                                                              END;

                                                   CaptionML=ENU=Sorting Method;
                                                   OptionCaptionML=ENU=" ,Item,Shelf or Bin,Due Date";
                                                   OptionString=" ",Item,"Shelf or Bin","Due Date" }
    { 13  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Open,Released;
                                                   OptionString=Open,Released;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"No."                                   ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Location@1009 : Record Location;
      Bin@1011 : Record Bin;
      WhseInternalPutAwayLine@1000 : Record "Whse. Internal Put-away Line";
      WhseSetup@1001 : Record "Warehouse Setup";
      WhseInternalPutAwayHeader@1002 : Record "Whse. Internal Put-away Header";
      NoSeriesMgt@1003 : Codeunit NoSeriesManagement;
      WmsManagement@1014 : Codeunit "WMS Management";
      ItemTrackingMgt@1015 : Codeunit "Item Tracking Management";
      HideValidationDialog@1005 : Boolean;
      Text001@1008 : TextConst 'ENU=must not be the %1 of the %2';
      Text002@1007 : TextConst 'ENU=You have changed %1 on the %2, but it has not been changed on the existing Warehouse Internal Put-Away Lines.\';
      Text003@1006 : TextConst 'ENU=You must update the existing Warehouse Internal Put-Away Lines manually.';
      Text004@1010 : TextConst 'ENU=You cannot rename a %1.';
      Text005@1012 : TextConst 'ENU=You cannot change the %1, because the document has one or more lines.';
      Text006@1013 : TextConst 'ENU=You are not allowed to use %1 %2.';

    LOCAL PROCEDURE SortWhseDoc@3();
    VAR
      SequenceNo@1000 : Integer;
    BEGIN
      WhseInternalPutAwayLine.RESET;
      WhseInternalPutAwayLine.SETRANGE("No.","No.");
      case "Sorting Method" of
        "Sorting Method"::Item:
          WhseInternalPutAwayLine.SETCURRENTKEY("No.","Item No.");
        "Sorting Method"::"Shelf or Bin":
          begin
            GetLocation("Location Code");
            if Location."Bin Mandatory" then
              WhseInternalPutAwayLine.SETCURRENTKEY("No.","From Bin Code")
            else
              WhseInternalPutAwayLine.SETCURRENTKEY("No.","Shelf No.");
          end;
        "Sorting Method"::"Due Date":
          WhseInternalPutAwayLine.SETCURRENTKEY("No.","Due Date");
      end;

      if WhseInternalPutAwayLine.FIND('-') then begin
        SequenceNo := 10000;
        repeat
          WhseInternalPutAwayLine."Sorting Sequence No." := SequenceNo;
          WhseInternalPutAwayLine.MODIFY;
          SequenceNo := SequenceNo + 10000;
        until WhseInternalPutAwayLine.NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE MessageIfIntPutawayLinesExist@7(ChangedFieldName@1000 : Text[80]);
    VAR
      WhseInternalPutAwayLine@1001 : Record "Whse. Internal Put-away Line";
    BEGIN
      WhseInternalPutAwayLine.SETRANGE("No.","No.");
      if not WhseInternalPutAwayLine.ISEMPTY then
        if not HideValidationDialog then
          MESSAGE(
            STRSUBSTNO(
              Text002,ChangedFieldName,TABLECAPTION) + Text003);
    END;

    [External]
    PROCEDURE AssistEdit@8(OldWhseInternalPutAwayHeader@1000 : Record "Whse. Internal Put-away Header") : Boolean;
    BEGIN
      WhseSetup.GET;
      with WhseInternalPutAwayHeader do begin
        WhseInternalPutAwayHeader := Rec;
        WhseSetup.TESTFIELD("Whse. Internal Put-away Nos.");
        if NoSeriesMgt.SelectSeries(
             WhseSetup."Whse. Internal Put-away Nos.",
             OldWhseInternalPutAwayHeader."No. Series","No. Series")
        then begin
          NoSeriesMgt.SetSeries("No.");
          Rec := WhseInternalPutAwayHeader;
          exit(true);
        end;
      end;
    END;

    [External]
    PROCEDURE GetDocumentStatus@15(LineNo@1003 : Integer) : Integer;
    VAR
      WhseInternalPutAwayLine@1001 : Record "Whse. Internal Put-away Line";
    BEGIN
      WhseInternalPutAwayLine.SETRANGE("No.","No.");
      with WhseInternalPutAwayLine do begin
        if LineNo <> 0 then
          SETFILTER("Line No.",'<>%1',LineNo);
        if not FINDFIRST then
          exit(Status::" ");

        SETRANGE(Status,Status::"Partially Put Away");
        if FINDFIRST then
          exit(Status);

        SETRANGE(Status,Status::"Completely Put Away");
        if FINDFIRST then begin
          SETFILTER(Status,'<%1',Status::"Completely Put Away");
          if FINDFIRST then
            exit(Status::"Partially Put Away");

          exit(Status);
        end;

        exit(Status);
      end;
    END;

    LOCAL PROCEDURE GetLocation@10(LocationCode@1000 : Code[10]);
    BEGIN
      if LocationCode = '' then
        CLEAR(Location)
      else
        if Location.Code <> LocationCode then
          Location.GET(LocationCode);
    END;

    [External]
    PROCEDURE SetWhseLocationFilter@2();
    VAR
      WmsManagement@1000 : Codeunit "WMS Management";
    BEGIN
      if USERID <> '' then begin
        FILTERGROUP := 2;
        SETRANGE("Location Code",WmsManagement.GetAllowedLocation("Location Code"));
        FILTERGROUP := 0;
      end;
    END;

    [External]
    PROCEDURE LookupInternalPutAwayHeader@1(VAR WhseInternalPutAwayHeader@1001 : Record "Whse. Internal Put-away Header");
    BEGIN
      COMMIT;
      if USERID <> '' then begin
        WhseInternalPutAwayHeader.FILTERGROUP := 2;
        WhseInternalPutAwayHeader.SETRANGE("Location Code");
      end;
      if PAGE.RUNMODAL(0,WhseInternalPutAwayHeader) = ACTION::LookupOK then;
      if USERID <> '' then begin
        WhseInternalPutAwayHeader.FILTERGROUP := 2;
        WhseInternalPutAwayHeader.SETRANGE("Location Code",WhseInternalPutAwayHeader."Location Code");
        WhseInternalPutAwayHeader.FILTERGROUP := 0;
      end;
    END;

    [External]
    PROCEDURE LookupLocation@4(VAR WhseInternalPutAwayHeader@1001 : Record "Whse. Internal Put-away Header");
    VAR
      Location@1000 : Record Location;
    BEGIN
      COMMIT;
      Location.FILTERGROUP := 2;
      Location.SETRANGE(Code);
      if PAGE.RUNMODAL(PAGE::"Locations with Warehouse List",Location) = ACTION::LookupOK then
        WhseInternalPutAwayHeader.VALIDATE("Location Code",Location.Code);
      Location.FILTERGROUP := 0;
    END;

    [External]
    PROCEDURE SetHideValidationDialog@14(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    [External]
    PROCEDURE DeleteRelatedLines@9();
    VAR
      WhseInternalPutAwayLine@1003 : Record "Whse. Internal Put-away Line";
      WhsePutAwayRqst@1002 : Record "Whse. Put-away Request";
      WhseCommentLine@1001 : Record "Warehouse Comment Line";
    BEGIN
      WhseInternalPutAwayLine.SETRANGE("No.","No.");
      WhseInternalPutAwayLine.DELETEALL;

      WhsePutAwayRqst.SETRANGE("Document Type",WhsePutAwayRqst."Document Type"::"Internal Put-away");
      WhsePutAwayRqst.SETRANGE("Document No.","No.");
      WhsePutAwayRqst.DELETEALL;

      WhseCommentLine.SETRANGE("Table Name",WhseCommentLine."Table Name"::"Internal Put-away");
      WhseCommentLine.SETRANGE(Type,WhseCommentLine.Type::" ");
      WhseCommentLine.SETRANGE("No.","No.");
      WhseCommentLine.DELETEALL;

      ItemTrackingMgt.DeleteWhseItemTrkgLines(
        DATABASE::"Whse. Internal Put-away Line",0,"No.",'',0,0,'',false);
    END;

    [External]
    PROCEDURE CheckPutawayRequired@5(LocationCode@1000 : Code[10]);
    BEGIN
      if LocationCode = '' then begin
        WhseSetup.GET;
        WhseSetup.TESTFIELD("Require Put-away");
      end else begin
        GetLocation(LocationCode);
        Location.TESTFIELD("Require Put-away");
      end;
    END;

    BEGIN
    END.
  }
}

