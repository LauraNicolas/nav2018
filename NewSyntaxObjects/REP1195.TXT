OBJECT Report 1195 Adjust Resource Costs/Prices
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Adjust Resource Costs/Prices;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);
                END;

  }
  DATASET
  {
    { 5508;    ;DataItem;Resource            ;
               DataItemTable=Resource;
               DataItemTableView=SORTING("No.");
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");

                                  case Selection of
                                    Selection::"Direct Unit Cost":
                                      OldFieldNo := "Direct Unit Cost";
                                    Selection::"Indirect Cost %":
                                      OldFieldNo := "Indirect Cost %";
                                    Selection::"Unit Cost":
                                      OldFieldNo := "Unit Cost";
                                    Selection::"Profit %":
                                      OldFieldNo := "Profit %";
                                    Selection::"Unit Price":
                                      OldFieldNo := "Unit Price";
                                  end;

                                  NewFieldNo := OldFieldNo * AdjFactor;

                                  if RoundingMethod.Code <> '' then begin
                                    RoundingMethod."Minimum Amount" := NewFieldNo;
                                    if RoundingMethod.FIND('=<') then begin
                                      NewFieldNo := NewFieldNo + RoundingMethod."Amount Added Before";
                                      if RoundingMethod.Precision > 0 then
                                        NewFieldNo := ROUND(NewFieldNo,RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                                      NewFieldNo := NewFieldNo + RoundingMethod."Amount Added After";
                                    end;
                                  end;

                                  case Selection of
                                    Selection::"Direct Unit Cost":
                                      VALIDATE("Direct Unit Cost",NewFieldNo);
                                    Selection::"Indirect Cost %":
                                      VALIDATE("Indirect Cost %",NewFieldNo);
                                    Selection::"Unit Cost":
                                      VALIDATE("Unit Cost",NewFieldNo);
                                    Selection::"Profit %":
                                      VALIDATE("Profit %",NewFieldNo);
                                    Selection::"Unit Price":
                                      VALIDATE("Unit Price",NewFieldNo);
                                  end;

                                  MODIFY;
                                END;

               ReqFilterFields="No.","Resource Group No." }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   if AdjFactor = 0 then
                     AdjFactor := 1;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 10  ;2   ;Field     ;
                  Name=Adjust Field;
                  CaptionML=ENU=Adjust Field;
                  ToolTipML=ENU=Specifies the type of cost or price to be adjusted.;
                  OptionCaptionML=ENU=Direct Unit Cost,Indirect Cost %,Unit Cost,Profit %,Unit Price;
                  ApplicationArea=#Jobs;
                  SourceExpr=Selection }

      { 3   ;2   ;Field     ;
                  Name=Adjustment Factor;
                  CaptionML=ENU=Adjustment Factor;
                  ToolTipML=ENU=Specifies an adjustment factor to multiply the amounts that you want suggested. By entering an adjustment factor, you can increase or decrease the amounts that are suggested.;
                  ApplicationArea=#Jobs;
                  DecimalPlaces=0:5;
                  SourceExpr=AdjFactor }

      { 1   ;2   ;Field     ;
                  Name=Rounding Method;
                  CaptionML=ENU=Rounding Method;
                  ToolTipML=ENU=Specifies a code for the rounding method that you want to apply to costs or prices that you adjust.;
                  ApplicationArea=#Jobs;
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing resources  #1##########';
      RoundingMethod@1003 : Record "Rounding Method";
      Window@1006 : Dialog;
      NewFieldNo@1001 : Decimal;
      OldFieldNo@1002 : Decimal;
      AdjFactor@1004 : Decimal;
      Selection@1005 : '"Direct Unit Cost","Indirect Cost %","Unit Cost","Profit %","Unit Price"';

    PROCEDURE InitializeRequest@1(SelectionFrom@1000 : Option;AdjFactorFrom@1001 : Decimal;RoundingMethodCode@1002 : Code[10]);
    BEGIN
      Selection := SelectionFrom;
      AdjFactor := AdjFactorFrom;
      RoundingMethod.Code := RoundingMethodCode;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

