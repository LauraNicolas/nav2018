OBJECT Codeunit 445 Purch.-Post Prepmt.  (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the prepayments for %1 %2?';
      Text001@1001 : TextConst 'ENU=Do you want to post a credit memo for the prepayments for %1 %2?';
      PurchInvHeader@1003 : Record "Purch. Inv. Header";
      PurchCrMemoHeader@1002 : Record "Purch. Cr. Memo Hdr.";

    [Internal]
    PROCEDURE PostPrepmtInvoiceYN@1(VAR PurchHeader2@1001 : Record "Purchase Header";Print@1002 : Boolean);
    VAR
      PurchHeader@1003 : Record "Purchase Header";
      PurchPostPrepmt@1000 : Codeunit "Purchase-Post Prepayments";
    BEGIN
      PurchHeader.COPY(PurchHeader2);
      with PurchHeader do begin
        if not CONFIRM(Text000,false,"Document Type","No.") then
          exit;

        PurchPostPrepmt.Invoice(PurchHeader);

        if Print then
          GetReport(PurchHeader,0);

        COMMIT;
        PurchHeader2 := PurchHeader;
      end;
    END;

    [Internal]
    PROCEDURE PostPrepmtCrMemoYN@2(VAR PurchHeader2@1001 : Record "Purchase Header";Print@1002 : Boolean);
    VAR
      PurchHeader@1003 : Record "Purchase Header";
      PurchPostPrepmt@1000 : Codeunit "Purchase-Post Prepayments";
    BEGIN
      PurchHeader.COPY(PurchHeader2);
      with PurchHeader do begin
        if not CONFIRM(Text001,false,"Document Type","No.") then
          exit;

        PurchPostPrepmt.CreditMemo(PurchHeader);

        if Print then
          GetReport(PurchHeader,1);

        COMMIT;
        PurchHeader2 := PurchHeader;
      end;
    END;

    LOCAL PROCEDURE GetReport@4(VAR PurchHeader@1000 : Record "Purchase Header";DocumentType@1001 : 'Invoice,"Credit Memo"');
    BEGIN
      with PurchHeader do
        case DocumentType of
          DocumentType::Invoice:
            begin
              PurchInvHeader."No." := "Last Prepayment No.";
              PurchInvHeader.SETRECFILTER;
              PurchInvHeader.PrintRecords(false);
            end;
          DocumentType::"Credit Memo":
            begin
              PurchCrMemoHeader."No." := "Last Prepmt. Cr. Memo No.";
              PurchCrMemoHeader.SETRECFILTER;
              PurchCrMemoHeader.PrintRecords(false);
            end;
        end;
    END;

    BEGIN
    END.
  }
}

