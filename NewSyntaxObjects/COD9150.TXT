OBJECT Codeunit 9150 My Records Update Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Table,18,OnAfterModifyEvent)]
    LOCAL PROCEDURE UpdateMyCustomerAfterCustomerModify@1(VAR Rec@1000 : Record Customer;VAR xRec@1001 : Record Customer;RunTrigger@1003 : Boolean);
    VAR
      MyCustomer@1002 : Record "My Customer";
    BEGIN
      if Rec.ISTEMPORARY then
        exit;

      MyCustomer.SETRANGE("Customer No.",Rec."No.");

      // xRec will be = Rec if called from code
      MyCustomer.SETFILTER(Name,'<>%1',Rec.Name);
      MyCustomer.MODIFYALL(Name,Rec.Name);
      MyCustomer.SETRANGE(Name);

      MyCustomer.SETFILTER("Phone No.",'<>%1',Rec."Phone No.");
      MyCustomer.MODIFYALL("Phone No.",Rec."Phone No.");
    END;

    [EventSubscriber(Table,23,OnAfterModifyEvent)]
    LOCAL PROCEDURE UpdateMyVendorAfterVendorModify@10(VAR Rec@1000 : Record Vendor;VAR xRec@1001 : Record Vendor;RunTrigger@1003 : Boolean);
    VAR
      MyVendor@1002 : Record "My Vendor";
    BEGIN
      if Rec.ISTEMPORARY then
        exit;

      MyVendor.SETRANGE("Vendor No.",Rec."No.");

      MyVendor.SETFILTER(Name,'<>%1',Rec.Name);
      MyVendor.MODIFYALL(Name,Rec.Name);
      MyVendor.SETRANGE(Name);

      MyVendor.SETFILTER("Phone No.",'<>%1',Rec."Phone No.");
      MyVendor.MODIFYALL("Phone No.",Rec."Phone No.");
    END;

    [EventSubscriber(Table,27,OnAfterModifyEvent)]
    LOCAL PROCEDURE UpdateMyItemAfterItemModify@8(VAR Rec@1000 : Record Item;VAR xRec@1001 : Record Item;RunTrigger@1003 : Boolean);
    VAR
      MyItem@1002 : Record "My Item";
    BEGIN
      if Rec.ISTEMPORARY then
        exit;

      MyItem.SETRANGE("Item No.",Rec."No.");

      MyItem.SETFILTER(Description,'<>%1',Rec.Description);
      MyItem.MODIFYALL(Description,Rec.Description);
      MyItem.SETRANGE(Description);

      MyItem.SETFILTER("Unit Price",'<>%1',Rec."Unit Price");
      MyItem.MODIFYALL("Unit Price",Rec."Unit Price");
    END;

    [EventSubscriber(Table,15,OnAfterModifyEvent)]
    LOCAL PROCEDURE UpdateMyAccountAfterAcountModify@6(VAR Rec@1000 : Record "G/L Account";VAR xRec@1001 : Record "G/L Account";RunTrigger@1003 : Boolean);
    VAR
      MyAccount@1002 : Record "My Account";
    BEGIN
      if Rec.ISTEMPORARY then
        exit;

      MyAccount.SETRANGE("Account No.",Rec."No.");

      MyAccount.SETFILTER(Name,'<>%1',Rec.Name);
      MyAccount.MODIFYALL(Name,Rec.Name);
    END;

    BEGIN
    END.
  }
}

