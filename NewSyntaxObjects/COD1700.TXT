OBJECT Codeunit 1700 Exp. Launcher Pos. Pay
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Check Ledger Entry";
    Permissions=TableData "Check Ledger Entry"=rimd,
                TableData "Data Exch."=rimd;
    OnRun=BEGIN
            PositivePayProcess(Rec,true);
          END;

  }
  CODE
  {

    [Internal]
    PROCEDURE PositivePayProcess@2(VAR CheckLedgerEntry@1015 : Record "Check Ledger Entry";ShowDialog@1016 : Boolean);
    VAR
      DataExch@1014 : Record "Data Exch.";
      DataExchDef@1013 : Record "Data Exch. Def";
      CheckLedgerEntry2@1012 : Record "Check Ledger Entry";
      BankAccount@1011 : Record "Bank Account";
      BankExportImportSetup@1010 : Record "Bank Export/Import Setup";
      FileManagement@1009 : Codeunit "File Management";
      ExpWritingPosPay@1008 : Codeunit "Exp. Writing Pos. Pay";
      ExpExternalDataPosPay@1007 : Codeunit "Exp. External Data Pos. Pay";
      DataExchEntryCodeDetail@1006 : Integer;
      DataExchEntryCodeFooter@1005 : Integer;
      DataExchDefCode@1004 : Code[20];
      Filename@1003 : Text;
      HeaderArray@1002 : ARRAY [100] OF Integer;
      DetailArray@1001 : ARRAY [100] OF Integer;
      FooterArray@1000 : ARRAY [100] OF Integer;
    BEGIN
      CheckLedgerEntry2.COPYFILTERS(CheckLedgerEntry);
      CheckLedgerEntry2.FINDFIRST;

      BankAccount.GET(CheckLedgerEntry2."Bank Account No.");

      BankExportImportSetup.SETRANGE(Code,BankAccount."Positive Pay Export Code");
      if BankExportImportSetup.FINDFIRST then begin
        DataExchDefCode := BankExportImportSetup."Data Exch. Def. Code";
        Filename := FileManagement.ServerTempFileName('txt');

        ProcessHeaders(BankAccount,DataExchDefCode,HeaderArray,Filename);
        ProcessDetails(CheckLedgerEntry2,DataExchDefCode,DataExchEntryCodeDetail,DetailArray,Filename);
        ProcessFooters(BankAccount,DataExchDefCode,FooterArray,Filename,DataExchEntryCodeDetail,DataExchEntryCodeFooter);

        ExpWritingPosPay.ExportPositivePay(DataExchEntryCodeDetail,DataExchEntryCodeFooter,Filename,FooterArray);

        // This should only be called from a test codeunit, calling CreateExportFile MUST pass in a FALSE parameter
        DataExchDef.GET(DataExchDefCode);
        if DataExchDef."Ext. Data Handling Codeunit" > 0 then begin
          DataExch.GET(DataExchEntryCodeDetail);
          if DataExchDef."Ext. Data Handling Codeunit" = CODEUNIT::"Exp. External Data Pos. Pay" then
            ExpExternalDataPosPay.CreateExportFile(DataExch,ShowDialog)
          else
            CODEUNIT.RUN(DataExchDef."Ext. Data Handling Codeunit",DataExch);
        end;

        if DataExchDef."User Feedback Codeunit" > 0 then begin
          DataExch.GET(DataExchEntryCodeDetail);
          CODEUNIT.RUN(DataExchDef."User Feedback Codeunit",DataExch);
        end;

        // Clean up the work tables.
        ExpWritingPosPay.CleanUpPositivePayWorkTables(HeaderArray,DetailArray,FooterArray);
      end;
    END;

    LOCAL PROCEDURE UpdateCheckLedger@1(VAR CheckLedgerEntry@1002 : Record "Check Ledger Entry";DataExchEntryCodeDetail@1001 : Integer);
    VAR
      CheckLedgerEntry2@1000 : Record "Check Ledger Entry";
    BEGIN
      CheckLedgerEntry2.COPYFILTERS(CheckLedgerEntry);
      CheckLedgerEntry2.SETFILTER(
        "Entry Status",
        '%1|%2|>%3',
        CheckLedgerEntry2."Entry Status"::Printed,
        CheckLedgerEntry2."Entry Status"::Posted,
        CheckLedgerEntry2."Entry Status"::"Test Print");
      CheckLedgerEntry2.SETRANGE("Positive Pay Exported",false);
      CheckLedgerEntry2.MODIFYALL("Data Exch. Entry No.",DataExchEntryCodeDetail,true);

      CheckLedgerEntry2.SETFILTER(
        "Entry Status",
        '%1|%2|%3',
        CheckLedgerEntry2."Entry Status"::Voided,
        CheckLedgerEntry2."Entry Status"::"Financially Voided",
        CheckLedgerEntry2."Entry Status"::"Test Print");
      CheckLedgerEntry2.MODIFYALL("Data Exch. Voided Entry No.",DataExchEntryCodeDetail,true);
    END;

    LOCAL PROCEDURE ProcessHeaders@3(VAR BankAccount@1006 : Record "Bank Account";DataExchDefCode@1001 : Code[20];VAR HeaderArray@1009 : ARRAY [100] OF Integer;Filename@1008 : Text);
    VAR
      DataExch@1002 : Record "Data Exch.";
      DataExchDef@1007 : Record "Data Exch. Def";
      DataExchLineDef@1018 : Record "Data Exch. Line Def";
      DataExchMapping@1004 : Record "Data Exch. Mapping";
      PositivePayExportMgt@1005 : Codeunit "Positive Pay Export Mgt";
      HdrCount@1000 : Integer;
    BEGIN
      HdrCount := 0;
      DataExchLineDef.INIT;
      DataExchLineDef.SETRANGE("Data Exch. Def Code",DataExchDefCode);
      DataExchLineDef.SETRANGE("Line Type",DataExchLineDef."Line Type"::Header);
      if DataExchLineDef.FINDSET then begin
        repeat
          // Insert the Data Exchange Header records
          DataExch."Entry No." := 0;
          DataExch."Data Exch. Def Code" := DataExchDefCode;
          DataExch."Data Exch. Line Def Code" := DataExchLineDef.Code;
          DataExch.INSERT;
          COMMIT;

          HdrCount := HdrCount + 1;
          HeaderArray[HdrCount] := DataExch."Entry No.";

          // It is only here where we know the True DataExch."Entry No"..
          DataExchMapping.SETRANGE("Data Exch. Def Code",DataExchDefCode);
          DataExchMapping.SETRANGE("Data Exch. Line Def Code",DataExchLineDef.Code);
          DataExchMapping.FINDFIRST;

          // Populate the Header/Detail/Footer work tables
          PositivePayExportMgt.PreparePosPayHeader(DataExch,BankAccount."Bank Account No.");
          if DataExchMapping."Pre-Mapping Codeunit" > 0 then
            CODEUNIT.RUN(DataExchMapping."Pre-Mapping Codeunit",DataExch);

          // Create the Entries and values in the Data Exch. Field table
          if DataExchMapping."Mapping Codeunit" > 0 then
            CODEUNIT.RUN(DataExchMapping."Mapping Codeunit",DataExch);

          DataExchDef.GET(DataExchDefCode);
          if DataExchDef."Reading/Writing Codeunit" = CODEUNIT::"Exp. Writing Pos. Pay" then
            PositivePayExportMgt.ExportDataExchToFlatFile(DataExch."Entry No.",Filename,DataExchLineDef."Line Type",HdrCount);
        until  DataExchLineDef.NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE ProcessDetails@4(VAR CheckLedgerEntry@1012 : Record "Check Ledger Entry";DataExchDefCode@1008 : Code[20];VAR DataExchEntryCodeDetail@1009 : Integer;VAR DetailArray@1010 : ARRAY [100] OF Integer;Filename@1007 : Text);
    VAR
      DataExch@1006 : Record "Data Exch.";
      DataExchDef@1005 : Record "Data Exch. Def";
      DataExchLineDef@1004 : Record "Data Exch. Line Def";
      DataExchMapping@1003 : Record "Data Exch. Mapping";
      PositivePayExportMgt@1002 : Codeunit "Positive Pay Export Mgt";
      CurrentDataExchEntryCodeDetail@1001 : Integer;
      DetailCount@1000 : Integer;
    BEGIN
      DetailCount := 0;
      DataExchLineDef.INIT;
      DataExchLineDef.SETRANGE("Data Exch. Def Code",DataExchDefCode);
      DataExchLineDef.SETRANGE("Line Type",DataExchLineDef."Line Type"::Detail);
      if DataExchLineDef.FINDSET then begin
        repeat
          // Insert the Data Exchange Detail records
          DataExch."Entry No." := 0;
          DataExch."Data Exch. Def Code" := DataExchDefCode;
          DataExch."Data Exch. Line Def Code" := DataExchLineDef.Code;
          DataExch.INSERT;
          COMMIT;

          DetailCount := DetailCount + 1;
          DetailArray[DetailCount] := DataExch."Entry No.";

          // It is only here where we know the True DataExch."Entry No"..
          DataExchMapping.SETRANGE("Data Exch. Def Code",DataExchDefCode);
          DataExchMapping.SETRANGE("Data Exch. Line Def Code",DataExchLineDef.Code);
          DataExchMapping.FINDFIRST;

          if DataExchEntryCodeDetail = 0 then
            DataExchEntryCodeDetail := DataExch."Entry No.";
          CurrentDataExchEntryCodeDetail := DataExch."Entry No.";

          UpdateCheckLedger(CheckLedgerEntry,CurrentDataExchEntryCodeDetail);

          // Populate the Header/Detail/Footer work tables
          if DataExchMapping."Pre-Mapping Codeunit" > 0 then
            CODEUNIT.RUN(DataExchMapping."Pre-Mapping Codeunit",DataExch);

          // Create the Entries and values in the Data Exch. Field table
          if DataExchMapping."Mapping Codeunit" > 0 then
            CODEUNIT.RUN(DataExchMapping."Mapping Codeunit",DataExch);

          DataExchDef.GET(DataExchDefCode);
          if DataExchDef."Reading/Writing Codeunit" > 0 then
            if DataExchDef."Reading/Writing Codeunit" = CODEUNIT::"Exp. Writing Pos. Pay" then
              PositivePayExportMgt.ExportDataExchToFlatFile(DataExch."Entry No.",Filename,DataExchLineDef."Line Type",0)
            else
              CODEUNIT.RUN(DataExchDef."Reading/Writing Codeunit",DataExch);

        until DataExchLineDef.NEXT = 0;
        // When we are done, we need to set the Check Ledger record(s) Entry No back to the original.
        if DataExchEntryCodeDetail > 0 then
          UpdateCheckLedger(CheckLedgerEntry,DataExchEntryCodeDetail);
      end;
    END;

    LOCAL PROCEDURE ProcessFooters@5(VAR BankAccount@1002 : Record "Bank Account";DataExchDefCode@1001 : Code[20];VAR FooterArray@1012 : ARRAY [100] OF Integer;Filename@1000 : Text;DataExchEntryCodeDetail@1010 : Integer;VAR DataExchEntryCodeFooter@1011 : Integer);
    VAR
      DataExch@1009 : Record "Data Exch.";
      DataExchDef@1008 : Record "Data Exch. Def";
      DataExchLineDef@1007 : Record "Data Exch. Line Def";
      DataExchMapping@1006 : Record "Data Exch. Mapping";
      PositivePayExportMgt@1005 : Codeunit "Positive Pay Export Mgt";
      FooterCount@1003 : Integer;
    BEGIN
      FooterCount := 0;
      DataExchLineDef.INIT;
      DataExchLineDef.SETRANGE("Data Exch. Def Code",DataExchDefCode);
      DataExchLineDef.SETRANGE("Line Type",DataExchLineDef."Line Type"::Footer);
      if DataExchLineDef.FINDSET then begin
        repeat
          // Insert the Data Exchange Footer records
          DataExch."Entry No." := 0;
          DataExch."Data Exch. Def Code" := DataExchDefCode;
          DataExch."Data Exch. Line Def Code" := DataExchLineDef.Code;
          DataExch.INSERT;
          COMMIT;

          FooterCount := FooterCount + 1;
          FooterArray[FooterCount] := DataExch."Entry No.";

          // It is only here where we know the True DataExch."Entry No"..
          DataExchMapping.SETRANGE("Data Exch. Def Code",DataExchDefCode);
          DataExchMapping.SETRANGE("Data Exch. Line Def Code",DataExchLineDef.Code);
          DataExchMapping.FINDFIRST;

          // Populate the Header/Detail/Footer work tables
          if DataExchMapping."Pre-Mapping Codeunit" > 0 then
            CODEUNIT.RUN(DataExchMapping."Pre-Mapping Codeunit",DataExch);

          // Create the Entries and values in the Data Exch. Field table
          PositivePayExportMgt.PreparePosPayFooter(DataExch,DataExchEntryCodeDetail,BankAccount."Bank Account No.");
          if DataExchMapping."Mapping Codeunit" > 0 then
            CODEUNIT.RUN(DataExchMapping."Mapping Codeunit",DataExch);

          DataExchDef.GET(DataExchDefCode);
          if DataExchDef."Reading/Writing Codeunit" = CODEUNIT::"Exp. Writing Pos. Pay" then
            PositivePayExportMgt.ExportDataExchToFlatFile(DataExch."Entry No.",Filename,DataExchLineDef."Line Type",0);
          DataExchEntryCodeFooter := DataExch."Entry No.";
        until DataExchLineDef.NEXT = 0;
      end;
    END;

    BEGIN
    END.
  }
}

