OBJECT Codeunit 1241 Fixed File Import
{
  OBJECT-PROPERTIES
  {
    Date=20150915D;
    Time=120000T;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo="Data Exch.";
    Permissions=TableData "Data Exch. Field"=rimd;
    OnRun=VAR
            ReadStream@1003 : InStream;
            ReadText@1002 : Text;
            ReadLen@1001 : Integer;
            LineNo@1000 : Integer;
            SkippedLineNo@1004 : Integer;
          BEGIN
            "File Content".CREATEINSTREAM(ReadStream);
            DataExchDef.GET("Data Exch. Def Code");
            LineNo := 1;
            repeat
              ReadLen := ReadStream.READTEXT(ReadText);
              if ReadLen > 0 then
                ParseLine(ReadText,Rec,LineNo,SkippedLineNo);
            until ReadLen = 0;
          END;

  }
  CODE
  {
    VAR
      DataExchDef@1000 : Record "Data Exch. Def";

    LOCAL PROCEDURE ParseLine@2(Line@1005 : Text;DataExch@1000 : Record "Data Exch.";VAR LineNo@1007 : Integer;VAR SkippedLineNo@1001 : Integer);
    VAR
      DataExchLineDef@1008 : Record "Data Exch. Line Def";
      DataExchColumnDef@1009 : Record "Data Exch. Column Def";
      DataExchField@1010 : Record "Data Exch. Field";
      StartPosition@1011 : Integer;
    BEGIN
      DataExchLineDef.SETRANGE("Data Exch. Def Code",DataExch."Data Exch. Def Code");
      DataExchLineDef.FINDFIRST;

      if ((LineNo + SkippedLineNo) <= DataExchDef."Header Lines") or
         ((DataExchLineDef."Data Line Tag" <> '') and (STRPOS(Line,DataExchLineDef."Data Line Tag") <> 1))
      then begin
        SkippedLineNo += 1;
        exit;
      end;

      DataExchColumnDef.SETRANGE("Data Exch. Def Code",DataExch."Data Exch. Def Code");
      DataExchColumnDef.SETRANGE("Data Exch. Line Def Code",DataExchLineDef.Code);
      DataExchColumnDef.FINDSET;

      StartPosition := 1;
      repeat
        DataExchField.InsertRecXMLField(DataExch."Entry No.",LineNo,DataExchColumnDef."Column No.",'',
          COPYSTR(Line,StartPosition,DataExchColumnDef.Length),DataExchLineDef.Code);
        StartPosition += DataExchColumnDef.Length;
      until DataExchColumnDef.NEXT = 0;
      LineNo += 1;
    END;

    BEGIN
    END.
  }
}

