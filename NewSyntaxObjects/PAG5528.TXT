OBJECT Page 5528 Purchase Invoice Line Entity
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Purchase Invoice Line Entity;
    SourceTable="Purch. Inv. Line Aggregate";
    DelayedInsert=true;
    PageType=ListPart;
    SourceTableTemporary=true;
    OnFindRecord=VAR
                   PurchInvAggregator@1002 : Codeunit "Purch. Inv. Aggregator";
                   DocumentIdFilter@1001 : Text;
                   FilterView@1000 : Text;
                 BEGIN
                   if not LinesLoaded then begin
                     FilterView := GETVIEW;
                     DocumentIdFilter := GETFILTER("Document Id");
                     PurchInvAggregator.LoadLines(Rec,DocumentIdFilter);
                     SETVIEW(FilterView);
                     if not FINDFIRST then
                       exit(false);
                     LinesLoaded := true;
                   end;

                   exit(true);
                 END;

    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                  VALIDATE(Type,Type::Item);
                  RegisterFieldSet(FIELDNO(Type));
                END;

    OnInsertRecord=VAR
                     PurchInvAggregator@1000 : Codeunit "Purch. Inv. Aggregator";
                   BEGIN
                     if InsertItem then
                       InsertItemOnTheFly;
                     PurchInvAggregator.PropagateInsertLine(Rec,TempFieldBuffer);
                   END;

    OnModifyRecord=VAR
                     PurchInvAggregator@1000 : Codeunit "Purch. Inv. Aggregator";
                   BEGIN
                     if InsertItem then
                       InsertItemOnTheFly;
                     PurchInvAggregator.PropagateModifyLine(Rec,TempFieldBuffer);
                   END;

    OnDeleteRecord=VAR
                     PurchInvAggregator@1000 : Codeunit "Purch. Inv. Aggregator";
                   BEGIN
                     PurchInvAggregator.PropagateDeleteLine(Rec);
                   END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=sequence;
                CaptionML=[@@@={Locked};
                           ENU=sequence];
                ApplicationArea=#All;
                SourceExpr="Line No.";
                OnValidate=BEGIN
                             if (xRec."Line No." <> "Line No.") and (xRec."Line No." <> 0) then
                               ERROR(CannotChangeLineNoErr);

                             RegisterFieldSet(FIELDNO("Line No."));
                           END;

                ImplicitType=Integer }

    { 4   ;2   ;Field     ;
                Name=itemId;
                CaptionML=[@@@={Locked};
                           ENU=itemId];
                ApplicationArea=#All;
                SourceExpr="Item Id";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Type));
                             RegisterFieldSet(FIELDNO("No."));
                             RegisterFieldSet(FIELDNO("Item Id"));

                             Item.SETRANGE(Id,"Item Id");

                             if not Item.FINDFIRST then begin
                               InsertItem := true;
                               CheckIntegrationIdInUse;

                               Item.Id := "Item Id";
                               RegisterFieldSet(Item.FIELDNO(Id));
                               exit;
                             end;

                             "No." := Item."No.";
                           END;

                ImplicitType=GUID }

    { 5   ;2   ;Field     ;
                Name=accountId;
                CaptionML=[@@@={Locked};
                           ENU=accountId];
                ApplicationArea=#All;
                SourceExpr="Account Id";
                OnValidate=VAR
                             EmptyGuid@1000 : GUID;
                           BEGIN
                             if "Account Id" <> EmptyGuid then
                               if Item."No." <> '' then
                                 ERROR(BothItemIdAndAccountIdAreSpecifiedErr);
                             RegisterFieldSet(FIELDNO(Type));
                             RegisterFieldSet(FIELDNO("Account Id"));
                             RegisterFieldSet(FIELDNO("No."));
                           END;

                ImplicitType=GUID }

    { 6   ;2   ;Field     ;
                Name=lineType;
                CaptionML=[@@@={Locked};
                           ENU=lineType];
                ApplicationArea=#All;
                SourceExpr="API Type";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Type));
                           END;

                ImplicitType=Option }

    { 23  ;2   ;Field     ;
                Name=lineDetails;
                CaptionML=[@@@={Locked};
                           ENU=lineDetails];
                ToolTipML=ENU=Specifies details about the line.;
                ApplicationArea=#All;
                SourceExpr=LineObjectDetailsJSON;
                OnValidate=VAR
                             GraphMgtComplexTypes@1000 : Codeunit "Graph Mgt - Complex Types";
                           BEGIN
                             if not InsertItem then
                               exit;

                             GraphMgtComplexTypes.ParseDocumentLineObjectDetailsFromJSON(
                               LineObjectDetailsJSON,Item."No.",Item.Description,Item."Description 2");

                             if Item."No." <> '' then
                               RegisterItemFieldSet(Item.FIELDNO("No."));

                             RegisterFieldSet(FIELDNO("No."));

                             if Item.Description <> '' then
                               RegisterItemFieldSet(Item.FIELDNO(Description));

                             if Description = '' then begin
                               Description := Item.Description;
                               RegisterFieldSet(FIELDNO(Description));
                             end;

                             if Item."Description 2" <> '' then begin
                               "Description 2" := Item."Description 2";
                               RegisterItemFieldSet(Item.FIELDNO("Description 2"));
                               RegisterFieldSet(FIELDNO("Description 2"));
                             end;
                           END;

                ODataEDMType=DOCUMENTLINEOBJECTDETAILS;
                ImplicitType=Text }

    { 7   ;2   ;Field     ;
                Name=description;
                CaptionML=ENU=description;
                ToolTipML=ENU=Specifies the description of the purchase invoice line.;
                ApplicationArea=#All;
                SourceExpr=Description;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Description));
                           END;

                ImplicitType=Text50 }

    { 24  ;2   ;Field     ;
                Name=unitOfMeasure;
                CaptionML=[@@@={Locked};
                           ENU=unitOfMeasure];
                ToolTipML=ENU=Specifies Unit of Measure.;
                ApplicationArea=#All;
                SourceExpr=UnitOfMeasureJSON;
                OnValidate=VAR
                             TempUnitOfMeasure@1002 : TEMPORARY Record "Unit of Measure";
                             PurchInvAggregator@1000 : Codeunit "Purch. Inv. Aggregator";
                             GraphCollectionMgtItem@1001 : Codeunit "Graph Collection Mgt - Item";
                           BEGIN
                             PurchInvAggregator.VerifyCanUpdateUOM(Rec);
                             GraphCollectionMgtItem.ParseJSONToUnitOfMeasure(UnitOfMeasureJSON,TempUnitOfMeasure);
                             "Unit of Measure Code" := TempUnitOfMeasure.Code;
                             RegisterFieldSet(FIELDNO("Unit of Measure Code"));

                             if InsertItem then
                               exit;

                             PurchInvAggregator.UpdateUnitOfMeasure(Item,UnitOfMeasureJSON);
                           END;

                ODataEDMType=ITEM-UOM;
                ImplicitType=Text }

    { 8   ;2   ;Field     ;
                Name=unitCost;
                CaptionML=[@@@={Locked};
                           ENU=directUnitCost];
                ApplicationArea=#All;
                SourceExpr="Direct Unit Cost";
                OnValidate=BEGIN
                             if InsertItem then begin
                               Item."Unit Cost" := "Direct Unit Cost";
                               RegisterFieldSet(Item.FIELDNO("Unit Cost"));
                             end;

                             RegisterFieldSet(FIELDNO("Direct Unit Cost"));
                           END;

                ImplicitType=Decimal }

    { 9   ;2   ;Field     ;
                Name=quantity;
                CaptionML=[@@@={Locked};
                           ENU=quantity];
                ApplicationArea=#All;
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Quantity));
                           END;

                ImplicitType=Decimal }

    { 10  ;2   ;Field     ;
                Name=discountAmount;
                CaptionML=[@@@={Locked};
                           ENU=discountAmount];
                ApplicationArea=#All;
                SourceExpr="Line Discount Amount";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Line Discount Amount"));
                           END;

                ImplicitType=Decimal }

    { 11  ;2   ;Field     ;
                Name=discountPercent;
                CaptionML=[@@@={Locked};
                           ENU=discountPercent];
                ApplicationArea=#All;
                SourceExpr="Line Discount %";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Line Discount %"));
                           END;

                ImplicitType=Decimal }

    { 12  ;2   ;Field     ;
                Name=discountAppliedBeforeTax;
                CaptionML=ENU=discountAppliedBeforeTax;
                ApplicationArea=#All;
                SourceExpr="Discount Applied Before Tax";
                ImplicitType=Boolean }

    { 13  ;2   ;Field     ;
                Name=amountExcludingTax;
                CaptionML=[@@@={Locked};
                           ENU=amountExcludingTax];
                ApplicationArea=#All;
                SourceExpr="Line Amount Excluding Tax";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Amount));
                           END;

                ImplicitType=Decimal }

    { 14  ;2   ;Field     ;
                Name=taxCode;
                CaptionML=[@@@={Locked};
                           ENU=taxCode];
                ApplicationArea=#All;
                SourceExpr="Tax Code";
                OnValidate=VAR
                             GeneralLedgerSetup@1000 : Record "General Ledger Setup";
                           BEGIN
                             if InsertItem then begin
                               if GeneralLedgerSetup.UseVat then
                                 exit;

                               Item."Tax Group Code" := COPYSTR("Tax Code",1,MAXSTRLEN(Item."Tax Group Code"));
                               RegisterFieldSet(FIELDNO("Tax Code"));
                             end;
                           END;

                ImplicitType=Code50 }

    { 15  ;2   ;Field     ;
                Name=taxPercent;
                CaptionML=[@@@={Locked};
                           ENU=taxPercent];
                ApplicationArea=#All;
                SourceExpr="VAT %";
                ImplicitType=Decimal }

    { 16  ;2   ;Field     ;
                Name=totalTaxAmount;
                CaptionML=[@@@={Locked};
                           ENU=totalTaxAmount];
                ApplicationArea=#All;
                SourceExpr="Line Tax Amount";
                ImplicitType=Decimal }

    { 17  ;2   ;Field     ;
                Name=amountIncludingTax;
                CaptionML=[@@@={Locked};
                           ENU=amountIncludingTax];
                ApplicationArea=#All;
                SourceExpr="Line Amount Including Tax";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Amount Including VAT"));
                           END;

                ImplicitType=Decimal }

    { 18  ;2   ;Field     ;
                Name=invoiceDiscountAllocation;
                CaptionML=[@@@={Locked};
                           ENU=invoiceDiscountAllocation];
                ApplicationArea=#All;
                SourceExpr="Inv. Discount Amount Excl. VAT";
                ImplicitType=Decimal }

    { 19  ;2   ;Field     ;
                Name=netAmount;
                CaptionML=[@@@={Locked};
                           ENU=netAmount];
                ApplicationArea=#All;
                SourceExpr=Amount;
                ImplicitType=Decimal }

    { 20  ;2   ;Field     ;
                Name=netTaxAmount;
                CaptionML=[@@@={Locked};
                           ENU=netTaxAmount];
                ApplicationArea=#All;
                SourceExpr="Tax Amount";
                ImplicitType=Decimal }

    { 21  ;2   ;Field     ;
                Name=netAmountIncludingTax;
                CaptionML=[@@@={Locked};
                           ENU=netAmountIncludingTax];
                ApplicationArea=#All;
                SourceExpr="Amount Including VAT";
                ImplicitType=Decimal }

    { 22  ;2   ;Field     ;
                Name=expectedReceiptDate;
                CaptionML=[@@@={Locked};
                           ENU=expectedReceiptDate];
                ApplicationArea=#All;
                SourceExpr="Expected Receipt Date";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Expected Receipt Date"));
                           END;

                ImplicitType=Date }

  }
  CODE
  {
    VAR
      TempFieldBuffer@1006 : TEMPORARY Record "Field Buffer";
      TempItemFieldSet@1005 : TEMPORARY Record Field;
      Item@1004 : Record Item;
      UnitOfMeasureJSON@1003 : Text;
      LineObjectDetailsJSON@1002 : Text;
      LinesLoaded@1001 : Boolean;
      InsertItem@1000 : Boolean;
      CannotChangeLineNoErr@1009 : TextConst '@@@={Locked};ENU=The value for sequence cannot be modified. Delete and insert the line again.';
      ItemWasDeletedErr@1008 : TextConst '@@@={Locked};ENU=The item was deleted.';
      IdIsAlreadyUsedErr@1007 : TextConst '@@@={Locked};ENU=The id is already in use used.';
      BothItemIdAndAccountIdAreSpecifiedErr@1010 : TextConst 'ENU=Both itemId and accountId are specified. Specify only one of them.';

    LOCAL PROCEDURE RegisterFieldSet@1(FieldNo@1000 : Integer);
    VAR
      LastOrderNo@1001 : Integer;
    BEGIN
      LastOrderNo := 1;
      if TempFieldBuffer.FINDLAST then
        LastOrderNo := TempFieldBuffer.Order + 1;

      CLEAR(TempFieldBuffer);
      TempFieldBuffer.Order := LastOrderNo;
      TempFieldBuffer."Table ID" := DATABASE::"Purch. Inv. Line Aggregate";
      TempFieldBuffer."Field ID" := FieldNo;
      TempFieldBuffer.INSERT;
    END;

    LOCAL PROCEDURE ClearCalculatedFields@2();
    BEGIN
      TempFieldBuffer.RESET;
      TempFieldBuffer.DELETEALL;
      TempItemFieldSet.RESET;
      TempItemFieldSet.DELETEALL;

      CLEAR(Item);
      CLEAR(UnitOfMeasureJSON);
      CLEAR(InsertItem);
      CLEAR(LineObjectDetailsJSON);
    END;

    LOCAL PROCEDURE SetCalculatedFields@3();
    VAR
      GraphMgtPurchInvLines@1001 : Codeunit "Graph Mgt - Purch. Inv. Lines";
      GraphMgtComplexTypes@1002 : Codeunit "Graph Mgt - Complex Types";
    BEGIN
      LineObjectDetailsJSON := GraphMgtComplexTypes.GetPurchaseLineDescriptionComplexType(Rec);
      UnitOfMeasureJSON := GraphMgtPurchInvLines.GetUnitOfMeasureJSON(Rec);
    END;

    LOCAL PROCEDURE RegisterItemFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      if TempItemFieldSet.GET(DATABASE::Item,FieldNo) then
        exit;

      TempItemFieldSet.INIT;
      TempItemFieldSet.TableNo := DATABASE::Item;
      TempItemFieldSet.VALIDATE("No.",FieldNo);
      TempItemFieldSet.INSERT(true);
    END;

    LOCAL PROCEDURE CheckIntegrationIdInUse@8();
    VAR
      IntegrationRecord@1000 : Record "Integration Record";
    BEGIN
      if not IntegrationRecord.GET("Item Id") then
        exit;

      if IntegrationRecord."Table ID" = DATABASE::Item then
        ERROR(ItemWasDeletedErr);

      ERROR(IdIsAlreadyUsedErr);
    END;

    LOCAL PROCEDURE InsertItemOnTheFly@16();
    VAR
      GraphCollectionMgtItem@1001 : Codeunit "Graph Collection Mgt - Item";
    BEGIN
      GraphCollectionMgtItem.InsertItemFromSalesDocument(Item,TempItemFieldSet,UnitOfMeasureJSON);
      VALIDATE("No.",Item."No.");

      if Description = '' then
        Description := Item.Description;
    END;

    BEGIN
    END.
  }
}

