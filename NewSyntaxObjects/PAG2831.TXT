OBJECT Page 2831 Native - Payments
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=nativePayments];
    ModifyAllowed=false;
    SourceTable="Native - Payment";
    DelayedInsert=true;
    PageType=ListPart;
    SourceTableTemporary=true;
    OnFindRecord=VAR
                   AppliesToInvoiceIdFilter@1001 : Text;
                   FilterView@1000 : Text;
                 BEGIN
                   if not PaymentsLoaded then begin
                     FilterView := GETVIEW;
                     AppliesToInvoiceIdFilter := GETFILTER("Applies-to Invoice Id");
                     if AppliesToInvoiceIdFilter <> '' then
                       NativePayments.LoadPayments(TempNativePayment,AppliesToInvoiceIdFilter)
                     else
                       NativePayments.LoadAllPayments(TempNativePayment);
                     COPY(TempNativePayment,true);

                     SETVIEW(FilterView);
                     if not FINDFIRST then
                       exit(false);
                     PaymentsLoaded := true;
                   end;

                   exit(true);
                 END;

    OnInsertRecord=VAR
                     GenJournalLine@1000 : Record "Gen. Journal Line";
                   BEGIN
                     CheckNecessaryFields;

                     // It does not get validated automatically
                     VALIDATE("Applies-to Invoice Id","Applies-to Invoice Id");

                     CheckIfAmountExceedsRemainingAmount(Amount,"Applies-to Invoice No.","Payment Date");

                     NativePayments.InsertJournalLine(Rec,GenJournalLine);
                     NativePayments.PostJournal(GenJournalLine);

                     NativePayments.LoadPayments(Rec,"Applies-to Invoice Id");
                     FINDLAST;

                     exit(false);
                   END;

    OnDeleteRecord=BEGIN
                     if not TempNativePayment.FINDLAST then
                       exit(false);

                     if RECORDID <> TempNativePayment.RECORDID then
                       ERROR(CanOnlyCancelLastPaymentErr);

                     NativePayments.CancelCustLedgerEntry("Ledger Entry No.");
                   END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=paymentNo;
                CaptionML=[@@@={Locked};
                           ENU=paymentNo];
                ApplicationArea=#All;
                SourceExpr="Payment No.";
                Editable=False;
                OnValidate=BEGIN
                             ERROR(PaymentNoShouldNotBeSpecifiedErr);
                           END;

                ImplicitType=Integer }

    { 4   ;2   ;Field     ;
                Name=customerId;
                CaptionML=[@@@={Locked};
                           ENU=customerId];
                ApplicationArea=#All;
                SourceExpr="Customer Id";
                OnValidate=VAR
                             Customer@1000 : Record Customer;
                           BEGIN
                             Customer.SETRANGE(Id,"Customer Id");
                             if not Customer.FINDFIRST then
                               ERROR(CustomerIdDoesNotMatchACustomerErr);

                             "Customer No." := Customer."No.";
                           END;

                ImplicitType=GUID }

    { 5   ;2   ;Field     ;
                Name=paymentDate;
                CaptionML=[@@@={Locked};
                           ENU=paymentDate];
                ApplicationArea=#All;
                SourceExpr="Payment Date";
                ImplicitType=Date }

    { 6   ;2   ;Field     ;
                Name=amount;
                CaptionML=[@@@={Locked};
                           ENU=amount];
                ApplicationArea=#All;
                SourceExpr=Amount;
                OnValidate=BEGIN
                             if Amount <= 0 then
                               ERROR(AmountShouldBePositiveErr);

                             Amount := -Amount;
                           END;

                ImplicitType=Decimal }

    { 7   ;2   ;Field     ;
                Name=appliesToInvoiceId;
                CaptionML=[@@@={Locked};
                           ENU=appliesToInvoiceId];
                ApplicationArea=#All;
                SourceExpr="Applies-to Invoice Id";
                OnValidate=VAR
                             SalesInvoiceHeader@1000 : Record "Sales Invoice Header";
                           BEGIN
                             SalesInvoiceHeader.RESET;
                             SalesInvoiceHeader.SETRANGE(Id,"Applies-to Invoice Id");
                             if not SalesInvoiceHeader.FINDFIRST then
                               ERROR(AppliesToInvoiceIdDoesNotMatchAPostedInvoiceErr);

                             "Applies-to Invoice No." := SalesInvoiceHeader."No.";

                             if "Customer No." = '' then begin
                               if SalesInvoiceHeader."Bill-to Customer No." <> '' then
                                 "Customer No." := SalesInvoiceHeader."Bill-to Customer No."
                               else
                                 "Customer No." := SalesInvoiceHeader."Sell-to Customer No.";
                             end;
                           END;

                ImplicitType=GUID }

    { 8   ;2   ;Field     ;
                Name=paymentMethodId;
                CaptionML=[@@@={Locked};
                           ENU=paymentMethodId];
                ApplicationArea=#All;
                SourceExpr="Payment Method Id";
                OnValidate=VAR
                             PaymentMethod@1000 : Record "Payment Method";
                           BEGIN
                             PaymentMethod.SETRANGE(Id,"Payment Method Id");
                             if not PaymentMethod.FINDFIRST then
                               ERROR(PaymentMethodIdDoesNotMatchAPaymentMethodErr);

                             "Payment Method Code" := PaymentMethod.Code;
                           END;

                ImplicitType=GUID }

  }
  CODE
  {
    VAR
      PaymentNoShouldNotBeSpecifiedErr@1015 : TextConst 'ENU=The "paymentNo" should not be specified.';
      CustomerIdDoesNotMatchACustomerErr@1011 : TextConst '@@@={Locked};ENU=The "customerId" does not match to a Customer.';
      AppliesToInvoiceIdDoesNotMatchAPostedInvoiceErr@1010 : TextConst '@@@={Locked};ENU=The "appliesToInvoiceId" should be the ID of an Open, Paid, Corrective, or Canceled Invoice.';
      PaymentMethodIdDoesNotMatchAPaymentMethodErr@1009 : TextConst '@@@={Locked};ENU=The "paymentMethodId" does not match to a Payment Method.';
      AmountShouldBePositiveErr@1001 : TextConst 'ENU=The"amount" should be a positive number.';
      AppliesToInvoiceIdShouldBeSpecifiedErr@1005 : TextConst 'ENU=The "appliesToInvoiceId" should be specified.';
      PaymentMethodIdShouldBeSpecifiedErr@1000 : TextConst 'ENU=The "paymentMethodId" should be specified.';
      AmountExceedsRemainingAmountErr@1003 : TextConst 'ENU=The "amount" exceeds the remaining amount of the invoice.';
      AmountExceedsRemainingDiscountAmountErr@1002 : TextConst 'ENU=The "amount" exceeds the remaining amount after discount of the invoice.';
      TempNativePayment@1014 : TEMPORARY Record "Native - Payment";
      NativePayments@1017 : Codeunit "Native - Payments";
      PaymentsLoaded@1006 : Boolean;
      CanOnlyCancelLastPaymentErr@1012 : TextConst 'ENU=Only the last payment can be canceled.';

    LOCAL PROCEDURE CheckNecessaryFields@4();
    VAR
      BlankGUID@1000 : GUID;
    BEGIN
      if "Applies-to Invoice Id" = BlankGUID then
        ERROR(AppliesToInvoiceIdShouldBeSpecifiedErr);

      if "Payment Method Id" = BlankGUID then
        ERROR(PaymentMethodIdShouldBeSpecifiedErr);
    END;

    PROCEDURE CheckIfAmountExceedsRemainingAmount@1(PaymentAmount@1000 : Decimal;InvoiceNumber@1004 : Code[20];PostingDate@1005 : Date);
    VAR
      CustLedgerEntry@1001 : Record "Cust. Ledger Entry";
      OriginalRemainingAmount@1002 : Decimal;
      RemAmountAfterDiscount@1003 : Decimal;
      PmtDiscountDate@1006 : Date;
    BEGIN
      CustLedgerEntry.SETRANGE("Document No.",InvoiceNumber);
      CustLedgerEntry.FINDFIRST;
      CustLedgerEntry.CALCFIELDS("Remaining Amount");
      OriginalRemainingAmount := CustLedgerEntry."Remaining Amount";
      RemAmountAfterDiscount := OriginalRemainingAmount - CustLedgerEntry."Remaining Pmt. Disc. Possible";
      PmtDiscountDate := CustLedgerEntry."Pmt. Discount Date";

      if ABS(PaymentAmount) > ABS(OriginalRemainingAmount) then
        ERROR(AmountExceedsRemainingAmountErr);

      if (PostingDate <= PmtDiscountDate) and
         (ABS(PaymentAmount) > RemAmountAfterDiscount)
      then
        ERROR(AmountExceedsRemainingDiscountAmountErr);
    END;

    BEGIN
    END.
  }
}

