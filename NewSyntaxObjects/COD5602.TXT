OBJECT Codeunit 5602 FA Get G/L Account No.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      FAPostingGr@1000 : Record "FA Posting Group";
      GLAccNo@1001 : Code[20];

    [External]
    PROCEDURE GetAccNo@1(VAR FALedgEntry@1000 : Record "FA Ledger Entry") : Code[20];
    BEGIN
      with FALedgEntry do begin
        FAPostingGr.GET("FA Posting Group");
        GLAccNo := '';
        if "FA Posting Category" = "FA Posting Category"::" " then
          case "FA Posting Type" of
            "FA Posting Type"::"Acquisition Cost":
              GLAccNo := FAPostingGr.GetAcquisitionCostAccount;
            "FA Posting Type"::Depreciation:
              GLAccNo := FAPostingGr.GetAccumDepreciationAccount;
            "FA Posting Type"::"Write-Down":
              GLAccNo := FAPostingGr.GetWriteDownAccount;
            "FA Posting Type"::Appreciation:
              GLAccNo := FAPostingGr.GetAppreciationAccount;
            "FA Posting Type"::"Custom 1":
              GLAccNo := FAPostingGr.GetCustom1Account;
            "FA Posting Type"::"Custom 2":
              GLAccNo := FAPostingGr.GetCustom2Account;
            "FA Posting Type"::"Proceeds on Disposal":
              GLAccNo := FAPostingGr.GetSalesAccountOnDisposalGain;
            "FA Posting Type"::"Gain/Loss":
              begin
                if "Result on Disposal" = "Result on Disposal"::Gain then
                  GLAccNo := FAPostingGr.GetGainsAccountOnDisposal;
                if "Result on Disposal" = "Result on Disposal"::Loss then
                  GLAccNo := FAPostingGr.GetLossesAccountOnDisposal;
              end;
          end;

        if "FA Posting Category" = "FA Posting Category"::Disposal then
          case "FA Posting Type" of
            "FA Posting Type"::"Acquisition Cost":
              GLAccNo := FAPostingGr.GetAcquisitionCostAccountOnDisposal;
            "FA Posting Type"::Depreciation:
              GLAccNo := FAPostingGr.GetAccumDepreciationAccountOnDisposal;
            "FA Posting Type"::"Write-Down":
              GLAccNo := FAPostingGr.GetWriteDownAccountOnDisposal;
            "FA Posting Type"::Appreciation:
              GLAccNo := FAPostingGr.GetAppreciationAccountOnDisposal;
            "FA Posting Type"::"Custom 1":
              GLAccNo := FAPostingGr.GetCustom1AccountOnDisposal;
            "FA Posting Type"::"Custom 2":
              GLAccNo := FAPostingGr.GetCustom2AccountOnDisposal;
            "FA Posting Type"::"Book Value on Disposal":
              begin
                if "Result on Disposal" = "Result on Disposal"::Gain then
                  GLAccNo := FAPostingGr.GetBookValueAccountOnDisposalGain;
                if "Result on Disposal" = "Result on Disposal"::Loss then
                  GLAccNo := FAPostingGr.GetBookValueAccountOnDisposalLoss;
                "Result on Disposal" := "Result on Disposal"::" ";
              end;
          end;

        if "FA Posting Category" = "FA Posting Category"::"Bal. Disposal" then
          case "FA Posting Type" of
            "FA Posting Type"::"Write-Down":
              GLAccNo := FAPostingGr.GetWriteDownBalAccountOnDisposal;
            "FA Posting Type"::Appreciation:
              GLAccNo := FAPostingGr.GetAppreciationBalAccountOnDisposal;
            "FA Posting Type"::"Custom 1":
              GLAccNo := FAPostingGr.GetCustom1BalAccountOnDisposal;
            "FA Posting Type"::"Custom 2":
              GLAccNo := FAPostingGr.GetCustom2BalAccountOnDisposal;
          end;
      end;
      exit(GLAccNo);
    END;

    [External]
    PROCEDURE GetMaintenanceAccNo@3(VAR MaintenanceLedgEntry@1000 : Record "Maintenance Ledger Entry") : Code[20];
    BEGIN
      FAPostingGr.GET(MaintenanceLedgEntry."FA Posting Group");
      exit(FAPostingGr.GetMaintenanceExpenseAccount);
    END;

    BEGIN
    END.
  }
}

