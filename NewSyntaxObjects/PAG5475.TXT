OBJECT Page 5475 Sales Invoice Entity
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=salesInvoices];
    SourceTable="Sales Invoice Entity Aggregate";
    DelayedInsert=true;
    PageType=API;
    OnOpenPage=BEGIN
                 SetPermissionFilters;
               END;

    OnAfterGetRecord=VAR
                       SalesInvoiceAggregator@1001 : Codeunit "Sales Invoice Aggregator";
                     BEGIN
                       SetCalculatedFields;
                       SalesInvoiceAggregator.RedistributeInvoiceDiscounts(Rec);
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     SalesInvoiceAggregator@1002 : Codeunit "Sales Invoice Aggregator";
                   BEGIN
                     CheckCustomerSpecified;
                     ProcessBillingPostalAddress;

                     SalesInvoiceAggregator.PropagateOnInsert(Rec,TempFieldBuffer);
                     SetDates;

                     UpdateDiscount;

                     SetCalculatedFields;

                     exit(false);
                   END;

    OnModifyRecord=VAR
                     SalesInvoiceAggregator@1002 : Codeunit "Sales Invoice Aggregator";
                   BEGIN
                     if xRec.Id <> Id then
                       ERROR(CannotChangeIDErr);

                     ProcessBillingPostalAddress;

                     SalesInvoiceAggregator.PropagateOnModify(Rec,TempFieldBuffer);
                     UpdateDiscount;

                     SetCalculatedFields;

                     exit(false);
                   END;

    OnDeleteRecord=VAR
                     SalesInvoiceAggregator@1000 : Codeunit "Sales Invoice Aggregator";
                   BEGIN
                     SalesInvoiceAggregator.PropagateOnDelete(Rec);

                     exit(false);
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 21  ;0   ;Container ;
                Name=Container21;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 19  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=id];
                ApplicationArea=#All;
                SourceExpr=Id;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Id));
                           END;

                ImplicitType=GUID }

    { 18  ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number];
                ApplicationArea=#All;
                SourceExpr="No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("No."));
                           END;

                ImplicitType=Code20 }

    { 23  ;2   ;Field     ;
                Name=externalDocumentNumber;
                CaptionML=[@@@={Locked};
                           ENU=externalDocumentNumber];
                ApplicationArea=#All;
                SourceExpr="External Document No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("External Document No."));
                           END;

                ImplicitType=Code35 }

    { 17  ;2   ;Field     ;
                Name=invoiceDate;
                CaptionML=[@@@={Locked};
                           ENU=invoiceDate];
                ApplicationArea=#All;
                SourceExpr="Document Date";
                OnValidate=BEGIN
                             DocumentDateVar := "Document Date";
                             DocumentDateSet := true;

                             RegisterFieldSet(FIELDNO("Document Date"));
                           END;

                ImplicitType=Date }

    { 31  ;2   ;Field     ;
                Name=dueDate;
                CaptionML=[@@@={Locked};
                           ENU=dueDate];
                ApplicationArea=#All;
                SourceExpr="Due Date";
                OnValidate=BEGIN
                             DueDateVar := "Due Date";
                             DueDateSet := true;

                             RegisterFieldSet(FIELDNO("Due Date"));
                           END;

                ImplicitType=Date }

    { 16  ;2   ;Field     ;
                Name=customerPurchaseOrderReference;
                CaptionML=[@@@={Locked};
                           ENU=customerPurchaseOrderReference];
                ApplicationArea=#All;
                SourceExpr="Your Reference";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Your Reference"));
                           END;

                ImplicitType=Text35 }

    { 14  ;2   ;Field     ;
                Name=customerId;
                CaptionML=[@@@={Locked};
                           ENU=customerId];
                ApplicationArea=#All;
                SourceExpr="Customer Id";
                OnValidate=BEGIN
                             Customer.SETRANGE(Id,"Customer Id");
                             if not Customer.FINDFIRST then
                               ERROR(CouldNotFindCustomerErr);

                             "Sell-to Customer No." := Customer."No.";
                             RegisterFieldSet(FIELDNO("Customer Id"));
                             RegisterFieldSet(FIELDNO("Sell-to Customer No."));
                           END;

                ImplicitType=GUID }

    { 25  ;2   ;Field     ;
                Name=contactId;
                CaptionML=[@@@={Locked};
                           ENU=contactId];
                ApplicationArea=#All;
                SourceExpr="Contact Graph Id";
                OnValidate=VAR
                             Contact@1000 : Record Contact;
                             Customer@1001 : Record Customer;
                             GraphIntContact@1003 : Codeunit "Graph Int. - Contact";
                           BEGIN
                             RegisterFieldSet(FIELDNO("Contact Graph Id"));

                             if "Contact Graph Id" = '' then
                               ERROR(ContactIdHasToHaveValueErr);

                             if not GraphIntContact.FindOrCreateCustomerFromGraphContactSafe("Contact Graph Id",Customer,Contact) then
                               exit;

                             UpdateCustomerFromGraphContactId(Customer);

                             if Contact."Company No." = Customer."No." then begin
                               VALIDATE("Sell-to Contact No.",Contact."No.");
                               VALIDATE("Sell-to Contact",Contact.Name);

                               RegisterFieldSet(FIELDNO("Sell-to Contact No."));
                               RegisterFieldSet(FIELDNO("Sell-to Contact"));
                             end;
                           END;

                ImplicitType=Text250 }

    { 12  ;2   ;Field     ;
                Name=customerNumber;
                CaptionML=[@@@={Locked};
                           ENU=customerNumber];
                ApplicationArea=#All;
                SourceExpr="Sell-to Customer No.";
                OnValidate=BEGIN
                             if Customer."No." <> '' then begin
                               if Customer."No." <> "Sell-to Customer No." then
                                 ERROR(CustomerValuesDontMatchErr);
                               exit;
                             end;

                             if not Customer.GET("Sell-to Customer No.") then
                               ERROR(CouldNotFindCustomerErr);

                             "Customer Id" := Customer.Id;
                             RegisterFieldSet(FIELDNO("Customer Id"));
                             RegisterFieldSet(FIELDNO("Sell-to Customer No."));
                           END;

                ImplicitType=Code20 }

    { 15  ;2   ;Field     ;
                Name=customerName;
                CaptionML=[@@@={Locked};
                           ENU=customerName];
                ApplicationArea=#All;
                SourceExpr="Sell-to Customer Name";
                Editable=FALSE;
                ImplicitType=Text50 }

    { 6   ;2   ;Field     ;
                Name=billingPostalAddress;
                CaptionML=[@@@={Locked};
                           ENU=billingPostalAddress];
                ToolTipML=ENU=Specifies the billing address of the Sales Invoice.;
                ApplicationArea=#All;
                SourceExpr=BillingPostalAddressJSONText;
                OnValidate=BEGIN
                             BillingPostalAddressSet := true;
                           END;

                ODataEDMType=POSTALADDRESS;
                ImplicitType=Text }

    { 27  ;2   ;Field     ;
                Name=currencyId;
                CaptionML=[@@@={Locked};
                           ENU=CurrencyId];
                ApplicationArea=#All;
                SourceExpr="Currency Id";
                OnValidate=BEGIN
                             if "Currency Id" = BlankGUID then
                               "Currency Code" := ''
                             else begin
                               Currency.SETRANGE(Id,"Currency Id");
                               if not Currency.FINDFIRST then
                                 ERROR(CurrencyIdDoesNotMatchACurrencyErr);

                               "Currency Code" := Currency.Code;
                             end;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;

                ImplicitType=GUID }

    { 22  ;2   ;Field     ;
                Name=currencyCode;
                CaptionML=[@@@={Locked};
                           ENU=currencyCode];
                ApplicationArea=#All;
                SourceExpr=CurrencyCodeTxt;
                OnValidate=BEGIN
                             "Currency Code" :=
                               GraphMgtGeneralTools.TranslateCurrencyCodeToNAVCurrencyCode(
                                 LCYCurrencyCode,COPYSTR(CurrencyCodeTxt,1,MAXSTRLEN(LCYCurrencyCode)));

                             if Currency.Code <> '' then begin
                               if Currency.Code <> "Currency Code" then
                                 ERROR(CurrencyValuesDontMatchErr);
                               exit;
                             end;

                             if "Currency Code" = '' then
                               "Currency Id" := BlankGUID
                             else begin
                               if not Currency.GET("Currency Code") then
                                 ERROR(CurrencyCodeDoesNotMatchACurrencyErr);

                               "Currency Id" := Currency.Id;
                             end;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;

                ImplicitType=Text }

    { 26  ;2   ;Field     ;
                Name=orderId;
                CaptionML=[@@@={Locked};
                           ENU=orderId];
                ApplicationArea=#All;
                SourceExpr="Order Id";
                Editable=False;
                ImplicitType=GUID }

    { 24  ;2   ;Field     ;
                Name=orderNumber;
                CaptionML=[@@@={Locked};
                           ENU=orderNumber];
                ApplicationArea=#All;
                SourceExpr="Order No.";
                Editable=False;
                ImplicitType=Code20 }

    { 30  ;2   ;Field     ;
                Name=paymentTermsId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentTermsId];
                ApplicationArea=#All;
                SourceExpr="Payment Terms Id";
                OnValidate=BEGIN
                             if "Payment Terms Id" = BlankGUID then
                               "Payment Terms Code" := ''
                             else begin
                               PaymentTerms.SETRANGE(Id,"Payment Terms Id");
                               if not PaymentTerms.FINDFIRST then
                                 ERROR(PaymentTermsIdDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Code" := PaymentTerms.Code;
                             end;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;

                ImplicitType=GUID }

    { 29  ;2   ;Field     ;
                Name=paymentTerms;
                CaptionML=[@@@={Locked};
                           ENU=paymentTerms];
                ApplicationArea=#All;
                SourceExpr="Payment Terms Code";
                OnValidate=BEGIN
                             if PaymentTerms.Code <> '' then begin
                               if PaymentTerms.Code <> "Payment Terms Code" then
                                 ERROR(PaymentTermsValuesDontMatchErr);
                               exit;
                             end;

                             if "Payment Terms Code" = '' then
                               "Payment Terms Id" := BlankGUID
                             else begin
                               if not PaymentTerms.GET("Payment Terms Code") then
                                 ERROR(PaymentTermsCodeDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Id" := PaymentTerms.Id;
                             end;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;

                ImplicitType=Code10 }

    { 5   ;2   ;Field     ;
                Name=shipmentMethodId;
                CaptionML=[@@@={Locked};
                           ENU=ShipmentMethodId];
                ApplicationArea=#All;
                SourceExpr="Shipment Method Id";
                OnValidate=BEGIN
                             if "Shipment Method Id" = BlankGUID then
                               "Shipment Method Code" := ''
                             else begin
                               ShipmentMethod.SETRANGE(Id,"Shipment Method Id");
                               if not ShipmentMethod.FINDFIRST then
                                 ERROR(ShipmentMethodIdDoesNotMatchAShipmentMethodErr);

                               "Shipment Method Code" := ShipmentMethod.Code;
                             end;

                             RegisterFieldSet(FIELDNO("Shipment Method Id"));
                             RegisterFieldSet(FIELDNO("Shipment Method Code"));
                           END;

                ImplicitType=GUID }

    { 4   ;2   ;Field     ;
                Name=shipmentMethod;
                CaptionML=[@@@={Locked};
                           ENU=shipmentMethod];
                ApplicationArea=#All;
                SourceExpr="Shipment Method Code";
                OnValidate=BEGIN
                             if ShipmentMethod.Code <> '' then begin
                               if ShipmentMethod.Code <> "Shipment Method Code" then
                                 ERROR(ShipmentMethodValuesDontMatchErr);
                               exit;
                             end;

                             if "Shipment Method Code" = '' then
                               "Shipment Method Id" := BlankGUID
                             else begin
                               if not ShipmentMethod.GET("Shipment Method Code") then
                                 ERROR(ShipmentMethodCodeDoesNotMatchAShipmentMethodErr);

                               "Shipment Method Id" := ShipmentMethod.Id;
                             end;

                             RegisterFieldSet(FIELDNO("Shipment Method Id"));
                             RegisterFieldSet(FIELDNO("Shipment Method Code"));
                           END;

                ImplicitType=Code10 }

    { 3   ;2   ;Field     ;
                Name=salesperson;
                CaptionML=[@@@={Locked};
                           ENU=salesperson];
                ApplicationArea=#All;
                SourceExpr="Salesperson Code";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Salesperson Code"));
                           END;

                ImplicitType=Code20 }

    { 28  ;2   ;Field     ;
                Name=pricesIncludeTax;
                CaptionML=[@@@={Locked};
                           ENU=pricesIncludeTax];
                ApplicationArea=#All;
                SourceExpr="Prices Including VAT";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Prices Including VAT"));
                           END;

                ImplicitType=Boolean }

    { 1   ;2   ;Part      ;
                Name=salesInvoiceLines;
                CaptionML=[@@@={Locked};
                           ENU=Lines];
                ApplicationArea=#All;
                SubPageLink="Document Id"=FIELD(Id);
                PagePartID="Sales Invoice Line Entity";
                PartType=Page }

    { 11  ;2   ;Field     ;
                Name=discountAmount;
                CaptionML=[@@@={Locked};
                           ENU=discountAmount];
                ApplicationArea=#All;
                SourceExpr="Invoice Discount Amount";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Invoice Discount Amount"));
                             InvoiceDiscountAmount := "Invoice Discount Amount";
                             DiscountAmountSet := true;
                           END;

                ImplicitType=Decimal }

    { 10  ;2   ;Field     ;
                Name=discountAppliedBeforeTax;
                CaptionML=[@@@={Locked};
                           ENU=discountAppliedBeforeTax];
                ApplicationArea=#All;
                SourceExpr="Discount Applied Before Tax";
                Editable=FALSE;
                ImplicitType=Boolean }

    { 9   ;2   ;Field     ;
                Name=totalAmountExcludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalAmountExcludingTax];
                ApplicationArea=#All;
                SourceExpr=Amount;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 8   ;2   ;Field     ;
                Name=totalTaxAmount;
                CaptionML=[@@@={Locked};
                           ENU=totalTaxAmount];
                ToolTipML=ENU=Specifies the total tax amount for the sales invoice.;
                ApplicationArea=#All;
                SourceExpr="Total Tax Amount";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Total Tax Amount"));
                           END;

                ImplicitType=Decimal }

    { 7   ;2   ;Field     ;
                Name=totalAmountIncludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalAmountIncludingTax];
                ApplicationArea=#All;
                SourceExpr="Amount Including VAT";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Amount Including VAT"));
                           END;

                ImplicitType=Decimal }

    { 13  ;2   ;Field     ;
                Name=status;
                CaptionML=[@@@={Locked};
                           ENU=status];
                ToolTipML=ENU=Specifies the status of the Sales Invoice (cancelled, paid, on hold, created).;
                ApplicationArea=#All;
                SourceExpr=Status;
                Editable=FALSE;
                ImplicitType=Option }

    { 2   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=lastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time";
                Editable=FALSE;
                ImplicitType=DateTime }

  }
  CODE
  {
    VAR
      CannotChangeIDErr@1004 : TextConst '@@@={Locked};ENU=The id cannot be changed.';
      TempFieldBuffer@1001 : TEMPORARY Record "Field Buffer";
      Customer@1003 : Record Customer;
      Currency@1014 : Record Currency;
      PaymentTerms@1016 : Record "Payment Terms";
      ShipmentMethod@1017 : Record "Shipment Method";
      GraphMgtGeneralTools@1009 : Codeunit "Graph Mgt - General Tools";
      LCYCurrencyCode@1008 : Code[10];
      CurrencyCodeTxt@1006 : Text;
      BillingPostalAddressJSONText@1002 : Text;
      BillingPostalAddressSet@1000 : Boolean;
      CustomerNotProvidedErr@1010 : TextConst '@@@={Locked};ENU=A customerNumber or a customerId must be provided.';
      CustomerValuesDontMatchErr@1024 : TextConst '@@@={Locked};ENU=The customer values do not match to a specific Customer.';
      CouldNotFindCustomerErr@1005 : TextConst '@@@={Locked};ENU=The customer cannot be found.';
      ContactIdHasToHaveValueErr@1007 : TextConst '@@@={Locked};ENU=Contact Id must have a value set.';
      CurrencyValuesDontMatchErr@1013 : TextConst '@@@={Locked};ENU=The currency values do not match to a specific Currency.';
      CurrencyIdDoesNotMatchACurrencyErr@1012 : TextConst '@@@={Locked};ENU=The "currencyId" does not match to a Currency.';
      CurrencyCodeDoesNotMatchACurrencyErr@1011 : TextConst '@@@={Locked};ENU=The "currencyCode" does not match to a Currency.';
      BlankGUID@1015 : GUID;
      PaymentTermsValuesDontMatchErr@1023 : TextConst '@@@={Locked};ENU=The payment terms values do not match to a specific Payment Terms.';
      PaymentTermsIdDoesNotMatchAPaymentTermsErr@1022 : TextConst '@@@={Locked};ENU=The "paymentTermsId" does not match to a Payment Terms.';
      PaymentTermsCodeDoesNotMatchAPaymentTermsErr@1021 : TextConst '@@@={Locked};ENU=The "paymentTermsCode" does not match to a Payment Terms.';
      ShipmentMethodValuesDontMatchErr@1020 : TextConst '@@@={Locked};ENU=The shipment method values do not match to a specific Shipment Method.';
      ShipmentMethodIdDoesNotMatchAShipmentMethodErr@1019 : TextConst '@@@={Locked};ENU=The "shipmentMethodId" does not match to a Shipment Method.';
      ShipmentMethodCodeDoesNotMatchAShipmentMethodErr@1018 : TextConst '@@@={Locked};ENU=The "shipmentMethodCode" does not match to a Shipment Method.';
      DiscountAmountSet@1025 : Boolean;
      InvoiceDiscountAmount@1026 : Decimal;
      DocumentDateSet@1030 : Boolean;
      DocumentDateVar@1029 : Date;
      DueDateSet@1028 : Boolean;
      DueDateVar@1027 : Date;
      DraftInvoiceActionErr@1031 : TextConst '@@@={Locked};ENU=The action can be applied to a draft invoice only.';
      CannotFindInvoiceErr@1032 : TextConst '@@@={Locked};ENU=The invoice cannot be found.';

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      GraphMgtSalesInvoice@1000 : Codeunit "Graph Mgt - Sales Invoice";
    BEGIN
      BillingPostalAddressJSONText := GraphMgtSalesInvoice.BillToCustomerAddressToJSON(Rec);
      CurrencyCodeTxt := GraphMgtGeneralTools.TranslateNAVCurrencyCodeToCurrencyCode(LCYCurrencyCode,"Currency Code");
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(BillingPostalAddressJSONText);
      CLEAR(InvoiceDiscountAmount);
      CLEAR(DiscountAmountSet);
      TempFieldBuffer.DELETEALL;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    VAR
      LastOrderNo@1001 : Integer;
    BEGIN
      LastOrderNo := 1;
      if TempFieldBuffer.FINDLAST then
        LastOrderNo := TempFieldBuffer.Order + 1;

      CLEAR(TempFieldBuffer);
      TempFieldBuffer.Order := LastOrderNo;
      TempFieldBuffer."Table ID" := DATABASE::"Sales Invoice Entity Aggregate";
      TempFieldBuffer."Field ID" := FieldNo;
      TempFieldBuffer.INSERT;
    END;

    LOCAL PROCEDURE CheckCustomerSpecified@1();
    BEGIN
      if ("Sell-to Customer No." = '') and
         ("Customer Id" = BlankGUID)
      then
        ERROR(CustomerNotProvidedErr);
    END;

    LOCAL PROCEDURE ProcessBillingPostalAddress@5();
    VAR
      GraphMgtSalesInvoice@1000 : Codeunit "Graph Mgt - Sales Invoice";
    BEGIN
      if not BillingPostalAddressSet then
        exit;

      GraphMgtSalesInvoice.ProcessComplexTypes(Rec,BillingPostalAddressJSONText);

      if xRec."Sell-to Address" <> "Sell-to Address" then
        RegisterFieldSet(FIELDNO("Sell-to Address"));

      if xRec."Sell-to Address 2" <> "Sell-to Address 2" then
        RegisterFieldSet(FIELDNO("Sell-to Address 2"));

      if xRec."Sell-to City" <> "Sell-to City" then
        RegisterFieldSet(FIELDNO("Sell-to City"));

      if xRec."Sell-to Country/Region Code" <> "Sell-to Country/Region Code" then
        RegisterFieldSet(FIELDNO("Sell-to Country/Region Code"));

      if xRec."Sell-to Post Code" <> "Sell-to Post Code" then
        RegisterFieldSet(FIELDNO("Sell-to Post Code"));

      if xRec."Sell-to County" <> "Sell-to County" then
        RegisterFieldSet(FIELDNO("Sell-to County"));
    END;

    LOCAL PROCEDURE UpdateCustomerFromGraphContactId@2(VAR Customer@1001 : Record Customer);
    VAR
      UpdateCustomer@1000 : Boolean;
    BEGIN
      UpdateCustomer := "Sell-to Customer No." = '';
      if not UpdateCustomer then begin
        TempFieldBuffer.RESET;
        TempFieldBuffer.SETRANGE("Field ID",FIELDNO("Customer Id"));
        UpdateCustomer := not TempFieldBuffer.FINDFIRST;
        TempFieldBuffer.RESET;
      end;

      if UpdateCustomer then begin
        VALIDATE("Customer Id",Customer.Id);
        VALIDATE("Sell-to Customer No.",Customer."No.");
        RegisterFieldSet(FIELDNO("Customer Id"));
        RegisterFieldSet(FIELDNO("Sell-to Customer No."));
      end;
    END;

    LOCAL PROCEDURE SetPermissionFilters@4();
    VAR
      SalesHeader@1001 : Record "Sales Header";
      SalesInvoiceHeader@1000 : Record "Sales Invoice Header";
      FilterText@1002 : Text;
    BEGIN
      SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Invoice);
      if not SalesHeader.READPERMISSION then
        FilterText :=
          STRSUBSTNO('<>%1&<>%2',Status::Draft,Status::"In Review");

      if not SalesInvoiceHeader.READPERMISSION then begin
        if FilterText <> '' then
          FilterText += '&';
        FilterText +=
          STRSUBSTNO(
            '<>%1&<>%2&<>%3&<>%4',Status::Canceled,Status::Corrective,
            Status::Open,Status::Paid);
      end;

      if FilterText <> '' then begin
        FILTERGROUP(2);
        SETFILTER(Status,FilterText);
        FILTERGROUP(0);
      end;
    END;

    LOCAL PROCEDURE UpdateDiscount@3();
    VAR
      SalesHeader@1002 : Record "Sales Header";
      SalesInvoiceAggregator@1001 : Codeunit "Sales Invoice Aggregator";
      SalesCalcDiscountByType@1000 : Codeunit "Sales - Calc Discount By Type";
    BEGIN
      if Posted then
        exit;

      if not DiscountAmountSet then begin
        SalesInvoiceAggregator.RedistributeInvoiceDiscounts(Rec);
        exit;
      end;

      SalesHeader.GET("Document Type"::Invoice,"No.");
      SalesCalcDiscountByType.ApplyInvDiscBasedOnAmt(InvoiceDiscountAmount,SalesHeader);
    END;

    LOCAL PROCEDURE SetDates@8();
    VAR
      SalesInvoiceAggregator@1000 : Codeunit "Sales Invoice Aggregator";
    BEGIN
      if not (DueDateSet or DocumentDateSet) then
        exit;

      TempFieldBuffer.RESET;
      TempFieldBuffer.DELETEALL;

      if DocumentDateSet then begin
        "Document Date" := DocumentDateVar;
        RegisterFieldSet(FIELDNO("Document Date"));
      end;

      if DueDateSet then begin
        "Due Date" := DueDateVar;
        RegisterFieldSet(FIELDNO("Due Date"));
      end;

      SalesInvoiceAggregator.PropagateOnModify(Rec,TempFieldBuffer);
      FIND;
    END;

    LOCAL PROCEDURE GetDraftInvoice@36(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      if Posted then
        ERROR(DraftInvoiceActionErr);

      SalesHeader.SETRANGE(Id,Id);
      if not SalesHeader.FINDFIRST then
        ERROR(CannotFindInvoiceErr);
    END;

    LOCAL PROCEDURE PostInvoice@20(VAR SalesHeader@1001 : Record "Sales Header";VAR SalesInvoiceHeader@1000 : Record "Sales Invoice Header");
    VAR
      LinesInstructionMgt@1005 : Codeunit "Lines Instruction Mgt.";
      PreAssignedNo@1006 : Code[20];
    BEGIN
      LinesInstructionMgt.SalesCheckAllLinesHaveQuantityAssigned(SalesHeader);
      PreAssignedNo := SalesHeader."No.";
      SalesHeader.SendToPosting(CODEUNIT::"Sales-Post");
      SalesInvoiceHeader.SETCURRENTKEY("Pre-Assigned No.");
      SalesInvoiceHeader.SETRANGE("Pre-Assigned No.",PreAssignedNo);
      SalesInvoiceHeader.FINDFIRST;
    END;

    LOCAL PROCEDURE SetActionResponse@47(VAR ActionContext@1004 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext";InvoiceId@1000 : GUID);
    VAR
      ODataActionManagement@1003 : Codeunit "OData Action Management";
    BEGIN
      ODataActionManagement.AddKey(FIELDNO(Id),InvoiceId);
      ODataActionManagement.SetDeleteResponseLocation(ActionContext,PAGE::"Sales Invoice Entity");
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE Post@18(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1001 : Record "Sales Header";
      SalesInvoiceHeader@1002 : Record "Sales Invoice Header";
    BEGIN
      GetDraftInvoice(SalesHeader);
      PostInvoice(SalesHeader,SalesInvoiceHeader);
      SetActionResponse(ActionContext,SalesInvoiceHeader.Id);
    END;

    BEGIN
    END.
  }
}

