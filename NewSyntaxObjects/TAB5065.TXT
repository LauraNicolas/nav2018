OBJECT Table 5065 Interaction Log Entry
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=VAR
               InteractionCommentLine@1000 : Record "Inter. Log Entry Comment Line";
               Attachment@1003 : Record Attachment;
               CampaignMgt@1001 : Codeunit "Campaign Target Group Mgt";
             BEGIN
               InteractionCommentLine.SETRANGE("Entry No.","Entry No.");
               InteractionCommentLine.DELETEALL;

               CampaignMgt.DeleteContfromTargetGr(Rec);
               if Attachment.GET("Attachment No.") then
                 Attachment.RemoveAttachment(false);
             END;

    CaptionML=ENU=Interaction Log Entry;
    LookupPageID="Interaction Log Entries";
    DrillDownPageID="Interaction Log Entries";
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No. }
    { 2   ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   CaptionML=ENU=Contact No. }
    { 3   ;   ;Contact Company No. ;Code20        ;TableRelation=Contact WHERE (Type=CONST(Company));
                                                   CaptionML=ENU=Contact Company No. }
    { 4   ;   ;Date                ;Date          ;CaptionML=ENU=Date }
    { 5   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 6   ;   ;Information Flow    ;Option        ;CaptionML=ENU=Information Flow;
                                                   OptionCaptionML=ENU=" ,Outbound,Inbound";
                                                   OptionString=" ",Outbound,Inbound }
    { 7   ;   ;Initiated By        ;Option        ;CaptionML=ENU=Initiated By;
                                                   OptionCaptionML=ENU=" ,Us,Them";
                                                   OptionString=" ",Us,Them }
    { 8   ;   ;Attachment No.      ;Integer       ;TableRelation=Attachment;
                                                   CaptionML=ENU=Attachment No. }
    { 9   ;   ;Cost (LCY)          ;Decimal       ;CaptionML=ENU=Cost (LCY);
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 10  ;   ;Duration (Min.)     ;Decimal       ;CaptionML=ENU=Duration (Min.);
                                                   DecimalPlaces=0:0;
                                                   Editable=false }
    { 11  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit "User Management";
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=false;
                                                   CaptionML=ENU=User ID }
    { 12  ;   ;Interaction Group Code;Code10      ;TableRelation="Interaction Group";
                                                   CaptionML=ENU=Interaction Group Code }
    { 13  ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=ENU=Interaction Template Code }
    { 14  ;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   CaptionML=ENU=Campaign No. }
    { 15  ;   ;Campaign Entry No.  ;Integer       ;TableRelation="Campaign Entry" WHERE ("Campaign No."=FIELD("Campaign No."));
                                                   CaptionML=ENU=Campaign Entry No. }
    { 16  ;   ;Campaign Response   ;Boolean       ;CaptionML=ENU=Campaign Response }
    { 17  ;   ;Campaign Target     ;Boolean       ;CaptionML=ENU=Campaign Target }
    { 18  ;   ;Segment No.         ;Code20        ;CaptionML=ENU=Segment No. }
    { 19  ;   ;Evaluation          ;Option        ;CaptionML=ENU=Evaluation;
                                                   OptionCaptionML=ENU=" ,Very Positive,Positive,Neutral,Negative,Very Negative";
                                                   OptionString=" ","Very Positive",Positive,Neutral,Negative,"Very Negative";
                                                   Editable=false }
    { 20  ;   ;Time of Interaction ;Time          ;CaptionML=ENU=Time of Interaction }
    { 21  ;   ;Attempt Failed      ;Boolean       ;CaptionML=ENU=Attempt Failed }
    { 23  ;   ;To-do No.           ;Code20        ;TableRelation="To-do";
                                                   CaptionML=ENU=Task No. }
    { 24  ;   ;Salesperson Code    ;Code20        ;TableRelation="Salesperson/Purchaser";
                                                   CaptionML=ENU=Salesperson Code }
    { 25  ;   ;Delivery Status     ;Option        ;CaptionML=ENU=Delivery Status;
                                                   OptionCaptionML=ENU=" ,In Progress,Error";
                                                   OptionString=" ","In Progress",Error }
    { 26  ;   ;Canceled            ;Boolean       ;CaptionML=ENU=Canceled }
    { 27  ;   ;Correspondence Type ;Option        ;CaptionML=ENU=Correspondence Type;
                                                   OptionCaptionML=ENU=" ,Hard Copy,Email,Fax";
                                                   OptionString=" ","Hard Copy",Email,Fax }
    { 28  ;   ;Contact Alt. Address Code;Code10   ;TableRelation="Contact Alt. Address".Code WHERE ("Contact No."=FIELD("Contact No."));
                                                   CaptionML=ENU=Contact Alt. Address Code }
    { 29  ;   ;Logged Segment Entry No.;Integer   ;TableRelation="Logged Segment";
                                                   CaptionML=ENU=Logged Segment Entry No. }
    { 30  ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=" ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet,Sales Return Order,Sales Finance Charge Memo,Sales Return Receipt,Purch. Return Shipment,Purch. Return Ord. Cnfrmn.,Service Contract,Service Contract Quote,Service Quote";
                                                   OptionString=" ","Sales Qte.","Sales Blnkt. Ord","Sales Ord. Cnfrmn.","Sales Inv.","Sales Shpt. Note","Sales Cr. Memo","Sales Stmnt.","Sales Rmdr.","Serv. Ord. Create","Serv. Ord. Post","Purch.Qte.","Purch. Blnkt. Ord.","Purch. Ord.","Purch. Inv.","Purch. Rcpt.","Purch. Cr. Memo","Cover Sheet","Sales Return Order","Sales Finance Charge Memo","Sales Return Receipt","Purch. Return Shipment","Purch. Return Ord. Cnfrmn.","Service Contract","Service Contract Quote","Service Quote" }
    { 31  ;   ;Document No.        ;Code20        ;TestTableRelation=false;
                                                   CaptionML=ENU=Document No. }
    { 32  ;   ;Version No.         ;Integer       ;CaptionML=ENU=Version No. }
    { 33  ;   ;Doc. No. Occurrence ;Integer       ;CaptionML=ENU=Doc. No. Occurrence }
    { 34  ;   ;Contact Via         ;Text80        ;CaptionML=ENU=Contact Via }
    { 35  ;   ;Send Word Docs. as Attmt.;Boolean  ;CaptionML=ENU=Send Word Docs. as Attmt. }
    { 36  ;   ;Interaction Language Code;Code10   ;TableRelation=Language;
                                                   CaptionML=ENU=Interaction Language Code }
    { 37  ;   ;E-Mail Logged       ;Boolean       ;CaptionML=ENU=Email Logged }
    { 38  ;   ;Subject             ;Text50        ;CaptionML=ENU=Subject }
    { 39  ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE ("No."=FIELD("Contact No.")));
                                                   CaptionML=ENU=Contact Name;
                                                   Editable=false }
    { 40  ;   ;Contact Company Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE ("No."=FIELD("Contact Company No."),
                                                                                          Type=CONST(Company)));
                                                   CaptionML=ENU=Contact Company Name;
                                                   Editable=false }
    { 43  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Inter. Log Entry Comment Line" WHERE ("Entry No."=FIELD("Entry No.")));
                                                   CaptionML=ENU=Comment;
                                                   Editable=false }
    { 44  ;   ;Opportunity No.     ;Code20        ;TableRelation=Opportunity;
                                                   CaptionML=ENU=Opportunity No. }
    { 45  ;   ;Postponed           ;Boolean       ;CaptionML=ENU=Postponed }
  }
  KEYS
  {
    {    ;"Entry No."                             ;Clustered=true }
    {    ;"Contact Company No.","Contact No.",Date,Postponed;
                                                   SumIndexFields="Cost (LCY)","Duration (Min.)" }
    {    ;"Contact Company No.",Date,"Contact No.",Canceled,"Initiated By","Attempt Failed",Postponed;
                                                   SumIndexFields="Cost (LCY)","Duration (Min.)";
                                                   MaintainSIFTIndex=false }
    {    ;"Interaction Group Code",Date            }
    {    ;"Interaction Group Code",Canceled,Date,Postponed;
                                                   SumIndexFields="Cost (LCY)","Duration (Min.)" }
    {    ;"Interaction Template Code",Date         }
    {    ;"Interaction Template Code",Canceled,Date,Postponed;
                                                   SumIndexFields="Cost (LCY)","Duration (Min.)" }
    {    ;Canceled,"Campaign No.","Campaign Entry No.",Date,Postponed;
                                                   SumIndexFields="Cost (LCY)","Duration (Min.)" }
    {    ;"Campaign No.","Campaign Entry No.",Date,Postponed;
                                                   SumIndexFields="Cost (LCY)","Duration (Min.)" }
    {    ;"Salesperson Code",Date,Postponed       ;SumIndexFields="Cost (LCY)","Duration (Min.)" }
    {    ;Canceled,"Salesperson Code",Date,Postponed;
                                                   SumIndexFields="Cost (LCY)","Duration (Min.)" }
    {    ;"Logged Segment Entry No.",Postponed     }
    {    ;"Attachment No."                         }
    {    ;"To-do No.",Date                         }
    {    ;"Contact No.","Correspondence Type","E-Mail Logged",Subject,Postponed }
    {    ;"Campaign No.","Campaign Target"         }
    {    ;"Campaign No.","Contact Company No.","Campaign Target",Postponed }
    {    ;"Opportunity No.",Date                   }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;"Entry No.",Description,"Contact No.",Date }
    { 2   ;Brick               ;"Salesperson Code",Description,Date,"Contact Name","Contact Company Name" }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 is marked %3.\';
      Text001@1001 : TextConst 'ENU=Do you wish to remove the checkmark?';
      Text002@1002 : TextConst 'ENU=Do you wish to mark %1 %2 as %3?';
      Text003@1003 : TextConst 'ENU=It is not possible to view sales statements after they have been printed.';
      Text004@1004 : TextConst 'ENU=It is not possible to show cover sheets after they have been printed.';
      Text005@1005 : TextConst 'ENU=Do you wish to remove the checkmark from the selected %1 lines?';
      Text006@1006 : TextConst 'ENU=Do you wish to mark the selected %1 lines as %2?';
      Text009@1007 : TextConst 'ENU=Do you want to remove Attachment?';
      Text010@1008 : TextConst 'ENU=Do you want to remove unique Attachments for the selected lines?';
      Text011@1009 : TextConst 'ENU=Very Positive,Positive,Neutral,Negative,Very Negative';

    [External]
    PROCEDURE AssignNewOpportunity@15();
    VAR
      Opportunity@1000 : Record Opportunity;
    BEGIN
      TESTFIELD(Canceled,false);
      TESTFIELD("Opportunity No.",'');
      Opportunity.CreateFromInteractionLogEntry(Rec);
      "Opportunity No." := Opportunity."No.";
      MODIFY;
    END;

    [External]
    PROCEDURE CanCreateOpportunity@13() : Boolean;
    BEGIN
      exit(not Canceled and ("Opportunity No." = ''));
    END;

    PROCEDURE CopyFromSegment@14(SegLine@1001 : Record "Segment Line");
    BEGIN
      "Contact No." := SegLine."Contact No.";
      "Contact Company No." := SegLine."Contact Company No.";
      Date := SegLine.Date;
      Description := SegLine.Description;
      "Information Flow" := SegLine."Information Flow";
      "Initiated By" := SegLine."Initiated By";
      "Attachment No." := SegLine."Attachment No.";
      "Cost (LCY)" := SegLine."Cost (LCY)";
      "Duration (Min.)" := SegLine."Duration (Min.)";
      "User ID" := USERID;
      "Interaction Group Code" := SegLine."Interaction Group Code";
      "Interaction Template Code" := SegLine."Interaction Template Code";
      "Interaction Language Code" := SegLine."Language Code";
      Subject := SegLine.Subject;
      "Campaign No." := SegLine."Campaign No.";
      "Campaign Entry No." := SegLine."Campaign Entry No.";
      "Campaign Response" := SegLine."Campaign Response";
      "Campaign Target" := SegLine."Campaign Target";
      "Segment No." := SegLine."Segment No.";
      Evaluation := SegLine.Evaluation;
      "Time of Interaction" := SegLine."Time of Interaction";
      "Attempt Failed" := SegLine."Attempt Failed";
      "To-do No." := SegLine."To-do No.";
      "Salesperson Code" := SegLine."Salesperson Code";
      "Correspondence Type" := SegLine."Correspondence Type";
      "Contact Alt. Address Code" := SegLine."Contact Alt. Address Code";
      "Document Type" := SegLine."Document Type";
      "Document No." := SegLine."Document No.";
      "Doc. No. Occurrence" := SegLine."Doc. No. Occurrence";
      "Version No." := SegLine."Version No.";
      "Send Word Docs. as Attmt." := SegLine."Send Word Doc. As Attmt.";
      "Contact Via" := SegLine."Contact Via";
      "Opportunity No." := SegLine."Opportunity No.";
    END;

    [External]
    PROCEDURE CreateInteraction@10();
    VAR
      TempSegLine@1000 : TEMPORARY Record "Segment Line";
    BEGIN
      TempSegLine.CreateInteractionFromInteractLogEntry(Rec);
    END;

    [External]
    PROCEDURE CreateTask@8();
    VAR
      TempTask@1000 : TEMPORARY Record "To-do";
    BEGIN
      TempTask.CreateTaskFromInteractLogEntry(Rec)
    END;

    [Internal]
    PROCEDURE OpenAttachment@5();
    VAR
      Attachment@1000 : Record Attachment;
      SegLine@1001 : Record "Segment Line";
      IStream@1003 : InStream;
      EmailMessageUrl@1002 : Text;
    BEGIN
      if "Attachment No." = 0 then
        exit;
      Attachment.GET("Attachment No.");

      if Attachment."Storage Type" <> Attachment."Storage Type"::"Exchange Storage" then begin
        SegLine."Contact No." := "Contact No.";
        SegLine."Salesperson Code" := "Salesperson Code";
        SegLine."Contact Alt. Address Code" := "Contact Alt. Address Code";
        SegLine.Date := Date;
        SegLine."Campaign No." := "Campaign No.";
        SegLine."Segment No." := "Segment No.";
        SegLine."Line No." := "Entry No.";
        SegLine.Description := Description;
        SegLine.Subject := Subject;
        SegLine."Language Code" := "Interaction Language Code";
        Attachment.ShowAttachment(SegLine,FORMAT("Entry No.") + ' ' + Description,false,false);
      end else begin
        Attachment.CALCFIELDS("Email Message Url");
        if Attachment."Email Message Url".HASVALUE then begin
          Attachment."Email Message Url".CREATEINSTREAM(IStream);
          IStream.READ(EmailMessageUrl);
          HYPERLINK(EmailMessageUrl);
        end else
          Attachment.DisplayInOutlook;
      end;
    END;

    [Internal]
    PROCEDURE ToggleCanceledCheckmark@7();
    VAR
      ErrorTxt@1004 : Text[80];
      MasterCanceledCheckmark@1001 : Boolean;
      RemoveUniqueAttachment@1000 : Boolean;
    BEGIN
      if FIND('-') then begin
        if ConfirmToggleCanceledCheckmark(COUNT,ErrorTxt) then begin
          MasterCanceledCheckmark := not Canceled;
          if FindUniqueAttachment and MasterCanceledCheckmark then
            RemoveUniqueAttachment := CONFIRM(ErrorTxt,false);
          SETCURRENTKEY("Entry No.");
          if FIND('-') then
            repeat
              SetCanceledCheckmark(MasterCanceledCheckmark,RemoveUniqueAttachment);
            until NEXT = 0
        end;
      end
    END;

    [Internal]
    PROCEDURE SetCanceledCheckmark@2(CanceledCheckmark@1003 : Boolean;RemoveUniqueAttachment@1004 : Boolean);
    VAR
      CampaignEntry@1000 : Record "Campaign Entry";
      LoggedSeg@1001 : Record "Logged Segment";
      Attachment@1002 : Record Attachment;
    BEGIN
      if Canceled and not CanceledCheckmark then begin
        if "Logged Segment Entry No." <> 0 then begin
          LoggedSeg.GET("Logged Segment Entry No.");
          LoggedSeg.TESTFIELD(Canceled,false);
        end;
        if "Campaign Entry No." <> 0 then begin
          CampaignEntry.GET("Campaign Entry No.");
          CampaignEntry.TESTFIELD(Canceled,false);
        end;
      end;

      if not Canceled and CanceledCheckmark then
        if UniqueAttachment and RemoveUniqueAttachment then begin
          if Attachment.GET("Attachment No.") then
            Attachment.RemoveAttachment(false);
          "Attachment No." := 0;
        end;

      Canceled := CanceledCheckmark;
      MODIFY;
    END;

    LOCAL PROCEDURE ConfirmToggleCanceledCheckmark@3(NumberOfSelectedLines@1000 : Integer;VAR ErrorTxt@1001 : Text[80]) : Boolean;
    BEGIN
      if NumberOfSelectedLines = 1 then begin
        ErrorTxt := Text009;
        if Canceled then
          exit(CONFIRM(
              Text000 +
              Text001,true,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));

        exit(CONFIRM(
            Text002,true,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));
      end;
      ErrorTxt := Text010;
      if Canceled then
        exit(CONFIRM(
            Text005,true,TABLECAPTION));

      exit(CONFIRM(
          Text006,true,TABLECAPTION,FIELDCAPTION(Canceled)));
    END;

    [External]
    PROCEDURE UniqueAttachment@4() IsUnique@1000 : Boolean;
    VAR
      InteractLogEntry@1001 : Record "Interaction Log Entry";
    BEGIN
      if "Attachment No." <> 0 then begin
        InteractLogEntry.SETCURRENTKEY("Attachment No.");
        InteractLogEntry.SETRANGE("Attachment No.","Attachment No.");
        InteractLogEntry.SETFILTER("Entry No.",'<>%1',"Entry No.");
        IsUnique := not InteractLogEntry.FINDFIRST;
      end;
    END;

    LOCAL PROCEDURE FindUniqueAttachment@6() IsUnique@1000 : Boolean;
    BEGIN
      if FIND('-') then
        repeat
          IsUnique := UniqueAttachment;
        until (NEXT = 0) or IsUnique
    END;

    [External]
    PROCEDURE ShowDocument@1();
    VAR
      SalesHeader@1000 : Record "Sales Header";
      SalesHeaderArchive@1011 : Record "Sales Header Archive";
      SalesInvHeader@1001 : Record "Sales Invoice Header";
      SalesShptHeader@1002 : Record "Sales Shipment Header";
      SalesCrMemoHeader@1003 : Record "Sales Cr.Memo Header";
      IssuedReminderHeader@1004 : Record "Issued Reminder Header";
      PurchHeader@1005 : Record "Purchase Header";
      PurchHeaderArchive@1012 : Record "Purchase Header Archive";
      PurchInvHeader@1006 : Record "Purch. Inv. Header";
      PurchRcptHeader@1007 : Record "Purch. Rcpt. Header";
      PurchCrMemoHeader@1008 : Record "Purch. Cr. Memo Hdr.";
      ServHeader@1009 : Record "Service Header";
      ReturnRcptHeader@1013 : Record "Return Receipt Header";
      IssuedFinChargeMemoHeader@1015 : Record "Issued Fin. Charge Memo Header";
      ReturnReceiptHeader@1014 : Record "Return Receipt Header";
      ReturnShipmentHeader@1016 : Record "Return Shipment Header";
      ServiceContractHeader@1017 : Record "Service Contract Header";
    BEGIN
      case "Document Type" of
        "Document Type"::"Sales Qte.":
          if "Version No." <> 0 then begin
            SalesHeaderArchive.GET(
              SalesHeaderArchive."Document Type"::Quote,"Document No.",
              "Doc. No. Occurrence","Version No.");
            SalesHeaderArchive.SETRANGE("Document Type",SalesHeaderArchive."Document Type"::Quote);
            SalesHeaderArchive.SETRANGE("No.","Document No.");
            SalesHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Sales Quote Archive",SalesHeaderArchive);
          end else begin
            SalesHeader.GET(SalesHeader."Document Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Sales Quote",SalesHeader);
          end;
        "Document Type"::"Sales Blnkt. Ord":
          begin
            SalesHeader.GET(SalesHeader."Document Type"::"Blanket Order","Document No.");
            PAGE.RUN(PAGE::"Blanket Sales Order",SalesHeader);
          end;
        "Document Type"::"Sales Ord. Cnfrmn.":
          if "Version No." <> 0 then begin
            SalesHeaderArchive.GET(
              SalesHeaderArchive."Document Type"::Order,"Document No.",
              "Doc. No. Occurrence","Version No.");
            SalesHeaderArchive.SETRANGE("Document Type",SalesHeaderArchive."Document Type"::Order);
            SalesHeaderArchive.SETRANGE("No.","Document No.");
            SalesHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Sales Order Archive",SalesHeaderArchive);
          end else begin
            SalesHeader.GET(SalesHeader."Document Type"::Order,"Document No.");
            PAGE.RUN(PAGE::"Sales Order",SalesHeader);
          end;
        "Document Type"::"Sales Inv.":
          begin
            SalesInvHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Sales Invoice",SalesInvHeader);
          end;
        "Document Type"::"Sales Shpt. Note":
          begin
            SalesShptHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Sales Shipment",SalesShptHeader);
          end;
        "Document Type"::"Sales Cr. Memo":
          begin
            SalesCrMemoHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Sales Credit Memo",SalesCrMemoHeader);
          end;
        "Document Type"::"Sales Stmnt.":
          ERROR(Text003);
        "Document Type"::"Sales Rmdr.":
          begin
            IssuedReminderHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Issued Reminder",IssuedReminderHeader);
          end;
        "Document Type"::"Serv. Ord. Create":
          begin
            ServHeader.GET(ServHeader."Document Type"::Order,"Document No.");
            PAGE.RUN(PAGE::"Service Order",ServHeader)
          end;
        "Document Type"::"Purch.Qte.":
          if "Version No." <> 0 then begin
            PurchHeaderArchive.GET(
              PurchHeaderArchive."Document Type"::Quote,"Document No.",
              "Doc. No. Occurrence","Version No.");
            PurchHeaderArchive.SETRANGE("Document Type",PurchHeaderArchive."Document Type"::Quote);
            PurchHeaderArchive.SETRANGE("No.","Document No.");
            PurchHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Purchase Quote Archive",PurchHeaderArchive);
          end else begin
            PurchHeader.GET(PurchHeader."Document Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Purchase Quote",PurchHeader);
          end;
        "Document Type"::"Purch. Blnkt. Ord.":
          begin
            PurchHeader.GET(PurchHeader."Document Type"::"Blanket Order","Document No.");
            PAGE.RUN(PAGE::"Blanket Purchase Order",PurchHeader);
          end;
        "Document Type"::"Purch. Ord.":
          if "Version No." <> 0 then begin
            PurchHeaderArchive.GET(
              PurchHeaderArchive."Document Type"::Order,"Document No.",
              "Doc. No. Occurrence","Version No.");
            PurchHeaderArchive.SETRANGE("Document Type",PurchHeaderArchive."Document Type"::Order);
            PurchHeaderArchive.SETRANGE("No.","Document No.");
            PurchHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Purchase Order Archive",PurchHeaderArchive);
          end else begin
            PurchHeader.GET(PurchHeader."Document Type"::Order,"Document No.");
            PAGE.RUN(PAGE::"Purchase Order",PurchHeader);
          end;
        "Document Type"::"Purch. Inv.":
          begin
            PurchInvHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Purchase Invoice",PurchInvHeader);
          end;
        "Document Type"::"Purch. Rcpt.":
          begin
            PurchRcptHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Purchase Receipt",PurchRcptHeader);
          end;
        "Document Type"::"Purch. Cr. Memo":
          begin
            PurchCrMemoHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Purchase Credit Memo",PurchCrMemoHeader);
          end;
        "Document Type"::"Cover Sheet":
          ERROR(Text004);
        "Document Type"::"Sales Return Order":
          if SalesHeader.GET(SalesHeader."Document Type"::"Return Order","Document No.") then
            PAGE.RUN(PAGE::"Sales Return Order",SalesHeader)
          else begin
            ReturnRcptHeader.SETRANGE("Return Order No.","Document No.");
            PAGE.RUN(PAGE::"Posted Return Receipt",ReturnRcptHeader);
          end;
        "Document Type"::"Sales Finance Charge Memo":
          begin
            IssuedFinChargeMemoHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Issued Finance Charge Memo",IssuedFinChargeMemoHeader);
          end;
        "Document Type"::"Sales Return Receipt":
          begin
            ReturnReceiptHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Return Receipt",ReturnReceiptHeader);
          end;
        "Document Type"::"Purch. Return Shipment":
          begin
            ReturnShipmentHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Return Shipment",ReturnShipmentHeader);
          end;
        "Document Type"::"Purch. Return Ord. Cnfrmn.":
          if PurchHeader.GET(PurchHeader."Document Type"::"Return Order","Document No.") then
            PAGE.RUN(PAGE::"Purchase Return Order",PurchHeader)
          else begin
            ReturnShipmentHeader.SETRANGE("Return Order No.","Document No.");
            PAGE.RUN(PAGE::"Posted Return Shipment",ReturnShipmentHeader);
          end;
        "Document Type"::"Service Contract":
          begin
            ServiceContractHeader.GET(ServiceContractHeader."Contract Type"::Contract,"Document No.");
            PAGE.RUN(PAGE::"Service Contract",ServiceContractHeader);
          end;
        "Document Type"::"Service Contract Quote":
          begin
            ServiceContractHeader.GET(ServiceContractHeader."Contract Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Service Contract Quote",ServiceContractHeader);
          end;
        "Document Type"::"Service Quote":
          begin
            ServHeader.GET(ServHeader."Document Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Service Quote",ServHeader);
          end;
      end;
    END;

    [External]
    PROCEDURE EvaluateInteraction@9();
    VAR
      Selected@1000 : Integer;
    BEGIN
      if FIND('-') then begin
        Selected := DIALOG.STRMENU(Text011);
        if Selected <> 0 then
          repeat
            Evaluation := Selected;
            MODIFY;
          until NEXT = 0
      end;
    END;

    [External]
    PROCEDURE ResumeInteraction@11();
    VAR
      TempSegLine@1000 : TEMPORARY Record "Segment Line";
      Cont@1006 : Record Contact;
      Salesperson@1005 : Record "Salesperson/Purchaser";
      Campaign@1004 : Record Campaign;
      Task@1003 : Record "To-do";
      Opportunity@1002 : Record Opportunity;
    BEGIN
      TempSegLine.DELETEALL;
      TempSegLine.INIT;
      TempSegLine.VALIDATE(Date,WORKDATE);
      CopyFromSegment(TempSegLine);

      if Task.GET(GETFILTER("To-do No.")) then
        TempSegLine.SETRANGE("To-do No.",Task."No.");
      if Cont.GET(GETFILTER("Contact Company No.")) then
        TempSegLine.SETRANGE("Contact Company No.",Cont."No.");
      if Cont.GET(GETFILTER("Contact No.")) then
        TempSegLine.SETRANGE("Contact No.",Cont."No.");
      if Salesperson.GET(GETFILTER("Salesperson Code")) then
        TempSegLine.SETRANGE("Salesperson Code",Salesperson.Code);
      if Campaign.GET(GETFILTER("Campaign No.")) then
        TempSegLine.SETRANGE("Campaign No.",Campaign."No.");
      if Opportunity.GET(GETFILTER("Opportunity No.")) then
        TempSegLine.SETRANGE("Opportunity No.",Opportunity."No.");

      TempSegLine.StartWizard;
    END;

    BEGIN
    END.
  }
}

