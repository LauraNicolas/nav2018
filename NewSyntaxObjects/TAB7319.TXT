OBJECT Table 7319 Posted Whse. Receipt Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               if Location.RequirePutaway("Location Code") then
                 TESTFIELD(Quantity,"Qty. Put Away");
             END;

    CaptionML=ENU=Posted Whse. Receipt Line;
    LookupPageID="Posted Whse. Receipt Lines";
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=ENU=No.;
                                                   Editable=false }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   Editable=false }
    { 3   ;   ;Source Type         ;Integer       ;CaptionML=ENU=Source Type;
                                                   Editable=false }
    { 4   ;   ;Source Subtype      ;Option        ;CaptionML=ENU=Source Subtype;
                                                   OptionCaptionML=ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                   OptionString="0","1","2","3","4","5","6","7","8","9","10";
                                                   Editable=false }
    { 6   ;   ;Source No.          ;Code20        ;CaptionML=ENU=Source No.;
                                                   Editable=false }
    { 7   ;   ;Source Line No.     ;Integer       ;CaptionML=ENU=Source Line No.;
                                                   Editable=false }
    { 9   ;   ;Source Document     ;Option        ;CaptionML=ENU=Source Document;
                                                   OptionCaptionML=ENU=,Sales Order,,,Sales Return Order,Purchase Order,,,Purchase Return Order,Inbound Transfer;
                                                   OptionString=,"Sales Order",,,"Sales Return Order","Purchase Order",,,"Purchase Return Order","Inbound Transfer";
                                                   Editable=false }
    { 10  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=ENU=Location Code;
                                                   Editable=false }
    { 11  ;   ;Shelf No.           ;Code10        ;CaptionML=ENU=Shelf No. }
    { 12  ;   ;Bin Code            ;Code20        ;TableRelation=IF ("Zone Code"=FILTER('')) Bin.Code WHERE ("Location Code"=FIELD("Location Code"))
                                                                 ELSE IF ("Zone Code"=FILTER(<>'')) Bin.Code WHERE ("Location Code"=FIELD("Location Code"),
                                                                                                                    "Zone Code"=FIELD("Zone Code"));
                                                   CaptionML=ENU=Bin Code }
    { 13  ;   ;Zone Code           ;Code10        ;TableRelation=Zone.Code WHERE ("Location Code"=FIELD("Location Code"));
                                                   CaptionML=ENU=Zone Code }
    { 14  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=ENU=Item No.;
                                                   Editable=false }
    { 15  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 16  ;   ;Qty. (Base)         ;Decimal       ;CaptionML=ENU=Qty. (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 25  ;   ;Qty. Put Away       ;Decimal       ;CaptionML=ENU=Qty. Put Away;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 26  ;   ;Qty. Put Away (Base);Decimal       ;CaptionML=ENU=Qty. Put Away (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 27  ;   ;Put-away Qty.       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Activity Line"."Qty. Outstanding" WHERE ("Activity Type"=CONST("Put-away"),
                                                                                                                       "Whse. Document Type"=CONST(Receipt),
                                                                                                                       "Whse. Document No."=FIELD("No."),
                                                                                                                       "Whse. Document Line No."=FIELD("Line No."),
                                                                                                                       "Unit of Measure Code"=FIELD("Unit of Measure Code"),
                                                                                                                       "Action Type"=FILTER(" "|Take),
                                                                                                                       "Original Breakbulk"=CONST(false)));
                                                   CaptionML=ENU=Put-away Qty.;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 28  ;   ;Put-away Qty. (Base);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Activity Line"."Qty. Outstanding (Base)" WHERE ("Activity Type"=CONST("Put-away"),
                                                                                                                              "Whse. Document Type"=CONST(Receipt),
                                                                                                                              "Whse. Document No."=FIELD("No."),
                                                                                                                              "Whse. Document Line No."=FIELD("Line No."),
                                                                                                                              "Action Type"=FILTER(" "|Take),
                                                                                                                              "Original Breakbulk"=CONST(false)));
                                                   CaptionML=ENU=Put-away Qty. (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 29  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE ("Item No."=FIELD("Item No."));
                                                   CaptionML=ENU=Unit of Measure Code;
                                                   Editable=false }
    { 30  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=ENU=Qty. per Unit of Measure;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 31  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE ("Item No."=FIELD("Item No."));
                                                   CaptionML=ENU=Variant Code;
                                                   Editable=false }
    { 32  ;   ;Description         ;Text50        ;CaptionML=ENU=Description;
                                                   Editable=false }
    { 33  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2;
                                                   Editable=false }
    { 36  ;   ;Due Date            ;Date          ;CaptionML=ENU=Due Date }
    { 37  ;   ;Starting Date       ;Date          ;CaptionML=ENU=Starting Date }
    { 50  ;   ;Qty. Cross-Docked   ;Decimal       ;CaptionML=ENU=Qty. Cross-Docked;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 51  ;   ;Qty. Cross-Docked (Base);Decimal   ;CaptionML=ENU=Qty. Cross-Docked (Base);
                                                   DecimalPlaces=0:5 }
    { 52  ;   ;Cross-Dock Zone Code;Code10        ;TableRelation=Zone.Code WHERE ("Location Code"=FIELD("Location Code"));
                                                   CaptionML=ENU=Cross-Dock Zone Code }
    { 53  ;   ;Cross-Dock Bin Code ;Code20        ;TableRelation=Bin.Code WHERE ("Location Code"=FIELD("Location Code"));
                                                   CaptionML=ENU=Cross-Dock Bin Code }
    { 60  ;   ;Posted Source Document;Option      ;CaptionML=ENU=Posted Source Document;
                                                   OptionCaptionML=ENU=" ,Posted Receipt,,Posted Return Receipt,,Posted Shipment,,Posted Return Shipment,,Posted Transfer Receipt";
                                                   OptionString=" ","Posted Receipt",,"Posted Return Receipt",,"Posted Shipment",,"Posted Return Shipment",,"Posted Transfer Receipt" }
    { 61  ;   ;Posted Source No.   ;Code20        ;CaptionML=ENU=Posted Source No. }
    { 62  ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 63  ;   ;Vendor Shipment No. ;Code35        ;CaptionML=ENU=Vendor Shipment No. }
    { 64  ;   ;Whse. Receipt No.   ;Code20        ;CaptionML=ENU=Whse. Receipt No.;
                                                   Editable=false }
    { 65  ;   ;Whse Receipt Line No.;Integer      ;CaptionML=ENU=Whse Receipt Line No.;
                                                   Editable=false }
    { 66  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=" ,Partially Put Away,Completely Put Away";
                                                   OptionString=" ","Partially Put Away","Completely Put Away";
                                                   Editable=false }
    { 6500;   ;Serial No.          ;Code20        ;CaptionML=ENU=Serial No.;
                                                   Editable=false }
    { 6501;   ;Lot No.             ;Code20        ;CaptionML=ENU=Lot No.;
                                                   Editable=false }
    { 6502;   ;Warranty Date       ;Date          ;CaptionML=ENU=Warranty Date }
    { 6503;   ;Expiration Date     ;Date          ;CaptionML=ENU=Expiration Date }
  }
  KEYS
  {
    {    ;"No.","Line No."                        ;Clustered=true }
    {    ;"Posted Source No.","Posting Date"       }
    {    ;"Item No.","Location Code","Variant Code";
                                                   SumIndexFields="Qty. (Base)","Qty. Put Away (Base)";
                                                   MaintainSIFTIndex=false }
    {    ;"Source Type","Source Subtype","Source No.","Source Line No.","Posted Source Document","Posted Source No." }
    { false;"Lot No."                              }
    { false;"Serial No."                           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Location@1004 : Record Location;
      HideValidationDialog@1005 : Boolean;
      Text000@1006 : TextConst 'ENU=Nothing to handle.';

    [External]
    PROCEDURE SetHideValidationDialog@8(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    [External]
    PROCEDURE GetLineStatus@1() : Integer;
    BEGIN
      if "Qty. Put Away" > 0 then
        if "Qty. Put Away" < Quantity then
          Status := Status::"Partially Put Away"
        else
          Status := Status::"Completely Put Away";

      exit(Status);
    END;

    LOCAL PROCEDURE GetLocation@2(LocationCode@1000 : Code[10]);
    BEGIN
      if LocationCode = '' then
        Location.GetLocationSetup(LocationCode,Location)
      else
        if Location.Code <> LocationCode then
          Location.GET(LocationCode);
    END;

    [External]
    PROCEDURE CreatePutAwayDoc@11(VAR PostedWhseRcptLine@1000 : Record "Posted Whse. Receipt Line";AssignedID@1001 : Code[50]);
    VAR
      WhseSetup@1003 : Record "Warehouse Setup";
      CreatePutAwayFromWhseSource@1002 : Report "Whse.-Source - Create Document";
    BEGIN
      GetLocation(PostedWhseRcptLine."Location Code");
      if not Location."Require Put-away" then begin
        if Location.Code = '' then begin
          WhseSetup.GET;
          WhseSetup.TESTFIELD("Require Put-away");
        end else
          Location.TESTFIELD("Require Put-away");
      end;
      PostedWhseRcptLine.SETFILTER(Quantity,'>0');
      PostedWhseRcptLine.SETFILTER(
        Status,'<>%1',PostedWhseRcptLine.Status::"Completely Put Away");
      if PostedWhseRcptLine.FIND('-') then begin
        CreatePutAwayFromWhseSource.SetPostedWhseReceiptLine(PostedWhseRcptLine,AssignedID);
        CreatePutAwayFromWhseSource.SetHideValidationDialog(HideValidationDialog);
        CreatePutAwayFromWhseSource.USEREQUESTPAGE(not HideValidationDialog);
        CreatePutAwayFromWhseSource.RUNMODAL;
        CreatePutAwayFromWhseSource.GetResultMessage(1);
        CLEAR(CreatePutAwayFromWhseSource);
      end else
        if not HideValidationDialog then
          MESSAGE(Text000);
    END;

    [External]
    PROCEDURE SetSourceFilter@42(SourceType@1004 : Integer;SourceSubType@1003 : Option;SourceNo@1002 : Code[20];SourceLineNo@1001 : Integer;SetKey@1005 : Boolean);
    BEGIN
      if SetKey then
        SETCURRENTKEY("Source Type","Source Subtype","Source No.","Source Line No.");
      SETRANGE("Source Type",SourceType);
      if SourceSubType >= 0 then
        SETRANGE("Source Subtype",SourceSubType);
      SETRANGE("Source No.",SourceNo);
      if SourceLineNo >= 0 then
        SETRANGE("Source Line No.",SourceLineNo);
    END;

    [External]
    PROCEDURE SetTracking@3(SerialNo@1001 : Code[20];LotNo@1000 : Code[20];WarrantyDate@1002 : Date;ExpirationDate@1003 : Date);
    BEGIN
      "Serial No." := SerialNo;
      "Lot No." := LotNo;
      "Warranty Date" := WarrantyDate;
      "Expiration Date" := ExpirationDate;
    END;

    [External]
    PROCEDURE SetTrackingFilterFromRelation@5(WhseItemEntryRelation@1000 : Record "Whse. Item Entry Relation");
    BEGIN
      SETRANGE("Serial No.",WhseItemEntryRelation."Serial No.");
      SETRANGE("Lot No.",WhseItemEntryRelation."Lot No.");
    END;

    BEGIN
    END.
  }
}

