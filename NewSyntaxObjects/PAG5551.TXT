OBJECT Page 5551 Fixed Asset Acquisition Wizard
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Fixed Asset Acquisition;
    InsertAllowed=false;
    DeleteAllowed=false;
    LinksAllowed=false;
    SourceTable="Gen. Journal Line";
    PageType=NavigatePage;
    SourceTableTemporary=true;
    ShowFilter=false;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 // We could check if values like FA Posting code, descirption are in the temp
                 if not GET then begin
                   INIT;
                   "Journal Template Name" := FixedAssetAcquisitionWizard.SelectFATemplate;
                   "Journal Batch Name" := FixedAssetAcquisitionWizard.GetAutogenJournalBatch;
                   "Document Type" := "Document Type"::Invoice;
                   "Account Type" := "Account Type"::"Fixed Asset";
                   "FA Posting Type" := "FA Posting Type"::"Acquisition Cost";
                   "Posting Date" := WORKDATE;
                   SetAccountNoFromFilter;
                   INSERT;
                 end;

                 EnableOpenFAGLJournal := JournalBatchIsEmpty;
                 OpenFAGLJournal := not EnableOpenFAGLJournal;
                 VerifyFADoNotExistInGLJournalLines;
                 ValidateCurrentStep(Step);
               END;

    ActionList=ACTIONS
    {
      { 8       ;    ;ActionContainer;
                      Name=ActionContainer8;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=PreviousPage;
                      CaptionML=ENU=Back;
                      ApplicationArea=#FixedAssets;
                      Visible=Step <> Step::"Already In Journal";
                      Enabled=Step <> Step::Intro;
                      InFooterBar=true;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(true);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=NextPage;
                      CaptionML=ENU=Next;
                      ApplicationArea=#FixedAssets;
                      Visible=Step <> Step::"Already In Journal";
                      Enabled=(Step <> Step::Done) AND (CurrStepIsValid);
                      InFooterBar=true;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(false);
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=ENU=Finish;
                      ApplicationArea=#FixedAssets;
                      Enabled=(Step = Step::Done) OR (Step = Step::"Already In Journal");
                      InFooterBar=true;
                      Image=Approve;
                      OnAction=VAR
                                 GenJnlLine@1010 : Record "Gen. Journal Line";
                                 PageGenJnlLine@1007 : Record "Gen. Journal Line";
                               BEGIN
                                 if Step <> Step::"Already In Journal" then
                                   CreateFAAcquisitionLines(GenJnlLine);

                                 if OpenFAGLJournal then begin
                                   PageGenJnlLine.VALIDATE("Journal Template Name","Journal Template Name");
                                   PageGenJnlLine.VALIDATE("Journal Batch Name","Journal Batch Name");
                                   PageGenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                   PageGenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
                                   PAGE.RUN(PAGE::"Fixed Asset G/L Journal",PageGenJnlLine);
                                 end else
                                   CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post Batch",GenJnlLine);

                                 CurrPage.CLOSE;
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=Exit;
                      CaptionML=ENU=Exit;
                      ApplicationArea=#FixedAssets;
                      OnAction=BEGIN
                                 CurrPage.CLOSE
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Name=Group96;
                Visible=TopBannerVisible AND (Step <> Step::Done);
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                Name=MediaResourcesStandard;
                ApplicationArea=#FixedAssets;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=false }

    { 98  ;1   ;Group     ;
                Name=Group98;
                Visible=TopBannerVisible AND (Step = Step::Done);
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                Name=MediaResourcesDone;
                ApplicationArea=#FixedAssets;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=false }

    { 11  ;1   ;Group     ;
                Name=Step1;
                CaptionML=ENU="";
                Visible=Step = Step::Intro;
                GroupType=Group }

    { 18  ;2   ;Group     ;
                Name=Para1.1;
                CaptionML=ENU=Welcome to Assisted Fixed Asset Acquisition;
                GroupType=Group }

    { 20  ;3   ;Group     ;
                Name=Para1.1.1;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=When you acquire a fixed asset, you can post the transaction to a G/L account, a vendor, or a bank account. }

    { 19  ;3   ;Group     ;
                Name=Para1.1.2;
                Visible=FALSE;
                GroupType=Group }

    { 17  ;2   ;Group     ;
                Name=Para1.2;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=Choose Next to specify how to post the acquisition. }

    { 2   ;1   ;Group     ;
                Name=Step2;
                CaptionML=ENU="";
                Visible=Step = Step::"FA Details";
                GroupType=Group }

    { 22  ;2   ;Group     ;
                Name=Para2.1;
                CaptionML=ENU=Provide information about the fixed asset.;
                GroupType=Group }

    { 3   ;3   ;Field     ;
                Name=AcquisitionCost;
                CaptionML=ENU=Acquisition Cost Incl. VAT;
                ApplicationArea=#FixedAssets;
                SourceExpr=Amount;
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;

                ImplicitType=Decimal }

    { 16  ;3   ;Field     ;
                Name=AcquisitionDate;
                CaptionML=ENU=Acquisition Date;
                ApplicationArea=#FixedAssets;
                SourceExpr="Posting Date";
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;

                ImplicitType=Date }

    { 32  ;1   ;Group     ;
                Name=Step3;
                CaptionML=ENU="";
                Visible=Step = Step::"Register Details";
                GroupType=Group }

    { 33  ;2   ;Group     ;
                Name=Para3.1;
                CaptionML=ENU=Which ledger do you want to post the acquisition to?;
                GroupType=Group }

    { 36  ;3   ;Field     ;
                Name=TypeOfAcquisitions;
                CaptionML=ENU=Post to;
                OptionCaptionML=ENU=G/L Account,Vendor,Bank Account;
                ApplicationArea=#FixedAssets;
                SourceExpr=AcquisitionOptions;
                OnValidate=BEGIN
                             case AcquisitionOptions of
                               AcquisitionOptions::"G/L Account":
                                 VALIDATE("Bal. Account Type","Bal. Account Type"::"G/L Account");
                               AcquisitionOptions::Vendor:
                                 "Bal. Account Type" := "Bal. Account Type"::Vendor;
                               AcquisitionOptions::"Bank Account":
                                 VALIDATE("Bal. Account Type","Bal. Account Type"::"Bank Account");
                             end;
                             ValidateCurrentStep(Step);
                           END;

                ImplicitType=Option }

    { 34  ;3   ;Group     ;
                Name=Group34;
                Visible=AcquisitionOptions = AcquisitionOptions::"G/L Account";
                GroupType=Group }

    { 26  ;4   ;Field     ;
                Name=BalancingAccountNo;
                CaptionML=ENU=Balancing Account No.;
                ApplicationArea=#FixedAssets;
                SourceExpr="Bal. Account No.";
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;

                ImplicitType=Code20 }

    { 27  ;3   ;Group     ;
                Name=Group27;
                Visible=AcquisitionOptions = AcquisitionOptions::Vendor;
                GroupType=Group }

    { 28  ;4   ;Field     ;
                Name=VendorNo;
                CaptionML=ENU=Vendor;
                ApplicationArea=#FixedAssets;
                SourceExpr="Bal. Account No.";
                ImplicitType=Code20 }

    { 29  ;4   ;Field     ;
                Name=ExternalDocNo;
                CaptionML=ENU=External Document No.;
                ApplicationArea=#FixedAssets;
                SourceExpr="External Document No.";
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;

                ImplicitType=Code35 }

    { 30  ;3   ;Group     ;
                Name=Group30;
                Visible=AcquisitionOptions = AcquisitionOptions::"Bank Account";
                GroupType=Group }

    { 31  ;4   ;Field     ;
                Name=Bank Account;
                CaptionML=ENU=Bank Account;
                ApplicationArea=#FixedAssets;
                SourceExpr="Bal. Account No.";
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;

                ImplicitType=Code20 }

    { 10  ;1   ;Group     ;
                Name=Step4;
                CaptionML=ENU="";
                Visible=Step = Step::Done;
                GroupType=Group }

    { 25  ;2   ;Group     ;
                Name=Para4.1;
                CaptionML=ENU=That's it!;
                GroupType=Group;
                InstructionalTextML=ENU=The information needed to post the acquisition is  now ready in the fixed asset G/L journal. }

    { 5   ;2   ;Group     ;
                Name=Para4.2;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=Choose Finish to automatically post the fixed asset G/L journal lines. }

    { 4   ;3   ;Field     ;
                Name=OpenFAGLJournal;
                CaptionML=ENU=Upon Finish, open the FA G/L journal.;
                ApplicationArea=#FixedAssets;
                SourceExpr=OpenFAGLJournal;
                Enabled=EnableOpenFAGLJournal;
                ImplicitType=Boolean }

    { 24  ;1   ;Group     ;
                Name=StepInJournal;
                CaptionML=ENU="";
                Visible=Step = Step::"Already In Journal";
                GroupType=Group }

    { 23  ;2   ;Group     ;
                Name=Para5.1;
                CaptionML=ENU=The fixed asset is already planned to be acquired.;
                GroupType=Group;
                InstructionalTextML=ENU=Fixed asset G/L journal already contains a line for the acquisition of this fixed asset. }

    { 21  ;2   ;Group     ;
                Name=Para5.2;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=Choose Finish to open the fixed asset G/L journal. Close this window to take no further action. }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1040 : Record "Media Repository";
      MediaRepositoryDone@1041 : Record "Media Repository";
      MediaResourcesStandard@1006 : Record "Media Resources";
      MediaResourcesDone@1004 : Record "Media Resources";
      TempBalancingGenJournalLine@1002 : TEMPORARY Record "Gen. Journal Line";
      FixedAssetAcquisitionWizard@1005 : Codeunit "Fixed Asset Acquisition Wizard";
      Step@1015 : 'Intro,"FA Details","Register Details",Done,"Already In Journal"';
      TopBannerVisible@1042 : Boolean;
      AcquisitionOptions@1000 : '"G/L Account",Vendor,"Bank Account"';
      OpenFAGLJournal@1018 : Boolean;
      CurrStepIsValid@1001 : Boolean;
      EnableOpenFAGLJournal@1003 : Boolean;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      if Backwards then
        Step := Step - 1
      else
        Step := Step + 1;
      ValidateCurrentStep(Step);

      if Step = Step::Done then begin
        TempBalancingGenJournalLine.INIT;
        TempBalancingGenJournalLine.TRANSFERFIELDS(Rec);
        TempBalancingGenJournalLine."Account No." := '';
        if not TempBalancingGenJournalLine.INSERT then
          TempBalancingGenJournalLine.MODIFY(true);
      end;

      CurrPage.UPDATE(true);
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      if MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE)) and
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE))
      then
        if MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") and
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        then
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    LOCAL PROCEDURE ValidateCurrentStep@7(CurrentStep@1000 : Option);
    BEGIN
      case CurrentStep of
        Step::Intro:
          CurrStepIsValid := true;
        Step::"FA Details":
          CurrStepIsValid := (Amount >= 0.0) and ("Posting Date" <> 0D);
        Step::"Register Details":
          begin
            CurrStepIsValid := "Bal. Account No." <> '';
            if AcquisitionOptions = AcquisitionOptions::Vendor then
              CurrStepIsValid := CurrStepIsValid and ("External Document No." <> '');
          end;
        Step::Done:
          CurrStepIsValid := true;
        else
          CurrStepIsValid := true;
      end;
    END;

    LOCAL PROCEDURE JournalBatchIsEmpty@1() : Boolean;
    VAR
      GenJournalLine@1000 : Record "Gen. Journal Line";
    BEGIN
      GenJournalLine.SETRANGE("Journal Batch Name","Journal Batch Name");
      exit(GenJournalLine.COUNT = 0);
    END;

    LOCAL PROCEDURE VerifyFADoNotExistInGLJournalLines@6();
    VAR
      GenJournalLine@1000 : Record "Gen. Journal Line";
    BEGIN
      GenJournalLine.SETRANGE("Account No.","Account No.");
      GenJournalLine.SETRANGE("Account Type",GenJournalLine."Account Type"::"Fixed Asset");
      GenJournalLine.SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
      if GenJournalLine.FINDFIRST then begin
        Step := Step::"Already In Journal";
        OpenFAGLJournal := true;
        COPY(GenJournalLine);
        INSERT;
      end
    END;

    BEGIN
    END.
  }
}

