OBJECT Codeunit 5982 Service-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice,Ship and &Consume';
      Text001@1001 : TextConst 'ENU=Do you want to post and print the %1?';
      ServiceHeader@1003 : Record "Service Header";
      ServShptHeader@1004 : Record "Service Shipment Header";
      ServInvHeader@1005 : Record "Service Invoice Header";
      ServCrMemoHeader@1006 : Record "Service Cr.Memo Header";
      ServicePost@1009 : Codeunit "Service-Post";
      Selection@1010 : Integer;
      Ship@1011 : Boolean;
      Consume@1007 : Boolean;
      Invoice@1000 : Boolean;

    [Internal]
    PROCEDURE PostDocument@5(VAR Rec@1000 : Record "Service Header");
    VAR
      DummyServLine@1001 : TEMPORARY Record "Service Line";
    BEGIN
      ServiceHeader.COPY(Rec);
      Code(DummyServLine);
      Rec := ServiceHeader;
    END;

    LOCAL PROCEDURE Code@2(VAR PassedServLine@1002 : Record "Service Line");
    BEGIN
      with ServiceHeader do begin
        case "Document Type" of
          "Document Type"::Order:
            begin
              Selection := STRMENU(Text000,3);
              if Selection = 0 then
                exit;
              Ship := Selection in [1,3,4];
              Consume := Selection in [4];
              Invoice := Selection in [2,3];
            end
          else
            if not CONFIRM(Text001,false,"Document Type") then
              exit;
        end;
        ServicePost.PostWithLines(ServiceHeader,PassedServLine,Ship,Consume,Invoice);
        GetReport(ServiceHeader);
        COMMIT;
      end;
    END;

    LOCAL PROCEDURE GetReport@3(VAR ServiceHeader@1000 : Record "Service Header");
    BEGIN
      with ServiceHeader do
        case "Document Type" of
          "Document Type"::Order:
            begin
              if Ship then begin
                ServShptHeader."No." := "Last Shipping No.";
                ServShptHeader.SETRECFILTER;
                ServShptHeader.PrintRecords(false);
              end;
              if Invoice then begin
                ServInvHeader."No." := "Last Posting No.";
                ServInvHeader.SETRECFILTER;
                ServInvHeader.PrintRecords(false);
              end;
            end;
          "Document Type"::Invoice:
            begin
              if "Last Posting No." = '' then
                ServInvHeader."No." := "No."
              else
                ServInvHeader."No." := "Last Posting No.";
              ServInvHeader.SETRECFILTER;
              ServInvHeader.PrintRecords(false);
            end;
          "Document Type"::"Credit Memo":
            begin
              if "Last Posting No." = '' then
                ServCrMemoHeader."No." := "No."
              else
                ServCrMemoHeader."No." := "Last Posting No.";
              ServCrMemoHeader.SETRECFILTER;
              ServCrMemoHeader.PrintRecords(false);
            end;
        end;
    END;

    BEGIN
    END.
  }
}

