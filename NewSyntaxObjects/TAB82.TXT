OBJECT Table 82 Item Journal Template
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Page ID");
             END;

    OnDelete=BEGIN
               ItemJnlLine.SETRANGE("Journal Template Name",Name);
               ItemJnlLine.DELETEALL(true);
               ItemJnlBatch.SETRANGE("Journal Template Name",Name);
               ItemJnlBatch.DELETEALL;
             END;

    OnRename=BEGIN
               ReservEngineMgt.RenamePointer(DATABASE::"Item Journal Line",
                 0,xRec.Name,'',0,0,
                 0,Name,'',0,0);
             END;

    CaptionML=ENU=Item Journal Template;
    LookupPageID="Item Journal Template List";
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=ENU=Name;
                                                   NotBlank=true }
    { 2   ;   ;Description         ;Text80        ;CaptionML=ENU=Description }
    { 5   ;   ;Test Report ID      ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Report));
                                                   CaptionML=ENU=Test Report ID }
    { 6   ;   ;Page ID             ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Page));
                                                   OnValidate=BEGIN
                                                                if "Page ID" = 0 then
                                                                  VALIDATE(Type);
                                                              END;

                                                   CaptionML=ENU=Page ID }
    { 7   ;   ;Posting Report ID   ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Report));
                                                   CaptionML=ENU=Posting Report ID }
    { 8   ;   ;Force Posting Report;Boolean       ;CaptionML=ENU=Force Posting Report }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "Test Report ID" := REPORT::"Inventory Posting - Test";
                                                                "Posting Report ID" := REPORT::"Item Register - Quantity";
                                                                "Whse. Register Report ID" := REPORT::"Warehouse Register - Quantity";
                                                                SourceCodeSetup.GET;
                                                                case Type of
                                                                  Type::Item:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Item Journal";
                                                                      "Page ID" := PAGE::"Item Journal";
                                                                    end;
                                                                  Type::Transfer:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Item Reclass. Journal";
                                                                      "Page ID" := PAGE::"Item Reclass. Journal";
                                                                    end;
                                                                  Type::"Phys. Inventory":
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Phys. Inventory Journal";
                                                                      "Page ID" := PAGE::"Phys. Inventory Journal";
                                                                    end;
                                                                  Type::Revaluation:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Revaluation Journal";
                                                                      "Page ID" := PAGE::"Revaluation Journal";
                                                                      "Test Report ID" := REPORT::"Revaluation Posting - Test";
                                                                      "Posting Report ID" := REPORT::"Item Register - Value";
                                                                    end;
                                                                  Type::Consumption:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Consumption Journal";
                                                                      "Page ID" := PAGE::"Consumption Journal";
                                                                    end;
                                                                  Type::Output:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Output Journal";
                                                                      "Page ID" := PAGE::"Output Journal";
                                                                    end;
                                                                  Type::Capacity:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Capacity Journal";
                                                                      "Page ID" := PAGE::"Capacity Journal";
                                                                    end;
                                                                  Type::"Prod. Order":
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Production Journal";
                                                                      "Page ID" := PAGE::"Production Journal";
                                                                    end;
                                                                end;
                                                                if Recurring then
                                                                  case Type of
                                                                    Type::Item:
                                                                      "Page ID" := PAGE::"Recurring Item Jnl.";
                                                                    Type::Consumption:
                                                                      "Page ID" := PAGE::"Recurring Consumption Journal";
                                                                    Type::Output:
                                                                      "Page ID" := PAGE::"Recurring Output Journal";
                                                                    Type::Capacity:
                                                                      "Page ID" := PAGE::"Recurring Capacity Journal";
                                                                  end;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Item,Transfer,Phys. Inventory,Revaluation,Consumption,Output,Capacity,Prod. Order;
                                                   OptionString=Item,Transfer,"Phys. Inventory",Revaluation,Consumption,Output,Capacity,"Prod. Order" }
    { 10  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   OnValidate=BEGIN
                                                                ItemJnlLine.SETRANGE("Journal Template Name",Name);
                                                                ItemJnlLine.MODIFYALL("Source Code","Source Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Source Code }
    { 11  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=ENU=Reason Code }
    { 12  ;   ;Recurring           ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Type);
                                                                if Recurring then
                                                                  TESTFIELD("No. Series",'');
                                                              END;

                                                   CaptionML=ENU=Recurring }
    { 15  ;   ;Test Report Caption ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Test Report ID")));
                                                   CaptionML=ENU=Test Report Caption;
                                                   Editable=false }
    { 16  ;   ;Page Caption        ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Page),
                                                                                                                "Object ID"=FIELD("Page ID")));
                                                   CaptionML=ENU=Page Caption;
                                                   Editable=false }
    { 17  ;   ;Posting Report Caption;Text250     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Posting Report ID")));
                                                   CaptionML=ENU=Posting Report Caption;
                                                   Editable=false }
    { 19  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if "No. Series" <> '' then begin
                                                                  if Recurring then
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  if "No. Series" = "Posting No. Series" then
                                                                    "Posting No. Series" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No. Series }
    { 20  ;   ;Posting No. Series  ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if ("Posting No. Series" = "No. Series") and ("Posting No. Series" <> '') then
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                              END;

                                                   CaptionML=ENU=Posting No. Series }
    { 21  ;   ;Whse. Register Report ID;Integer   ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Report));
                                                   AccessByPermission=TableData "Bin Content"=R;
                                                   CaptionML=ENU=Whse. Register Report ID }
    { 22  ;   ;Whse. Register Report Caption;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Whse. Register Report ID")));
                                                   AccessByPermission=TableData "Bin Content"=R;
                                                   CaptionML=ENU=Whse. Register Report Caption;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description,Type                    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'ENU=must not be %1';
      ItemJnlBatch@1002 : Record "Item Journal Batch";
      ItemJnlLine@1003 : Record "Item Journal Line";
      SourceCodeSetup@1004 : Record "Source Code Setup";
      ReservEngineMgt@1005 : Codeunit "Reservation Engine Mgt.";

    BEGIN
    END.
  }
}

