OBJECT Codeunit 88 Sales Post via Job Queue
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Job Queue Entry";
    OnRun=VAR
            SalesHeader@1000 : Record "Sales Header";
            SalesPostPrint@1002 : Codeunit "Sales-Post + Print";
            RecRef@1001 : RecordRef;
          BEGIN
            TESTFIELD("Record ID to Process");
            RecRef.GET("Record ID to Process");
            RecRef.SETTABLE(SalesHeader);
            SalesHeader.FIND;

            SetJobQueueStatus(SalesHeader,SalesHeader."Job Queue Status"::Posting);
            if not CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader) then begin
              SetJobQueueStatus(SalesHeader,SalesHeader."Job Queue Status"::Error);
              ERROR(GETLASTERRORTEXT);
            end;
            if SalesHeader."Print Posted Documents" then
              SalesPostPrint.GetReport(SalesHeader);

            SetJobQueueStatus(SalesHeader,SalesHeader."Job Queue Status"::" ");
          END;

  }
  CODE
  {
    VAR
      PostDescription@1000 : TextConst '@@@="%1 = document type, %2 = document number. Example: Post Sales Order 1234.";ENU=Post Sales %1 %2.';
      PostAndPrintDescription@1002 : TextConst '@@@="%1 = document type, %2 = document number. Example: Post Sales Order 1234.";ENU=Post and Print Sales %1 %2.';
      Confirmation@1001 : TextConst '@@@="%1=document type, %2=number, e.g. Order 123  or Invoice 234.";ENU=%1 %2 has been scheduled for posting.';
      WrongJobQueueStatus@1003 : TextConst '@@@="%1 = document type, %2 = document number. Example: Sales Order 1234 or Invoice 1234.";ENU=%1 %2 cannot be posted because it has already been scheduled for posting. Choose the Remove from Job Queue action to reset the job queue status and then post again.';

    LOCAL PROCEDURE SetJobQueueStatus@5(VAR SalesHeader@1001 : Record "Sales Header";NewStatus@1000 : Option);
    BEGIN
      SalesHeader.LOCKTABLE;
      if SalesHeader.FIND then begin
        SalesHeader."Job Queue Status" := NewStatus;
        SalesHeader.MODIFY;
        COMMIT;
      end;
    END;

    [External]
    PROCEDURE EnqueueSalesDoc@1(VAR SalesHeader@1000 : Record "Sales Header");
    VAR
      TempInvoice@1006 : Boolean;
      TempRcpt@1005 : Boolean;
      TempShip@1004 : Boolean;
    BEGIN
      with SalesHeader do begin
        if not ("Job Queue Status" in ["Job Queue Status"::" ","Job Queue Status"::Error]) then
          ERROR(WrongJobQueueStatus,"Document Type","No.");
        TempInvoice := Invoice;
        TempRcpt := Receive;
        TempShip := Ship;
        if Status = Status::Open then
          CODEUNIT.RUN(CODEUNIT::"Release Sales Document",SalesHeader);
        Invoice := TempInvoice;
        Receive := TempRcpt;
        Ship := TempShip;
        "Job Queue Status" := "Job Queue Status"::"Scheduled for Posting";
        "Job Queue Entry ID" := EnqueueJobEntry(SalesHeader);
        MODIFY;

        MESSAGE(Confirmation,"Document Type","No.");
      end;
    END;

    LOCAL PROCEDURE EnqueueJobEntry@3(SalesHeader@1000 : Record "Sales Header") : GUID;
    VAR
      JobQueueEntry@1001 : Record "Job Queue Entry";
    BEGIN
      with JobQueueEntry do begin
        CLEAR(ID);
        "Object Type to Run" := "Object Type to Run"::Codeunit;
        "Object ID to Run" := CODEUNIT::"Sales Post via Job Queue";
        "Record ID to Process" := SalesHeader.RECORDID;
        FillJobEntryFromSalesSetup(JobQueueEntry,SalesHeader."Print Posted Documents");
        FillJobEntrySalesDescription(JobQueueEntry,SalesHeader);
        CODEUNIT.RUN(CODEUNIT::"Job Queue - Enqueue",JobQueueEntry);
        exit(ID)
      end;
    END;

    LOCAL PROCEDURE FillJobEntryFromSalesSetup@4(VAR JobQueueEntry@1001 : Record "Job Queue Entry";PostAndPrint@1000 : Boolean);
    VAR
      SalesSetup@1002 : Record "Sales & Receivables Setup";
    BEGIN
      SalesSetup.GET;
      with JobQueueEntry do begin
        "Notify On Success" := SalesSetup."Notify On Success";
        "Job Queue Category Code" := SalesSetup."Job Queue Category Code";
        if PostAndPrint then
          Priority := SalesSetup."Job Q. Prio. for Post & Print"
        else
          Priority := SalesSetup."Job Queue Priority for Post";
      end;
    END;

    LOCAL PROCEDURE FillJobEntrySalesDescription@6(VAR JobQueueEntry@1000 : Record "Job Queue Entry";SalesHeader@1001 : Record "Sales Header");
    BEGIN
      with JobQueueEntry do begin
        if SalesHeader."Print Posted Documents" then
          Description := PostAndPrintDescription
        else
          Description := PostDescription;
        Description :=
          COPYSTR(STRSUBSTNO(Description,SalesHeader."Document Type",SalesHeader."No."),1,MAXSTRLEN(Description));
      end;
    END;

    [External]
    PROCEDURE CancelQueueEntry@2(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      with SalesHeader do
        if "Job Queue Status" <> "Job Queue Status"::" " then begin
          DeleteJobs(SalesHeader);
          "Job Queue Status" := "Job Queue Status"::" ";
          MODIFY;
        end;
    END;

    LOCAL PROCEDURE DeleteJobs@8(SalesHeader@1000 : Record "Sales Header");
    VAR
      JobQueueEntry@1001 : Record "Job Queue Entry";
    BEGIN
      with SalesHeader do begin
        if not ISNULLGUID("Job Queue Entry ID") then
          JobQueueEntry.SETRANGE(ID,"Job Queue Entry ID");
        JobQueueEntry.SETRANGE("Record ID to Process",RECORDID);
        if not JobQueueEntry.ISEMPTY then
          JobQueueEntry.DELETEALL(true);
      end;
    END;

    BEGIN
    END.
  }
}

