OBJECT Table 2132 O365 Settings Menu
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Key := Key + 1;
             END;

    CaptionML=ENU=O365 Settings Menu;
  }
  FIELDS
  {
    { 1   ;   ;Key                 ;Integer       ;AutoIncrement=true;
                                                   CaptionML=ENU=Key }
    { 2   ;   ;Page ID             ;Integer       ;CaptionML=ENU=Page ID }
    { 3   ;   ;Title               ;Text30        ;CaptionML=ENU=Title }
    { 4   ;   ;Description         ;Text80        ;CaptionML=ENU=Description }
    { 5   ;   ;Link                ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=ENU=Link }
    { 6   ;   ;On Open Action      ;Option        ;CaptionML=ENU=On Open Action;
                                                   OptionCaptionML=ENU=Hyperlink,Page;
                                                   OptionString=Hyperlink,Page }
    { 10  ;   ;Parameter           ;Text250       ;CaptionML=ENU=Parameter }
  }
  KEYS
  {
    {    ;Key                                     ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      UnexpectedParemeterErr@1000 : TextConst '@@@=%1 - the parameter''s value;ENU=Unexpected parameter: %1.';

    [External]
    PROCEDURE OpenPage@3();
    BEGIN
      if Parameter <> '' then
        OpenPageWithParameter
      else
        PAGE.RUN("Page ID");
    END;

    LOCAL PROCEDURE OpenPageWithParameter@4();
    BEGIN
      case "Page ID" of
        PAGE::"O365 Import from Excel Wizard":
          OpenImportFromExcelWizard;
        else
          ERROR(UnexpectedParemeterErr,Parameter);
      end;
    END;

    [External]
    PROCEDURE OpenLink@5();
    BEGIN
      HYPERLINK(Link);
    END;

    [External]
    PROCEDURE InsertPageMenuItem@1(PageIDValue@1002 : Integer;TitleValue@1001 : Text[30];DescriptionValue@1000 : Text[80]);
    BEGIN
      "Page ID" := PageIDValue;
      Title := TitleValue;
      Description := DescriptionValue;
      "On Open Action" := "On Open Action"::Page;
      INSERT(true);
    END;

    [External]
    PROCEDURE InsertPageWithParameterMenuItem@6(PageIDValue@1002 : Integer;PageParameter@1004 : Text[250];TitleValue@1001 : Text[30];DescriptionValue@1000 : Text[80]);
    BEGIN
      "Page ID" := PageIDValue;
      Parameter := PageParameter;
      Title := TitleValue;
      Description := DescriptionValue;
      "On Open Action" := "On Open Action"::Page;
      INSERT(true);
    END;

    [External]
    PROCEDURE InsertHyperlinkMenuItem@2(HyperlinkValue@1002 : Text[250];TitleValue@1001 : Text[30];DescriptionValue@1000 : Text[80]);
    BEGIN
      Link := HyperlinkValue;
      Title := TitleValue;
      Description := DescriptionValue;
      "On Open Action" := "On Open Action"::Hyperlink;
      INSERT(true);
    END;

    LOCAL PROCEDURE OpenImportFromExcelWizard@7();
    VAR
      DummyCustomer@1001 : Record Customer;
      DummyItem@1002 : Record Item;
      O365ImportFromExcelWizard@1000 : Page "O365 Import from Excel Wizard";
    BEGIN
      case Parameter of
        DummyCustomer.TABLENAME:
          O365ImportFromExcelWizard.PrepareCustomerImportData;
        DummyItem.TABLENAME:
          O365ImportFromExcelWizard.PrepareItemImportData;
        else
          ERROR(UnexpectedParemeterErr,Parameter);
      end;
      O365ImportFromExcelWizard.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

