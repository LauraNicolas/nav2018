OBJECT Codeunit 5671 FA. Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="FA Journal Batch";
    OnRun=BEGIN
            FAJnlBatch.COPY(Rec);
            Code;
            COPY(FAJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.';
      FAJnlTemplate@1004 : Record "FA Journal Template";
      FAJnlBatch@1005 : Record "FA Journal Batch";
      FAJnlLine@1006 : Record "FA Journal Line";
      FAReg@1007 : Record "FA Register";
      JournalWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with FAJnlBatch do begin
        FAJnlTemplate.GET("Journal Template Name");
        FAJnlTemplate.TESTFIELD("Posting Report ID");
        FAJnlTemplate.TESTFIELD("Maint. Posting Report ID");

        if not CONFIRM(Text000,false) then
          exit;

        FIND('-');
        repeat
          FAJnlLine."Journal Template Name" := "Journal Template Name";
          FAJnlLine."Journal Batch Name" := Name;
          FAJnlLine."Line No." := 1;
          if CODEUNIT.RUN(CODEUNIT::"FA Jnl.-Post Batch",FAJnlLine) then begin
            if FAReg.GET(FAJnlLine."Line No.") then begin
              FAReg.SETRECFILTER;
              if FAReg."From Entry No." > 0 then
                REPORT.RUN(FAJnlTemplate."Posting Report ID",false,false,FAReg);
              if FAReg."From Maintenance Entry No." > 0 then
                REPORT.RUN(FAJnlTemplate."Maint. Posting Report ID",false,false,FAReg);
            end;
            MARK(false);
          end
          else begin
            MARK(true);
            JournalWithErrors := true;
          end;
        until NEXT = 0;

        if not JournalWithErrors then
          MESSAGE(Text001)
        else
          MESSAGE(
            Text002 +
            Text003);

        if not FIND('=><') then begin
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP := 0;
          Name := '';
        end;
      end;
    END;

    BEGIN
    END.
  }
}

