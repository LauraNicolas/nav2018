OBJECT Report 491 Delete Invd Blnkt Purch Orders
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Delete Invd Blnkt Purch Orders;
    ProcessingOnly=true;
  }
  DATASET
  {
    { 4458;    ;DataItem;Purchase Header     ;
               DataItemTable="Purchase Header";
               DataItemTableView=SORTING("Document Type","No.") WHERE("Document Type"=CONST("Blanket Order"));
               ReqFilterHeadingML=ENU=Blanket Purchase Order;
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit "Approvals Mgmt.";
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  PurchLine.RESET;
                                  PurchLine.SETRANGE("Document Type","Document Type");
                                  PurchLine.SETRANGE("Document No.","No.");
                                  PurchLine.SETFILTER("Quantity Invoiced",'<>0');
                                  if PurchLine.FINDFIRST then begin
                                    PurchLine.SETRANGE("Quantity Invoiced");
                                    PurchLine.SETFILTER("Outstanding Quantity",'<>0');
                                    if not PurchLine.FINDFIRST then begin
                                      PurchLine.SETRANGE("Outstanding Quantity");
                                      PurchLine.SETFILTER("Qty. Rcd. Not Invoiced",'<>0');
                                      if not PurchLine.FINDFIRST then begin
                                        PurchLine.LOCKTABLE;
                                        if not PurchLine.FINDFIRST then begin
                                          PurchLine.SETRANGE("Qty. Rcd. Not Invoiced");
                                          PurchLine2.SETRANGE("Blanket Order No.","No.");
                                          if not PurchLine2.FINDFIRST then begin
                                            PurchLine.DELETEALL;

                                            PurchCommentLine.SETRANGE("Document Type","Document Type");
                                            PurchCommentLine.SETRANGE("No.","No.");
                                            PurchCommentLine.DELETEALL;

                                            ApprovalsMgmt.DeleteApprovalEntries(RECORDID);

                                            DELETE;

                                            COMMIT;
                                          end;
                                        end;
                                      end;
                                    end;
                                  end;
                                END;

               ReqFilterFields="No.","Buy-from Vendor No.","Pay-to Vendor No." }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing purch. orders #1##########';
      PurchLine@1001 : Record "Purchase Line";
      PurchLine2@1002 : Record "Purchase Line";
      PurchCommentLine@1003 : Record "Purch. Comment Line";
      Window@1005 : Dialog;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

