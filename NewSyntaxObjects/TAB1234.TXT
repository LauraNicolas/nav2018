OBJECT Table 1234 CSV Buffer
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=CSV Buffer;
  }
  FIELDS
  {
    { 1   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 2   ;   ;Field No.           ;Integer       ;CaptionML=ENU=Field No. }
    { 3   ;   ;Value               ;Text250       ;CaptionML=ENU=Value }
  }
  KEYS
  {
    {    ;"Line No.","Field No."                  ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      IndexDoesNotExistErr@1000 : TextConst '@@@="%1 = line no, %2 = index of the field";ENU=The field in line %1 with index %2 does not exist. The data could not be retrieved.';
      CSVFile@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File";
      StreamReader@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamReader";
      Separator@1004 : Text[1];

    [External]
    PROCEDURE InsertEntry@12(LineNo@1001 : Integer;FieldNo@1002 : Integer;FieldValue@1003 : Text[250]);
    BEGIN
      INIT;
      "Line No." := LineNo;
      "Field No." := FieldNo;
      Value := FieldValue;
      INSERT;
    END;

    [Internal]
    PROCEDURE LoadData@1(CSVFileName@1000 : Text;CSVFieldSeparator@1001 : Text[1]);
    BEGIN
      InitializeReader(CSVFileName,CSVFieldSeparator);
      ReadLines(0);
      StreamReader.Close;
    END;

    [Internal]
    PROCEDURE SaveData@10(CSVFileName@1000 : Text;CSVFieldSeparator@1001 : Text[1]);
    VAR
      FileMode@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.FileMode";
      StreamWriter@1004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamWriter";
      NumberOfColumns@1003 : Integer;
    BEGIN
      StreamWriter := StreamWriter.StreamWriter(CSVFile.Open(CSVFileName,FileMode.Create));
      NumberOfColumns := GetNumberOfColumns;
      if FINDSET then
        repeat
          StreamWriter.Write(Value);
          if "Field No." < NumberOfColumns then
            StreamWriter.Write(CSVFieldSeparator)
          else
            StreamWriter.WriteLine;
        until NEXT = 0;
      StreamWriter.Close;
    END;

    [Internal]
    PROCEDURE InitializeReader@2(CSVFileName@1000 : Text;CSVFieldSeparator@1001 : Text[1]);
    VAR
      Encoding@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding";
    BEGIN
      StreamReader := StreamReader.StreamReader(CSVFile.OpenRead(CSVFileName),Encoding.Default);
      Separator := CSVFieldSeparator;
    END;

    [Internal]
    PROCEDURE ReadLines@3(NumberOfLines@1000 : Integer) : Boolean;
    VAR
      String@1004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";
      CurrentLineNo@1002 : Integer;
      CurrentFieldNo@1003 : Integer;
      CurrentIndex@1005 : Integer;
      NextIndex@1006 : Integer;
      Length@1007 : Integer;
    BEGIN
      if StreamReader.EndOfStream then
        exit(false);
      repeat
        String := StreamReader.ReadLine;
        CurrentLineNo += 1;
        CurrentIndex := 0;
        repeat
          CurrentFieldNo += 1;
          INIT;
          "Line No." := CurrentLineNo;
          "Field No." := CurrentFieldNo;
          NextIndex := String.IndexOf(Separator,CurrentIndex);
          if NextIndex = -1 then
            Length := String.Length - CurrentIndex
          else
            Length := NextIndex - CurrentIndex;
          if Length > 250 then
            Length := 250;
          Value := String.Substring(CurrentIndex,Length);
          CurrentIndex := NextIndex + 1;
          INSERT;
        until NextIndex = -1;
        CurrentFieldNo := 0;
      until StreamReader.EndOfStream or (CurrentLineNo = NumberOfLines);
      exit(true);
    END;

    [External]
    PROCEDURE ResetFilters@5();
    BEGIN
      SETRANGE("Line No.");
      SETRANGE("Field No.");
      SETRANGE(Value);
    END;

    [External]
    PROCEDURE GetValue@6(LineNo@1000 : Integer;FieldNo@1001 : Integer) : Text[250];
    VAR
      TempCSVBuffer@1002 : TEMPORARY Record "CSV Buffer";
    BEGIN
      TempCSVBuffer.COPY(Rec,true);
      if not TempCSVBuffer.GET(LineNo,FieldNo) then
        ERROR(IndexDoesNotExistErr,LineNo,FieldNo);
      exit(TempCSVBuffer.Value);
    END;

    [External]
    PROCEDURE GetCSVLinesWhere@7(FilterFieldNo@1000 : Integer;FilterValue@1001 : Text;VAR TempResultCSVBuffer@1002 : TEMPORARY Record "CSV Buffer");
    VAR
      TempCSVBuffer@1004 : TEMPORARY Record "CSV Buffer";
    BEGIN
      TempResultCSVBuffer.RESET;
      TempResultCSVBuffer.DELETEALL;
      TempCSVBuffer.COPY(Rec,true);
      SETRANGE("Field No.",FilterFieldNo);
      SETRANGE(Value,FilterValue);
      if FINDSET then
        repeat
          TempCSVBuffer.SETRANGE("Line No.","Line No.");
          TempCSVBuffer.FINDSET;
          repeat
            TempResultCSVBuffer := TempCSVBuffer;
            TempResultCSVBuffer.INSERT;
          until TempCSVBuffer.NEXT = 0;
        until NEXT = 0;
      TempResultCSVBuffer.SETRANGE("Field No.",1);
    END;

    [External]
    PROCEDURE GetValueOfLineAt@8(FieldNo@1000 : Integer) : Text[250];
    VAR
      TempCSVBuffer@1001 : TEMPORARY Record "CSV Buffer";
    BEGIN
      TempCSVBuffer.COPY(Rec,true);
      if not TempCSVBuffer.GET("Line No.",FieldNo) then
        ERROR(IndexDoesNotExistErr,"Line No.",FieldNo);
      exit(TempCSVBuffer.Value);
    END;

    [External]
    PROCEDURE GetNumberOfColumns@4() : Integer;
    VAR
      TempCSVBuffer@1000 : TEMPORARY Record "CSV Buffer";
    BEGIN
      TempCSVBuffer.COPY(Rec,true);
      TempCSVBuffer.ResetFilters;
      TempCSVBuffer.SETRANGE("Line No.","Line No.");
      if TempCSVBuffer.FINDLAST then
        exit(TempCSVBuffer."Field No.");

      exit(0);
    END;

    [External]
    PROCEDURE GetNumberOfLines@9() : Integer;
    BEGIN
      if FINDLAST then
        exit("Line No.");

      exit(0);
    END;

    BEGIN
    END.
  }
}

