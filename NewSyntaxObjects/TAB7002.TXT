OBJECT Table 7002 Sales Price
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               if "Sales Type" = "Sales Type"::"All Customers" then
                 "Sales Code" := ''
               else
                 TESTFIELD("Sales Code");
               TESTFIELD("Item No.");
             END;

    OnRename=BEGIN
               if "Sales Type" <> "Sales Type"::"All Customers" then
                 TESTFIELD("Sales Code");
               TESTFIELD("Item No.");
             END;

    CaptionML=ENU=Sales Price;
    LookupPageID="Sales Prices";
  }
  FIELDS
  {
    { 1   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                if "Item No." <> xRec."Item No." then begin
                                                                  Item.GET("Item No.");
                                                                  "Unit of Measure Code" := Item."Sales Unit of Measure";
                                                                  "Variant Code" := '';
                                                                end;

                                                                if "Sales Type" = "Sales Type"::"Customer Price Group" then
                                                                  if CustPriceGr.GET("Sales Code") and
                                                                     (CustPriceGr."Allow Invoice Disc." = "Allow Invoice Disc.")
                                                                  then
                                                                    exit;

                                                                UpdateValuesFromItem;
                                                              END;

                                                   CaptionML=ENU=Item No.;
                                                   NotBlank=true }
    { 2   ;   ;Sales Code          ;Code20        ;TableRelation=IF ("Sales Type"=CONST("Customer Price Group")) "Customer Price Group"
                                                                 ELSE IF ("Sales Type"=CONST(Customer)) Customer
                                                                 ELSE IF ("Sales Type"=CONST(Campaign)) Campaign;
                                                   OnValidate=BEGIN
                                                                if "Sales Code" <> '' then
                                                                  case "Sales Type" of
                                                                    "Sales Type"::"All Customers":
                                                                      ERROR(Text001,FIELDCAPTION("Sales Code"));
                                                                    "Sales Type"::"Customer Price Group":
                                                                      begin
                                                                        CustPriceGr.GET("Sales Code");
                                                                        "Price Includes VAT" := CustPriceGr."Price Includes VAT";
                                                                        "VAT Bus. Posting Gr. (Price)" := CustPriceGr."VAT Bus. Posting Gr. (Price)";
                                                                        "Allow Line Disc." := CustPriceGr."Allow Line Disc.";
                                                                        "Allow Invoice Disc." := CustPriceGr."Allow Invoice Disc.";
                                                                      end;
                                                                    "Sales Type"::Customer:
                                                                      begin
                                                                        Cust.GET("Sales Code");
                                                                        "Currency Code" := Cust."Currency Code";
                                                                        "Price Includes VAT" := Cust."Prices Including VAT";
                                                                        "VAT Bus. Posting Gr. (Price)" := Cust."VAT Bus. Posting Group";
                                                                        "Allow Line Disc." := Cust."Allow Line Disc.";
                                                                      end;
                                                                    "Sales Type"::Campaign:
                                                                      begin
                                                                        Campaign.GET("Sales Code");
                                                                        "Starting Date" := Campaign."Starting Date";
                                                                        "Ending Date" := Campaign."Ending Date";
                                                                      end;
                                                                  end;
                                                              END;

                                                   CaptionML=ENU=Sales Code }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code }
    { 4   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                if ("Starting Date" > "Ending Date") and ("Ending Date" <> 0D) then
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));

                                                                if CurrFieldNo = 0 then
                                                                  exit;

                                                                if "Starting Date" <> 0D then
                                                                  if "Sales Type" = "Sales Type"::Campaign then
                                                                    ERROR(Text002,"Sales Type");
                                                              END;

                                                   CaptionML=ENU=Starting Date }
    { 5   ;   ;Unit Price          ;Decimal       ;CaptionML=ENU=Unit Price;
                                                   MinValue=0;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 7   ;   ;Price Includes VAT  ;Boolean       ;CaptionML=ENU=Price Includes VAT }
    { 10  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Allow Invoice Disc. }
    { 11  ;   ;VAT Bus. Posting Gr. (Price);Code20;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=VAT Bus. Posting Gr. (Price) }
    { 13  ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                if "Sales Type" <> xRec."Sales Type" then begin
                                                                  VALIDATE("Sales Code",'');
                                                                  UpdateValuesFromItem;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Sales Type;
                                                   OptionCaptionML=ENU=Customer,Customer Price Group,All Customers,Campaign;
                                                   OptionString=Customer,"Customer Price Group","All Customers",Campaign }
    { 14  ;   ;Minimum Quantity    ;Decimal       ;CaptionML=ENU=Minimum Quantity;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 15  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                if CurrFieldNo = 0 then
                                                                  exit;

                                                                VALIDATE("Starting Date");

                                                                if "Ending Date" <> 0D then
                                                                  if "Sales Type" = "Sales Type"::Campaign then
                                                                    ERROR(Text002,"Sales Type");
                                                              END;

                                                   CaptionML=ENU=Ending Date }
    { 5400;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE ("Item No."=FIELD("Item No."));
                                                   CaptionML=ENU=Unit of Measure Code }
    { 5700;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE ("Item No."=FIELD("Item No."));
                                                   CaptionML=ENU=Variant Code }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Allow Line Disc. }
  }
  KEYS
  {
    {    ;"Item No.","Sales Type","Sales Code","Starting Date","Currency Code","Variant Code","Unit of Measure Code","Minimum Quantity";
                                                   Clustered=true }
    {    ;"Sales Type","Sales Code","Item No.","Starting Date","Currency Code","Variant Code","Unit of Measure Code","Minimum Quantity" }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;"Sales Type","Sales Code","Item No.","Starting Date","Unit Price","Ending Date" }
  }
  CODE
  {
    VAR
      CustPriceGr@1000 : Record "Customer Price Group";
      Text000@1001 : TextConst 'ENU=%1 cannot be after %2';
      Cust@1002 : Record Customer;
      Text001@1003 : TextConst 'ENU=%1 must be blank.';
      Campaign@1005 : Record Campaign;
      Item@1004 : Record Item;
      Text002@1006 : TextConst 'ENU="If Sales Type = %1, then you can only change Starting Date and Ending Date from the Campaign Card."';

    LOCAL PROCEDURE UpdateValuesFromItem@1();
    BEGIN
      if Item.GET("Item No.") then begin
        "Allow Invoice Disc." := Item."Allow Invoice Disc.";
        if "Sales Type" = "Sales Type"::"All Customers" then begin
          "Price Includes VAT" := Item."Price Includes VAT";
          "VAT Bus. Posting Gr. (Price)" := Item."VAT Bus. Posting Gr. (Price)";
        end;
      end;
    END;

    PROCEDURE CopySalesPriceToCustomersSalesPrice@3(VAR SalesPrice@1000 : Record "Sales Price";CustNo@1002 : Code[20]);
    VAR
      NewSalesPrice@1001 : Record "Sales Price";
    BEGIN
      if SalesPrice.FINDSET then
        repeat
          NewSalesPrice := SalesPrice;
          NewSalesPrice."Sales Type" := NewSalesPrice."Sales Type"::Customer;
          NewSalesPrice."Sales Code" := CustNo;
          if NewSalesPrice.INSERT then;
        until SalesPrice.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

