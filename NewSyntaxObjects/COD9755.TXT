OBJECT Codeunit 9755 Media Resources Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE InsertMediaFromInstream@1(MediaResourceCode@1001 : Code[50];MediaInstream@1000 : InStream) : Boolean;
    VAR
      MediaResources@1002 : Record "Media Resources";
    BEGIN
      if MediaResources.GET(MediaResourceCode) then
        exit(true);

      MediaResources.INIT;
      MediaResources.VALIDATE(Code,MediaResourceCode);
      MediaResources."Media Reference".IMPORTSTREAM(MediaInstream,MediaResourceCode);
      exit(MediaResources.INSERT(true));
    END;

    PROCEDURE InsertMediaFromFile@2(MediaResourceCode@1001 : Code[50];FileName@1000 : Text) : Boolean;
    VAR
      MediaResources@1002 : Record "Media Resources";
    BEGIN
      if MediaResources.GET(MediaResourceCode) then
        exit(true);

      MediaResources.INIT;
      MediaResources.VALIDATE(Code,MediaResourceCode);
      MediaResources."Media Reference".IMPORTFILE(FileName,MediaResourceCode);
      exit(MediaResources.INSERT(true));
    END;

    PROCEDURE InsertMediaSetFromFile@3(MediaResourceCode@1001 : Code[50];FileName@1000 : Text) : Boolean;
    VAR
      MediaResources@1002 : Record "Media Resources";
    BEGIN
      if MediaResources.GET(MediaResourceCode) then
        exit(true);

      MediaResources.INIT;
      MediaResources.VALIDATE(Code,MediaResourceCode);
      MediaResources."MediaSet Reference".IMPORTFILE(FileName,MediaResourceCode);
      exit(MediaResources.INSERT(true));
    END;

    PROCEDURE InsertBLOBFromFile@4(FilePath@1007 : Text;FileName@1000 : Text) : Code[50];
    VAR
      MediaResources@1002 : Record "Media Resources";
      File@1003 : File;
      BLOBInStream@1004 : InStream;
      BLOBOutStream@1005 : OutStream;
      MediaResourceCode@1001 : Code[50];
    BEGIN
      MediaResourceCode := COPYSTR(FileName,1,MAXSTRLEN(MediaResourceCode));
      if MediaResources.GET(MediaResourceCode) then
        exit(MediaResourceCode);

      if not File.OPEN(FilePath + FileName) then
        exit('');
      File.CREATEINSTREAM(BLOBInStream);

      MediaResources.INIT;
      MediaResources.VALIDATE(Code,MediaResourceCode);
      MediaResources.Blob.CREATEOUTSTREAM(BLOBOutStream);
      COPYSTREAM(BLOBOutStream,BLOBInStream);
      File.CLOSE;
      MediaResources.INSERT(true);

      exit(MediaResourceCode);
    END;

    PROCEDURE InsertBlobFromText@5(MediaResourcesCode@1001 : Code[50];BlobContent@1000 : Text) : Boolean;
    VAR
      MediaResources@1003 : Record "Media Resources";
      TextOutStream@1002 : OutStream;
    BEGIN
      if MediaResources.GET(MediaResourcesCode) then
        exit(true);

      MediaResources.INIT;
      MediaResources.VALIDATE(Code,MediaResourcesCode);
      MediaResources.Blob.CREATEOUTSTREAM(TextOutStream);
      TextOutStream.WRITETEXT(BlobContent);

      exit(MediaResources.INSERT(true));
    END;

    PROCEDURE ReadTextFromMediaResource@6(MediaResourcesCode@1000 : Code[50]) MediaText : Text;
    VAR
      MediaResources@1002 : Record "Media Resources";
      TextInStream@1001 : InStream;
    BEGIN
      if not MediaResources.GET(MediaResourcesCode) then
        exit;

      MediaResources.CALCFIELDS(Blob);
      MediaResources.Blob.CREATEINSTREAM(TextInStream);
      TextInStream.READ(MediaText);
    END;

    BEGIN
    END.
  }
}

