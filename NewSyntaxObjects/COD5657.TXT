OBJECT Codeunit 5657 Make Ins. Coverage Ledg. Entry
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Ins. Coverage Ledger Entry"=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE CopyFromJnlLine@1(VAR InsCoverageLedgEntry@1000 : Record "Ins. Coverage Ledger Entry";VAR InsuranceJnlLine@1001 : Record "Insurance Journal Line");
    BEGIN
      with InsCoverageLedgEntry do begin
        "User ID" := USERID;
        "Insurance No." := InsuranceJnlLine."Insurance No.";
        "FA No." := InsuranceJnlLine."FA No.";
        "FA Description" := InsuranceJnlLine."FA Description";
        "Posting Date" := InsuranceJnlLine."Posting Date";
        "Document Type" := InsuranceJnlLine."Document Type";
        "Document Date" := InsuranceJnlLine."Document Date";
        if "Document Date" = 0D then
          "Document Date" := "Posting Date";
        "Document No." := InsuranceJnlLine."Document No.";
        "External Document No." := InsuranceJnlLine."External Document No.";
        Amount := InsuranceJnlLine.Amount;
        Description := InsuranceJnlLine.Description;
        "Index Entry" := InsuranceJnlLine."Index Entry";
        "Global Dimension 1 Code" := InsuranceJnlLine."Shortcut Dimension 1 Code";
        "Global Dimension 2 Code" := InsuranceJnlLine."Shortcut Dimension 2 Code";
        "Source Code" := InsuranceJnlLine."Source Code";
        "Journal Batch Name" := InsuranceJnlLine."Journal Batch Name";
        "Reason Code" := InsuranceJnlLine."Reason Code";
        "Disposed FA" := SetDisposedFA("FA No.");
        "No. Series" := InsuranceJnlLine."Posting No. Series";
      end;
    END;

    [External]
    PROCEDURE CopyFromInsuranceCard@2(VAR InsCoverageLedgEntry@1000 : Record "Ins. Coverage Ledger Entry";VAR Insurance@1001 : Record Insurance);
    BEGIN
      with InsCoverageLedgEntry do begin
        "FA Class Code" := Insurance."FA Class Code";
        "FA Subclass Code" := Insurance."FA Subclass Code";
        "FA Location Code" := Insurance."FA Location Code";
        "Location Code" := Insurance."Location Code";
      end;
    END;

    [External]
    PROCEDURE SetDisposedFA@3(FANo@1000 : Code[20]) : Boolean;
    VAR
      FASetup@1001 : Record "FA Setup";
      FADeprBook@1002 : Record "FA Depreciation Book";
    BEGIN
      FASetup.GET;
      FASetup.TESTFIELD("Insurance Depr. Book");
      if FADeprBook.GET(FANo,FASetup."Insurance Depr. Book") then
        exit(FADeprBook."Disposal Date" > 0D);

      exit(false);
    END;

    [External]
    PROCEDURE UpdateInsCoverageLedgerEntryFromFASetup@4(InsDeprBookCode@1000 : Code[10]);
    VAR
      InsCoverageLedgEntry@1001 : Record "Ins. Coverage Ledger Entry";
      FADeprBook@1002 : Record "FA Depreciation Book";
    BEGIN
      with InsCoverageLedgEntry do begin
        if ISEMPTY then
          exit;

        SETRANGE("Disposed FA",true);
        MODIFYALL("Disposed FA",false);
        if InsDeprBookCode <> '' then begin
          RESET;
          FADeprBook.SETRANGE("Depreciation Book Code",InsDeprBookCode);
          FADeprBook.SETFILTER("Disposal Date",'<>%1',0D);
          if FADeprBook.FINDSET then
            repeat
              SETRANGE("FA No.",FADeprBook."FA No.");
              MODIFYALL("Disposed FA",true)
            until FADeprBook.NEXT = 0;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

