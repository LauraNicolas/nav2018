OBJECT Table 5902 Service Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               if TempTrackingSpecification.FINDFIRST then
                 InsertItemTracking;

               if Quantity <> 0 then
                 ReserveServLine.VerifyQuantity(Rec,xRec);

               if Type = Type::Item then
                 if ServHeader.InventoryPickConflict("Document Type","Document No.",ServHeader."Shipping Advice") then
                   DisplayConflictError(ServHeader.InvPickConflictResolutionTxt);

               IsCustCrLimitChecked := false;
             END;

    OnModify=BEGIN
               if "Document Type" = ServiceLine."Document Type"::Invoice then
                 CheckIfCanBeModified;

               if "Spare Part Action" in
                  ["Spare Part Action"::"Component Replaced",
                   "Spare Part Action"::"Component Installed",
                   "Spare Part Action"::" "]
               then begin
                 if (Type <> xRec.Type) or ("No." <> xRec."No.") then
                   ReserveServLine.DeleteLine(Rec);
                 UpdateReservation(0);
               end;

               UpdateServiceLedgerEntry;
               IsCustCrLimitChecked := false;
             END;

    OnDelete=VAR
               ServiceLine2@1000 : Record "Service Line";
             BEGIN
               TestStatusOpen;
               if Type = Type::Item then
                 WhseValidateSourceLine.ServiceLineDelete(Rec);
               if Type in [Type::"G/L Account",Type::Cost,Type::Resource] then
                 TESTFIELD("Qty. Shipped Not Invoiced",0);

               if ("Document Type" = "Document Type"::Invoice) and ("Appl.-to Service Entry" > 0) then
                 ERROR(Text045);

               if (Quantity <> 0) and ItemExists("No.") then begin
                 ReserveServLine.DeleteLine(Rec);
                 CALCFIELDS("Reserved Qty. (Base)");
                 TESTFIELD("Reserved Qty. (Base)",0);
                 if "Shipment No." = '' then
                   TESTFIELD("Qty. Shipped Not Invoiced",0);
               end;

               ReserveServLine.DeleteLine(Rec);
               if (Type = Type::Item) and Item.GET("No.") then
                 NonstockItemMgt.DelNonStockFSM(Rec);

               if (Type <> Type::" ") and
                  (("Contract No." <> '') or
                   ("Shipment No." <> ''))
               then
                 UpdateServDocRegister(true);

               if "Line No." <> 0 then begin
                 ServiceLine2.RESET;
                 ServiceLine2.SETRANGE("Document Type","Document Type");
                 ServiceLine2.SETRANGE("Document No.","Document No.");
                 ServiceLine2.SETRANGE("Attached to Line No.","Line No.");
                 ServiceLine2.SETFILTER("Line No.",'<>%1',"Line No.");
                 ServiceLine2.DELETEALL(true);
               end;
             END;

    OnRename=BEGIN
               ERROR(Text002,TABLECAPTION);
             END;

    CaptionML=ENU=Service Line;
    LookupPageID="Service Line List";
    DrillDownPageID="Service Line List";
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo;
                                                   OptionString=Quote,Order,Invoice,"Credit Memo" }
    { 2   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Customer No.;
                                                   Editable=false }
    { 3   ;   ;Document No.        ;Code20        ;TableRelation="Service Header"."No." WHERE ("Document Type"=FIELD("Document Type"));
                                                   CaptionML=ENU=Document No. }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 5   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CheckIfCanBeModified;

                                                                GetServHeader;
                                                                TestStatusOpen;
                                                                TESTFIELD("Qty. Shipped Not Invoiced",0);
                                                                TESTFIELD("Quantity Shipped",0);
                                                                TESTFIELD("Shipment No.",'');

                                                                if xRec.Type = xRec.Type::Item then
                                                                  WhseValidateSourceLine.ServiceLineVerifyChange(Rec,xRec);

                                                                if Type = Type::Item then begin
                                                                  GetLocation("Location Code");
                                                                  Location.TESTFIELD("Directed Put-away and Pick",false);
                                                                end;

                                                                UpdateReservation(FIELDNO(Type));

                                                                ServiceLine := Rec;

                                                                if "Document Type" in ["Document Type"::Invoice,"Document Type"::"Credit Memo"] then
                                                                  UpdateServDocRegister(true);
                                                                Clearfields;

                                                                "Currency Code" := ServiceLine."Currency Code";
                                                                VALIDATE("Service Item Line No.",ServiceLine."Service Item Line No.");

                                                                if Type = Type::Item then begin
                                                                  if ServHeader.InventoryPickConflict("Document Type","Document No.",ServHeader."Shipping Advice") then
                                                                    DisplayConflictError(ServHeader.InvPickConflictResolutionTxt);
                                                                  if ServHeader.WhseShpmntConflict("Document Type","Document No.",ServHeader."Shipping Advice") then
                                                                    DisplayConflictError(ServHeader.WhseShpmtConflictResolutionTxt);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,Item,Resource,Cost,G/L Account";
                                                   OptionString=" ",Item,Resource,Cost,"G/L Account" }
    { 6   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(" ")) "Standard Text"
                                                                 ELSE IF (Type=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF (Type=CONST(Item)) Item WHERE (Type=CONST(Inventory))
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST(Cost)) "Service Cost";
                                                   OnValidate=BEGIN
                                                                CheckIfCanBeModified;

                                                                TESTFIELD("Qty. Shipped Not Invoiced",0);
                                                                TESTFIELD("Quantity Shipped",0);
                                                                TESTFIELD("Shipment No.",'');
                                                                CheckItemAvailable(FIELDNO("No."));
                                                                TestStatusOpen;

                                                                Clearfields;

                                                                UpdateReservation(FIELDNO("No."));

                                                                if "No." = '' then
                                                                  exit;

                                                                GetServHeader;

                                                                if ServHeader."Document Type" = ServHeader."Document Type"::Quote then begin
                                                                  if ServHeader."Customer No." = '' then
                                                                    ERROR(
                                                                      Text031,
                                                                      ServHeader.FIELDCAPTION("Customer No."));
                                                                  if ServHeader."Bill-to Customer No." = '' then
                                                                    ERROR(
                                                                      Text031,
                                                                      ServHeader.FIELDCAPTION("Bill-to Customer No."));
                                                                end else
                                                                  ServHeader.TESTFIELD("Customer No.");

                                                                InitHeaderDefaults(ServHeader);

                                                                if "Service Item Line No." <> 0 then begin
                                                                  ServItemLine.GET("Document Type","Document No.","Service Item Line No.");
                                                                  VALIDATE("Contract No.",ServItemLine."Contract No.")
                                                                end else
                                                                  VALIDATE("Contract No.",ServHeader."Contract No.");

                                                                case Type of
                                                                  Type::" ":
                                                                    CopyFromStdTxt;
                                                                  Type::"G/L Account":
                                                                    CopyFromGLAccount;
                                                                  Type::Cost:
                                                                    CopyFromCost;
                                                                  Type::Item:
                                                                    begin
                                                                      CopyFromItem;
                                                                      if ServItem.GET("Service Item No.") then
                                                                        CopyFromServItem;
                                                                    end;
                                                                  Type::Resource:
                                                                    CopyFromResource;
                                                                end;

                                                                if Type <> Type::" " then begin
                                                                  VALIDATE("VAT Prod. Posting Group");
                                                                  VALIDATE("Unit of Measure Code");
                                                                  if Quantity <> 0 then begin
                                                                    InitOutstanding;
                                                                    if "Document Type" = "Document Type"::"Credit Memo" then
                                                                      InitQtyToInvoice
                                                                    else
                                                                      InitQtyToShip;
                                                                    UpdateWithWarehouseShip;
                                                                  end;
                                                                  UpdateUnitPrice(FIELDNO("No."));
                                                                  AdjustMaxLabourUnitPrice("Unit Price");

                                                                  if (Type <> Type::Cost) and
                                                                     not ReplaceServItemAction
                                                                  then
                                                                    VALIDATE(Quantity,xRec.Quantity);
                                                                  UpdateAmounts;
                                                                end;
                                                                UpdateReservation(FIELDNO("No."));

                                                                GetDefaultBin;

                                                                if not ISTEMPORARY then
                                                                  CreateDim(
                                                                    DimMgt.TypeToTableID5(Type),"No.",
                                                                    DATABASE::Job,"Job No.",
                                                                    DATABASE::"Responsibility Center","Responsibility Center");

                                                                if ServiceLine.GET("Document Type","Document No.","Line No.") then
                                                                  MODIFY;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 7   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                UpdateWithWarehouseShip;
                                                                GetServHeader;
                                                                if Type = Type::Item then begin
                                                                  if Quantity <> 0 then
                                                                    WhseValidateSourceLine.ServiceLineVerifyChange(Rec,xRec);
                                                                  if "Location Code" <> xRec."Location Code" then begin
                                                                    TESTFIELD("Reserved Quantity",0);
                                                                    TESTFIELD("Shipment No.",'');
                                                                    TESTFIELD("Qty. Shipped Not Invoiced",0);
                                                                    CheckItemAvailable(FIELDNO("Location Code"));
                                                                    UpdateReservation(FIELDNO("Location Code"));
                                                                  end;
                                                                  GetUnitCost;
                                                                end;
                                                                GetDefaultBin;
                                                              END;

                                                   CaptionML=ENU=Location Code }
    { 8   ;   ;Posting Group       ;Code20        ;TableRelation=IF (Type=CONST(Item)) "Inventory Posting Group";
                                                   CaptionML=ENU=Posting Group;
                                                   Editable=false }
    { 11  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 12  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2 }
    { 13  ;   ;Unit of Measure     ;Text10        ;CaptionML=ENU=Unit of Measure }
    { 15  ;   ;Quantity            ;Decimal       ;OnValidate=VAR
                                                                ItemLedgEntry@1000 : Record "Item Ledger Entry";
                                                              BEGIN
                                                                GetServHeader;
                                                                TESTFIELD(Type);
                                                                TESTFIELD("No.");
                                                                TestStatusOpen;

                                                                if Quantity < 0 then
                                                                  FIELDERROR(Quantity,Text029);

                                                                case "Spare Part Action" of
                                                                  "Spare Part Action"::Permanent,"Spare Part Action"::"Temporary":
                                                                    if Quantity <> 1 then
                                                                      ERROR(Text011,ServItem.TABLECAPTION);
                                                                  "Spare Part Action"::"Component Replaced","Spare Part Action"::"Component Installed":
                                                                    if Quantity <> ROUND(Quantity,1) then
                                                                      ERROR(Text026,FIELDCAPTION(Quantity));
                                                                end;

                                                                "Quantity (Base)" := CalcBaseQty(Quantity);

                                                                if "Document Type" <> "Document Type"::"Credit Memo" then begin
                                                                  if (Quantity * "Quantity Shipped" < 0) or
                                                                     ((ABS(Quantity) < ABS("Quantity Shipped")) and ("Shipment No." = ''))
                                                                  then
                                                                    FIELDERROR(Quantity,STRSUBSTNO(Text003,FIELDCAPTION("Quantity Shipped")));
                                                                  if ("Quantity (Base)" * "Qty. Shipped (Base)" < 0) or
                                                                     ((ABS("Quantity (Base)") < ABS("Qty. Shipped (Base)")) and ("Shipment No." = ''))
                                                                  then
                                                                    FIELDERROR("Quantity (Base)",STRSUBSTNO(Text003,FIELDCAPTION("Qty. Shipped (Base)")));
                                                                end;

                                                                if (xRec.Quantity <> Quantity) or (xRec."Quantity (Base)" <> "Quantity (Base)") then begin
                                                                  InitOutstanding;
                                                                  if "Document Type" = "Document Type"::"Credit Memo" then
                                                                    InitQtyToInvoice
                                                                  else
                                                                    InitQtyToShip;
                                                                end;
                                                                CheckItemAvailable(FIELDNO(Quantity));

                                                                if (Quantity * xRec.Quantity < 0) or (Quantity = 0) then
                                                                  InitItemAppl(false);

                                                                if Type = Type::Item then begin
                                                                  WhseValidateSourceLine.ServiceLineVerifyChange(Rec,xRec);
                                                                  UpdateUnitPrice(FIELDNO(Quantity));
                                                                  UpdateReservation(FIELDNO(Quantity));
                                                                  UpdateWithWarehouseShip;
                                                                  if ("Quantity (Base)" * xRec."Quantity (Base)" <= 0) and ("No." <> '') then begin
                                                                    GetItem;
                                                                    if (Item."Costing Method" = Item."Costing Method"::Standard) and not IsShipment then
                                                                      GetUnitCost;
                                                                  end;
                                                                  if ("Appl.-from Item Entry" <> 0) and (xRec.Quantity < Quantity) then
                                                                    CheckApplFromItemLedgEntry(ItemLedgEntry);
                                                                end else
                                                                  VALIDATE("Line Discount %");

                                                                if (xRec.Quantity <> Quantity) and (Quantity = 0) and
                                                                   ((Amount <> 0) or
                                                                    ("Amount Including VAT" <> 0) or
                                                                    ("VAT Base Amount" <> 0))
                                                                then begin
                                                                  Amount := 0;
                                                                  "Amount Including VAT" := 0;
                                                                  "VAT Base Amount" := 0;
                                                                end;
                                                                if "Job Planning Line No." <> 0 then
                                                                  VALIDATE("Job Planning Line No.");
                                                              END;

                                                   CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5 }
    { 16  ;   ;Outstanding Quantity;Decimal       ;CaptionML=ENU=Outstanding Quantity;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 17  ;   ;Qty. to Invoice     ;Decimal       ;OnValidate=BEGIN
                                                                if "Qty. to Invoice" < 0 then
                                                                  FIELDERROR("Qty. to Invoice",Text029);

                                                                if "Qty. to Invoice" > 0 then begin
                                                                  "Qty. to Consume" := 0;
                                                                  "Qty. to Consume (Base)" := 0;
                                                                end;

                                                                if "Qty. to Invoice" = MaxQtyToInvoice then
                                                                  InitQtyToInvoice
                                                                else
                                                                  "Qty. to Invoice (Base)" := CalcBaseQty("Qty. to Invoice");
                                                                if ("Qty. to Invoice" * Quantity < 0) or
                                                                   (ABS("Qty. to Invoice") > ABS(MaxQtyToInvoice))
                                                                then
                                                                  ERROR(
                                                                    Text000,
                                                                    MaxQtyToInvoice);
                                                                if ("Qty. to Invoice (Base)" * "Quantity (Base)" < 0) or
                                                                   (ABS("Qty. to Invoice (Base)") > ABS(MaxQtyToInvoiceBase))
                                                                then
                                                                  ERROR(
                                                                    Text001,
                                                                    MaxQtyToInvoiceBase);
                                                                "VAT Difference" := 0;
                                                                CalcInvDiscToInvoice;
                                                                UpdateAmounts;
                                                              END;

                                                   CaptionML=ENU=Qty. to Invoice;
                                                   DecimalPlaces=0:5 }
    { 18  ;   ;Qty. to Ship        ;Decimal       ;OnValidate=BEGIN
                                                                if "Qty. to Ship" < 0 then
                                                                  FIELDERROR("Qty. to Ship",Text029);

                                                                if (CurrFieldNo <> 0) and
                                                                   (Type = Type::Item) and
                                                                   ("Qty. to Ship" <> 0)
                                                                then
                                                                  CheckWarehouse;

                                                                if "Qty. to Ship" = "Outstanding Quantity" then begin
                                                                  if not LineRequiresShipmentOrReceipt then
                                                                    InitQtyToShip
                                                                  else
                                                                    "Qty. to Ship (Base)" := CalcBaseQty("Qty. to Ship");
                                                                  if "Qty. to Consume" <> 0 then
                                                                    VALIDATE("Qty. to Consume","Qty. to Ship")
                                                                  else
                                                                    VALIDATE("Qty. to Consume",0);
                                                                end else begin
                                                                  "Qty. to Ship (Base)" := CalcBaseQty("Qty. to Ship");
                                                                  if "Qty. to Consume" <> 0 then
                                                                    VALIDATE("Qty. to Consume","Qty. to Ship")
                                                                  else
                                                                    VALIDATE("Qty. to Consume",0);
                                                                end;
                                                                if ((("Qty. to Ship" < 0) xor (Quantity < 0)) and (Quantity <> 0) and ("Qty. to Ship" <> 0)) or
                                                                   (ABS("Qty. to Ship") > ABS("Outstanding Quantity")) or
                                                                   (((Quantity < 0) xor ("Outstanding Quantity" < 0)) and (Quantity <> 0) and ("Outstanding Quantity" <> 0))
                                                                then
                                                                  ERROR(
                                                                    Text016,
                                                                    "Outstanding Quantity");
                                                                if ((("Qty. to Ship (Base)" < 0) xor ("Quantity (Base)" < 0)) and ("Qty. to Ship (Base)" <> 0) and ("Quantity (Base)" <> 0)) or
                                                                   (ABS("Qty. to Ship (Base)") > ABS("Outstanding Qty. (Base)")) or
                                                                   ((("Quantity (Base)" < 0) xor ("Outstanding Qty. (Base)" < 0)) and ("Quantity (Base)" <> 0) and ("Outstanding Qty. (Base)" <> 0))
                                                                then
                                                                  ERROR(
                                                                    Text017,
                                                                    "Outstanding Qty. (Base)");
                                                              END;

                                                   CaptionML=ENU=Qty. to Ship;
                                                   DecimalPlaces=0:5 }
    { 22  ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                GetServHeader;
                                                                if ("Appl.-to Service Entry" > 0) and (CurrFieldNo <> 0) then
                                                                  ERROR(Text052,FIELDCAPTION("Unit Price"));
                                                                if ("Unit Price" > ServHeader."Max. Labor Unit Price") and
                                                                   (Type = Type::Resource) and
                                                                   (ServHeader."Max. Labor Unit Price" <> 0)
                                                                then
                                                                  ERROR(
                                                                    Text022,
                                                                    FIELDCAPTION("Unit Price"),ServHeader.FIELDCAPTION("Max. Labor Unit Price"),
                                                                    ServHeader.TABLECAPTION);

                                                                VALIDATE("Line Discount %");
                                                              END;

                                                   CaptionML=ENU=Unit Price;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code";
                                                   CaptionClass=GetCaptionClass(FIELDNO("Unit Price")) }
    { 23  ;   ;Unit Cost (LCY)     ;Decimal       ;OnValidate=BEGIN
                                                                GetServHeader;
                                                                Currency.Initialize("Currency Code");
                                                                if "Unit Cost (LCY)" <> xRec."Unit Cost (LCY)" then
                                                                  if (CurrFieldNo = FIELDNO("Unit Cost (LCY)")) and
                                                                     (Type = Type::Item) and ("No." <> '') and ("Quantity (Base)" <> 0)
                                                                  then begin
                                                                    GetItem;
                                                                    if (Item."Costing Method" = Item."Costing Method"::Standard) and not IsShipment then begin
                                                                      if "Document Type" in ["Document Type"::"Credit Memo"] then
                                                                        ERROR(
                                                                          Text037,
                                                                          FIELDCAPTION("Unit Cost (LCY)"),Item.FIELDCAPTION("Costing Method"),
                                                                          Item."Costing Method",FIELDCAPTION(Quantity));
                                                                      ERROR(
                                                                        Text038,
                                                                        FIELDCAPTION("Unit Cost (LCY)"),Item.FIELDCAPTION("Costing Method"),
                                                                        Item."Costing Method",FIELDCAPTION(Quantity));
                                                                    end;
                                                                  end;

                                                                if "Currency Code" <> '' then begin
                                                                  Currency.TESTFIELD("Unit-Amount Rounding Precision");
                                                                  "Unit Cost" :=
                                                                    ROUND(
                                                                      CurrExchRate.ExchangeAmtLCYToFCY(
                                                                        GetDate,"Currency Code","Unit Cost (LCY)",
                                                                        ServHeader."Currency Factor"),Currency."Unit-Amount Rounding Precision")
                                                                end else
                                                                  "Unit Cost" := "Unit Cost (LCY)";

                                                                UpdateRemainingCostsAndAmounts;
                                                              END;

                                                   CaptionML=ENU=Unit Cost (LCY);
                                                   AutoFormatType=2 }
    { 25  ;   ;VAT %               ;Decimal       ;CaptionML=ENU=VAT %;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 27  ;   ;Line Discount %     ;Decimal       ;OnValidate=BEGIN
                                                                if CurrFieldNo = FIELDNO("Line Discount %") then
                                                                  TestStatusOpen;
                                                                GetServHeader;
                                                                if (CurrFieldNo in
                                                                    [FIELDNO("Line Discount %"),
                                                                     FIELDNO("Line Discount Amount"),
                                                                     FIELDNO("Line Amount")]) and
                                                                   ("Document Type" <> "Document Type"::Invoice)
                                                                then
                                                                  CheckLineDiscount("Line Discount %");

                                                                "Line Discount Amount" :=
                                                                  ROUND(
                                                                    ROUND(CalcChargeableQty * "Unit Price",Currency."Amount Rounding Precision") *
                                                                    "Line Discount %" / 100,Currency."Amount Rounding Precision");
                                                                "Inv. Discount Amount" := 0;
                                                                "Inv. Disc. Amount to Invoice" := 0;

                                                                UpdateAmounts;
                                                              END;

                                                   CaptionML=ENU=Line Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 28  ;   ;Line Discount Amount;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                GetServHeader;
                                                                TESTFIELD(Quantity);
                                                                if "Line Discount Amount" <> xRec."Line Discount Amount" then
                                                                  if ROUND(CalcChargeableQty * "Unit Price",Currency."Amount Rounding Precision") <> 0 then
                                                                    "Line Discount %" :=
                                                                      ROUND(
                                                                        "Line Discount Amount" /
                                                                        ROUND(CalcChargeableQty * "Unit Price",
                                                                          Currency."Amount Rounding Precision") * 100,0.00001)
                                                                  else
                                                                    "Line Discount %" := 0;
                                                                "Inv. Discount Amount" := 0;
                                                                "Inv. Disc. Amount to Invoice" := 0;
                                                                VALIDATE("Line Discount %");
                                                              END;

                                                   CaptionML=ENU=Line Discount Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 29  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                GetServHeader;
                                                                Amount := ROUND(Amount,Currency."Amount Rounding Precision");
                                                                case "VAT Calculation Type" of
                                                                  "VAT Calculation Type"::"Normal VAT",
                                                                  "VAT Calculation Type"::"Reverse Charge VAT":
                                                                    begin
                                                                      "VAT Base Amount" :=
                                                                        ROUND(Amount * (1 - ServHeader."VAT Base Discount %" / 100),Currency."Amount Rounding Precision");
                                                                      "Amount Including VAT" :=
                                                                        ROUND(Amount + "VAT Base Amount" * "VAT %" / 100,Currency."Amount Rounding Precision");
                                                                    end;
                                                                  "VAT Calculation Type"::"Full VAT":
                                                                    if Amount <> 0 then
                                                                      FIELDERROR(Amount,
                                                                        STRSUBSTNO(
                                                                          Text013,FIELDCAPTION("VAT Calculation Type"),
                                                                          "VAT Calculation Type"));
                                                                  "VAT Calculation Type"::"Sales Tax":
                                                                    begin
                                                                      ServHeader.TESTFIELD("VAT Base Discount %",0);
                                                                      "VAT Base Amount" := ROUND(Amount,Currency."Amount Rounding Precision");
                                                                      "Amount Including VAT" :=
                                                                        Amount +
                                                                        SalesTaxCalculate.CalculateTax(
                                                                          "Tax Area Code","Tax Group Code","Tax Liable",ServHeader."Posting Date",
                                                                          "VAT Base Amount","Quantity (Base)",ServHeader."Currency Factor");
                                                                      if "VAT Base Amount" <> 0 then
                                                                        "VAT %" :=
                                                                          ROUND(100 * ("Amount Including VAT" - "VAT Base Amount") / "VAT Base Amount",0.00001)
                                                                      else
                                                                        "VAT %" := 0;
                                                                      "Amount Including VAT" := ROUND("Amount Including VAT",Currency."Amount Rounding Precision");
                                                                    end;
                                                                end;

                                                                InitOutstandingAmount;
                                                              END;

                                                   CaptionML=ENU=Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 30  ;   ;Amount Including VAT;Decimal       ;OnValidate=BEGIN
                                                                GetServHeader;
                                                                "Amount Including VAT" := ROUND("Amount Including VAT",Currency."Amount Rounding Precision");
                                                                case "VAT Calculation Type" of
                                                                  "VAT Calculation Type"::"Normal VAT",
                                                                  "VAT Calculation Type"::"Reverse Charge VAT":
                                                                    begin
                                                                      Amount :=
                                                                        ROUND(
                                                                          "Amount Including VAT" /
                                                                          (1 + (1 - ServHeader."VAT Base Discount %" / 100) * "VAT %" / 100),
                                                                          Currency."Amount Rounding Precision");
                                                                      "VAT Base Amount" :=
                                                                        ROUND(Amount * (1 - ServHeader."VAT Base Discount %" / 100),Currency."Amount Rounding Precision");
                                                                    end;
                                                                  "VAT Calculation Type"::"Full VAT":
                                                                    begin
                                                                      Amount := 0;
                                                                      "VAT Base Amount" := 0;
                                                                    end;
                                                                  "VAT Calculation Type"::"Sales Tax":
                                                                    begin
                                                                      ServHeader.TESTFIELD("VAT Base Discount %",0);
                                                                      Amount :=
                                                                        SalesTaxCalculate.ReverseCalculateTax(
                                                                          "Tax Area Code","Tax Group Code","Tax Liable",ServHeader."Posting Date",
                                                                          "Amount Including VAT","Quantity (Base)",ServHeader."Currency Factor");
                                                                      if Amount <> 0 then
                                                                        "VAT %" :=
                                                                          ROUND(100 * ("Amount Including VAT" - Amount) / Amount,0.00001)
                                                                      else
                                                                        "VAT %" := 0;
                                                                      Amount := ROUND(Amount,Currency."Amount Rounding Precision");
                                                                      "VAT Base Amount" := Amount;
                                                                    end;
                                                                end;

                                                                InitOutstandingAmount;
                                                              END;

                                                   CaptionML=ENU=Amount Including VAT;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 32  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=true;
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                if ("Allow Invoice Disc." <> xRec."Allow Invoice Disc.") and
                                                                   not "Allow Invoice Disc."
                                                                then begin
                                                                  "Inv. Discount Amount" := 0;
                                                                  "Inv. Disc. Amount to Invoice" := 0;
                                                                  UpdateAmounts;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Allow Invoice Disc. }
    { 34  ;   ;Gross Weight        ;Decimal       ;CaptionML=ENU=Gross Weight;
                                                   DecimalPlaces=0:5 }
    { 35  ;   ;Net Weight          ;Decimal       ;CaptionML=ENU=Net Weight;
                                                   DecimalPlaces=0:5 }
    { 36  ;   ;Units per Parcel    ;Decimal       ;CaptionML=ENU=Units per Parcel;
                                                   DecimalPlaces=0:5 }
    { 37  ;   ;Unit Volume         ;Decimal       ;CaptionML=ENU=Unit Volume;
                                                   DecimalPlaces=0:5 }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;OnValidate=VAR
                                                                ItemLedgEntry@1000 : Record "Item Ledger Entry";
                                                              BEGIN
                                                                if "Appl.-to Item Entry" <> 0 then begin
                                                                  TESTFIELD(Type,Type::Item);
                                                                  TESTFIELD(Quantity);

                                                                  ItemLedgEntry.GET("Appl.-to Item Entry");
                                                                  ItemLedgEntry.TESTFIELD(Positive,true);
                                                                  VALIDATE("Unit Cost (LCY)",CalcUnitCost(ItemLedgEntry));
                                                                  "Location Code" := ItemLedgEntry."Location Code";
                                                                  if not ItemLedgEntry.Open then
                                                                    MESSAGE(Text042,"Appl.-to Item Entry");
                                                                end;
                                                              END;

                                                   OnLookup=BEGIN
                                                              SelectItemEntry(FIELDNO("Appl.-to Item Entry"));
                                                            END;

                                                   AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Appl.-to Item Entry }
    { 40  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1),
                                                                                               Blocked=CONST(false));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 41  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2),
                                                                                               Blocked=CONST(false));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 42  ;   ;Customer Price Group;Code10        ;TableRelation="Customer Price Group";
                                                   OnValidate=BEGIN
                                                                if Type = Type::Item then
                                                                  UpdateUnitPrice(FIELDNO("Customer Price Group"));
                                                              END;

                                                   CaptionML=ENU=Customer Price Group;
                                                   Editable=false }
    { 45  ;   ;Job No.             ;Code20        ;TableRelation=Job."No." WHERE ("Bill-to Customer No."=FIELD("Bill-to Customer No."));
                                                   OnValidate=VAR
                                                                Job@1000 : Record Job;
                                                              BEGIN
                                                                TESTFIELD("Quantity Consumed",0);
                                                                VALIDATE("Job Task No.",'');

                                                                if "Job No." <> '' then begin
                                                                  Job.GET("Job No.");
                                                                  Job.TestBlocked;
                                                                end;

                                                                CreateDim(
                                                                  DATABASE::Job,"Job No.",
                                                                  DimMgt.TypeToTableID5(Type),"No.",
                                                                  DATABASE::"Responsibility Center","Responsibility Center");
                                                              END;

                                                   CaptionML=ENU=Job No. }
    { 46  ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE ("Job No."=FIELD("Job No."));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Quantity Consumed",0);
                                                                if "Job Task No." = '' then
                                                                  "Job Line Type" := "Job Line Type"::" ";

                                                                if "Job Task No." <> xRec."Job Task No." then
                                                                  VALIDATE("Job Planning Line No.",0);
                                                              END;

                                                   CaptionML=ENU=Job Task No. }
    { 47  ;   ;Job Line Type       ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("Quantity Consumed",0);
                                                                TESTFIELD("Job No.");
                                                                TESTFIELD("Job Task No.");
                                                                if "Job Planning Line No." <> 0 then
                                                                  ERROR(Text048,FIELDCAPTION("Job Line Type"),FIELDCAPTION("Job Planning Line No."));
                                                              END;

                                                   CaptionML=ENU=Job Line Type;
                                                   OptionCaptionML=ENU=" ,Budget,Billable,Both Budget and Billable";
                                                   OptionString=" ",Budget,Billable,"Both Budget and Billable" }
    { 52  ;   ;Work Type Code      ;Code10        ;TableRelation="Work Type";
                                                   OnValidate=VAR
                                                                WorkType@1000 : Record "Work Type";
                                                              BEGIN
                                                                if Type = Type::Resource then begin
                                                                  TestStatusOpen;
                                                                  if WorkType.GET("Work Type Code") then
                                                                    VALIDATE("Unit of Measure Code",WorkType."Unit of Measure Code");
                                                                  UpdateUnitPrice(FIELDNO("Work Type Code"));
                                                                  VALIDATE("Unit Price");
                                                                  FindResUnitCost;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Work Type Code }
    { 57  ;   ;Outstanding Amount  ;Decimal       ;OnValidate=VAR
                                                                Currency2@1000 : Record Currency;
                                                              BEGIN
                                                                GetServHeader;
                                                                Currency2.InitRoundingPrecision;
                                                                if ServHeader."Currency Code" <> '' then
                                                                  "Outstanding Amount (LCY)" :=
                                                                    ROUND(
                                                                      CurrExchRate.ExchangeAmtFCYToLCY(
                                                                        GetDate,"Currency Code",
                                                                        "Outstanding Amount",ServHeader."Currency Factor"),
                                                                      Currency2."Amount Rounding Precision")
                                                                else
                                                                  "Outstanding Amount (LCY)" :=
                                                                    ROUND("Outstanding Amount",Currency2."Amount Rounding Precision");
                                                              END;

                                                   CaptionML=ENU=Outstanding Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 58  ;   ;Qty. Shipped Not Invoiced;Decimal  ;CaptionML=ENU=Qty. Shipped Not Invoiced;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 59  ;   ;Shipped Not Invoiced;Decimal       ;OnValidate=VAR
                                                                Currency2@1000 : Record Currency;
                                                              BEGIN
                                                                GetServHeader;
                                                                Currency2.InitRoundingPrecision;
                                                                if ServHeader."Currency Code" <> '' then
                                                                  "Shipped Not Invoiced (LCY)" :=
                                                                    ROUND(
                                                                      CurrExchRate.ExchangeAmtFCYToLCY(
                                                                        GetDate,"Currency Code",
                                                                        "Shipped Not Invoiced",ServHeader."Currency Factor"),
                                                                      Currency2."Amount Rounding Precision")
                                                                else
                                                                  "Shipped Not Invoiced (LCY)" :=
                                                                    ROUND("Shipped Not Invoiced",Currency2."Amount Rounding Precision");
                                                              END;

                                                   CaptionML=ENU=Shipped Not Invoiced;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 60  ;   ;Quantity Shipped    ;Decimal       ;CaptionML=ENU=Quantity Shipped;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 61  ;   ;Quantity Invoiced   ;Decimal       ;CaptionML=ENU=Quantity Invoiced;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 63  ;   ;Shipment No.        ;Code20        ;OnValidate=VAR
                                                                ServShptHeader@1000 : Record "Service Shipment Header";
                                                                ServDocReg@1001 : Record "Service Document Register";
                                                              BEGIN
                                                                if "Shipment No." <> xRec."Shipment No." then begin
                                                                  if "Shipment No." <> '' then begin
                                                                    GetServHeader;
                                                                    if "Document Type" = "Document Type"::"Credit Memo" then begin
                                                                      ServShptHeader.RESET;
                                                                      ServShptHeader.SETCURRENTKEY("Customer No.","Posting Date");
                                                                      ServShptHeader.SETRANGE("Customer No.",ServHeader."Customer No.");
                                                                      ServShptHeader.SETRANGE("Ship-to Code",ServHeader."Ship-to Code");
                                                                      ServShptHeader.SETRANGE("Bill-to Customer No.",ServHeader."Bill-to Customer No.");
                                                                      ServShptHeader.SETRANGE("No.","Shipment No.");
                                                                      ServShptHeader.FINDFIRST;
                                                                    end;
                                                                  end;
                                                                  TESTFIELD("Appl.-to Service Entry",0);
                                                                  ServDocReg.RESET;
                                                                  ServDocReg.SETRANGE("Destination Document Type","Document Type");
                                                                  ServDocReg.SETRANGE("Destination Document No.","Document No.");
                                                                  ServDocReg.SETRANGE("Source Document Type",ServDocReg."Source Document Type"::Order);
                                                                  ServDocReg.SETRANGE("Source Document No.",xRec."Shipment No.");
                                                                  ServDocReg.DELETEALL;
                                                                  CLEAR(ServDocReg);
                                                                end;
                                                              END;

                                                   OnLookup=VAR
                                                              ServShptHeader@1000 : Record "Service Shipment Header";
                                                            BEGIN
                                                              GetServHeader;
                                                              if "Document Type" = "Document Type"::"Credit Memo" then begin
                                                                ServShptHeader.RESET;
                                                                ServShptHeader.SETCURRENTKEY("Customer No.","Posting Date");
                                                                ServShptHeader.FILTERGROUP(2);
                                                                ServShptHeader.SETRANGE("Customer No.",ServHeader."Customer No.");
                                                                ServShptHeader.SETRANGE("Ship-to Code",ServHeader."Ship-to Code");
                                                                ServShptHeader.SETRANGE("Bill-to Customer No.",ServHeader."Bill-to Customer No.");
                                                                ServShptHeader.FILTERGROUP(0);
                                                                ServShptHeader."No." := "Shipment No.";
                                                                if PAGE.RUNMODAL(0,ServShptHeader) = ACTION::LookupOK then
                                                                  VALIDATE("Shipment No.",ServShptHeader."No.");
                                                              end
                                                            END;

                                                   CaptionML=ENU=Shipment No. }
    { 64  ;   ;Shipment Line No.   ;Integer       ;CaptionML=ENU=Shipment Line No.;
                                                   Editable=false }
    { 68  ;   ;Bill-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Bill-to Customer No.;
                                                   Editable=false }
    { 69  ;   ;Inv. Discount Amount;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD(Quantity);
                                                                CalcInvDiscToInvoice;
                                                                UpdateAmounts;
                                                              END;

                                                   CaptionML=ENU=Inv. Discount Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 74  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   OnValidate=VAR
                                                                GenBusPostingGroup@1000 : Record "Gen. Business Posting Group";
                                                              BEGIN
                                                                if "Gen. Bus. Posting Group" <> xRec."Gen. Bus. Posting Group" then
                                                                  if GenBusPostingGroup.ValidateVatBusPostingGroup(GenBusPostingGroup,"Gen. Bus. Posting Group") then
                                                                    VALIDATE("VAT Bus. Posting Group",GenBusPostingGroup."Def. VAT Bus. Posting Group");
                                                              END;

                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 75  ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=VAR
                                                                GenProdPostingGroup@1000 : Record "Gen. Product Posting Group";
                                                              BEGIN
                                                                TestStatusOpen;
                                                                if "Gen. Prod. Posting Group" <> xRec."Gen. Prod. Posting Group" then
                                                                  if GenProdPostingGroup.ValidateVatProdPostingGroup(GenProdPostingGroup,"Gen. Prod. Posting Group") then
                                                                    VALIDATE("VAT Prod. Posting Group",GenProdPostingGroup."Def. VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=ENU=Gen. Prod. Posting Group }
    { 77  ;   ;VAT Calculation Type;Option        ;CaptionML=ENU=VAT Calculation Type;
                                                   OptionCaptionML=ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   OptionString="Normal VAT","Reverse Charge VAT","Full VAT","Sales Tax";
                                                   Editable=false }
    { 78  ;   ;Transaction Type    ;Code10        ;TableRelation="Transaction Type";
                                                   CaptionML=ENU=Transaction Type }
    { 79  ;   ;Transport Method    ;Code10        ;TableRelation="Transport Method";
                                                   CaptionML=ENU=Transport Method }
    { 80  ;   ;Attached to Line No.;Integer       ;TableRelation="Service Line"."Line No." WHERE ("Document Type"=FIELD("Document Type"),
                                                                                                  "Document No."=FIELD("Document No."));
                                                   CaptionML=ENU=Attached to Line No.;
                                                   Editable=false }
    { 81  ;   ;Exit Point          ;Code10        ;TableRelation="Entry/Exit Point";
                                                   CaptionML=ENU=Exit Point }
    { 82  ;   ;Area                ;Code10        ;TableRelation=Area;
                                                   CaptionML=ENU=Area }
    { 83  ;   ;Transaction Specification;Code10   ;TableRelation="Transaction Specification";
                                                   CaptionML=ENU=Transaction Specification }
    { 85  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   OnValidate=BEGIN
                                                                UpdateAmounts;
                                                              END;

                                                   CaptionML=ENU=Tax Area Code }
    { 86  ;   ;Tax Liable          ;Boolean       ;OnValidate=BEGIN
                                                                UpdateAmounts;
                                                              END;

                                                   CaptionML=ENU=Tax Liable }
    { 87  ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                UpdateAmounts;
                                                              END;

                                                   CaptionML=ENU=Tax Group Code }
    { 88  ;   ;VAT Clause Code     ;Code20        ;TableRelation="VAT Clause";
                                                   CaptionML=ENU=VAT Clause Code }
    { 89  ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 90  ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   OnValidate=VAR
                                                                VATPostingSetup@1000 : Record "VAT Posting Setup";
                                                              BEGIN
                                                                TestStatusOpen;
                                                                GetServHeader;
                                                                VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group");
                                                                "VAT Difference" := 0;
                                                                "VAT %" := VATPostingSetup."VAT %";
                                                                "VAT Calculation Type" := VATPostingSetup."VAT Calculation Type";
                                                                "VAT Identifier" := VATPostingSetup."VAT Identifier";
                                                                "VAT Clause Code" := VATPostingSetup."VAT Clause Code";
                                                                case "VAT Calculation Type" of
                                                                  "VAT Calculation Type"::"Reverse Charge VAT",
                                                                  "VAT Calculation Type"::"Sales Tax":
                                                                    "VAT %" := 0;
                                                                  "VAT Calculation Type"::"Full VAT":
                                                                    TESTFIELD(Type,Type::Cost);
                                                                end;

                                                                if ServHeader."Prices Including VAT" and (Type in [Type::Item,Type::Resource]) then
                                                                  "Unit Price" :=
                                                                    ROUND(
                                                                      "Unit Price" * (100 + "VAT %") / (100 + xRec."VAT %"),
                                                                      Currency."Unit-Amount Rounding Precision");
                                                                UpdateAmounts;
                                                              END;

                                                   CaptionML=ENU=VAT Prod. Posting Group }
    { 91  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code;
                                                   Editable=false }
    { 92  ;   ;Outstanding Amount (LCY);Decimal   ;CaptionML=ENU=Outstanding Amount (LCY);
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 93  ;   ;Shipped Not Invoiced (LCY);Decimal ;CaptionML=ENU=Shipped Not Invoiced (LCY);
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 95  ;   ;Reserved Quantity   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry".Quantity WHERE ("Source ID"=FIELD("Document No."),
                                                                                                        "Source Ref. No."=FIELD("Line No."),
                                                                                                        "Source Type"=CONST(5902),
                                                                                                        "Source Subtype"=FIELD("Document Type"),
                                                                                                        "Reservation Status"=CONST(Reservation)));
                                                   CaptionML=ENU=Reserved Quantity;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 96  ;   ;Reserve             ;Option        ;OnValidate=BEGIN
                                                                if Reserve <> Reserve::Never then begin
                                                                  TESTFIELD(Type,Type::Item);
                                                                  TESTFIELD("No.");
                                                                end;
                                                                CALCFIELDS("Reserved Qty. (Base)");
                                                                if (Reserve = Reserve::Never) and ("Reserved Qty. (Base)" > 0) then
                                                                  TESTFIELD("Reserved Qty. (Base)",0);

                                                                if xRec.Reserve = Reserve::Always then begin
                                                                  GetItem;
                                                                  if Item.Reserve = Item.Reserve::Always then
                                                                    TESTFIELD(Reserve,Reserve::Always);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Reserve;
                                                   OptionCaptionML=ENU=Never,Optional,Always;
                                                   OptionString=Never,Optional,Always }
    { 99  ;   ;VAT Base Amount     ;Decimal       ;CaptionML=ENU=VAT Base Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 100 ;   ;Unit Cost           ;Decimal       ;CaptionML=ENU=Unit Cost;
                                                   Editable=false;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 101 ;   ;System-Created Entry;Boolean       ;CaptionML=ENU=System-Created Entry;
                                                   Editable=false }
    { 103 ;   ;Line Amount         ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD(Type);
                                                                TESTFIELD(Quantity);
                                                                TESTFIELD("Unit Price");
                                                                Currency.Initialize("Currency Code");
                                                                "Line Amount" := ROUND("Line Amount",Currency."Amount Rounding Precision");
                                                                VALIDATE(
                                                                  "Line Discount Amount",
                                                                  ROUND(CalcChargeableQty * "Unit Price",Currency."Amount Rounding Precision") - "Line Amount");
                                                              END;

                                                   CaptionML=ENU=Line Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code";
                                                   CaptionClass=GetCaptionClass(FIELDNO("Line Amount")) }
    { 104 ;   ;VAT Difference      ;Decimal       ;CaptionML=ENU=VAT Difference;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 105 ;   ;Inv. Disc. Amount to Invoice;Decimal;
                                                   CaptionML=ENU=Inv. Disc. Amount to Invoice;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 106 ;   ;VAT Identifier      ;Code20        ;CaptionML=ENU=VAT Identifier;
                                                   Editable=false }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
    { 950 ;   ;Time Sheet No.      ;Code20        ;TableRelation="Time Sheet Header";
                                                   CaptionML=ENU=Time Sheet No. }
    { 951 ;   ;Time Sheet Line No. ;Integer       ;TableRelation="Time Sheet Line"."Line No." WHERE ("Time Sheet No."=FIELD("Time Sheet No."));
                                                   CaptionML=ENU=Time Sheet Line No. }
    { 952 ;   ;Time Sheet Date     ;Date          ;TableRelation="Time Sheet Detail".Date WHERE ("Time Sheet No."=FIELD("Time Sheet No."),
                                                                                                 "Time Sheet Line No."=FIELD("Time Sheet Line No."));
                                                   CaptionML=ENU=Time Sheet Date }
    { 1019;   ;Job Planning Line No.;Integer      ;OnValidate=VAR
                                                                JobPlanningLine@1000 : Record "Job Planning Line";
                                                              BEGIN
                                                                if "Job Planning Line No." <> 0 then begin
                                                                  JobPlanningLine.GET("Job No.","Job Task No.","Job Planning Line No.");
                                                                  JobPlanningLine.TESTFIELD("Job No.","Job No.");
                                                                  JobPlanningLine.TESTFIELD("Job Task No.","Job Task No.");
                                                                  case Type of
                                                                    Type::Resource:
                                                                      JobPlanningLine.TESTFIELD(Type,JobPlanningLine.Type::Resource);
                                                                    Type::Item:
                                                                      JobPlanningLine.TESTFIELD(Type,JobPlanningLine.Type::Item);
                                                                    Type::"G/L Account":
                                                                      JobPlanningLine.TESTFIELD(Type,JobPlanningLine.Type::"G/L Account");
                                                                  end;
                                                                  JobPlanningLine.TESTFIELD("No.","No.");
                                                                  JobPlanningLine.TESTFIELD("Usage Link",true);
                                                                  JobPlanningLine.TESTFIELD("System-Created Entry",false);
                                                                  "Job Line Type" := JobPlanningLine."Line Type" + 1;
                                                                  VALIDATE("Job Remaining Qty.",JobPlanningLine."Remaining Qty." - Quantity);
                                                                end else
                                                                  VALIDATE("Job Remaining Qty.",0);
                                                              END;

                                                   OnLookup=VAR
                                                              JobPlanningLine@1000 : Record "Job Planning Line";
                                                            BEGIN
                                                              JobPlanningLine.SETRANGE("Job No.","Job No.");
                                                              JobPlanningLine.SETRANGE("Job Task No.","Job Task No.");
                                                              case Type of
                                                                Type::"G/L Account":
                                                                  JobPlanningLine.SETRANGE(Type,JobPlanningLine.Type::"G/L Account");
                                                                Type::Item:
                                                                  JobPlanningLine.SETRANGE(Type,JobPlanningLine.Type::Item);
                                                              end;
                                                              JobPlanningLine.SETRANGE("No.","No.");
                                                              JobPlanningLine.SETRANGE("Usage Link",true);
                                                              JobPlanningLine.SETRANGE("System-Created Entry",false);

                                                              if PAGE.RUNMODAL(0,JobPlanningLine) = ACTION::LookupOK then
                                                                VALIDATE("Job Planning Line No.",JobPlanningLine."Line No.");
                                                            END;

                                                   AccessByPermission=TableData Job=R;
                                                   CaptionML=ENU=Job Planning Line No.;
                                                   BlankZero=true }
    { 1030;   ;Job Remaining Qty.  ;Decimal       ;OnValidate=VAR
                                                                JobPlanningLine@1000 : Record "Job Planning Line";
                                                              BEGIN
                                                                if ("Job Remaining Qty." <> 0) and ("Job Planning Line No." = 0) then
                                                                  ERROR(Text047,FIELDCAPTION("Job Remaining Qty."),FIELDCAPTION("Job Planning Line No."));

                                                                if "Job Planning Line No." <> 0 then begin
                                                                  JobPlanningLine.GET("Job No.","Job Task No.","Job Planning Line No.");
                                                                  if JobPlanningLine.Quantity >= 0 then begin
                                                                    if "Job Remaining Qty." < 0 then
                                                                      "Job Remaining Qty." := 0;
                                                                  end else begin
                                                                    if "Job Remaining Qty." > 0 then
                                                                      "Job Remaining Qty." := 0;
                                                                  end;
                                                                end;
                                                                "Job Remaining Qty. (Base)" := CalcBaseQty("Job Remaining Qty.");

                                                                UpdateRemainingCostsAndAmounts;
                                                              END;

                                                   AccessByPermission=TableData Job=R;
                                                   CaptionML=ENU=Job Remaining Qty.;
                                                   DecimalPlaces=0:5 }
    { 1031;   ;Job Remaining Qty. (Base);Decimal  ;CaptionML=ENU=Job Remaining Qty. (Base) }
    { 1032;   ;Job Remaining Total Cost;Decimal   ;AccessByPermission=TableData Job=R;
                                                   CaptionML=ENU=Job Remaining Total Cost;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 1033;   ;Job Remaining Total Cost (LCY);Decimal;
                                                   AccessByPermission=TableData Job=R;
                                                   CaptionML=ENU=Job Remaining Total Cost (LCY);
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 1034;   ;Job Remaining Line Amount;Decimal  ;AccessByPermission=TableData Job=R;
                                                   CaptionML=ENU=Job Remaining Line Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE ("Item No."=FIELD("No."));
                                                   OnValidate=VAR
                                                                ItemVariant@1000 : Record "Item Variant";
                                                              BEGIN
                                                                if "Variant Code" <> '' then
                                                                  TESTFIELD(Type,Type::Item);
                                                                TestStatusOpen;

                                                                if xRec."Variant Code" <> "Variant Code" then begin
                                                                  TESTFIELD("Qty. Shipped Not Invoiced",0);
                                                                  TESTFIELD("Shipment No.",'');
                                                                  InitItemAppl(false);
                                                                end;

                                                                CheckItemAvailable(FIELDNO("Variant Code"));
                                                                UpdateReservation(FIELDNO("Variant Code"));

                                                                if Type = Type::Item then begin
                                                                  GetUnitCost;
                                                                  UpdateUnitPrice(FIELDNO("Variant Code"));
                                                                  WhseValidateSourceLine.ServiceLineVerifyChange(Rec,xRec);
                                                                end;

                                                                GetDefaultBin;

                                                                if "Variant Code" = '' then begin
                                                                  if Type = Type::Item then begin
                                                                    Item.GET("No.");
                                                                    Description := Item.Description;
                                                                    "Description 2" := Item."Description 2";
                                                                    GetItemTranslation;
                                                                  end;
                                                                  exit;
                                                                end;

                                                                ItemVariant.GET("No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                                "Description 2" := ItemVariant."Description 2";

                                                                GetServHeader;
                                                                if ServHeader."Language Code" <> '' then
                                                                  GetItemTranslation;
                                                              END;

                                                   CaptionML=ENU=Variant Code }
    { 5403;   ;Bin Code            ;Code20        ;TableRelation=IF ("Document Type"=FILTER(Order|Invoice),
                                                                     "Location Code"=FILTER(<>''),
                                                                     Type=CONST(Item)) "Bin Content"."Bin Code" WHERE ("Location Code"=FIELD("Location Code"),
                                                                                                                       "Item No."=FIELD("No."),
                                                                                                                       "Variant Code"=FIELD("Variant Code"))
                                                                                                                       ELSE IF ("Document Type"=FILTER("Credit Memo"),
                                                                                                                                "Location Code"=FILTER(<>''),
                                                                                                                                Type=CONST(Item)) Bin.Code WHERE ("Location Code"=FIELD("Location Code"));
                                                   OnValidate=VAR
                                                                WMSManagement@1000 : Codeunit "WMS Management";
                                                                WhseIntegrationManagement@1003 : Codeunit "Whse. Integration Management";
                                                              BEGIN
                                                                TESTFIELD("Location Code");
                                                                TESTFIELD(Type,Type::Item);

                                                                if "Bin Code" <> '' then
                                                                  if "Document Type" in ["Document Type"::Order,"Document Type"::Invoice] then
                                                                    WMSManagement.FindBinContent("Location Code","Bin Code","No.","Variant Code",'')
                                                                  else
                                                                    if "Document Type" = "Document Type"::"Credit Memo" then
                                                                      WMSManagement.FindBin("Location Code","Bin Code",'');

                                                                if xRec."Bin Code" <> "Bin Code" then begin
                                                                  TESTFIELD("Qty. Shipped Not Invoiced",0);
                                                                  TESTFIELD("Shipment No.",'');
                                                                end;

                                                                if "Bin Code" <> '' then
                                                                  WhseIntegrationManagement.CheckBinTypeCode(
                                                                    DATABASE::"Service Line",
                                                                    FIELDCAPTION("Bin Code"),
                                                                    "Location Code",
                                                                    "Bin Code",
                                                                    "Document Type");
                                                              END;

                                                   OnLookup=VAR
                                                              WMSManagement@1001 : Codeunit "WMS Management";
                                                              BinCode@1000 : Code[20];
                                                            BEGIN
                                                              TESTFIELD("Location Code");
                                                              TESTFIELD(Type,Type::Item);

                                                              if "Document Type" in ["Document Type"::Order,"Document Type"::Invoice] then
                                                                BinCode := WMSManagement.BinContentLookUp("Location Code","No.","Variant Code",'',"Bin Code")
                                                              else
                                                                if "Document Type" = "Document Type"::"Credit Memo" then
                                                                  BinCode := WMSManagement.BinLookUp("Location Code","No.","Variant Code",'');

                                                              if BinCode <> '' then
                                                                VALIDATE("Bin Code",BinCode);
                                                            END;

                                                   CaptionML=ENU=Bin Code }
    { 5404;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=ENU=Qty. per Unit of Measure;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5405;   ;Planned             ;Boolean       ;CaptionML=ENU=Planned;
                                                   Editable=false }
    { 5407;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE ("Item No."=FIELD("No."))
                                                                 ELSE IF (Type=CONST(Resource)) "Resource Unit of Measure".Code WHERE ("Resource No."=FIELD("No."))
                                                                 ELSE "Unit of Measure";
                                                   OnValidate=VAR
                                                                UnitOfMeasure@1003 : Record "Unit of Measure";
                                                                UnitOfMeasureTranslation@1000 : Record "Unit of Measure Translation";
                                                                ResUnitofMeasure@1001 : Record "Resource Unit of Measure";
                                                              BEGIN
                                                                TESTFIELD("Quantity Shipped",0);
                                                                TESTFIELD("Qty. Shipped (Base)",0);
                                                                TestStatusOpen;

                                                                if "Unit of Measure Code" = '' then
                                                                  "Unit of Measure" := ''
                                                                else begin
                                                                  if not UnitOfMeasure.GET("Unit of Measure Code") then
                                                                    UnitOfMeasure.INIT;
                                                                  "Unit of Measure" := UnitOfMeasure.Description;
                                                                  GetServHeader;
                                                                  if ServHeader."Language Code" <> '' then begin
                                                                    UnitOfMeasureTranslation.SETRANGE(Code,"Unit of Measure Code");
                                                                    UnitOfMeasureTranslation.SETRANGE("Language Code",ServHeader."Language Code");
                                                                    if UnitOfMeasureTranslation.FINDFIRST then
                                                                      "Unit of Measure" := UnitOfMeasureTranslation.Description;
                                                                  end;
                                                                end;

                                                                case Type of
                                                                  Type::Item:
                                                                    begin
                                                                      if Quantity <> 0 then
                                                                        WhseValidateSourceLine.ServiceLineVerifyChange(Rec,xRec);
                                                                      GetItem;
                                                                      GetUnitCost;
                                                                      UpdateUnitPrice(FIELDNO("Unit of Measure Code"));
                                                                      "Gross Weight" := Item."Gross Weight" * "Qty. per Unit of Measure";
                                                                      "Net Weight" := Item."Net Weight" * "Qty. per Unit of Measure";
                                                                      "Unit Volume" := Item."Unit Volume" * "Qty. per Unit of Measure";
                                                                      "Units per Parcel" := ROUND(Item."Units per Parcel" / "Qty. per Unit of Measure",0.00001);
                                                                      if "Qty. per Unit of Measure" > xRec."Qty. per Unit of Measure" then
                                                                        InitItemAppl(false);
                                                                    end;
                                                                  Type::Resource:
                                                                    begin
                                                                      if "Unit of Measure Code" = '' then begin
                                                                        GetResource;
                                                                        "Unit of Measure Code" := Resource."Base Unit of Measure";
                                                                        if UnitOfMeasure.GET("Unit of Measure Code") then
                                                                          "Unit of Measure" := UnitOfMeasure.Description;
                                                                      end;
                                                                      ResUnitofMeasure.GET("No.","Unit of Measure Code");
                                                                      "Qty. per Unit of Measure" := ResUnitofMeasure."Qty. per Unit of Measure";
                                                                      UpdateUnitPrice(FIELDNO("Unit of Measure Code"));
                                                                      FindResUnitCost;
                                                                    end;
                                                                  Type::"G/L Account",Type::" ",Type::Cost:
                                                                    "Qty. per Unit of Measure" := 1;
                                                                end;

                                                                VALIDATE(Quantity);
                                                                CheckItemAvailable(FIELDNO("Unit of Measure Code"));
                                                                UpdateReservation(FIELDNO("Unit of Measure Code"));
                                                              END;

                                                   CaptionML=ENU=Unit of Measure Code }
    { 5415;   ;Quantity (Base)     ;Decimal       ;OnValidate=BEGIN
                                                                if "Quantity (Base)" < 0 then
                                                                  FIELDERROR("Quantity (Base)",Text029);

                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE(Quantity,"Quantity (Base)");
                                                                UpdateUnitPrice(FIELDNO("Quantity (Base)"));
                                                              END;

                                                   CaptionML=ENU=Quantity (Base);
                                                   DecimalPlaces=0:5 }
    { 5416;   ;Outstanding Qty. (Base);Decimal    ;CaptionML=ENU=Outstanding Qty. (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5417;   ;Qty. to Invoice (Base);Decimal     ;OnValidate=BEGIN
                                                                if "Qty. to Invoice (Base)" < 0 then
                                                                  FIELDERROR("Qty. to Invoice (Base)",Text029);

                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Invoice","Qty. to Invoice (Base)");
                                                              END;

                                                   CaptionML=ENU=Qty. to Invoice (Base);
                                                   DecimalPlaces=0:5 }
    { 5418;   ;Qty. to Ship (Base) ;Decimal       ;OnValidate=BEGIN
                                                                if "Qty. to Ship (Base)" < 0 then
                                                                  FIELDERROR("Qty. to Ship (Base)",Text029);

                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Ship","Qty. to Ship (Base)");
                                                              END;

                                                   CaptionML=ENU=Qty. to Ship (Base);
                                                   DecimalPlaces=0:5 }
    { 5458;   ;Qty. Shipped Not Invd. (Base);Decimal;
                                                   CaptionML=ENU=Qty. Shipped Not Invd. (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5460;   ;Qty. Shipped (Base) ;Decimal       ;CaptionML=ENU=Qty. Shipped (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5461;   ;Qty. Invoiced (Base);Decimal       ;CaptionML=ENU=Qty. Invoiced (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5495;   ;Reserved Qty. (Base);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry"."Quantity (Base)" WHERE ("Source ID"=FIELD("Document No."),
                                                                                                                 "Source Ref. No."=FIELD("Line No."),
                                                                                                                 "Source Type"=CONST(5902),
                                                                                                                 "Source Subtype"=FIELD("Document Type"),
                                                                                                                 "Reservation Status"=CONST(Reservation)));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Qty. per Unit of Measure");
                                                                CALCFIELDS("Reserved Quantity");
                                                                Planned := "Reserved Quantity" = "Outstanding Quantity";
                                                              END;

                                                   CaptionML=ENU=Reserved Qty. (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5700;   ;Responsibility Center;Code10       ;TableRelation="Responsibility Center";
                                                   OnValidate=BEGIN
                                                                CreateDim(
                                                                  DATABASE::"Responsibility Center","Responsibility Center",
                                                                  DimMgt.TypeToTableID5(Type),"No.",
                                                                  DATABASE::Job,"Job No.");
                                                              END;

                                                   CaptionML=ENU=Responsibility Center;
                                                   Editable=false }
    { 5702;   ;Substitution Available;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Item Substitution" WHERE (Type=CONST(Item),
                                                                                                "No."=FIELD("No."),
                                                                                                "Substitute Type"=CONST(Item)));
                                                   CaptionML=ENU=Substitution Available;
                                                   Editable=false }
    { 5709;   ;Item Category Code  ;Code20        ;TableRelation="Item Category";
                                                   CaptionML=ENU=Item Category Code }
    { 5710;   ;Nonstock            ;Boolean       ;CaptionML=ENU=Nonstock;
                                                   Editable=false }
    { 5712;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE ("Item Category Code"=FIELD("Item Category Code"));
                                                   ValidateTableRelation=false;
                                                   CaptionML=ENU=Product Group Code }
    { 5750;   ;Whse. Outstanding Qty. (Base);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Shipment Line"."Qty. Outstanding (Base)" WHERE ("Source Type"=CONST(5902),
                                                                                                                              "Source Subtype"=FIELD("Document Type"),
                                                                                                                              "Source No."=FIELD("Document No."),
                                                                                                                              "Source Line No."=FIELD("Line No.")));
                                                   CaptionML=ENU=Whse. Outstanding Qty. (Base);
                                                   BlankZero=true;
                                                   Editable=false }
    { 5752;   ;Completely Shipped  ;Boolean       ;CaptionML=ENU=Completely Shipped;
                                                   Editable=false }
    { 5790;   ;Requested Delivery Date;Date       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                if ("Requested Delivery Date" <> xRec."Requested Delivery Date") and
                                                                   ("Promised Delivery Date" <> 0D)
                                                                then
                                                                  ERROR(
                                                                    Text046,
                                                                    FIELDCAPTION("Requested Delivery Date"),
                                                                    FIELDCAPTION("Promised Delivery Date"));

                                                                if "Requested Delivery Date" <> 0D then
                                                                  VALIDATE("Planned Delivery Date","Requested Delivery Date")
                                                              END;

                                                   CaptionML=ENU=Requested Delivery Date }
    { 5791;   ;Promised Delivery Date;Date        ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                if "Promised Delivery Date" <> 0D then
                                                                  VALIDATE("Planned Delivery Date","Promised Delivery Date")
                                                                else
                                                                  VALIDATE("Requested Delivery Date");
                                                              END;

                                                   CaptionML=ENU=Promised Delivery Date }
    { 5792;   ;Shipping Time       ;DateFormula   ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                              END;

                                                   AccessByPermission=TableData "Shipping Agent Services"=R;
                                                   CaptionML=ENU=Shipping Time }
    { 5794;   ;Planned Delivery Date;Date         ;OnValidate=BEGIN
                                                                VALIDATE("Needed by Date","Planned Delivery Date");
                                                              END;

                                                   CaptionML=ENU=Planned Delivery Date }
    { 5796;   ;Shipping Agent Code ;Code10        ;TableRelation="Shipping Agent";
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                if "Shipping Agent Code" <> xRec."Shipping Agent Code" then
                                                                  VALIDATE("Shipping Agent Service Code",'');
                                                              END;

                                                   AccessByPermission=TableData "Shipping Agent Services"=R;
                                                   CaptionML=ENU=Shipping Agent Code }
    { 5797;   ;Shipping Agent Service Code;Code10 ;TableRelation="Shipping Agent Services".Code WHERE ("Shipping Agent Code"=FIELD("Shipping Agent Code"));
                                                   OnValidate=VAR
                                                                ShippingAgentServices@1000 : Record "Shipping Agent Services";
                                                              BEGIN
                                                                TestStatusOpen;
                                                                if "Shipping Agent Service Code" <> xRec."Shipping Agent Service Code" then
                                                                  CLEAR("Shipping Time");

                                                                if ShippingAgentServices.GET("Shipping Agent Code","Shipping Agent Service Code") then
                                                                  "Shipping Time" := ShippingAgentServices."Shipping Time"
                                                                else begin
                                                                  GetServHeader;
                                                                  "Shipping Time" := ServHeader."Shipping Time";
                                                                end;

                                                                if ShippingAgentServices."Shipping Time" <> xRec."Shipping Time" then
                                                                  VALIDATE("Shipping Time","Shipping Time");
                                                              END;

                                                   CaptionML=ENU=Shipping Agent Service Code }
    { 5811;   ;Appl.-from Item Entry;Integer      ;OnValidate=VAR
                                                                ItemLedgEntry@1000 : Record "Item Ledger Entry";
                                                              BEGIN
                                                                if "Appl.-from Item Entry" <> 0 then begin
                                                                  CheckApplFromItemLedgEntry(ItemLedgEntry);
                                                                  VALIDATE("Unit Cost (LCY)",CalcUnitCost(ItemLedgEntry));
                                                                end;
                                                              END;

                                                   OnLookup=BEGIN
                                                              SelectItemEntry(FIELDNO("Appl.-from Item Entry"));
                                                            END;

                                                   AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Appl.-from Item Entry;
                                                   MinValue=0 }
    { 5902;   ;Service Item No.    ;Code20        ;TableRelation="Service Item"."No.";
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Quantity Shipped",0);
                                                                TESTFIELD("Shipment No.",'');
                                                                if "Service Item No." <> '' then begin
                                                                  if "Document Type" in ["Document Type"::Invoice,"Document Type"::"Credit Memo"] then
                                                                    exit;
                                                                  ServItemLine.RESET;
                                                                  ServItemLine.SETRANGE("Document Type","Document Type");
                                                                  ServItemLine.SETRANGE("Document No.","Document No.");
                                                                  ServItemLine.SETRANGE("Service Item No.","Service Item No.");
                                                                  ServItemLine.FIND('-');
                                                                  VALIDATE("Service Item Line No.",ServItemLine."Line No.");
                                                                end;

                                                                if "Service Item No." <> xRec."Service Item No." then begin
                                                                  if "Service Item No." = '' then
                                                                    VALIDATE("Service Item Line No.",0);
                                                                  VALIDATE("No.");
                                                                end;
                                                              END;

                                                   OnLookup=BEGIN
                                                              if "Document Type" in ["Document Type"::Invoice,"Document Type"::"Credit Memo"] then begin
                                                                ServItem.RESET;
                                                                ServItem.SETCURRENTKEY("Customer No.");
                                                                ServItem.FILTERGROUP(2);
                                                                ServItem.SETRANGE("Customer No.","Customer No.");
                                                                ServItem.FILTERGROUP(0);
                                                                if PAGE.RUNMODAL(0,ServItem) = ACTION::LookupOK then
                                                                  VALIDATE("Service Item No.",ServItem."No.");
                                                              end
                                                              else begin
                                                                ServItemLine.RESET;
                                                                ServItemLine.SETCURRENTKEY("Document Type","Document No.","Service Item No.");
                                                                ServItemLine.FILTERGROUP(2);
                                                                ServItemLine.SETRANGE("Document Type","Document Type");
                                                                ServItemLine.SETRANGE("Document No.","Document No.");
                                                                ServItemLine.FILTERGROUP(0);
                                                                ServItemLine."Service Item No." := "Service Item No.";
                                                                if PAGE.RUNMODAL(0,ServItemLine) = ACTION::LookupOK then
                                                                  VALIDATE("Service Item Line No.",ServItemLine."Line No.");
                                                              end;

                                                              if "Service Item No." <> xRec."Service Item No." then
                                                                VALIDATE("No.");
                                                            END;

                                                   CaptionML=ENU=Service Item No. }
    { 5903;   ;Appl.-to Service Entry;Integer     ;AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Appl.-to Service Entry;
                                                   Editable=false }
    { 5904;   ;Service Item Line No.;Integer      ;TableRelation="Service Item Line"."Line No." WHERE ("Document Type"=FIELD("Document Type"),
                                                                                                       "Document No."=FIELD("Document No."));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Quantity Shipped",0);
                                                                ErrorIfAlreadySelectedSI("Service Item Line No.");
                                                                if ServItemLine.GET("Document Type","Document No.","Service Item Line No.") then begin
                                                                  "Service Item No." := ServItemLine."Service Item No.";
                                                                  "Service Item Serial No." := ServItemLine."Serial No.";
                                                                  "Fault Area Code" := ServItemLine."Fault Area Code";
                                                                  "Symptom Code" := ServItemLine."Symptom Code";
                                                                  "Fault Code" := ServItemLine."Fault Code";
                                                                  "Resolution Code" := ServItemLine."Resolution Code";
                                                                  "Service Price Group Code" := ServItemLine."Service Price Group Code";
                                                                  "Serv. Price Adjmt. Gr. Code" := ServItemLine."Serv. Price Adjmt. Gr. Code";
                                                                  if "No." <> '' then
                                                                    VALIDATE("Contract No.",ServItemLine."Contract No.");
                                                                end else begin
                                                                  "Service Item No." := '';
                                                                  "Service Item Serial No." := '';
                                                                end;
                                                                CALCFIELDS("Service Item Line Description");
                                                              END;

                                                   CaptionML=ENU=Service Item Line No. }
    { 5905;   ;Service Item Serial No.;Code20     ;OnValidate=BEGIN
                                                                if "Service Item Serial No." <> '' then begin
                                                                  ServItemLine.RESET;
                                                                  ServItemLine.SETRANGE("Document Type","Document Type");
                                                                  ServItemLine.SETRANGE("Document No.","Document No.");
                                                                  ServItemLine.SETRANGE("Serial No.","Service Item Serial No.");
                                                                  ServItemLine.FIND('-');
                                                                  VALIDATE("Service Item Line No.",ServItemLine."Line No.");
                                                                end;
                                                              END;

                                                   OnLookup=BEGIN
                                                              ServItemLine.RESET;
                                                              ServItemLine.SETRANGE("Document Type","Document Type");
                                                              ServItemLine.SETRANGE("Document No.","Document No.");
                                                              ServItemLine."Serial No." := "Service Item Serial No.";
                                                              if PAGE.RUNMODAL(0,ServItemLine) = ACTION::LookupOK then
                                                                VALIDATE("Service Item Line No.",ServItemLine."Line No.");
                                                            END;

                                                   CaptionML=ENU=Service Item Serial No. }
    { 5906;   ;Service Item Line Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Item Line".Description WHERE ("Document Type"=FIELD("Document Type"),
                                                                                                             "Document No."=FIELD("Document No."),
                                                                                                             "Line No."=FIELD("Service Item Line No.")));
                                                   CaptionML=ENU=Service Item Line Description;
                                                   Editable=false }
    { 5907;   ;Serv. Price Adjmt. Gr. Code;Code10 ;TableRelation="Service Price Adjustment Group";
                                                   CaptionML=ENU=Serv. Price Adjmt. Gr. Code;
                                                   Editable=false }
    { 5908;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 5909;   ;Order Date          ;Date          ;CaptionML=ENU=Order Date;
                                                   Editable=false }
    { 5910;   ;Needed by Date      ;Date          ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                if CurrFieldNo = FIELDNO("Needed by Date") then
                                                                  if xRec."Needed by Date" <> 0D then
                                                                    TESTFIELD("Needed by Date");
                                                                if "Needed by Date" <> 0D then
                                                                  CheckItemAvailable(FIELDNO("Needed by Date"));
                                                                if CurrFieldNo = FIELDNO("Planned Delivery Date") then
                                                                  UpdateReservation(CurrFieldNo)
                                                                else
                                                                  UpdateReservation(FIELDNO("Needed by Date"));
                                                                "Planned Delivery Date" := "Needed by Date";
                                                              END;

                                                   CaptionML=ENU=Needed by Date }
    { 5916;   ;Ship-to Code        ;Code10        ;TableRelation="Ship-to Address".Code WHERE ("Customer No."=FIELD("Customer No."));
                                                   CaptionML=ENU=Ship-to Code;
                                                   Editable=false }
    { 5917;   ;Qty. to Consume     ;Decimal       ;OnValidate=BEGIN
                                                                if CurrFieldNo = FIELDNO("Qty. to Consume") then
                                                                  CheckWarehouse;

                                                                if "Qty. to Consume" < 0 then
                                                                  FIELDERROR("Qty. to Consume",Text029);

                                                                if "Qty. to Consume" = MaxQtyToConsume then
                                                                  InitQtyToConsume
                                                                else begin
                                                                  "Qty. to Consume (Base)" := CalcBaseQty("Qty. to Consume");
                                                                  InitQtyToInvoice;
                                                                end;

                                                                if "Qty. to Consume" > 0 then begin
                                                                  "Qty. to Ship" := "Qty. to Consume";
                                                                  "Qty. to Ship (Base)" := "Qty. to Consume (Base)";
                                                                  "Qty. to Invoice" := 0;
                                                                  "Qty. to Invoice (Base)" := 0;
                                                                end;

                                                                if ("Qty. to Consume" * Quantity < 0) or
                                                                   (ABS("Qty. to Consume") > ABS(MaxQtyToConsume))
                                                                then
                                                                  ERROR(
                                                                    Text028,
                                                                    MaxQtyToConsume);
                                                                if ("Qty. to Consume (Base)" * "Quantity (Base)" < 0) or
                                                                   (ABS("Qty. to Consume (Base)") > ABS(MaxQtyToConsumeBase))
                                                                then
                                                                  ERROR(
                                                                    Text032,
                                                                    MaxQtyToConsumeBase);

                                                                if (xRec."Qty. to Consume" <> "Qty. to Consume") or
                                                                   (xRec."Qty. to Consume (Base)" <> "Qty. to Consume (Base)")
                                                                then
                                                                  VALIDATE("Line Discount %");
                                                              END;

                                                   CaptionML=ENU=Qty. to Consume;
                                                   DecimalPlaces=0:5;
                                                   BlankZero=true }
    { 5918;   ;Quantity Consumed   ;Decimal       ;CaptionML=ENU=Quantity Consumed;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5919;   ;Qty. to Consume (Base);Decimal     ;OnValidate=BEGIN
                                                                if LineRequiresShipmentOrReceipt then
                                                                  exit;
                                                                if "Qty. to Consume (Base)" < 0 then
                                                                  FIELDERROR("Qty. to Consume (Base)",Text029);

                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Invoice","Qty. to Invoice (Base)");
                                                              END;

                                                   CaptionML=ENU=Qty. to Consume (Base);
                                                   DecimalPlaces=0:5;
                                                   BlankZero=true }
    { 5920;   ;Qty. Consumed (Base);Decimal       ;CaptionML=ENU=Qty. Consumed (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5928;   ;Service Price Group Code;Code10    ;TableRelation="Service Price Group";
                                                   CaptionML=ENU=Service Price Group Code }
    { 5929;   ;Fault Area Code     ;Code10        ;TableRelation="Fault Area";
                                                   OnValidate=BEGIN
                                                                if "Fault Area Code" <> xRec."Fault Area Code" then
                                                                  "Fault Code" := '';
                                                              END;

                                                   CaptionML=ENU=Fault Area Code }
    { 5930;   ;Symptom Code        ;Code10        ;TableRelation="Symptom Code";
                                                   OnValidate=BEGIN
                                                                if "Symptom Code" <> xRec."Symptom Code" then
                                                                  "Fault Code" := '';
                                                              END;

                                                   CaptionML=ENU=Symptom Code }
    { 5931;   ;Fault Code          ;Code10        ;TableRelation="Fault Code".Code WHERE ("Fault Area Code"=FIELD("Fault Area Code"),
                                                                                          "Symptom Code"=FIELD("Symptom Code"));
                                                   CaptionML=ENU=Fault Code }
    { 5932;   ;Resolution Code     ;Code10        ;TableRelation="Resolution Code";
                                                   CaptionML=ENU=Resolution Code }
    { 5933;   ;Exclude Warranty    ;Boolean       ;OnValidate=BEGIN
                                                                if not (Type in [Type::Item,Type::Resource]) then
                                                                  if CurrFieldNo = FIELDNO("Exclude Warranty") then
                                                                    FIELDERROR(Type)
                                                                  else
                                                                    exit;

                                                                if CurrFieldNo = FIELDNO("Exclude Warranty") then begin
                                                                  ServItemLine.GET("Document Type","Document No.","Service Item Line No.");
                                                                  ServItemLine.TESTFIELD(Warranty,true);
                                                                  if "Exclude Warranty" and (not Warranty) then
                                                                    FIELDERROR(Warranty);
                                                                end;
                                                                if HideWarrantyWarning = false then
                                                                  if "Fault Reason Code" <> '' then begin
                                                                    FaultReasonCode.GET("Fault Reason Code");
                                                                    if FaultReasonCode."Exclude Warranty Discount" and
                                                                       not "Exclude Warranty"
                                                                    then
                                                                      ERROR(
                                                                        Text008,
                                                                        FIELDCAPTION("Exclude Warranty"),
                                                                        FaultReasonCode.FIELDCAPTION("Exclude Warranty Discount"),
                                                                        "Fault Reason Code",
                                                                        FaultReasonCode.TABLECAPTION);
                                                                  end;
                                                                if HideWarrantyWarning = false then
                                                                  if "Exclude Warranty" <> xRec."Exclude Warranty" then
                                                                    if not
                                                                       CONFIRM(
                                                                         Text009,
                                                                         false,FIELDCAPTION("Exclude Warranty"))
                                                                    then begin
                                                                      "Exclude Warranty" := xRec."Exclude Warranty";
                                                                      exit;
                                                                    end;
                                                                VALIDATE("Contract No.");
                                                                if "Exclude Warranty" then
                                                                  VALIDATE(Warranty,false)
                                                                else
                                                                  VALIDATE(Warranty,true);
                                                              END;

                                                   CaptionML=ENU=Exclude Warranty;
                                                   Editable=true }
    { 5934;   ;Warranty            ;Boolean       ;OnValidate=BEGIN
                                                                UpdateDiscountsAmounts;
                                                              END;

                                                   CaptionML=ENU=Warranty;
                                                   Editable=false }
    { 5936;   ;Contract No.        ;Code20        ;TableRelation="Service Contract Header"."Contract No." WHERE ("Contract Type"=CONST(Contract));
                                                   OnValidate=VAR
                                                                Res@1001 : Record Resource;
                                                                ServCost@1000 : Record "Service Cost";
                                                                ContractGroup@1002 : Record "Contract Group";
                                                                ContractServDisc@1003 : Record "Contract/Service Discount";
                                                                ServContractHeader@1004 : Record "Service Contract Header";
                                                              BEGIN
                                                                if "Shipment Line No." <> 0 then
                                                                  if "Shipment No." <> '' then
                                                                    FIELDERROR("Contract No.");

                                                                if "Document Type" in ["Document Type"::Invoice,"Document Type"::"Credit Memo"] then begin
                                                                  if "Contract No." <> xRec."Contract No." then begin
                                                                    TESTFIELD("Appl.-to Service Entry",0);
                                                                    UpdateServDocRegister(false);
                                                                  end;
                                                                end else begin
                                                                  ServMgtSetup.GET;
                                                                  if not ServItem.GET("Service Item No.") then
                                                                    CLEAR(ServItem);
                                                                  if "Contract No." = '' then
                                                                    "Contract Disc. %" := 0
                                                                  else begin
                                                                    GetServHeader;
                                                                    if ServContractHeader.GET(ServContractHeader."Contract Type"::Contract,"Contract No.") then begin
                                                                      if (ServContractHeader."Starting Date" <= WORKDATE) and not "Exclude Contract Discount" then begin
                                                                        if not ContractGroup.GET(ServContractHeader."Contract Group Code") then
                                                                          ContractGroup.INIT;
                                                                        if not ContractGroup."Disc. on Contr. Orders Only" or
                                                                           (ContractGroup."Disc. on Contr. Orders Only" and (ServHeader."Contract No." <> ''))
                                                                        then begin
                                                                          case Type of
                                                                            Type::" ":
                                                                              "Contract Disc. %" := 0;
                                                                            Type::Item:
                                                                              begin
                                                                                ContractServDisc.INIT;
                                                                                ContractServDisc."Contract Type" := ContractServDisc."Contract Type"::Contract;
                                                                                ContractServDisc."Contract No." := ServContractHeader."Contract No.";
                                                                                ContractServDisc.Type := ContractServDisc.Type::"Service Item Group";
                                                                                ContractServDisc."No." := ServItem."Service Item Group Code";
                                                                                ContractServDisc."Starting Date" := "Posting Date";
                                                                                CODEUNIT.RUN(CODEUNIT::"ContractDiscount-Find",ContractServDisc);
                                                                                "Contract Disc. %" := ContractServDisc."Discount %";
                                                                              end;
                                                                            Type::Resource:
                                                                              begin
                                                                                Res.GET("No.");
                                                                                ContractServDisc.INIT;
                                                                                ContractServDisc."Contract Type" := ContractServDisc."Contract Type"::Contract;
                                                                                ContractServDisc."Contract No." := ServContractHeader."Contract No.";
                                                                                ContractServDisc.Type := ContractServDisc.Type::"Resource Group";
                                                                                ContractServDisc."No." := Res."Resource Group No.";
                                                                                ContractServDisc."Starting Date" := "Posting Date";
                                                                                CODEUNIT.RUN(CODEUNIT::"ContractDiscount-Find",ContractServDisc);
                                                                                "Contract Disc. %" := ContractServDisc."Discount %";
                                                                              end;
                                                                            Type::Cost:
                                                                              begin
                                                                                ServCost.GET("No.");
                                                                                ContractServDisc.INIT;
                                                                                ContractServDisc."Contract Type" := ContractServDisc."Contract Type"::Contract;
                                                                                ContractServDisc."Contract No." := ServContractHeader."Contract No.";
                                                                                ContractServDisc.Type := ContractServDisc.Type::Cost;
                                                                                ContractServDisc."No." := "No.";
                                                                                ContractServDisc."Starting Date" := "Posting Date";
                                                                                CODEUNIT.RUN(CODEUNIT::"ContractDiscount-Find",ContractServDisc);
                                                                                "Contract Disc. %" := ContractServDisc."Discount %";
                                                                              end;
                                                                          end;
                                                                        end else
                                                                          "Contract Disc. %" := 0;
                                                                      end;
                                                                    end else
                                                                      "Contract Disc. %" := 0;
                                                                  end;

                                                                  if Warranty then
                                                                    case Type of
                                                                      Type::Item:
                                                                        "Warranty Disc. %" := ServItem."Warranty % (Parts)";
                                                                      Type::Resource:
                                                                        "Warranty Disc. %" := ServItem."Warranty % (Labor)";
                                                                      else
                                                                        "Warranty Disc. %" := 0;
                                                                    end;

                                                                  UpdateDiscountsAmounts;
                                                                end;
                                                              END;

                                                   OnLookup=VAR
                                                              ServContractHeader@1000 : Record "Service Contract Header";
                                                            BEGIN
                                                              GetServHeader;
                                                              ServContractHeader.FILTERGROUP(2);
                                                              ServContractHeader.SETRANGE("Customer No.",ServHeader."Customer No.");
                                                              ServContractHeader.SETRANGE("Contract Type",ServContractHeader."Contract Type"::Contract);
                                                              ServContractHeader.FILTERGROUP(0);
                                                              if (PAGE.RUNMODAL(0,ServContractHeader) = ACTION::LookupOK) and
                                                                 ("Document Type" in ["Document Type"::Invoice,"Document Type"::"Credit Memo"])
                                                              then
                                                                VALIDATE("Contract No.",ServContractHeader."Contract No.");
                                                            END;

                                                   CaptionML=ENU=Contract No. }
    { 5938;   ;Contract Disc. %    ;Decimal       ;OnValidate=BEGIN
                                                                UpdateAmounts;
                                                              END;

                                                   CaptionML=ENU=Contract Disc. %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=false }
    { 5939;   ;Warranty Disc. %    ;Decimal       ;OnValidate=BEGIN
                                                                UpdateUnitPrice(FIELDNO(Warranty));
                                                                UpdateAmounts;
                                                              END;

                                                   CaptionML=ENU=Warranty Disc. %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=false }
    { 5965;   ;Component Line No.  ;Integer       ;CaptionML=ENU=Component Line No. }
    { 5966;   ;Spare Part Action   ;Option        ;CaptionML=ENU=Spare Part Action;
                                                   OptionCaptionML=ENU=" ,Permanent,Temporary,Component Replaced,Component Installed";
                                                   OptionString=" ",Permanent,Temporary,"Component Replaced","Component Installed" }
    { 5967;   ;Fault Reason Code   ;Code10        ;TableRelation="Fault Reason Code";
                                                   OnValidate=VAR
                                                                NewWarranty@1000 : Boolean;
                                                                OldExcludeContractDiscount@1001 : Boolean;
                                                              BEGIN
                                                                SetHideWarrantyWarning := true;
                                                                OldExcludeContractDiscount := "Exclude Contract Discount";
                                                                if FaultReasonCode.GET("Fault Reason Code") then begin
                                                                  if FaultReasonCode."Exclude Warranty Discount" and
                                                                     (not (Type in [Type::Item,Type::Resource]))
                                                                  then
                                                                    ERROR(
                                                                      Text027,
                                                                      FIELDCAPTION("Fault Reason Code"),
                                                                      FaultReasonCode.Code,
                                                                      FaultReasonCode.FIELDCAPTION("Exclude Warranty Discount"));
                                                                  "Exclude Contract Discount" := FaultReasonCode."Exclude Contract Discount";
                                                                  NewWarranty := (not FaultReasonCode."Exclude Warranty Discount") and
                                                                    ("Exclude Warranty" or Warranty);
                                                                  VALIDATE("Exclude Warranty",
                                                                    FaultReasonCode."Exclude Warranty Discount" and
                                                                    ("Exclude Warranty" or Warranty));
                                                                  VALIDATE(Warranty,NewWarranty);
                                                                  if OldExcludeContractDiscount and (not "Exclude Contract Discount") then
                                                                    VALIDATE("Contract No.");
                                                                end else begin
                                                                  "Exclude Contract Discount" := false;
                                                                  if "Exclude Warranty" then begin
                                                                    VALIDATE("Exclude Warranty",false);
                                                                    VALIDATE(Warranty,true);
                                                                  end else
                                                                    if OldExcludeContractDiscount <> "Exclude Contract Discount" then
                                                                      if OldExcludeContractDiscount and (not "Exclude Contract Discount") then
                                                                        VALIDATE("Contract No.")
                                                                      else
                                                                        VALIDATE(Warranty);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Fault Reason Code }
    { 5968;   ;Replaced Item No.   ;Code20        ;TableRelation=IF ("Replaced Item Type"=CONST(Item)) Item
                                                                 ELSE IF ("Replaced Item Type"=CONST("Service Item")) "Service Item";
                                                   CaptionML=ENU=Replaced Item No. }
    { 5969;   ;Exclude Contract Discount;Boolean  ;OnValidate=BEGIN
                                                                if Type = Type::"G/L Account" then
                                                                  FIELDERROR(Type);

                                                                if "Fault Reason Code" <> '' then begin
                                                                  FaultReasonCode.GET("Fault Reason Code");
                                                                  if FaultReasonCode."Exclude Contract Discount" and
                                                                     not "Exclude Contract Discount"
                                                                  then
                                                                    ERROR(
                                                                      Text008,
                                                                      FIELDCAPTION("Exclude Contract Discount"),
                                                                      FaultReasonCode.FIELDCAPTION("Exclude Contract Discount"),
                                                                      "Fault Reason Code",
                                                                      FaultReasonCode.TABLECAPTION);
                                                                end;

                                                                if "Exclude Contract Discount" <> xRec."Exclude Contract Discount" then begin
                                                                  if not
                                                                     CONFIRM(
                                                                       STRSUBSTNO(Text009,
                                                                         FIELDCAPTION("Exclude Contract Discount")),false)
                                                                  then begin
                                                                    "Exclude Contract Discount" := xRec."Exclude Contract Discount";
                                                                    exit;
                                                                  end;
                                                                  VALIDATE("Contract No.");
                                                                  VALIDATE(Warranty);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Exclude Contract Discount;
                                                   Editable=true }
    { 5970;   ;Replaced Item Type  ;Option        ;CaptionML=ENU=Replaced Item Type;
                                                   OptionCaptionML=ENU=" ,Service Item,Item";
                                                   OptionString=" ","Service Item",Item }
    { 5994;   ;Price Adjmt. Status ;Option        ;CaptionML=ENU=Price Adjmt. Status;
                                                   OptionCaptionML=ENU=" ,Adjusted,Modified";
                                                   OptionString=" ",Adjusted,Modified;
                                                   Editable=false }
    { 5997;   ;Line Discount Type  ;Option        ;CaptionML=ENU=Line Discount Type;
                                                   OptionCaptionML=ENU=" ,Warranty Disc.,Contract Disc.,Line Disc.,Manual";
                                                   OptionString=" ","Warranty Disc.","Contract Disc.","Line Disc.",Manual;
                                                   Editable=false }
    { 5999;   ;Copy Components From;Option        ;CaptionML=ENU=Copy Components From;
                                                   OptionCaptionML=ENU=None,Item BOM,Old Service Item,Old Serv.Item w/o Serial No.;
                                                   OptionString=None,"Item BOM","Old Service Item","Old Serv.Item w/o Serial No." }
    { 6608;   ;Return Reason Code  ;Code10        ;TableRelation="Return Reason";
                                                   OnValidate=VAR
                                                                ReturnReason@1000 : Record "Return Reason";
                                                              BEGIN
                                                                if "Return Reason Code" = '' then
                                                                  UpdateUnitPrice(FIELDNO("Return Reason Code"));

                                                                if ReturnReason.GET("Return Reason Code") then begin
                                                                  if ReturnReason."Default Location Code" <> '' then
                                                                    VALIDATE("Location Code",ReturnReason."Default Location Code");
                                                                  if ReturnReason."Inventory Value Zero" then begin
                                                                    VALIDATE("Unit Cost (LCY)",0);
                                                                    VALIDATE("Unit Price",0);
                                                                  end else
                                                                    if "Unit Price" = 0 then
                                                                      UpdateUnitPrice(FIELDNO("Return Reason Code"));
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Return Reason Code }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Allow Line Disc. }
    { 7002;   ;Customer Disc. Group;Code20        ;TableRelation="Customer Discount Group";
                                                   OnValidate=BEGIN
                                                                if Type = Type::Item then
                                                                  UpdateUnitPrice(FIELDNO("Customer Disc. Group"));
                                                              END;

                                                   CaptionML=ENU=Customer Disc. Group }
    { 7300;   ;Qty. Picked         ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Picked (Base)" := CalcBaseQty("Qty. Picked");
                                                                "Completely Picked" := "Qty. Picked" >= 0;
                                                              END;

                                                   CaptionML=ENU=Qty. Picked;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 7301;   ;Qty. Picked (Base)  ;Decimal       ;CaptionML=ENU=Qty. Picked (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 7302;   ;Completely Picked   ;Boolean       ;CaptionML=ENU=Completely Picked;
                                                   Editable=false }
    { 7303;   ;Pick Qty. (Base)    ;Decimal       ;CaptionML=ENU=Pick Qty. (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Document Type","Document No.","Line No.";
                                                   SumIndexFields=Amount,"Amount Including VAT","Outstanding Amount","Shipped Not Invoiced","Outstanding Amount (LCY)","Shipped Not Invoiced (LCY)","Line Amount";
                                                   Clustered=true }
    {    ;Type,"No.","Order Date"                  }
    {    ;"Service Item No.",Type,"Posting Date"   }
    {    ;"Document Type","Bill-to Customer No.","Currency Code";
                                                   SumIndexFields="Outstanding Amount","Shipped Not Invoiced","Outstanding Amount (LCY)","Shipped Not Invoiced (LCY)" }
    {    ;"Document Type","Document No.","Service Item No." }
    {    ;"Document Type","Document No.","Service Item Line No.","Serv. Price Adjmt. Gr. Code";
                                                   SumIndexFields="Line Amount" }
    {    ;"Document Type","Document No.","Service Item Line No.",Type,"No." }
    {    ;Type,"No.","Variant Code","Location Code","Needed by Date","Document Type","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code";
                                                   SumIndexFields="Quantity (Base)","Outstanding Qty. (Base)" }
    {    ;"Appl.-to Service Entry"                 }
    {    ;"Document Type","Document No.","Service Item Line No.","Component Line No." }
    {    ;"Fault Reason Code"                      }
    {    ;"Document Type","Customer No.","Shipment No.";
                                                   SumIndexFields="Outstanding Amount (LCY)" }
    {    ;"Document Type","Document No.","Location Code" }
    {    ;"Document Type","Document No.",Type,"No." }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1085 : TextConst 'ENU=You cannot invoice more than %1 units.';
      Text001@1084 : TextConst 'ENU=You cannot invoice more than %1 base units.';
      Text002@1002 : TextConst 'ENU=You cannot rename a %1.';
      Text003@1086 : TextConst 'ENU=must not be less than %1';
      Text004@1004 : TextConst 'ENU=You must confirm %1 %2, because %3 is not equal to %4 in %5 %6.';
      Text005@1005 : TextConst 'ENU=The update has been interrupted to respect the warning.';
      Text006@1006 : TextConst 'ENU=Replace Component,New Component,Ignore';
      Text007@1007 : TextConst 'ENU=You must select a %1.';
      Text008@1008 : TextConst 'ENU=You cannot change the value of the %1 field because the %2 field in the Fault Reason Codes window contains a check mark for the %3 %4.';
      Text009@1009 : TextConst 'ENU=You have changed the value of the field %1.\Do you want to continue ?';
      Text010@1010 : TextConst 'ENU=%1 cannot be less than %2.';
      Text011@1011 : TextConst 'ENU=When replacing a %1 the quantity must be 1.';
      Text012@1087 : TextConst 'ENU=Automatic reservation is not possible.\Do you want to reserve items manually?';
      Text013@1013 : TextConst 'ENU=" must be 0 when %1 is %2."';
      Text015@1015 : TextConst 'ENU=You have already selected %1 %2 for replacement.';
      Text016@1083 : TextConst 'ENU=You cannot ship more than %1 units.';
      Text017@1082 : TextConst 'ENU=You cannot ship more than %1 base units.';
      Text018@1018 : TextConst 'ENU=%1 %2 is greater than %3 and was adjusted to %4.';
      CompAlreadyReplacedErr@1021 : TextConst '@@@="%1 = Line No.";ENU=The component that you selected has already been replaced in service line %1.';
      ServMgtSetup@1023 : Record "Service Mgt. Setup";
      ServiceLine@1024 : Record "Service Line";
      ServHeader@1026 : Record "Service Header";
      ServItem@1028 : Record "Service Item";
      ServItemLine@1030 : Record "Service Item Line";
      Item@1033 : Record Item;
      Resource@1089 : Record Resource;
      Location@1017 : Record Location;
      FaultReasonCode@1037 : Record "Fault Reason Code";
      Currency@1043 : Record Currency;
      CurrExchRate@1044 : Record "Currency Exchange Rate";
      TempTrackingSpecification@1016 : TEMPORARY Record "Tracking Specification";
      SKU@1091 : Record "Stockkeeping Unit";
      DimMgt@1058 : Codeunit DimensionManagement;
      SalesTaxCalculate@1061 : Codeunit "Sales Tax Calculate";
      UOMMgt@1062 : Codeunit "Unit of Measure Management";
      NonstockItemMgt@1064 : Codeunit "Nonstock Item Management";
      ReserveServLine@1066 : Codeunit "Service Line-Reserve";
      WhseValidateSourceLine@1056 : Codeunit "Whse. Validate Source Line";
      Select@1069 : Integer;
      FullAutoReservation@1078 : Boolean;
      HideReplacementDialog@1074 : Boolean;
      Text022@1019 : TextConst 'ENU=The %1 cannot be greater than the %2 set on the %3.';
      Text023@1077 : TextConst 'ENU=You must enter a serial number.';
      ReplaceServItemAction@1080 : Boolean;
      Text026@1068 : TextConst 'ENU=When replacing or creating a service item component you may only enter a whole number into the %1 field.';
      Text027@1070 : TextConst 'ENU=The %1 %2 with a check mark in the %3 field cannot be entered if the service line type is other than Item or Resource.';
      Text028@1014 : TextConst 'ENU=You cannot consume more than %1 units.';
      Text029@1098 : TextConst 'ENU=must be positive';
      Text030@1094 : TextConst 'ENU=must be negative';
      Text031@1093 : TextConst 'ENU=You must specify %1.';
      Text032@1095 : TextConst 'ENU=You cannot consume more than %1 base units.';
      Text033@1001 : TextConst 'ENU=The line you are trying to change has the adjusted price.\';
      Text034@1000 : TextConst 'ENU=Do you want to continue?';
      Text035@1076 : TextConst 'ENU=Warehouse';
      Text036@1102 : TextConst 'ENU=Inventory';
      Text037@1092 : TextConst 'ENU=You cannot change %1 when %2 is %3 and %4 is positive.';
      Text038@1088 : TextConst 'ENU=You cannot change %1 when %2 is %3 and %4 is negative.';
      Text039@1081 : TextConst 'ENU=You cannot return more than %1 units for %2 %3.';
      Text040@1073 : TextConst 'ENU=You must use form %1 to enter %2, if item tracking is used.';
      Text041@1105 : TextConst 'ENU=There were no Resource Lines to split.';
      Text042@1072 : TextConst 'ENU=When posting the Applied to Ledger Entry %1 will be opened first';
      HideCostWarning@1003 : Boolean;
      HideWarrantyWarning@1096 : Boolean;
      Text043@1097 : TextConst 'ENU=You cannot change the value of the %1 field manually if %2 for this line is %3.';
      Text044@1012 : TextConst 'ENU=Do you want to split the resource line and use it to create resource lines\for the other service items with divided amounts?';
      Text045@1099 : TextConst 'ENU=You cannot delete this service line because one or more service entries exist for this line.';
      Text046@1067 : TextConst 'ENU=You cannot change the %1 when the %2 has been filled in.';
      Text047@1100 : TextConst 'ENU=%1 can only be set when %2 is set.';
      Text048@1101 : TextConst 'ENU=%1 cannot be changed when %2 is set.';
      Text049@1103 : TextConst '@@@=Example: Inventory put-away is required for Line 50000.;ENU="%1 is required for %2 = %3."';
      Text050@1104 : TextConst 'ENU=\The entered information may be disregarded by warehouse operations.';
      StatusCheckSuspended@1106 : Boolean;
      Text051@1107 : TextConst 'ENU=You cannot add an item line.';
      Text052@1108 : TextConst 'ENU=You cannot change the %1 field because one or more service entries exist for this line.';
      Text053@1020 : TextConst 'ENU=You cannot modify the service line because one or more service entries exist for this line.';
      IsCustCrLimitChecked@1049 : Boolean;
      LocationChangedMsg@1055 : TextConst '@@@="%1 = Item No., %2 = Item serial No., %3 = Location code";ENU=Item %1 with serial number %2 is stored on location %3. The Location Code field on the service line will be updated.';

    LOCAL PROCEDURE CheckItemAvailable@3(CalledByFieldNo@1000 : Integer);
    VAR
      ItemCheckAvail@1001 : Codeunit "Item-Check Avail.";
    BEGIN
      if "Needed by Date" = 0D then begin
        GetServHeader;
        if ServHeader."Order Date" <> 0D then
          VALIDATE("Needed by Date",ServHeader."Order Date")
        else
          VALIDATE("Needed by Date",WORKDATE);
      end;

      if CurrFieldNo <> CalledByFieldNo then
        exit;
      if not GUIALLOWED then
        exit;
      if Reserve = Reserve::Always then
        exit;
      if (Type <> Type::Item) or ("No." = '') then
        exit;
      if Quantity <= 0 then
        exit;
      if Nonstock then
        exit;
      if not ("Document Type" in ["Document Type"::Order,"Document Type"::Invoice]) then
        exit;

      if ItemCheckAvail.ServiceInvLineCheck(Rec) then
        ItemCheckAvail.RaiseUpdateInterruptedError;
    END;

    [External]
    PROCEDURE CreateDim@26(Type1@1000 : Integer;No1@1001 : Code[20];Type2@1002 : Integer;No2@1003 : Code[20];Type3@1004 : Integer;No3@1005 : Code[20]);
    VAR
      SourceCodeSetup@1006 : Record "Source Code Setup";
      TableID@1007 : ARRAY [10] OF Integer;
      No@1008 : ARRAY [10] OF Code[20];
      DimensionSetID@1009 : Integer;
    BEGIN
      SourceCodeSetup.GET;
      GetServHeader;
      if not ServItemLine.GET(ServHeader."Document Type",ServHeader."No.","Service Item Line No.") then
        ServItemLine.INIT;

      TableID[1] := Type1;
      No[1] := No1;
      TableID[2] := Type2;
      No[2] := No2;
      TableID[3] := Type3;
      No[3] := No3;
      OnAfterCreateDimTableIDs(Rec,CurrFieldNo,TableID,No);

      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      DimensionSetID := ServItemLine."Dimension Set ID";
      if DimensionSetID = 0 then
        DimensionSetID := ServHeader."Dimension Set ID";
      UpdateDimSetupFromDimSetID(TableID,No,DimensionSetID);
      "Dimension Set ID" :=
        DimMgt.GetRecDefaultDimID(
          Rec,CurrFieldNo,TableID,No,SourceCodeSetup."Service Management",
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",DimensionSetID,DATABASE::Customer);
      DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    [External]
    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    [External]
    PROCEDURE LookupShortcutDimCode@28(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      ValidateShortcutDimCode(FieldNumber,ShortcutDimCode);
    END;

    [External]
    PROCEDURE ShowShortcutDimCode@27(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      DimMgt.GetShortcutDimensions("Dimension Set ID",ShortcutDimCode);
    END;

    LOCAL PROCEDURE ReplaceServItem@8() : Boolean;
    VAR
      ServItemReplacement@1000 : Page "Service Item Replacement";
      SerialNo@1001 : Code[20];
      VariantCode@1002 : Code[10];
      LocationCode@1004 : Code[10];
    BEGIN
      ErrorIfAlreadySelectedSI("Service Item Line No.");
      CLEAR(ServItemReplacement);
      ServItemReplacement.SetValues("Service Item No.","No.","Variant Code");
      COMMIT;
      if ServItemReplacement.RUNMODAL = ACTION::OK then begin
        SerialNo := ServItemReplacement.ReturnSerialNo;
        VariantCode := ServItemReplacement.ReturnVariantCode;
        if SerialNo = '' then
          CheckItemTrackingCode(Item)
        else
          if FindSerialNoStorageLocation(LocationCode,Item."No.",SerialNo,VariantCode) and (LocationCode <> "Location Code") then begin
            VALIDATE("Location Code",LocationCode);
            MESSAGE(STRSUBSTNO(LocationChangedMsg,Item."No.",SerialNo,LocationCode));
          end;

        "Variant Code" := VariantCode;
        VALIDATE(Quantity,1);
        TempTrackingSpecification.DELETEALL;
        TempTrackingSpecification."Serial No." := SerialNo;
        TempTrackingSpecification."Variant Code" := VariantCode;
        TempTrackingSpecification.INSERT;
        if "Line No." <> 0 then
          InsertItemTracking;
        case ServItemReplacement.ReturnReplacement of
          0:
            "Spare Part Action" := "Spare Part Action"::"Temporary";
          1:
            "Spare Part Action" := "Spare Part Action"::Permanent;
        end;
        "Copy Components From" := ServItemReplacement.ReturnCopyComponentsFrom;
        exit(true);
      end;
      ReserveServLine.DeleteLine(Rec);
      Clearfields;
      VALIDATE("No.",'');
      exit(false);
    END;

    LOCAL PROCEDURE FindSerialNoStorageLocation@86(VAR LocationCode@1004 : Code[10];ItemNo@1000 : Code[20];SerialNo@1001 : Code[20];VariantCode@1003 : Code[10]) : Boolean;
    VAR
      ItemLedgerEntry@1002 : Record "Item Ledger Entry";
    BEGIN
      ItemLedgerEntry.SETRANGE("Item No.",ItemNo);
      ItemLedgerEntry.SETRANGE("Serial No.",SerialNo);
      ItemLedgerEntry.SETRANGE("Variant Code",VariantCode);
      ItemLedgerEntry.SETRANGE(Open,true);
      if not ItemLedgerEntry.FINDLAST then
        exit(false);

      LocationCode := ItemLedgerEntry."Location Code";
      exit(true);
    END;

    LOCAL PROCEDURE CheckItemTrackingCode@87(ReplacementItem@1001 : Record Item);
    VAR
      ItemTrackingCode@1000 : Record "Item Tracking Code";
    BEGIN
      if ReplacementItem."Item Tracking Code" = '' then
        exit;

      ItemTrackingCode.GET(ReplacementItem."Item Tracking Code");
      if ItemTrackingCode."SN Specific Tracking" then
        ERROR(Text023);
    END;

    LOCAL PROCEDURE ErrorIfAlreadySelectedSI@107(ServItemLineNo@1000 : Integer);
    BEGIN
      if "Document Type" <> "Document Type"::Order then
        exit;
      if ServItemLineNo <> 0 then begin
        ServItemLine.GET("Document Type","Document No.",ServItemLineNo);
        if (ServItemLine."Service Item No." = '') or
           (ServItemLine."Item No." <> "No.")
        then
          exit;
      end;

      ServiceLine.RESET;
      ServiceLine.SETCURRENTKEY("Document Type","Document No.","Service Item Line No.",Type,"No.");
      ServiceLine.SETRANGE("Document Type","Document Type");
      ServiceLine.SETRANGE("Document No.","Document No.");
      ServiceLine.SETRANGE("Service Item Line No.",ServItemLineNo);
      ServiceLine.SETRANGE(Type,Type::Item);
      ServiceLine.SETFILTER("Line No.",'<>%1',"Line No.");
      ServiceLine.SETRANGE("No.","No.");
      if ServiceLine.FINDFIRST then
        ERROR(Text015,Item.TABLECAPTION,"No.");
    END;

    LOCAL PROCEDURE CalculateDiscount@7();
    VAR
      SalesPriceCalcMgt@1002 : Codeunit "Sales Price Calc. Mgt.";
      Discounts@1000 : ARRAY [4] OF Decimal;
      i@1001 : Integer;
    BEGIN
      if "Exclude Warranty" or not Warranty then
        Discounts[1] := 0
      else begin
        if ServItemLine.GET("Document Type","Document No.","Service Item Line No.") then
          case Type of
            Type::Item:
              "Warranty Disc. %" := ServItemLine."Warranty % (Parts)";
            Type::Resource:
              "Warranty Disc. %" := ServItemLine."Warranty % (Labor)";
          end;
        Discounts[1] := "Warranty Disc. %";
      end;

      if "Exclude Contract Discount" then
        if CurrFieldNo = FIELDNO("Fault Reason Code") then
          Discounts[2] := "Line Discount %"
        else
          Discounts[2] := 0
      else
        Discounts[2] := "Contract Disc. %";

      ServHeader.GET("Document Type","Document No.");
      SalesPriceCalcMgt.FindServLineDisc(ServHeader,Rec);
      Discounts[3] := "Line Discount %";
      if Discounts[3] > 100 then
        Discounts[3] := 100;

      "Line Discount Type" := "Line Discount Type"::" ";
      "Line Discount %" := 0;

      if "Line Discount Type" = "Line Discount Type"::Manual then
        Discounts[4] := "Line Discount %"
      else
        Discounts[4] := 0;

      for i := 1 to 4 do
        if Discounts[i] > "Line Discount %" then begin
          "Line Discount Type" := i;
          "Line Discount %" := Discounts[i];
        end;
    END;

    [Internal]
    PROCEDURE UpdateAmounts@13();
    VAR
      CustCheckCrLimit@1000 : Codeunit "Cust-Check Cr. Limit";
    BEGIN
      if GUIALLOWED and (CurrFieldNo <> 0) then
        ConfirmAdjPriceLineChange;

      GetServHeader;

      if "Line Amount" <> xRec."Line Amount" then
        "VAT Difference" := 0;
      if "Line Amount" <>
         ROUND(
           CalcChargeableQty * "Unit Price",
           Currency."Amount Rounding Precision") - "Line Discount Amount"
      then begin
        "Line Amount" :=
          ROUND(CalcChargeableQty * "Unit Price",
            Currency."Amount Rounding Precision") - "Line Discount Amount";
        "VAT Difference" := 0;
      end;
      UpdateVATAmounts;

      InitOutstandingAmount;
      if not IsCustCrLimitChecked and (CurrFieldNo <> 0) then begin
        IsCustCrLimitChecked := true;
        CustCheckCrLimit.ServiceLineCheck(Rec);
      end;
      UpdateRemainingCostsAndAmounts;
    END;

    LOCAL PROCEDURE GetItem@12();
    BEGIN
      TESTFIELD("No.");
      if "No." <> Item."No." then
        Item.GET("No.");
    END;

    LOCAL PROCEDURE GetDate@22() : Date;
    BEGIN
      if ServHeader."Document Type" = ServHeader."Document Type"::Quote then
        exit(WORKDATE);

      exit(ServHeader."Posting Date");
    END;

    LOCAL PROCEDURE GetServHeader@14();
    BEGIN
      TESTFIELD("Document No.");
      if ("Document Type" <> ServHeader."Document Type") or ("Document No." <> ServHeader."No.") then begin
        ServHeader.GET("Document Type","Document No.");
        if ServHeader."Currency Code" = '' then
          Currency.InitRoundingPrecision
        else begin
          ServHeader.TESTFIELD("Currency Factor");
          Currency.GET(ServHeader."Currency Code");
          Currency.TESTFIELD("Amount Rounding Precision");
        end;
      end;
    END;

    LOCAL PROCEDURE InitHeaderDefaults@90(ServHeader@1000 : Record "Service Header");
    VAR
      ServOrderMgt@1001 : Codeunit ServOrderManagement;
    BEGIN
      "Customer No." := ServHeader."Customer No.";
      if "Service Item Line No." <> 0 then begin
        ServItemLine.GET(ServHeader."Document Type",ServHeader."No.","Service Item Line No.");
        "Ship-to Code" := ServItemLine."Ship-to Code";
      end else
        "Ship-to Code" := ServHeader."Ship-to Code";
      if "Posting Date" = 0D then
        "Posting Date" := ServHeader."Posting Date";
      "Document Type" := ServHeader."Document Type";

      "Order Date" := ServHeader."Order Date";
      "Replaced Item No." := '';
      "Component Line No." := 0;
      "Spare Part Action" := 0;
      "Price Adjmt. Status" := "Price Adjmt. Status"::" ";
      "Exclude Warranty" := false;
      "Exclude Contract Discount" := false;
      "Fault Reason Code" := '';

      "Bill-to Customer No." := ServHeader."Bill-to Customer No.";
      "Customer Price Group" := ServHeader."Customer Price Group";
      "Customer Disc. Group" := ServHeader."Customer Disc. Group";
      "Allow Line Disc." := ServHeader."Allow Line Disc.";
      "Bin Code" := '';
      "Transaction Type" := ServHeader."Transaction Type";
      "Transport Method" := ServHeader."Transport Method";
      "Exit Point" := ServHeader."Exit Point";
      Area := ServHeader.Area;
      "Transaction Specification" := ServHeader."Transaction Specification";

      "Location Code" := '';
      if Type = Type::Resource then
        "Location Code" := ServOrderMgt.FindResLocationCode("No.",ServHeader."Order Date");
      if "Location Code" = '' then
        "Location Code" := ServHeader."Location Code";
      if Type = Type::Item then begin
        if (xRec."No." <> "No.") and (Quantity <> 0) then
          WhseValidateSourceLine.ServiceLineVerifyChange(Rec,xRec);
        GetLocation("Location Code");
      end;

      "Gen. Bus. Posting Group" := ServHeader."Gen. Bus. Posting Group";
      "VAT Bus. Posting Group" := ServHeader."VAT Bus. Posting Group";
      "Tax Area Code" := ServHeader."Tax Area Code";
      "Tax Liable" := ServHeader."Tax Liable";
      "Responsibility Center" := ServHeader."Responsibility Center";
      "Posting Date" := ServHeader."Posting Date";
      "Currency Code" := ServHeader."Currency Code";

      "Shipping Agent Code" := ServHeader."Shipping Agent Code";
      "Shipping Agent Service Code" := ServHeader."Shipping Agent Service Code";
      "Shipping Time" := ServHeader."Shipping Time";

      OnAfterAssignHeaderValues(Rec,ServHeader);
    END;

    [Internal]
    PROCEDURE UpdateUnitPrice@19(CalledByFieldNo@1000 : Integer);
    VAR
      SalesPriceCalcMgt@1001 : Codeunit "Sales Price Calc. Mgt.";
    BEGIN
      OnBeforeUpdateUnitPrice(Rec,xRec,CalledByFieldNo,CurrFieldNo);

      TESTFIELD("Qty. per Unit of Measure");
      ServHeader.GET("Document Type","Document No.");

      CalculateDiscount;
      SalesPriceCalcMgt.FindServLinePrice(ServHeader,Rec,CalledByFieldNo);
      VALIDATE("Unit Price");

      OnAfterUpdateUnitPrice(Rec,xRec,CalledByFieldNo,CurrFieldNo);
    END;

    [External]
    PROCEDURE ShowDimensions@25();
    BEGIN
      if ("Contract No." <> '') and ("Appl.-to Service Entry" <> 0) then
        ViewDimensionSetEntries
      else
        "Dimension Set ID" :=
          DimMgt.EditDimensionSet2(
            "Dimension Set ID",STRSUBSTNO('%1 %2 %3',"Document Type","Document No.","Line No."),
            "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    [External]
    PROCEDURE ShowReservation@10();
    VAR
      Reservation@1000 : Page Reservation;
    BEGIN
      TESTFIELD(Type,Type::Item);
      TESTFIELD("No.");
      TESTFIELD(Reserve);
      CLEAR(Reservation);
      Reservation.SetServiceLine(Rec);
      Reservation.RUNMODAL;
    END;

    [External]
    PROCEDURE ShowReservationEntries@21(Modal@1000 : Boolean);
    VAR
      ReservEntry@1001 : Record "Reservation Entry";
      ReservEngineMgt@1002 : Codeunit "Reservation Engine Mgt.";
    BEGIN
      TESTFIELD(Type,Type::Item);
      TESTFIELD("No.");
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,true);
      ReserveServLine.FilterReservFor(ReservEntry,Rec);
      if Modal then
        PAGE.RUNMODAL(PAGE::"Reservation Entries",ReservEntry)
      else
        PAGE.RUN(PAGE::"Reservation Entries",ReservEntry);
    END;

    [External]
    PROCEDURE AutoReserve@39();
    VAR
      ReservMgt@1002 : Codeunit "Reservation Management";
      QtyToReserve@1000 : Decimal;
      QtyToReserveBase@1001 : Decimal;
    BEGIN
      TESTFIELD(Type,Type::Item);
      TESTFIELD("No.");
      if Reserve = Reserve::Never then
        FIELDERROR(Reserve);
      ReserveServLine.ReservQuantity(Rec,QtyToReserve,QtyToReserveBase);
      if QtyToReserveBase <> 0 then begin
        ReservMgt.SetServLine(Rec);
        ReservMgt.AutoReserve(FullAutoReservation,'',"Order Date",QtyToReserve,QtyToReserveBase);
        FIND;
        if not FullAutoReservation then begin
          COMMIT;
          if CONFIRM(Text012,true) then begin
            ShowReservation;
            FIND;
          end;
        end;
      end;
    END;

    LOCAL PROCEDURE Clearfields@6();
    VAR
      TempServLine@1000 : TEMPORARY Record "Service Line";
    BEGIN
      TempServLine := Rec;
      INIT;

      if CurrFieldNo <> FIELDNO(Type) then
        "No." := TempServLine."No.";

      Type := TempServLine.Type;
      if Type <> Type::" " then
        Quantity := TempServLine.Quantity;

      "Line No." := TempServLine."Line No.";
      VALIDATE("Service Item Line No.",TempServLine."Service Item Line No.");
      "Service Item No." := TempServLine."Service Item No.";
      "Service Item Serial No." := TempServLine."Service Item Serial No.";
      "Document Type" := TempServLine."Document Type";
      "Document No." := TempServLine."Document No.";
      "Gen. Bus. Posting Group" := TempServLine."Gen. Bus. Posting Group";
      "Order Date" := TempServLine."Order Date";
      "Customer No." := TempServLine."Customer No.";
      "Ship-to Code" := TempServLine."Ship-to Code";
      "Posting Date" := TempServLine."Posting Date";
      "System-Created Entry" := TempServLine."System-Created Entry";
      "Price Adjmt. Status" := "Price Adjmt. Status"::" ";
      "Time Sheet No." := TempServLine."Time Sheet No.";
      "Time Sheet Line No." := TempServLine."Time Sheet Line No.";
      "Time Sheet Date" := TempServLine."Time Sheet Date";
      if "No." <> xRec."No." then
        VALIDATE("Job Planning Line No.",0);
    END;

    [External]
    PROCEDURE ShowNonstock@32();
    VAR
      NonstockItem@1000 : Record "Nonstock Item";
      ConfigTemplateHeader@1002 : Record "Config. Template Header";
      ItemTemplate@1001 : Record "Item Template";
    BEGIN
      TESTFIELD(Type,Type::Item);
      TESTFIELD("No.",'');
      if PAGE.RUNMODAL(PAGE::"Nonstock Item List",NonstockItem) = ACTION::LookupOK then begin
        NonstockItem.TESTFIELD("Item Template Code");
        ConfigTemplateHeader.SETRANGE("Table ID",DATABASE::Item);
        ConfigTemplateHeader.SETRANGE(Code,NonstockItem."Item Template Code");
        ConfigTemplateHeader.SETRANGE(Enabled,true);
        ConfigTemplateHeader.FINDFIRST;

        TestConfigTemplateLineField(NonstockItem."Item Template Code",ItemTemplate.FIELDNO("Gen. Prod. Posting Group"));
        TestConfigTemplateLineField(NonstockItem."Item Template Code",ItemTemplate.FIELDNO("Inventory Posting Group"));

        "No." := NonstockItem."Entry No.";
        NonstockItemMgt.NonStockFSM(Rec);
        VALIDATE("No.","No.");
        VALIDATE("Unit Price",NonstockItem."Unit Price");
      end;
    END;

    LOCAL PROCEDURE TestConfigTemplateLineField@102(ItemTemplateCode@1001 : Code[10];FieldNo@1002 : Integer);
    VAR
      ConfigTemplateLine@1000 : Record "Config. Template Line";
    BEGIN
      ConfigTemplateLine.SETRANGE("Data Template Code",ItemTemplateCode);
      ConfigTemplateLine.SETRANGE("Table ID",DATABASE::Item);
      ConfigTemplateLine.SETRANGE("Field ID",FieldNo);
      ConfigTemplateLine.FINDFIRST;
      ConfigTemplateLine.TESTFIELD("Default Value");
    END;

    LOCAL PROCEDURE CalcBaseQty@11(Qty@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      exit(ROUND(Qty * "Qty. per Unit of Measure",0.00001));
    END;

    LOCAL PROCEDURE CopyFromCost@94();
    VAR
      ServCost@1000 : Record "Service Cost";
      GLAcc@1001 : Record "G/L Account";
    BEGIN
      ServCost.GET("No.");
      if ServCost."Cost Type" = ServCost."Cost Type"::Travel then
        if ServHeader."Service Zone Code" <> ServCost."Service Zone Code" then
          if not HideCostWarning then
            if not
               CONFIRM(
                 STRSUBSTNO(
                   Text004,ServCost.TABLECAPTION,"No.",
                   ServCost.FIELDCAPTION("Service Zone Code"),
                   ServHeader.FIELDCAPTION("Service Zone Code"),
                   ServHeader.TABLECAPTION,ServHeader."No."),false)
            then
              ERROR(Text005);
      Description := ServCost.Description;
      VALIDATE("Unit Cost (LCY)",ServCost."Default Unit Cost");
      "Unit Price" := ServCost."Default Unit Price";
      "Unit of Measure Code" := ServCost."Unit of Measure Code";
      GLAcc.GET(ServCost."Account No.");
      GLAcc.TESTFIELD("Gen. Prod. Posting Group");
      "Gen. Prod. Posting Group" := GLAcc."Gen. Prod. Posting Group";
      "VAT Prod. Posting Group" := GLAcc."VAT Prod. Posting Group";
      "Tax Group Code" := GLAcc."Tax Group Code";
      if "Service Item Line No." <> 0 then
        if FaultReasonCode.GET(ServItemLine."Fault Reason Code") and
           (not FaultReasonCode."Exclude Warranty Discount")
        then
          VALIDATE("Fault Reason Code",ServItemLine."Fault Reason Code");
      Quantity := ServCost."Default Quantity";

      OnAfterAssignServCostValues(Rec,ServCost);
    END;

    LOCAL PROCEDURE CopyFromStdTxt@91();
    VAR
      StandardText@1000 : Record "Standard Text";
    BEGIN
      StandardText.GET("No.");
      Description := StandardText.Description;

      OnAfterAssignStdTxtValues(Rec,StandardText);
    END;

    LOCAL PROCEDURE CopyFromGLAccount@92();
    VAR
      GLAcc@1000 : Record "G/L Account";
    BEGIN
      GLAcc.GET("No.");
      GLAcc.CheckGLAcc;
      if not "System-Created Entry" then
        GLAcc.TESTFIELD("Direct Posting",true);
      Description := GLAcc.Name;
      "Gen. Prod. Posting Group" := GLAcc."Gen. Prod. Posting Group";
      "VAT Prod. Posting Group" := GLAcc."VAT Prod. Posting Group";
      "Tax Group Code" := GLAcc."Tax Group Code";
      "Allow Invoice Disc." := false;

      OnAfterAssignGLAccountValues(Rec,GLAcc);
    END;

    LOCAL PROCEDURE CopyFromItem@93();
    BEGIN
      GetItem;
      Item.TESTFIELD(Blocked,false);
      Item.TESTFIELD("Inventory Posting Group");
      Item.TESTFIELD("Gen. Prod. Posting Group");
      Description := Item.Description;
      "Description 2" := Item."Description 2";
      GetUnitCost;
      "Allow Invoice Disc." := Item."Allow Invoice Disc.";
      "Units per Parcel" := Item."Units per Parcel";
      CALCFIELDS("Substitution Available");

      "Gen. Prod. Posting Group" := Item."Gen. Prod. Posting Group";
      "VAT Prod. Posting Group" := Item."VAT Prod. Posting Group";
      "Tax Group Code" := Item."Tax Group Code";
      "Posting Group" := Item."Inventory Posting Group";
      "Item Category Code" := Item."Item Category Code";
      "Product Group Code" := Item."Product Group Code";
      "Variant Code" := '';
      Nonstock := Item."Created From Nonstock Item";
      "Unit of Measure Code" := Item."Sales Unit of Measure";

      if ServHeader."Language Code" <> '' then
        GetItemTranslation;

      if Item.Reserve = Item.Reserve::Optional then
        Reserve := ServHeader.Reserve
      else
        Reserve := Item.Reserve;

      if "Service Item Line No." <> 0 then begin
        "Warranty Disc. %" := ServItemLine."Warranty % (Parts)";
        Warranty :=
          ServItemLine.Warranty and
          (ServHeader."Order Date" >= ServItemLine."Warranty Starting Date (Parts)") and
          (ServHeader."Order Date" <= ServItemLine."Warranty Ending Date (Parts)") and
          not "Exclude Warranty";
        VALIDATE("Fault Reason Code",ServItemLine."Fault Reason Code");
      end else begin
        Warranty := false;
        "Warranty Disc. %" := 0;
      end;

      OnAfterAssignItemValues(Rec,Item);
    END;

    LOCAL PROCEDURE CopyFromServItem@96();
    VAR
      ServItem2@1000 : Record "Service Item";
      ServItemComponent@1001 : Record "Service Item Component";
    BEGIN
      if ServItem."Item No." = "No." then begin
        ServItemLine.RESET;
        if not HideReplacementDialog then begin
          ReplaceServItemAction := ReplaceServItem;
          if not ReplaceServItemAction then
            exit;
        end;
      end else begin
        ServItem.CALCFIELDS("Service Item Components");
        if ServItem."Service Item Components" and not HideReplacementDialog then begin
          Select := STRMENU(Text006,3);
          case Select of
            1:
              begin
                COMMIT;
                ServItemComponent.RESET;
                ServItemComponent.SETRANGE(Active,true);
                ServItemComponent.SETRANGE("Parent Service Item No.",ServItem."No.");
                if PAGE.RUNMODAL(0,ServItemComponent) = ACTION::LookupOK then begin
                  "Replaced Item Type" := ServItemComponent.Type + 1;
                  "Replaced Item No." := ServItemComponent."No.";
                  "Component Line No." := ServItemComponent."Line No.";
                  CheckIfServItemReplacement("Component Line No.");
                  if ServItemComponent.Type = ServItemComponent.Type::"Service Item" then begin
                    ServItem2.GET(ServItemComponent."No.");
                    "Warranty Disc. %" := ServItem2."Warranty % (Parts)";
                  end;
                  "Spare Part Action" := "Spare Part Action"::"Component Replaced";
                end else
                  ERROR(Text007,ServItemComponent.TABLECAPTION);
              end;
            2:
              begin
                "Replaced Item No." := '';
                "Component Line No." := 0;
                "Spare Part Action" := "Spare Part Action"::"Component Installed";
              end;
          end;
        end;
      end;

      OnAfterAssignServItemValues(Rec,ServItem,ServItemComponent);
    END;

    LOCAL PROCEDURE CopyFromResource@98();
    VAR
      Res@1000 : Record Resource;
    BEGIN
      Res.GET("No.");
      Res.TESTFIELD(Blocked,false);
      Res.TESTFIELD("Gen. Prod. Posting Group");
      Description := Res.Name;
      "Description 2" := Res."Name 2";
      if "Service Item Line No." <> 0 then begin
        "Warranty Disc. %" := ServItemLine."Warranty % (Labor)";
        Warranty :=
          ServItemLine.Warranty and
          (ServHeader."Order Date" >= ServItemLine."Warranty Starting Date (Labor)") and
          (ServHeader."Order Date" <= ServItemLine."Warranty Ending Date (Labor)") and
          not "Exclude Warranty";
        VALIDATE("Fault Reason Code",ServItemLine."Fault Reason Code");
      end else begin
        Warranty := false;
        "Warranty Disc. %" := 0;
      end;
      "Unit of Measure Code" := Res."Base Unit of Measure";
      VALIDATE("Unit Cost (LCY)",Res."Unit Cost");
      "Gen. Prod. Posting Group" := Res."Gen. Prod. Posting Group";
      "VAT Prod. Posting Group" := Res."VAT Prod. Posting Group";
      "Tax Group Code" := Res."Tax Group Code";
      FindResUnitCost;

      OnAfterAssignResourceValues(Rec,Res);
    END;

    [Internal]
    PROCEDURE ShowItemSub@30();
    VAR
      ItemSubstMgt@1000 : Codeunit "Item Subst.";
    BEGIN
      ItemSubstMgt.ItemServiceSubstGet(Rec);
    END;

    [External]
    PROCEDURE SetHideReplacementDialog@18(NewHideReplacementDialog@1000 : Boolean);
    BEGIN
      HideReplacementDialog := NewHideReplacementDialog;
    END;

    LOCAL PROCEDURE CheckIfServItemReplacement@9(ComponentLineNo@1000 : Integer);
    BEGIN
      if "Service Item Line No." <> 0 then begin
        ServiceLine.RESET;
        ServiceLine.SETCURRENTKEY("Document Type","Document No.","Service Item Line No.","Component Line No.");
        ServiceLine.SETRANGE("Document Type","Document Type");
        ServiceLine.SETRANGE("Document No.","Document No.");
        ServiceLine.SETRANGE("Service Item Line No.","Service Item Line No.");
        ServiceLine.SETFILTER("Line No.",'<>%1',"Line No.");
        ServiceLine.SETRANGE("Component Line No.",ComponentLineNo);
        ServiceLine.SETFILTER("Spare Part Action",'<>%1',"Spare Part Action"::" ");
        if ServiceLine.FINDFIRST then
          ERROR(CompAlreadyReplacedErr,ServiceLine."Line No.");
      end;
    END;

    [External]
    PROCEDURE IsInbound@79() : Boolean;
    BEGIN
      case "Document Type" of
        "Document Type"::Quote,"Document Type"::Order,ServiceLine."Document Type"::Invoice:
          exit("Quantity (Base)" < 0);
        ServiceLine."Document Type"::"Credit Memo":
          exit("Quantity (Base)" > 0);
      end;

      exit(false);
    END;

    [External]
    PROCEDURE OpenItemTrackingLines@6500();
    BEGIN
      TESTFIELD(Type,Type::Item);
      TESTFIELD("No.");
      TESTFIELD("Quantity (Base)");
      ReserveServLine.CallItemTracking(Rec);
    END;

    LOCAL PROCEDURE InsertItemTracking@20();
    VAR
      ReservEntry@1001 : Record "Reservation Entry";
      CreateReservEntry@1000 : Codeunit "Create Reserv. Entry";
    BEGIN
      ServiceLine := Rec;
      if TempTrackingSpecification.FINDFIRST then begin
        ReserveServLine.DeleteLine(Rec);
        CLEAR(CreateReservEntry);
        with ServiceLine do begin
          CreateReservEntry.CreateReservEntryFor(DATABASE::"Service Line","Document Type","Document No.",
            '',0,"Line No.","Qty. per Unit of Measure",Quantity,"Quantity (Base)",
            TempTrackingSpecification."Serial No.",TempTrackingSpecification."Lot No.");
          CreateReservEntry.CreateEntry("No.","Variant Code","Location Code",Description,
            0D,"Posting Date",0,ReservEntry."Reservation Status"::Surplus);
        end;
        TempTrackingSpecification.DELETEALL;
      end;
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10]);
    BEGIN
      if LocationCode = '' then
        CLEAR(Location)
      else
        if Location.Code <> LocationCode then
          Location.GET(LocationCode);
    END;

    LOCAL PROCEDURE GetDefaultBin@50();
    VAR
      Bin@1002 : Record Bin;
      BinType@1001 : Record "Bin Type";
      WMSManagement@1000 : Codeunit "WMS Management";
    BEGIN
      if Type <> Type::Item then
        exit;

      "Bin Code" := '';

      if ("Location Code" <> '') and ("No." <> '') then begin
        GetLocation("Location Code");
        if not Location."Bin Mandatory" then
          exit;
        if (not Location."Directed Put-away and Pick") or ("Document Type" <> "Document Type"::Order) then begin
          WMSManagement.GetDefaultBin("No.","Variant Code","Location Code","Bin Code");
          if ("Document Type" <> "Document Type"::Order) and ("Bin Code" <> '') and Location."Directed Put-away and Pick"
          then begin
            // Clear the bin code if the bin is not of pick type
            Bin.GET("Location Code","Bin Code");
            BinType.GET(Bin."Bin Type Code");
            if not BinType.Pick then
              "Bin Code" := '';
          end;
        end;
      end;
    END;

    LOCAL PROCEDURE GetItemTranslation@42();
    VAR
      ItemTranslation@1000 : Record "Item Translation";
    BEGIN
      GetServHeader;
      if ItemTranslation.GET("No.","Variant Code",ServHeader."Language Code") then begin
        Description := ItemTranslation.Description;
        "Description 2" := ItemTranslation."Description 2";
      end;
    END;

    LOCAL PROCEDURE GetSKU@5806() : Boolean;
    BEGIN
      if (SKU."Location Code" = "Location Code") and
         (SKU."Item No." = "No.") and
         (SKU."Variant Code" = "Variant Code")
      then
        exit(true);
      if SKU.GET("Location Code","No.","Variant Code") then
        exit(true);

      exit(false);
    END;

    [External]
    PROCEDURE GetUnitCost@5808();
    BEGIN
      TESTFIELD(Type,Type::Item);
      TESTFIELD("No.");
      GetItem;
      "Qty. per Unit of Measure" := UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code");
      if GetSKU then
        VALIDATE("Unit Cost (LCY)",SKU."Unit Cost" * "Qty. per Unit of Measure")
      else
        VALIDATE("Unit Cost (LCY)",Item."Unit Cost" * "Qty. per Unit of Measure");
    END;

    LOCAL PROCEDURE FindResUnitCost@54();
    VAR
      ResCost@1000 : Record "Resource Cost";
    BEGIN
      ResCost.INIT;
      ResCost.Code := "No.";
      ResCost."Work Type Code" := "Work Type Code";
      CODEUNIT.RUN(CODEUNIT::"Resource-Find Cost",ResCost);
      VALIDATE("Unit Cost (LCY)",ResCost."Unit Cost" * "Qty. per Unit of Measure");
    END;

    [External]
    PROCEDURE InitOutstanding@62();
    BEGIN
      if "Document Type" = "Document Type"::"Credit Memo" then begin
        "Outstanding Quantity" := Quantity;
        "Outstanding Qty. (Base)" := "Quantity (Base)";
      end else begin
        "Outstanding Quantity" := Quantity - "Quantity Shipped";
        "Outstanding Qty. (Base)" := "Quantity (Base)" - "Qty. Shipped (Base)";
        "Qty. Shipped Not Invoiced" := "Quantity Shipped" - "Quantity Invoiced" - "Quantity Consumed";
        "Qty. Shipped Not Invd. (Base)" := "Qty. Shipped (Base)" - "Qty. Invoiced (Base)" - "Qty. Consumed (Base)";
      end;
      CALCFIELDS("Reserved Quantity");
      Planned := "Reserved Quantity" = "Outstanding Quantity";
      "Completely Shipped" := (Quantity <> 0) and ("Outstanding Quantity" = 0);
      InitOutstandingAmount;
    END;

    [External]
    PROCEDURE InitOutstandingAmount@61();
    VAR
      AmountInclVAT@1000 : Decimal;
    BEGIN
      if (Quantity = 0) or (CalcChargeableQty = 0) then begin
        "Outstanding Amount" := 0;
        "Outstanding Amount (LCY)" := 0;
        "Shipped Not Invoiced" := 0;
        "Shipped Not Invoiced (LCY)" := 0;
      end else begin
        GetServHeader;
        AmountInclVAT := "Line Amount" - "Inv. Discount Amount";
        if not ServHeader."Prices Including VAT" then
          if "VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax" then
            AmountInclVAT := AmountInclVAT +
              ROUND(
                SalesTaxCalculate.CalculateTax(
                  "Tax Area Code","Tax Group Code","Tax Liable",ServHeader."Posting Date",
                  "Line Amount" - "Inv. Discount Amount","Quantity (Base)",ServHeader."Currency Factor"),
                Currency."Amount Rounding Precision")
          else
            AmountInclVAT :=
              ROUND(
                AmountInclVAT *
                (1 + "VAT %" / 100 * (1 - ServHeader."VAT Base Discount %" / 100)),
                Currency."Amount Rounding Precision");
        VALIDATE(
          "Outstanding Amount",
          ROUND(
            AmountInclVAT * "Outstanding Quantity" / Quantity,
            Currency."Amount Rounding Precision"));
        if "Document Type" <> "Document Type"::"Credit Memo" then
          VALIDATE(
            "Shipped Not Invoiced",
            ROUND(
              AmountInclVAT * "Qty. Shipped Not Invoiced" / CalcChargeableQty,
              Currency."Amount Rounding Precision"));
      end;

      OnAfterInitOutstandingAmount(Rec,ServHeader,Currency);
    END;

    [External]
    PROCEDURE InitQtyToShip@60();
    BEGIN
      if LineRequiresShipmentOrReceipt then begin
        "Qty. to Ship" := 0;
        "Qty. to Ship (Base)" := 0;
      end else begin
        "Qty. to Ship" := "Outstanding Quantity";
        "Qty. to Ship (Base)" := "Outstanding Qty. (Base)";
      end;
      VALIDATE("Qty. to Consume");
      InitQtyToInvoice;

      OnAfterInitQtyToShip(Rec,CurrFieldNo);
    END;

    [External]
    PROCEDURE InitQtyToInvoice@59();
    BEGIN
      "Qty. to Invoice" := MaxQtyToInvoice;
      "Qty. to Invoice (Base)" := MaxQtyToInvoiceBase;
      "VAT Difference" := 0;
      CalcInvDiscToInvoice;

      OnAfterInitQtyToInvoice(Rec,CurrFieldNo);
    END;

    [External]
    PROCEDURE MaxQtyToInvoice@58() : Decimal;
    BEGIN
      if "Document Type" = "Document Type"::"Credit Memo" then
        exit(Quantity);

      exit("Quantity Shipped" + "Qty. to Ship" - "Quantity Invoiced" - "Quantity Consumed" - "Qty. to Consume");
    END;

    LOCAL PROCEDURE MaxQtyToInvoiceBase@57() : Decimal;
    BEGIN
      if "Document Type" = "Document Type"::"Credit Memo" then
        exit("Quantity (Base)");

      exit(
        "Qty. Shipped (Base)" + "Qty. to Ship (Base)" -
        "Qty. Invoiced (Base)" - "Qty. Consumed (Base)" -
        "Qty. to Consume (Base)");
    END;

    LOCAL PROCEDURE CalcInvDiscToInvoice@37();
    VAR
      OldInvDiscAmtToInv@1000 : Decimal;
    BEGIN
      GetServHeader;
      OldInvDiscAmtToInv := "Inv. Disc. Amount to Invoice";
      if (Quantity = 0) or (CalcChargeableQty = 0) then
        VALIDATE("Inv. Disc. Amount to Invoice",0)
      else
        VALIDATE(
          "Inv. Disc. Amount to Invoice",
          ROUND(
            "Inv. Discount Amount" * "Qty. to Invoice" / CalcChargeableQty,
            Currency."Amount Rounding Precision"));

      if OldInvDiscAmtToInv <> "Inv. Disc. Amount to Invoice" then begin
        "Amount Including VAT" := "Amount Including VAT" - "VAT Difference";
        "VAT Difference" := 0;
      end;
    END;

    [External]
    PROCEDURE ItemExists@63(ItemNo@1000 : Code[20]) : Boolean;
    VAR
      Item2@1001 : Record Item;
    BEGIN
      if Type = Type::Item then
        if not Item2.GET(ItemNo) then
          exit(false);
      exit(true);
    END;

    LOCAL PROCEDURE InitItemAppl@40(OnlyApplTo@1000 : Boolean);
    BEGIN
      "Appl.-to Item Entry" := 0;
      if not OnlyApplTo then
        "Appl.-from Item Entry" := 0;
    END;

    LOCAL PROCEDURE GetResource@49();
    BEGIN
      TESTFIELD("No.");
      if "No." <> Resource."No." then
        Resource.GET("No.");
    END;

    LOCAL PROCEDURE GetCaptionClass@34(FieldNumber@1000 : Integer) : Text[80];
    BEGIN
      if not ServHeader.GET("Document Type","Document No.") then begin
        ServHeader."No." := '';
        ServHeader.INIT;
      end;
      if ServHeader."Prices Including VAT" then
        exit('2,1,' + GetFieldCaption(FieldNumber));

      exit('2,0,' + GetFieldCaption(FieldNumber));
    END;

    LOCAL PROCEDURE GetFieldCaption@31(FieldNumber@1000 : Integer) : Text[100];
    VAR
      Field@1001 : Record Field;
    BEGIN
      Field.GET(DATABASE::"Service Line",FieldNumber);
      exit(Field."Field Caption");
    END;

    LOCAL PROCEDURE UpdateVATAmounts@38();
    VAR
      ServiceLine2@1000 : Record "Service Line";
      TotalLineAmount@1005 : Decimal;
      TotalInvDiscAmount@1004 : Decimal;
      TotalAmount@1001 : Decimal;
      TotalAmountInclVAT@1002 : Decimal;
      TotalQuantityBase@1003 : Decimal;
    BEGIN
      GetServHeader;
      ServiceLine2.SETRANGE("Document Type","Document Type");
      ServiceLine2.SETRANGE("Document No.","Document No.");
      ServiceLine2.SETFILTER("Line No.",'<>%1',"Line No.");
      if "Line Amount" = 0 then
        if xRec."Line Amount" >= 0 then
          ServiceLine2.SETFILTER(Amount,'>%1',0)
        else
          ServiceLine2.SETFILTER(Amount,'<%1',0)
      else
        if "Line Amount" > 0 then
          ServiceLine2.SETFILTER(Amount,'>%1',0)
        else
          ServiceLine2.SETFILTER(Amount,'<%1',0);
      ServiceLine2.SETRANGE("VAT Identifier","VAT Identifier");
      ServiceLine2.SETRANGE("Tax Group Code","Tax Group Code");

      if "Line Amount" = "Inv. Discount Amount" then begin
        Amount := 0;
        "VAT Base Amount" := 0;
        "Amount Including VAT" := 0;
      end else begin
        TotalLineAmount := 0;
        TotalInvDiscAmount := 0;
        TotalAmount := 0;
        TotalAmountInclVAT := 0;
        TotalQuantityBase := 0;
        if ("VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax") or
           (("VAT Calculation Type" in
             ["VAT Calculation Type"::"Normal VAT",
              "VAT Calculation Type"::"Reverse Charge VAT"]) and
            ("VAT %" <> 0))
        then
          if not ServiceLine2.ISEMPTY then begin
            ServiceLine2.CALCSUMS("Line Amount","Inv. Discount Amount",Amount,"Amount Including VAT","Quantity (Base)");
            TotalLineAmount := ServiceLine2."Line Amount";
            TotalInvDiscAmount := ServiceLine2."Inv. Discount Amount";
            TotalAmount := ServiceLine2.Amount;
            TotalAmountInclVAT := ServiceLine2."Amount Including VAT";
            TotalQuantityBase := ServiceLine2."Quantity (Base)";
          end;

        if ServHeader."Prices Including VAT" then
          case "VAT Calculation Type" of
            "VAT Calculation Type"::"Normal VAT",
            "VAT Calculation Type"::"Reverse Charge VAT":
              begin
                Amount :=
                  (TotalLineAmount - TotalInvDiscAmount + "Line Amount" - "Inv. Discount Amount") / (1 + "VAT %" / 100) -
                  TotalAmount;
                "VAT Base Amount" :=
                  ROUND(
                    Amount * (1 - ServHeader."VAT Base Discount %" / 100),
                    Currency."Amount Rounding Precision");
                "Amount Including VAT" :=
                  ROUND(TotalAmount + Amount +
                    (TotalAmount + Amount) * (1 - ServHeader."VAT Base Discount %" / 100) * "VAT %" / 100 -
                    TotalAmountInclVAT,Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                Amount := ROUND(Amount,Currency."Amount Rounding Precision");
              end;
            "VAT Calculation Type"::"Full VAT":
              begin
                Amount := 0;
                "VAT Base Amount" := 0;
              end;
            "VAT Calculation Type"::"Sales Tax":
              begin
                ServHeader.TESTFIELD("VAT Base Discount %",0);
                Amount :=
                  SalesTaxCalculate.ReverseCalculateTax(
                    "Tax Area Code","Tax Group Code","Tax Liable",ServHeader."Posting Date",
                    TotalAmountInclVAT + "Amount Including VAT",TotalQuantityBase + "Quantity (Base)",
                    ServHeader."Currency Factor") -
                  TotalAmount;
                if Amount <> 0 then
                  "VAT %" :=
                    ROUND(100 * ("Amount Including VAT" - Amount) / Amount,0.00001)
                else
                  "VAT %" := 0;
                Amount := ROUND(Amount,Currency."Amount Rounding Precision");
                "VAT Base Amount" := Amount;
              end;
          end
        else
          case "VAT Calculation Type" of
            "VAT Calculation Type"::"Normal VAT",
            "VAT Calculation Type"::"Reverse Charge VAT":
              VALIDATE(Amount,ROUND("Line Amount" - "Inv. Discount Amount",Currency."Amount Rounding Precision"));
            "VAT Calculation Type"::"Full VAT":
              begin
                Amount := 0;
                "VAT Base Amount" := 0;
                "Amount Including VAT" := "Line Amount" - "Inv. Discount Amount";
              end;
            "VAT Calculation Type"::"Sales Tax":
              begin
                Amount := ROUND("Line Amount" - "Inv. Discount Amount",Currency."Amount Rounding Precision");
                "VAT Base Amount" := Amount;
                "Amount Including VAT" :=
                  TotalAmount + Amount +
                  ROUND(
                    SalesTaxCalculate.CalculateTax(
                      "Tax Area Code","Tax Group Code","Tax Liable",ServHeader."Posting Date",
                      TotalAmount + Amount,TotalQuantityBase + "Quantity (Base)",
                      ServHeader."Currency Factor"),Currency."Amount Rounding Precision") -
                  TotalAmountInclVAT;
                if "VAT Base Amount" <> 0 then
                  "VAT %" :=
                    ROUND(100 * ("Amount Including VAT" - "VAT Base Amount") / "VAT Base Amount",0.00001)
                else
                  "VAT %" := 0;
              end;
          end;
      end;
    END;

    [External]
    PROCEDURE MaxQtyToConsume@47() : Decimal;
    BEGIN
      exit(Quantity - "Quantity Shipped");
    END;

    [External]
    PROCEDURE MaxQtyToConsumeBase@46() : Decimal;
    BEGIN
      exit("Quantity (Base)" - "Qty. Shipped (Base)");
    END;

    [External]
    PROCEDURE InitQtyToConsume@48();
    BEGIN
      "Qty. to Consume" := MaxQtyToConsume;
      "Qty. to Consume (Base)" := MaxQtyToConsumeBase;
      OnAfterInitQtyToConsume(Rec,CurrFieldNo);

      InitQtyToInvoice;
    END;

    [External]
    PROCEDURE SetServHeader@24(NewServHeader@1000 : Record "Service Header");
    BEGIN
      ServHeader := NewServHeader;

      if ServHeader."Currency Code" = '' then
        Currency.InitRoundingPrecision
      else begin
        ServHeader.TESTFIELD("Currency Factor");
        Currency.GET(ServHeader."Currency Code");
        Currency.TESTFIELD("Amount Rounding Precision");
      end;
    END;

    [External]
    PROCEDURE CalcVATAmountLines@35(QtyType@1000 : 'General,Invoicing,Shipping,Consuming';VAR ServHeader@1001 : Record "Service Header";VAR ServiceLine@1002 : Record "Service Line";VAR VATAmountLine@1003 : Record "VAT Amount Line";isShip@1008 : Boolean);
    VAR
      Cust@1013 : Record Customer;
      CustPostingGroup@1014 : Record "Customer Posting Group";
      PrevVatAmountLine@1007 : Record "VAT Amount Line";
      Currency@1004 : Record Currency;
      SalesSetup@1009 : Record "Sales & Receivables Setup";
      SalesTaxCalculate@1005 : Codeunit "Sales Tax Calculate";
      QtyFactor@1006 : Decimal;
      TotalVATAmount@1012 : Decimal;
      RoundingLineInserted@1011 : Boolean;
    BEGIN
      if ServHeader."Currency Code" = '' then
        Currency.InitRoundingPrecision
      else
        Currency.GET(ServHeader."Currency Code");

      VATAmountLine.DELETEALL;

      with ServiceLine do begin
        SETRANGE("Document Type",ServHeader."Document Type");
        SETRANGE("Document No.",ServHeader."No.");
        SETFILTER(Type,'>0');
        SETFILTER(Quantity,'<>0');
        SalesSetup.GET;
        if SalesSetup."Invoice Rounding" then begin
          Cust.GET(ServHeader."Bill-to Customer No.");
          CustPostingGroup.GET(Cust."Customer Posting Group");
        end;
        if FINDSET then
          repeat
            if Type = Type::"G/L Account" then
              RoundingLineInserted := ("No." = CustPostingGroup."Invoice Rounding Account") or RoundingLineInserted;
            if "VAT Calculation Type" in
               ["VAT Calculation Type"::"Reverse Charge VAT","VAT Calculation Type"::"Sales Tax"]
            then
              "VAT %" := 0;
            if not
               VATAmountLine.GET(
                 "VAT Identifier",
                 "VAT Calculation Type",
                 "Tax Group Code",
                 false,"Line Amount" >= 0)
            then begin
              VATAmountLine.INIT;
              VATAmountLine."VAT Identifier" := "VAT Identifier";
              VATAmountLine."VAT Calculation Type" := "VAT Calculation Type";
              VATAmountLine."Tax Group Code" := "Tax Group Code";
              VATAmountLine."VAT %" := "VAT %";
              VATAmountLine.Modified := true;
              VATAmountLine.Positive := "Line Amount" >= 0;
              VATAmountLine.INSERT;
            end;
            QtyFactor := 0;
            case QtyType of
              QtyType::Invoicing:
                begin
                  case true of
                    ("Document Type" in ["Document Type"::Order,"Document Type"::Invoice]) and not isShip:
                      begin
                        if CalcChargeableQty <> 0 then
                          QtyFactor := GetAbsMin("Qty. to Invoice","Qty. Shipped Not Invoiced") / CalcChargeableQty;
                        VATAmountLine.Quantity :=
                          VATAmountLine.Quantity + GetAbsMin("Qty. to Invoice (Base)","Qty. Shipped Not Invd. (Base)");
                      end;
                    "Document Type" in ["Document Type"::"Credit Memo"]:
                      begin
                        QtyFactor := GetAbsMin("Qty. to Invoice",Quantity) / Quantity;
                        VATAmountLine.Quantity :=
                          VATAmountLine.Quantity + GetAbsMin("Qty. to Invoice (Base)","Quantity (Base)");
                      end;
                    else begin
                      if CalcChargeableQty <> 0 then
                        QtyFactor := "Qty. to Invoice" / CalcChargeableQty;
                      VATAmountLine.Quantity := VATAmountLine.Quantity + "Qty. to Invoice (Base)";
                    end;
                  end;
                  VATAmountLine."Line Amount" :=
                    VATAmountLine."Line Amount" +
                    ROUND("Line Amount" * QtyFactor,Currency."Amount Rounding Precision");
                  if "Allow Invoice Disc." then
                    VATAmountLine."Inv. Disc. Base Amount" :=
                      VATAmountLine."Inv. Disc. Base Amount" +
                      ROUND("Line Amount" * QtyFactor,Currency."Amount Rounding Precision");
                  VATAmountLine."Invoice Discount Amount" :=
                    VATAmountLine."Invoice Discount Amount" + "Inv. Disc. Amount to Invoice";
                  VATAmountLine."VAT Difference" := VATAmountLine."VAT Difference" + "VAT Difference";
                  VATAmountLine.MODIFY;
                end;
              QtyType::Shipping:
                begin
                  if "Document Type" in
                     ["Document Type"::"Credit Memo"]
                  then begin
                    QtyFactor := 1;
                    VATAmountLine.Quantity := VATAmountLine.Quantity + "Quantity (Base)";
                  end else begin
                    QtyFactor := "Qty. to Ship" / Quantity;
                    VATAmountLine.Quantity := VATAmountLine.Quantity + "Qty. to Ship (Base)";
                  end;
                  VATAmountLine."Line Amount" :=
                    VATAmountLine."Line Amount" +
                    ROUND("Line Amount" * QtyFactor,Currency."Amount Rounding Precision");
                  if "Allow Invoice Disc." then
                    VATAmountLine."Inv. Disc. Base Amount" :=
                      VATAmountLine."Inv. Disc. Base Amount" +
                      ROUND("Line Amount" * QtyFactor,Currency."Amount Rounding Precision");
                  VATAmountLine."Invoice Discount Amount" :=
                    VATAmountLine."Invoice Discount Amount" +
                    ROUND("Inv. Discount Amount" * QtyFactor,Currency."Amount Rounding Precision");
                  VATAmountLine."VAT Difference" := VATAmountLine."VAT Difference" + "VAT Difference";
                  VATAmountLine.MODIFY;
                end;
              QtyType::Consuming:
                begin
                  case true of
                    ("Document Type" = "Document Type"::Order) and not isShip:
                      begin
                        QtyFactor := GetAbsMin("Qty. to Consume","Qty. Shipped Not Invoiced") / Quantity;
                        VATAmountLine.Quantity :=
                          VATAmountLine.Quantity + GetAbsMin("Qty. to Consume (Base)","Qty. Shipped Not Invd. (Base)");
                      end;
                    else begin
                      QtyFactor := "Qty. to Consume" / Quantity;
                      VATAmountLine.Quantity := VATAmountLine.Quantity + "Qty. to Consume (Base)";
                    end;
                  end;
                end
              else begin
                VATAmountLine.Quantity := VATAmountLine.Quantity + "Quantity (Base)";
                VATAmountLine."Line Amount" := VATAmountLine."Line Amount" + "Line Amount";
                if "Allow Invoice Disc." then
                  VATAmountLine."Inv. Disc. Base Amount" :=
                    VATAmountLine."Inv. Disc. Base Amount" + "Line Amount";
                VATAmountLine."Invoice Discount Amount" :=
                  VATAmountLine."Invoice Discount Amount" + "Inv. Discount Amount";
                VATAmountLine."VAT Difference" := VATAmountLine."VAT Difference" + "VAT Difference";
                VATAmountLine.MODIFY;
              end;
            end;
            TotalVATAmount := TotalVATAmount + "Amount Including VAT" - Amount + "VAT Difference";
          until NEXT = 0;
        SETRANGE(Type);
        SETRANGE(Quantity);
      end;

      with VATAmountLine do
        if FIND('-') then
          repeat
            if (PrevVatAmountLine."VAT Identifier" <> "VAT Identifier") or
               (PrevVatAmountLine."VAT Calculation Type" <> "VAT Calculation Type") or
               (PrevVatAmountLine."Tax Group Code" <> "Tax Group Code") or
               (PrevVatAmountLine."Use Tax" <> "Use Tax")
            then
              PrevVatAmountLine.INIT;
            if ServHeader."Prices Including VAT" then begin
              case "VAT Calculation Type" of
                "VAT Calculation Type"::"Normal VAT",
                "VAT Calculation Type"::"Reverse Charge VAT":
                  begin
                    "VAT Base" :=
                      ROUND(
                        ("Line Amount" - "Invoice Discount Amount") / (1 + "VAT %" / 100),
                        Currency."Amount Rounding Precision") - "VAT Difference";
                    "VAT Amount" :=
                      "VAT Difference" +
                      ROUND(
                        PrevVatAmountLine."VAT Amount" +
                        ("Line Amount" - "Invoice Discount Amount" - "VAT Base" - "VAT Difference") *
                        (1 - ServHeader."VAT Base Discount %" / 100),
                        Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                    "Amount Including VAT" := "VAT Base" + "VAT Amount";
                    if Positive then
                      PrevVatAmountLine.INIT
                    else begin
                      PrevVatAmountLine := VATAmountLine;
                      PrevVatAmountLine."VAT Amount" :=
                        ("Line Amount" - "Invoice Discount Amount" - "VAT Base" - "VAT Difference") *
                        (1 - ServHeader."VAT Base Discount %" / 100);
                      PrevVatAmountLine."VAT Amount" :=
                        PrevVatAmountLine."VAT Amount" -
                        ROUND(PrevVatAmountLine."VAT Amount",Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                    end;
                  end;
                "VAT Calculation Type"::"Full VAT":
                  begin
                    "VAT Base" := 0;
                    "VAT Amount" := "VAT Difference" + "Line Amount" - "Invoice Discount Amount";
                    "Amount Including VAT" := "VAT Amount";
                  end;
                "VAT Calculation Type"::"Sales Tax":
                  begin
                    "Amount Including VAT" := "Line Amount" - "Invoice Discount Amount";
                    "VAT Base" :=
                      ROUND(
                        SalesTaxCalculate.ReverseCalculateTax(
                          ServHeader."Tax Area Code","Tax Group Code",ServHeader."Tax Liable",
                          ServHeader."Posting Date","Amount Including VAT",Quantity,ServHeader."Currency Factor"),
                        Currency."Amount Rounding Precision");
                    "VAT Amount" := "VAT Difference" + "Amount Including VAT" - "VAT Base";
                    if "VAT Base" = 0 then
                      "VAT %" := 0
                    else
                      "VAT %" := ROUND(100 * "VAT Amount" / "VAT Base",0.00001);
                  end;
              end;
            end else
              case "VAT Calculation Type" of
                "VAT Calculation Type"::"Normal VAT",
                "VAT Calculation Type"::"Reverse Charge VAT":
                  begin
                    "VAT Base" := "Line Amount" - "Invoice Discount Amount";
                    "VAT Amount" :=
                      "VAT Difference" +
                      ROUND(
                        PrevVatAmountLine."VAT Amount" +
                        "VAT Base" * "VAT %" / 100 * (1 - ServHeader."VAT Base Discount %" / 100),
                        Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                    "Amount Including VAT" := "Line Amount" - "Invoice Discount Amount" + "VAT Amount";
                    if Positive then
                      PrevVatAmountLine.INIT
                    else begin
                      PrevVatAmountLine := VATAmountLine;
                      PrevVatAmountLine."VAT Amount" :=
                        "VAT Base" * "VAT %" / 100 * (1 - ServHeader."VAT Base Discount %" / 100);
                      PrevVatAmountLine."VAT Amount" :=
                        PrevVatAmountLine."VAT Amount" -
                        ROUND(PrevVatAmountLine."VAT Amount",Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                    end;
                  end;
                "VAT Calculation Type"::"Full VAT":
                  begin
                    "VAT Base" := 0;
                    "VAT Amount" := "VAT Difference" + "Line Amount" - "Invoice Discount Amount";
                    "Amount Including VAT" := "VAT Amount";
                  end;
                "VAT Calculation Type"::"Sales Tax":
                  begin
                    "VAT Base" := "Line Amount" - "Invoice Discount Amount";
                    "VAT Amount" :=
                      SalesTaxCalculate.CalculateTax(
                        ServHeader."Tax Area Code","Tax Group Code",ServHeader."Tax Liable",
                        ServHeader."Posting Date","VAT Base",Quantity,ServHeader."Currency Factor");
                    if "VAT Base" = 0 then
                      "VAT %" := 0
                    else
                      "VAT %" := ROUND(100 * "VAT Amount" / "VAT Base",0.00001);
                    "VAT Amount" :=
                      "VAT Difference" +
                      ROUND("VAT Amount",Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                    "Amount Including VAT" := "VAT Base" + "VAT Amount";
                  end;
              end;

            if RoundingLineInserted then
              TotalVATAmount := TotalVATAmount - "VAT Amount";
            "Calculated VAT Amount" := "VAT Amount" - "VAT Difference";
            MODIFY;
          until NEXT = 0;

      if RoundingLineInserted and (TotalVATAmount <> 0) then
        if VATAmountLine.GET(ServiceLine."VAT Identifier",ServiceLine."VAT Calculation Type",
             ServiceLine."Tax Group Code",false,ServiceLine."Line Amount" >= 0)
        then begin
          VATAmountLine."VAT Amount" := VATAmountLine."VAT Amount" + TotalVATAmount;
          VATAmountLine."Amount Including VAT" := VATAmountLine."Amount Including VAT" + TotalVATAmount;
          VATAmountLine."Calculated VAT Amount" := VATAmountLine."Calculated VAT Amount" + TotalVATAmount;
          VATAmountLine.MODIFY;
        end;
    END;

    LOCAL PROCEDURE GetAbsMin@56(QTyToHandle@1000 : Decimal;QtyHandled@1001 : Decimal) : Decimal;
    BEGIN
      if QtyHandled = 0 then
        exit(QTyToHandle);
      if ABS(QtyHandled) < ABS(QTyToHandle) then
        exit(QtyHandled);

      exit(QTyToHandle);
    END;

    [External]
    PROCEDURE UpdateVATOnLines@36(QtyType@1000 : 'General,Invoicing,Shipping';VAR ServHeader@1001 : Record "Service Header";VAR ServiceLine@1002 : Record "Service Line";VAR VATAmountLine@1003 : Record "VAT Amount Line");
    VAR
      TempVATAmountLineRemainder@1004 : TEMPORARY Record "VAT Amount Line";
      Currency@1005 : Record Currency;
      NewAmount@1006 : Decimal;
      NewAmountIncludingVAT@1007 : Decimal;
      NewVATBaseAmount@1008 : Decimal;
      VATAmount@1009 : Decimal;
      VATDifference@1010 : Decimal;
      InvDiscAmount@1011 : Decimal;
      LineAmountToInvoice@1012 : Decimal;
    BEGIN
      if QtyType = QtyType::Shipping then
        exit;
      if ServHeader."Currency Code" = '' then
        Currency.InitRoundingPrecision
      else
        Currency.GET(ServHeader."Currency Code");

      TempVATAmountLineRemainder.DELETEALL;

      with ServiceLine do begin
        SETRANGE("Document Type",ServHeader."Document Type");
        SETRANGE("Document No.",ServHeader."No.");
        SETFILTER(Type,'>0');
        SETFILTER(Quantity,'<>0');
        case QtyType of
          QtyType::Invoicing:
            SETFILTER("Qty. to Invoice",'<>0');
          QtyType::Shipping:
            SETFILTER("Qty. to Ship",'<>0');
        end;
        LOCKTABLE;
        if FIND('-') then
          repeat
            VATAmountLine.GET("VAT Identifier","VAT Calculation Type","Tax Group Code",false,"Line Amount" >= 0);
            if VATAmountLine.Modified then begin
              if not
                 TempVATAmountLineRemainder.GET(
                   "VAT Identifier",
                   "VAT Calculation Type",
                   "Tax Group Code",
                   false,"Line Amount" >= 0)
              then begin
                TempVATAmountLineRemainder := VATAmountLine;
                TempVATAmountLineRemainder.INIT;
                TempVATAmountLineRemainder.INSERT;
              end;

              if QtyType = QtyType::General then
                LineAmountToInvoice := "Line Amount"
              else
                LineAmountToInvoice :=
                  ROUND("Line Amount" * "Qty. to Invoice" / CalcChargeableQty,Currency."Amount Rounding Precision");

              if "Allow Invoice Disc." then begin
                if VATAmountLine."Inv. Disc. Base Amount" = 0 then
                  InvDiscAmount := 0
                else begin
                  TempVATAmountLineRemainder."Invoice Discount Amount" :=
                    TempVATAmountLineRemainder."Invoice Discount Amount" +
                    VATAmountLine."Invoice Discount Amount" * LineAmountToInvoice /
                    VATAmountLine."Inv. Disc. Base Amount";
                  InvDiscAmount :=
                    ROUND(
                      TempVATAmountLineRemainder."Invoice Discount Amount",Currency."Amount Rounding Precision");
                  TempVATAmountLineRemainder."Invoice Discount Amount" :=
                    TempVATAmountLineRemainder."Invoice Discount Amount" - InvDiscAmount;
                end;
                if QtyType = QtyType::General then begin
                  "Inv. Discount Amount" := InvDiscAmount;
                  CalcInvDiscToInvoice;
                end else
                  "Inv. Disc. Amount to Invoice" := InvDiscAmount;
              end else
                InvDiscAmount := 0;

              if QtyType = QtyType::General then
                if ServHeader."Prices Including VAT" then begin
                  if (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount" = 0) or
                     ("Line Amount" = 0)
                  then begin
                    VATAmount := 0;
                    NewAmountIncludingVAT := 0;
                  end else begin
                    VATAmount :=
                      TempVATAmountLineRemainder."VAT Amount" +
                      VATAmountLine."VAT Amount" *
                      ("Line Amount" - "Inv. Discount Amount") /
                      (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount");
                    NewAmountIncludingVAT :=
                      TempVATAmountLineRemainder."Amount Including VAT" +
                      VATAmountLine."Amount Including VAT" *
                      ("Line Amount" - "Inv. Discount Amount") /
                      (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount");
                  end;
                  NewAmount :=
                    ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision") -
                    ROUND(VATAmount,Currency."Amount Rounding Precision");
                  NewVATBaseAmount :=
                    ROUND(
                      NewAmount * (1 - ServHeader."VAT Base Discount %" / 100),
                      Currency."Amount Rounding Precision");
                end else begin
                  if "VAT Calculation Type" = "VAT Calculation Type"::"Full VAT" then begin
                    VATAmount := "Line Amount" - "Inv. Discount Amount";
                    NewAmount := 0;
                    NewVATBaseAmount := 0;
                  end else begin
                    NewAmount := "Line Amount" - "Inv. Discount Amount";
                    NewVATBaseAmount :=
                      ROUND(
                        NewAmount * (1 - ServHeader."VAT Base Discount %" / 100),
                        Currency."Amount Rounding Precision");
                    if VATAmountLine."VAT Base" = 0 then
                      VATAmount := 0
                    else
                      VATAmount :=
                        TempVATAmountLineRemainder."VAT Amount" +
                        VATAmountLine."VAT Amount" * NewAmount / VATAmountLine."VAT Base";
                  end;
                  NewAmountIncludingVAT := NewAmount + ROUND(VATAmount,Currency."Amount Rounding Precision");
                end
              else begin
                if (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount") = 0 then
                  VATDifference := 0
                else
                  VATDifference :=
                    TempVATAmountLineRemainder."VAT Difference" +
                    VATAmountLine."VAT Difference" * (LineAmountToInvoice - InvDiscAmount) /
                    (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount");
                if LineAmountToInvoice = 0 then
                  "VAT Difference" := 0
                else
                  "VAT Difference" := ROUND(VATDifference,Currency."Amount Rounding Precision");
              end;

              if QtyType = QtyType::General then begin
                Amount := NewAmount;
                "Amount Including VAT" := ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision");
                "VAT Base Amount" := NewVATBaseAmount;
              end;
              InitOutstanding;
              MODIFY;

              TempVATAmountLineRemainder."Amount Including VAT" :=
                NewAmountIncludingVAT - ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision");
              TempVATAmountLineRemainder."VAT Amount" := VATAmount - NewAmountIncludingVAT + NewAmount;
              TempVATAmountLineRemainder."VAT Difference" := VATDifference - "VAT Difference";
              TempVATAmountLineRemainder.MODIFY;
            end;
          until NEXT = 0;
        SETRANGE(Type);
        SETRANGE(Quantity);
        SETRANGE("Qty. to Invoice");
        SETRANGE("Qty. to Ship");
      end;
    END;

    LOCAL PROCEDURE CalcUnitCost@5809(ItemLedgEntry@1000 : Record "Item Ledger Entry") : Decimal;
    VAR
      ValueEntry@1001 : Record "Value Entry";
      UnitCost@1004 : Decimal;
    BEGIN
      with ValueEntry do begin
        SETCURRENTKEY("Item Ledger Entry No.");
        SETRANGE("Item Ledger Entry No.",ItemLedgEntry."Entry No.");
        CALCSUMS("Cost Amount (Actual)","Cost Amount (Expected)");
        UnitCost :=
          ("Cost Amount (Expected)" + "Cost Amount (Actual)") / ItemLedgEntry.Quantity;
      end;

      exit(ABS(UnitCost * "Qty. per Unit of Measure"));
    END;

    LOCAL PROCEDURE SelectItemEntry@45(CurrentFieldNo@1000 : Integer);
    VAR
      ItemLedgEntry@1001 : Record "Item Ledger Entry";
      ServLine3@1002 : Record "Service Line";
    BEGIN
      ItemLedgEntry.SETRANGE("Item No.","No.");
      if "Location Code" <> '' then
        ItemLedgEntry.SETRANGE("Location Code","Location Code");
      ItemLedgEntry.SETRANGE("Variant Code","Variant Code");

      if CurrentFieldNo = FIELDNO("Appl.-to Item Entry") then begin
        ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
        ItemLedgEntry.SETRANGE(Positive,true);
        ItemLedgEntry.SETRANGE(Open,true);
      end else begin
        ItemLedgEntry.SETCURRENTKEY("Item No.",Positive);
        ItemLedgEntry.SETRANGE(Positive,false);
      end;
      if PAGE.RUNMODAL(PAGE::"Item Ledger Entries",ItemLedgEntry) = ACTION::LookupOK then begin
        ServLine3 := Rec;
        if CurrentFieldNo = FIELDNO("Appl.-to Item Entry") then
          ServLine3.VALIDATE("Appl.-to Item Entry",ItemLedgEntry."Entry No.")
        else
          ServLine3.VALIDATE("Appl.-from Item Entry",ItemLedgEntry."Entry No.");
        CheckItemAvailable(CurrentFieldNo);
        Rec := ServLine3;
      end;
    END;

    [External]
    PROCEDURE CalcChargeableQty@1() : Decimal;
    BEGIN
      exit(Quantity - "Quantity Consumed" - "Qty. to Consume");
    END;

    [External]
    PROCEDURE SignedXX@5(Value@1000 : Decimal) : Decimal;
    BEGIN
      case "Document Type" of
        "Document Type"::Quote,
        "Document Type"::Order,
        "Document Type"::Invoice:
          exit(-Value);
        "Document Type"::"Credit Memo":
          exit(Value);
      end;
    END;

    [External]
    PROCEDURE IsShipment@55() : Boolean;
    BEGIN
      exit(SignedXX("Quantity (Base)") < 0);
    END;

    LOCAL PROCEDURE AdjustMaxLabourUnitPrice@23(ResUnitPrice@1000 : Decimal);
    VAR
      Res@1001 : Record Resource;
    BEGIN
      if Type <> Type::Resource then
        exit;
      if (ResUnitPrice > ServHeader."Max. Labor Unit Price") and
         (ServHeader."Max. Labor Unit Price" <> 0)
      then begin
        Res.GET("No.");
        "Unit Price" := ServHeader."Max. Labor Unit Price";
        MESSAGE(
          STRSUBSTNO(
            Text018,
            Res.TABLECAPTION,FIELDCAPTION("Unit Price"),
            ServHeader.FIELDCAPTION("Max. Labor Unit Price"),
            ServHeader."Max. Labor Unit Price"));
      end
    END;

    [External]
    PROCEDURE CheckLineDiscount@33(LineDisc@1000 : Decimal);
    BEGIN
      if ("Line Discount Type" = "Line Discount Type"::"Contract Disc.") and
         ("Contract No." <> '') and not "Exclude Contract Discount" and
         not ("Document Type" = "Document Type"::Invoice)
      then
        ERROR(Text043,FIELDCAPTION("Line Discount %"),FIELDCAPTION("Line Discount Type"),"Line Discount Type");

      if (LineDisc < "Warranty Disc. %") and
         Warranty and not "Exclude Warranty"
      then
        ERROR(Text010,FIELDCAPTION("Line Discount %"),FIELDCAPTION("Warranty Disc. %"));

      if "Line Discount %" <> 0 then
        "Line Discount Type" := "Line Discount Type"::Manual
      else
        "Line Discount Type" := "Line Discount Type"::" ";
    END;

    [External]
    PROCEDURE ConfirmAdjPriceLineChange@41();
    BEGIN
      if "Price Adjmt. Status" = "Price Adjmt. Status"::Adjusted then
        if CONFIRM(Text033 + Text034,false) then
          "Price Adjmt. Status" := "Price Adjmt. Status"::Modified
        else
          ERROR('');
    END;

    [External]
    PROCEDURE SetHideCostWarning@2(Value@1000 : Boolean);
    BEGIN
      HideCostWarning := Value;
    END;

    LOCAL PROCEDURE CheckApplFromItemLedgEntry@157(VAR ItemLedgEntry@1000 : Record "Item Ledger Entry");
    VAR
      ItemTrackingLines@1003 : Page "Item Tracking Lines";
      QtyBase@1001 : Decimal;
      ShippedQtyNotReturned@1002 : Decimal;
    BEGIN
      if "Appl.-from Item Entry" = 0 then
        exit;

      TESTFIELD(Type,Type::Item);
      TESTFIELD(Quantity);
      if "Document Type" in ["Document Type"::"Credit Memo"] then begin
        if Quantity < 0 then
          FIELDERROR(Quantity,Text029);
      end else begin
        if Quantity > 0 then
          FIELDERROR(Quantity,Text030);
      end;

      ItemLedgEntry.GET("Appl.-from Item Entry");
      ItemLedgEntry.TESTFIELD(Positive,false);
      ItemLedgEntry.TESTFIELD("Item No.","No.");
      ItemLedgEntry.TESTFIELD("Variant Code","Variant Code");
      if ItemLedgEntry.TrackingExists then
        ERROR(Text040,ItemTrackingLines.CAPTION,FIELDCAPTION("Appl.-from Item Entry"));

      if "Document Type" in ["Document Type"::"Credit Memo"] then
        QtyBase := "Quantity (Base)"
      else
        QtyBase := "Qty. to Ship (Base)";

      if ABS(QtyBase) > -ItemLedgEntry."Shipped Qty. Not Returned" then begin
        if "Qty. per Unit of Measure" = 0 then
          ShippedQtyNotReturned := ItemLedgEntry."Shipped Qty. Not Returned"
        else
          ShippedQtyNotReturned :=
            ROUND(ItemLedgEntry."Shipped Qty. Not Returned" / "Qty. per Unit of Measure",0.00001);
        ERROR(
          Text039,
          -ShippedQtyNotReturned,ItemLedgEntry.TABLECAPTION,ItemLedgEntry."Entry No.");
      end;
    END;

    [External]
    PROCEDURE SetHideWarrantyWarning@16(Value@1000 : Boolean);
    BEGIN
      HideWarrantyWarning := Value;
    END;

    [External]
    PROCEDURE SplitResourceLine@43();
    VAR
      SumQty@1002 : Decimal;
      Qty@1001 : Decimal;
      TempDiscount@1005 : Decimal;
      NoOfServItems@1000 : Integer;
      NextLine@1004 : Integer;
    BEGIN
      TESTFIELD(Type,Type::Resource);
      TESTFIELD("No.");
      TESTFIELD("Service Item Line No.");
      TESTFIELD(Quantity);
      TESTFIELD("Quantity Shipped",0);

      ServItemLine.RESET;
      ServItemLine.SETRANGE("Document Type","Document Type");
      ServItemLine.SETRANGE("Document No.","Document No.");
      NoOfServItems := ServItemLine.COUNT;
      if NoOfServItems <= 1 then
        ERROR(Text041);

      if CONFIRM(Text044) then begin
        ServiceLine.RESET;
        ServiceLine.SETRANGE("Document Type","Document Type");
        ServiceLine.SETRANGE("Document No.","Document No.");
        if ServiceLine.FINDLAST then
          NextLine := ServiceLine."Line No." + 10000
        else
          NextLine := 10000;

        Qty := ROUND(Quantity / NoOfServItems,0.01);
        if ServItemLine.FIND('-') then
          repeat
            if ServItemLine."Line No." <> "Service Item Line No." then begin
              CLEAR(ServiceLine);
              ServiceLine.INIT;
              ServiceLine."Document Type" := "Document Type";
              ServiceLine."Document No." := "Document No.";
              ServiceLine."Line No." := NextLine;
              ServiceLine.INSERT(true);
              ServiceLine.TRANSFERFIELDS(Rec,false);
              ServiceLine.VALIDATE("Service Item Line No.",ServItemLine."Line No.");
              ServiceLine.VALIDATE("No.");

              ServiceLine.VALIDATE(Quantity,Qty);
              SumQty := SumQty + Qty;
              if "Qty. to Consume" > 0 then
                ServiceLine.VALIDATE("Qty. to Consume",Qty);

              ServiceLine.VALIDATE("Contract No.",ServItemLine."Contract No.");
              if not ServiceLine."Exclude Warranty" then
                ServiceLine.VALIDATE(Warranty,ServItemLine.Warranty);

              TempDiscount := "Line Discount %" - "Contract Disc. %" - "Warranty Disc. %";
              if TempDiscount > 0 then begin
                ServiceLine."Line Discount %" := ServiceLine."Line Discount %" + TempDiscount;
                if ServiceLine."Line Discount %" > 100 then
                  ServiceLine."Line Discount %" := 100;
                ServiceLine.VALIDATE("Line Discount %");
              end;

              ServiceLine.MODIFY(true);
              NextLine := NextLine + 10000;
            end;
          until ServItemLine.NEXT = 0;

        if ServiceLine.GET("Document Type","Document No.","Line No.") then begin
          if "Qty. to Consume" > 0 then
            ServiceLine.VALIDATE("Qty. to Consume",Quantity - SumQty);
          ServiceLine.VALIDATE(Quantity,Quantity - SumQty);
          ServiceLine.MODIFY(true);
        end;
      end;
    END;

    LOCAL PROCEDURE UpdateDiscountsAmounts@44();
    BEGIN
      if Type <> Type::" " then begin
        TESTFIELD("Qty. per Unit of Measure");
        CalculateDiscount;
        VALIDATE("Unit Price");
      end;
    END;

    LOCAL PROCEDURE UpdateRemainingCostsAndAmounts@15();
    VAR
      TotalPrice@1000 : Decimal;
      AmountRoundingPrecision@1002 : Decimal;
      AmountRoundingPrecisionFCY@1001 : Decimal;
    BEGIN
      if "Job Remaining Qty." <> 0 then begin
        CLEAR(Currency);
        Currency.InitRoundingPrecision;
        AmountRoundingPrecision := Currency."Amount Rounding Precision";
        GetServHeader;
        AmountRoundingPrecisionFCY := Currency."Amount Rounding Precision";

        "Job Remaining Total Cost" := ROUND("Unit Cost" * "Job Remaining Qty.",AmountRoundingPrecisionFCY);
        "Job Remaining Total Cost (LCY)" := ROUND(
            CurrExchRate.ExchangeAmtFCYToLCY(
              GetDate,"Currency Code",
              "Job Remaining Total Cost",ServHeader."Currency Factor"),
            AmountRoundingPrecision);

        TotalPrice := ROUND("Job Remaining Qty." * "Unit Price",AmountRoundingPrecisionFCY);
        "Job Remaining Line Amount" := TotalPrice - ROUND(TotalPrice * "Line Discount %" / 100,AmountRoundingPrecisionFCY);
      end else begin
        "Job Remaining Total Cost" := 0;
        "Job Remaining Total Cost (LCY)" := 0;
        "Job Remaining Line Amount" := 0;
      end;
    END;

    LOCAL PROCEDURE UpdateServDocRegister@1102601000(DeleteRecord@1102601000 : Boolean);
    VAR
      ServiceLine2@1102601001 : Record "Service Line";
      ServDocReg@1000 : Record "Service Document Register";
    BEGIN
      ServiceLine2.RESET;
      ServiceLine2.SETRANGE("Document Type","Document Type");
      ServiceLine2.SETRANGE("Document No.","Document No.");
      if DeleteRecord then
        ServiceLine2.SETRANGE("Contract No.","Contract No.")
      else
        ServiceLine2.SETRANGE("Contract No.",xRec."Contract No.");
      ServiceLine2.SETFILTER("Line No.",'<>%1',"Line No.");

      if ServiceLine2.ISEMPTY then
        if xRec."Contract No." <> '' then begin
          ServDocReg.RESET;
          if "Document Type" = "Document Type"::Invoice then
            ServDocReg.SETRANGE("Destination Document Type",ServDocReg."Destination Document Type"::Invoice)
          else
            if "Document Type" = "Document Type"::"Credit Memo" then
              ServDocReg.SETRANGE("Destination Document Type",ServDocReg."Destination Document Type"::"Credit Memo");
          ServDocReg.SETRANGE("Destination Document No.","Document No.");
          ServDocReg.SETRANGE("Source Document Type",ServDocReg."Source Document Type"::Contract);
          ServDocReg.SETRANGE("Source Document No.",xRec."Contract No.");
          ServDocReg.DELETEALL;
        end;

      if ("Contract No." <> '') and (Type <> Type::" ") and not DeleteRecord then begin
        if "Document Type" = "Document Type"::Invoice then
          ServDocReg.InsertServSalesDocument(
            ServDocReg."Source Document Type"::Contract,"Contract No.",
            ServDocReg."Destination Document Type"::Invoice,"Document No.")
        else
          if "Document Type" = "Document Type"::"Credit Memo" then
            ServDocReg.InsertServSalesDocument(
              ServDocReg."Source Document Type"::Contract,"Contract No.",
              ServDocReg."Destination Document Type"::"Credit Memo","Document No.")
      end;
    END;

    [External]
    PROCEDURE RowID1@51() : Text[250];
    VAR
      ItemTrackingMgt@1000 : Codeunit "Item Tracking Management";
    BEGIN
      exit(ItemTrackingMgt.ComposeRowID(DATABASE::"Service Line","Document Type",
          "Document No.",'',0,"Line No."));
    END;

    LOCAL PROCEDURE UpdateReservation@53(CalledByFieldNo@1000 : Integer);
    VAR
      ReservationCheckDateConfl@1001 : Codeunit "Reservation-Check Date Confl.";
    BEGIN
      if (CurrFieldNo <> CalledByFieldNo) and (CurrFieldNo <> 0) then
        exit;

      case CalledByFieldNo of
        FIELDNO("Needed by Date"),FIELDNO("Planned Delivery Date"):
          if (xRec."Needed by Date" <> "Needed by Date") and
             (Quantity <> 0) and
             (Reserve <> Reserve::Never)
          then
            ReservationCheckDateConfl.ServiceInvLineCheck(Rec,true);
        FIELDNO(Quantity):
          ReserveServLine.VerifyQuantity(Rec,xRec);
      end;
      ReserveServLine.VerifyChange(Rec,xRec);
    END;

    [External]
    PROCEDURE ShowTracking@52();
    VAR
      OrderTrackingForm@1000 : Page "Order Tracking";
    BEGIN
      OrderTrackingForm.SetServLine(Rec);
      OrderTrackingForm.RUNMODAL;
    END;

    [External]
    PROCEDURE ShowOrderPromisingLine@64();
    VAR
      OrderPromisingLine@1000 : Record "Order Promising Line";
      OrderPromisingLines@1001 : Page "Order Promising Lines";
    BEGIN
      OrderPromisingLine.SETRANGE("Source Type",OrderPromisingLine."Source Type"::"Service Order");
      OrderPromisingLine.SETRANGE("Source Type",OrderPromisingLine."Source Type"::"Service Order");
      OrderPromisingLine.SETRANGE("Source ID","Document No.");
      OrderPromisingLine.SETRANGE("Source Line No.","Line No.");

      OrderPromisingLines.SetSourceType(OrderPromisingLine."Source Type"::"Service Order");
      OrderPromisingLines.SETTABLEVIEW(OrderPromisingLine);
      OrderPromisingLines.RUNMODAL;
    END;

    [External]
    PROCEDURE FilterLinesWithItemToPlan@70(VAR Item@1000 : Record Item);
    BEGIN
      RESET;
      SETCURRENTKEY(Type,"No.","Variant Code","Location Code","Needed by Date","Document Type");
      SETRANGE("Document Type","Document Type"::Order);
      SETRANGE(Type,Type::Item);
      SETRANGE("No.",Item."No.");
      SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      SETFILTER("Needed by Date",Item.GETFILTER("Date Filter"));
      SETFILTER("Shortcut Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      SETFILTER("Shortcut Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
      SETFILTER("Outstanding Qty. (Base)",'<>0');
    END;

    [External]
    PROCEDURE FindLinesWithItemToPlan@68(VAR Item@1000 : Record Item) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item);
      exit(FIND('-'));
    END;

    [External]
    PROCEDURE LinesWithItemToPlanExist@67(VAR Item@1000 : Record Item) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item);
      exit(not ISEMPTY);
    END;

    LOCAL PROCEDURE UpdateServiceLedgerEntry@65();
    VAR
      ServiceLedgerEntry@1000 : Record "Service Ledger Entry";
      Currency@1002 : Record Currency;
      GeneralLedgerSetup@1004 : Record "General Ledger Setup";
      CurrencyExchangeRate@1006 : Record "Currency Exchange Rate";
      LCYRoundingPrecision@1001 : Decimal;
      CurrencyFactor@1005 : Decimal;
    BEGIN
      if "Appl.-to Service Entry" = 0 then
        exit;
      if not ServiceLedgerEntry.GET("Appl.-to Service Entry") then
        exit;
      if ("Unit Price" = xRec."Unit Price") and ("Unit Cost" = xRec."Unit Cost") and (Amount = xRec.Amount) and
         ("Line Discount Amount" = xRec."Line Discount Amount") and ("Line Discount %" = xRec."Line Discount %")
      then
        exit;

      CurrencyFactor := 1;
      if "Currency Code" <> '' then begin
        CurrencyExchangeRate.SETRANGE("Currency Code","Currency Code");
        CurrencyExchangeRate.SETRANGE("Starting Date",0D,"Order Date");
        if CurrencyExchangeRate.FINDLAST then
          CurrencyFactor := CurrencyExchangeRate."Adjustment Exch. Rate Amount" / CurrencyExchangeRate."Relational Exch. Rate Amount";
      end;
      GeneralLedgerSetup.GET;
      LCYRoundingPrecision := 0.01;
      if Currency.GET(GeneralLedgerSetup."LCY Code") then
        LCYRoundingPrecision := Currency."Amount Rounding Precision";

      if "Unit Price" <> xRec."Unit Price" then
        ServiceLedgerEntry."Unit Price" := -ROUND("Unit Price" / CurrencyFactor,LCYRoundingPrecision);
      if "Unit Cost (LCY)" <> xRec."Unit Cost (LCY)" then
        ServiceLedgerEntry."Unit Cost" := "Unit Cost (LCY)";
      if Amount <> xRec.Amount then begin
        ServiceLedgerEntry.Amount := -Amount;
        ServiceLedgerEntry."Amount (LCY)" := -ROUND(Amount / CurrencyFactor,LCYRoundingPrecision);
      end;
      if "Line Discount Amount" <> xRec."Line Discount Amount" then
        ServiceLedgerEntry."Discount Amount" := ROUND("Line Discount Amount" / CurrencyFactor,LCYRoundingPrecision);
      if "Line Discount %" <> xRec."Line Discount %" then
        ServiceLedgerEntry."Discount %" := "Line Discount %";
      ServiceLedgerEntry.MODIFY;
    END;

    LOCAL PROCEDURE UpdateWithWarehouseShip@66();
    BEGIN
      if Type <> Type::Item then
        exit;
      if "Document Type" in ["Document Type"::Quote,"Document Type"::Order] then
        if Location.RequireShipment("Location Code") then begin
          VALIDATE("Qty. to Ship",0);
          VALIDATE("Qty. to Invoice",0);
        end else
          VALIDATE("Qty. to Ship","Outstanding Quantity");
    END;

    LOCAL PROCEDURE CheckWarehouse@71();
    VAR
      Location2@1000 : Record Location;
      WhseSetup@1001 : Record "Warehouse Setup";
      ShowDialog@1002 : '" ",Message,Error';
      DialogText@1003 : Text[100];
    BEGIN
      GetLocation("Location Code");
      if "Location Code" = '' then begin
        WhseSetup.GET;
        Location2."Require Shipment" := WhseSetup."Require Shipment";
        Location2."Require Pick" := WhseSetup."Require Pick";
        Location2."Require Receive" := WhseSetup."Require Receive";
        Location2."Require Put-away" := WhseSetup."Require Put-away";
      end else
        Location2 := Location;

      DialogText := Text035 + ' ';

      if "Document Type" = "Document Type"::Order then
        if Location2."Directed Put-away and Pick" then begin
          ShowDialog := ShowDialog::Error;
          if Quantity >= 0 then
            DialogText := DialogText + ' ' + Location2.GetRequirementText(Location2.FIELDNO("Require Shipment"))
          else
            DialogText := DialogText + Location2.GetRequirementText(Location2.FIELDNO("Require Receive"));
        end else begin
          if (Quantity >= 0) and (Location2."Require Shipment" or Location2."Require Pick") then begin
            if WhseValidateSourceLine.WhseLinesExist(DATABASE::"Service Line","Document Type","Document No.","Line No.",0,Quantity)
            then
              ShowDialog := ShowDialog::Error
            else
              if Location2."Require Shipment" then
                ShowDialog := ShowDialog::Message;
            if Location2."Require Shipment" then
              DialogText :=
                DialogText + Location2.GetRequirementText(Location2.FIELDNO("Require Shipment"))
            else begin
              DialogText := Text036;
              DialogText :=
                DialogText + Location2.GetRequirementText(Location2.FIELDNO("Require Pick"));
            end;
          end;

          if (Quantity < 0) and (Location2."Require Receive" or Location2."Require Put-away") then begin
            if WhseValidateSourceLine.WhseLinesExist(
                 DATABASE::"Service Line",
                 "Document Type",
                 "Document No.",
                 "Line No.",
                 0,
                 Quantity)
            then
              ShowDialog := ShowDialog::Error
            else
              if Location2."Require Receive" then
                ShowDialog := ShowDialog::Message;
            if Location2."Require Receive" then
              DialogText := DialogText + Location2.GetRequirementText(Location2.FIELDNO("Require Receive"))
            else
              DialogText := Text036 + ' ' + Location2.GetRequirementText(Location2.FIELDNO("Require Put-away"));
          end;
        end;

      case ShowDialog of
        ShowDialog::Message:
          MESSAGE(STRSUBSTNO(Text049,DialogText,FIELDCAPTION("Line No."),"Line No.") + Text050);
        ShowDialog::Error:
          ERROR(Text049,DialogText,FIELDCAPTION("Line No."),"Line No.");
      end;

      HandleDedicatedBin(true);
    END;

    LOCAL PROCEDURE HandleDedicatedBin@73(IssueWarning@1000 : Boolean);
    VAR
      WhseIntegrationMgt@1001 : Codeunit "Whse. Integration Management";
    BEGIN
      WhseIntegrationMgt.CheckIfBinDedicatedOnSrcDoc("Location Code","Bin Code",IssueWarning);
    END;

    LOCAL PROCEDURE TestStatusOpen@72();
    VAR
      ServHeader@1000 : Record "Service Header";
    BEGIN
      if StatusCheckSuspended then
        exit;
      ServHeader.GET("Document Type","Document No.");
      if (Type = Type::Item) or (xRec.Type = Type::Item) then
        ServHeader.TESTFIELD("Release Status",ServHeader."Release Status"::Open);
    END;

    [External]
    PROCEDURE SuspendStatusCheck@69(bSuspend@1000 : Boolean);
    BEGIN
      StatusCheckSuspended := bSuspend;
    END;

    LOCAL PROCEDURE LineRequiresShipmentOrReceipt@74() : Boolean;
    VAR
      Location@1000 : Record Location;
    BEGIN
      if ("Document Type" <> "Document Type"::Order) or (Type <> Type::Item) then
        exit(false);
      exit(Location.RequireReceive("Location Code") or Location.RequireShipment("Location Code"));
    END;

    LOCAL PROCEDURE DisplayConflictError@76(ErrTxt@1000 : Text[500]);
    VAR
      DisplayedError@1001 : Text[600];
    BEGIN
      DisplayedError := Text051 + ErrTxt;
      ERROR(DisplayedError);
    END;

    [External]
    PROCEDURE GetDueDate@75() : Date;
    BEGIN
      exit(EvaluateDaysBack("Shipping Time","Needed by Date"));
    END;

    [External]
    PROCEDURE GetShipmentDate@77() : Date;
    VAR
      Location@1001 : Record Location;
      InventorySetup@1002 : Record "Inventory Setup";
    BEGIN
      if Location.GET("Location Code") then
        exit(EvaluateDaysBack(Location."Outbound Whse. Handling Time",GetDueDate));
      InventorySetup.GET;
      exit(EvaluateDaysBack(InventorySetup."Outbound Whse. Handling Time",GetDueDate));
    END;

    [External]
    PROCEDURE OutstandingInvoiceAmountFromShipment@4(CustomerNo@1000 : Code[20]) : Decimal;
    VAR
      ServiceLine@1001 : Record "Service Line";
    BEGIN
      ServiceLine.SETCURRENTKEY("Document Type","Customer No.","Shipment No.");
      ServiceLine.SETRANGE("Document Type",ServiceLine."Document Type"::Invoice);
      ServiceLine.SETRANGE("Customer No.",CustomerNo);
      ServiceLine.SETFILTER("Shipment No.",'<>%1','');
      ServiceLine.CALCSUMS("Outstanding Amount (LCY)");
      exit(ServiceLine."Outstanding Amount (LCY)");
    END;

    LOCAL PROCEDURE EvaluateDaysBack@78(InputFormula@1001 : DateFormula;InputDate@1000 : Date) : Date;
    VAR
      DFCode@1002 : Code[10];
      DF@1003 : DateFormula;
    BEGIN
      if FORMAT(InputFormula) = '' then
        exit(InputDate);
      DFCode := FORMAT(InputFormula);
      if not (COPYSTR(DFCode,1,1) in ['+','-']) then
        DFCode := '+' + DFCode;
      DFCode := CONVERTSTR(DFCode,'+-','-+');
      EVALUATE(DF,DFCode);
      exit(CALCDATE(DF,InputDate));
    END;

    LOCAL PROCEDURE CheckIfCanBeModified@81();
    BEGIN
      if ("Appl.-to Service Entry" > 0) and ("Contract No." <> '') then
        ERROR(Text053);
    END;

    LOCAL PROCEDURE ViewDimensionSetEntries@80();
    BEGIN
      DimMgt.ShowDimensionSet(
        "Dimension Set ID",STRSUBSTNO('%1 %2 %3',TABLECAPTION,"Document No.","Line No."));
    END;

    [External]
    PROCEDURE TestItemFields@88(ItemNo@1000 : Code[20];VariantCode@1001 : Code[10];LocationCode@1002 : Code[10]);
    BEGIN
      TESTFIELD(Type,Type::Item);
      TESTFIELD("No.",ItemNo);
      TESTFIELD("Variant Code",VariantCode);
      TESTFIELD("Location Code",LocationCode);
    END;

    [External]
    PROCEDURE TestBinCode@82();
    VAR
      Location@1001 : Record Location;
    BEGIN
      if ("Location Code" = '') or (Type <> Type::Item) then
        exit;
      Location.GET("Location Code");
      if not Location."Bin Mandatory" then
        exit;
      if ("Document Type" in ["Document Type"::Invoice,"Document Type"::"Credit Memo"]) or
         not Location."Directed Put-away and Pick"
      then
        TESTFIELD("Bin Code");
    END;

    [External]
    PROCEDURE GetNextLineNo@83(ServiceLineSource@1001 : Record "Service Line";BelowxRec@1000 : Boolean) : Integer;
    VAR
      ServiceLine@1006 : Record "Service Line";
      LowLineNo@1005 : Integer;
      HighLineNo@1004 : Integer;
      NextLineNo@1003 : Integer;
      LineStep@1002 : Integer;
    BEGIN
      LowLineNo := 0;
      HighLineNo := 0;
      NextLineNo := 0;
      LineStep := 10000;
      ServiceLine.SETRANGE("Document Type","Document Type");
      ServiceLine.SETRANGE("Document No.","Document No.");

      if ServiceLine.FIND('+') then
        if not ServiceLine.GET(ServiceLineSource."Document Type",ServiceLineSource."Document No.",ServiceLineSource."Line No.") then
          NextLineNo := ServiceLine."Line No." + LineStep
        else
          if BelowxRec then begin
            ServiceLine.FINDLAST;
            NextLineNo := ServiceLine."Line No." + LineStep;
          end else
            if ServiceLine.NEXT(-1) = 0 then begin
              LowLineNo := 0;
              HighLineNo := ServiceLineSource."Line No.";
            end else begin
              ServiceLine := ServiceLineSource;
              ServiceLine.NEXT(-1);
              LowLineNo := ServiceLine."Line No.";
              HighLineNo := ServiceLineSource."Line No.";
            end
      else
        NextLineNo := LineStep;

      if NextLineNo = 0 then
        NextLineNo := ROUND((LowLineNo + HighLineNo) / 2,1,'<');

      if ServiceLine.GET("Document Type","Document No.",NextLineNo) then
        exit(0);
      exit(NextLineNo);
    END;

    [External]
    PROCEDURE DeleteWithAttachedLines@84();
    BEGIN
      SETRANGE("Document Type","Document Type");
      SETRANGE("Document No.","Document No.");
      SETRANGE("Attached to Line No.","Line No.");
      DELETEALL;

      SETRANGE("Document Type");
      SETRANGE("Document No.");
      SETRANGE("Attached to Line No.");
      DELETE;
    END;

    LOCAL PROCEDURE UpdateDimSetupFromDimSetID@85(VAR TableID@1001 : ARRAY [10] OF Integer;VAR No@1004 : ARRAY [10] OF Code[20];InheritFromDimSetID@1000 : Integer);
    VAR
      TempDimSetEntry@1002 : TEMPORARY Record "Dimension Set Entry";
      LastAddedTableID@1003 : Integer;
    BEGIN
      DimMgt.GetDimensionSet(TempDimSetEntry,InheritFromDimSetID);
      ServHeader.GET("Document Type","Document No.");
      LastAddedTableID := 3;
      UpdateDimSetupByDefaultDim(
        DATABASE::"Service Order Type",ServHeader."Service Order Type",TempDimSetEntry,TableID,No,LastAddedTableID);
      UpdateDimSetupByDefaultDim(
        DATABASE::Customer,ServHeader."Bill-to Customer No.",TempDimSetEntry,TableID,No,LastAddedTableID);
      UpdateDimSetupByDefaultDim(
        DATABASE::"Salesperson/Purchaser",ServHeader."Salesperson Code",TempDimSetEntry,TableID,No,LastAddedTableID);
      UpdateDimSetupByDefaultDim(
        DATABASE::"Service Contract Header",ServHeader."Contract No.",TempDimSetEntry,TableID,No,LastAddedTableID);
      UpdateDimSetupByDefaultDim(
        DATABASE::"Service Item",ServItemLine."Service Item No.",TempDimSetEntry,TableID,No,LastAddedTableID);
      UpdateDimSetupByDefaultDim(
        DATABASE::"Service Item Group",ServItemLine."Service Item Group Code",TempDimSetEntry,TableID,No,LastAddedTableID);
    END;

    LOCAL PROCEDURE UpdateDimSetupByDefaultDim@95(SourceID@1002 : Integer;SourceNo@1003 : Code[20];VAR TempDimSetEntry@1001 : TEMPORARY Record "Dimension Set Entry";VAR TableID@1000 : ARRAY [10] OF Integer;VAR No@1007 : ARRAY [10] OF Code[20];VAR LastAddedTableID@1005 : Integer);
    VAR
      DefaultDim@1004 : Record "Default Dimension";
      TableAdded@1006 : Boolean;
    BEGIN
      if SourceNo = '' then
        exit;

      DefaultDim.SETRANGE("Table ID",SourceID);
      DefaultDim.SETRANGE("No.",SourceNo);
      if DefaultDim.FINDSET then
        repeat
          TempDimSetEntry.SETRANGE("Dimension Code",DefaultDim."Dimension Code");
          TempDimSetEntry.SETRANGE("Dimension Value Code",DefaultDim."Dimension Value Code");
          if TempDimSetEntry.FINDFIRST then begin
            UpdateDimSetup(TableID,No,DefaultDim."Table ID",DefaultDim."No.",LastAddedTableID);
            TableAdded := true;
          end;
        until (DefaultDim.NEXT = 0) or TableAdded;
    END;

    LOCAL PROCEDURE UpdateDimSetup@89(VAR TableID@1000 : ARRAY [10] OF Integer;VAR No@1005 : ARRAY [10] OF Code[20];NewTableID@1001 : Integer;NewNo@1006 : Code[20];VAR LastAddedTableID@1002 : Integer);
    VAR
      TableAlreadyAdded@1004 : Boolean;
      i@1003 : Integer;
    BEGIN
      for i := 1 to LastAddedTableID do
        if TableID[i] = NewTableID then
          TableAlreadyAdded := true;

      if not TableAlreadyAdded then begin
        LastAddedTableID += 1;
        TableID[LastAddedTableID] := NewTableID;
        No[LastAddedTableID] := NewNo;
      end;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterAssignHeaderValues@134(VAR ServiceLine@1000 : Record "Service Line";ServiceHeader@1001 : Record "Service Header");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterAssignStdTxtValues@135(VAR ServiceLine@1000 : Record "Service Line";StandardText@1001 : Record "Standard Text");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterAssignGLAccountValues@138(VAR ServiceLine@1000 : Record "Service Line";GLAccount@1001 : Record "G/L Account");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterAssignServCostValues@137(VAR ServiceLine@1000 : Record "Service Line";ServiceCost@1001 : Record "Service Cost");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterAssignItemValues@136(VAR ServiceLine@1000 : Record "Service Line";Item@1001 : Record Item);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterAssignServItemValues@99(VAR ServiceLine@1003 : Record "Service Line";ServiceItem@1001 : Record "Service Item";ServiceItemComp@1000 : Record "Service Item Component");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterAssignResourceValues@139(VAR ServiceLine@1000 : Record "Service Line";Resource@1001 : Record Resource);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateUnitPrice@126(VAR ServiceLine@1000 : Record "Service Line";xServiceLine@1001 : Record "Service Line";CalledByFieldNo@1002 : Integer;CurrFieldNo@1003 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeUpdateUnitPrice@127(VAR ServiceLine@1003 : Record "Service Line";xServiceLine@1002 : Record "Service Line";CalledByFieldNo@1001 : Integer;CurrFieldNo@1000 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitOutstandingAmount@132(VAR ServiceLine@1000 : Record "Service Line";ServiceHeader@1001 : Record "Service Header";Currency@1002 : Record Currency);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitQtyToInvoice@128(VAR ServiceLine@1000 : Record "Service Line";CurrFieldNo@1001 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitQtyToShip@129(VAR ServiceLine@1000 : Record "Service Line";CurrFieldNo@1001 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitQtyToConsume@130(VAR ServiceLine@1000 : Record "Service Line";CurrFieldNo@1001 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateDimTableIDs@17(VAR ServiceLine@1000 : Record "Service Line";FieldNo@1001 : Integer;TableID@1003 : ARRAY [10] OF Integer;No@1002 : ARRAY [10] OF Code[20]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

