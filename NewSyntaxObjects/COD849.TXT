OBJECT Codeunit 849 Cash Flow Account - Indent
{
  OBJECT-PROPERTIES
  {
    Date=20150915D;
    Time=120000T;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            if not
               CONFIRM(
                 Text1000 +
                 Text1003,true)
            then
              exit;

            Indentation;
          END;

  }
  CODE
  {
    VAR
      Text1000@1000 : TextConst 'ENU=This function updates the indentation of all the cash flow accounts in the chart of cash flow accounts. All accounts between a Begin-Total and the matching End-Total are indented one level. The Totaling for each End-total is also updated.\\';
      Text1003@1003 : TextConst 'ENU=Do you want to indent the chart of accounts?';
      Text1004@1004 : TextConst 'ENU=Indenting the Chart of Accounts #1##########';
      Text1005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.';
      CFAccount@1006 : Record "Cash Flow Account";
      Window@1007 : Dialog;
      AccNo@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    LOCAL PROCEDURE Indentation@1000();
    BEGIN
      Window.OPEN(Text1004);

      with CFAccount do
        if FIND('-') then
          repeat
            Window.UPDATE(1,"No.");

            if "Account Type" = "Account Type"::"End-Total" then begin
              if i < 1 then
                ERROR(
                  Text1005,
                  "No.");
              Totaling := AccNo[i] + '..' + "No.";
              i := i - 1;
            end;

            Indentation := i;
            MODIFY;

            if "Account Type" = "Account Type"::"Begin-Total" then begin
              i := i + 1;
              AccNo[i] := "No.";
            end;
          until NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

