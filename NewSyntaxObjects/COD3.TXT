OBJECT Codeunit 3 G/L Account-Indent
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            if not
               CONFIRM(
                 Text000 +
                 Text001 +
                 Text002 +
                 Text003,true)
            then
              exit;

            Indent;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="This function updates the indentation of all the G/L accounts in the chart of accounts. "';
      Text001@1001 : TextConst 'ENU="All accounts between a Begin-Total and the matching End-Total are indented one level. "';
      Text002@1002 : TextConst 'ENU=The Totaling for each End-total is also updated.';
      Text003@1003 : TextConst 'ENU=\\Do you want to indent the chart of accounts?';
      Text004@1004 : TextConst 'ENU=Indenting the Chart of Accounts #1##########';
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.';
      GLAcc@1006 : Record "G/L Account";
      Window@1007 : Dialog;
      AccNo@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    [External]
    PROCEDURE Indent@1();
    BEGIN
      Window.OPEN(Text004);

      with GLAcc do
        if FIND('-') then
          repeat
            Window.UPDATE(1,"No.");

            if "Account Type" = "Account Type"::"End-Total" then begin
              if i < 1 then
                ERROR(
                  Text005,
                  "No.");
              if Totaling = '' then
                Totaling := AccNo[i] + '..' + "No.";
              i := i - 1;
            end;

            Indentation := i;
            MODIFY;

            if "Account Type" = "Account Type"::"Begin-Total" then begin
              i := i + 1;
              AccNo[i] := "No.";
            end;
          until NEXT = 0;

      Window.CLOSE;
    END;

    [External]
    PROCEDURE RunICAccountIndent@4();
    BEGIN
      if not
         CONFIRM(
           Text000 +
           Text001 +
           Text003,true)
      then
        exit;

      IndentICAccount;
    END;

    LOCAL PROCEDURE IndentICAccount@2();
    VAR
      ICGLAcc@1000 : Record "IC G/L Account";
    BEGIN
      Window.OPEN(Text004);
      with ICGLAcc do
        if FIND('-') then
          repeat
            Window.UPDATE(1,"No.");

            if "Account Type" = "Account Type"::"End-Total" then begin
              if i < 1 then
                ERROR(
                  Text005,
                  "No.");
              i := i - 1;
            end;

            Indentation := i;
            MODIFY;

            if "Account Type" = "Account Type"::"Begin-Total" then begin
              i := i + 1;
              AccNo[i] := "No.";
            end;
          until NEXT = 0;
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

