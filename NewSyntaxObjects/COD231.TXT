OBJECT Codeunit 231 Gen. Jnl.-Post
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Gen. Journal Line";
    EventSubscriberInstance=Manual;
    OnRun=VAR
            GenJnlLine@1000 : Record "Gen. Journal Line";
          BEGIN
            GenJnlLine.COPY(Rec);
            Code(GenJnlLine);
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.';
      Text005@1009 : TextConst 'ENU=Using %1 for Declining Balance can result in misleading numbers for subsequent years. You should manually check the postings and correct them if necessary. Do you want to continue?';
      Text006@1010 : TextConst '@@@=Source Code in Genenral Journal Template must not be equal to Job G/L WIP in Source Code Setup.;ENU=%1 in %2 must not be equal to %3 in %4.';
      PreviewMode@1011 : Boolean;

    LOCAL PROCEDURE Code@1(VAR GenJnlLine@1004 : Record "Gen. Journal Line");
    VAR
      GenJnlTemplate@1005 : Record "Gen. Journal Template";
      FALedgEntry@1000 : Record "FA Ledger Entry";
      SourceCodeSetup@1001 : Record "Source Code Setup";
      GenJnlPostBatch@1002 : Codeunit "Gen. Jnl.-Post Batch";
      TempJnlBatchName@1003 : Code[10];
    BEGIN
      with GenJnlLine do begin
        GenJnlTemplate.GET("Journal Template Name");
        if GenJnlTemplate.Type = GenJnlTemplate.Type::Jobs then begin
          SourceCodeSetup.GET;
          if GenJnlTemplate."Source Code" = SourceCodeSetup."Job G/L WIP" then
            ERROR(Text006,GenJnlTemplate.FIELDCAPTION("Source Code"),GenJnlTemplate.TABLECAPTION,
              SourceCodeSetup.FIELDCAPTION("Job G/L WIP"),SourceCodeSetup.TABLECAPTION);
        end;
        GenJnlTemplate.TESTFIELD("Force Posting Report",false);
        if GenJnlTemplate.Recurring and (GETFILTER("Posting Date") <> '') then
          FIELDERROR("Posting Date",Text000);

        if not PreviewMode then
          if not CONFIRM(Text001,false) then
            exit;

        if "Account Type" = "Account Type"::"Fixed Asset" then begin
          FALedgEntry.SETRANGE("FA No.","Account No.");
          FALedgEntry.SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          if FALedgEntry.FINDFIRST and "Depr. Acquisition Cost" then
            if not CONFIRM(Text005,false,FIELDCAPTION("Depr. Acquisition Cost")) then
              exit;
        end;

        TempJnlBatchName := "Journal Batch Name";

        GenJnlPostBatch.SetPreviewMode(PreviewMode);
        GenJnlPostBatch.RUN(GenJnlLine);

        if PreviewMode then
          exit;

        if "Line No." = 0 then
          MESSAGE(Text002)
        else
          if TempJnlBatchName = "Journal Batch Name" then
            MESSAGE(Text003)
          else
            MESSAGE(
              Text004,
              "Journal Batch Name");

        if not FIND('=><') or (TempJnlBatchName <> "Journal Batch Name") then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        end;
      end;
    END;

    [Internal]
    PROCEDURE Preview@2(VAR GenJournalLineSource@1003 : Record "Gen. Journal Line");
    VAR
      GenJnlPostPreview@1002 : Codeunit "Gen. Jnl.-Post Preview";
      GenJnlPost@1001 : Codeunit "Gen. Jnl.-Post";
    BEGIN
      BINDSUBSCRIPTION(GenJnlPost);
      GenJnlPostPreview.Preview(GenJnlPost,GenJournalLineSource);
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnRunPreview@4(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1002 : Variant);
    VAR
      GenJournalLine@1004 : Record "Gen. Journal Line";
      GenJnlPost@1003 : Codeunit "Gen. Jnl.-Post";
    BEGIN
      GenJnlPost := Subscriber;
      GenJournalLine.COPY(RecVar);
      PreviewMode := true;
      Result := GenJnlPost.RUN(GenJournalLine);
    END;

    BEGIN
    END.
  }
}

