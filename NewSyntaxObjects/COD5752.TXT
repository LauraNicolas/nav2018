OBJECT Codeunit 5752 Get Source Doc. Outbound
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=If %1 is %2 in %3 no. %4, then all associated lines where type is %5 must use the same location.';
      Text002@1001 : TextConst 'ENU=The warehouse shipment was not created because the Shipping Advice field is set to Complete, and item no. %1 is not available in location code %2.\\You can create the warehouse shipment by either changing the Shipping Advice field to Partial in %3 no. %4 or by manually filling in the warehouse shipment document.';
      Text003@1002 : TextConst 'ENU=The warehouse shipment was not created because an open warehouse shipment exists for the Sales Header and Shipping Advice is %1.\\You must add the item(s) as new line(s) to the existing warehouse shipment or change Shipping Advice to Partial.';
      Text004@1003 : TextConst 'ENU=No %1 was found. The warehouse shipment could not be created.';
      GetSourceDocuments@1004 : Report "Get Source Documents";

    LOCAL PROCEDURE CreateWhseShipmentHeaderFromWhseRequest@13(VAR WarehouseRequest@1000 : Record "Warehouse Request") : Boolean;
    BEGIN
      if WarehouseRequest.ISEMPTY then
        exit(false);

      CLEAR(GetSourceDocuments);
      GetSourceDocuments.USEREQUESTPAGE(false);
      GetSourceDocuments.SETTABLEVIEW(WarehouseRequest);
      GetSourceDocuments.SetHideDialog(true);
      GetSourceDocuments.RUNMODAL;

      exit(true);
    END;

    [External]
    PROCEDURE GetOutboundDocs@1(VAR WhseShptHeader@1003 : Record "Warehouse Shipment Header");
    VAR
      WhseGetSourceFilterRec@1001 : Record "Warehouse Source Filter";
      WhseSourceFilterSelection@1002 : Page "Filters to Get Source Docs.";
    BEGIN
      WhseShptHeader.FIND;
      WhseSourceFilterSelection.SetOneCreatedShptHeader(WhseShptHeader);
      WhseGetSourceFilterRec.FILTERGROUP(2);
      WhseGetSourceFilterRec.SETRANGE(Type,WhseGetSourceFilterRec.Type::Outbound);
      WhseGetSourceFilterRec.FILTERGROUP(0);
      WhseSourceFilterSelection.SETTABLEVIEW(WhseGetSourceFilterRec);
      WhseSourceFilterSelection.RUNMODAL;

      WhseShptHeader."Document Status" := WhseShptHeader.GetDocumentStatus(0);
      WhseShptHeader.MODIFY;
    END;

    [External]
    PROCEDURE GetSingleOutboundDoc@2(VAR WhseShptHeader@1000 : Record "Warehouse Shipment Header");
    VAR
      WhseRqst@1001 : Record "Warehouse Request";
      SourceDocSelection@1003 : Page "Source Documents";
    BEGIN
      CLEAR(GetSourceDocuments);
      WhseShptHeader.FIND;

      WhseRqst.FILTERGROUP(2);
      WhseRqst.SETRANGE(Type,WhseRqst.Type::Outbound);
      WhseRqst.SETRANGE("Location Code",WhseShptHeader."Location Code");
      WhseRqst.FILTERGROUP(0);
      WhseRqst.SETRANGE("Document Status",WhseRqst."Document Status"::Released);
      WhseRqst.SETRANGE("Completely Handled",false);

      SourceDocSelection.LOOKUPMODE(true);
      SourceDocSelection.SETTABLEVIEW(WhseRqst);
      if SourceDocSelection.RUNMODAL <> ACTION::LookupOK then
        exit;
      SourceDocSelection.GetResult(WhseRqst);

      GetSourceDocuments.SetOneCreatedShptHeader(WhseShptHeader);
      GetSourceDocuments.SetSkipBlocked(true);
      GetSourceDocuments.USEREQUESTPAGE(false);
      WhseRqst.SETRANGE("Location Code",WhseShptHeader."Location Code");
      GetSourceDocuments.SETTABLEVIEW(WhseRqst);
      GetSourceDocuments.RUNMODAL;

      WhseShptHeader."Document Status" :=
        WhseShptHeader.GetDocumentStatus(0);
      WhseShptHeader.MODIFY;
    END;

    [Internal]
    PROCEDURE CreateFromSalesOrder@3(SalesHeader@1000 : Record "Sales Header");
    BEGIN
      ShowResult(CreateFromSalesOrderHideDialog(SalesHeader));
    END;

    [Internal]
    PROCEDURE CreateFromSalesOrderHideDialog@15(SalesHeader@1000 : Record "Sales Header") : Boolean;
    VAR
      WhseRqst@1001 : Record "Warehouse Request";
    BEGIN
      if not SalesHeader.IsApprovedForPosting then
        exit(false);

      FindWarehouseRequestForSalesOrder(WhseRqst,SalesHeader);

      if WhseRqst.ISEMPTY then
        exit(false);

      CreateWhseShipmentHeaderFromWhseRequest(WhseRqst);
      exit(true);
    END;

    [External]
    PROCEDURE CreateFromPurchaseReturnOrder@6(PurchHeader@1000 : Record "Purchase Header");
    BEGIN
      ShowResult(CreateFromPurchReturnOrderHideDialog(PurchHeader));
    END;

    [External]
    PROCEDURE CreateFromPurchReturnOrderHideDialog@20(PurchHeader@1000 : Record "Purchase Header") : Boolean;
    VAR
      WhseRqst@1001 : Record "Warehouse Request";
    BEGIN
      FindWarehouseRequestForPurchReturnOrder(WhseRqst,PurchHeader);
      exit(CreateWhseShipmentHeaderFromWhseRequest(WhseRqst));
    END;

    [External]
    PROCEDURE CreateFromOutbndTransferOrder@4(TransHeader@1000 : Record "Transfer Header");
    BEGIN
      ShowResult(CreateFromOutbndTransferOrderHideDialog(TransHeader));
    END;

    [External]
    PROCEDURE CreateFromOutbndTransferOrderHideDialog@23(TransHeader@1000 : Record "Transfer Header") : Boolean;
    VAR
      WhseRqst@1001 : Record "Warehouse Request";
    BEGIN
      FindWarehouseRequestForOutbndTransferOrder(WhseRqst,TransHeader);
      exit(CreateWhseShipmentHeaderFromWhseRequest(WhseRqst));
    END;

    [External]
    PROCEDURE CreateFromServiceOrder@10(ServiceHeader@1000 : Record "Service Header");
    BEGIN
      ShowResult(CreateFromServiceOrderHideDialog(ServiceHeader));
    END;

    [External]
    PROCEDURE CreateFromServiceOrderHideDialog@26(ServiceHeader@1000 : Record "Service Header") : Boolean;
    VAR
      WhseRqst@1001 : Record "Warehouse Request";
    BEGIN
      FindWarehouseRequestForServiceOrder(WhseRqst,ServiceHeader);
      exit(CreateWhseShipmentHeaderFromWhseRequest(WhseRqst));
    END;

    [External]
    PROCEDURE GetSingleWhsePickDoc@5(CurrentWhseWkshTemplate@1008 : Code[10];CurrentWhseWkshName@1004 : Code[10];LocationCode@1000 : Code[10]);
    VAR
      PickWkshName@1005 : Record "Whse. Worksheet Name";
      WhsePickRqst@1001 : Record "Whse. Pick Request";
      GetWhseSourceDocuments@1007 : Report "Get Outbound Source Documents";
      WhsePickDocSelection@1003 : Page "Pick Selection";
    BEGIN
      PickWkshName.GET(CurrentWhseWkshTemplate,CurrentWhseWkshName,LocationCode);

      WhsePickRqst.FILTERGROUP(2);
      WhsePickRqst.SETRANGE(Status,WhsePickRqst.Status::Released);
      WhsePickRqst.SETRANGE("Completely Picked",false);
      WhsePickRqst.SETRANGE("Location Code",LocationCode);
      WhsePickRqst.FILTERGROUP(0);

      WhsePickDocSelection.LOOKUPMODE(true);
      WhsePickDocSelection.SETTABLEVIEW(WhsePickRqst);
      if WhsePickDocSelection.RUNMODAL <> ACTION::LookupOK then
        exit;
      WhsePickDocSelection.GetResult(WhsePickRqst);

      GetWhseSourceDocuments.SetPickWkshName(
        CurrentWhseWkshTemplate,CurrentWhseWkshName,LocationCode);
      GetWhseSourceDocuments.USEREQUESTPAGE(false);
      GetWhseSourceDocuments.SETTABLEVIEW(WhsePickRqst);
      GetWhseSourceDocuments.RUNMODAL;
    END;

    [External]
    PROCEDURE CheckSalesHeader@7(SalesHeader@1000 : Record "Sales Header";ShowError@1001 : Boolean) : Boolean;
    VAR
      SalesLine@1002 : Record "Sales Line";
      SalesLine2@1003 : Record "Sales Line";
      CurrItemVariant@1010 : Record "Item Variant";
      SalesOrder@1004 : Page "Sales Order";
      QtyOutstandingBase@1006 : Decimal;
      RecordNo@1007 : Integer;
      TotalNoOfRecords@1008 : Integer;
    BEGIN
      with SalesHeader do begin
        if not ("Shipping Advice" = "Shipping Advice"::Complete) then
          exit(false);

        SalesLine.SETCURRENTKEY("Document Type",Type,"No.");
        SalesLine.SETRANGE("Document Type","Document Type");
        SalesLine.SETRANGE("Document No.","No.");
        SalesLine.SETRANGE(Type,SalesLine.Type::Item);
        if SalesLine.FINDSET then begin
          SalesLine2.COPYFILTERS(SalesLine);
          SalesLine2.SETCURRENTKEY("Document Type","Document No.","Location Code");
          SalesLine2.SETFILTER("Location Code",'<> %1',SalesLine."Location Code");
          if not SalesLine2.ISEMPTY then begin
            if ShowError then
              ERROR(Text001,FIELDCAPTION("Shipping Advice"),"Shipping Advice",
                SalesOrder.CAPTION,"No.",SalesLine.Type);
            exit(true);
          end;

          SetItemVariant(CurrItemVariant,SalesLine."No.",SalesLine."Variant Code");
          TotalNoOfRecords := SalesLine.COUNT;
          repeat
            RecordNo += 1;
            if EqualItemVariant(CurrItemVariant,SalesLine."No.",SalesLine."Variant Code") then
              QtyOutstandingBase += SalesLine."Outstanding Qty. (Base)"
            else begin
              if CheckAvailability(
                   CurrItemVariant,QtyOutstandingBase,SalesLine."Location Code",
                   SalesOrder.CAPTION,DATABASE::"Sales Line","Document Type","No.",ShowError)
              then
                exit(true);
              SetItemVariant(CurrItemVariant,SalesLine."No.",SalesLine."Variant Code");
              QtyOutstandingBase := SalesLine."Outstanding Qty. (Base)";
            end;
            if RecordNo = TotalNoOfRecords then begin // last record
              if CheckAvailability(
                   CurrItemVariant,QtyOutstandingBase,SalesLine."Location Code",
                   SalesOrder.CAPTION,DATABASE::"Sales Line","Document Type","No.",ShowError)
              then
                exit(true);
            end;
          until SalesLine.NEXT = 0; // sorted by item
        end;
      end;
    END;

    [External]
    PROCEDURE CheckTransferHeader@8(TransferHeader@1000 : Record "Transfer Header";ShowError@1001 : Boolean) : Boolean;
    VAR
      TransferLine@1002 : Record "Transfer Line";
      CurrItemVariant@1009 : Record "Item Variant";
      TransferOrder@1003 : Page "Transfer Order";
      QtyOutstandingBase@1005 : Decimal;
      RecordNo@1006 : Integer;
      TotalNoOfRecords@1007 : Integer;
    BEGIN
      with TransferHeader do begin
        if not ("Shipping Advice" = "Shipping Advice"::Complete) then
          exit(false);

        TransferLine.SETCURRENTKEY("Item No.");
        TransferLine.SETRANGE("Document No.","No.");
        if TransferLine.FINDSET then begin
          SetItemVariant(CurrItemVariant,TransferLine."Item No.",TransferLine."Variant Code");
          TotalNoOfRecords := TransferLine.COUNT;
          repeat
            RecordNo += 1;
            if EqualItemVariant(CurrItemVariant,TransferLine."Item No.",TransferLine."Variant Code") then
              QtyOutstandingBase += TransferLine."Outstanding Qty. (Base)"
            else begin
              if CheckAvailability(
                   CurrItemVariant,QtyOutstandingBase,TransferLine."Transfer-from Code",
                   TransferOrder.CAPTION,DATABASE::"Transfer Line",0,"No.",ShowError)
              then // outbound
                exit(true);
              SetItemVariant(CurrItemVariant,TransferLine."Item No.",TransferLine."Variant Code");
              QtyOutstandingBase := TransferLine."Outstanding Qty. (Base)";
            end;
            if RecordNo = TotalNoOfRecords then begin // last record
              if CheckAvailability(
                   CurrItemVariant,QtyOutstandingBase,TransferLine."Transfer-from Code",
                   TransferOrder.CAPTION,DATABASE::"Transfer Line",0,"No.",ShowError)
              then // outbound
                exit(true);
            end;
          until TransferLine.NEXT = 0; // sorted by item
        end;
      end;
    END;

    LOCAL PROCEDURE CheckAvailability@9(CurrItemVariant@1013 : Record "Item Variant";QtyBaseNeeded@1001 : Decimal;LocationCode@1011 : Code[10];FormCaption@1002 : Text[1024];SourceType@1003 : Integer;SourceSubType@1004 : Integer;SourceID@1005 : Code[20];ShowError@1006 : Boolean) : Boolean;
    VAR
      Item@1007 : Record Item;
      ReservEntry@1008 : Record "Reservation Entry";
      ReservEntry2@1009 : Record "Reservation Entry";
      QtyReservedForOrder@1010 : Decimal;
    BEGIN
      with Item do begin
        GET(CurrItemVariant."Item No.");
        SETRANGE("Location Filter",LocationCode);
        SETRANGE("Variant Filter",CurrItemVariant.Code);
        CALCFIELDS(Inventory,"Reserved Qty. on Inventory");

        // find qty reserved for this order
        ReservEntry.SetSourceFilter(SourceType,SourceSubType,SourceID,-1,true);
        ReservEntry.SETRANGE("Item No.",CurrItemVariant."Item No.");
        ReservEntry.SETRANGE("Location Code",LocationCode);
        ReservEntry.SETRANGE("Variant Code",CurrItemVariant.Code);
        ReservEntry.SETRANGE("Reservation Status",ReservEntry."Reservation Status"::Reservation);
        if ReservEntry.FINDSET then
          repeat
            ReservEntry2.GET(ReservEntry."Entry No.",not ReservEntry.Positive);
            QtyReservedForOrder += ReservEntry2."Quantity (Base)";
          until ReservEntry.NEXT = 0;

        if Inventory - ("Reserved Qty. on Inventory" - QtyReservedForOrder) < QtyBaseNeeded then begin
          if ShowError then
            ERROR(Text002,CurrItemVariant."Item No.",LocationCode,FormCaption,SourceID);
          exit(true);
        end;
      end;
    END;

    LOCAL PROCEDURE OpenWarehouseShipmentPage@14();
    VAR
      WarehouseShipmentHeader@1000 : Record "Warehouse Shipment Header";
    BEGIN
      GetSourceDocuments.GetLastShptHeader(WarehouseShipmentHeader);
      PAGE.RUN(PAGE::"Warehouse Shipment",WarehouseShipmentHeader);
    END;

    LOCAL PROCEDURE GetRequireShipRqst@11(VAR WhseRqst@1000 : Record "Warehouse Request");
    VAR
      Location@1001 : Record Location;
      LocationCode@1002 : Text;
    BEGIN
      if WhseRqst.FINDSET then begin
        repeat
          if Location.RequireShipment(WhseRqst."Location Code") then
            LocationCode += WhseRqst."Location Code" + '|';
        until WhseRqst.NEXT = 0;
        if LocationCode <> '' then begin
          LocationCode := COPYSTR(LocationCode,1,STRLEN(LocationCode) - 1);
          if LocationCode[1] = '|' then
            LocationCode := '''''' + LocationCode;
        end;
        WhseRqst.SETFILTER("Location Code",LocationCode);
      end;
    END;

    LOCAL PROCEDURE SetItemVariant@12(VAR CurrItemVariant@1000 : Record "Item Variant";ItemNo@1001 : Code[20];VariantCode@1002 : Code[10]);
    BEGIN
      CurrItemVariant."Item No." := ItemNo;
      CurrItemVariant.Code := VariantCode;
    END;

    LOCAL PROCEDURE EqualItemVariant@16(CurrItemVariant@1002 : Record "Item Variant";ItemNo@1001 : Code[20];VariantCode@1000 : Code[10]) : Boolean;
    BEGIN
      exit((CurrItemVariant."Item No." = ItemNo) and (CurrItemVariant.Code = VariantCode));
    END;

    LOCAL PROCEDURE FindWarehouseRequestForSalesOrder@17(VAR WhseRqst@1000 : Record "Warehouse Request";SalesHeader@1001 : Record "Sales Header");
    BEGIN
      with SalesHeader do begin
        TESTFIELD(Status,Status::Released);
        if WhseShpmntConflict("Document Type","No.","Shipping Advice") then
          ERROR(Text003,FORMAT("Shipping Advice"));
        CheckSalesHeader(SalesHeader,true);
        WhseRqst.SETRANGE(Type,WhseRqst.Type::Outbound);
        WhseRqst.SetSourceFilter(DATABASE::"Sales Line","Document Type","No.");
        WhseRqst.SETRANGE("Document Status",WhseRqst."Document Status"::Released);
        GetRequireShipRqst(WhseRqst);
      end;
    END;

    LOCAL PROCEDURE FindWarehouseRequestForPurchReturnOrder@19(VAR WhseRqst@1001 : Record "Warehouse Request";PurchHeader@1000 : Record "Purchase Header");
    BEGIN
      with PurchHeader do begin
        TESTFIELD(Status,Status::Released);
        WhseRqst.SETRANGE(Type,WhseRqst.Type::Outbound);
        WhseRqst.SetSourceFilter(DATABASE::"Purchase Line","Document Type","No.");
        WhseRqst.SETRANGE("Document Status",WhseRqst."Document Status"::Released);
        GetRequireShipRqst(WhseRqst);
      end;
    END;

    LOCAL PROCEDURE FindWarehouseRequestForOutbndTransferOrder@22(VAR WhseRqst@1000 : Record "Warehouse Request";TransHeader@1001 : Record "Transfer Header");
    BEGIN
      with TransHeader do begin
        TESTFIELD(Status,Status::Released);
        CheckTransferHeader(TransHeader,true);
        WhseRqst.SETRANGE(Type,WhseRqst.Type::Outbound);
        WhseRqst.SetSourceFilter(DATABASE::"Transfer Line",0,"No.");
        WhseRqst.SETRANGE("Document Status",WhseRqst."Document Status"::Released);
        GetRequireShipRqst(WhseRqst);
      end;
    END;

    LOCAL PROCEDURE FindWarehouseRequestForServiceOrder@27(VAR WhseRqst@1000 : Record "Warehouse Request";ServiceHeader@1001 : Record "Service Header");
    BEGIN
      with ServiceHeader do begin
        TESTFIELD("Release Status","Release Status"::"Released to Ship");
        WhseRqst.SETRANGE(Type,WhseRqst.Type::Outbound);
        WhseRqst.SetSourceFilter(DATABASE::"Service Line","Document Type","No.");
        WhseRqst.SETRANGE("Document Status",WhseRqst."Document Status"::Released);
        GetRequireShipRqst(WhseRqst);
      end;
    END;

    LOCAL PROCEDURE ShowResult@30(WhseShipmentCreated@1000 : Boolean);
    VAR
      WarehouseRequest@1001 : Record "Warehouse Request";
    BEGIN
      if WhseShipmentCreated then begin
        GetSourceDocuments.ShowShipmentDialog;
        OpenWarehouseShipmentPage;
      end else
        MESSAGE(Text004,WarehouseRequest.TABLECAPTION);
    END;

    BEGIN
    END.
  }
}

