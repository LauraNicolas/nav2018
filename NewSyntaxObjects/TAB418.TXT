OBJECT Table 418 IC Inbox Transaction
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ICInboxJnlLine@1000 : Record "IC Inbox Jnl. Line";
               ICInboxPurchHdr@1001 : Record "IC Inbox Purchase Header";
               ICInboxSalesHdr@1002 : Record "IC Inbox Sales Header";
             BEGIN
               case "Source Type" of
                 "Source Type"::Journal:
                   begin
                     ICInboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
                     ICInboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
                     if ICInboxJnlLine.FINDFIRST then
                       ICInboxJnlLine.DELETEALL(true);
                   end;
                 "Source Type"::"Sales Document":
                   begin
                     ICInboxSalesHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxSalesHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxSalesHdr.SETRANGE("Transaction Source","Transaction Source");
                     if ICInboxSalesHdr.FINDFIRST then
                       ICInboxSalesHdr.DELETE(true);
                   end;
                 "Source Type"::"Purchase Document":
                   begin
                     ICInboxPurchHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxPurchHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxPurchHdr.SETRANGE("Transaction Source","Transaction Source");
                     if ICInboxPurchHdr.FINDFIRST then
                       ICInboxPurchHdr.DELETE(true);
                   end;
               end;
             END;

    CaptionML=ENU=IC Inbox Transaction;
  }
  FIELDS
  {
    { 1   ;   ;Transaction No.     ;Integer       ;CaptionML=ENU=Transaction No.;
                                                   Editable=false }
    { 2   ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner";
                                                   CaptionML=ENU=IC Partner Code;
                                                   Editable=false }
    { 3   ;   ;Source Type         ;Option        ;CaptionML=ENU=Source Type;
                                                   OptionCaptionML=ENU=Journal,Sales Document,Purchase Document;
                                                   OptionString=Journal,"Sales Document","Purchase Document";
                                                   Editable=false }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order";
                                                   OptionString=" ",Payment,Invoice,"Credit Memo",Refund,Order,"Return Order";
                                                   Editable=false }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No.;
                                                   Editable=false }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date;
                                                   Editable=false }
    { 8   ;   ;Transaction Source  ;Option        ;CaptionML=ENU=Transaction Source;
                                                   OptionCaptionML=ENU=Returned by Partner,Created by Partner;
                                                   OptionString="Returned by Partner","Created by Partner";
                                                   Editable=false }
    { 9   ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date;
                                                   Editable=false }
    { 10  ;   ;Line Action         ;Option        ;OnValidate=BEGIN
                                                                if (("Line Action" = "Line Action"::"Return to IC Partner") or ("Line Action" = "Line Action"::Accept)) and
                                                                   ("Transaction Source" = "Transaction Source"::"Returned by Partner")
                                                                then
                                                                  ERROR(Text000,"Transaction No.","IC Partner Code");

                                                                if "Line Action" = "Line Action"::Accept then
                                                                  InboxCheckAccept;
                                                              END;

                                                   CaptionML=ENU=Line Action;
                                                   OptionCaptionML=ENU=No Action,Accept,Return to IC Partner,Cancel;
                                                   OptionString="No Action",Accept,"Return to IC Partner",Cancel }
    { 11  ;   ;Original Document No.;Code20       ;CaptionML=ENU=Original Document No. }
    { 12  ;   ;IC Partner G/L Acc. No.;Code20     ;CaptionML=ENU=IC Partner G/L Acc. No. }
    { 13  ;   ;Source Line No.     ;Integer       ;CaptionML=ENU=Source Line No. }
  }
  KEYS
  {
    {    ;"Transaction No.","IC Partner Code","Transaction Source","Document Type";
                                                   Clustered=true }
    {    ;"IC Partner Code"                        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Transaction No. %1 has been returned by IC Partner %2.\You can only cancel returned transactions.';

    [External]
    PROCEDURE ShowDetails@1();
    VAR
      ICInBoxJnlLine@1005 : Record "IC Inbox Jnl. Line";
      ICInboxSalesHeader@1004 : Record "IC Inbox Sales Header";
      ICInboxPurchHeader@1003 : Record "IC Inbox Purchase Header";
      ICInboxJnlLines@1002 : Page "IC Inbox Jnl. Lines";
      ICInboxSalesDoc@1001 : Page "IC Inbox Sales Doc.";
      ICInboxPurchDoc@1000 : Page "IC Inbox Purchase Doc.";
    BEGIN
      case "Source Type" of
        "Source Type"::Journal:
          begin
            ICInBoxJnlLine.SETRANGE("Transaction No.","Transaction No.");
            ICInBoxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
            ICInBoxJnlLine.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(ICInboxJnlLines);
            ICInboxJnlLines.SETTABLEVIEW(ICInBoxJnlLine);
            ICInboxJnlLines.RUNMODAL;
          end;
        "Source Type"::"Sales Document":
          begin
            ICInboxSalesHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxSalesHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxSalesHeader.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(ICInboxSalesDoc);
            ICInboxSalesDoc.SETTABLEVIEW(ICInboxSalesHeader);
            ICInboxSalesDoc.RUNMODAL;
          end;
        "Source Type"::"Purchase Document":
          begin
            ICInboxPurchHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxPurchHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxPurchHeader.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(ICInboxPurchDoc);
            ICInboxPurchDoc.SETTABLEVIEW(ICInboxPurchHeader);
            ICInboxPurchDoc.RUNMODAL;
          end;
      end;
    END;

    LOCAL PROCEDURE InboxCheckAccept@40();
    VAR
      ICInboxTransaction2@1001 : Record "IC Inbox Transaction";
      HandledICInboxTrans@1002 : Record "Handled IC Inbox Trans.";
      Text001@1003 : TextConst 'ENU=Transaction No. %2 is a copy of Transaction No. %1, which has already been set to Accept.\Do you also want to accept Transaction No. %2?';
      Text002@1000 : TextConst 'ENU=A copy of Transaction No. %1 has already been accepted and is now in the Handled IC Inbox Transactions window.\Do you also want to accept Transaction No. %1?';
      ICInboxPurchHeader@1004 : Record "IC Inbox Purchase Header";
      PurchHeader@1005 : Record "Purchase Header";
      PurchInvHeader@1006 : Record "Purch. Inv. Header";
      Text003@1007 : TextConst 'ENU=A purchase order already exists for transaction %1. If you accept and post this document, you should delete the original purchase order %2 to avoid duplicate postings.';
      Text004@1008 : TextConst 'ENU=Purchase invoice %1 has already been posted for transaction %2. If you accept and post this document, you will have duplicate postings.\Are you sure you want to accept the transaction?';
    BEGIN
      HandledICInboxTrans.SETRANGE("IC Partner Code","IC Partner Code");
      HandledICInboxTrans.SETRANGE("Document Type","Document Type");
      HandledICInboxTrans.SETRANGE("Source Type","Source Type");
      HandledICInboxTrans.SETRANGE("Document No.","Document No.");
      if HandledICInboxTrans.FINDFIRST then
        if not CONFIRM(STRSUBSTNO(Text002,"Transaction No."),false) then
          ERROR('');

      ICInboxTransaction2.SETRANGE("IC Partner Code","IC Partner Code");
      ICInboxTransaction2.SETRANGE("Document Type","Document Type");
      ICInboxTransaction2.SETRANGE("Source Type","Source Type");
      ICInboxTransaction2.SETRANGE("Document No.","Document No.");
      ICInboxTransaction2.SETFILTER("Transaction No.",'<>%1',"Transaction No.");
      ICInboxTransaction2.SETRANGE("IC Partner G/L Acc. No.","IC Partner G/L Acc. No.");
      ICInboxTransaction2.SETRANGE("Source Line No.","Source Line No.");
      ICInboxTransaction2.SETRANGE("Line Action","Line Action"::Accept);
      if ICInboxTransaction2.FINDFIRST then
        if not CONFIRM(STRSUBSTNO(Text001,ICInboxTransaction2."Transaction No.","Transaction No."),false) then
          ERROR('');
      if ("Source Type" = "Source Type"::"Purchase Document") and
         ("Document Type" in ["Document Type"::Invoice,"Document Type"::"Credit Memo"])
      then begin
        ICInboxPurchHeader.GET("Transaction No.","IC Partner Code","Transaction Source");
        if ICInboxPurchHeader."Your Reference" <> '' then begin
          PurchHeader.SETRANGE("Your Reference",ICInboxPurchHeader."Your Reference");
          if PurchHeader.FINDFIRST then
            MESSAGE(Text003,ICInboxPurchHeader."IC Transaction No.",ICInboxPurchHeader."Your Reference")
          else begin
            PurchInvHeader.SETRANGE("Your Reference",ICInboxPurchHeader."Your Reference");
            if PurchInvHeader.FINDFIRST then
              if not CONFIRM(Text004,true,ICInboxPurchHeader."Your Reference",ICInboxPurchHeader."IC Transaction No.") then
                "Line Action" := xRec."Line Action";
          end;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

