OBJECT Table 1228 Payment Jnl. Export Error Text
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Payment Jnl. Export Error Text;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=ENU=Journal Template Name }
    { 2   ;   ;Journal Batch Name  ;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE ("Journal Template Name"=FIELD("Journal Template Name"));
                                                   CaptionML=ENU=Journal Batch Name }
    { 3   ;   ;Journal Line No.    ;Integer       ;CaptionML=ENU=Journal Line No. }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 5   ;   ;Error Text          ;Text250       ;CaptionML=ENU=Error Text }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 7   ;   ;Additional Information;Text250     ;CaptionML=ENU=Additional Information }
    { 8   ;   ;Support URL         ;Text250       ;CaptionML=ENU=Support URL }
  }
  KEYS
  {
    {    ;"Journal Template Name","Journal Batch Name","Document No.","Journal Line No.","Line No.";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE CreateNew@3(GenJnlLine@1000 : Record "Gen. Journal Line";NewText@1001 : Text;NewAddnlInfo@1003 : Text;NewExtSupportInfo@1002 : Text);
    BEGIN
      SetLineFilters(GenJnlLine);
      if FINDLAST then;
      "Journal Template Name" := GenJnlLine."Journal Template Name";
      "Journal Batch Name" := GenJnlLine."Journal Batch Name";
      "Document No." := GenJnlLine."Document No.";
      "Journal Line No." := GenJnlLine."Line No.";
      "Line No." += 1;
      "Error Text" := COPYSTR(NewText,1,MAXSTRLEN("Error Text"));
      "Additional Information" := COPYSTR(NewAddnlInfo,1,MAXSTRLEN("Additional Information"));
      "Support URL" := COPYSTR(NewExtSupportInfo,1,MAXSTRLEN("Support URL"));
      INSERT;
    END;

    [External]
    PROCEDURE JnlLineHasErrors@1(GenJnlLine@1000 : Record "Gen. Journal Line") : Boolean;
    BEGIN
      SetLineFilters(GenJnlLine);
      exit(not ISEMPTY);
    END;

    [External]
    PROCEDURE JnlBatchHasErrors@5(GenJnlLine@1000 : Record "Gen. Journal Line") : Boolean;
    BEGIN
      SetBatchFilters(GenJnlLine);
      exit(not ISEMPTY);
    END;

    [External]
    PROCEDURE DeleteJnlLineErrors@2(GenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      if JnlLineHasErrors(GenJnlLine) then
        DELETEALL;
    END;

    [External]
    PROCEDURE DeleteJnlBatchErrors@4(GenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      if JnlBatchHasErrors(GenJnlLine) then
        DELETEALL;
    END;

    LOCAL PROCEDURE SetBatchFilters@8(GenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      if (GenJnlLine."Journal Template Name" = '') and (GenJnlLine."Journal Batch Name" = '') then
        SETRANGE("Document No.",GenJnlLine."Document No.");
    END;

    LOCAL PROCEDURE SetLineFilters@9(GenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      SETRANGE("Document No.",GenJnlLine."Document No.");
      SETRANGE("Journal Line No.",GenJnlLine."Line No.");
    END;

    BEGIN
    END.
  }
}

