OBJECT Report 8611 Create Customer Journal Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Customer Journal Lines;
    ProcessingOnly=true;
    OnPostReport=BEGIN
                   MESSAGE(Text004);
                 END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;Customer            ;
               DataItemTable=Customer;
               DataItemTableView=SORTING("No.") WHERE("Bill-to Customer No."=FILTER(''));
               OnPreDataItem=BEGIN
                               CheckJournalTemplate;
                               CheckBatchName;
                               CheckPostingDate;

                               GenJnlLine.SETRANGE("Journal Template Name",JournalTemplate);
                               GenJnlLine.SETRANGE("Journal Batch Name",BatchName);
                               if GenJnlLine.FINDLAST then
                                 LineNo := GenJnlLine."Line No." + 10000
                               else
                                 LineNo := 10000;

                               GenJnlBatch.GET(JournalTemplate,BatchName);
                               if TemplateCode <> '' then
                                 StdGenJournal.GET(JournalTemplate,TemplateCode);
                             END;

               OnAfterGetRecord=VAR
                                  StdGenJournalLine@1000 : Record "Standard General Journal Line";
                                BEGIN
                                  GenJnlLine.INIT;
                                  if GetStandardJournalLine then begin
                                    Initialize(StdGenJournal,GenJnlBatch.Name);

                                    StdGenJournalLine.SETRANGE("Journal Template Name",StdGenJournal."Journal Template Name");
                                    StdGenJournalLine.SETRANGE("Standard Journal Code",StdGenJournal.Code);
                                    if StdGenJournalLine.FINDSET then
                                      repeat
                                        CopyGenJnlFromStdJnl(StdGenJournalLine,GenJnlLine);

                                        if PostingDate <> 0D then
                                          GenJnlLine.VALIDATE("Posting Date",PostingDate);

                                        GenJnlLine.VALIDATE("Document Type",DocumentTypes);
                                        GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::Customer);
                                        GenJnlLine.VALIDATE("Account No.","No.");
                                        if (GenJnlBatch."Bal. Account Type" = GenJnlBatch."Bal. Account Type"::"G/L Account") and
                                           (GenJnlBatch."Bal. Account No." <> '')
                                        then begin
                                          GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"G/L Account");
                                          GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
                                        end else
                                          if "Customer Posting Group" <> '' then
                                            if CustPostGrp.GET("Customer Posting Group") then begin
                                              GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"G/L Account");
                                              GenJnlLine.VALIDATE("Bal. Account No.",CustPostGrp."Receivables Account");
                                            end;

                                        if DocumentDate <> 0D then begin
                                          GenJnlLine.VALIDATE("Posting Date",DocumentDate);
                                          GenJnlLine."Posting Date" := PostingDate;
                                        end;

                                        GenJnlLine.MODIFY(true);
                                      until StdGenJournalLine.NEXT = 0;
                                  end else begin
                                    GenJnlLine.VALIDATE("Journal Template Name",GenJnlLine.GETFILTER("Journal Template Name"));
                                    GenJnlLine.VALIDATE("Journal Batch Name",BatchName);
                                    GenJnlLine."Line No." := LineNo;
                                    LineNo := LineNo + 10000;

                                    if PostingDate <> 0D then
                                      GenJnlLine.VALIDATE("Posting Date",PostingDate);

                                    GenJnlLine.VALIDATE("Document Type",DocumentTypes);
                                    GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::Customer);
                                    GenJnlLine.VALIDATE("Account No.","No.");
                                    if (GenJnlBatch."Bal. Account Type" = GenJnlBatch."Bal. Account Type"::"G/L Account") and
                                       (GenJnlBatch."Bal. Account No." <> '')
                                    then begin
                                      GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"G/L Account");
                                      GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
                                    end else
                                      if "Customer Posting Group" <> '' then
                                        if CustPostGrp.GET("Customer Posting Group") then begin
                                          GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"G/L Account");
                                          GenJnlLine.VALIDATE("Bal. Account No.",CustPostGrp."Receivables Account");
                                        end;
                                    if DocumentDate <> 0D then begin
                                      GenJnlLine.VALIDATE("Posting Date",DocumentDate);
                                      GenJnlLine."Posting Date" := PostingDate;
                                    end;

                                    if not GenJnlLine.INSERT(true) then
                                      GenJnlLine.MODIFY(true);
                                  end;
                                END;

               ReqFilterFields="No.","Currency Code","Country/Region Code","Salesperson Code","Customer Posting Group",Blocked }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   if PostingDate = 0D then
                     PostingDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 2   ;2   ;Field     ;
                  Name=Document Type;
                  CaptionML=ENU=Document Type;
                  ToolTipML=ENU=Specifies the type of document that is processed by the report or batch job.;
                  OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentTypes }

      { 4   ;2   ;Field     ;
                  Name=Posting Date;
                  CaptionML=ENU=Posting Date;
                  ToolTipML=ENU=Specifies the date for the posting of this batch job. By default, the working date is entered, but you can change it.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               CheckPostingDate;
                             END;
                              }

      { 6   ;2   ;Field     ;
                  Name=Document Date;
                  CaptionML=ENU=Document Date;
                  ToolTipML=ENU=Specifies the document date that will be inserted on the created records.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocumentDate }

      { 12  ;2   ;Field     ;
                  Name=Journal Template;
                  CaptionML=ENU=Journal Template;
                  ToolTipML=ENU=Specifies the journal template that the customer journal is based on.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=JournalTemplate;
                  TableRelation="Gen. Journal Batch".Name;
                  OnValidate=BEGIN
                               CheckJournalTemplate;
                             END;

                  OnLookup=VAR
                             GenJnlTemplate@1001 : Record "Gen. Journal Template";
                             GenJnlTemplates@1000 : Page "General Journal Templates";
                           BEGIN
                             GenJnlTemplate.SETRANGE(Type,GenJnlTemplate.Type::General);
                             GenJnlTemplate.SETRANGE(Recurring,false);
                             GenJnlTemplates.SETTABLEVIEW(GenJnlTemplate);

                             GenJnlTemplates.LOOKUPMODE := true;
                             GenJnlTemplates.EDITABLE := false;
                             if GenJnlTemplates.RUNMODAL = ACTION::LookupOK then begin
                               GenJnlTemplates.GETRECORD(GenJnlTemplate);
                               JournalTemplate := GenJnlTemplate.Name;
                             end;
                           END;
                            }

      { 8   ;2   ;Field     ;
                  Name=Batch Name;
                  CaptionML=ENU=Batch Name;
                  ToolTipML=ENU=Specifies the name of the journal batch.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=BatchName;
                  TableRelation="Gen. Journal Batch".Name;
                  OnValidate=BEGIN
                               CheckBatchName;
                             END;

                  OnLookup=VAR
                             GenJnlBatches@1001 : Page "General Journal Batches";
                           BEGIN
                             if JournalTemplate <> '' then begin
                               GenJnlBatch.SETRANGE("Journal Template Name",JournalTemplate);
                               GenJnlBatches.SETTABLEVIEW(GenJnlBatch);
                             end;

                             GenJnlBatches.LOOKUPMODE := true;
                             GenJnlBatches.EDITABLE := false;
                             if GenJnlBatches.RUNMODAL = ACTION::LookupOK then begin
                               GenJnlBatches.GETRECORD(GenJnlBatch);
                               BatchName := GenJnlBatch.Name;
                             end;
                           END;
                            }

      { 10  ;2   ;Field     ;
                  Name=Standard General Journal;
                  CaptionML=ENU=Standard General Journal;
                  ToolTipML=ENU=Specifies the standard general journal that the batch job uses.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=TemplateCode;
                  TableRelation="Standard General Journal".Code;
                  OnLookup=VAR
                             StdGenJournal1@1005 : Record "Standard General Journal";
                             StdGenJnls@1000 : Page "Standard General Journals";
                           BEGIN
                             if JournalTemplate <> '' then begin
                               StdGenJournal1.SETRANGE("Journal Template Name",JournalTemplate);
                               StdGenJnls.SETTABLEVIEW(StdGenJournal1);
                             end;

                             StdGenJnls.LOOKUPMODE := true;
                             StdGenJnls.EDITABLE := false;
                             if StdGenJnls.RUNMODAL = ACTION::LookupOK then begin
                               StdGenJnls.GETRECORD(StdGenJournal1);
                               TemplateCode := StdGenJournal1.Code;
                             end;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      StdGenJournal@1007 : Record "Standard General Journal";
      GenJnlBatch@1006 : Record "Gen. Journal Batch";
      GenJnlLine@1005 : Record "Gen. Journal Line";
      LastGenJnlLine@1012 : Record "Gen. Journal Line";
      CustPostGrp@1009 : Record "Customer Posting Group";
      DocumentTypes@1000 : '" ",Payment,Invoice,"Credit Memo","Finance Charge Memo",Reminder,Refund';
      PostingDate@1001 : Date;
      DocumentDate@1002 : Date;
      BatchName@1003 : Code[10];
      TemplateCode@1004 : Code[20];
      LineNo@1008 : Integer;
      JournalTemplate@1015 : Text[10];
      Text001@1016 : TextConst 'ENU=Gen. Journal Template name is blank.';
      Text002@1013 : TextConst 'ENU=Gen. Journal Batch name is blank.';
      Text004@1017 : TextConst 'ENU=General journal lines are successfully created.';
      PostingDateIsEmptyErr@1010 : TextConst 'ENU=The posting date is empty.';

    LOCAL PROCEDURE GetStandardJournalLine@3() : Boolean;
    VAR
      StdGenJounalLine@1000 : Record "Standard General Journal Line";
    BEGIN
      if TemplateCode = '' then
        exit(false);
      StdGenJounalLine.SETRANGE("Journal Template Name",StdGenJournal."Journal Template Name");
      StdGenJounalLine.SETRANGE("Standard Journal Code",StdGenJournal.Code);
      exit(StdGenJounalLine.FINDFIRST)
    END;

    [External]
    PROCEDURE Initialize@15(VAR StdGenJnl@1001 : Record "Standard General Journal";JnlBatchName@1000 : Code[10]);
    BEGIN
      GenJnlLine."Journal Template Name" := StdGenJnl."Journal Template Name";
      GenJnlLine."Journal Batch Name" := JnlBatchName;
      GenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      LastGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      LastGenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      if LastGenJnlLine.FINDLAST then;

      GenJnlBatch.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlBatch.SETRANGE(Name,JnlBatchName);

      if GenJnlBatch.FINDFIRST then;
    END;

    LOCAL PROCEDURE CopyGenJnlFromStdJnl@1(StdGenJnlLine@1001 : Record "Standard General Journal Line";VAR GenJnlLine@1002 : Record "Gen. Journal Line");
    VAR
      GenJnlManagement@1000 : Codeunit GenJnlManagement;
      Balance@1007 : Decimal;
      TotalBalance@1006 : Decimal;
      ShowBalance@1005 : Boolean;
      ShowTotalBalance@1004 : Boolean;
    BEGIN
      GenJnlLine.INIT;
      GenJnlLine."Line No." := 0;
      GenJnlManagement.CalcBalance(GenJnlLine,LastGenJnlLine,Balance,TotalBalance,ShowBalance,ShowTotalBalance);
      GenJnlLine.SetUpNewLine(LastGenJnlLine,Balance,true);
      if LastGenJnlLine."Line No." <> 0 then
        GenJnlLine."Line No." := LastGenJnlLine."Line No." + 10000
      else
        GenJnlLine."Line No." := 10000;

      GenJnlLine.TRANSFERFIELDS(StdGenJnlLine,false);
      GenJnlLine.UpdateLineBalance;
      GenJnlLine.VALIDATE("Currency Code");

      if GenJnlLine."VAT Prod. Posting Group" <> '' then
        GenJnlLine.VALIDATE("VAT Prod. Posting Group");
      if (GenJnlLine."VAT %" <> 0) and GenJnlBatch."Allow VAT Difference" then
        GenJnlLine.VALIDATE("VAT Amount",StdGenJnlLine."VAT Amount");
      GenJnlLine.VALIDATE("Bal. VAT Prod. Posting Group");

      if GenJnlBatch."Allow VAT Difference" then
        GenJnlLine.VALIDATE("Bal. VAT Amount",StdGenJnlLine."Bal. VAT Amount");
      GenJnlLine.INSERT(true);

      LastGenJnlLine := GenJnlLine;
    END;

    [External]
    PROCEDURE InitializeRequest@2(DocumentTypesFrom@1000 : Option;PostingDateFrom@1001 : Date;DocumentDateFrom@1002 : Date);
    BEGIN
      DocumentTypes := DocumentTypesFrom;
      PostingDate := PostingDateFrom;
      DocumentDate := DocumentDateFrom;
    END;

    [External]
    PROCEDURE InitializeRequestTemplate@4(JournalTemplateFrom@1000 : Text[10];BatchNameFrom@1001 : Code[10];TemplateCodeFrom@1002 : Code[20]);
    BEGIN
      JournalTemplate := JournalTemplateFrom;
      BatchName := BatchNameFrom;
      TemplateCode := TemplateCodeFrom;
    END;

    LOCAL PROCEDURE CheckPostingDate@24();
    BEGIN
      if PostingDate = 0D then
        ERROR(PostingDateIsEmptyErr);
    END;

    LOCAL PROCEDURE CheckBatchName@32();
    BEGIN
      if BatchName = '' then
        ERROR(Text002);
    END;

    LOCAL PROCEDURE CheckJournalTemplate@33();
    BEGIN
      if JournalTemplate = '' then
        ERROR(Text001);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

