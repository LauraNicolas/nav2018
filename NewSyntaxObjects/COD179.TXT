OBJECT Codeunit 179 Reversal-Post
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Reversal Entry";
    OnRun=VAR
            GLReg@1003 : Record "G/L Register";
            GenJnlTemplate@1002 : Record "Gen. Journal Template";
            GenJnlPostReverse@1001 : Codeunit "Gen. Jnl.-Post Reverse";
            Txt@1004 : Text[1024];
            WarningText@1000 : Text[250];
            Number@1005 : Integer;
          BEGIN
            RESET;
            SETRANGE("Entry Type","Entry Type"::"Fixed Asset");
            if FINDFIRST then
              WarningText := Text007;
            SETRANGE("Entry Type");
            if PrintRegister then
              Txt := Text004 + WarningText + '\' + Text005
            else
              Txt := Text004 + WarningText + '\' + Text002;
            if not FINDFIRST then
              ERROR(Text006);

            if not HideDialog then
              if not CONFIRM(Txt,false) then
                exit;

            ReversalEntry := Rec;
            if "Reversal Type" = "Reversal Type"::Transaction then
              ReversalEntry.SetReverseFilter("Transaction No.","Reversal Type")
            else
              ReversalEntry.SetReverseFilter("G/L Register No.","Reversal Type");
            ReversalEntry.CheckEntries;
            GET(1);
            if "Reversal Type" = "Reversal Type"::Register then
              Number := "G/L Register No."
            else
              Number := "Transaction No.";
            if not ReversalEntry.VerifyReversalEntries(Rec,Number,"Reversal Type") then
              ERROR(Text008);
            GenJnlPostReverse.Reverse(ReversalEntry,Rec);
            if PrintRegister then begin
              GenJnlTemplate.VALIDATE(Type);
              if GenJnlTemplate."Posting Report ID" <> 0 then
                if GLReg.FINDLAST then begin;
                  GLReg.SETRECFILTER;
                  REPORT.RUN(GenJnlTemplate."Posting Report ID",false,false,GLReg);
                end;
            end;
            DELETEALL;
            if not HideDialog then
              MESSAGE(Text003);
          END;

  }
  CODE
  {
    VAR
      Text002@1007 : TextConst 'ENU=Do you want to reverse the entries?';
      Text003@1005 : TextConst 'ENU=The entries were successfully reversed.';
      Text004@1012 : TextConst 'ENU=To reverse these entries, correcting entries will be posted.';
      Text005@1000 : TextConst 'ENU=Do you want to reverse the entries and print the report?';
      Text006@1001 : TextConst 'ENU=There is nothing to reverse.';
      Text007@1004 : TextConst 'ENU=\There are one or more FA Ledger Entries. You should consider using the fixed asset function Cancel Entries.';
      Text008@1003 : TextConst 'ENU=Changes have been made to posted entries after the window was opened.\Close and reopen the window to continue.';
      ReversalEntry@1002 : Record "Reversal Entry";
      PrintRegister@1009 : Boolean;
      HideDialog@1006 : Boolean;

    [External]
    PROCEDURE SetPrint@1000(NewPrintRegister@1001 : Boolean);
    BEGIN
      PrintRegister := NewPrintRegister;
    END;

    [External]
    PROCEDURE SetHideDialog@1(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    BEGIN
    END.
  }
}

