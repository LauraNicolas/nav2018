OBJECT Codeunit 130411 Sys. Warmup Scenarios
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [Test]
    [External]
    PROCEDURE WarmupInvoicePosting@1();
    VAR
      Customer@1006 : Record Customer;
      Item@1002 : Record Item;
      SalesHeader@1000 : Record "Sales Header";
    BEGIN
      if not Customer.FINDFIRST then
        exit;

      Item.SETFILTER(Inventory,'<>%1',0);
      if not Item.FINDFIRST then
        exit;

      CreateSalesInvoice(SalesHeader,Customer,Item);
      PostSalesInvoice(SalesHeader);
    END;

    LOCAL PROCEDURE GetRandomString@2() : Text;
    BEGIN
      exit(DELCHR(FORMAT(CREATEGUID),'=','{}-'));
    END;

    LOCAL PROCEDURE CreateSalesInvoice@3(VAR SalesHeader@1000 : Record "Sales Header";Customer@1001 : Record Customer;Item@1002 : Record Item);
    BEGIN
      CreateSalesHeader(SalesHeader,Customer);
      CreateSalesLine(SalesHeader,Item);
    END;

    LOCAL PROCEDURE PostSalesInvoice@4(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      SalesHeader.Ship := true;
      SalesHeader.Invoice := true;
      CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader);
    END;

    LOCAL PROCEDURE CreateSalesHeader@5(VAR SalesHeader@1000 : Record "Sales Header";Customer@1001 : Record Customer);
    BEGIN
      SalesHeader.VALIDATE("Document Type",SalesHeader."Document Type"::Invoice);
      SalesHeader."No." := COPYSTR(GetRandomString,1,MAXSTRLEN(SalesHeader."No."));
      SalesHeader.INSERT(true);
      SalesHeader.VALIDATE("Sell-to Customer No.",Customer."No.");
      SalesHeader.MODIFY(true);
    END;

    LOCAL PROCEDURE CreateSalesLine@6(SalesHeader@1000 : Record "Sales Header";Item@1001 : Record Item);
    VAR
      SalesLine@1002 : Record "Sales Line";
    BEGIN
      SalesLine.VALIDATE("Document Type",SalesHeader."Document Type");
      SalesLine.VALIDATE("Document No.",SalesHeader."No.");
      SalesLine.VALIDATE("Sell-to Customer No.",SalesHeader."Sell-to Customer No.");
      SalesLine.VALIDATE("Line No.",10000);
      SalesLine.INSERT(true);
      SalesLine.VALIDATE(Type,SalesLine.Type::Item);
      SalesLine.VALIDATE("No.",Item."No.");
      SalesLine.VALIDATE(Quantity,1);
      SalesLine.MODIFY(true);
    END;

    BEGIN
    END.
  }
}

