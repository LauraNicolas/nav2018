OBJECT Page 5496 Sales Order Line Entity
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Order Line Entity;
    SourceTable="Sales Invoice Line Aggregate";
    DelayedInsert=true;
    PageType=ListPart;
    SourceTableTemporary=true;
    OnFindRecord=VAR
                   GraphMgtSalesOrderBuffer@1001 : Codeunit "Graph Mgt - Sales Order Buffer";
                   DocumentIdFilter@1002 : Text;
                   FilterView@1003 : Text;
                 BEGIN
                   if not LinesLoaded then begin
                     FilterView := GETVIEW;
                     DocumentIdFilter := GETFILTER("Document Id");
                     GraphMgtSalesOrderBuffer.LoadLines(Rec,DocumentIdFilter);
                     SETVIEW(FilterView);
                     if not FINDFIRST then
                       exit(false);
                     LinesLoaded := true;
                   end;

                   exit(true);
                 END;

    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                  VALIDATE(Type,Type::Item);
                  RegisterFieldSet(FIELDNO(Type));
                END;

    OnInsertRecord=VAR
                     GraphMgtSalesOrderBuffer@1000 : Codeunit "Graph Mgt - Sales Order Buffer";
                   BEGIN
                     if InsertItem then
                       InsertItemOnTheFly;

                     GraphMgtSalesOrderBuffer.PropagateInsertLine(Rec,TempFieldBuffer);

                     SetCalculatedFields;
                   END;

    OnModifyRecord=VAR
                     GraphMgtSalesOrderBuffer@1000 : Codeunit "Graph Mgt - Sales Order Buffer";
                   BEGIN
                     if InsertItem then
                       InsertItemOnTheFly;

                     GraphMgtSalesOrderBuffer.PropagateModifyLine(Rec,TempFieldBuffer);

                     SetCalculatedFields;
                   END;

    OnDeleteRecord=VAR
                     GraphMgtSalesOrderBuffer@1000 : Codeunit "Graph Mgt - Sales Order Buffer";
                   BEGIN
                     GraphMgtSalesOrderBuffer.PropagateDeleteLine(Rec);
                   END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group2;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                Name=sequence;
                CaptionML=[@@@={Locked};
                           ENU=sequence];
                ApplicationArea=#All;
                SourceExpr="Line No.";
                OnValidate=BEGIN
                             if xRec."Line No." <> "Line No." then
                               ERROR(CannotChangeLineNoErr);

                             RegisterFieldSet(FIELDNO("Line No."));
                           END;

                ImplicitType=Integer }

    { 16  ;2   ;Field     ;
                Name=itemId;
                CaptionML=[@@@={Locked};
                           ENU=itemId];
                ApplicationArea=#All;
                SourceExpr="Item Id";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Type));
                             RegisterFieldSet(FIELDNO("No."));
                             RegisterFieldSet(FIELDNO("Item Id"));

                             Item.SETRANGE(Id,"Item Id");

                             if not Item.FINDFIRST then begin
                               InsertItem := true;
                               CheckIntegrationIdInUse;

                               Item.Id := "Item Id";
                               RegisterItemFieldSet(Item.FIELDNO(Id));
                               exit;
                             end;

                             "No." := Item."No.";
                           END;

                ImplicitType=GUID }

    { 25  ;2   ;Field     ;
                Name=accountId;
                CaptionML=[@@@={Locked};
                           ENU=accountId];
                ApplicationArea=#All;
                SourceExpr="Account Id";
                OnValidate=VAR
                             EmptyGuid@1000 : GUID;
                           BEGIN
                             if "Account Id" <> EmptyGuid then
                               if Item."No." <> '' then
                                 ERROR(BothItemIdAndAccountIdAreSpecifiedErr);
                             RegisterFieldSet(FIELDNO(Type));
                             RegisterFieldSet(FIELDNO("Account Id"));
                             RegisterFieldSet(FIELDNO("No."));
                           END;

                ImplicitType=GUID }

    { 24  ;2   ;Field     ;
                Name=lineType;
                CaptionML=[@@@={Locked};
                           ENU=lineType];
                ApplicationArea=#All;
                SourceExpr="API Type";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Type));
                           END;

                ImplicitType=Option }

    { 23  ;2   ;Field     ;
                Name=lineDetails;
                CaptionML=[@@@={Locked};
                           ENU=lineDetails];
                ToolTipML=ENU=Specifies details about the line.;
                ApplicationArea=#All;
                SourceExpr=LineObjectDetailsJSON;
                OnValidate=VAR
                             GraphMgtComplexTypes@1000 : Codeunit "Graph Mgt - Complex Types";
                           BEGIN
                             if not InsertItem then
                               exit;

                             GraphMgtComplexTypes.ParseDocumentLineObjectDetailsFromJSON(
                               LineObjectDetailsJSON,Item."No.",Item.Description,Item."Description 2");

                             if Item."No." <> '' then
                               RegisterItemFieldSet(Item.FIELDNO("No."));

                             RegisterFieldSet(FIELDNO("No."));

                             if Item.Description <> '' then
                               RegisterItemFieldSet(Item.FIELDNO(Description));

                             if Description = '' then begin
                               Description := Item.Description;
                               RegisterFieldSet(FIELDNO(Description));
                             end;

                             if Item."Description 2" <> '' then begin
                               "Description 2" := Item."Description 2";
                               RegisterItemFieldSet(Item.FIELDNO("Description 2"));
                               RegisterFieldSet(FIELDNO("Description 2"));
                             end;
                           END;

                ODataEDMType=DOCUMENTLINEOBJECTDETAILS;
                ImplicitType=Text }

    { 7   ;2   ;Field     ;
                Name=description;
                CaptionML=ENU=description;
                ToolTipML=ENU=Specifies the description of the sales order line.;
                ApplicationArea=#All;
                SourceExpr=Description;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Description));
                           END;

                ImplicitType=Text50 }

    { 28  ;2   ;Field     ;
                Name=unitOfMeasureId;
                CaptionML=[@@@={Locked};
                           ENU=UnitOfMeasureId];
                ApplicationArea=#All;
                SourceExpr="Unit of Measure Id";
                OnValidate=VAR
                             SalesInvoiceAggregator@1001 : Codeunit "Sales Invoice Aggregator";
                             GraphMgtSalesInvLines@1004 : Codeunit "Graph Mgt - Sales Inv. Lines";
                             BlankGUID@1003 : GUID;
                           BEGIN
                             SalesInvoiceAggregator.VerifyCanUpdateUOM(Rec);

                             if (UnitOfMeasureJSON = 'null') and ("Unit of Measure Id" <> BlankGUID) then
                               exit;

                             if "Unit of Measure Id" = BlankGUID then
                               "Unit of Measure Code" := ''
                             else begin
                               UnitOfMeasure.SETRANGE(Id,"Unit of Measure Id");
                               if not UnitOfMeasure.FINDFIRST then
                                 ERROR(UnitOfMeasureIdDoesNotMatchAUnitOfMeasureErr);

                               "Unit of Measure Code" := UnitOfMeasure.Code;
                             end;

                             RegisterFieldSet(FIELDNO("Unit of Measure Code"));

                             if InsertItem then
                               exit;

                             Item.SETRANGE(Id,"Item Id");
                             if Item.FINDFIRST then
                               SalesInvoiceAggregator.UpdateUnitOfMeasure(Item,GraphMgtSalesInvLines.GetUnitOfMeasureJSON(Rec));
                           END;

                ImplicitType=GUID }

    { 5   ;2   ;Field     ;
                Name=unitOfMeasure;
                CaptionML=[@@@={Locked};
                           ENU=unitOfMeasure];
                ToolTipML=ENU=Specifies Unit of Measure.;
                ApplicationArea=#All;
                SourceExpr=UnitOfMeasureJSON;
                OnValidate=VAR
                             TempUnitOfMeasure@1002 : TEMPORARY Record "Unit of Measure";
                             SalesInvoiceAggregator@1000 : Codeunit "Sales Invoice Aggregator";
                             GraphCollectionMgtItem@1001 : Codeunit "Graph Collection Mgt - Item";
                             GraphMgtSalesInvLines@1003 : Codeunit "Graph Mgt - Sales Inv. Lines";
                           BEGIN
                             SalesInvoiceAggregator.VerifyCanUpdateUOM(Rec);

                             if UnitOfMeasureJSON = 'null' then
                               TempUnitOfMeasure.Code := ''
                             else
                               GraphCollectionMgtItem.ParseJSONToUnitOfMeasure(UnitOfMeasureJSON,TempUnitOfMeasure);

                             if (UnitOfMeasureJSON = 'null') and (UnitOfMeasure.Code <> '') then
                               exit;
                             if (UnitOfMeasure.Code <> '') and (UnitOfMeasure.Code <> TempUnitOfMeasure.Code) then
                               ERROR(UnitOfMeasureValuesDontMatchErr);

                             "Unit of Measure Code" := TempUnitOfMeasure.Code;
                             RegisterFieldSet(FIELDNO("Unit of Measure Code"));

                             if InsertItem then
                               exit;

                             Item.SETRANGE(Id,"Item Id");
                             if Item.FINDFIRST then begin
                               if UnitOfMeasureJSON = 'null' then
                                 SalesInvoiceAggregator.UpdateUnitOfMeasure(Item,GraphMgtSalesInvLines.GetUnitOfMeasureJSON(Rec))
                               else
                                 SalesInvoiceAggregator.UpdateUnitOfMeasure(Item,UnitOfMeasureJSON);
                             end;
                           END;

                ODataEDMType=ITEM-UOM;
                ImplicitType=Text }

    { 8   ;2   ;Field     ;
                Name=quantity;
                CaptionML=[@@@={Locked};
                           ENU=quantity];
                ApplicationArea=#All;
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Quantity));
                           END;

                ImplicitType=Decimal }

    { 9   ;2   ;Field     ;
                Name=unitPrice;
                CaptionML=[@@@={Locked};
                           ENU=unitPrice];
                ApplicationArea=#All;
                SourceExpr="Unit Price";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Unit Price"));
                           END;

                ImplicitType=Decimal }

    { 10  ;2   ;Field     ;
                Name=discountAmount;
                CaptionML=[@@@={Locked};
                           ENU=discountAmount];
                ApplicationArea=#All;
                SourceExpr="Line Discount Amount";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Line Discount Amount"));
                           END;

                ImplicitType=Decimal }

    { 11  ;2   ;Field     ;
                Name=discountPercent;
                CaptionML=[@@@={Locked};
                           ENU=discountPercent];
                ApplicationArea=#All;
                SourceExpr="Line Discount %";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Line Discount %"));
                           END;

                ImplicitType=Decimal }

    { 19  ;2   ;Field     ;
                Name=discountAppliedBeforeTax;
                CaptionML=[@@@={Locked};
                           ENU=discountAppliedBeforeTax];
                ApplicationArea=#All;
                SourceExpr="Discount Applied Before Tax";
                Editable=FALSE;
                ImplicitType=Boolean }

    { 15  ;2   ;Field     ;
                Name=amountExcludingTax;
                CaptionML=[@@@={Locked};
                           ENU=amountExcludingTax];
                ApplicationArea=#All;
                SourceExpr="Line Amount Excluding Tax";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Amount));
                           END;

                ImplicitType=Decimal }

    { 12  ;2   ;Field     ;
                Name=taxCode;
                CaptionML=[@@@={Locked};
                           ENU=taxCode];
                ApplicationArea=#All;
                SourceExpr="Tax Code";
                OnValidate=VAR
                             GeneralLedgerSetup@1000 : Record "General Ledger Setup";
                           BEGIN
                             if InsertItem then begin
                               if GeneralLedgerSetup.UseVat then
                                 exit;

                               Item."Tax Group Code" := COPYSTR("Tax Code",1,MAXSTRLEN(Item."Tax Group Code"));
                               RegisterItemFieldSet(Item.FIELDNO("Tax Group Code"));
                             end;
                           END;

                ImplicitType=Code50 }

    { 3   ;2   ;Field     ;
                Name=taxPercent;
                CaptionML=[@@@={Locked};
                           ENU=taxPercent];
                ApplicationArea=#All;
                SourceExpr="VAT %";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 14  ;2   ;Field     ;
                Name=totalTaxAmount;
                CaptionML=[@@@={Locked};
                           ENU=totalTaxAmount];
                ApplicationArea=#All;
                SourceExpr="Line Tax Amount";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 18  ;2   ;Field     ;
                Name=amountIncludingTax;
                CaptionML=[@@@={Locked};
                           ENU=amountIncludingTax];
                ApplicationArea=#All;
                SourceExpr="Line Amount Including Tax";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Amount Including VAT"));
                           END;

                ImplicitType=Decimal }

    { 29  ;2   ;Field     ;
                Name=invoiceDiscountAllocation;
                CaptionML=[@@@={Locked};
                           ENU=invoiceDiscountAllocation];
                ApplicationArea=#All;
                SourceExpr="Inv. Discount Amount Excl. VAT";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 27  ;2   ;Field     ;
                Name=netAmount;
                CaptionML=[@@@={Locked};
                           ENU=netAmount];
                ApplicationArea=#All;
                SourceExpr=Amount;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 6   ;2   ;Field     ;
                Name=netTaxAmount;
                CaptionML=[@@@={Locked};
                           ENU=netTaxAmount];
                ApplicationArea=#All;
                SourceExpr="Tax Amount";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 26  ;2   ;Field     ;
                Name=netAmountIncludingTax;
                CaptionML=[@@@={Locked};
                           ENU=netAmountIncludingTax];
                ApplicationArea=#All;
                SourceExpr="Amount Including VAT";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 13  ;2   ;Field     ;
                Name=shipmentDate;
                CaptionML=[@@@={Locked};
                           ENU=shipmentDate];
                ApplicationArea=#All;
                SourceExpr="Shipment Date";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Shipment Date"));
                           END;

                ImplicitType=Date }

    { 17  ;2   ;Field     ;
                Name=shippedQuantity;
                CaptionML=[@@@={Locked};
                           ENU=shippedQuantity];
                ApplicationArea=#All;
                SourceExpr="Quantity Shipped";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Quantity Shipped"));
                           END;

                ImplicitType=Decimal }

    { 20  ;2   ;Field     ;
                Name=invoicedQuantity;
                CaptionML=[@@@={Locked};
                           ENU=invoicedQuantity];
                ApplicationArea=#All;
                SourceExpr="Quantity Invoiced";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Quantity Invoiced"));
                           END;

                ImplicitType=Decimal }

    { 21  ;2   ;Field     ;
                Name=invoiceQuantity;
                CaptionML=[@@@={Locked};
                           ENU=invoiceQuantity];
                ApplicationArea=#All;
                SourceExpr="Qty. to Invoice";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Qty. to Invoice"));
                           END;

                ImplicitType=Decimal }

    { 22  ;2   ;Field     ;
                Name=shipQuantity;
                CaptionML=[@@@={Locked};
                           ENU=shipQuantity];
                ApplicationArea=#All;
                SourceExpr="Qty. to Ship";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Qty. to Ship"));
                           END;

                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      TempFieldBuffer@1009 : TEMPORARY Record "Field Buffer";
      TempItemFieldSet@1008 : TEMPORARY Record Field;
      Item@1007 : Record Item;
      UnitOfMeasure@1012 : Record "Unit of Measure";
      UnitOfMeasureJSON@1006 : Text;
      LineObjectDetailsJSON@1003 : Text;
      LinesLoaded@1002 : Boolean;
      InsertItem@1000 : Boolean;
      CannotChangeLineNoErr@1001 : TextConst '@@@={Locked};ENU=The value for sequence cannot be modified. Delete and insert the line again.';
      ItemWasDeletedErr@1005 : TextConst '@@@={Locked};ENU=The item was deleted.';
      IdIsAlreadyUsedErr@1004 : TextConst '@@@={Locked};ENU=The id is already in use used.';
      BothItemIdAndAccountIdAreSpecifiedErr@1014 : TextConst 'ENU=Both itemId and accountId are specified. Specify only one of them.';
      UnitOfMeasureValuesDontMatchErr@1011 : TextConst '@@@={Locked};ENU=The unit of measure values do not match to a specific Unit of Measure.';
      UnitOfMeasureIdDoesNotMatchAUnitOfMeasureErr@1010 : TextConst '@@@={Locked};ENU=The "unitOfMeasureId" does not match to a Unit of Measure.';

    LOCAL PROCEDURE RegisterFieldSet@1(FieldNo@1000 : Integer);
    VAR
      LastOrderNo@1001 : Integer;
    BEGIN
      LastOrderNo := 1;
      if TempFieldBuffer.FINDLAST then
        LastOrderNo := TempFieldBuffer.Order + 1;

      CLEAR(TempFieldBuffer);
      TempFieldBuffer.Order := LastOrderNo;
      TempFieldBuffer."Table ID" := DATABASE::"Sales Invoice Line Aggregate";
      TempFieldBuffer."Field ID" := FieldNo;
      TempFieldBuffer.INSERT;
    END;

    LOCAL PROCEDURE ClearCalculatedFields@2();
    BEGIN
      TempFieldBuffer.RESET;
      TempFieldBuffer.DELETEALL;
      TempItemFieldSet.RESET;
      TempItemFieldSet.DELETEALL;

      CLEAR(Item);
      CLEAR(UnitOfMeasureJSON);
      CLEAR(InsertItem);
      CLEAR(LineObjectDetailsJSON);
    END;

    LOCAL PROCEDURE SetCalculatedFields@3();
    VAR
      GraphMgtSalesInvLines@1001 : Codeunit "Graph Mgt - Sales Inv. Lines";
      GraphMgtComplexTypes@1002 : Codeunit "Graph Mgt - Complex Types";
    BEGIN
      LineObjectDetailsJSON := GraphMgtComplexTypes.GetSalesLineDescriptionComplexType(Rec);
      UnitOfMeasureJSON := GraphMgtSalesInvLines.GetUnitOfMeasureJSON(Rec);
    END;

    LOCAL PROCEDURE RegisterItemFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      if TempItemFieldSet.GET(DATABASE::Item,FieldNo) then
        exit;

      TempItemFieldSet.INIT;
      TempItemFieldSet.TableNo := DATABASE::Item;
      TempItemFieldSet.VALIDATE("No.",FieldNo);
      TempItemFieldSet.INSERT(true);
    END;

    LOCAL PROCEDURE CheckIntegrationIdInUse@8();
    VAR
      IntegrationRecord@1000 : Record "Integration Record";
    BEGIN
      if not IntegrationRecord.GET("Item Id") then
        exit;

      if IntegrationRecord."Table ID" = DATABASE::Item then
        ERROR(ItemWasDeletedErr);

      ERROR(IdIsAlreadyUsedErr);
    END;

    LOCAL PROCEDURE InsertItemOnTheFly@16();
    VAR
      GraphCollectionMgtItem@1001 : Codeunit "Graph Collection Mgt - Item";
    BEGIN
      GraphCollectionMgtItem.InsertItemFromSalesDocument(Item,TempItemFieldSet,UnitOfMeasureJSON);
      VALIDATE("No.",Item."No.");

      if Description = '' then
        Description := Item.Description;
    END;

    BEGIN
    END.
  }
}

