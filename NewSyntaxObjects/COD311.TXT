OBJECT Codeunit 311 Item-Check Avail.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "My Notifications"=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The update has been interrupted to respect the warning.';
      AvailableToPromise@1006 : Codeunit "Available to Promise";
      NotificationLifecycleMgt@1021 : Codeunit "Notification Lifecycle Mgt.";
      ItemNo@1017 : Code[20];
      UnitOfMeasureCode@1016 : Code[10];
      ItemLocationCode@1022 : Code[10];
      NewItemNetChange@1015 : Decimal;
      OldItemNetChange@1014 : Decimal;
      OldItemNetResChange@1024 : Decimal;
      NewItemNetResChange@1025 : Decimal;
      ItemNetChange@1013 : Decimal;
      QtyPerUnitOfMeasure@1012 : Decimal;
      InitialQtyAvailable@1011 : Decimal;
      UseOrderPromise@1010 : Boolean;
      GrossReq@1009 : Decimal;
      ReservedReq@1001 : Decimal;
      SchedRcpt@1008 : Decimal;
      ReservedRcpt@1002 : Decimal;
      EarliestAvailDate@1005 : Date;
      InventoryQty@1004 : Decimal;
      OldItemShipmentDate@1003 : Date;
      NotificationMsg@1007 : TextConst '@@@="%1=Item No.";ENU=The available inventory for item %1 is lower than the entered quantity at this location.';
      DetailsTxt@1019 : TextConst 'ENU=Show details';
      ItemAvailabilityNotificationTxt@1020 : TextConst 'ENU=Item availability is low.';
      ItemAvailabilityNotificationDescriptionTxt@1018 : TextConst 'ENU=Show a warning when someone creates a sales order or sales invoice for an item that is out of stock.';

    [External]
    PROCEDURE ItemJnlCheckLine@1(ItemJnlLine@1000 : Record "Item Journal Line") Rollback : Boolean;
    BEGIN
      NotificationLifecycleMgt.RecallNotificationsForRecordWithAdditionalContext(
        ItemJnlLine.RECORDID,GetItemAvailabilityNotificationId,true);
      if ItemJnlLineShowWarning(ItemJnlLine) then
        Rollback := ShowAndHandleAvailabilityPage(ItemJnlLine.RECORDID);
    END;

    [External]
    PROCEDURE SalesLineCheck@2(SalesLine@1000 : Record "Sales Line") Rollback : Boolean;
    VAR
      TempAsmHeader@1007 : TEMPORARY Record "Assembly Header";
      TempAsmLine@1003 : TEMPORARY Record "Assembly Line";
      ATOLink@1001 : Record "Assemble-to-Order Link";
    BEGIN
      NotificationLifecycleMgt.RecallNotificationsForRecordWithAdditionalContext(
        SalesLine.RECORDID,GetItemAvailabilityNotificationId,true);
      if SalesLineShowWarning(SalesLine) then
        Rollback := ShowAndHandleAvailabilityPage(SalesLine.RECORDID);

      if not Rollback then
        if ATOLink.SalesLineCheckAvailShowWarning(SalesLine,TempAsmHeader,TempAsmLine) then
          Rollback := ShowAsmWarningYesNo(TempAsmHeader,TempAsmLine);
    END;

    [External]
    PROCEDURE TransferLineCheck@3(TransLine@1000 : Record "Transfer Line") Rollback : Boolean;
    BEGIN
      NotificationLifecycleMgt.RecallNotificationsForRecordWithAdditionalContext(
        TransLine.RECORDID,GetItemAvailabilityNotificationId,true);
      if TransferLineShowWarning(TransLine) then
        Rollback := ShowAndHandleAvailabilityPage(TransLine.RECORDID);
    END;

    [External]
    PROCEDURE ServiceInvLineCheck@4(ServInvLine@1000 : Record "Service Line") Rollback : Boolean;
    BEGIN
      NotificationLifecycleMgt.RecallNotificationsForRecordWithAdditionalContext(
        ServInvLine.RECORDID,GetItemAvailabilityNotificationId,true);
      if ServiceInvLineShowWarning(ServInvLine) then
        Rollback := ShowAndHandleAvailabilityPage(ServInvLine.RECORDID);
    END;

    [External]
    PROCEDURE JobPlanningLineCheck@5(JobPlanningLine@1000 : Record "Job Planning Line") Rollback : Boolean;
    BEGIN
      NotificationLifecycleMgt.RecallNotificationsForRecordWithAdditionalContext(
        JobPlanningLine.RECORDID,GetItemAvailabilityNotificationId,true);
      if JobPlanningLineShowWarning(JobPlanningLine) then
        Rollback := ShowAndHandleAvailabilityPage(JobPlanningLine.RECORDID);
    END;

    [External]
    PROCEDURE AssemblyLineCheck@6(AssemblyLine@1000 : Record "Assembly Line") Rollback : Boolean;
    BEGIN
      NotificationLifecycleMgt.RecallNotificationsForRecordWithAdditionalContext(
        AssemblyLine.RECORDID,GetItemAvailabilityNotificationId,true);
      if AsmOrderLineShowWarning(AssemblyLine) then
        Rollback := ShowAndHandleAvailabilityPage(AssemblyLine.RECORDID);
    END;

    [External]
    PROCEDURE ShowAsmWarningYesNo@22(VAR AsmHeader@1003 : Record "Assembly Header";VAR AsmLine@1002 : Record "Assembly Line") Rollback : Boolean;
    VAR
      AsmLineMgt@1000 : Codeunit "Assembly Line Management";
    BEGIN
      Rollback := AsmLineMgt.ShowAvailability(false,AsmHeader,AsmLine);
    END;

    [External]
    PROCEDURE ItemJnlLineShowWarning@15(ItemJnlLine@1000 : Record "Item Journal Line") : Boolean;
    BEGIN
      if not ShowWarningForThisItem(ItemJnlLine."Item No.") then
        exit(false);

      case ItemJnlLine."Entry Type" of
        ItemJnlLine."Entry Type"::Purchase,ItemJnlLine."Entry Type"::"Positive Adjmt.":
          ItemNetChange := ItemJnlLine.Quantity;
        ItemJnlLine."Entry Type"::Sale,ItemJnlLine."Entry Type"::"Negative Adjmt.",ItemJnlLine."Entry Type"::Transfer:
          ItemNetChange := -ItemJnlLine.Quantity;
      end;
      exit(
        ShowWarning(
          ItemJnlLine."Item No.",
          ItemJnlLine."Variant Code",
          ItemJnlLine."Location Code",
          ItemJnlLine."Unit of Measure Code",
          ItemJnlLine."Qty. per Unit of Measure",
          ItemNetChange,
          0,
          0D,
          0D));
    END;

    [External]
    PROCEDURE SalesLineShowWarning@14(SalesLine@1000 : Record "Sales Line") : Boolean;
    VAR
      OldSalesLine@1002 : Record "Sales Line";
      CompanyInfo@1004 : Record "Company Information";
      LookAheadDate@1005 : Date;
    BEGIN
      if SalesLine."Drop Shipment" then
        exit(false);
      if SalesLine.IsServiceItem then
        exit(false);
      if SalesLine.FullQtyIsForAsmToOrder then
        exit(false);
      if not ShowWarningForThisItem(SalesLine."No.") then
        exit(false);

      CLEAR(AvailableToPromise);

      OldItemNetChange := 0;
      OldSalesLine := SalesLine;
      if OldSalesLine.FIND then begin // Find previous quantity within Check-Avail. Period
        CompanyInfo.GET;
        LookAheadDate :=
          AvailableToPromise.AdjustedEndingDate(
            CALCDATE(CompanyInfo."Check-Avail. Period Calc.",SalesLine."Shipment Date"),
            CompanyInfo."Check-Avail. Time Bucket");
        if (OldSalesLine."Document Type" = OldSalesLine."Document Type"::Order) and
           (OldSalesLine."No." = SalesLine."No.") and
           (OldSalesLine."Variant Code" = SalesLine."Variant Code") and
           (OldSalesLine."Location Code" = SalesLine."Location Code") and
           (OldSalesLine."Bin Code" = SalesLine."Bin Code") and
           not OldSalesLine."Drop Shipment" and
           (OldSalesLine."Shipment Date" <= LookAheadDate)
        then
          if OldSalesLine."Shipment Date" > SalesLine."Shipment Date" then
            AvailableToPromise.SetChangedSalesLine(OldSalesLine)
          else begin
            OldItemNetChange := -OldSalesLine."Outstanding Qty. (Base)";
            OldSalesLine.CALCFIELDS("Reserved Qty. (Base)");
            OldItemNetResChange := -OldSalesLine."Reserved Qty. (Base)";
          end;
      end;

      NewItemNetResChange := -(SalesLine."Qty. to Asm. to Order (Base)" - OldSalesLine.QtyBaseOnATO);

      if SalesLine."Document Type" = SalesLine."Document Type"::Order then
        UseOrderPromise := true;
      exit(
        ShowWarning(
          SalesLine."No.",
          SalesLine."Variant Code",
          SalesLine."Location Code",
          SalesLine."Unit of Measure Code",
          SalesLine."Qty. per Unit of Measure",
          -SalesLine."Outstanding Quantity",
          OldItemNetChange,
          SalesLine."Shipment Date",
          OldSalesLine."Shipment Date"));
    END;

    LOCAL PROCEDURE ShowWarning@13(ItemNoArg@1000 : Code[20];ItemVariantCodeArg@1001 : Code[10];ItemLocationCodeArg@1002 : Code[10];UnitOfMeasureCodeArg@1004 : Code[10];QtyPerUnitOfMeasureArg@1005 : Decimal;NewItemNetChangeArg@1006 : Decimal;OldItemNetChangeArg@1007 : Decimal;ShipmentDateArg@1008 : Date;OldShipmentDateArg@1009 : Date) : Boolean;
    VAR
      Item@1003 : Record Item;
    BEGIN
      ItemNo := ItemNoArg;
      UnitOfMeasureCode := UnitOfMeasureCodeArg;
      QtyPerUnitOfMeasure := QtyPerUnitOfMeasureArg;
      NewItemNetChange := NewItemNetChangeArg;
      OldItemNetChange := ConvertQty(OldItemNetChangeArg);
      OldItemShipmentDate := OldShipmentDateArg;
      ItemLocationCode := ItemLocationCodeArg;

      if NewItemNetChange >= 0 then
        exit(false);

      SetFilterOnItem(Item,ItemNo,ItemVariantCodeArg,ItemLocationCode,ShipmentDateArg);
      Calculate(Item);
      exit(InitialQtyAvailable + ItemNetChange - OldItemNetResChange < 0);
    END;

    LOCAL PROCEDURE SetFilterOnItem@28(VAR Item@1000 : Record Item;ItemNo@1003 : Code[20];ItemVariantCode@1002 : Code[10];ItemLocationCode@1001 : Code[10];ShipmentDate@1004 : Date);
    BEGIN
      Item.GET(ItemNo);
      Item.SETRANGE("No.",ItemNo);
      Item.SETRANGE("Variant Filter",ItemVariantCode);
      Item.SETRANGE("Location Filter",ItemLocationCode);
      Item.SETRANGE("Drop Shipment Filter",false);

      if UseOrderPromise then
        Item.SETRANGE("Date Filter",0D,ShipmentDate)
      else
        Item.SETRANGE("Date Filter",0D,WORKDATE);
    END;

    LOCAL PROCEDURE Calculate@12(VAR Item@1002 : Record Item);
    VAR
      CompanyInfo@1000 : Record "Company Information";
    BEGIN
      CompanyInfo.GET;
      QtyAvailToPromise(Item,CompanyInfo);
      EarliestAvailDate := EarliestAvailabilityDate(Item,CompanyInfo);

      if not UseOrderPromise then
        SchedRcpt := 0;

      OldItemNetResChange := ConvertQty(OldItemNetResChange);
      NewItemNetResChange := ConvertQty(NewItemNetResChange);

      ItemNetChange := 0;
      if Item."No." = ItemNo then begin
        ItemNetChange := NewItemNetChange;
        if GrossReq + OldItemNetChange >= 0 then
          GrossReq := GrossReq + OldItemNetChange;
      end;

      InitialQtyAvailable :=
        InventoryQty +
        (SchedRcpt - ReservedRcpt) - (GrossReq - ReservedReq) -
        NewItemNetResChange;
    END;

    LOCAL PROCEDURE QtyAvailToPromise@17(VAR Item@1000 : Record Item;CompanyInfo@1003 : Record "Company Information");
    BEGIN
      AvailableToPromise.QtyAvailabletoPromise(
        Item,GrossReq,SchedRcpt,Item.GETRANGEMAX("Date Filter"),
        CompanyInfo."Check-Avail. Time Bucket",CompanyInfo."Check-Avail. Period Calc.");
      InventoryQty := ConvertQty(AvailableToPromise.CalcAvailableInventory(Item));
      GrossReq := ConvertQty(GrossReq);
      ReservedReq := ConvertQty(AvailableToPromise.CalcReservedRequirement(Item) + OldItemNetResChange);
      SchedRcpt := ConvertQty(SchedRcpt);
      ReservedRcpt := ConvertQty(AvailableToPromise.CalcReservedReceipt(Item));
    END;

    LOCAL PROCEDURE EarliestAvailabilityDate@19(VAR Item@1000 : Record Item;CompanyInfo@1002 : Record "Company Information") : Date;
    VAR
      AvailableQty@1003 : Decimal;
      NewItemNetChangeBase@1001 : Decimal;
      OldItemNetChangeBase@1004 : Decimal;
    BEGIN
      NewItemNetChangeBase := ConvertQtyToBaseQty(NewItemNetChange);
      OldItemNetChangeBase := ConvertQtyToBaseQty(OldItemNetChange);
      exit(
        AvailableToPromise.EarliestAvailabilityDate(
          Item,-NewItemNetChangeBase,Item.GETRANGEMAX("Date Filter"),-OldItemNetChangeBase,OldItemShipmentDate,AvailableQty,
          CompanyInfo."Check-Avail. Time Bucket",CompanyInfo."Check-Avail. Period Calc."));
    END;

    LOCAL PROCEDURE ConvertQty@31(Qty@1000 : Decimal) : Decimal;
    BEGIN
      if QtyPerUnitOfMeasure = 0 then
        QtyPerUnitOfMeasure := 1;
      exit(ROUND(Qty / QtyPerUnitOfMeasure,0.00001));
    END;

    LOCAL PROCEDURE ConvertQtyToBaseQty@26(Qty@1000 : Decimal) : Decimal;
    BEGIN
      if QtyPerUnitOfMeasure = 0 then
        QtyPerUnitOfMeasure := 1;
      exit(ROUND(Qty * QtyPerUnitOfMeasure,0.00001));
    END;

    [External]
    PROCEDURE TransferLineShowWarning@11(TransLine@1000 : Record "Transfer Line") : Boolean;
    VAR
      OldTransLine@1002 : Record "Transfer Line";
    BEGIN
      if not ShowWarningForThisItem(TransLine."Item No.") then
        exit(false);

      UseOrderPromise := true;

      OldTransLine := TransLine;
      if OldTransLine.FIND then // Find previous quantity
        if (OldTransLine."Item No." = TransLine."Item No.") and
           (OldTransLine."Variant Code" = TransLine."Variant Code") and
           (OldTransLine."Transfer-from Code" = TransLine."Transfer-from Code")
        then begin
          OldItemNetChange := -OldTransLine."Outstanding Qty. (Base)";
          OldTransLine.CALCFIELDS("Reserved Qty. Outbnd. (Base)");
          OldItemNetResChange := -OldTransLine."Reserved Qty. Outbnd. (Base)";
        end;

      exit(
        ShowWarning(
          TransLine."Item No.",
          TransLine."Variant Code",
          TransLine."Transfer-from Code",
          TransLine."Unit of Measure Code",
          TransLine."Qty. per Unit of Measure",
          -TransLine."Outstanding Quantity",
          OldItemNetChange,
          TransLine."Shipment Date",
          OldTransLine."Shipment Date"));
    END;

    [External]
    PROCEDURE ServiceInvLineShowWarning@10(ServLine@1000 : Record "Service Line") : Boolean;
    VAR
      OldServLine@1002 : Record "Service Line";
    BEGIN
      if not ShowWarningForThisItem(ServLine."No.") then
        exit(false);

      OldItemNetChange := 0;

      OldServLine := ServLine;

      if OldServLine.FIND then // Find previous quantity
        if (OldServLine."Document Type" = OldServLine."Document Type"::Order) and
           (OldServLine."No." = ServLine."No.") and
           (OldServLine."Variant Code" = ServLine."Variant Code") and
           (OldServLine."Location Code" = ServLine."Location Code") and
           (OldServLine."Bin Code" = ServLine."Bin Code")
        then begin
          OldItemNetChange := -OldServLine."Outstanding Qty. (Base)";
          OldServLine.CALCFIELDS("Reserved Qty. (Base)");
          OldItemNetResChange := -OldServLine."Reserved Qty. (Base)";
        end;

      UseOrderPromise := true;
      exit(
        ShowWarning(
          ServLine."No.",
          ServLine."Variant Code",
          ServLine."Location Code",
          ServLine."Unit of Measure Code",
          ServLine."Qty. per Unit of Measure",
          -ServLine."Outstanding Quantity",
          OldItemNetChange,
          ServLine."Needed by Date",
          OldServLine."Needed by Date"));
    END;

    [External]
    PROCEDURE JobPlanningLineShowWarning@8(JobPlanningLine@1000 : Record "Job Planning Line") : Boolean;
    VAR
      OldJobPlanningLine@1002 : Record "Job Planning Line";
    BEGIN
      if not ShowWarningForThisItem(JobPlanningLine."No.") then
        exit(false);

      OldItemNetChange := 0;

      OldJobPlanningLine := JobPlanningLine;

      if OldJobPlanningLine.FIND then // Find previous quantity
        if (OldJobPlanningLine.Type = OldJobPlanningLine.Type::Item) and
           (OldJobPlanningLine."No." = JobPlanningLine."No.") and
           (OldJobPlanningLine."Variant Code" = JobPlanningLine."Variant Code") and
           (OldJobPlanningLine."Location Code" = JobPlanningLine."Location Code") and
           (OldJobPlanningLine."Bin Code" = JobPlanningLine."Bin Code")
        then begin
          OldItemNetChange := -OldJobPlanningLine."Quantity (Base)";
          OldJobPlanningLine.CALCFIELDS("Reserved Qty. (Base)");
          OldItemNetResChange := -OldJobPlanningLine."Reserved Qty. (Base)";
        end;

      UseOrderPromise := true;
      exit(
        ShowWarning(
          JobPlanningLine."No.",
          JobPlanningLine."Variant Code",
          JobPlanningLine."Location Code",
          JobPlanningLine."Unit of Measure Code",
          JobPlanningLine."Qty. per Unit of Measure",
          -JobPlanningLine."Remaining Qty.",
          OldItemNetChange,
          JobPlanningLine."Planning Date",
          OldJobPlanningLine."Planning Date"));
    END;

    [External]
    PROCEDURE AsmOrderLineShowWarning@9(AssemblyLine@1000 : Record "Assembly Line") : Boolean;
    VAR
      OldAssemblyLine@1001 : Record "Assembly Line";
    BEGIN
      OldItemNetChange := 0;

      OldAssemblyLine := AssemblyLine;

      if OldAssemblyLine.FIND then // Find previous quantity
        if (OldAssemblyLine."Document Type" = OldAssemblyLine."Document Type"::Order) and
           (OldAssemblyLine.Type = OldAssemblyLine.Type::Item) and
           (OldAssemblyLine."No." = AssemblyLine."No.") and
           (OldAssemblyLine."Variant Code" = AssemblyLine."Variant Code") and
           (OldAssemblyLine."Location Code" = AssemblyLine."Location Code") and
           (OldAssemblyLine."Bin Code" = AssemblyLine."Bin Code")
        then begin
          OldItemNetChange := -OldAssemblyLine."Remaining Quantity (Base)";
          OldAssemblyLine.CALCFIELDS("Reserved Qty. (Base)");
          OldItemNetResChange := -OldAssemblyLine."Reserved Qty. (Base)";
        end;

      UseOrderPromise := true;
      exit(
        ShowWarning(
          AssemblyLine."No.",
          AssemblyLine."Variant Code",
          AssemblyLine."Location Code",
          AssemblyLine."Unit of Measure Code",
          AssemblyLine."Qty. per Unit of Measure",
          -AssemblyLine."Remaining Quantity",
          OldItemNetChange,
          AssemblyLine."Due Date",
          OldAssemblyLine."Due Date"));
    END;

    [External]
    PROCEDURE AsmOrderCalculate@18(AssemblyHeader@1000 : Record "Assembly Header";VAR InventoryQty2@1006 : Decimal;VAR GrossReq2@1005 : Decimal;VAR ReservedReq2@1007 : Decimal;VAR SchedRcpt2@1004 : Decimal;VAR ReservedRcpt2@1008 : Decimal);
    VAR
      OldAssemblyHeader@1001 : Record "Assembly Header";
      Item@1002 : Record Item;
      CompanyInfo@1003 : Record "Company Information";
    BEGIN
      with AssemblyHeader do begin
        UseOrderPromise := true;

        if "Due Date" = 0D then
          "Due Date" := WORKDATE;
        SetFilterOnItem(Item,"Item No.","Variant Code","Location Code","Due Date");
        CompanyInfo.GET;
        QtyAvailToPromise(Item,CompanyInfo);

        OldAssemblyHeader := AssemblyHeader;
        if OldAssemblyHeader.FIND then // Find previous quantity
          if (OldAssemblyHeader."Document Type" = OldAssemblyHeader."Document Type"::Order) and
             (OldAssemblyHeader."No." = "No.") and
             (OldAssemblyHeader."Item No." = "Item No.") and
             (OldAssemblyHeader."Variant Code" = "Variant Code") and
             (OldAssemblyHeader."Location Code" = "Location Code") and
             (OldAssemblyHeader."Bin Code" = "Bin Code")
          then begin
            OldAssemblyHeader.CALCFIELDS("Reserved Qty. (Base)");
            SchedRcpt :=
              SchedRcpt - ConvertQty(OldAssemblyHeader."Remaining Quantity (Base)" - OldAssemblyHeader."Reserved Qty. (Base)");
          end;
      end;
      FetchCalculation2(InventoryQty2,GrossReq2,ReservedReq2,SchedRcpt2,ReservedRcpt2);
    END;

    [External]
    PROCEDURE FetchCalculation@16(VAR ItemNo2@1007 : Code[20];VAR UnitOfMeasureCode2@1006 : Code[10];VAR InventoryQty2@1005 : Decimal;VAR GrossReq2@1004 : Decimal;VAR ReservedReq2@1009 : Decimal;VAR SchedRcpt2@1003 : Decimal;VAR ReservedRcpt2@1010 : Decimal;VAR CurrentQuantity2@1002 : Decimal;VAR CurrentReservedQty2@1008 : Decimal;VAR TotalQuantity2@1001 : Decimal;VAR EarliestAvailDate2@1000 : Date);
    BEGIN
      ItemNo2 := ItemNo;
      UnitOfMeasureCode2 := UnitOfMeasureCode;
      FetchCalculation2(InventoryQty2,GrossReq2,ReservedReq2,SchedRcpt2,ReservedRcpt2);
      CurrentQuantity2 := -NewItemNetChange;
      CurrentReservedQty2 := -(NewItemNetResChange + OldItemNetResChange);
      TotalQuantity2 := InitialQtyAvailable + ItemNetChange;
      EarliestAvailDate2 := EarliestAvailDate;
    END;

    LOCAL PROCEDURE FetchCalculation2@25(VAR InventoryQty2@1002 : Decimal;VAR GrossReq2@1001 : Decimal;VAR ReservedReq2@1003 : Decimal;VAR SchedRcpt2@1000 : Decimal;VAR ReservedRcpt2@1004 : Decimal);
    BEGIN
      InventoryQty2 := InventoryQty;
      GrossReq2 := GrossReq;
      ReservedReq2 := ReservedReq;
      SchedRcpt2 := SchedRcpt;
      ReservedRcpt2 := ReservedRcpt;
    END;

    [External]
    PROCEDURE RaiseUpdateInterruptedError@20();
    BEGIN
      ERROR(Text000);
    END;

    LOCAL PROCEDURE ShowAndHandleAvailabilityPage@36(RecordId@1000 : RecordID) Rollback : Boolean;
    VAR
      ItemNo2@1008 : Code[20];
      UnitOfMeasureCode2@1007 : Code[10];
      InventoryQty2@1006 : Decimal;
      GrossReq2@1005 : Decimal;
      ReservedReq2@1010 : Decimal;
      SchedRcpt2@1004 : Decimal;
      ReservedRcpt2@1011 : Decimal;
      CurrentQuantity2@1003 : Decimal;
      CurrentReservedQty2@1009 : Decimal;
      TotalQuantity2@1002 : Decimal;
      EarliestAvailDate2@1001 : Date;
    BEGIN
      if not GUIALLOWED then
        exit(false);

      FetchCalculation(
        ItemNo2,UnitOfMeasureCode2,InventoryQty2,
        GrossReq2,ReservedReq2,SchedRcpt2,ReservedRcpt2,
        CurrentQuantity2,CurrentReservedQty2,TotalQuantity2,EarliestAvailDate2);
      Rollback := CreateAndSendNotification(UnitOfMeasureCode2,InventoryQty2,
          GrossReq2,ReservedReq2,SchedRcpt2,ReservedRcpt2,
          CurrentQuantity2,CurrentReservedQty2,TotalQuantity2,EarliestAvailDate2,RecordId,ItemLocationCode);
    END;

    [External]
    PROCEDURE ShowNotificationDetails@34(AvailabilityCheckNotification@1000 : Notification);
    VAR
      ItemAvailabilityCheck@1001 : Page "Item Availability Check";
    BEGIN
      ItemAvailabilityCheck.InitializeFromNotification(AvailabilityCheckNotification);
      ItemAvailabilityCheck.SetHeading(AvailabilityCheckNotification.MESSAGE);
      ItemAvailabilityCheck.RUNMODAL;
    END;

    LOCAL PROCEDURE CreateAndSendNotification@30(UnitOfMeasureCode@1009 : Code[20];InventoryQty@1008 : Decimal;GrossReq@1007 : Decimal;ReservedReq@1006 : Decimal;SchedRcpt@1005 : Decimal;ReservedRcpt@1004 : Decimal;CurrentQuantity@1003 : Decimal;CurrentReservedQty@1002 : Decimal;TotalQuantity@1001 : Decimal;EarliestAvailDate@1000 : Date;RecordId@1014 : RecordID;LocationCode@1012 : Code[10]) : Boolean;
    VAR
      ItemAvailabilityCheck@1011 : Page "Item Availability Check";
      AvailabilityCheckNotification@1010 : Notification;
    BEGIN
      AvailabilityCheckNotification.ID(CREATEGUID);
      AvailabilityCheckNotification.MESSAGE(STRSUBSTNO(NotificationMsg,ItemNo));
      AvailabilityCheckNotification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
      AvailabilityCheckNotification.ADDACTION(DetailsTxt,CODEUNIT::"Item-Check Avail.",'ShowNotificationDetails');
      ItemAvailabilityCheck.PopulateDataOnNotification(AvailabilityCheckNotification,ItemNo,UnitOfMeasureCode,
        InventoryQty,GrossReq,ReservedReq,SchedRcpt,ReservedRcpt,CurrentQuantity,CurrentReservedQty,
        TotalQuantity,EarliestAvailDate,LocationCode);
      NotificationLifecycleMgt.SendNotificationWithAdditionalContext(
        AvailabilityCheckNotification,RecordId,GetItemAvailabilityNotificationId);
      exit(false);
    END;

    [External]
    PROCEDURE ShowWarningForThisItem@7(ItemNo@1000 : Code[20]) : Boolean;
    VAR
      Item@1001 : Record Item;
      SalesSetup@1002 : Record "Sales & Receivables Setup";
    BEGIN
      if not Item.GET(ItemNo) then
        exit(false);

      if Item.Type = Item.Type::Service then
        exit(false);

      if not IsItemAvailabilityNotificationEnabled(Item) then
        exit(false);

      case Item."Stockout Warning" of
        Item."Stockout Warning"::No:
          exit(false);
        Item."Stockout Warning"::Yes:
          exit(true);
        Item."Stockout Warning"::Default:
          begin
            SalesSetup.GET;
            if SalesSetup."Stockout Warning" then
              exit(true);
            exit(false);
          end;
      end;
    END;

    [External]
    PROCEDURE GetItemAvailabilityNotificationId@27() : GUID;
    BEGIN
      exit('2712AD06-C48B-4C20-820E-347A60C9AD00');
    END;

    LOCAL PROCEDURE IsItemAvailabilityNotificationEnabled@24(Item@1000 : Record Item) : Boolean;
    VAR
      MyNotifications@1001 : Record "My Notifications";
    BEGIN
      exit(MyNotifications.IsEnabledForRecord(GetItemAvailabilityNotificationId,Item));
    END;

    [EventSubscriber(Page,1518,OnInitializingNotificationWithDefaultState)]
    LOCAL PROCEDURE OnInitializingNotificationWithDefaultState@29();
    VAR
      MyNotifications@1000 : Record "My Notifications";
    BEGIN
      MyNotifications.InsertDefaultWithTableNum(GetItemAvailabilityNotificationId,
        ItemAvailabilityNotificationTxt,
        ItemAvailabilityNotificationDescriptionTxt,
        DATABASE::Item);
    END;

    BEGIN
    END.
  }
}

