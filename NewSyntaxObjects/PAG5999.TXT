OBJECT Page 5999 Available - Service Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Service Line"=rm;
    Editable=false;
    CaptionML=ENU=Available - Service Lines;
    InsertAllowed=false;
    DeleteAllowed=false;
    ModifyAllowed=false;
    SourceTable="Service Line";
    DataCaptionExpr=CaptionText;
    SourceTableView=SORTING(Type,"No.","Variant Code","Location Code","Needed by Date","Document Type","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    PageType=List;
    OnOpenPage=BEGIN
                 ReservEntry.TESTFIELD("Source Type");

                 SETRANGE(Type,Type::Item);
                 SETRANGE("No.",ReservEntry."Item No.");
                 SETRANGE("Variant Code",ReservEntry."Variant Code");
                 SETRANGE("Location Code",ReservEntry."Location Code");

                 SETFILTER("Needed by Date",ReservMgt.GetAvailabilityFilter(ReservEntry."Shipment Date"));

                 case CurrentSubType of
                   0,1,2,4:
                     if ReservMgt.IsPositive then
                       SETFILTER("Quantity (Base)",'<0')
                     else
                       SETFILTER("Quantity (Base)",'>0');
                   3:
                     if not ReservMgt.IsPositive then
                       SETFILTER("Quantity (Base)",'<0')
                     else
                       SETFILTER("Quantity (Base)",'>0');
                 end;
               END;

    OnAfterGetRecord=BEGIN
                       ReservMgt.ServiceInvLineUpdateValues(Rec,QtyToReserve,QtyToReserveBase,QtyReservedThisLine,QtyReservedThisLineBase);
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      Name=ActionContainer1900000004;
                      ActionContainerType=ActionItems }
      { 21      ;1   ;ActionGroup;
                      Name=F&unctions;
                      CaptionML=ENU=F&unctions;
                      Image=Action }
      { 22      ;2   ;Action    ;
                      Name=Reserve;
                      CaptionML=ENU=&Reserve;
                      ToolTipML=ENU=Reserve the quantity that is required on the document line that you opened this window for.;
                      ApplicationArea=#Advanced;
                      Image=Reserve;
                      OnAction=BEGIN
                                 ReservEntry.LOCKTABLE;
                                 UpdateReservMgt;
                                 ReservMgt.ServiceInvLineUpdateValues(Rec,QtyToReserve,QtyToReserveBase,QtyReservedThisLine,QtyReservedThisLineBase);
                                 ReservMgt.CalculateRemainingQty(NewQtyReservedThisLine,NewQtyReservedThisLineBase);
                                 ReservMgt.CopySign(NewQtyReservedThisLine,QtyToReserve);
                                 ReservMgt.CopySign(NewQtyReservedThisLineBase,QtyToReserveBase);
                                 if NewQtyReservedThisLineBase <> 0 then
                                   if ABS(NewQtyReservedThisLineBase) > ABS(QtyToReserveBase) then
                                     CreateReservation(QtyToReserve,QtyToReserveBase)
                                   else
                                     CreateReservation(NewQtyReservedThisLine,NewQtyReservedThisLineBase)
                                 else
                                   ERROR(Text000);
                               END;
                                }
      { 23      ;2   ;Action    ;
                      Name=CancelReservation;
                      AccessByPermission=TableData Item=R;
                      CaptionML=ENU=&Cancel Reservation;
                      ToolTipML=ENU=Cancel the reservation that exists for the document line that you opened this window for.;
                      ApplicationArea=#Advanced;
                      Image=Cancel;
                      OnAction=BEGIN
                                 if not CONFIRM(Text001,false) then
                                   exit;

                                 ReservEntry2.COPY(ReservEntry);
                                 ReserveServiceInvLine.FilterReservFor(ReservEntry2,Rec);

                                 if ReservEntry2.FIND('-') then begin
                                   UpdateReservMgt;
                                   repeat
                                     ReservEngineMgt.CancelReservation(ReservEntry2);
                                   until ReservEntry2.NEXT = 0;

                                   UpdateReservFrom;
                                 end;
                               END;
                                }
      { 24      ;2   ;Action    ;
                      Name=ShowDocument;
                      ShortCutKey=Shift+F7;
                      CaptionML=ENU=&Show Document;
                      ToolTipML=ENU=Open the document that the information on the line comes from.;
                      ApplicationArea=#Service;
                      Image=View;
                      OnAction=BEGIN
                                 ServHeader.GET("Document Type","Document No.");
                                 PAGE.RUN(PAGE::"Service Order",ServHeader);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                Name=Document No.;
                ToolTipML=ENU=Specifies the service order number associated with this line.;
                ApplicationArea=#Service;
                SourceExpr="Document No.";
                ImplicitType=Code20 }

    { 6   ;2   ;Field     ;
                Name=Location Code;
                ToolTipML=ENU=Specifies the inventory location from where the items on the line should be taken and where they should be registered.;
                ApplicationArea=#Location;
                SourceExpr="Location Code";
                ImplicitType=Code10 }

    { 8   ;2   ;Field     ;
                Name=Needed by Date;
                ToolTipML=ENU=Specifies the date when you require the item to be available for a service order.;
                ApplicationArea=#Service;
                SourceExpr="Needed by Date";
                ImplicitType=Date }

    { 10  ;2   ;Field     ;
                Name=Outstanding Qty. (Base);
                ToolTipML=ENU=Specifies the quantity of registered items, resource time, costs, or payments to the general ledger account that have not been shipped.;
                ApplicationArea=#Service;
                SourceExpr="Outstanding Qty. (Base)";
                ImplicitType=Decimal }

    { 12  ;2   ;Field     ;
                Name=Reserved Qty. (Base);
                ToolTipML=ENU=Specifies the quantity of registered items that have been reserved.;
                ApplicationArea=#Service;
                SourceExpr="Reserved Qty. (Base)";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 17  ;2   ;Field     ;
                Name=QtyToReserveBase;
                CaptionML=ENU=Available Quantity;
                ToolTipML=ENU=Specifies the quantity of the item that is available.;
                ApplicationArea=#Service;
                DecimalPlaces=0:5;
                SourceExpr=QtyToReserveBase;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 19  ;2   ;Field     ;
                Name=ReservedThisLine;
                CaptionML=ENU=Current Reserved Quantity;
                ToolTipML=ENU=Specifies the quantity of the item that is reserved for the document type.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=ReservedThisLine;
                OnDrillDown=BEGIN
                              ReservEntry2.RESET;
                              ReserveServiceInvLine.FilterReservFor(ReservEntry2,Rec);
                              ReservEntry2.SETRANGE("Reservation Status",ReservEntry2."Reservation Status"::Reservation);
                              ReservMgt.MarkReservConnection(ReservEntry2,ReservEntry);
                              PAGE.RUNMODAL(PAGE::"Reservation Entries",ReservEntry2);
                              UpdateReservFrom;
                              CurrPage.UPDATE;
                            END;
                             }

    { 1900000007;0;Container;
                Name=Container1900000007;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Name=Part1900383207;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Name=Part1905767507;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Fully reserved.';
      Text001@1001 : TextConst 'ENU=Do you want to cancel the reservation?';
      Text002@1002 : TextConst 'ENU=The available quantity is %1.';
      ReservEntry@1003 : Record "Reservation Entry";
      ReservEntry2@1004 : Record "Reservation Entry";
      ServHeader@1005 : Record "Service Header";
      SalesLine@1006 : Record "Sales Line";
      PurchLine@1007 : Record "Purchase Line";
      ReqLine@1009 : Record "Requisition Line";
      ProdOrderLine@1010 : Record "Prod. Order Line";
      ProdOrderComp@1011 : Record "Prod. Order Component";
      PlanningComponent@1012 : Record "Planning Component";
      TransLine@1013 : Record "Transfer Line";
      ServInvLine@1014 : Record "Service Line";
      JobPlanningLine@1030 : Record "Job Planning Line";
      AssemblyLine@1036 : Record "Assembly Line";
      AssemblyHeader@1035 : Record "Assembly Header";
      AssemblyLineReserve@1034 : Codeunit "Assembly Line-Reserve";
      AssemblyHeaderReserve@1033 : Codeunit "Assembly Header-Reserve";
      ReservMgt@1015 : Codeunit "Reservation Management";
      ReservEngineMgt@1016 : Codeunit "Reservation Engine Mgt.";
      ReserveSalesLine@1017 : Codeunit "Sales Line-Reserve";
      ReserveReqLine@1018 : Codeunit "Req. Line-Reserve";
      ReservePurchLine@1019 : Codeunit "Purch. Line-Reserve";
      ReserveProdOrderLine@1021 : Codeunit "Prod. Order Line-Reserve";
      ReserveProdOrderComp@1022 : Codeunit "Prod. Order Comp.-Reserve";
      ReservePlanningComponent@1023 : Codeunit "Plng. Component-Reserve";
      ReserveTransLine@1024 : Codeunit "Transfer Line-Reserve";
      ReserveServiceInvLine@1025 : Codeunit "Service Line-Reserve";
      JobPlanningLineReserve@1031 : Codeunit "Job Planning Line-Reserve";
      QtyToReserve@1026 : Decimal;
      QtyToReserveBase@1008 : Decimal;
      QtyReservedThisLine@1027 : Decimal;
      QtyReservedThisLineBase@1020 : Decimal;
      NewQtyReservedThisLine@1028 : Decimal;
      NewQtyReservedThisLineBase@1037 : Decimal;
      CaptionText@1029 : Text[80];
      CurrentSubType@1032 : Option;

    [External]
    PROCEDURE SetSalesLine@1(VAR CurrentSalesLine@1000 : Record "Sales Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentSalesLine.TESTFIELD(Type,CurrentSalesLine.Type::Item);
      SalesLine := CurrentSalesLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetSalesLine(SalesLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveSalesLine.FilterReservFor(ReservEntry,SalesLine);
      CaptionText := ReserveSalesLine.Caption(SalesLine);
      CurrentSubType := CurrentSubType;
    END;

    [External]
    PROCEDURE SetReqLine@2(VAR CurrentReqLine@1000 : Record "Requisition Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      ReqLine := CurrentReqLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetReqLine(ReqLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveReqLine.FilterReservFor(ReservEntry,ReqLine);
      CaptionText := ReserveReqLine.Caption(ReqLine);
    END;

    [External]
    PROCEDURE SetPurchLine@3(VAR CurrentPurchLine@1000 : Record "Purchase Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentPurchLine.TESTFIELD(Type,CurrentPurchLine.Type::Item);
      PurchLine := CurrentPurchLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetPurchLine(PurchLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReservePurchLine.FilterReservFor(ReservEntry,PurchLine);
      CaptionText := ReservePurchLine.Caption(PurchLine);
    END;

    [External]
    PROCEDURE SetProdOrderLine@16(VAR CurrentProdOrderLine@1000 : Record "Prod. Order Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      ProdOrderLine := CurrentProdOrderLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetProdOrderLine(ProdOrderLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveProdOrderLine.FilterReservFor(ReservEntry,ProdOrderLine);
      CaptionText := ReserveProdOrderLine.Caption(ProdOrderLine);
    END;

    [External]
    PROCEDURE SetProdOrderComponent@15(VAR CurrentProdOrderComp@1000 : Record "Prod. Order Component";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      ProdOrderComp := CurrentProdOrderComp;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetProdOrderComponent(ProdOrderComp);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveProdOrderComp.FilterReservFor(ReservEntry,ProdOrderComp);
      CaptionText := ReserveProdOrderComp.Caption(ProdOrderComp);
    END;

    [External]
    PROCEDURE SetPlanningComponent@13(VAR CurrentPlanningComponent@1000 : Record "Planning Component";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      PlanningComponent := CurrentPlanningComponent;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetPlanningComponent(PlanningComponent);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReservePlanningComponent.FilterReservFor(ReservEntry,PlanningComponent);
      CaptionText := ReservePlanningComponent.Caption(PlanningComponent);
    END;

    [External]
    PROCEDURE SetTransferLine@9(VAR CurrentTransLine@1000 : Record "Transfer Line";CurrentReservEntry@1001 : Record "Reservation Entry";Direction@1002 : 'Outbound,Inbound');
    BEGIN
      TransLine := CurrentTransLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetTransferLine(TransLine,Direction);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveTransLine.FilterReservFor(ReservEntry,TransLine,Direction);
      CaptionText := ReserveTransLine.Caption(TransLine);
    END;

    [External]
    PROCEDURE SetServInvLine@8(VAR CurrentServInvLine@1000 : Record "Service Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentServInvLine.TESTFIELD(Type,CurrentServInvLine.Type::Item);
      ServInvLine := CurrentServInvLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetServLine(ServInvLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveServiceInvLine.FilterReservFor(ReservEntry,ServInvLine);
      CaptionText := ReserveServiceInvLine.Caption(ServInvLine);
    END;

    [External]
    PROCEDURE SetJobPlanningLine@10(VAR CurrentJobPlanningLine@1000 : Record "Job Planning Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentJobPlanningLine.TESTFIELD(Type,CurrentJobPlanningLine.Type::Item);
      JobPlanningLine := CurrentJobPlanningLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetJobPlanningLine(JobPlanningLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      JobPlanningLineReserve.FilterReservFor(ReservEntry,JobPlanningLine);
      CaptionText := JobPlanningLineReserve.Caption(JobPlanningLine);
    END;

    LOCAL PROCEDURE CreateReservation@6(ReserveQuantity@1001 : Decimal;ReserveQuantityBase@1000 : Decimal);
    VAR
      TrackingSpecification@1002 : Record "Tracking Specification";
    BEGIN
      if ABS("Outstanding Qty. (Base)") + "Reserved Qty. (Base)" < ReserveQuantityBase then
        ERROR(Text002,ABS("Outstanding Qty. (Base)") + "Reserved Qty. (Base)");

      TESTFIELD(Type,Type::Item);
      TESTFIELD("No.",ReservEntry."Item No.");
      TESTFIELD("Variant Code",ReservEntry."Variant Code");
      TESTFIELD("Location Code",ReservEntry."Location Code");

      TrackingSpecification.InitTrackingSpecification2(
        DATABASE::"Service Line","Document Type","Document No.",'',0,"Line No.",
        "Variant Code","Location Code","Qty. per Unit of Measure");
      ReservMgt.CreateReservation(
        ReservEntry.Description,"Posting Date",ReserveQuantity,ReserveQuantityBase,TrackingSpecification);
      UpdateReservFrom;
    END;

    LOCAL PROCEDURE UpdateReservFrom@17();
    BEGIN
      case ReservEntry."Source Type" of
        DATABASE::"Sales Line":
          begin
            SalesLine.FIND;
            SetSalesLine(SalesLine,ReservEntry);
          end;
        DATABASE::"Requisition Line":
          begin
            ReqLine.FIND;
            SetReqLine(ReqLine,ReservEntry);
          end;
        DATABASE::"Purchase Line":
          begin
            PurchLine.FIND;
            SetPurchLine(PurchLine,ReservEntry);
          end;
        DATABASE::"Prod. Order Line":
          begin
            ProdOrderLine.FIND;
            SetProdOrderLine(ProdOrderLine,ReservEntry);
          end;
        DATABASE::"Prod. Order Component":
          begin
            ProdOrderComp.FIND;
            SetProdOrderComponent(ProdOrderComp,ReservEntry);
          end;
        DATABASE::"Planning Component":
          begin
            PlanningComponent.FIND;
            SetPlanningComponent(PlanningComponent,ReservEntry);
          end;
        DATABASE::"Transfer Line":
          begin
            TransLine.FIND;
            SetTransferLine(TransLine,ReservEntry,ReservEntry."Source Subtype");
          end;
        DATABASE::"Service Line":
          begin
            ServInvLine.FIND;
            SetServInvLine(ServInvLine,ReservEntry);
          end;
        DATABASE::"Job Planning Line":
          begin
            JobPlanningLine.FIND;
            SetJobPlanningLine(JobPlanningLine,ReservEntry);
          end;
      end;
    END;

    LOCAL PROCEDURE UpdateReservMgt@7();
    BEGIN
      CLEAR(ReservMgt);
      case ReservEntry."Source Type" of
        DATABASE::"Sales Line":
          ReservMgt.SetSalesLine(SalesLine);
        DATABASE::"Requisition Line":
          ReservMgt.SetReqLine(ReqLine);
        DATABASE::"Purchase Line":
          ReservMgt.SetPurchLine(PurchLine);
        DATABASE::"Prod. Order Line":
          ReservMgt.SetProdOrderLine(ProdOrderLine);
        DATABASE::"Prod. Order Component":
          ReservMgt.SetProdOrderComponent(ProdOrderComp);
        DATABASE::"Planning Component":
          ReservMgt.SetPlanningComponent(PlanningComponent);
        DATABASE::"Transfer Line":
          ReservMgt.SetTransferLine(TransLine,ReservEntry."Source Subtype");
        DATABASE::"Service Line":
          ReservMgt.SetServLine(ServInvLine);
        DATABASE::"Assembly Header":
          ReservMgt.SetAssemblyHeader(AssemblyHeader);
      end;
    END;

    LOCAL PROCEDURE ReservedThisLine@5() : Decimal;
    BEGIN
      ReservEntry2.RESET;
      ReserveServiceInvLine.FilterReservFor(ReservEntry2,Rec);
      ReservEntry2.SETRANGE("Reservation Status",ReservEntry2."Reservation Status"::Reservation);
      exit(ReservMgt.MarkReservConnection(ReservEntry2,ReservEntry));
    END;

    [External]
    PROCEDURE SetCurrentSubType@11(SubType@1000 : Option);
    BEGIN
      CurrentSubType := SubType;
    END;

    [External]
    PROCEDURE SetAssemblyLine@14(VAR CurrentAsmLine@1002 : Record "Assembly Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentAsmLine.TESTFIELD(Type,CurrentAsmLine.Type::Item);
      AssemblyLine := CurrentAsmLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetAssemblyLine(AssemblyLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      AssemblyLineReserve.FilterReservFor(ReservEntry,AssemblyLine);
      CaptionText := AssemblyLineReserve.Caption(AssemblyLine);
    END;

    [External]
    PROCEDURE SetAssemblyHeader@12(VAR CurrentAsmHeader@1000 : Record "Assembly Header";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      AssemblyHeader := CurrentAsmHeader;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetAssemblyHeader(AssemblyHeader);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      AssemblyHeaderReserve.FilterReservFor(ReservEntry,AssemblyHeader);
      CaptionText := AssemblyHeaderReserve.Caption(AssemblyHeader);
    END;

    BEGIN
    END.
  }
}

