OBJECT Page 5337 CRM Coupled Fields
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Editable=false;
    CaptionML=ENU=CRM Coupled Fields;
    SourceTable="Coupling Field Buffer";
    PageType=ListPart;
    SourceTableTemporary=true;
    ShowFilter=false;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 5   ;2   ;Field     ;
                Name=Field Name;
                ToolTipML=ENU=Specifies the field's name in Dynamics 365 for Sales.;
                ApplicationArea=#Suite;
                SourceExpr="Field Name";
                ImplicitType=Text50 }

    { 3   ;2   ;Field     ;
                Name=Value;
                ToolTipML=ENU=Specifies the field's value in Dynamics 365.;
                ApplicationArea=#Suite;
                SourceExpr=Value;
                ImplicitType=Text250 }

    { 4   ;2   ;Field     ;
                Name=Integration Value;
                ToolTipML=ENU=Specifies the field's value in Dynamics 365 for Sales.;
                ApplicationArea=#Suite;
                SourceExpr="Integration Value";
                ImplicitType=Text250 }

    { 6   ;2   ;Field     ;
                Name=Direction;
                ToolTipML=ENU=Specifies the direction of data integration.;
                ApplicationArea=#Suite;
                SourceExpr=Direction;
                ImplicitType=Option }

    { 7   ;2   ;Field     ;
                Name=Validate Field;
                ToolTipML=ENU=Specifies whether to validate the field's value in Dynamics 365.;
                ApplicationArea=#Suite;
                SourceExpr="Validate Field";
                ImplicitType=Boolean }

  }
  CODE
  {

    [Internal]
    PROCEDURE SetSourceRecord@1(CouplingRecordBuffer@1000 : Record "Coupling Record Buffer");
    VAR
      IntegrationFieldMapping@1004 : Record "Integration Field Mapping";
      CRMRecordRef@1001 : RecordRef;
      NAVRecordRef@1003 : RecordRef;
      RecordID@1002 : RecordID;
      CRMRecordIsSet@1005 : Boolean;
    BEGIN
      RecordID := CouplingRecordBuffer."NAV Record ID";
      NAVRecordRef := RecordID.GETRECORD;
      NAVRecordRef.FIND;

      CRMRecordIsSet := not ISNULLGUID(CouplingRecordBuffer."CRM ID");
      if CRMRecordIsSet then begin
        CRMRecordRef.OPEN(CouplingRecordBuffer."CRM Table ID");
        FindCRMRecRefByPK(CRMRecordRef,CouplingRecordBuffer."CRM ID");
      end;

      DELETEALL;
      IntegrationFieldMapping.RESET;
      IntegrationFieldMapping.SETRANGE("Integration Table Mapping Name",CouplingRecordBuffer."CRM Table Name");
      if IntegrationFieldMapping.FINDSET then
        repeat
          INIT;
          case IntegrationFieldMapping.Direction of
            IntegrationFieldMapping.Direction::Bidirectional,
            IntegrationFieldMapping.Direction::ToIntegrationTable:
              "Field Name" :=
                GetFieldCaption(CouplingRecordBuffer."CRM Table ID",IntegrationFieldMapping."Integration Table Field No.");
            IntegrationFieldMapping.Direction::FromIntegrationTable:
              "Field Name" :=
                GetFieldCaption(CouplingRecordBuffer."NAV Table ID",IntegrationFieldMapping."Field No.");
          end;
          if IntegrationFieldMapping."Field No." <> 0 then
            Value := GetFieldValue(NAVRecordRef,IntegrationFieldMapping."Field No.");
          if CRMRecordIsSet and (IntegrationFieldMapping."Integration Table Field No." <> 0) then
            "Integration Value" := GetFieldValue(CRMRecordRef,IntegrationFieldMapping."Integration Table Field No.");
          Direction := IntegrationFieldMapping.Direction;
          "Validate Field" := IntegrationFieldMapping."Validate Field";
          INSERT;
        until IntegrationFieldMapping.NEXT = 0;
    END;

    LOCAL PROCEDURE GetFieldCaption@2(TableNo@1000 : Integer;FieldNo@1001 : Integer) FieldCaption : Text[50];
    VAR
      FieldRef@1002 : FieldRef;
      RecRef@1003 : RecordRef;
    BEGIN
      RecRef.OPEN(TableNo);
      FieldRef := RecRef.FIELD(FieldNo);
      FieldCaption := COPYSTR(FieldRef.CAPTION,1,MAXSTRLEN(FieldCaption));
      RecRef.CLOSE;
    END;

    LOCAL PROCEDURE GetFieldValue@3(RecordRef@1000 : RecordRef;FieldNo@1001 : Integer) : Text[250];
    VAR
      FieldRef@1002 : FieldRef;
    BEGIN
      FieldRef := RecordRef.FIELD(FieldNo);
      exit(COPYSTR(FORMAT(FieldRef.VALUE),1,250));
    END;

    LOCAL PROCEDURE FindCRMRecRefByPK@5(VAR RecordRef@1000 : RecordRef;CRMId@1001 : GUID) : Boolean;
    VAR
      FieldRef@1002 : FieldRef;
      PrimaryKeyRef@1003 : KeyRef;
    BEGIN
      PrimaryKeyRef := RecordRef.KEYINDEX(1);
      FieldRef := PrimaryKeyRef.FIELDINDEX(1);
      FieldRef.SETRANGE(CRMId);
      exit(RecordRef.FINDFIRST);
    END;

    BEGIN
    END.
  }
}

