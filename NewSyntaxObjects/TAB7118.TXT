OBJECT Table 7118 Analysis Column
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Analysis Column;
    LookupPageID="Analysis Columns";
    DrillDownPageID="Analysis Columns";
  }
  FIELDS
  {
    { 1   ;   ;Analysis Area       ;Option        ;CaptionML=ENU=Analysis Area;
                                                   OptionCaptionML=ENU=Sales,Purchase,Inventory;
                                                   OptionString=Sales,Purchase,Inventory }
    { 2   ;   ;Analysis Column Template;Code10    ;TableRelation="Analysis Column Template".Name WHERE ("Analysis Area"=FIELD("Analysis Area"));
                                                   CaptionML=ENU=Analysis Column Template }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 4   ;   ;Column No.          ;Code10        ;CaptionML=ENU=Column No. }
    { 5   ;   ;Column Header       ;Text50        ;CaptionML=ENU=Column Header }
    { 6   ;   ;Column Type         ;Option        ;InitValue="Net Change";
                                                   CaptionML=ENU=Column Type;
                                                   OptionCaptionML=ENU=Formula,Net Change,Balance at Date,Beginning Balance,Year to Date,Rest of Fiscal Year,Entire Fiscal Year;
                                                   OptionString=Formula,"Net Change","Balance at Date","Beginning Balance","Year to Date","Rest of Fiscal Year","Entire Fiscal Year" }
    { 7   ;   ;Ledger Entry Type   ;Option        ;CaptionML=ENU=Ledger Entry Type;
                                                   OptionCaptionML=ENU=Item Entries,Item Budget Entries;
                                                   OptionString="Item Entries","Item Budget Entries" }
    { 8   ;   ;Formula             ;Code80        ;OnValidate=BEGIN
                                                                ItemSchedLine.CheckFormula(Formula);
                                                              END;

                                                   CaptionML=ENU=Formula }
    { 9   ;   ;Comparison Date Formula;DateFormula;OnValidate=BEGIN
                                                                if FORMAT("Comparison Date Formula") <> '' then
                                                                  VALIDATE("Comparison Period Formula",'');
                                                              END;

                                                   CaptionML=ENU=Comparison Date Formula }
    { 10  ;   ;Show Opposite Sign  ;Boolean       ;CaptionML=ENU=Show Opposite Sign }
    { 11  ;   ;Show                ;Option        ;InitValue=Always;
                                                   CaptionML=ENU=Show;
                                                   OptionCaptionML=ENU=Always,Never,When Positive,When Negative;
                                                   OptionString=Always,Never,"When Positive","When Negative";
                                                   NotBlank=true }
    { 12  ;   ;Rounding Factor     ;Option        ;CaptionML=ENU=Rounding Factor;
                                                   OptionCaptionML=ENU=None,1,1000,1000000;
                                                   OptionString=None,"1","1000","1000000" }
    { 13  ;   ;Comparison Period Formula;Code20   ;OnValidate=VAR
                                                                Steps@1001 : Integer;
                                                                Type@1000 : '" ",Period,"Fiscal year","Fiscal Halfyear","Fiscal Quarter"';
                                                                RangeFromType@1007 : 'Int,CP,LP';
                                                                RangeToType@1006 : 'Int,CP,LP';
                                                                RangeFromInt@1005 : Integer;
                                                                RangeToInt@1004 : Integer;
                                                              BEGIN
                                                                ParsePeriodFormula(
                                                                  "Comparison Period Formula",
                                                                  Steps,Type,RangeFromType,RangeToType,RangeFromInt,RangeToInt);
                                                                if "Comparison Period Formula" <> '' then
                                                                  CLEAR("Comparison Date Formula");
                                                              END;

                                                   CaptionML=ENU=Comparison Period Formula }
    { 14  ;   ;Analysis Type Code  ;Code10        ;TableRelation="Analysis Type";
                                                   OnValidate=VAR
                                                                ItemAnalysisType@1000 : Record "Analysis Type";
                                                              BEGIN
                                                                if "Analysis Type Code" <> '' then begin
                                                                  ItemAnalysisType.GET("Analysis Type Code");
                                                                  if "Column Header" = '' then
                                                                    "Column Header" := ItemAnalysisType.Name;
                                                                  "Item Ledger Entry Type Filter" := ItemAnalysisType."Item Ledger Entry Type Filter";
                                                                  "Value Entry Type Filter" := ItemAnalysisType."Value Entry Type Filter";
                                                                  "Value Type" := ItemAnalysisType."Value Type";
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Analysis Type Code }
    { 15  ;   ;Item Ledger Entry Type Filter;Text250;
                                                   OnValidate=BEGIN
                                                                AnalysisRepMgmt.ValidateFilter(
                                                                  "Item Ledger Entry Type Filter",DATABASE::"Analysis Column",
                                                                  FIELDNO("Item Ledger Entry Type Filter"),true);
                                                              END;

                                                   CaptionML=ENU=Item Ledger Entry Type Filter }
    { 16  ;   ;Value Entry Type Filter;Text250    ;OnValidate=BEGIN
                                                                AnalysisRepMgmt.ValidateFilter(
                                                                  "Value Entry Type Filter",DATABASE::"Analysis Column",
                                                                  FIELDNO("Value Entry Type Filter"),true);
                                                              END;

                                                   CaptionML=ENU=Value Entry Type Filter }
    { 17  ;   ;Value Type          ;Option        ;CaptionML=ENU=Value Type;
                                                   OptionCaptionML=ENU=" ,Quantity,Sales Amount,Cost Amount,Non-Invntble Amount,Unit Price,Standard Cost,Indirect Cost,Unit Cost";
                                                   OptionString=" ",Quantity,"Sales Amount","Cost Amount","Non-Invntble Amount","Unit Price","Standard Cost","Indirect Cost","Unit Cost" }
    { 18  ;   ;Invoiced            ;Boolean       ;CaptionML=ENU=Invoiced }
  }
  KEYS
  {
    {    ;"Analysis Area","Analysis Column Template","Line No.";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ItemSchedLine@1000 : TEMPORARY Record "Analysis Line";
      Text001@1003 : TextConst 'ENU=%1 is not a valid Period Formula';
      Text002@1002 : TextConst '@@@=Period;ENU=P';
      Text003@1001 : TextConst '@@@=Fiscal year;ENU=FY';
      Text004@1005 : TextConst '@@@=Current Period;ENU=CP';
      Text005@1004 : TextConst '@@@=Last period;ENU=LP';
      AnalysisRepMgmt@1006 : Codeunit "Analysis Report Management";

    [External]
    PROCEDURE ParsePeriodFormula@26(Formula@1002 : Code[20];VAR Steps@1001 : Integer;VAR Type@1006 : '" ",Period,"Fiscal Year"';VAR RangeFromType@1009 : 'Int,CP,LP';VAR RangeToType@1008 : 'Int,CP,LP';VAR RangeFromInt@1003 : Integer;VAR RangeToInt@1000 : Integer);
    VAR
      OriginalFormula@1004 : Code[20];
    BEGIN
      // <PeriodFormula> ::= <signed integer> <formula> | blank
      // <signed integer> ::= <sign> <positive integer> | blank
      // <sign> ::= + | - | blank
      // <positive integer> ::= <digit 1-9> <digits>
      // <digit 1-9> ::= 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
      // <digits> ::= 0 <digits> | <digit 1-9> <digits> | blank
      // <formula> ::= P | FY <range> | FH <range> | FQ <range>
      // <range> ::= blank | [<range2>]
      // <range2> ::= <index> .. <index> | <index>
      // <index> ::= <positive integer> | CP | LP

      OriginalFormula := Formula;
      Formula := DELCHR(Formula);

      if not ParseFormula(Formula,Steps,Type) then
        ERROR(Text001,OriginalFormula);

      if Type = Type::"Fiscal Year" then
        if not ParseRange(Formula,RangeFromType,RangeFromInt,RangeToType,RangeToInt) then
          ERROR(Text001,OriginalFormula);

      if Formula <> '' then
        ERROR(Text001,OriginalFormula);
    END;

    LOCAL PROCEDURE ParseFormula@5(VAR Formula@1002 : Code[20];VAR Steps@1001 : Integer;VAR Type@1006 : '" ",Period,"Fiscal year","Fiscal Halfyear","Fiscal Quarter"') : Boolean;
    BEGIN
      Steps := 0;
      Type := Type::" ";

      if Formula = '' then
        exit(true);

      if not ParseSignedInteger(Formula,Steps) then
        exit(false);

      if Formula = '' then
        exit(false);

      if not ParseType(Formula,Type) then
        exit(false);

      exit(true);
    END;

    LOCAL PROCEDURE ParseSignedInteger@1(VAR Formula@1000 : Code[20];VAR Int@1001 : Integer) : Boolean;
    BEGIN
      Int := 0;

      case COPYSTR(Formula,1,1) of
        '-':
          begin
            Formula := COPYSTR(Formula,2);
            if not ParseInt(Formula,Int,false) then
              exit(false);
            Int := -Int;
          end;
        '+':
          begin
            Formula := COPYSTR(Formula,2);
            if not ParseInt(Formula,Int,false) then
              exit(false);
          end;
        else
          begin
          if not ParseInt(Formula,Int,true) then
            exit(false);
        end;
      end;
      exit(true);
    END;

    LOCAL PROCEDURE ParseInt@2(VAR Formula@1001 : Code[20];VAR Int@1000 : Integer;AllowNotInt@1003 : Boolean) : Boolean;
    VAR
      IntegerStr@1002 : Code[20];
    BEGIN
      if COPYSTR(Formula,1,1) in ['1'..'9'] then
        repeat
          IntegerStr := IntegerStr + COPYSTR(Formula,1,1);
          Formula := COPYSTR(Formula,2);
          if Formula = '' then
            exit(false);
        until not (COPYSTR(Formula,1,1) in ['0'..'9'])
      else
        exit(AllowNotInt);
      EVALUATE(Int,IntegerStr);
      exit(true);
    END;

    LOCAL PROCEDURE ParseType@3(VAR Formula@1001 : Code[20];VAR Type@1000 : '" ",Period,"Fiscal Year"') : Boolean;
    BEGIN
      case ReadToken(Formula) of
        Text002:
          Type := Type::Period;
        Text003:
          Type := Type::"Fiscal Year";
        else
          exit(false);
      end;
      exit(true);
    END;

    LOCAL PROCEDURE ParseRange@6(VAR Formula@1002 : Code[20];VAR FromType@1001 : 'Int,CP,LP';VAR FromInt@1004 : Integer;VAR ToType@1006 : 'Int,CP,LP';VAR ToInt@1005 : Integer) : Boolean;
    BEGIN
      FromType := FromType::CP;
      ToType := ToType::CP;

      if Formula = '' then
        exit(true);

      if not ParseToken(Formula,'[') then
        exit(false);

      if not ParseIndex(Formula,FromType,FromInt) then
        exit(false);
      if Formula = '' then
        exit(false);

      if COPYSTR(Formula,1,1) = '.' then begin
        if not ParseToken(Formula,'..') then
          exit(false);
        if not ParseIndex(Formula,ToType,ToInt) then
          exit(false);
      end else begin
        ToType := FromType;
        ToInt := FromInt;
      end;

      if not ParseToken(Formula,']') then
        exit(false);

      exit(true);
    END;

    LOCAL PROCEDURE ParseIndex@11(VAR Formula@1002 : Code[20];VAR IndexType@1001 : 'Int,CP,LP';VAR Index@1004 : Integer) : Boolean;
    BEGIN
      if Formula = '' then
        exit(false);

      if ParseInt(Formula,Index,false) then
        IndexType := IndexType::Int
      else
        case ReadToken(Formula) of
          Text004:
            IndexType := IndexType::CP;
          Text005:
            IndexType := IndexType::LP;
          else
            exit(false);
        end;

      exit(true);
    END;

    LOCAL PROCEDURE ParseToken@13(VAR Formula@1000 : Code[20];Token@1001 : Code[20]) : Boolean;
    BEGIN
      if COPYSTR(Formula,1,STRLEN(Token)) <> Token then
        exit(false);
      Formula := COPYSTR(Formula,STRLEN(Token) + 1);
      exit(true)
    END;

    LOCAL PROCEDURE ReadToken@7(VAR Formula@1000 : Code[20]) : Code[20];
    VAR
      Token@1001 : Code[20];
      p@1002 : Integer;
    BEGIN
      for p := 1 to STRLEN(Formula) do begin
        if COPYSTR(Formula,p,1) in ['[',']','.'] then begin
          Formula := COPYSTR(Formula,STRLEN(Token) + 1);
          exit(Token);
        end;
        Token := Token + COPYSTR(Formula,p,1);
      end;

      Formula := '';
      exit(Token);
    END;

    BEGIN
    END.
  }
}

