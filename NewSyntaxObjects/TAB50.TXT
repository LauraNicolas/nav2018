OBJECT Table 50 Accounting Period
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               AccountingPeriod2 := Rec;
               if AccountingPeriod2.FIND('>') then
                 AccountingPeriod2.TESTFIELD("Date Locked",false);
               UpdateAvgItems(1);
             END;

    OnModify=BEGIN
               UpdateAvgItems(2);
             END;

    OnDelete=BEGIN
               TESTFIELD("Date Locked",false);
               UpdateAvgItems(3);
             END;

    OnRename=BEGIN
               TESTFIELD("Date Locked",false);
               AccountingPeriod2 := Rec;
               if AccountingPeriod2.FIND('>') then
                 AccountingPeriod2.TESTFIELD("Date Locked",false);
               UpdateAvgItems(4);
             END;

    CaptionML=ENU=Accounting Period;
    LookupPageID="Accounting Periods";
  }
  FIELDS
  {
    { 1   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                Name := FORMAT("Starting Date",0,Text000);
                                                              END;

                                                   CaptionML=ENU=Starting Date;
                                                   NotBlank=true }
    { 2   ;   ;Name                ;Text10        ;CaptionML=ENU=Name }
    { 3   ;   ;New Fiscal Year     ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Date Locked",false);
                                                                if "New Fiscal Year" then begin
                                                                  if not InvtSetup.GET then
                                                                    exit;
                                                                  "Average Cost Calc. Type" := InvtSetup."Average Cost Calc. Type";
                                                                  "Average Cost Period" := InvtSetup."Average Cost Period";
                                                                end else begin
                                                                  "Average Cost Calc. Type" := "Average Cost Calc. Type"::" ";
                                                                  "Average Cost Period" := "Average Cost Period"::" ";
                                                                end;
                                                              END;

                                                   CaptionML=ENU=New Fiscal Year }
    { 4   ;   ;Closed              ;Boolean       ;CaptionML=ENU=Closed;
                                                   Editable=false }
    { 5   ;   ;Date Locked         ;Boolean       ;CaptionML=ENU=Date Locked;
                                                   Editable=false }
    { 5804;   ;Average Cost Calc. Type;Option     ;AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Average Cost Calc. Type;
                                                   OptionCaptionML=ENU=" ,Item,Item & Location & Variant";
                                                   OptionString=" ",Item,"Item & Location & Variant";
                                                   Editable=false }
    { 5805;   ;Average Cost Period ;Option        ;AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Average Cost Period;
                                                   OptionCaptionML=ENU=" ,Day,Week,Month,Quarter,Year,Accounting Period";
                                                   OptionString=" ",Day,Week,Month,Quarter,Year,"Accounting Period";
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Starting Date"                         ;Clustered=true }
    {    ;"New Fiscal Year","Date Locked"          }
    {    ;Closed                                   }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;"Starting Date",Name,"New Fiscal Year",Closed }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=<Month Text>';
      AccountingPeriod2@1001 : Record "Accounting Period";
      InvtSetup@1002 : Record "Inventory Setup";

    [Internal]
    PROCEDURE UpdateAvgItems@1(UpdateType@1005 : Option);
    VAR
      ChangeAvgCostSetting@1002 : Codeunit "Change Average Cost Setting";
    BEGIN
      ChangeAvgCostSetting.UpdateAvgCostFromAccPeriodChg(Rec,xRec,UpdateType);
    END;

    [External]
    PROCEDURE GetFiscalYearEndDate@9(ReferenceDate@1000 : Date) : Date;
    VAR
      AccountingPeriod@1001 : Record "Accounting Period";
    BEGIN
      with AccountingPeriod do begin
        SETRANGE("New Fiscal Year",true);
        SETRANGE("Starting Date",0D,ReferenceDate);
        if FINDLAST then
          SETRANGE("Starting Date");
        if FIND('>') then
          exit("Starting Date" - 1);
      end;
    END;

    [External]
    PROCEDURE GetFiscalYearStartDate@10(ReferenceDate@1000 : Date) : Date;
    VAR
      AccountingPeriod@1001 : Record "Accounting Period";
    BEGIN
      with AccountingPeriod do begin
        SETRANGE("New Fiscal Year",true);
        SETRANGE("Starting Date",0D,ReferenceDate);
        if FINDLAST then
          exit("Starting Date")
      end;
    END;

    BEGIN
    END.
  }
}

