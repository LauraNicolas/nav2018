OBJECT Codeunit 5943 Lock-OpenServContract
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1001 : TextConst 'ENU=It is not possible to lock this %1 Service %2 because some lines have zero %3.';
      Text001@1000 : TextConst 'ENU=It is not possible to open a %1 service contract';
      Text002@1002 : TextConst 'ENU=New lines have been added to this contract.\Would you like to continue?';
      SignServContractDoc@1003 : Codeunit SignServContractDoc;
      Text003@1004 : TextConst 'ENU=You cannot lock service contract with negative annual amount.';
      Text004@1005 : TextConst 'ENU=You cannot lock service contract with zero annual amount when invoice period is different from None.';

    [Internal]
    PROCEDURE LockServContract@1(FromServContractHeader@1000 : Record "Service Contract Header");
    VAR
      ServContractHeader@1002 : Record "Service Contract Header";
      ServContractLine@1001 : Record "Service Contract Line";
    BEGIN
      ServContractHeader := FromServContractHeader;
      with ServContractHeader do begin
        if "Change Status" = "Change Status"::Locked then
          exit;
        CALCFIELDS("Calcd. Annual Amount");
        TESTFIELD("Annual Amount","Calcd. Annual Amount");
        if "Annual Amount" < 0 then
          ERROR(Text003);
        if "Invoice Period" <> "Invoice Period"::None then
          if "Annual Amount" = 0 then
            ERROR(Text004);

        LOCKTABLE;
        if ("Contract Type" = "Contract Type"::Contract) and
           (Status = Status::Signed)
        then begin
          ServContractLine.RESET;
          ServContractLine.SETRANGE("Contract Type","Contract Type");
          ServContractLine.SETRANGE("Contract No.","Contract No.");
          ServContractLine.SETRANGE("Line Amount",0);
          if not ServContractLine.ISEMPTY then
            ERROR(Text000,Status,"Contract Type",ServContractLine.FIELDCAPTION("Line Amount"));
          ServContractLine.RESET;
          ServContractLine.SETRANGE("Contract Type","Contract Type");
          ServContractLine.SETRANGE("Contract No.","Contract No.");
          ServContractLine.SETRANGE("New Line",true);
          if not ServContractLine.ISEMPTY then begin
            if  not CONFIRM(Text002) then
              exit;
            SignServContractDoc.AddendumToContract(ServContractHeader);
          end;
        end;
        GET(FromServContractHeader."Contract Type",FromServContractHeader."Contract No.");
        "Change Status" := "Change Status"::Locked;
        MODIFY;
      end;
    END;

    [External]
    PROCEDURE OpenServContract@2(ServContractHeader@1000 : Record "Service Contract Header");
    BEGIN
      with ServContractHeader do begin
        if "Change Status" = "Change Status"::Open then
          exit;
        LOCKTABLE;
        if (Status = Status::Canceled) and ("Contract Type" = "Contract Type"::Contract)then
          ERROR(Text001,Status);
        "Change Status" := "Change Status"::Open;
        MODIFY;
      end;
    END;

    BEGIN
    END.
  }
}

