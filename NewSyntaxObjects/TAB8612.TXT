OBJECT Table 8612 Config. Question
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Config. Question;
  }
  FIELDS
  {
    { 1   ;   ;Questionnaire Code  ;Code10        ;TableRelation="Config. Questionnaire".Code;
                                                   CaptionML=ENU=Questionnaire Code;
                                                   Editable=false }
    { 2   ;   ;Question Area Code  ;Code10        ;TableRelation="Config. Question Area".Code WHERE ("Questionnaire Code"=FIELD("Questionnaire Code"));
                                                   CaptionML=ENU=Question Area Code;
                                                   Editable=false }
    { 3   ;   ;No.                 ;Integer       ;CaptionML=ENU=No.;
                                                   MinValue=1 }
    { 4   ;   ;Question            ;Text250       ;CaptionML=ENU=Question }
    { 5   ;   ;Answer Option       ;Text250       ;CaptionML=ENU=Answer Option;
                                                   Editable=false }
    { 6   ;   ;Answer              ;Text250       ;OnValidate=VAR
                                                                TempConfigPackageField@1005 : TEMPORARY Record "Config. Package Field";
                                                                ConfigValidateMgt@1001 : Codeunit "Config. Validate Management";
                                                                ConfigPackageManagement@1004 : Codeunit "Config. Package Management";
                                                                RecRef@1003 : RecordRef;
                                                                FieldRef@1002 : FieldRef;
                                                                ValidationError@1000 : Text[250];
                                                              BEGIN
                                                                if ("Field ID" <> 0) and (Answer <> '') then begin
                                                                  RecRef.OPEN("Table ID",true);
                                                                  FieldRef := RecRef.FIELD("Field ID");
                                                                  ValidationError := ConfigValidateMgt.EvaluateValue(FieldRef,Answer,false);
                                                                  if ValidationError <> '' then
                                                                    ERROR(ValidationError);

                                                                  Answer := FORMAT(FieldRef.VALUE);

                                                                  ConfigPackageManagement.GetFieldsOrder(RecRef,'',TempConfigPackageField);
                                                                  ValidationError := ConfigValidateMgt.ValidateFieldRefRelationAgainstCompanyData(FieldRef,TempConfigPackageField);
                                                                  if ValidationError <> '' then
                                                                    ERROR(ValidationError);
                                                                end;
                                                              END;

                                                   OnLookup=BEGIN
                                                              AnswerLookup;
                                                            END;

                                                   CaptionML=ENU=Answer }
    { 7   ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID }
    { 8   ;   ;Field ID            ;Integer       ;OnLookup=BEGIN
                                                              FieldLookup;
                                                            END;

                                                   CaptionML=ENU=Field ID }
    { 9   ;   ;Reference           ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=ENU=Reference }
    { 10  ;   ;Question Origin     ;Text30        ;CaptionML=ENU=Question Origin }
    { 11  ;   ;Field Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD("Table ID"),
                                                                                             "No."=FIELD("Field ID")));
                                                   OnLookup=BEGIN
                                                              FieldLookup;
                                                            END;

                                                   CaptionML=ENU=Field Name;
                                                   Editable=false }
    { 12  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD("Table ID"),
                                                                                                   "No."=FIELD("Field ID")));
                                                   OnLookup=BEGIN
                                                              FieldLookup;
                                                            END;

                                                   CaptionML=ENU=Field Caption;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Questionnaire Code","Question Area Code","No.";
                                                   Clustered=true }
    {    ;"Questionnaire Code","Question Area Code","Field ID" }
    {    ;"Table ID","Field ID"                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text002@1001 : TextConst 'ENU=Question no. %1 already exists for the field %2.';

    [Internal]
    PROCEDURE FieldLookup@1();
    VAR
      ConfigQuestion1@1004 : Record "Config. Question";
      ConfigQuestionArea@1003 : Record "Config. Question Area";
      Field@1002 : Record Field;
      ConfigQuestionnaireMgt@1000 : Codeunit "Questionnaire Management";
      Fields@1001 : Page "Field List";
    BEGIN
      ConfigQuestionArea.GET("Questionnaire Code","Question Area Code");

      if ConfigQuestionArea."Table ID" = 0 then
        exit;

      Field.SETRANGE(TableNo,ConfigQuestionArea."Table ID");
      Field.SETRANGE(Class,Field.Class::Normal);
      Fields.SETTABLEVIEW(Field);
      Fields.LOOKUPMODE := true;
      Fields.EDITABLE := false;
      if Fields.RUNMODAL = ACTION::LookupOK then begin
        Fields.GETRECORD(Field);
        "Table ID" := Field.TableNo;
        "Field ID" := Field."No.";

        ConfigQuestion1.SETRANGE("Questionnaire Code","Questionnaire Code");
        ConfigQuestion1.SETRANGE("Question Area Code","Question Area Code");
        ConfigQuestion1.SETRANGE("Table ID","Table ID");
        ConfigQuestion1.SETRANGE("Field ID","Field ID");
        if ConfigQuestion1.FINDFIRST then begin
          "Field ID" := 0;
          ConfigQuestion1.CALCFIELDS("Field Caption");
          ERROR(Text002,ConfigQuestion1."No.",ConfigQuestion1."Field Caption");
        end;

        if Question = '' then
          Question := Field."Field Caption" + '?';
        "Answer Option" := ConfigQuestionnaireMgt.BuildAnswerOption("Table ID","Field ID");
        CALCFIELDS("Field Name","Field Caption");
      end;
    END;

    LOCAL PROCEDURE AnswerLookup@2();
    VAR
      ConfigPackageData@1001 : Record "Config. Package Data";
      ConfigValidateMgt@1002 : Codeunit "Config. Validate Management";
      ConfigPackageDataPage@1000 : Page "Config. Package Data";
      RelatedTableID@1003 : Integer;
      RelatedFieldID@1004 : Integer;
    BEGIN
      ConfigValidateMgt.GetRelationInfoByIDs("Table ID","Field ID",RelatedTableID,RelatedFieldID);
      if RelatedTableID <> 0 then begin
        ConfigPackageData.SETRANGE("Table ID",RelatedTableID);
        ConfigPackageData.SETRANGE("Field ID",RelatedFieldID);

        CLEAR(ConfigPackageDataPage);
        ConfigPackageDataPage.SETTABLEVIEW(ConfigPackageData);
        ConfigPackageDataPage.LOOKUPMODE := true;
        if ConfigPackageDataPage.RUNMODAL = ACTION::LookupOK then begin
          ConfigPackageDataPage.GETRECORD(ConfigPackageData);
          Answer := ConfigPackageData.Value;
        end;
      end;
    END;

    [Internal]
    PROCEDURE LookupValue@3() : Text;
    VAR
      RecRef@1000 : RecordRef;
      FieldRef@1001 : FieldRef;
    BEGIN
      if "Table ID" > 0 then begin
        RecRef.OPEN("Table ID");
        RecRef.FINDFIRST;
        FieldRef := RecRef.FIELD("Field ID");
        exit(FORMAT(FieldRef.VALUE));
      end;
    END;

    BEGIN
    END.
  }
}

