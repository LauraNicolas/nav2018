OBJECT Codeunit 48 PostingSetupManagement
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      MyNotifications@1000 : Record "My Notifications";
      MissingAccountTxt@1005 : TextConst '@@@="%1 = Field caption, %2 = Table caption";ENU=%1 is missing in %2.';
      SetupMissingAccountTxt@1004 : TextConst 'ENU=Set up missing account';
      MissingAccountNotificationTxt@1006 : TextConst 'ENU=G/L Account is missing in posting group or setup.';
      MissingAccountNotificationDescriptionTxt@1002 : TextConst 'ENU=Show a warning when required G/L Account is missing in posting group or setup.';

    [External]
    PROCEDURE CheckCustPostingGroupReceivablesAccount@26(PostingGroup@1000 : Code[20]);
    VAR
      CustomerPostingGroup@1001 : Record "Customer Posting Group";
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      if not CustomerPostingGroup.GET(PostingGroup) then
        exit;

      if CustomerPostingGroup."Receivables Account" = '' then
        SendCustPostingGroupNotification(CustomerPostingGroup,CustomerPostingGroup.FIELDCAPTION("Receivables Account"));
    END;

    [External]
    PROCEDURE CheckVendPostingGroupPayablesAccount@28(PostingGroup@1000 : Code[20]);
    VAR
      VendorPostingGroup@1001 : Record "Vendor Posting Group";
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      if not VendorPostingGroup.GET(PostingGroup) then
        exit;

      if VendorPostingGroup."Payables Account" = '' then
        SendVendPostingGroupNotification(VendorPostingGroup,VendorPostingGroup.FIELDCAPTION("Payables Account"));
    END;

    [External]
    PROCEDURE CheckGenPostingSetupSalesAccount@6(GenBusGroupCode@1001 : Code[20];GenProdGroupCode@1002 : Code[20]);
    VAR
      GenPostingSetup@1000 : Record "General Posting Setup";
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      if not GenPostingSetup.GET(GenBusGroupCode,GenProdGroupCode) then
        CreateGenPostingSetup(GenBusGroupCode,GenProdGroupCode);

      if GenPostingSetup."Sales Account" = '' then
        SendGenPostingSetupNotification(GenPostingSetup,GenPostingSetup.FIELDCAPTION("Sales Account"));
    END;

    [External]
    PROCEDURE CheckGenPostingSetupPurchAccount@14(GenBusGroupCode@1001 : Code[20];GenProdGroupCode@1002 : Code[20]);
    VAR
      GenPostingSetup@1000 : Record "General Posting Setup";
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      if not GenPostingSetup.GET(GenBusGroupCode,GenProdGroupCode) then
        CreateGenPostingSetup(GenBusGroupCode,GenProdGroupCode);

      if GenPostingSetup."Purch. Account" = '' then
        SendGenPostingSetupNotification(GenPostingSetup,GenPostingSetup.FIELDCAPTION("Purch. Account"));
    END;

    [External]
    PROCEDURE CheckGenPostingSetupCOGSAccount@19(GenBusGroupCode@1001 : Code[20];GenProdGroupCode@1002 : Code[20]);
    VAR
      GenPostingSetup@1000 : Record "General Posting Setup";
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      if not GenPostingSetup.GET(GenBusGroupCode,GenProdGroupCode) then
        CreateGenPostingSetup(GenBusGroupCode,GenProdGroupCode);

      if GenPostingSetup."COGS Account" = '' then
        SendGenPostingSetupNotification(GenPostingSetup,GenPostingSetup.FIELDCAPTION("COGS Account"));
    END;

    [External]
    PROCEDURE CheckVATPostingSetupSalesAccount@8(VATBusGroupCode@1001 : Code[20];VATProdGroupCode@1000 : Code[20]);
    VAR
      VATPostingSetup@1002 : Record "VAT Posting Setup";
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      if not VATPostingSetup.GET(VATBusGroupCode,VATProdGroupCode) then
        CreateVATPostingSetup(VATBusGroupCode,VATProdGroupCode);

      if VATPostingSetup."VAT Calculation Type" = VATPostingSetup."VAT Calculation Type"::"Sales Tax" then
        exit;

      if VATPostingSetup."Sales VAT Account" = '' then
        SendVATPostingSetupNotification(VATPostingSetup,VATPostingSetup.FIELDCAPTION("Sales VAT Account"));
    END;

    [External]
    PROCEDURE CheckVATPostingSetupPurchAccount@29(VATBusGroupCode@1001 : Code[20];VATProdGroupCode@1000 : Code[20]);
    VAR
      VATPostingSetup@1002 : Record "VAT Posting Setup";
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      if not VATPostingSetup.GET(VATBusGroupCode,VATProdGroupCode) then
        CreateVATPostingSetup(VATBusGroupCode,VATProdGroupCode);

      if VATPostingSetup."VAT Calculation Type" = VATPostingSetup."VAT Calculation Type"::"Sales Tax" then
        exit;

      if VATPostingSetup."Purchase VAT Account" = '' then
        SendVATPostingSetupNotification(VATPostingSetup,VATPostingSetup.FIELDCAPTION("Purchase VAT Account"));
    END;

    [External]
    PROCEDURE CheckInvtPostingSetupInventoryAccount@21(LocationCode@1000 : Code[10];PostingGroup@1001 : Code[20]);
    VAR
      InventoryPostingSetup@1003 : Record "Inventory Posting Setup";
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      if not InventoryPostingSetup.GET(LocationCode,PostingGroup) then
        CreateInvtPostingSetup(LocationCode,PostingGroup);

      if InventoryPostingSetup."Inventory Account" = '' then
        SendInvtPostingSetupNotification(InventoryPostingSetup,InventoryPostingSetup.FIELDCAPTION("Inventory Account"));
    END;

    PROCEDURE GetPostingSetupNotificationID@15() : GUID;
    BEGIN
      exit('7c2a2ca8-bdf7-4428-b520-ed17887ff30c');
    END;

    LOCAL PROCEDURE CreateGenPostingSetup@101(GenBusGroupCode@1001 : Code[20];GenProdGroupCode@1002 : Code[20]);
    VAR
      GenPostingSetup@1000 : Record "General Posting Setup";
    BEGIN
      GenPostingSetup.INIT;
      GenPostingSetup.VALIDATE("Gen. Bus. Posting Group",GenBusGroupCode);
      GenPostingSetup.VALIDATE("Gen. Prod. Posting Group",GenProdGroupCode);
      GenPostingSetup.INSERT;
    END;

    LOCAL PROCEDURE CreateVATPostingSetup@102(VATBusGroupCode@1001 : Code[20];VATProdGroupCode@1002 : Code[20]);
    VAR
      VATPostingSetup@1000 : Record "VAT Posting Setup";
    BEGIN
      VATPostingSetup.INIT;
      VATPostingSetup.VALIDATE("VAT Bus. Posting Group",VATBusGroupCode);
      VATPostingSetup.VALIDATE("VAT Prod. Posting Group",VATProdGroupCode);
      VATPostingSetup.INSERT;
    END;

    LOCAL PROCEDURE CreateInvtPostingSetup@103(LocationCode@1001 : Code[10];PostingGroupCode@1002 : Code[20]);
    VAR
      InventoryPostingSetup@1000 : Record "Inventory Posting Setup";
    BEGIN
      InventoryPostingSetup.INIT;
      InventoryPostingSetup.VALIDATE("Location Code",LocationCode);
      InventoryPostingSetup.VALIDATE("Invt. Posting Group Code",PostingGroupCode);
      InventoryPostingSetup.INSERT;
    END;

    PROCEDURE IsPostingSetupNotificationEnabled@16() : Boolean;
    BEGIN
      if not MyNotifications.GET(USERID,GetPostingSetupNotificationID) then
        exit(false);

      exit(MyNotifications.Enabled);
    END;

    LOCAL PROCEDURE SendPostingSetupNotification@100(NotificationMsg@1000 : Text;ActionMsg@1002 : Text;ActionName@1003 : Text;GroupCode1@1004 : Code[20];GroupCode2@1005 : Code[20]);
    VAR
      SendNotification@1001 : Notification;
    BEGIN
      SendNotification.ID := CREATEGUID;
      SendNotification.MESSAGE(NotificationMsg);
      SendNotification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
      SendNotification.SETDATA('GroupCode1',GroupCode1);
      if GroupCode2 <> '' then
        SendNotification.SETDATA('GroupCode2',GroupCode2);
      SendNotification.ADDACTION(ActionMsg,CODEUNIT::PostingSetupManagement,ActionName);
      SendNotification.SEND;
    END;

    [External]
    PROCEDURE SendCustPostingGroupNotification@7(CustomerPostingGroup@1001 : Record "Customer Posting Group";FieldCaption@1000 : Text);
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      SendPostingSetupNotification(
        STRSUBSTNO(MissingAccountTxt,FieldCaption,CustomerPostingGroup.TABLECAPTION),
        SetupMissingAccountTxt,'ShowCustomerPostingGroups',CustomerPostingGroup.Code,'');
    END;

    [External]
    PROCEDURE SendVendPostingGroupNotification@9(VendorPostingGroup@1001 : Record "Vendor Posting Group";FieldCaption@1000 : Text);
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      SendPostingSetupNotification(
        STRSUBSTNO(MissingAccountTxt,FieldCaption,VendorPostingGroup.TABLECAPTION),
        SetupMissingAccountTxt,'ShowVendorPostingGroups',VendorPostingGroup.Code,'');
    END;

    [External]
    PROCEDURE SendInvtPostingSetupNotification@40(InvtPostingSetup@1001 : Record "Inventory Posting Setup";FieldCaption@1000 : Text);
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      SendPostingSetupNotification(
        STRSUBSTNO(MissingAccountTxt,FieldCaption,InvtPostingSetup.TABLECAPTION),
        SetupMissingAccountTxt,'ShowInventoryPostingSetup',
        InvtPostingSetup."Invt. Posting Group Code",InvtPostingSetup."Location Code");
    END;

    [External]
    PROCEDURE SendGenPostingSetupNotification@44(GenPostingSetup@1000 : Record "General Posting Setup";FieldCaption@1001 : Text);
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      SendPostingSetupNotification(
        STRSUBSTNO(MissingAccountTxt,FieldCaption,GenPostingSetup.TABLECAPTION),
        SetupMissingAccountTxt,'ShowGenPostingSetup',
        GenPostingSetup."Gen. Bus. Posting Group",GenPostingSetup."Gen. Prod. Posting Group");
    END;

    [External]
    PROCEDURE SendVATPostingSetupNotification@13(VATPostingSetup@1000 : Record "VAT Posting Setup";FieldCaption@1001 : Text);
    BEGIN
      if not IsPostingSetupNotificationEnabled then
        exit;

      SendPostingSetupNotification(
        STRSUBSTNO(MissingAccountTxt,FieldCaption,VATPostingSetup.TABLECAPTION),
        SetupMissingAccountTxt,'ShowVATPostingSetup',
        VATPostingSetup."VAT Bus. Posting Group",VATPostingSetup."VAT Prod. Posting Group");
    END;

    PROCEDURE ShowCustomerPostingGroups@1(SetupNotification@1000 : Notification);
    VAR
      CustomerPostingGroup@1002 : Record "Customer Posting Group";
      CustomerPostingGroups@1003 : Page "Customer Posting Groups";
      PostingGroupCode@1001 : Code[20];
    BEGIN
      CLEAR(CustomerPostingGroups);
      PostingGroupCode := SetupNotification.GETDATA('GroupCode1');
      if PostingGroupCode <> '' then begin
        CustomerPostingGroup.GET(PostingGroupCode);
        CustomerPostingGroups.SETRECORD(CustomerPostingGroup);
      end;
      CustomerPostingGroups.SETTABLEVIEW(CustomerPostingGroup);
      CustomerPostingGroups.RUNMODAL;
    END;

    PROCEDURE ShowVendorPostingGroups@2(SetupNotification@1000 : Notification);
    VAR
      VendorPostingGroup@1001 : Record "Vendor Posting Group";
      VendorPostingGroups@1002 : Page "Vendor Posting Groups";
      PostingGroupCode@1003 : Code[20];
    BEGIN
      CLEAR(VendorPostingGroups);
      PostingGroupCode := SetupNotification.GETDATA('GroupCode1');
      if PostingGroupCode <> '' then begin
        VendorPostingGroup.GET(PostingGroupCode);
        VendorPostingGroups.SETRECORD(VendorPostingGroup);
      end;
      VendorPostingGroups.SETTABLEVIEW(VendorPostingGroup);
      VendorPostingGroups.RUNMODAL;
    END;

    PROCEDURE ShowInventoryPostingSetup@5(SetupNotification@1000 : Notification);
    VAR
      InventoryPostingSetupRec@1001 : Record "Inventory Posting Setup";
      InventoryPostingSetupPage@1002 : Page "Inventory Posting Setup";
      PostingGroupCode@1003 : Code[20];
      LocationCode@1004 : Code[10];
    BEGIN
      CLEAR(InventoryPostingSetupPage);
      PostingGroupCode := SetupNotification.GETDATA('GroupCode1');
      LocationCode := SetupNotification.GETDATA('GroupCode2');
      if PostingGroupCode <> '' then begin
        InventoryPostingSetupRec.GET(LocationCode,PostingGroupCode);
        InventoryPostingSetupPage.SETRECORD(InventoryPostingSetupRec);
      end;
      InventoryPostingSetupPage.SETTABLEVIEW(InventoryPostingSetupRec);
      InventoryPostingSetupPage.RUNMODAL;
    END;

    PROCEDURE ShowGenPostingSetup@3(SetupNotification@1000 : Notification);
    VAR
      GenPostingSetupRec@1001 : Record "General Posting Setup";
      GenPostingSetupPage@1002 : Page "General Posting Setup";
      BusPostingGroupCode@1003 : Code[20];
      ProdPostingGroupCode@1004 : Code[20];
    BEGIN
      CLEAR(GenPostingSetupPage);
      BusPostingGroupCode := SetupNotification.GETDATA('GroupCode1');
      ProdPostingGroupCode := SetupNotification.GETDATA('GroupCode2');
      if ProdPostingGroupCode <> '' then begin
        GenPostingSetupRec.GET(BusPostingGroupCode,ProdPostingGroupCode);
        GenPostingSetupPage.SETRECORD(GenPostingSetupRec);
      end;
      GenPostingSetupPage.SETTABLEVIEW(GenPostingSetupRec);
      GenPostingSetupPage.RUNMODAL;
    END;

    PROCEDURE ShowVATPostingSetup@4(SetupNotification@1000 : Notification);
    VAR
      VATPostingSetupRec@1004 : Record "VAT Posting Setup";
      VATPostingSetupPage@1003 : Page "VAT Posting Setup";
      BusPostingGroupCode@1002 : Code[20];
      ProdPostingGroupCode@1001 : Code[20];
    BEGIN
      CLEAR(VATPostingSetupPage);
      BusPostingGroupCode := SetupNotification.GETDATA('GroupCode1');
      ProdPostingGroupCode := SetupNotification.GETDATA('GroupCode2');
      if ProdPostingGroupCode <> '' then begin
        VATPostingSetupRec.GET(BusPostingGroupCode,ProdPostingGroupCode);
        VATPostingSetupPage.SETRECORD(VATPostingSetupRec);
      end;
      VATPostingSetupPage.SETTABLEVIEW(VATPostingSetupRec);
      VATPostingSetupPage.RUNMODAL;
    END;

    [EventSubscriber(Page,1518,OnInitializingNotificationWithDefaultState)]
    LOCAL PROCEDURE OnInitializingNotificationWithDefaultState@17();
    BEGIN
      MyNotifications.InsertDefault(
        GetPostingSetupNotificationID,MissingAccountNotificationTxt,MissingAccountNotificationDescriptionTxt,false);
    END;

    BEGIN
    END.
  }
}

