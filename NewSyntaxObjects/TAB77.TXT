OBJECT Table 77 Report Selections
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckEmailBodyUsage;
             END;

    OnModify=BEGIN
               TESTFIELD("Report ID");
               CheckEmailBodyUsage;
             END;

    CaptionML=ENU=Report Selections;
  }
  FIELDS
  {
    { 1   ;   ;Usage               ;Option        ;CaptionML=ENU=Usage;
                                                   OptionCaptionML=ENU=S.Quote,S.Order,S.Invoice,S.Cr.Memo,S.Test,P.Quote,P.Order,P.Invoice,P.Cr.Memo,P.Receipt,P.Ret.Shpt.,P.Test,B.Stmt,B.Recon.Test,B.Check,Reminder,Fin.Charge,Rem.Test,F.C.Test,Prod. Order,S.Blanket,P.Blanket,M1,M2,M3,M4,Inv1,Inv2,Inv3,SM.Quote,SM.Order,SM.Invoice,SM.Credit Memo,SM.Contract Quote,SM.Contract,SM.Test,S.Return,P.Return,S.Shipment,S.Ret.Rcpt.,S.Work Order,Invt. Period Test,SM.Shipment,S.Test Prepmt.,P.Test Prepmt.,S.Arch. Quote,S.Arch. Order,P.Arch. Quote,P.Arch. Order,S. Arch. Return Order,P. Arch. Return Order,Asm. Order,P.Assembly Order,S.Order Pick Instruction,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,C.Statement,V.Remittance,JQ,S.Invoice Draft,Pro Forma S. Invoice;
                                                   OptionString="S.Quote","S.Order","S.Invoice","S.Cr.Memo","S.Test","P.Quote","P.Order","P.Invoice","P.Cr.Memo","P.Receipt","P.Ret.Shpt.","P.Test","B.Stmt","B.Recon.Test","B.Check",Reminder,"Fin.Charge","Rem.Test","F.C.Test","Prod. Order","S.Blanket","P.Blanket",M1,M2,M3,M4,Inv1,Inv2,Inv3,"SM.Quote","SM.Order","SM.Invoice","SM.Credit Memo","SM.Contract Quote","SM.Contract","SM.Test","S.Return","P.Return","S.Shipment","S.Ret.Rcpt.","S.Work Order","Invt. Period Test","SM.Shipment","S.Test Prepmt.","P.Test Prepmt.","S.Arch. Quote","S.Arch. Order","P.Arch. Quote","P.Arch. Order","S. Arch. Return Order","P. Arch. Return Order","Asm. Order","P.Assembly Order","S.Order Pick Instruction",,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,"C.Statement","V.Remittance",JQ,"S.Invoice Draft","Pro Forma S. Invoice" }
    { 2   ;   ;Sequence            ;Code10        ;CaptionML=ENU=Sequence;
                                                   Numeric=true }
    { 3   ;   ;Report ID           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Report));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Report Caption");
                                                                VALIDATE("Use for Email Body",false);
                                                              END;

                                                   CaptionML=ENU=Report ID }
    { 4   ;   ;Report Caption      ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Report ID")));
                                                   CaptionML=ENU=Report Caption;
                                                   Editable=false }
    { 7   ;   ;Custom Report Layout Code;Code20   ;TableRelation="Custom Report Layout".Code WHERE (Code=FIELD("Custom Report Layout Code"));
                                                   CaptionML=ENU=Custom Report Layout Code;
                                                   Editable=false }
    { 19  ;   ;Use for Email Attachment;Boolean   ;InitValue=true;
                                                   OnValidate=BEGIN
                                                                if not "Use for Email Body" then
                                                                  VALIDATE("Email Body Layout Code",'');
                                                              END;

                                                   CaptionML=ENU=Use for Email Attachment }
    { 20  ;   ;Use for Email Body  ;Boolean       ;OnValidate=BEGIN
                                                                if not "Use for Email Body" then
                                                                  VALIDATE("Email Body Layout Code",'');
                                                              END;

                                                   CaptionML=ENU=Use for Email Body }
    { 21  ;   ;Email Body Layout Code;Code20      ;TableRelation=IF ("Email Body Layout Type"=CONST("Custom Report Layout")) "Custom Report Layout".Code WHERE (Code=FIELD("Email Body Layout Code"),
                                                                                                                                                                "Report ID"=FIELD("Report ID"))
                                                                                                                                                                ELSE IF ("Email Body Layout Type"=CONST("HTML Layout")) "O365 HTML Template".Code;
                                                   OnValidate=BEGIN
                                                                if "Email Body Layout Code" <> '' then
                                                                  TESTFIELD("Use for Email Body",true);
                                                                CALCFIELDS("Email Body Layout Description");
                                                              END;

                                                   CaptionML=ENU=Email Body Layout Code }
    { 22  ;   ;Email Body Layout Description;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (Code=FIELD("Email Body Layout Code")));
                                                   OnLookup=VAR
                                                              CustomReportLayout@1001 : Record "Custom Report Layout";
                                                            BEGIN
                                                              if "Email Body Layout Type" = "Email Body Layout Type"::"Custom Report Layout" then
                                                                if CustomReportLayout.LookupLayoutOK("Report ID") then
                                                                  VALIDATE("Email Body Layout Code",CustomReportLayout.Code);
                                                            END;

                                                   CaptionML=ENU=Email Body Layout Description;
                                                   Editable=false }
    { 25  ;   ;Email Body Layout Type;Option      ;CaptionML=ENU=Email Body Layout Type;
                                                   OptionCaptionML=ENU=Custom Report Layout,HTML Layout;
                                                   OptionString="Custom Report Layout","HTML Layout" }
  }
  KEYS
  {
    {    ;Usage,Sequence                          ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ReportSelection2@1000 : Record "Report Selections";
      MustSelectAndEmailBodyOrAttahmentErr@1001 : TextConst '@@@="%1 = Usage, for example Sales Invoice";ENU=You must select an email body or attachment in report selection for %1.';
      EmailBodyIsAlreadyDefinedErr@1002 : TextConst '@@@="%1 = Usage, for example Sales Invoice";ENU=An email body is already defined for %1.';
      CannotBeUsedAsAnEmailBodyErr@1003 : TextConst '@@@="%1 = Report ID,%2 = Type";ENU=Report %1 uses the %2 which cannot be used as an email body.';
      ReportLayoutSelection@1004 : Record "Report Layout Selection";
      InteractionMgt@1006 : Codeunit "Interaction Mgt.";

    [External]
    PROCEDURE NewRecord@1();
    BEGIN
      ReportSelection2.SETRANGE(Usage,Usage);
      if ReportSelection2.FINDLAST and (ReportSelection2.Sequence <> '') then
        Sequence := INCSTR(ReportSelection2.Sequence)
      else
        Sequence := '1';
    END;

    LOCAL PROCEDURE CheckEmailBodyUsage@19();
    VAR
      ReportSelections@1001 : Record "Report Selections";
      ReportLayoutSelection@1000 : Record "Report Layout Selection";
    BEGIN
      if "Use for Email Body" then begin
        ReportSelections.FilterEmailBodyUsage(Usage);
        ReportSelections.SETFILTER(Sequence,'<>%1',Sequence);
        if not ReportSelections.ISEMPTY then
          ERROR(EmailBodyIsAlreadyDefinedErr,Usage);

        if "Email Body Layout Code" = '' then
          if ReportLayoutSelection.GetDefaultType("Report ID") =
             ReportLayoutSelection.Type::"RDLC (built-in)"
          then
            ERROR(CannotBeUsedAsAnEmailBodyErr,"Report ID",ReportLayoutSelection.Type);
      end;
    END;

    [External]
    PROCEDURE FilterPrintUsage@2(ReportUsage@1000 : Integer);
    BEGIN
      RESET;
      SETRANGE(Usage,ReportUsage);
    END;

    [External]
    PROCEDURE FilterEmailUsage@3(ReportUsage@1000 : Integer);
    BEGIN
      RESET;
      SETRANGE(Usage,ReportUsage);
      SETRANGE("Use for Email Body",true);
    END;

    [External]
    PROCEDURE FilterEmailBodyUsage@13(ReportUsage@1000 : Integer);
    BEGIN
      RESET;
      SETRANGE(Usage,ReportUsage);
      SETRANGE("Use for Email Body",true);
    END;

    [External]
    PROCEDURE FilterEmailAttachmentUsage@11(ReportUsage@1000 : Integer);
    BEGIN
      RESET;
      SETRANGE(Usage,ReportUsage);
      SETRANGE("Use for Email Attachment",true);
    END;

    [External]
    PROCEDURE FindPrintUsage@4(ReportUsage@1000 : Integer;CustNo@1002 : Code[20];VAR ReportSelections@1001 : Record "Report Selections");
    BEGIN
      FilterPrintUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');

      FindReportSelections(ReportSelections,CustNo);
      ReportSelections.FINDSET;
    END;

    [External]
    PROCEDURE FindPrintUsageVendor@33(ReportUsage@1002 : Integer;VendorNo@1001 : Code[20];VAR ReportSelections@1000 : Record "Report Selections");
    BEGIN
      FilterPrintUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');

      FindReportSelectionsVendor(ReportSelections,VendorNo);
      ReportSelections.FINDSET;
    END;

    [External]
    PROCEDURE FindEmailAttachmentUsage@10(ReportUsage@1000 : Integer;CustNo@1002 : Code[20];VAR ReportSelections@1001 : Record "Report Selections") : Boolean;
    BEGIN
      FilterEmailAttachmentUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');
      SETRANGE("Use for Email Attachment",true);

      FindReportSelections(ReportSelections,CustNo);
      exit(ReportSelections.FINDSET);
    END;

    [External]
    PROCEDURE FindEmailAttachmentUsageVendor@45(ReportUsage@1002 : Integer;VendorNo@1001 : Code[20];VAR ReportSelections@1000 : Record "Report Selections") : Boolean;
    BEGIN
      FilterEmailAttachmentUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');
      SETRANGE("Use for Email Attachment",true);

      FindReportSelectionsVendor(ReportSelections,VendorNo);
      exit(ReportSelections.FINDSET);
    END;

    [External]
    PROCEDURE FindEmailBodyUsage@5(ReportUsage@1000 : Integer;CustNo@1002 : Code[20];VAR ReportSelections@1001 : Record "Report Selections") : Boolean;
    BEGIN
      FilterEmailBodyUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');

      FindReportSelections(ReportSelections,CustNo);
      exit(ReportSelections.FINDSET);
    END;

    [External]
    PROCEDURE FindEmailBodyUsageVendor@42(ReportUsage@1002 : Integer;VendorNo@1001 : Code[20];VAR ReportSelections@1000 : Record "Report Selections") : Boolean;
    BEGIN
      FilterEmailBodyUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');

      FindReportSelectionsVendor(ReportSelections,VendorNo);
      exit(ReportSelections.FINDSET);
    END;

    [External]
    PROCEDURE PrintWithCheck@6(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;CustNo@1002 : Code[20]);
    BEGIN
      PrintWithGUIYesNoWithCheck(ReportUsage,RecordVariant,true,CustNo);
    END;

    [External]
    PROCEDURE PrintWithGUIYesNoWithCheck@12(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;IsGUI@1002 : Boolean;CustNo@1003 : Code[20]);
    VAR
      TempReportSelections@1004 : TEMPORARY Record "Report Selections";
    BEGIN
      FilterPrintUsage(ReportUsage);

      FindReportSelections(TempReportSelections,CustNo);
      if not TempReportSelections.FINDSET then
        FINDSET;
      with TempReportSelections do
        repeat
          ReportLayoutSelection.SetTempLayoutSelected("Custom Report Layout Code");
          TESTFIELD("Report ID");
          REPORT.RUNMODAL("Report ID",IsGUI,false,RecordVariant)
        until NEXT = 0;
      ReportLayoutSelection.SetTempLayoutSelected('');
    END;

    [External]
    PROCEDURE Print@7(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;CustNo@1002 : Code[20]);
    BEGIN
      PrintWithGUIYesNo(ReportUsage,RecordVariant,true,CustNo);
    END;

    [External]
    PROCEDURE PrintWithGUIYesNo@8(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;IsGUI@1002 : Boolean;CustNo@1003 : Code[20]);
    VAR
      TempReportSelections@1004 : TEMPORARY Record "Report Selections";
    BEGIN
      FindPrintUsage(ReportUsage,CustNo,TempReportSelections);
      with TempReportSelections do
        repeat
          ReportLayoutSelection.SetTempLayoutSelected("Custom Report Layout Code");
          REPORT.RUNMODAL("Report ID",IsGUI,false,RecordVariant)
        until NEXT = 0;
      ReportLayoutSelection.SetTempLayoutSelected('');
    END;

    [External]
    PROCEDURE PrintWithGUIYesNoVendor@32(ReportUsage@1003 : Integer;RecordVariant@1002 : Variant;IsGUI@1001 : Boolean;VendorNo@1000 : Code[20]);
    VAR
      TempReportSelections@1004 : TEMPORARY Record "Report Selections";
    BEGIN
      FindPrintUsageVendor(ReportUsage,VendorNo,TempReportSelections);
      with TempReportSelections do
        repeat
          ReportLayoutSelection.SetTempLayoutSelected("Custom Report Layout Code");
          REPORT.RUNMODAL("Report ID",IsGUI,false,RecordVariant)
        until NEXT = 0;
      ReportLayoutSelection.SetTempLayoutSelected('');
    END;

    [Internal]
    PROCEDURE GetHtmlReport@29(VAR ServerEmailBodyFilePath@1001 : Text[250];ReportUsage@1002 : Integer;RecordVariant@1004 : Variant;CustNo@1003 : Code[20]);
    VAR
      TempBodyReportSelections@1000 : TEMPORARY Record "Report Selections";
    BEGIN
      ServerEmailBodyFilePath := '';

      FindPrintUsage(ReportUsage,CustNo,TempBodyReportSelections);

      ServerEmailBodyFilePath :=
        SaveReportAsHTML(TempBodyReportSelections."Report ID",RecordVariant,TempBodyReportSelections."Custom Report Layout Code");
    END;

    [Internal]
    PROCEDURE GetPdfReport@52(VAR ServerEmailBodyFilePath@1001 : Text[250];ReportUsage@1002 : Integer;RecordVariant@1004 : Variant;CustNo@1003 : Code[20]);
    VAR
      TempBodyReportSelections@1000 : TEMPORARY Record "Report Selections";
    BEGIN
      ServerEmailBodyFilePath := '';

      FindPrintUsage(ReportUsage,CustNo,TempBodyReportSelections);

      ServerEmailBodyFilePath :=
        SaveReportAsPDF(TempBodyReportSelections."Report ID",RecordVariant,TempBodyReportSelections."Custom Report Layout Code");
    END;

    [Internal]
    PROCEDURE GetEmailBodyInPdf@85(VAR ServerEmailBodyFilePath@1001 : Text[250];ReportUsage@1002 : Integer;RecordVariant@1004 : Variant;CustNo@1003 : Code[20];VAR CustEmailAddress@1005 : Text[250]) : Boolean;
    VAR
      TempBodyReportSelections@1000 : TEMPORARY Record "Report Selections";
    BEGIN
      ServerEmailBodyFilePath := '';

      CustEmailAddress := GetCustEmailAddress(CustNo);

      if not FindEmailBodyUsage(ReportUsage,CustNo,TempBodyReportSelections) then
        exit(false);

      ServerEmailBodyFilePath :=
        SaveReportAsPDF(TempBodyReportSelections."Report ID",RecordVariant,TempBodyReportSelections."Email Body Layout Code");

      CustEmailAddress := FindEmailAddressForEmailLayout(TempBodyReportSelections."Email Body Layout Code",CustNo,ReportUsage);
      if CustEmailAddress = '' then
        CustEmailAddress := GetCustEmailAddress(CustNo);

      exit(true);
    END;

    [Internal]
    PROCEDURE GetEmailBody@20(VAR ServerEmailBodyFilePath@1001 : Text[250];ReportUsage@1002 : Integer;RecordVariant@1004 : Variant;CustNo@1003 : Code[20];VAR CustEmailAddress@1005 : Text[250]) : Boolean;
    VAR
      TempBodyReportSelections@1000 : TEMPORARY Record "Report Selections";
      O365HTMLTemplMgt@1006 : Codeunit "O365 HTML Templ. Mgt.";
    BEGIN
      ServerEmailBodyFilePath := '';

      if CustEmailAddress = '' then
        CustEmailAddress := GetCustEmailAddress(CustNo);

      if not FindEmailBodyUsage(ReportUsage,CustNo,TempBodyReportSelections) then
        exit(false);

      case "Email Body Layout Type" of
        "Email Body Layout Type"::"Custom Report Layout":
          ServerEmailBodyFilePath :=
            SaveReportAsHTML(TempBodyReportSelections."Report ID",RecordVariant,TempBodyReportSelections."Email Body Layout Code");
        "Email Body Layout Type"::"HTML Layout":
          ServerEmailBodyFilePath :=
            O365HTMLTemplMgt.CreateEmailBodyFromReportSelections(Rec,RecordVariant,CustEmailAddress);
      end;

      CustEmailAddress := FindEmailAddressForEmailLayout(TempBodyReportSelections."Email Body Layout Code",CustNo,ReportUsage);
      if CustEmailAddress = '' then
        CustEmailAddress := GetCustEmailAddress(CustNo);

      exit(true);
    END;

    [Internal]
    PROCEDURE GetEmailBodyVendor@40(VAR ServerEmailBodyFilePath@1004 : Text[250];ReportUsage@1003 : Integer;RecordVariant@1002 : Variant;VendorNo@1001 : Code[20];VAR VendorEmailAddress@1000 : Text[250]) : Boolean;
    VAR
      TempBodyReportSelections@1005 : TEMPORARY Record "Report Selections";
    BEGIN
      ServerEmailBodyFilePath := '';

      VendorEmailAddress := GetVendorEmailAddress(VendorNo);

      if not FindEmailBodyUsageVendor(ReportUsage,VendorNo,TempBodyReportSelections) then
        exit(false);

      ServerEmailBodyFilePath :=
        SaveReportAsHTML(TempBodyReportSelections."Report ID",RecordVariant,TempBodyReportSelections."Email Body Layout Code");

      VendorEmailAddress :=
        FindEmailAddressForEmailLayoutVendor(TempBodyReportSelections."Email Body Layout Code",VendorNo,ReportUsage);
      if VendorEmailAddress = '' then
        VendorEmailAddress := GetVendorEmailAddress(VendorNo);

      exit(true);
    END;

    [Internal]
    PROCEDURE SendEmailInBackground@30(JobQueueEntry@1000 : Record "Job Queue Entry");
    VAR
      RecRef@1005 : RecordRef;
      ReportUsage@1004 : Integer;
      DocNo@1003 : Code[20];
      DocName@1002 : Text[150];
      No@1001 : Code[20];
      ParamString@1006 : Text;
    BEGIN
      // Called from codeunit 260 OnRun trigger - in a background process.
      RecRef.GET(JobQueueEntry."Record ID to Process");
      RecRef.LOCKTABLE;
      RecRef.FIND;
      RecRef.SETRECFILTER;
      ParamString := JobQueueEntry."Parameter String";  // Are set in function SendEmailToCust
      GetJobQueueParameters(ParamString,ReportUsage,DocNo,DocName,No);

      if ParamString = 'Vendor' then
        SendEmailToVendorDirectly(ReportUsage,RecRef,DocNo,DocName,false,No)
      else
        SendEmailToCustDirectly(ReportUsage,RecRef,DocNo,DocName,false,No);
    END;

    PROCEDURE GetJobQueueParameters@56(VAR ParameterString@1000 : Text;VAR ReportUsage@1001 : Integer;VAR DocNo@1002 : Code[20];VAR DocName@1003 : Text[150];VAR CustNo@1004 : Code[20]) WasSuccessful : Boolean;
    BEGIN
      WasSuccessful := EVALUATE(ReportUsage,GetNextJobQueueParam(ParameterString));
      WasSuccessful := WasSuccessful and EVALUATE(DocNo,GetNextJobQueueParam(ParameterString));
      WasSuccessful := WasSuccessful and EVALUATE(DocName,GetNextJobQueueParam(ParameterString));
      WasSuccessful := WasSuccessful and EVALUATE(CustNo,GetNextJobQueueParam(ParameterString));
    END;

    LOCAL PROCEDURE GetNextJobQueueParam@31(VAR Parameter@1000 : Text) : Text;
    VAR
      i@1001 : Integer;
      Result@1002 : Text;
    BEGIN
      i := STRPOS(Parameter,'|');
      if i > 0 then
        Result := COPYSTR(Parameter,1,i - 1);
      if (i + 1) < STRLEN(Parameter) then
        Parameter := COPYSTR(Parameter,i + 1);
      exit(Result);
    END;

    [Internal]
    PROCEDURE SendEmailToCust@9(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;DocNo@1006 : Code[20];DocName@1004 : Text[150];ShowDialog@1007 : Boolean;CustNo@1010 : Code[20]);
    VAR
      JobQueueEntry@1003 : Record "Job Queue Entry";
      SMTPMail@1002 : Codeunit "SMTP Mail";
      OfficeMgt@1008 : Codeunit "Office Management";
      RecRef@1005 : RecordRef;
    BEGIN
      if ShowDialog or not SMTPMail.IsEnabled or (GetCustEmailAddress(CustNo) = '') or OfficeMgt.IsAvailable then begin
        SendEmailToCustDirectly(ReportUsage,RecordVariant,DocNo,DocName,true,CustNo);
        exit;
      end;

      RecRef.GETTABLE(RecordVariant);
      JobQueueEntry.INIT;
      JobQueueEntry."Object Type to Run" := JobQueueEntry."Object Type to Run"::Codeunit;
      JobQueueEntry."Object ID to Run" := CODEUNIT::"Document-Mailing";
      JobQueueEntry."Maximum No. of Attempts to Run" := 3;
      JobQueueEntry."Record ID to Process" := RecRef.RECORDID;
      JobQueueEntry."Parameter String" := STRSUBSTNO('%1|%2|%3|%4|',ReportUsage,DocNo,DocName,CustNo);
      JobQueueEntry.Description := COPYSTR(DocName,1,MAXSTRLEN(JobQueueEntry.Description));
      CODEUNIT.RUN(CODEUNIT::"Job Queue - Enqueue",JobQueueEntry);
    END;

    [Internal]
    PROCEDURE SendEmailToVendor@34(ReportUsage@1005 : Integer;RecordVariant@1004 : Variant;DocNo@1003 : Code[20];DocName@1002 : Text[150];ShowDialog@1001 : Boolean;VendorNo@1000 : Code[20]);
    VAR
      JobQueueEntry@1008 : Record "Job Queue Entry";
      SMTPMail@1007 : Codeunit "SMTP Mail";
      OfficeMgt@1009 : Codeunit "Office Management";
      RecRef@1006 : RecordRef;
    BEGIN
      if ShowDialog or not SMTPMail.IsEnabled or (GetVendorEmailAddress(VendorNo) = '') or OfficeMgt.IsAvailable then begin
        SendEmailToVendorDirectly(ReportUsage,RecordVariant,DocNo,DocName,true,VendorNo);
        exit;
      end;

      RecRef.GETTABLE(RecordVariant);
      JobQueueEntry.INIT;
      JobQueueEntry."Object Type to Run" := JobQueueEntry."Object Type to Run"::Codeunit;
      JobQueueEntry."Object ID to Run" := CODEUNIT::"Document-Mailing";
      JobQueueEntry."Maximum No. of Attempts to Run" := 3;
      JobQueueEntry."Record ID to Process" := RecRef.RECORDID;
      JobQueueEntry."Parameter String" := STRSUBSTNO('%1|%2|%3|%4|%5',ReportUsage,DocNo,DocName,VendorNo,'Vendor');
      JobQueueEntry.Description := COPYSTR(DocName,1,MAXSTRLEN(JobQueueEntry.Description));
      CODEUNIT.RUN(CODEUNIT::"Job Queue - Enqueue",JobQueueEntry);
    END;

    LOCAL PROCEDURE SendEmailToCustDirectly@28(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;DocNo@1006 : Code[20];DocName@1004 : Text[150];ShowDialog@1007 : Boolean;CustNo@1010 : Code[20]) : Boolean;
    VAR
      TempAttachReportSelections@1008 : TEMPORARY Record "Report Selections";
      CustomReportSelection@1014 : Record "Custom Report Selection";
      ReportDistributionManagement@1002 : Codeunit "Report Distribution Management";
      FoundBody@1005 : Boolean;
      FoundAttachment@1011 : Boolean;
      ServerEmailBodyFilePath@1009 : Text[250];
      EmailAddress@1012 : Text[250];
    BEGIN
      InteractionMgt.SetEmailDraftLogging(true);
      BINDSUBSCRIPTION(ReportDistributionManagement);
      FoundBody := GetEmailBody(ServerEmailBodyFilePath,ReportUsage,RecordVariant,CustNo,EmailAddress);
      UNBINDSUBSCRIPTION(ReportDistributionManagement);
      InteractionMgt.SetEmailDraftLogging(false);
      FoundAttachment := FindEmailAttachmentUsage(ReportUsage,CustNo,TempAttachReportSelections);

      CustomReportSelection.SETRANGE("Source Type",DATABASE::Customer);
      CustomReportSelection.SETFILTER("Source No.",CustNo);
      exit(SendEmailDirectly(
          ReportUsage,RecordVariant,DocNo,DocName,FoundBody,FoundAttachment,ServerEmailBodyFilePath,EmailAddress,ShowDialog,
          TempAttachReportSelections,CustomReportSelection));
    END;

    LOCAL PROCEDURE SendEmailToVendorDirectly@37(ReportUsage@1005 : Integer;RecordVariant@1004 : Variant;DocNo@1003 : Code[20];DocName@1002 : Text[150];ShowDialog@1001 : Boolean;VendorNo@1000 : Code[20]) : Boolean;
    VAR
      TempAttachReportSelections@1014 : TEMPORARY Record "Report Selections";
      CustomReportSelection@1013 : Record "Custom Report Selection";
      FoundBody@1010 : Boolean;
      FoundAttachment@1009 : Boolean;
      ServerEmailBodyFilePath@1007 : Text[250];
      EmailAddress@1006 : Text[250];
    BEGIN
      InteractionMgt.SetEmailDraftLogging(true);
      FoundBody := GetEmailBodyVendor(ServerEmailBodyFilePath,ReportUsage,RecordVariant,VendorNo,EmailAddress);
      InteractionMgt.SetEmailDraftLogging(false);
      FoundAttachment := FindEmailAttachmentUsageVendor(ReportUsage,VendorNo,TempAttachReportSelections);

      CustomReportSelection.SETRANGE("Source Type",DATABASE::Vendor);
      CustomReportSelection.SETFILTER("Source No.",VendorNo);
      exit(SendEmailDirectly(
          ReportUsage,RecordVariant,DocNo,DocName,FoundBody,FoundAttachment,ServerEmailBodyFilePath,EmailAddress,ShowDialog,
          TempAttachReportSelections,CustomReportSelection));
    END;

    LOCAL PROCEDURE SendEmailDirectly@50(ReportUsage@1005 : Integer;RecordVariant@1004 : Variant;DocNo@1003 : Code[20];DocName@1002 : Text[150];FoundBody@1006 : Boolean;FoundAttachment@1007 : Boolean;ServerEmailBodyFilePath@1008 : Text[250];VAR DefaultEmailAddress@1010 : Text[250];ShowDialog@1009 : Boolean;VAR TempAttachReportSelections@1020 : TEMPORARY Record "Report Selections";VAR CustomReportSelection@1000 : Record "Custom Report Selection") AllEmailsWereSuccessful : Boolean;
    VAR
      DocumentMailing@1017 : Codeunit "Document-Mailing";
      OfficeAttachmentManager@1016 : Codeunit "Office Attachment Manager";
      ServerAttachmentFilePath@1013 : Text[250];
      EmailAddress@1001 : Text[250];
    BEGIN
      AllEmailsWereSuccessful := true;

      ShowNoBodyNoAttachmentError(ReportUsage,FoundBody,FoundAttachment);

      if FoundBody and not FoundAttachment then
        AllEmailsWereSuccessful :=
          DocumentMailing.EmailFile('','',ServerEmailBodyFilePath,DocNo,EmailAddress,DocName,not ShowDialog,ReportUsage);

      if not FoundBody then
        InteractionMgt.SetEmailDraftLogging(true);
      if FoundAttachment then begin
        if ReportUsage = Usage::JQ then begin
          Usage := ReportUsage;
          CustomReportSelection.SETFILTER(Usage,GETFILTER(Usage));
          if CustomReportSelection.FINDFIRST then
            if CustomReportSelection."Send To Email" <> '' then
              DefaultEmailAddress := CustomReportSelection."Send To Email";
        end;

        with TempAttachReportSelections do begin
          OfficeAttachmentManager.IncrementCount(COUNT - 1);
          repeat
            EmailAddress := COPYSTR(
                GetNextEmailAddressFromCustomReportSelection(CustomReportSelection,DefaultEmailAddress,Usage,Sequence),
                1,MAXSTRLEN(EmailAddress));
            ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
            AllEmailsWereSuccessful := AllEmailsWereSuccessful and DocumentMailing.EmailFile(
                ServerAttachmentFilePath,
                '',
                ServerEmailBodyFilePath,
                DocNo,
                EmailAddress,
                DocName,
                not ShowDialog,
                ReportUsage);
          until NEXT = 0;
        end;
      end;
      InteractionMgt.SetEmailDraftLogging(false);

      exit(AllEmailsWereSuccessful);
    END;

    [Internal]
    PROCEDURE SendToDisk@17(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;DocNo@1006 : Code[20];DocName@1007 : Text;CustNo@1004 : Code[20]);
    VAR
      TempReportSelections@1005 : TEMPORARY Record "Report Selections";
      ElectronicDocumentFormat@1008 : Record "Electronic Document Format";
      FileManagement@1010 : Codeunit "File Management";
      ServerAttachmentFilePath@1002 : Text[250];
      ClientAttachmentFileName@1009 : Text;
    BEGIN
      FindPrintUsage(ReportUsage,CustNo,TempReportSelections);
      with TempReportSelections do
        repeat
          ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
          ClientAttachmentFileName := ElectronicDocumentFormat.GetAttachmentFileName(DocNo,DocName,'pdf');
          FileManagement.DownloadHandler(
            ServerAttachmentFilePath,
            '',
            '',
            FileManagement.GetToFilterText('',ClientAttachmentFileName),
            ClientAttachmentFileName);
        until NEXT = 0;
    END;

    [Internal]
    PROCEDURE SendToDiskVendor@48(ReportUsage@1004 : Integer;RecordVariant@1003 : Variant;DocNo@1002 : Code[20];DocName@1001 : Text;VendorNo@1000 : Code[20]);
    VAR
      TempReportSelections@1009 : TEMPORARY Record "Report Selections";
      ElectronicDocumentFormat@1008 : Record "Electronic Document Format";
      FileManagement@1007 : Codeunit "File Management";
      ServerAttachmentFilePath@1006 : Text[250];
      ClientAttachmentFileName@1005 : Text;
    BEGIN
      FindPrintUsageVendor(ReportUsage,VendorNo,TempReportSelections);
      with TempReportSelections do
        repeat
          ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
          ClientAttachmentFileName := ElectronicDocumentFormat.GetAttachmentFileName(DocNo,DocName,'pdf');
          FileManagement.DownloadHandler(
            ServerAttachmentFilePath,
            '',
            '',
            FileManagement.GetToFilterText('',ClientAttachmentFileName),
            ClientAttachmentFileName);
        until NEXT = 0;
    END;

    [Internal]
    PROCEDURE SendToZip@18(ReportUsage@1003 : Integer;RecordVariant@1002 : Variant;DocNo@1001 : Code[20];CustNo@1007 : Code[20];VAR FileManagement@1000 : Codeunit "File Management");
    VAR
      TempReportSelections@1006 : TEMPORARY Record "Report Selections";
      ElectronicDocumentFormat@1005 : Record "Electronic Document Format";
      ServerAttachmentFilePath@1004 : Text;
    BEGIN
      FindPrintUsage(ReportUsage,CustNo,TempReportSelections);
      with TempReportSelections do
        repeat
          ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
          FileManagement.AddFileToZipArchive(
            ServerAttachmentFilePath,
            ElectronicDocumentFormat.GetAttachmentFileName(DocNo,'Invoice','pdf'));
        until NEXT = 0;
    END;

    [Internal]
    PROCEDURE SendToZipVendor@47(ReportUsage@1004 : Integer;RecordVariant@1003 : Variant;DocNo@1002 : Code[20];VendorNo@1001 : Code[20];VAR FileManagement@1000 : Codeunit "File Management");
    VAR
      TempReportSelections@1007 : TEMPORARY Record "Report Selections";
      ElectronicDocumentFormat@1006 : Record "Electronic Document Format";
      ServerAttachmentFilePath@1005 : Text;
    BEGIN
      FindPrintUsageVendor(ReportUsage,VendorNo,TempReportSelections);
      with TempReportSelections do
        repeat
          ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
          FileManagement.AddFileToZipArchive(
            ServerAttachmentFilePath,
            ElectronicDocumentFormat.GetAttachmentFileName(DocNo,'Purchase Order','pdf'));
        until NEXT = 0;
    END;

    [External]
    PROCEDURE GetCustEmailAddress@16(BillToCustomerNo@1000 : Code[20]) : Text[250];
    VAR
      Customer@1002 : Record Customer;
      Contact@1003 : Record Contact;
      ToAddress@1001 : Text;
    BEGIN
      if Customer.GET(BillToCustomerNo) then
        ToAddress := Customer."E-Mail"
      else
        if Contact.GET(BillToCustomerNo) then
          ToAddress := Contact."E-Mail";
      exit(ToAddress);
    END;

    [External]
    PROCEDURE GetVendorEmailAddress@35(BuyFromVendorNo@1000 : Code[20]) : Text[250];
    VAR
      Vendor@1002 : Record Vendor;
      ToAddress@1001 : Text;
    BEGIN
      if Vendor.GET(BuyFromVendorNo) then
        ToAddress := Vendor."E-Mail";

      exit(ToAddress);
    END;

    LOCAL PROCEDURE SaveReportAsPDF@14(ReportID@1000 : Integer;RecordVariant@1002 : Variant;LayoutCode@1003 : Code[20]) FilePath : Text[250];
    VAR
      ReportLayoutSelection@1004 : Record "Report Layout Selection";
      FileMgt@1001 : Codeunit "File Management";
    BEGIN
      FilePath := COPYSTR(FileMgt.ServerTempFileName('pdf'),1,250);

      ReportLayoutSelection.SetTempLayoutSelected(LayoutCode);
      REPORT.SAVEASPDF(ReportID,FilePath,RecordVariant);
      ReportLayoutSelection.SetTempLayoutSelected('');

      COMMIT;
    END;

    LOCAL PROCEDURE SaveReportAsHTML@15(ReportID@1003 : Integer;RecordVariant@1002 : Variant;LayoutCode@1004 : Code[20]) FilePath : Text[250];
    VAR
      ReportLayoutSelection@1000 : Record "Report Layout Selection";
      FileMgt@1001 : Codeunit "File Management";
    BEGIN
      FilePath := COPYSTR(FileMgt.ServerTempFileName('html'),1,250);

      ReportLayoutSelection.SetTempLayoutSelected(LayoutCode);
      REPORT.SAVEASHTML(ReportID,FilePath,RecordVariant);
      ReportLayoutSelection.SetTempLayoutSelected('');

      COMMIT;
    END;

    LOCAL PROCEDURE FindReportSelections@38(VAR ReportSelections@1000 : Record "Report Selections";CustNo@1001 : Code[20]) : Boolean;
    BEGIN
      if CopyCustomReportSectionToReportSelection(CustNo,ReportSelections) then
        exit(true);
      exit(CopyReportSelectionToReportSelection(ReportSelections));
    END;

    LOCAL PROCEDURE FindReportSelectionsVendor@36(VAR ReportSelections@1001 : Record "Report Selections";VendorNo@1000 : Code[20]) : Boolean;
    BEGIN
      if CopyCustomReportSectionToReportSelectionVendor(VendorNo,ReportSelections) then
        exit(true);
      exit(CopyReportSelectionToReportSelection(ReportSelections));
    END;

    LOCAL PROCEDURE CopyCustomReportSectionToReportSelection@21(CustNo@1002 : Code[20];VAR ToReportSelections@1001 : Record "Report Selections") : Boolean;
    VAR
      CustomReportSelection@1000 : Record "Custom Report Selection";
    BEGIN
      GetCustomReportSelectionByUsageFilter(CustomReportSelection,CustNo,GETFILTER(Usage));
      CopyToReportSelection(ToReportSelections,CustomReportSelection);

      if not ToReportSelections.FINDSET then
        exit(false);
      exit(true);
    END;

    LOCAL PROCEDURE CopyCustomReportSectionToReportSelectionVendor@39(VendorNo@1001 : Code[20];VAR ToReportSelections@1000 : Record "Report Selections") : Boolean;
    VAR
      CustomReportSelection@1002 : Record "Custom Report Selection";
    BEGIN
      GetCustomReportSelectionByUsageFilterVendor(CustomReportSelection,VendorNo,GETFILTER(Usage));
      CopyToReportSelection(ToReportSelections,CustomReportSelection);

      if not ToReportSelections.FINDSET then
        exit(false);
      exit(true);
    END;

    LOCAL PROCEDURE CopyToReportSelection@49(VAR ToReportSelections@1000 : Record "Report Selections";VAR CustomReportSelection@1002 : Record "Custom Report Selection");
    BEGIN
      ToReportSelections.RESET;
      ToReportSelections.DELETEALL;
      if CustomReportSelection.FINDSET then
        repeat
          ToReportSelections.Usage := CustomReportSelection.Usage;
          ToReportSelections.Sequence := FORMAT(CustomReportSelection.Sequence);
          ToReportSelections."Report ID" := CustomReportSelection."Report ID";
          ToReportSelections."Custom Report Layout Code" := CustomReportSelection."Custom Report Layout Code";
          ToReportSelections."Email Body Layout Code" := CustomReportSelection."Email Body Layout Code";
          ToReportSelections."Use for Email Attachment" := CustomReportSelection."Use for Email Attachment";
          ToReportSelections."Use for Email Body" := CustomReportSelection."Use for Email Body";
          ToReportSelections.INSERT;
        until CustomReportSelection.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyReportSelectionToReportSelection@22(VAR ToReportSelections@1000 : Record "Report Selections") : Boolean;
    BEGIN
      ToReportSelections.RESET;
      ToReportSelections.DELETEALL;
      if FINDSET then
        repeat
          ToReportSelections := Rec;
          ToReportSelections.INSERT;
        until NEXT = 0;

      exit(ToReportSelections.FINDSET);
    END;

    LOCAL PROCEDURE GetCustomReportSelection@23(VAR CustomReportSelection@1000 : Record "Custom Report Selection";CustNo@1001 : Code[20]) : Boolean;
    BEGIN
      CustomReportSelection.SETRANGE("Source Type",DATABASE::Customer);
      CustomReportSelection.SETFILTER("Source No.",CustNo);
      if CustomReportSelection.ISEMPTY then
        exit(false);

      CustomReportSelection.SETFILTER("Use for Email Attachment",GETFILTER("Use for Email Attachment"));
      CustomReportSelection.SETFILTER("Use for Email Body",GETFILTER("Use for Email Body"));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionVendor@44(VAR CustomReportSelection@1001 : Record "Custom Report Selection";VendorNo@1000 : Code[20]) : Boolean;
    BEGIN
      CustomReportSelection.SETRANGE("Source Type",DATABASE::Vendor);
      CustomReportSelection.SETFILTER("Source No.",VendorNo);
      if CustomReportSelection.ISEMPTY then
        exit(false);

      CustomReportSelection.SETFILTER("Use for Email Attachment",GETFILTER("Use for Email Attachment"));
      CustomReportSelection.SETFILTER("Use for Email Body",GETFILTER("Use for Email Body"));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionByUsageFilter@24(VAR CustomReportSelection@1002 : Record "Custom Report Selection";CustNo@1001 : Code[20];ReportUsageFilter@1000 : Text) : Boolean;
    BEGIN
      CustomReportSelection.SETFILTER(Usage,ReportUsageFilter);
      exit(GetCustomReportSelection(CustomReportSelection,CustNo));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionByUsageFilterVendor@41(VAR CustomReportSelection@1002 : Record "Custom Report Selection";VendorNo@1001 : Code[20];ReportUsageFilter@1000 : Text) : Boolean;
    BEGIN
      CustomReportSelection.SETFILTER(Usage,ReportUsageFilter);
      exit(GetCustomReportSelectionVendor(CustomReportSelection,VendorNo));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionByUsageOption@25(VAR CustomReportSelection@1002 : Record "Custom Report Selection";CustNo@1001 : Code[20];ReportUsage@1000 : Integer) : Boolean;
    BEGIN
      CustomReportSelection.SETRANGE(Usage,ReportUsage);
      exit(GetCustomReportSelection(CustomReportSelection,CustNo));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionByUsageOptionVendor@46(VAR CustomReportSelection@1002 : Record "Custom Report Selection";VendorNo@1001 : Code[20];ReportUsage@1000 : Integer) : Boolean;
    BEGIN
      CustomReportSelection.SETRANGE(Usage,ReportUsage);
      exit(GetCustomReportSelectionVendor(CustomReportSelection,VendorNo));
    END;

    LOCAL PROCEDURE GetNextEmailAddressFromCustomReportSelection@54(VAR CustomReportSelection@1001 : Record "Custom Report Selection";DefaultEmailAddress@1000 : Text;UsageValue@1002 : Option;SequenceText@1003 : Text) : Text;
    VAR
      SequenceInteger@1004 : Integer;
    BEGIN
      if EVALUATE(SequenceInteger,SequenceText) then begin
        CustomReportSelection.SETRANGE(Usage,UsageValue);
        CustomReportSelection.SETRANGE(Sequence,SequenceInteger);
        if CustomReportSelection.FINDFIRST then
          if CustomReportSelection."Send To Email" <> '' then
            exit(CustomReportSelection."Send To Email");
      end;
      exit(DefaultEmailAddress);
    END;

    [External]
    PROCEDURE PrintForUsage@26(ReportUsage@1000 : Integer);
    BEGIN
      FilterPrintUsage(ReportUsage);
      if FINDSET then
        repeat
          REPORT.RUNMODAL("Report ID",true);
        until NEXT = 0;
    END;

    LOCAL PROCEDURE FindEmailAddressForEmailLayout@27(LayoutCode@1002 : Code[20];CustNo@1001 : Code[20];ReportUsage@1000 : Integer) : Text[200];
    VAR
      CustomReportSelection@1003 : Record "Custom Report Selection";
    BEGIN
      // Search for a potential email address from Custom Report Selections
      GetCustomReportSelectionByUsageOption(CustomReportSelection,CustNo,ReportUsage);
      CustomReportSelection.SETFILTER("Send To Email",'<>%1','');
      CustomReportSelection.SETRANGE("Email Body Layout Code",LayoutCode);
      if CustomReportSelection.FINDFIRST then
        exit(CustomReportSelection."Send To Email");

      // Relax the filter and search for an email address
      CustomReportSelection.SETFILTER("Use for Email Body",'');
      CustomReportSelection.SETRANGE("Email Body Layout Code",'');
      if CustomReportSelection.FINDFIRST then
        exit(CustomReportSelection."Send To Email");
      exit('');
    END;

    LOCAL PROCEDURE FindEmailAddressForEmailLayoutVendor@43(LayoutCode@1002 : Code[20];VendorNo@1001 : Code[20];ReportUsage@1000 : Integer) : Text[200];
    VAR
      CustomReportSelection@1003 : Record "Custom Report Selection";
    BEGIN
      // Search for a potential email address from Custom Report Selections
      GetCustomReportSelectionByUsageOptionVendor(CustomReportSelection,VendorNo,ReportUsage);
      CustomReportSelection.SETFILTER("Send To Email",'<>%1','');
      CustomReportSelection.SETRANGE("Email Body Layout Code",LayoutCode);
      if CustomReportSelection.FINDFIRST then
        exit(CustomReportSelection."Send To Email");

      // Relax the filter and search for an email address
      CustomReportSelection.SETFILTER("Use for Email Body",'');
      CustomReportSelection.SETRANGE("Email Body Layout Code",'');
      if CustomReportSelection.FINDFIRST then
        exit(CustomReportSelection."Send To Email");
      exit('');
    END;

    LOCAL PROCEDURE ShowNoBodyNoAttachmentError@51(ReportUsage@1000 : Integer;FoundBody@1001 : Boolean;FoundAttachment@1002 : Boolean);
    BEGIN
      if not (FoundBody or FoundAttachment) then begin
        Usage := ReportUsage;
        ERROR(MustSelectAndEmailBodyOrAttahmentErr,Usage);
      end;
    END;

    PROCEDURE ReportUsageToDocumentType@53(VAR DocumentType@1001 : Option;ReportUsage@1000 : Integer) : Boolean;
    VAR
      SalesHeader@1002 : Record "Sales Header";
    BEGIN
      case ReportUsage of
        Usage::"S.Invoice",Usage::"S.Invoice Draft",Usage::"P.Invoice":
          DocumentType := SalesHeader."Document Type"::Invoice;
        Usage::"S.Quote",Usage::"P.Quote":
          DocumentType := SalesHeader."Document Type"::Quote;
        Usage::"S.Cr.Memo",Usage::"P.Cr.Memo":
          DocumentType := SalesHeader."Document Type"::"Credit Memo";
        Usage::"S.Order",Usage::"P.Order":
          DocumentType := SalesHeader."Document Type"::Order;
        else
          exit(false);
      end;
      exit(true);
    END;

    [Internal]
    PROCEDURE SendEmailInForeground@55(DocRecordID@1007 : RecordID;DocNo@1008 : Code[20];DocName@1009 : Text[150];ReportUsage@1010 : Integer;SourceIsCustomer@1011 : Boolean;SourceNo@1001 : Code[20]) : Boolean;
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      // Blocks the user until the email is sent; use SendEmailInBackground for normal purposes.

      RecRef.GET(DocRecordID);
      RecRef.LOCKTABLE;
      RecRef.FIND;
      RecRef.SETRECFILTER;

      if SourceIsCustomer then
        exit(SendEmailToCustDirectly(ReportUsage,RecRef,DocNo,DocName,false,SourceNo));

      exit(SendEmailToVendorDirectly(ReportUsage,RecRef,DocNo,DocName,false,SourceNo));
    END;

    BEGIN
    END.
  }
}

