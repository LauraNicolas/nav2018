OBJECT Page 605 IC Chart of Accounts
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Intercompany Chart of Accounts;
    SourceTable="IC G/L Account";
    PageType=List;
    CardPageID="IC G/L Account Card";
    PromotedActionCategoriesML=ENU=New,Process,Report,Import/Export;
    OnAfterGetRecord=BEGIN
                       NameIndent := 0;
                       FormatLine;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      Name=ActionContainer1900000004;
                      ActionContainerType=ActionItems }
      { 14      ;1   ;ActionGroup;
                      Name=F&unctions;
                      CaptionML=ENU=F&unctions;
                      Image=Action }
      { 18      ;2   ;Action    ;
                      Name=Map to Acc. with Same No.;
                      CaptionML=ENU=Map to Acc. with Same No.;
                      ToolTipML=ENU=Map the selected intercompany G/L accounts to G/L accounts with the same number.;
                      ApplicationArea=#Intercompany;
                      Promoted=true;
                      Image=MapAccounts;
                      PromotedCategory=Process;
                      PromotedOnly=true;
                      OnAction=VAR
                                 ICGLAcc@1001 : Record "IC G/L Account";
                                 ICMapping@1000 : Codeunit "IC Mapping";
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ICGLAcc);
                                 if ICGLAcc.FIND('-') and CONFIRM(Text000) then
                                   repeat
                                     ICMapping.MapAccounts(ICGLAcc);
                                   until ICGLAcc.NEXT = 0;
                               END;
                                }
      { 23      ;2   ;Action    ;
                      Name=Copy from Chart of Accounts;
                      CaptionML=ENU=Copy from Chart of Accounts;
                      ToolTipML=ENU=Create intercompany G/L accounts from G/L accounts.;
                      ApplicationArea=#Intercompany;
                      Promoted=true;
                      Image=CopyFromChartOfAccounts;
                      PromotedCategory=Process;
                      PromotedOnly=true;
                      OnAction=BEGIN
                                 CopyFromChartOfAccounts;
                               END;
                                }
      { 20      ;2   ;Action    ;
                      Name=In&dent IC Chart of Accounts;
                      CaptionML=ENU=In&dent IC Chart of Accounts;
                      ToolTipML=ENU=Indent accounts between a Begin-Total and the matching End-Total one level to make the chart of accounts easier to read.;
                      ApplicationArea=#Intercompany;
                      Promoted=true;
                      Image=Indent;
                      PromotedCategory=Process;
                      PromotedOnly=true;
                      OnAction=VAR
                                 IndentCOA@1000 : Codeunit "G/L Account-Indent";
                               BEGIN
                                 IndentCOA.RunICAccountIndent;
                               END;
                                }
      { 21      ;2   ;Separator ;
                      Name=Separator21 }
      { 17      ;2   ;Action    ;
                      Name=Import;
                      Ellipsis=true;
                      CaptionML=ENU=Import;
                      ToolTipML=ENU=Import an intercompany chart of accounts from a file.;
                      ApplicationArea=#Intercompany;
                      Promoted=true;
                      Image=Import;
                      PromotedCategory=Category4;
                      PromotedOnly=true;
                      OnAction=BEGIN
                                 ImportFromXML;
                               END;
                                }
      { 22      ;2   ;Action    ;
                      Name=E&xport;
                      Ellipsis=true;
                      CaptionML=ENU=E&xport;
                      ToolTipML=ENU=Export the intercompany chart of accounts to a file.;
                      ApplicationArea=#Intercompany;
                      Promoted=true;
                      Image=Export;
                      PromotedCategory=Category4;
                      PromotedOnly=true;
                      OnAction=BEGIN
                                 ExportToXML;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                IndentationColumnName=NameIndent;
                IndentationControls=Name;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Name=No.;
                ToolTipML=ENU=Specifies the number of the record.;
                ApplicationArea=#Intercompany;
                SourceExpr="No.";
                Style=Strong;
                StyleExpr=Emphasize;
                ImplicitType=Code20 }

    { 4   ;2   ;Field     ;
                Name=Name;
                ToolTipML=ENU=Specifies the name of the IC general ledger account.;
                ApplicationArea=#Intercompany;
                SourceExpr=Name;
                Style=Strong;
                StyleExpr=Emphasize;
                ImplicitType=Text50 }

    { 8   ;2   ;Field     ;
                Name=Income/Balance;
                ToolTipML=ENU=Specifies whether a general ledger account is an income statement account or a balance sheet account.;
                ApplicationArea=#Intercompany;
                SourceExpr="Income/Balance";
                ImplicitType=Option }

    { 6   ;2   ;Field     ;
                Name=Account Type;
                ToolTipML=ENU=Specifies the purpose of the account. Newly created accounts are automatically assigned the Posting account type, but you can change this.;
                ApplicationArea=#Intercompany;
                SourceExpr="Account Type";
                ImplicitType=Option }

    { 10  ;2   ;Field     ;
                Name=Blocked;
                ToolTipML=ENU=Specifies if the record is blocked from being used.;
                ApplicationArea=#Intercompany;
                SourceExpr=Blocked;
                ImplicitType=Boolean }

    { 12  ;2   ;Field     ;
                Name=Map-to G/L Acc. No.;
                ToolTipML=ENU=Specifies the number of the G/L account in your chart of accounts that corresponds to this intercompany G/L account.;
                ApplicationArea=#Intercompany;
                SourceExpr="Map-to G/L Acc. No.";
                ImplicitType=Code20 }

    { 1900000007;0;Container;
                Name=Container1900000007;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Name=Part1900383207;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Name=Part1905767507;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Are you sure you want to map the selected lines?';
      Text001@1002 : TextConst 'ENU=Select file to import into %1';
      Text002@1001 : TextConst 'ENU=ICGLAcc.xml';
      Text004@1004 : TextConst 'ENU=Are you sure you want to copy from %1?';
      Emphasize@19018670 : Boolean INDATASET;
      NameIndent@19079073 : Integer INDATASET;
      Text005@1005 : TextConst 'ENU=Enter the file name.';
      Text006@1006 : TextConst 'ENU=XML Files (*.xml)|*.xml|All Files (*.*)|*.*';

    LOCAL PROCEDURE CopyFromChartOfAccounts@1();
    VAR
      GLAcc@1000 : Record "G/L Account";
      ICGLAcc@1001 : Record "IC G/L Account";
      ChartofAcc@1005 : Page "Chart of Accounts";
      ICGLAccEmpty@1002 : Boolean;
      ICGLAccExists@1003 : Boolean;
      PrevIndentation@1004 : Integer;
    BEGIN
      if not CONFIRM(Text004,false,ChartofAcc.CAPTION) then
        exit;

      ICGLAccEmpty := not ICGLAcc.FINDFIRST;
      ICGLAcc.LOCKTABLE;
      if GLAcc.FIND('-') then
        repeat
          if GLAcc."Account Type" = GLAcc."Account Type"::"End-Total" then
            PrevIndentation := PrevIndentation - 1;
          if not ICGLAccEmpty then
            ICGLAccExists := ICGLAcc.GET(GLAcc."No.");
          if not ICGLAccExists and not GLAcc.Blocked then begin
            ICGLAcc.INIT;
            ICGLAcc."No." := GLAcc."No.";
            ICGLAcc.Name := GLAcc.Name;
            ICGLAcc."Account Type" := GLAcc."Account Type";
            ICGLAcc."Income/Balance" := GLAcc."Income/Balance";
            ICGLAcc.Indentation := PrevIndentation;
            ICGLAcc.INSERT;
          end;
          PrevIndentation := GLAcc.Indentation;
          if GLAcc."Account Type" = GLAcc."Account Type"::"Begin-Total" then
            PrevIndentation := PrevIndentation + 1;
        until GLAcc.NEXT = 0;
    END;

    LOCAL PROCEDURE ImportFromXML@2();
    VAR
      CompanyInfo@1006 : Record "Company Information";
      ICGLAccIO@1002 : XMLport "IC G/L Account Import/Export";
      IFile@1000 : File;
      IStr@1001 : InStream;
      FileName@1004 : Text[1024];
      StartFileName@1003 : Text[1024];
    BEGIN
      CompanyInfo.GET;

      StartFileName := CompanyInfo."IC Inbox Details";
      if StartFileName <> '' then begin
        if StartFileName[STRLEN(StartFileName)] <> '\' then
          StartFileName := StartFileName + '\';
        StartFileName := StartFileName + '*.xml';
      end;

      if not UPLOAD(STRSUBSTNO(Text001,TABLECAPTION),'',Text006,StartFileName,FileName) then
        ERROR(Text005);

      IFile.OPEN(FileName);
      IFile.CREATEINSTREAM(IStr);
      ICGLAccIO.SETSOURCE(IStr);
      ICGLAccIO.IMPORT;
    END;

    LOCAL PROCEDURE ExportToXML@3();
    VAR
      CompanyInfo@1006 : Record "Company Information";
      FileMgt@1000 : Codeunit "File Management";
      ICGLAccIO@1005 : XMLport "IC G/L Account Import/Export";
      OFile@1002 : File;
      OStr@1001 : OutStream;
      FileName@1004 : Text;
      DefaultFileName@1003 : Text;
    BEGIN
      CompanyInfo.GET;

      DefaultFileName := CompanyInfo."IC Inbox Details";
      if DefaultFileName <> '' then
        if DefaultFileName[STRLEN(DefaultFileName)] <> '\' then
          DefaultFileName := DefaultFileName + '\';
      DefaultFileName := DefaultFileName + Text002;

      FileName := FileMgt.ServerTempFileName('xml');
      if FileName = '' then
        exit;

      OFile.CREATE(FileName);
      OFile.CREATEOUTSTREAM(OStr);
      ICGLAccIO.SETDESTINATION(OStr);
      ICGLAccIO.EXPORT;
      OFile.CLOSE;
      CLEAR(OStr);

      DOWNLOAD(FileName,'Export',TEMPORARYPATH,'',DefaultFileName);
    END;

    LOCAL PROCEDURE FormatLine@19039177();
    BEGIN
      NameIndent := Indentation;
      Emphasize := "Account Type" <> "Account Type"::Posting;
    END;

    BEGIN
    END.
  }
}

