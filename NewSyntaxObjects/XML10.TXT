OBJECT XMLport 10 IC G/L Account Import/Export
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=IC G/L Account Import/Export;
    OnPreXMLport=VAR
                   ICGLAcc@1000 : Record "IC G/L Account";
                 BEGIN
                   TempICGLAcc.DELETEALL;
                   if ICGLAcc.FIND('-') then
                     repeat
                       TempICGLAcc := ICGLAcc;
                       TempICGLAcc.INSERT;
                     until ICGLAcc.NEXT = 0;
                 END;

    OnPostXMLport=VAR
                    OrgICGLAcc@1000 : Record "IC G/L Account";
                    MsgTxt@1001 : Text[1024];
                  BEGIN
                    if XMLInbound then begin
                      if TempICGLAcc.FIND('-') then
                        repeat
                          Deleted := Deleted + 1;
                          OrgICGLAcc.GET(TempICGLAcc."No.");
                          OrgICGLAcc.DELETE;
                        until TempICGLAcc.NEXT = 0;

                      if Inserted > 0 then
                        if Inserted = 1 then
                          MsgTxt := STRSUBSTNO(Text001,Inserted,OrgICGLAcc.TABLECAPTION)
                        else
                          MsgTxt := STRSUBSTNO(Text002,Inserted);

                      if Modified > 0 then begin
                        if MsgTxt <> '' then
                          MsgTxt := MsgTxt + '\';
                        if Modified = 1 then
                          MsgTxt := MsgTxt + STRSUBSTNO(Text003,Modified,OrgICGLAcc.TABLECAPTION)
                        else
                          MsgTxt := MsgTxt + STRSUBSTNO(Text004,Modified);
                      end;

                      if Deleted > 0 then begin
                        if MsgTxt <> '' then
                          MsgTxt := MsgTxt + '\';
                        if Deleted = 1 then
                          MsgTxt := MsgTxt + STRSUBSTNO(Text005,Deleted,OrgICGLAcc.TABLECAPTION)
                        else
                          MsgTxt := MsgTxt + STRSUBSTNO(Text006,Deleted);
                      end;

                      if Inserted + Deleted + Modified = 0 then
                        MsgTxt := Text000;

                      MESSAGE(MsgTxt);
                    end;
                  END;

  }
  ELEMENTS
  {
    { ['{1E89538F-0A82-40BD-8DF1-44A5C22F9C57}'];;GLAccounts          ;Element ;Text     }

    { ['{9D19931E-BC36-42C2-B6CD-B7EFB0661449}'];1;ICGLAccount        ;Element ;Table   ;
                                                  VariableName=ICGLAcc;
                                                  SourceTable="IC G/L Account";
                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 OrgICGLAcc@1000 : Record "IC G/L Account";
                                                                               BEGIN
                                                                                 XMLInbound := true;

                                                                                 if TempICGLAcc.GET(ICGLAcc."No.") then begin
                                                                                   if (ICGLAcc.Name <> TempICGLAcc.Name) or (ICGLAcc."Account Type" <> TempICGLAcc."Account Type") or
                                                                                      (ICGLAcc."Income/Balance" <> TempICGLAcc."Income/Balance") or (ICGLAcc.Blocked <> TempICGLAcc.Blocked)
                                                                                   then
                                                                                     Modified := Modified + 1;
                                                                                   ICGLAcc."Map-to G/L Acc. No." := TempICGLAcc."Map-to G/L Acc. No.";
                                                                                   OrgICGLAcc.GET(ICGLAcc."No.");
                                                                                   OrgICGLAcc.DELETE;
                                                                                   TempICGLAcc.DELETE;
                                                                                 end else
                                                                                   Inserted := Inserted + 1;
                                                                               END;
                                                                                }

    { ['{11763B3F-4E93-45E1-9E70-53CCC8AF0957}'];2;No                 ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=ICGLAcc::No. }

    { ['{713D14AA-F768-41E1-95A9-E5BF8E79EDC3}'];2;Name               ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=ICGLAcc::Name }

    { ['{44D2D0F5-F6CC-4A14-815E-65890604ACE2}'];2;AccountType        ;Attribute;Field  ;
                                                  DataType=Option;
                                                  SourceField=ICGLAcc::Account Type }

    { ['{EC03F478-8924-4A44-8D4A-4A58DCBC4497}'];2;IncomeBalance      ;Attribute;Field  ;
                                                  DataType=Option;
                                                  SourceField=ICGLAcc::Income/Balance }

    { ['{6A89E279-D0B2-4487-97DB-3E6784F861E0}'];2;Blocked            ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=ICGLAcc::Blocked }

    { ['{1D6A6364-E468-480F-BE29-0E50ED6179F7}'];2;Indentation        ;Attribute;Field  ;
                                                  DataType=Integer;
                                                  SourceField=ICGLAcc::Indentation }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      TempICGLAcc@1000 : TEMPORARY Record "IC G/L Account";
      XMLInbound@1011 : Boolean;
      Inserted@1003 : Integer;
      Deleted@1002 : Integer;
      Modified@1001 : Integer;
      Text000@1010 : TextConst 'ENU=There were no changes.';
      Text001@1004 : TextConst 'ENU=%1 %2 was added.';
      Text002@1005 : TextConst 'ENU=%1 IC G/L Accounts were added.';
      Text003@1007 : TextConst 'ENU=%1 %2 was updated.';
      Text004@1006 : TextConst 'ENU=%1 IC G/L Accounts were updated.';
      Text005@1009 : TextConst 'ENU=%1 %2 was deleted.';
      Text006@1008 : TextConst 'ENU=%1 IC G/L Accounts were deleted.';

    BEGIN
    END.
  }
}

