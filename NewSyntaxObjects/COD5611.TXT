OBJECT Codeunit 5611 Calculate Normal Depreciation
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "FA Ledger Entry"=r,
                TableData "FA Posting Type Setup"=r;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="Force No. of Days must only be specified if %1 %2 = %3."';
      Text001@1001 : TextConst 'ENU=%2 must not be 100 for %1.';
      Text002@1002 : TextConst 'ENU="%2 must be %3 if %4 %5 = %6 for %1."';
      Text003@1003 : TextConst 'ENU=%2 must not be later than %3 for %1.';
      Text004@1004 : TextConst 'ENU=%1 %2 must not be used together with the Half-Year Convention for %3.';
      FA@1005 : Record "Fixed Asset";
      FALedgEntry@1006 : Record "FA Ledger Entry";
      DeprBook@1007 : Record "Depreciation Book";
      FADeprBook@1008 : Record "FA Depreciation Book";
      DepreciationCalc@1009 : Codeunit "Depreciation Calculation";
      DeprBookCode@1010 : Code[10];
      DaysInFiscalYear@1011 : Integer;
      EntryAmounts@1012 : ARRAY [4] OF Decimal;
      MinusBookValue@1013 : Decimal;
      DateFromProjection@1014 : Date;
      SkipOnZero@1015 : Boolean;
      UntilDate@1016 : Date;
      Sign@1017 : Integer;
      FirstDeprDate@1018 : Date;
      NumberOfDays@1019 : Integer;
      NumberOfDays2@1020 : Integer;
      DaysInPeriod@1021 : Integer;
      UseDeprStartingDate@1022 : Boolean;
      BookValue@1023 : Decimal;
      BookValue2@1024 : Decimal;
      DeprBasis@1025 : Decimal;
      SalvageValue@1026 : Decimal;
      SalvageValue2@1027 : Decimal;
      AcquisitionDate@1028 : Date;
      DisposalDate@1029 : Date;
      DeprMethod@1030 : 'StraightLine,DB1,DB2,DB1SL,DB2SL,"User-Defined",Manual,BelowZero';
      DeprStartingDate@1031 : Date;
      FirstUserDefinedDeprDate@1032 : Date;
      SLPercent@1033 : Decimal;
      DBPercent@1034 : Decimal;
      FixedAmount@1035 : Decimal;
      DeprYears@1036 : Decimal;
      DeprTableCode@1037 : Code[10];
      FinalRoundingAmount@1038 : Decimal;
      EndingBookValue@1039 : Decimal;
      AmountBelowZero@1040 : Decimal;
      PercentBelowZero@1041 : Decimal;
      StartingDate@1042 : Date;
      EndingDate@1043 : Date;
      Factor@1044 : Decimal;
      UseHalfYearConvention@1045 : Boolean;
      NewYearDate@1046 : Date;
      DeprInTwoFiscalYears@1047 : Boolean;
      TempDeprAmount@1048 : Decimal;
      Text005@1049 : TextConst 'ENU=%1 must not be used together with the Half-Year Convention for %2.';
      Text006@1050 : TextConst 'ENU=%1 must be %2 or later for %3.';
      Text007@1051 : TextConst 'ENU=%1 must not be used together with %2 for %3.';
      Text008@1052 : TextConst 'ENU="%1 must not be used together with %2 = %3 for %4."';
      Year365Days@1053 : Boolean;

    [External]
    PROCEDURE Calculate@1(VAR DeprAmount@1000 : Decimal;VAR NumberOfDays4@1001 : Integer;FANo@1002 : Code[20];DeprBookCode2@1003 : Code[10];UntilDate2@1004 : Date;EntryAmounts2@1005 : ARRAY [4] OF Decimal;DateFromProjection2@1006 : Date;DaysInPeriod2@1007 : Integer);
    VAR
      i@1008 : Integer;
    BEGIN
      CLEARALL;
      DeprAmount := 0;
      NumberOfDays4 := 0;
      DeprBookCode := DeprBookCode2;
      FA.GET(FANo);
      DeprBook.GET(DeprBookCode);
      if not FADeprBook.GET(FANo,DeprBookCode) then
        exit;
      UntilDate := UntilDate2;
      for i := 1 to 4 do
        EntryAmounts[i] := EntryAmounts2[i];
      DateFromProjection := DateFromProjection2;
      DaysInPeriod := DaysInPeriod2;

      FALedgEntry.LOCKTABLE;
      with DeprBook do
        if DaysInPeriod > 0 then
          if "Periodic Depr. Date Calc." <> "Periodic Depr. Date Calc."::"Last Entry" then begin
            "Periodic Depr. Date Calc." := "Periodic Depr. Date Calc."::"Last Entry";
            ERROR(
              Text000,
              TABLECAPTION,FIELDCAPTION("Periodic Depr. Date Calc."),"Periodic Depr. Date Calc.");
          end;
      TransferValues;
      if not SkipRecord then begin
        Sign := 1;
        if not FADeprBook."Use FA Ledger Check" then begin
          if DeprBook."Use FA Ledger Check" then
            FADeprBook.TESTFIELD("Use FA Ledger Check",true);
          FADeprBook.TESTFIELD("Fixed Depr. Amount below Zero",0);
          FADeprBook.TESTFIELD("Depr. below Zero %",0);
          Sign := DepreciationCalc.GetSign(BookValue,DeprBasis,SalvageValue,MinusBookValue);
          if Sign = 0 then
            exit;
          if Sign = -1 then
            DepreciationCalc.GetNewSigns(BookValue,DeprBasis,SalvageValue,MinusBookValue);
        end;
        if (FADeprBook."Fixed Depr. Amount below Zero" > 0) or
           (FADeprBook."Depr. below Zero %" > 0)
        then
          FADeprBook.TESTFIELD("Use FA Ledger Check",true);

        if BookValue + SalvageValue <= 0 then
          SkipOnZero := true;
        if (SalvageValue >= 0) and (BookValue <= EndingBookValue) then
          SkipOnZero := true;

        if not
           (SkipOnZero and
            not DeprBook."Allow Depr. below Zero" and
            not DeprBook."Use FA Ledger Check")
        then begin
          if SkipOnZero then
            DeprMethod := DeprMethod::BelowZero;
          DeprAmount := Sign * CalculateDeprAmount;
          if Sign * DeprAmount > 0 then
            DeprAmount := 0;
          NumberOfDays4 := NumberOfDays2;
        end;
      end;
    END;

    LOCAL PROCEDURE SkipRecord@4() : Boolean;
    BEGIN
      exit(
        (DisposalDate > 0D) or
        (AcquisitionDate = 0D) or
        (DeprMethod = DeprMethod::Manual) or
        (AcquisitionDate > UntilDate) or
        FA.Inactive or
        FA.Blocked);
    END;

    LOCAL PROCEDURE CalculateDeprAmount@2() : Decimal;
    VAR
      Amount@1000 : Decimal;
    BEGIN
      with FA do begin
        if DateFromProjection > 0D then
          FirstDeprDate := DateFromProjection
        else begin
          FirstDeprDate := DepreciationCalc.GetFirstDeprDate("No.",DeprBookCode,Year365Days);
          if FirstDeprDate > UntilDate then
            exit(0);
          UseDeprStartingDate := DepreciationCalc.UseDeprStartingDate("No.",DeprBookCode);
          if UseDeprStartingDate then
            FirstDeprDate := DeprStartingDate;
        end;
        if FirstDeprDate < DeprStartingDate then
          FirstDeprDate := DeprStartingDate;
        NumberOfDays := DepreciationCalc.DeprDays(FirstDeprDate,UntilDate,Year365Days);
        Factor := 1;
        if NumberOfDays <= 0 then
          exit(0);
        if DaysInPeriod > 0 then begin
          Factor := DaysInPeriod / NumberOfDays;
          NumberOfDays := DaysInPeriod;
        end;
        UseHalfYearConvention := SetHalfYearConventionMethod;
        // Method Last Entry
        if UseDeprStartingDate or
           (DateFromProjection > 0D) or
           (DeprMethod = DeprMethod::BelowZero) or
           (DeprBook."Periodic Depr. Date Calc." = DeprBook."Periodic Depr. Date Calc."::"Last Entry")
        then begin
          NumberOfDays2 := NumberOfDays;
          if UseHalfYearConvention then
            Amount := CalcHalfYearConventionDepr
          else
            case DeprMethod of
              DeprMethod::StraightLine:
                Amount := CalcSLAmount;
              DeprMethod::DB1:
                Amount := CalcDB1Amount;
              DeprMethod::DB2:
                Amount := CalcDB2Amount;
              DeprMethod::DB1SL,
              DeprMethod::DB2SL:
                Amount := CalcDBSLAmount;
              DeprMethod::Manual:
                Amount := 0;
              DeprMethod::"User-Defined":
                Amount := CalcUserDefinedAmount(UntilDate);
              DeprMethod::BelowZero:
                Amount := DepreciationCalc.CalcRounding(DeprBookCode,CalcBelowZeroAmount);
            end;
        end
          // Method Last Depreciation Entry
        else begin
          if UseHalfYearConvention then
            DeprBook.TESTFIELD(
              "Periodic Depr. Date Calc.",DeprBook."Periodic Depr. Date Calc."::"Last Entry");
          Amount := 0;
          StartingDate := 0D;
          EndingDate := 0D;
          DepreciationCalc.GetDeprPeriod(
            "No.",DeprBookCode,UntilDate,StartingDate,EndingDate,NumberOfDays,Year365Days);
          FirstDeprDate := StartingDate;
          NumberOfDays2 := DepreciationCalc.DeprDays(FirstDeprDate,UntilDate,Year365Days);
          while NumberOfDays > 0 do begin
            DepreciationCalc.CalculateDeprInPeriod(
              "No.",DeprBookCode,EndingDate,Amount,Sign,
              BookValue,DeprBasis,SalvageValue,MinusBookValue);
            if DepreciationCalc.GetSign(
                 BookValue,DeprBasis,SalvageValue,MinusBookValue) <> 1
            then
              exit(0);
            case DeprMethod of
              DeprMethod::StraightLine:
                Amount := Amount + CalcSLAmount;
              DeprMethod::DB1:
                Amount := Amount + CalcDB1Amount;
              DeprMethod::DB2:
                Amount := Amount + CalcDB2Amount;
              DeprMethod::Manual:
                Amount := 0;
              DeprMethod::"User-Defined":
                Amount := Amount + CalcUserDefinedAmount(EndingDate);
            end;
            DepreciationCalc.GetDeprPeriod(
              "No.",DeprBookCode,UntilDate,StartingDate,EndingDate,NumberOfDays,Year365Days);
            FirstDeprDate := StartingDate;
          end;
        end;
      end;
      if Amount >= 0 then
        exit(0);
      if not SkipOnZero then
        DepreciationCalc.AdjustDepr(
          DeprBookCode,Amount,ABS(BookValue2),-ABS(SalvageValue2),
          EndingBookValue,FinalRoundingAmount);
      exit(ROUND(Amount));
    END;

    LOCAL PROCEDURE CalcTempDeprAmount@19(VAR DeprAmount@1000 : Decimal) : Boolean;
    BEGIN
      DeprAmount := 0;
      if FADeprBook."Temp. Ending Date" = 0D then
        exit(false);
      if (FirstDeprDate <= FADeprBook."Temp. Ending Date") and (UntilDate > FADeprBook."Temp. Ending Date") then
        ERROR(
          Text006,
          FADeprBook.FIELDCAPTION("Temp. Ending Date"),
          UntilDate,
          FAName);
      if FADeprBook."Temp. Ending Date" >= UntilDate then begin
        if FADeprBook."Use Half-Year Convention" then
          ERROR(
            Text005,
            FADeprBook.FIELDCAPTION("Temp. Ending Date"),
            FAName);
        if FADeprBook."Use DB% First Fiscal Year" then
          ERROR(
            Text007,
            FADeprBook.FIELDCAPTION("Temp. Ending Date"),
            FADeprBook.FIELDCAPTION("Use DB% First Fiscal Year"),
            FAName);
        if FADeprBook."Depreciation Method" = FADeprBook."Depreciation Method"::"User-Defined" then
          ERROR(
            Text008,
            FADeprBook.FIELDCAPTION("Temp. Ending Date"),
            FADeprBook.FIELDCAPTION("Depreciation Method"),
            FADeprBook."Depreciation Method",
            FAName);
        if DeprMethod = DeprMethod::BelowZero then
          ERROR(
            Text007,
            FADeprBook.FIELDCAPTION("Temp. Ending Date"),
            DeprBook.FIELDCAPTION("Allow Depr. below Zero"),
            FAName);
        DeprBook.TESTFIELD(
          "Periodic Depr. Date Calc.",DeprBook."Periodic Depr. Date Calc."::"Last Entry");
        DeprAmount := -(NumberOfDays / DaysInFiscalYear) * FADeprBook."Temp. Fixed Depr. Amount";
        exit(true)
      end;
      exit(false);
    END;

    LOCAL PROCEDURE CalcSLAmount@6() : Decimal;
    VAR
      RemainingLife@1000 : Decimal;
    BEGIN
      if CalcTempDeprAmount(TempDeprAmount) then
        exit(TempDeprAmount);

      if SLPercent > 0 then
        exit((-SLPercent / 100) * (NumberOfDays / DaysInFiscalYear) * DeprBasis);

      if FixedAmount > 0 then
        exit(-FixedAmount * NumberOfDays / DaysInFiscalYear);

      if DeprYears > 0 then begin
        RemainingLife :=
          (DeprYears * DaysInFiscalYear) -
          DepreciationCalc.DeprDays(
            DeprStartingDate,DepreciationCalc.Yesterday(FirstDeprDate,Year365Days),Year365Days);
        if RemainingLife < 1 then
          exit(-BookValue);

        exit(-(BookValue + SalvageValue - MinusBookValue) * NumberOfDays / RemainingLife);
      end;
      exit(0);
    END;

    LOCAL PROCEDURE CalcDB1Amount@11() : Decimal;
    VAR
      DeprInFiscalYear@1000 : Decimal;
    BEGIN
      if CalcTempDeprAmount(TempDeprAmount) then
        exit(TempDeprAmount);

      if DateFromProjection = 0D then
        DeprInFiscalYear := DepreciationCalc.DeprInFiscalYear(FA."No.",DeprBookCode,UntilDate)
      else
        DeprInFiscalYear := EntryAmounts[3];
      if DeprInTwoFiscalYears then
        DeprInFiscalYear := 0;
      exit(
        -(DBPercent / 100) * (NumberOfDays / DaysInFiscalYear) *
        (BookValue + SalvageValue - MinusBookValue - Sign * DeprInFiscalYear));
    END;

    LOCAL PROCEDURE CalcDB2Amount@14() : Decimal;
    BEGIN
      if CalcTempDeprAmount(TempDeprAmount) then
        exit(TempDeprAmount);

      exit(
        -(1 - POWER(1 - DBPercent / 100,NumberOfDays / DaysInFiscalYear)) *
        (BookValue - MinusBookValue));
    END;

    LOCAL PROCEDURE CalcDBSLAmount@12() : Decimal;
    VAR
      FADateCalc@1002 : Codeunit "FA Date Calculation";
      SLAmount@1000 : Decimal;
      DBAmount@1001 : Decimal;
    BEGIN
      if DeprMethod = DeprMethod::DB1SL then
        DBAmount := CalcDB1Amount
      else
        DBAmount := CalcDB2Amount;
      if FADeprBook."Use DB% First Fiscal Year" then
        if FADateCalc.GetFiscalYear(DeprBookCode,UntilDate) =
           FADateCalc.GetFiscalYear(DeprBookCode,DeprStartingDate)
        then
          exit(DBAmount);
      SLAmount := CalcSLAmount;
      if SLAmount < DBAmount then
        exit(SLAmount);

      exit(DBAmount)
    END;

    LOCAL PROCEDURE CalcUserDefinedAmount@8(EndingDate@1000 : Date) : Decimal;
    VAR
      TableDeprCalc@1001 : Codeunit "Table Depr. Calculation";
    BEGIN
      if CalcTempDeprAmount(TempDeprAmount) then
        ERROR('');

      exit(
        -TableDeprCalc.GetTablePercent(DeprBook.Code,DeprTableCode,
          FirstUserDefinedDeprDate,FirstDeprDate,EndingDate) *
        DeprBasis * Factor);
    END;

    LOCAL PROCEDURE CalcBelowZeroAmount@9() : Decimal;
    BEGIN
      if CalcTempDeprAmount(TempDeprAmount) then
        ERROR('');

      if PercentBelowZero > 0 then
        exit((-PercentBelowZero / 100) * (NumberOfDays / DaysInFiscalYear) * DeprBasis);
      if AmountBelowZero > 0 then
        exit(-AmountBelowZero * NumberOfDays / DaysInFiscalYear);
      exit(0);
    END;

    LOCAL PROCEDURE TransferValues@3();
    BEGIN
      with FADeprBook do begin
        TESTFIELD("Depreciation Starting Date");
        if "Depreciation Method" = "Depreciation Method"::"User-Defined" then begin
          TESTFIELD("Depreciation Table Code");
          TESTFIELD("First User-Defined Depr. Date");
        end;
        case "Depreciation Method" of
          "Depreciation Method"::"Declining-Balance 1",
          "Depreciation Method"::"Declining-Balance 2",
          "Depreciation Method"::"DB1/SL",
          "Depreciation Method"::"DB2/SL":
            if "Declining-Balance %" >= 100 then
              ERROR(Text001,FAName,FIELDCAPTION("Declining-Balance %"));
        end;
        if (DeprBook."Periodic Depr. Date Calc." = DeprBook."Periodic Depr. Date Calc."::"Last Depr. Entry") and
           ("Depreciation Method" <> "Depreciation Method"::"Straight-Line")
        then begin
          "Depreciation Method" := "Depreciation Method"::"Straight-Line";
          ERROR(
            Text002,
            FAName,
            FIELDCAPTION("Depreciation Method"),
            "Depreciation Method",
            DeprBook.TABLECAPTION,
            DeprBook.FIELDCAPTION("Periodic Depr. Date Calc."),
            DeprBook."Periodic Depr. Date Calc.");
        end;

        if DateFromProjection = 0D then begin
          CALCFIELDS("Book Value");
          BookValue := "Book Value";
        end else
          BookValue := EntryAmounts[1];
        MinusBookValue := DepreciationCalc.GetMinusBookValue(FA."No.",DeprBookCode,0D,0D);
        CALCFIELDS("Depreciable Basis","Salvage Value");
        DeprBasis := "Depreciable Basis";
        SalvageValue := "Salvage Value";
        BookValue2 := BookValue;
        SalvageValue2 := SalvageValue;
        DeprMethod := "Depreciation Method";
        DeprStartingDate := "Depreciation Starting Date";
        DeprTableCode := "Depreciation Table Code";
        FirstUserDefinedDeprDate := "First User-Defined Depr. Date";
        if ("Depreciation Method" = "Depreciation Method"::"User-Defined") and
           (FirstUserDefinedDeprDate > DeprStartingDate)
        then
          ERROR(
            Text003,
            FAName,FIELDCAPTION("First User-Defined Depr. Date"),FIELDCAPTION("Depreciation Starting Date"));
        SLPercent := "Straight-Line %";
        DBPercent := "Declining-Balance %";
        DeprYears := "No. of Depreciation Years";
        if "Depreciation Ending Date" > 0D then begin
          if  "Depreciation Starting Date" > "Depreciation Ending Date" then
            ERROR(
              Text003,
              FAName,FIELDCAPTION("Depreciation Starting Date"),FIELDCAPTION("Depreciation Ending Date"));
          DeprYears :=
            DepreciationCalc.DeprDays(
              "Depreciation Starting Date","Depreciation Ending Date",false) / 360;
        end;
        FixedAmount := "Fixed Depr. Amount";
        FinalRoundingAmount := "Final Rounding Amount";
        if FinalRoundingAmount = 0 then
          FinalRoundingAmount := DeprBook."Default Final Rounding Amount";
        EndingBookValue := "Ending Book Value";
        if not "Ignore Def. Ending Book Value" and (EndingBookValue = 0) then
          EndingBookValue := DeprBook."Default Ending Book Value";
        AcquisitionDate := "Acquisition Date";
        DisposalDate := "Disposal Date";
        PercentBelowZero := "Depr. below Zero %";
        AmountBelowZero := "Fixed Depr. Amount below Zero";
        DaysInFiscalYear := DeprBook."No. of Days in Fiscal Year";
        if DaysInFiscalYear = 0 then
          DaysInFiscalYear := 360;
        Year365Days := DeprBook."Fiscal Year 365 Days";
        if Year365Days then begin
          DaysInFiscalYear := 365;
          DeprYears :=
            DepreciationCalc.DeprDays(
              "Depreciation Starting Date","Depreciation Ending Date",true) / DaysInFiscalYear;
        end;
      end;
    END;

    LOCAL PROCEDURE FAName@5() : Text[200];
    VAR
      DepreciationCalc@1000 : Codeunit "Depreciation Calculation";
    BEGIN
      exit(DepreciationCalc.FAName(FA,DeprBookCode));
    END;

    LOCAL PROCEDURE SetHalfYearConventionMethod@7() : Boolean;
    VAR
      AccountingPeriod@1000 : Record "Accounting Period";
    BEGIN
      if not FADeprBook."Use Half-Year Convention" then
        exit(false);
      if FADeprBook."Depreciation Method" = FADeprBook."Depreciation Method"::Manual then
        exit(false);
      if DeprMethod = DeprMethod::BelowZero then
        exit(false);

      AccountingPeriod.SETRANGE("New Fiscal Year",true);
      AccountingPeriod.SETFILTER(
        "Starting Date",'>=%1',
        DepreciationCalc.ToMorrow(FADeprBook."Depreciation Starting Date",Year365Days));
      AccountingPeriod.FINDFIRST;
      NewYearDate := AccountingPeriod."Starting Date";
      if FirstDeprDate >= NewYearDate then
        exit(false);

      if DeprBook."No. of Days in Fiscal Year" <> 0 then
        DeprBook.TESTFIELD("No. of Days in Fiscal Year",360);
      if DeprMethod in
         [DeprMethod::DB2,
          DeprMethod::DB2SL,
          DeprMethod::"User-Defined"]
      then
        ERROR(
          Text004,
          FADeprBook.FIELDCAPTION("Depreciation Method"),
          FADeprBook."Depreciation Method",
          FAName);
      exit(true);
    END;

    LOCAL PROCEDURE CalcHalfYearConventionDepr@21() : Decimal;
    VAR
      DeprAmount@1000 : Decimal;
      HalfYearPercent@1001 : Decimal;
      HalfYearFactor@1002 : Decimal;
      OriginalNumberOfDays@1003 : Integer;
      OriginalBookValue@1004 : Decimal;
      OriginalFirstDeprDate@1005 : Date;
    BEGIN
      if CalcTempDeprAmount(TempDeprAmount) then
        ERROR('');

      if (DeprMethod = DeprMethod::DB1) or (DeprMethod = DeprMethod::DB1SL) then
        HalfYearPercent := DBPercent
      else
        if SLPercent > 0 then
          HalfYearPercent := SLPercent
        else
          if DeprYears > 0 then
            HalfYearPercent :=
              100 /
              (DepreciationCalc.DeprDays(NewYearDate,FADeprBook."Depreciation Ending Date",Year365Days) +
               DaysInFiscalYear / 2) * DaysInFiscalYear
          else
            HalfYearPercent := 0;

      HalfYearFactor :=
        DaysInFiscalYear / 2 /
        DepreciationCalc.DeprDays(
          FADeprBook."Depreciation Starting Date",
          DepreciationCalc.Yesterday(NewYearDate,Year365Days),
          Year365Days);
      DeprInTwoFiscalYears := UntilDate >= NewYearDate;

      OriginalNumberOfDays := NumberOfDays;
      OriginalBookValue := BookValue;
      OriginalFirstDeprDate := FirstDeprDate;

      if DeprInTwoFiscalYears then
        NumberOfDays :=
          DepreciationCalc.DeprDays(
            FirstDeprDate,DepreciationCalc.Yesterday(NewYearDate,Year365Days),Year365Days);
      if FixedAmount > 0 then
        DeprAmount := -FixedAmount * NumberOfDays / DaysInFiscalYear * HalfYearFactor
      else
        DeprAmount :=
          (-HalfYearPercent / 100) * (NumberOfDays / DaysInFiscalYear) * DeprBasis * HalfYearFactor;
      if DeprInTwoFiscalYears then begin
        NumberOfDays := DepreciationCalc.DeprDays(NewYearDate,UntilDate,Year365Days);
        FirstDeprDate := NewYearDate;
        BookValue := BookValue + DeprAmount;
        case DeprMethod of
          DeprMethod::StraightLine:
            DeprAmount := DeprAmount + CalcSLAmount;
          DeprMethod::DB1:
            DeprAmount := DeprAmount + CalcDB1Amount;
          DeprMethod::DB1SL:
            DeprAmount := DeprAmount + CalcDBSLAmount;
        end;
      end;
      NumberOfDays := OriginalNumberOfDays;
      BookValue := OriginalBookValue;
      FirstDeprDate := OriginalFirstDeprDate;
      DeprInTwoFiscalYears := false;
      exit(DeprAmount);
    END;

    BEGIN
    END.
  }
}

