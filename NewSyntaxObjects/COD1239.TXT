OBJECT Codeunit 1239 XML Buffer Reader
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DefaultNamespace@1000 : Text;

    [TryFunction]
    [Internal]
    PROCEDURE SaveToFile@7(FilePath@1000 : Text;VAR XMLBuffer@1001 : Record "XML Buffer");
    VAR
      TempXMLBuffer@1004 : TEMPORARY Record "XML Buffer";
      TempAttributeXMLBuffer@1003 : TEMPORARY Record "XML Buffer";
      XMLDOMManagement@1005 : Codeunit "XML DOM Management";
      XmlDocument@1007 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      RootElement@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      Header@1002 : Text;
    BEGIN
      TempXMLBuffer.CopyImportFrom(XMLBuffer);
      TempXMLBuffer := XMLBuffer;
      TempXMLBuffer.SETCURRENTKEY("Parent Entry No.",Type,"Node Number");
      Header := '<?xml version="1.0" encoding="UTF-8"?>' +
        '<' + TempXMLBuffer.GetElementName + ' ';

      DefaultNamespace := TempXMLBuffer.GetAttributeValue('xmlns');
      if TempXMLBuffer.FindAttributes(TempAttributeXMLBuffer) then
        repeat
          Header += TempAttributeXMLBuffer.Name + '="' + TempAttributeXMLBuffer.Value + '" ';
        until TempAttributeXMLBuffer.NEXT = 0;
      Header += '/>';

      XMLDOMManagement.LoadXMLDocumentFromText(Header,XmlDocument);
      RootElement := XmlDocument.DocumentElement;

      SaveChildElements(TempXMLBuffer,RootElement,XmlDocument);

      XmlDocument.Save(FilePath);
    END;

    LOCAL PROCEDURE SaveChildElements@33(VAR TempParentElementXMLBuffer@1000 : TEMPORARY Record "XML Buffer";XMLCurrElement@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";XmlDocument@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      TempElementXMLBuffer@1001 : TEMPORARY Record "XML Buffer";
      ChildElement@1005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      Namespace@1006 : Text;
    BEGIN
      if TempParentElementXMLBuffer.FindChildElements(TempElementXMLBuffer) then
        repeat
          if TempElementXMLBuffer.Namespace = '' then
            Namespace := DefaultNamespace
          else
            Namespace := TempElementXMLBuffer.Namespace;
          ChildElement := XmlDocument.CreateNode('element',TempElementXMLBuffer.GetElementName,Namespace);
          if TempElementXMLBuffer.Value <> '' then
            ChildElement.InnerText := TempElementXMLBuffer.Value;
          XMLCurrElement.AppendChild(ChildElement);
          SaveAttributes(TempElementXMLBuffer,ChildElement,XmlDocument);
          SaveChildElements(TempElementXMLBuffer,ChildElement,XmlDocument);
        until TempElementXMLBuffer.NEXT = 0;
    END;

    LOCAL PROCEDURE SaveAttributes@9(VAR TempParentElementXMLBuffer@1001 : TEMPORARY Record "XML Buffer";XMLCurrElement@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";XmlDocument@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      TempAttributeXMLBuffer@1000 : TEMPORARY Record "XML Buffer";
      Attribute@1005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlAttribute";
    BEGIN
      if TempParentElementXMLBuffer.FindAttributes(TempAttributeXMLBuffer) then
        repeat
          Attribute := XmlDocument.CreateAttribute(TempAttributeXMLBuffer.Name);
          Attribute.InnerText := TempAttributeXMLBuffer.Value;
          XMLCurrElement.Attributes.SetNamedItem(Attribute);
        until TempAttributeXMLBuffer.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

