OBJECT Table 130400 CAL Test Suite
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnDelete=VAR
               CALTestLine@1000 : Record "CAL Test Line";
             BEGIN
               CALTestLine.SETRANGE("Test Suite",Name);
               CALTestLine.DELETEALL(true);
             END;

    CaptionML=ENU=CAL Test Suite;
    LookupPageID="CAL Test Suites";
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=ENU=Name;
                                                   NotBlank=true }
    { 2   ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 3   ;   ;Tests to Execute    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("CAL Test Line" WHERE ("Test Suite"=FIELD(Name),
                                                                                            "Line Type"=CONST(Function),
                                                                                            Run=CONST(true)));
                                                   CaptionML=ENU=Tests to Execute;
                                                   Editable=false }
    { 4   ;   ;Tests not Executed  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("CAL Test Line" WHERE ("Test Suite"=FIELD(Name),
                                                                                            "Line Type"=CONST(Function),
                                                                                            Run=CONST(true),
                                                                                            Result=CONST(" ")));
                                                   CaptionML=ENU=Tests not Executed;
                                                   Editable=false }
    { 5   ;   ;Failures            ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("CAL Test Line" WHERE ("Test Suite"=FIELD(Name),
                                                                                            "Line Type"=CONST(Function),
                                                                                            Run=CONST(true),
                                                                                            Result=CONST(Failure)));
                                                   CaptionML=ENU=Failures;
                                                   Editable=false }
    { 6   ;   ;Last Run            ;DateTime      ;CaptionML=ENU=Last Run;
                                                   Editable=false }
    { 8   ;   ;Export              ;Boolean       ;CaptionML=ENU=Export }
    { 21  ;   ;Attachment          ;BLOB          ;CaptionML=ENU=Attachment }
    { 23  ;   ;Update Test Coverage Map;Boolean   ;CaptionML=ENU=Update Test Coverage Map }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CouldNotExportErr@1005 : TextConst '@@@={Locked};ENU=Could not export Unit Test XML definition.';
      UTTxt@1003 : TextConst '@@@={Locked};ENU=UT';
      CALTestSuiteXML@1001 : XMLport "CAL Test Suite";
      CALTestResultsXML@1000 : XMLport "CAL Test Results";

    [Internal]
    PROCEDURE ExportTestSuiteSetup@1();
    VAR
      CALTestSuite@1000 : Record "CAL Test Suite";
      TempBlob@1004 : Record TempBlob;
      FileMgt@1003 : Codeunit "File Management";
      OStream@1001 : OutStream;
    BEGIN
      TempBlob.Blob.CREATEOUTSTREAM(OStream);
      CALTestSuite.SETRANGE(Name,Name);

      CALTestSuiteXML.SETDESTINATION(OStream);
      CALTestSuiteXML.SETTABLEVIEW(CALTestSuite);

      if not CALTestSuiteXML.EXPORT then
        ERROR(CouldNotExportErr);

      FileMgt.ServerTempFileName('*.xml');
      FileMgt.BLOBExport(TempBlob,UTTxt + Name,true);
    END;

    [Internal]
    PROCEDURE ImportTestSuiteSetup@2();
    VAR
      TempBlob@1002 : Record TempBlob;
      FileMgt@1001 : Codeunit "File Management";
      IStream@1004 : InStream;
    BEGIN
      FileMgt.BLOBImport(TempBlob,'*.xml');
      TempBlob.Blob.CREATEINSTREAM(IStream);
      CALTestSuiteXML.SETSOURCE(IStream);
      CALTestSuiteXML.IMPORT;
    END;

    [Internal]
    PROCEDURE ExportTestSuiteResult@4();
    VAR
      CALTestSuite@1000 : Record "CAL Test Suite";
      TempBlob@1004 : Record TempBlob;
      FileMgt@1003 : Codeunit "File Management";
      OStream@1001 : OutStream;
    BEGIN
      TempBlob.Blob.CREATEOUTSTREAM(OStream);
      CALTestSuite.SETRANGE(Name,Name);

      CALTestResultsXML.SETDESTINATION(OStream);
      CALTestResultsXML.SETTABLEVIEW(CALTestSuite);

      if not CALTestResultsXML.EXPORT then
        ERROR(CouldNotExportErr);

      FileMgt.ServerTempFileName('*.xml');
      FileMgt.BLOBExport(TempBlob,UTTxt + Name,true);
    END;

    [Internal]
    PROCEDURE ImportTestSuiteResult@3();
    VAR
      TempBlob@1002 : Record TempBlob;
      FileMgt@1001 : Codeunit "File Management";
      IStream@1004 : InStream;
    BEGIN
      FileMgt.BLOBImport(TempBlob,'*.xml');
      TempBlob.Blob.CREATEINSTREAM(IStream);
      CALTestResultsXML.SETSOURCE(IStream);
      CALTestResultsXML.IMPORT;
    END;

    BEGIN
    END.
  }
}

