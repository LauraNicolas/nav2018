OBJECT Table 337 Reservation Entry
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ActionMessageEntry@1000 : Record "Action Message Entry";
             BEGIN
               ActionMessageEntry.SETCURRENTKEY("Reservation Entry");
               ActionMessageEntry.SETRANGE("Reservation Entry","Entry No.");
               ActionMessageEntry.DELETEALL;
             END;

    CaptionML=ENU=Reservation Entry;
    LookupPageID="Reservation Entries";
    DrillDownPageID="Reservation Entries";
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=true;
                                                   CaptionML=ENU=Entry No. }
    { 2   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=ENU=Item No. }
    { 3   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=ENU=Location Code }
    { 4   ;   ;Quantity (Base)     ;Decimal       ;OnValidate=BEGIN
                                                                Quantity := CalcReservationQuantity;
                                                                "Qty. to Handle (Base)" := "Quantity (Base)";
                                                                "Qty. to Invoice (Base)" := "Quantity (Base)";
                                                              END;

                                                   CaptionML=ENU=Quantity (Base);
                                                   DecimalPlaces=0:5 }
    { 5   ;   ;Reservation Status  ;Option        ;CaptionML=ENU=Reservation Status;
                                                   OptionCaptionML=ENU=Reservation,Tracking,Surplus,Prospect;
                                                   OptionString=Reservation,Tracking,Surplus,Prospect }
    { 7   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 8   ;   ;Creation Date       ;Date          ;CaptionML=ENU=Creation Date }
    { 9   ;   ;Transferred from Entry No.;Integer ;TableRelation="Reservation Entry";
                                                   CaptionML=ENU=Transferred from Entry No. }
    { 10  ;   ;Source Type         ;Integer       ;CaptionML=ENU=Source Type }
    { 11  ;   ;Source Subtype      ;Option        ;CaptionML=ENU=Source Subtype;
                                                   OptionCaptionML=ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                   OptionString="0","1","2","3","4","5","6","7","8","9","10" }
    { 12  ;   ;Source ID           ;Code20        ;CaptionML=ENU=Source ID }
    { 13  ;   ;Source Batch Name   ;Code10        ;CaptionML=ENU=Source Batch Name }
    { 14  ;   ;Source Prod. Order Line;Integer    ;CaptionML=ENU=Source Prod. Order Line }
    { 15  ;   ;Source Ref. No.     ;Integer       ;CaptionML=ENU=Source Ref. No. }
    { 16  ;   ;Item Ledger Entry No.;Integer      ;TableRelation="Item Ledger Entry";
                                                   CaptionML=ENU=Item Ledger Entry No.;
                                                   Editable=false }
    { 22  ;   ;Expected Receipt Date;Date         ;CaptionML=ENU=Expected Receipt Date }
    { 23  ;   ;Shipment Date       ;Date          ;CaptionML=ENU=Shipment Date }
    { 24  ;   ;Serial No.          ;Code20        ;CaptionML=ENU=Serial No. }
    { 25  ;   ;Created By          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit "User Management";
                                                            BEGIN
                                                              UserMgt.LookupUserID("Created By");
                                                            END;

                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Created By }
    { 27  ;   ;Changed By          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit "User Management";
                                                            BEGIN
                                                              UserMgt.LookupUserID("Changed By");
                                                            END;

                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Changed By }
    { 28  ;   ;Positive            ;Boolean       ;CaptionML=ENU=Positive;
                                                   Editable=false }
    { 29  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                Quantity := ROUND("Quantity (Base)" / "Qty. per Unit of Measure",0.00001);
                                                              END;

                                                   CaptionML=ENU=Qty. per Unit of Measure;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 30  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5 }
    { 31  ;   ;Action Message Adjustment;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Action Message Entry".Quantity WHERE ("Reservation Entry"=FIELD("Entry No."),
                                                                                                          Calculation=CONST(Sum)));
                                                   CaptionML=ENU=Action Message Adjustment;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 32  ;   ;Binding             ;Option        ;CaptionML=ENU=Binding;
                                                   OptionCaptionML=ENU=" ,Order-to-Order";
                                                   OptionString=" ","Order-to-Order";
                                                   Editable=false }
    { 33  ;   ;Suppressed Action Msg.;Boolean     ;CaptionML=ENU=Suppressed Action Msg. }
    { 34  ;   ;Planning Flexibility;Option        ;CaptionML=ENU=Planning Flexibility;
                                                   OptionCaptionML=ENU=Unlimited,None;
                                                   OptionString=Unlimited,None }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;CaptionML=ENU=Appl.-to Item Entry }
    { 40  ;   ;Warranty Date       ;Date          ;CaptionML=ENU=Warranty Date;
                                                   Editable=false }
    { 41  ;   ;Expiration Date     ;Date          ;CaptionML=ENU=Expiration Date;
                                                   Editable=false }
    { 50  ;   ;Qty. to Handle (Base);Decimal      ;CaptionML=ENU=Qty. to Handle (Base);
                                                   DecimalPlaces=0:5 }
    { 51  ;   ;Qty. to Invoice (Base);Decimal     ;CaptionML=ENU=Qty. to Invoice (Base);
                                                   DecimalPlaces=0:5 }
    { 53  ;   ;Quantity Invoiced (Base);Decimal   ;CaptionML=ENU=Quantity Invoiced (Base);
                                                   DecimalPlaces=0:5 }
    { 80  ;   ;New Serial No.      ;Code20        ;CaptionML=ENU=New Serial No.;
                                                   Editable=false }
    { 81  ;   ;New Lot No.         ;Code20        ;CaptionML=ENU=New Lot No.;
                                                   Editable=false }
    { 900 ;   ;Disallow Cancellation;Boolean      ;CaptionML=ENU=Disallow Cancellation }
    { 5400;   ;Lot No.             ;Code20        ;CaptionML=ENU=Lot No. }
    { 5401;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE ("Item No."=FIELD("Item No."));
                                                   CaptionML=ENU=Variant Code }
    { 5811;   ;Appl.-from Item Entry;Integer      ;CaptionML=ENU=Appl.-from Item Entry;
                                                   MinValue=0 }
    { 5817;   ;Correction          ;Boolean       ;CaptionML=ENU=Correction }
    { 6505;   ;New Expiration Date ;Date          ;CaptionML=ENU=New Expiration Date;
                                                   Editable=false }
    { 6510;   ;Item Tracking       ;Option        ;CaptionML=ENU=Item Tracking;
                                                   OptionCaptionML=ENU=None,Lot No.,Lot and Serial No.,Serial No.;
                                                   OptionString=None,"Lot No.","Lot and Serial No.","Serial No.";
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Entry No.",Positive                    ;Clustered=true }
    {    ;"Source ID","Source Ref. No.","Source Type","Source Subtype","Source Batch Name","Source Prod. Order Line","Reservation Status","Shipment Date","Expected Receipt Date";
                                                   SumIndexFields="Quantity (Base)",Quantity,"Qty. to Handle (Base)","Qty. to Invoice (Base)" }
    {    ;"Item No.","Variant Code","Location Code";
                                                   MaintainSIFTIndex=false }
    {    ;"Item No.","Variant Code","Location Code","Reservation Status","Shipment Date","Expected Receipt Date","Serial No.","Lot No.";
                                                   SumIndexFields="Quantity (Base)";
                                                   MaintainSIFTIndex=false }
    {    ;"Item No.","Source Type","Source Subtype","Reservation Status","Location Code","Variant Code","Shipment Date","Expected Receipt Date","Serial No.","Lot No.";
                                                   SumIndexFields="Quantity (Base)",Quantity;
                                                   MaintainSQLIndex=false;
                                                   MaintainSIFTIndex=false }
    {    ;"Item No.","Variant Code","Location Code","Item Tracking","Reservation Status","Lot No.","Serial No.";
                                                   SumIndexFields="Quantity (Base)";
                                                   MaintainSQLIndex=false;
                                                   MaintainSIFTIndex=false }
    { false;"Lot No."                              }
    { false;"Serial No."                           }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;"Entry No.",Positive,"Item No.",Description,Quantity }
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Line';

    [External]
    PROCEDURE TextCaption@1() : Text[255];
    VAR
      ItemLedgEntry@1000 : Record "Item Ledger Entry";
      SalesLine@1007 : Record "Sales Line";
      ReqLine@1006 : Record "Requisition Line";
      PurchLine@1005 : Record "Purchase Line";
      ItemJnlLine@1004 : Record "Item Journal Line";
      ProdOrderLine@1003 : Record "Prod. Order Line";
      ProdOrderComp@1002 : Record "Prod. Order Component";
      AssemblyHeader@1011 : Record "Assembly Header";
      AssemblyLine@1012 : Record "Assembly Line";
      TransLine@1001 : Record "Transfer Line";
      ServLine@1009 : Record "Service Line";
      JobJnlLine@1010 : Record "Job Journal Line";
    BEGIN
      case "Source Type" of
        DATABASE::"Item Ledger Entry":
          exit(ItemLedgEntry.TABLECAPTION);
        DATABASE::"Sales Line":
          exit(SalesLine.TABLECAPTION);
        DATABASE::"Requisition Line":
          exit(ReqLine.TABLECAPTION);
        DATABASE::"Purchase Line":
          exit(PurchLine.TABLECAPTION);
        DATABASE::"Item Journal Line":
          exit(ItemJnlLine.TABLECAPTION);
        DATABASE::"Job Journal Line":
          exit(JobJnlLine.TABLECAPTION);
        DATABASE::"Prod. Order Line":
          exit(ProdOrderLine.TABLECAPTION);
        DATABASE::"Prod. Order Component":
          exit(ProdOrderComp.TABLECAPTION);
        DATABASE::"Assembly Header":
          exit(AssemblyHeader.TABLECAPTION);
        DATABASE::"Assembly Line":
          exit(AssemblyLine.TABLECAPTION);
        DATABASE::"Transfer Line":
          exit(TransLine.TABLECAPTION);
        DATABASE::"Service Line":
          exit(ServLine.TABLECAPTION);
        else
          exit(Text001);
      end;
    END;

    [External]
    PROCEDURE SummEntryNo@3() : Integer;
    BEGIN
      case "Source Type" of
        DATABASE::"Item Ledger Entry":
          exit(1);
        DATABASE::"Purchase Line":
          exit(11 + "Source Subtype");
        DATABASE::"Requisition Line":
          exit(21);
        DATABASE::"Sales Line":
          exit(31 + "Source Subtype");
        DATABASE::"Item Journal Line":
          exit(41 + "Source Subtype");
        DATABASE::"Job Journal Line":
          exit(51 + "Source Subtype");
        DATABASE::"Prod. Order Line":
          exit(61 + "Source Subtype");
        DATABASE::"Prod. Order Component":
          exit(71 + "Source Subtype");
        DATABASE::"Transfer Line":
          exit(101 + "Source Subtype");
        DATABASE::"Service Line":
          exit(110);
        DATABASE::"Assembly Header":
          exit(141 + "Source Subtype");
        DATABASE::"Assembly Line":
          exit(151 + "Source Subtype");
        else
          exit(0);
      end;
    END;

    [External]
    PROCEDURE HasSamePointer@24(ReservEntry@1000 : Record "Reservation Entry") : Boolean;
    BEGIN
      exit(
        ("Source Type" = ReservEntry."Source Type") and
        ("Source Subtype" = ReservEntry."Source Subtype") and
        ("Source ID" = ReservEntry."Source ID") and
        ("Source Batch Name" = ReservEntry."Source Batch Name") and
        ("Source Prod. Order Line" = ReservEntry."Source Prod. Order Line") and
        ("Source Ref. No." = ReservEntry."Source Ref. No."));
    END;

    [External]
    PROCEDURE HasSamePointerWithSpec@58(TrackingSpecification@1000 : Record "Tracking Specification") : Boolean;
    BEGIN
      exit(
        ("Source Type" = TrackingSpecification."Source Type") and
        ("Source Subtype" = TrackingSpecification."Source Subtype") and
        ("Source ID" = TrackingSpecification."Source ID") and
        ("Source Batch Name" = TrackingSpecification."Source Batch Name") and
        ("Source Prod. Order Line" = TrackingSpecification."Source Prod. Order Line") and
        ("Source Ref. No." = TrackingSpecification."Source Ref. No."));
    END;

    [External]
    PROCEDURE SetPointer@2(RowID@1000 : Text[250]);
    VAR
      ItemTrackingMgt@1001 : Codeunit "Item Tracking Management";
      StrArray@1002 : ARRAY [6] OF Text[100];
    BEGIN
      ItemTrackingMgt.DecomposeRowID(RowID,StrArray);
      EVALUATE("Source Type",StrArray[1]);
      EVALUATE("Source Subtype",StrArray[2]);
      "Source ID" := StrArray[3];
      "Source Batch Name" := StrArray[4];
      EVALUATE("Source Prod. Order Line",StrArray[5]);
      EVALUATE("Source Ref. No.",StrArray[6]);
    END;

    [External]
    PROCEDURE SetPointerFilter@15();
    BEGIN
      SETCURRENTKEY(
        "Source ID","Source Ref. No.","Source Type","Source Subtype",
        "Source Batch Name","Source Prod. Order Line","Reservation Status",
        "Shipment Date","Expected Receipt Date");
      SETRANGE("Source ID","Source ID");
      SETRANGE("Source Ref. No.","Source Ref. No.");
      SETRANGE("Source Type","Source Type");
      SETRANGE("Source Subtype","Source Subtype");
      SETRANGE("Source Batch Name","Source Batch Name");
      SETRANGE("Source Prod. Order Line","Source Prod. Order Line");
    END;

    [External]
    PROCEDURE Lock@4();
    VAR
      Rec2@1000 : Record "Reservation Entry";
    BEGIN
      Rec2.SETCURRENTKEY("Item No.");
      if "Item No." <> '' then
        Rec2.SETRANGE("Item No.","Item No.");
      Rec2.LOCKTABLE;
      if Rec2.FINDLAST then;
    END;

    [External]
    PROCEDURE SetItemData@29(ItemNo@1000 : Code[20];ItemDescription@1001 : Text[50];LocationCode@1002 : Code[10];VariantCode@1003 : Code[10];QtyPerUoM@1004 : Decimal);
    BEGIN
      "Item No." := ItemNo;
      Description := ItemDescription;
      "Location Code" := LocationCode;
      "Variant Code" := VariantCode;
      "Qty. per Unit of Measure" := QtyPerUoM;
    END;

    [External]
    PROCEDURE SetSource@21(SourceType@1000 : Integer;SourceSubtype@1001 : Integer;SourceID@1002 : Code[20];SourceRefNo@1003 : Integer;SourceBatchName@1004 : Code[10];SourceProdOrderLine@1005 : Integer);
    BEGIN
      "Source Type" := SourceType;
      "Source Subtype" := SourceSubtype;
      "Source ID" := SourceID;
      "Source Ref. No." := SourceRefNo;
      "Source Batch Name" := SourceBatchName;
      "Source Prod. Order Line" := SourceProdOrderLine;
    END;

    [External]
    PROCEDURE SetSourceFilter@31(SourceType@1011 : Integer;SourceSubtype@1010 : Integer;SourceID@1009 : Code[20];SourceRefNo@1008 : Integer;SourceKey@1000 : Boolean);
    BEGIN
      if SourceKey then
        SETCURRENTKEY(
          "Source ID","Source Ref. No.","Source Type","Source Subtype",
          "Source Batch Name","Source Prod. Order Line");
      SETRANGE("Source Type",SourceType);
      if SourceSubtype >= 0 then
        SETRANGE("Source Subtype",SourceSubtype);
      SETRANGE("Source ID",SourceID);
      if SourceRefNo >= 0 then
        SETRANGE("Source Ref. No.",SourceRefNo);
    END;

    [External]
    PROCEDURE SetSourceFilter2@9(SourceBatchName@1007 : Code[10];SourceProdOrderLine@1006 : Integer);
    BEGIN
      SETRANGE("Source Batch Name",SourceBatchName);
      SETRANGE("Source Prod. Order Line",SourceProdOrderLine);
    END;

    [External]
    PROCEDURE ClearTracking@20();
    BEGIN
      "Serial No." := '';
      "Lot No." := '';
      "Warranty Date" := 0D;
      "Expiration Date" := 0D;
    END;

    [External]
    PROCEDURE ClearTrackingFilter@14();
    BEGIN
      SETRANGE("Serial No.");
      SETRANGE("Lot No.");
    END;

    [External]
    PROCEDURE CopyTrackingFromItemLedgEntry@25(ItemLedgerEntry@1000 : Record "Item Ledger Entry");
    BEGIN
      "Serial No." := ItemLedgerEntry."Serial No.";
      "Lot No." := ItemLedgerEntry."Lot No.";
    END;

    [External]
    PROCEDURE CopyTrackingFromReservEntry@26(ReservationEntry@1000 : Record "Reservation Entry");
    BEGIN
      "Serial No." := ReservationEntry."Serial No.";
      "Lot No." := ReservationEntry."Lot No.";
    END;

    [External]
    PROCEDURE CopyTrackingFromSpec@19(TrackingSpecification@1000 : Record "Tracking Specification");
    BEGIN
      "Serial No." := TrackingSpecification."Serial No.";
      "Lot No." := TrackingSpecification."Lot No.";
      "Warranty Date" := TrackingSpecification."Warranty Date";
      "Expiration Date" := TrackingSpecification."Expiration Date";
    END;

    [External]
    PROCEDURE CopyTrackingFromWhseActivLine@27(WarehouseActivityLine@1000 : Record "Warehouse Activity Line");
    BEGIN
      "Serial No." := WarehouseActivityLine."Serial No.";
      "Lot No." := WarehouseActivityLine."Lot No.";
      "Expiration Date" := WarehouseActivityLine."Expiration Date";
    END;

    [External]
    PROCEDURE SetTrackingFilter@12(SerialNo@1007 : Code[20];LotNo@1006 : Code[20]);
    BEGIN
      SETRANGE("Serial No.",SerialNo);
      SETRANGE("Lot No.",LotNo);
    END;

    [External]
    PROCEDURE SetTrackingFilterBlank@23();
    BEGIN
      SETRANGE("Serial No.",'');
      SETRANGE("Lot No.",'');
    END;

    [External]
    PROCEDURE SetTrackingFilterFromItemJnlLine@8(ItemJournalLine@1000 : Record "Item Journal Line");
    BEGIN
      SETRANGE("Serial No.",ItemJournalLine."Serial No.");
      SETRANGE("Lot No.",ItemJournalLine."Lot No.");
    END;

    [External]
    PROCEDURE SetTrackingFilterFromReservEntry@17(ReservEntry@1000 : Record "Reservation Entry");
    BEGIN
      SETRANGE("Serial No.",ReservEntry."Serial No.");
      SETRANGE("Lot No.",ReservEntry."Lot No.");
    END;

    [External]
    PROCEDURE SetTrackingFilterFromSpec@16(TrackingSpecification@1000 : Record "Tracking Specification");
    BEGIN
      SETRANGE("Serial No.",TrackingSpecification."Serial No.");
      SETRANGE("Lot No.",TrackingSpecification."Lot No.");
    END;

    [External]
    PROCEDURE SetTrackingFilterFromWhseSpec@18(WhseItemTrackingLine@1000 : Record "Whse. Item Tracking Line");
    BEGIN
      SETRANGE("Serial No.",WhseItemTrackingLine."Serial No.");
      SETRANGE("Lot No.",WhseItemTrackingLine."Lot No.");
    END;

    [External]
    PROCEDURE UpdateItemTracking@5();
    VAR
      ItemTrackingMgt@1000 : Codeunit "Item Tracking Management";
    BEGIN
      "Item Tracking" := ItemTrackingMgt.ItemTrackingOption("Lot No.","Serial No.");
    END;

    [External]
    PROCEDURE UpdateActionMessageEntries@13(OldReservEntry@1000 : Record "Reservation Entry");
    VAR
      ActionMessageEntry@1002 : Record "Action Message Entry";
      ActionMessageEntry2@1003 : Record "Action Message Entry";
      OldReservEntry2@1004 : Record "Reservation Entry";
    BEGIN
      if OldReservEntry."Reservation Status" = OldReservEntry."Reservation Status"::Surplus then begin
        ActionMessageEntry.FilterFromReservEntry(OldReservEntry);
        if ActionMessageEntry.FINDSET then
          repeat
            ActionMessageEntry2 := ActionMessageEntry;
            ActionMessageEntry2.TransferFromReservEntry(Rec);
            ActionMessageEntry2.MODIFY;
          until ActionMessageEntry.NEXT = 0;
        MODIFY;
      end else
        if OldReservEntry2.GET(OldReservEntry."Entry No.",not OldReservEntry.Positive) then begin
          if HasSamePointer(OldReservEntry2) then begin
            OldReservEntry2.DELETE;
            DELETE;
          end else
            MODIFY;
        end else
          MODIFY;
    END;

    [External]
    PROCEDURE ClearItemTrackingFields@7();
    BEGIN
      "Lot No." := '';
      "Serial No." := '';
      UpdateItemTracking;
    END;

    LOCAL PROCEDURE CalcReservationQuantity@62() : Decimal;
    VAR
      ReservEntry@1001 : Record "Reservation Entry";
    BEGIN
      if "Qty. per Unit of Measure" = 1 then
        exit("Quantity (Base)");

      ReservEntry.SETFILTER("Entry No.",'<>%1',"Entry No.");
      ReservEntry.SetSourceFilter("Source Type","Source Subtype","Source ID","Source Ref. No.",false);
      ReservEntry.SetSourceFilter2("Source Batch Name","Source Prod. Order Line");
      ReservEntry.SETRANGE("Reservation Status","Reservation Status"::Reservation);
      ReservEntry.CALCSUMS("Quantity (Base)",Quantity);
      exit(
        ROUND((ReservEntry."Quantity (Base)" + "Quantity (Base)") / "Qty. per Unit of Measure",0.00001) -
        ReservEntry.Quantity);
    END;

    [External]
    PROCEDURE ClearApplFromToItemEntry@6();
    BEGIN
      if Positive then
        "Appl.-to Item Entry" := 0
      else
        "Appl.-from Item Entry" := 0;
    END;

    [External]
    PROCEDURE TestItemFields@10(ItemNo@1000 : Code[20];VariantCode@1001 : Code[10];LocationCode@1002 : Code[10]);
    BEGIN
      TESTFIELD("Item No.",ItemNo);
      TESTFIELD("Variant Code",VariantCode);
      TESTFIELD("Location Code",LocationCode);
    END;

    [External]
    PROCEDURE TrackingExists@11() : Boolean;
    BEGIN
      exit(("Serial No." <> '') or ("Lot No." <> ''));
    END;

    [External]
    PROCEDURE TransferReservations@22(VAR OldReservEntry@1006 : Record "Reservation Entry";ItemNo@1000 : Code[20];VariantCode@1002 : Code[10];LocationCode@1003 : Code[10];TransferAll@1007 : Boolean;TransferQty@1015 : Decimal;QtyPerUOM@1008 : Decimal;SourceType@1009 : Integer;SourceSubtype@1010 : Option;SourceID@1011 : Code[20];SourceBatchName@1012 : Code[10];SourceProdOrderLine@1013 : Integer;SourceRefNo@1014 : Integer);
    VAR
      NewReservEntry@1004 : Record "Reservation Entry";
      CreateReservEntry@1005 : Codeunit "Create Reserv. Entry";
      Status@1001 : 'Reservation,Tracking,Surplus,Prospect';
    BEGIN
      if TransferAll then begin
        OldReservEntry.FINDSET;
        OldReservEntry.TESTFIELD("Qty. per Unit of Measure",QtyPerUOM);
        repeat
          OldReservEntry.TestItemFields(ItemNo,VariantCode,LocationCode);

          NewReservEntry := OldReservEntry;
          NewReservEntry.SetSource(SourceType,SourceSubtype,SourceID,SourceRefNo,SourceBatchName,SourceProdOrderLine);

          NewReservEntry.UpdateActionMessageEntries(OldReservEntry);
        until OldReservEntry.NEXT = 0;
      end else
        for Status := Status::Reservation to Status::Prospect do begin
          if TransferQty = 0 then
            exit;
          OldReservEntry.SETRANGE("Reservation Status",Status);
          if OldReservEntry.FINDSET then
            repeat
              OldReservEntry.TestItemFields(ItemNo,VariantCode,LocationCode);

              TransferQty :=
                CreateReservEntry.TransferReservEntry(
                  SourceType,SourceSubtype,SourceID,SourceBatchName,SourceProdOrderLine,SourceRefNo,
                  QtyPerUOM,OldReservEntry,TransferQty);
            until (OldReservEntry.NEXT = 0) or (TransferQty = 0);
        end;
    END;

    BEGIN
    END.
  }
}

