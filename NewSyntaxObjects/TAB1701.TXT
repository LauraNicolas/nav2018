OBJECT Table 1701 Deferral Header
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields="Schedule Description";
    OnDelete=VAR
               DeferralLine@1000 : Record "Deferral Line";
             BEGIN
               // If the user deletes the header, all associated lines should also be deleted
               DeferralLine.SETRANGE("Deferral Doc. Type","Deferral Doc. Type");
               DeferralLine.SETRANGE("Gen. Jnl. Template Name","Gen. Jnl. Template Name");
               DeferralLine.SETRANGE("Gen. Jnl. Batch Name","Gen. Jnl. Batch Name");
               DeferralLine.SETRANGE("Document Type","Document Type");
               DeferralLine.SETRANGE("Document No.","Document No.");
               DeferralLine.SETRANGE("Line No.","Line No.");
               DeferralLine.DELETEALL;
             END;

    CaptionML=ENU=Deferral Header;
  }
  FIELDS
  {
    { 1   ;   ;Deferral Doc. Type  ;Option        ;CaptionML=ENU=Deferral Doc. Type;
                                                   OptionCaptionML=ENU=Purchase,Sales,G/L;
                                                   OptionString=Purchase,Sales,"G/L" }
    { 2   ;   ;Gen. Jnl. Template Name;Code10     ;CaptionML=ENU=Gen. Jnl. Template Name }
    { 3   ;   ;Gen. Jnl. Batch Name;Code10        ;CaptionML=ENU=Gen. Jnl. Batch Name }
    { 4   ;   ;Document Type       ;Integer       ;CaptionML=ENU=Document Type }
    { 5   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 6   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 7   ;   ;Deferral Code       ;Code10        ;CaptionML=ENU=Deferral Code;
                                                   NotBlank=true }
    { 8   ;   ;Amount to Defer     ;Decimal       ;OnValidate=BEGIN
                                                                if "Initial Amount to Defer" < 0 then begin// Negative amount
                                                                  if "Amount to Defer" < "Initial Amount to Defer" then
                                                                    ERROR(AmountToDeferErr);
                                                                  if "Amount to Defer" > 0 then
                                                                    ERROR(AmountToDeferErr)
                                                                end;

                                                                if "Initial Amount to Defer" >= 0 then begin// Positive amount
                                                                  if "Amount to Defer" > "Initial Amount to Defer" then
                                                                    ERROR(AmountToDeferErr);
                                                                  if "Amount to Defer" < 0 then
                                                                    ERROR(AmountToDeferErr);
                                                                end;

                                                                if "Amount to Defer" = 0 then
                                                                  ERROR(ZeroAmountToDeferErr);
                                                              END;

                                                   CaptionML=ENU=Amount to Defer;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 9   ;   ;Amount to Defer (LCY);Decimal      ;CaptionML=ENU=Amount to Defer (LCY);
                                                   AutoFormatType=1 }
    { 10  ;   ;Calc. Method        ;Option        ;CaptionML=ENU=Calc. Method;
                                                   OptionCaptionML=ENU=Straight-Line,Equal per Period,Days per Period,User-Defined;
                                                   OptionString="Straight-Line","Equal per Period","Days per Period","User-Defined" }
    { 11  ;   ;Start Date          ;Date          ;OnValidate=VAR
                                                                AccountingPeriod@1000 : Record "Accounting Period";
                                                              BEGIN
                                                                if GenJnlCheckLine.DateNotAllowed("Start Date") then
                                                                  ERROR(InvalidPostingDateErr,"Start Date");

                                                                AccountingPeriod.SETFILTER("Starting Date",'>=%1',"Start Date");
                                                                if AccountingPeriod.ISEMPTY then
                                                                  ERROR(DeferSchedOutOfBoundsErr);
                                                              END;

                                                   CaptionML=ENU=Start Date }
    { 12  ;   ;No. of Periods      ;Integer       ;OnValidate=BEGIN
                                                                if "No. of Periods" < 1 then
                                                                  ERROR(NumberofPeriodsErr);
                                                              END;

                                                   CaptionML=ENU=No. of Periods;
                                                   NotBlank=true;
                                                   BlankZero=true }
    { 13  ;   ;Schedule Description;Text50        ;CaptionML=ENU=Schedule Description }
    { 14  ;   ;Initial Amount to Defer;Decimal    ;CaptionML=ENU=Initial Amount to Defer }
    { 15  ;   ;Currency Code       ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=ENU=Currency Code }
    { 20  ;   ;Schedule Line Total ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Deferral Line".Amount WHERE ("Deferral Doc. Type"=FIELD("Deferral Doc. Type"),
                                                                                                 "Gen. Jnl. Template Name"=FIELD("Gen. Jnl. Template Name"),
                                                                                                 "Gen. Jnl. Batch Name"=FIELD("Gen. Jnl. Batch Name"),
                                                                                                 "Document Type"=FIELD("Document Type"),
                                                                                                 "Document No."=FIELD("Document No."),
                                                                                                 "Line No."=FIELD("Line No.")));
                                                   CaptionML=ENU=Schedule Line Total }
  }
  KEYS
  {
    {    ;"Deferral Doc. Type","Gen. Jnl. Template Name","Gen. Jnl. Batch Name","Document Type","Document No.","Line No.";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AmountToDeferErr@1000 : TextConst 'ENU=The deferred amount cannot be greater than the document line amount.';
      GenJnlCheckLine@1001 : Codeunit "Gen. Jnl.-Check Line";
      InvalidPostingDateErr@1004 : TextConst '@@@="%1=The date passed in for the posting date.";ENU=%1 is not within the range of posting dates for your company.';
      DeferSchedOutOfBoundsErr@1003 : TextConst 'ENU=The deferral schedule falls outside the accounting periods that have been set up for the company.';
      SelectionMsg@1005 : TextConst 'ENU=You must specify a deferral code for this line before you can view the deferral schedule.';
      DeferralUtilities@1006 : Codeunit "Deferral Utilities";
      NumberofPeriodsErr@1007 : TextConst 'ENU=You must specify one or more periods.';
      ZeroAmountToDeferErr@1008 : TextConst 'ENU=The Amount to Defer cannot be 0.';

    [External]
    PROCEDURE CalculateSchedule@1() : Boolean;
    VAR
      DeferralDescription@1000 : Text[50];
    BEGIN
      if "Deferral Code" = '' then begin
        MESSAGE(SelectionMsg);
        exit(false);
      end;
      DeferralDescription := "Schedule Description";
      DeferralUtilities.CreateDeferralSchedule("Deferral Code","Deferral Doc. Type","Gen. Jnl. Template Name",
        "Gen. Jnl. Batch Name","Document Type","Document No.","Line No.","Amount to Defer",
        "Calc. Method","Start Date","No. of Periods",false,DeferralDescription,false,"Currency Code");
      exit(true);
    END;

    BEGIN
    END.
  }
}

