OBJECT Codeunit 5646 FAReclassJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "FA Reclass. Journal Template"=imd,
                TableData "FA Reclass. Journal Batch"=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=RECLASSIFY';
      Text001@1001 : TextConst 'ENU=FA Reclass. Journal';
      Text002@1002 : TextConst 'ENU=DEFAULT';
      Text003@1003 : TextConst 'ENU=Default Journal';
      OldFANo@1004 : Code[20];
      OldFANo2@1005 : Code[20];
      OpenFromBatch@1006 : Boolean;

    [External]
    PROCEDURE TemplateSelection@4(PageID@1003 : Integer;VAR FAReclassJnlLine@1002 : Record "FA Reclass. Journal Line";VAR JnlSelected@1001 : Boolean);
    VAR
      FAReclassJnlTempl@1000 : Record "FA Reclass. Journal Template";
    BEGIN
      JnlSelected := true;

      FAReclassJnlTempl.RESET;
      FAReclassJnlTempl.SETRANGE("Page ID",PageID);

      case FAReclassJnlTempl.COUNT of
        0:
          begin
            FAReclassJnlTempl.INIT;
            FAReclassJnlTempl.Name := Text000;
            FAReclassJnlTempl.Description := Text001;
            FAReclassJnlTempl.VALIDATE("Page ID");
            FAReclassJnlTempl.INSERT;
            COMMIT;
          end;
        1:
          FAReclassJnlTempl.FINDFIRST;
        else
          JnlSelected := PAGE.RUNMODAL(0,FAReclassJnlTempl) = ACTION::LookupOK;
      end;
      if JnlSelected then begin
        FAReclassJnlLine.FILTERGROUP := 2;
        FAReclassJnlLine.SETRANGE("Journal Template Name",FAReclassJnlTempl.Name);
        FAReclassJnlLine.FILTERGROUP := 0;
        if OpenFromBatch then begin
          FAReclassJnlLine."Journal Template Name" := '';
          PAGE.RUN(FAReclassJnlTempl."Page ID",FAReclassJnlLine);
        end;
      end;
    END;

    [External]
    PROCEDURE TemplateSelectionFromBatch@5(VAR FAReclassJnlBatch@1000 : Record "FA Reclass. Journal Batch");
    VAR
      FAReclassJnlLine@1002 : Record "FA Reclass. Journal Line";
      FAReclassJnlTempl@1003 : Record "FA Reclass. Journal Template";
    BEGIN
      OpenFromBatch := true;
      FAReclassJnlTempl.GET(FAReclassJnlBatch."Journal Template Name");
      FAReclassJnlTempl.TESTFIELD("Page ID");
      FAReclassJnlBatch.TESTFIELD(Name);

      FAReclassJnlLine.FILTERGROUP := 2;
      FAReclassJnlLine.SETRANGE("Journal Template Name",FAReclassJnlTempl.Name);
      FAReclassJnlLine.FILTERGROUP := 0;

      FAReclassJnlLine."Journal Template Name" := '';
      FAReclassJnlLine."Journal Batch Name" := FAReclassJnlBatch.Name;
      PAGE.RUN(FAReclassJnlTempl."Page ID",FAReclassJnlLine);
    END;

    [External]
    PROCEDURE OpenJournal@1(VAR CurrentJnlBatchName@1000 : Code[10];VAR FAReclassJnlLine@1001 : Record "FA Reclass. Journal Line");
    BEGIN
      CheckTemplateName(FAReclassJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      FAReclassJnlLine.FILTERGROUP := 2;
      FAReclassJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      FAReclassJnlLine.FILTERGROUP := 0;
    END;

    [External]
    PROCEDURE OpenJnlBatch@12(VAR FAReclassJnlBatch@1000 : Record "FA Reclass. Journal Batch");
    VAR
      FAReclassJnlTemplate@1002 : Record "FA Reclass. Journal Template";
      FAReclassJnlLine@1004 : Record "FA Reclass. Journal Line";
      JnlSelected@1003 : Boolean;
    BEGIN
      if FAReclassJnlBatch.GETFILTER("Journal Template Name") <> '' then
        exit;
      FAReclassJnlBatch.FILTERGROUP(2);
      if FAReclassJnlBatch.GETFILTER("Journal Template Name") <> '' then begin
        FAReclassJnlBatch.FILTERGROUP(0);
        exit;
      end;
      FAReclassJnlBatch.FILTERGROUP(0);

      if not FAReclassJnlBatch.FIND('-') then begin
        if not FAReclassJnlTemplate.FINDFIRST then
          TemplateSelection(0,FAReclassJnlLine,JnlSelected);
        if FAReclassJnlTemplate.FINDFIRST then
          CheckTemplateName(FAReclassJnlTemplate.Name,FAReclassJnlBatch.Name);
      end;
      FAReclassJnlBatch.FIND('-');
      JnlSelected := true;
      if FAReclassJnlBatch.GETFILTER("Journal Template Name") <> '' then
        FAReclassJnlTemplate.SETRANGE(Name,FAReclassJnlBatch.GETFILTER("Journal Template Name"));
      case FAReclassJnlTemplate.COUNT of
        1:
          FAReclassJnlTemplate.FINDFIRST;
        else
          JnlSelected := PAGE.RUNMODAL(0,FAReclassJnlTemplate) = ACTION::LookupOK;
      end;
      if not JnlSelected then
        ERROR('');

      FAReclassJnlBatch.FILTERGROUP(2);
      FAReclassJnlBatch.SETRANGE("Journal Template Name",FAReclassJnlTemplate.Name);
      FAReclassJnlBatch.FILTERGROUP(0);
    END;

    [External]
    PROCEDURE CheckName@6(CurrentJnlBatchName@1000 : Code[10];VAR FAReclassJnlLine@1001 : Record "FA Reclass. Journal Line");
    VAR
      FAReclassJnlBatch@1002 : Record "FA Reclass. Journal Batch";
    BEGIN
      FAReclassJnlBatch.GET(FAReclassJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    LOCAL PROCEDURE CheckTemplateName@2(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      FAReclassJnlBatch@1002 : Record "FA Reclass. Journal Batch";
    BEGIN
      FAReclassJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      if not FAReclassJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) then begin
        if not FAReclassJnlBatch.FINDFIRST then begin
          FAReclassJnlBatch.INIT;
          FAReclassJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          FAReclassJnlBatch.Name := Text002;
          FAReclassJnlBatch.INSERT(true);
          FAReclassJnlBatch.Description := Text003;
          FAReclassJnlBatch.MODIFY;
          COMMIT;
        end;
        CurrentJnlBatchName := FAReclassJnlBatch.Name;
      end;
    END;

    [External]
    PROCEDURE SetName@8(CurrentJnlBatchName@1000 : Code[10];VAR FAReclassJnlLine@1001 : Record "FA Reclass. Journal Line");
    BEGIN
      FAReclassJnlLine.FILTERGROUP := 2;
      FAReclassJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      FAReclassJnlLine.FILTERGROUP := 0;
      if FAReclassJnlLine.FIND('-') then;
    END;

    [External]
    PROCEDURE LookupName@10(VAR CurrentJnlBatchName@1000 : Code[10];VAR FAReclassJnlLine@1001 : Record "FA Reclass. Journal Line");
    VAR
      FAReclassJnlBatch@1002 : Record "FA Reclass. Journal Batch";
    BEGIN
      COMMIT;
      FAReclassJnlBatch."Journal Template Name" := FAReclassJnlLine.GETRANGEMAX("Journal Template Name");
      FAReclassJnlBatch.Name := FAReclassJnlLine.GETRANGEMAX("Journal Batch Name");
      FAReclassJnlBatch.FILTERGROUP(2);
      FAReclassJnlBatch.SETRANGE("Journal Template Name",FAReclassJnlBatch."Journal Template Name");
      FAReclassJnlBatch.FILTERGROUP(0);
      if PAGE.RUNMODAL(0,FAReclassJnlBatch) = ACTION::LookupOK then begin
        CurrentJnlBatchName := FAReclassJnlBatch.Name;
        SetName(CurrentJnlBatchName,FAReclassJnlLine);
      end;
    END;

    [External]
    PROCEDURE GetFAS@3(FAReclassJnlLine@1000 : Record "FA Reclass. Journal Line";VAR FADescription@1001 : Text[50];VAR FADescription2@1002 : Text[50]);
    VAR
      FA@1003 : Record "Fixed Asset";
    BEGIN
      if FAReclassJnlLine."FA No." <> OldFANo then begin
        FADescription := '';
        if FAReclassJnlLine."FA No." <> '' then
          if FA.GET(FAReclassJnlLine."FA No.") then
            FADescription := FA.Description;
        OldFANo := FAReclassJnlLine."FA No.";
      end;
      if FAReclassJnlLine."New FA No." <> OldFANo2 then begin
        FADescription2 := '';
        if FAReclassJnlLine."New FA No." <> '' then
          if FA.GET(FAReclassJnlLine."New FA No.") then
            FADescription2 := FA.Description;
        OldFANo2 := FAReclassJnlLine."New FA No.";
      end;
    END;

    BEGIN
    END.
  }
}

