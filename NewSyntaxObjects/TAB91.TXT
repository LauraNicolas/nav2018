OBJECT Table 91 User Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=VAR
               User@1000 : Record User;
             BEGIN
               if "E-Mail" <> '' then
                 exit;
               if "User ID" <> '' then
                 exit;
               User.SETRANGE("User Name","User ID");
               if User.FINDFIRST then
                 "E-Mail" := COPYSTR(User."Contact Email",1,MAXSTRLEN("E-Mail"));
             END;

    OnDelete=VAR
               NotificationSetup@1000 : Record "Notification Setup";
             BEGIN
               NotificationSetup.SETRANGE("User ID","User ID");
               NotificationSetup.DELETEALL(true);
             END;

    CaptionML=ENU=User Setup;
    LookupPageID="User Setup";
    DrillDownPageID="User Setup";
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=VAR
                                                                UserMgt@1000 : Codeunit "User Management";
                                                              BEGIN
                                                                UserMgt.ValidateUserID("User ID");
                                                              END;

                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit "User Management";
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   ValidateTableRelation=false;
                                                   CaptionML=ENU=User ID;
                                                   NotBlank=true }
    { 2   ;   ;Allow Posting From  ;Date          ;CaptionML=ENU=Allow Posting From }
    { 3   ;   ;Allow Posting To    ;Date          ;CaptionML=ENU=Allow Posting To }
    { 4   ;   ;Register Time       ;Boolean       ;CaptionML=ENU=Register Time }
    { 10  ;   ;Salespers./Purch. Code;Code20      ;TableRelation="Salesperson/Purchaser";
                                                   OnValidate=VAR
                                                                UserSetup@1000 : Record "User Setup";
                                                              BEGIN
                                                                if "Salespers./Purch. Code" <> '' then begin
                                                                  UserSetup.SETCURRENTKEY("Salespers./Purch. Code");
                                                                  UserSetup.SETRANGE("Salespers./Purch. Code","Salespers./Purch. Code");
                                                                  if UserSetup.FINDFIRST then
                                                                    ERROR(Text001,"Salespers./Purch. Code",UserSetup."User ID");
                                                                  UpdateSalesPerson;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Salespers./Purch. Code }
    { 11  ;   ;Approver ID         ;Code50        ;TableRelation="User Setup"."User ID";
                                                   OnValidate=BEGIN
                                                                if "Approver ID" = "User ID" then
                                                                  FIELDERROR("Approver ID");
                                                              END;

                                                   OnLookup=VAR
                                                              UserSetup@1000 : Record "User Setup";
                                                            BEGIN
                                                              UserSetup.SETFILTER("User ID",'<>%1',"User ID");
                                                              if PAGE.RUNMODAL(PAGE::"Approval User Setup",UserSetup) = ACTION::LookupOK then
                                                                VALIDATE("Approver ID",UserSetup."User ID");
                                                            END;

                                                   CaptionML=ENU=Approver ID }
    { 12  ;   ;Sales Amount Approval Limit;Integer;OnValidate=BEGIN
                                                                if "Unlimited Sales Approval" and ("Sales Amount Approval Limit" <> 0) then
                                                                  ERROR(Text003,FIELDCAPTION("Sales Amount Approval Limit"),FIELDCAPTION("Unlimited Sales Approval"));
                                                                if "Sales Amount Approval Limit" < 0 then
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=ENU=Sales Amount Approval Limit;
                                                   BlankZero=true }
    { 13  ;   ;Purchase Amount Approval Limit;Integer;
                                                   OnValidate=BEGIN
                                                                if "Unlimited Purchase Approval" and ("Purchase Amount Approval Limit" <> 0) then
                                                                  ERROR(Text003,FIELDCAPTION("Purchase Amount Approval Limit"),FIELDCAPTION("Unlimited Purchase Approval"));
                                                                if "Purchase Amount Approval Limit" < 0 then
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=ENU=Purchase Amount Approval Limit;
                                                   BlankZero=true }
    { 14  ;   ;Unlimited Sales Approval;Boolean   ;OnValidate=BEGIN
                                                                if "Unlimited Sales Approval" then
                                                                  "Sales Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=ENU=Unlimited Sales Approval }
    { 15  ;   ;Unlimited Purchase Approval;Boolean;OnValidate=BEGIN
                                                                if "Unlimited Purchase Approval" then
                                                                  "Purchase Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=ENU=Unlimited Purchase Approval }
    { 16  ;   ;Substitute          ;Code50        ;TableRelation="User Setup"."User ID";
                                                   OnValidate=BEGIN
                                                                if Substitute = "User ID" then
                                                                  FIELDERROR(Substitute);
                                                              END;

                                                   OnLookup=VAR
                                                              UserSetup@1000 : Record "User Setup";
                                                            BEGIN
                                                              UserSetup.SETFILTER("User ID",'<>%1',"User ID");
                                                              if PAGE.RUNMODAL(PAGE::"Approval User Setup",UserSetup) = ACTION::LookupOK then
                                                                VALIDATE(Substitute,UserSetup."User ID");
                                                            END;

                                                   CaptionML=ENU=Substitute }
    { 17  ;   ;E-Mail              ;Text100       ;OnValidate=VAR
                                                                MailManagement@1000 : Codeunit "Mail Management";
                                                              BEGIN
                                                                UpdateSalesPerson;
                                                                MailManagement.ValidateEmailAddressField("E-Mail");
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=ENU=E-Mail }
    { 19  ;   ;Request Amount Approval Limit;Integer;
                                                   OnValidate=BEGIN
                                                                if "Unlimited Request Approval" and ("Request Amount Approval Limit" <> 0) then
                                                                  ERROR(Text003,FIELDCAPTION("Request Amount Approval Limit"),FIELDCAPTION("Unlimited Request Approval"));
                                                                if "Request Amount Approval Limit" < 0 then
                                                                  ERROR(Text005);
                                                              END;

                                                   CaptionML=ENU=Request Amount Approval Limit;
                                                   BlankZero=true }
    { 20  ;   ;Unlimited Request Approval;Boolean ;OnValidate=BEGIN
                                                                if "Unlimited Request Approval" then
                                                                  "Request Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=ENU=Unlimited Request Approval }
    { 21  ;   ;Approval Administrator;Boolean     ;OnValidate=VAR
                                                                UserSetup@1000 : Record "User Setup";
                                                              BEGIN
                                                                if "Approval Administrator" then begin
                                                                  UserSetup.SETRANGE("Approval Administrator",true);
                                                                  if not UserSetup.ISEMPTY then
                                                                    FIELDERROR("Approval Administrator");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Approval Administrator }
    { 31  ;   ;License Type        ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."License Type" WHERE ("User Name"=FIELD("User ID")));
                                                   CaptionML=ENU=License Type;
                                                   OptionCaptionML=ENU=Full User,Limited User,Device Only User,Windows Group,External User;
                                                   OptionString="Full User","Limited User","Device Only User","Windows Group","External User" }
    { 950 ;   ;Time Sheet Admin.   ;Boolean       ;CaptionML=ENU=Time Sheet Admin. }
    { 5600;   ;Allow FA Posting From;Date         ;CaptionML=ENU=Allow FA Posting From }
    { 5601;   ;Allow FA Posting To ;Date          ;CaptionML=ENU=Allow FA Posting To }
    { 5700;   ;Sales Resp. Ctr. Filter;Code10     ;TableRelation="Responsibility Center".Code;
                                                   CaptionML=ENU=Sales Resp. Ctr. Filter }
    { 5701;   ;Purchase Resp. Ctr. Filter;Code10  ;TableRelation="Responsibility Center";
                                                   CaptionML=ENU=Purchase Resp. Ctr. Filter }
    { 5900;   ;Service Resp. Ctr. Filter;Code10   ;TableRelation="Responsibility Center";
                                                   CaptionML=ENU=Service Resp. Ctr. Filter }
  }
  KEYS
  {
    {    ;"User ID"                               ;Clustered=true }
    {    ;"Salespers./Purch. Code"                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=The %1 Salesperson/Purchaser code is already assigned to another User ID %2.';
      Text003@1002 : TextConst 'ENU="You cannot have both a %1 and %2. "';
      Text005@1004 : TextConst 'ENU=You cannot have approval limits less than zero.';

    [External]
    PROCEDURE CreateApprovalUserSetup@3(User@1000 : Record User);
    VAR
      UserSetup@1001 : Record "User Setup";
      ApprovalUserSetup@1002 : Record "User Setup";
    BEGIN
      ApprovalUserSetup.INIT;
      ApprovalUserSetup.VALIDATE("User ID",User."User Name");
      ApprovalUserSetup.VALIDATE("Sales Amount Approval Limit",GetDefaultSalesAmountApprovalLimit);
      ApprovalUserSetup.VALIDATE("Purchase Amount Approval Limit",GetDefaultPurchaseAmountApprovalLimit);
      ApprovalUserSetup.VALIDATE("E-Mail",User."Contact Email");
      UserSetup.SETRANGE("Sales Amount Approval Limit",UserSetup.GetDefaultSalesAmountApprovalLimit);
      if UserSetup.FINDFIRST then
        ApprovalUserSetup.VALIDATE("Approver ID",UserSetup."Approver ID");
      if ApprovalUserSetup.INSERT then;
    END;

    [External]
    PROCEDURE GetDefaultSalesAmountApprovalLimit@1() : Integer;
    VAR
      UserSetup@1001 : Record "User Setup";
      DefaultApprovalLimit@1000 : Integer;
      LimitedApprovers@1002 : Integer;
    BEGIN
      UserSetup.SETRANGE("Unlimited Sales Approval",false);

      if UserSetup.FINDFIRST then begin
        DefaultApprovalLimit := UserSetup."Sales Amount Approval Limit";
        LimitedApprovers := UserSetup.COUNT;
        UserSetup.SETRANGE("Sales Amount Approval Limit",DefaultApprovalLimit);
        if LimitedApprovers = UserSetup.COUNT then
          exit(DefaultApprovalLimit);
      end;

      // Return 0 if no user setup exists or no default value is found
      exit(0);
    END;

    [External]
    PROCEDURE GetDefaultPurchaseAmountApprovalLimit@2() : Integer;
    VAR
      UserSetup@1002 : Record "User Setup";
      DefaultApprovalLimit@1001 : Integer;
      LimitedApprovers@1000 : Integer;
    BEGIN
      UserSetup.SETRANGE("Unlimited Purchase Approval",false);

      if UserSetup.FINDFIRST then begin
        DefaultApprovalLimit := UserSetup."Purchase Amount Approval Limit";
        LimitedApprovers := UserSetup.COUNT;
        UserSetup.SETRANGE("Purchase Amount Approval Limit",DefaultApprovalLimit);
        if LimitedApprovers = UserSetup.COUNT then
          exit(DefaultApprovalLimit);
      end;

      // Return 0 if no user setup exists or no default value is found
      exit(0);
    END;

    [External]
    PROCEDURE HideExternalUsers@5();
    VAR
      PermissionManager@1001 : Codeunit "Permission Manager";
      OriginalFilterGroup@1000 : Integer;
    BEGIN
      if not PermissionManager.SoftwareAsAService then
        exit;

      OriginalFilterGroup := FILTERGROUP;
      FILTERGROUP := 2;
      CALCFIELDS("License Type");
      SETFILTER("License Type",'<>%1',"License Type"::"External User");
      FILTERGROUP := OriginalFilterGroup;
    END;

    LOCAL PROCEDURE UpdateSalesPerson@4();
    VAR
      SalespersonPurchaser@1000 : Record "Salesperson/Purchaser";
    BEGIN
      if ("E-Mail" <> '') and SalespersonPurchaser.GET("Salespers./Purch. Code") then begin
        SalespersonPurchaser."E-Mail" := COPYSTR("E-Mail",1,MAXSTRLEN(SalespersonPurchaser."E-Mail"));
        SalespersonPurchaser.MODIFY;
      end;
    END;

    BEGIN
    END.
  }
}

