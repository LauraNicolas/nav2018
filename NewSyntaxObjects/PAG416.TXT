OBJECT Page 416 G/L Account Balance Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Lines;
    SaveValues=true;
    InsertAllowed=false;
    DeleteAllowed=false;
    LinksAllowed=false;
    SourceTable=Date;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 RESET;
               END;

    OnFindRecord=BEGIN
                   exit(PeriodFormMgt.FindDate(Which,Rec,GLPeriodLength));
                 END;

    OnNextRecord=BEGIN
                   exit(PeriodFormMgt.NextDate(Steps,Rec,GLPeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       if DebitCreditTotals then
                         GLAcc.CALCFIELDS("Net Change","Debit Amount","Credit Amount")
                       else begin
                         GLAcc.CALCFIELDS("Net Change");
                         if GLAcc."Net Change" > 0 then begin
                           GLAcc."Debit Amount" := GLAcc."Net Change";
                           GLAcc."Credit Amount" := 0
                         end else begin
                           GLAcc."Debit Amount" := 0;
                           GLAcc."Credit Amount" := -GLAcc."Net Change"
                         end
                       end
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 14  ;2   ;Field     ;
                Name=Period Start;
                CaptionML=ENU=Period Start;
                ToolTipML=ENU=Specifies the start date of the period defined on the line for the bank account balance.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Period Start";
                Editable=FALSE;
                ImplicitType=Date }

    { 16  ;2   ;Field     ;
                Name=Period Name;
                CaptionML=ENU=Period Name;
                ToolTipML=ENU=Specifies the name of the period shown in the line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Period Name";
                Editable=FALSE;
                ImplicitType=Text31 }

    { 4   ;2   ;Field     ;
                Name=DebitAmount;
                DrillDown=true;
                CaptionML=ENU=Debit Amount;
                ToolTipML=ENU=Specifies the debit amount for the period on the line.;
                ApplicationArea=#Basic,#Suite;
                BlankNumbers=BlankZero;
                SourceExpr=GLAcc."Debit Amount";
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              BalanceDrillDown;
                            END;
                             }

    { 2   ;2   ;Field     ;
                Name=CreditAmount;
                DrillDown=true;
                CaptionML=ENU=Credit Amount;
                ToolTipML=ENU=Specifies the credit amount for the period on the line.;
                ApplicationArea=#Basic,#Suite;
                BlankNumbers=BlankZero;
                SourceExpr=GLAcc."Credit Amount";
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              BalanceDrillDown;
                            END;
                             }

    { 10  ;2   ;Field     ;
                Name=NetChange;
                DrillDown=true;
                CaptionML=ENU=Net Change;
                ToolTipML=ENU=Specifies changes in the actual general ledger amount.;
                ApplicationArea=#Basic,#Suite;
                BlankZero=true;
                SourceExpr=GLAcc."Net Change";
                AutoFormatType=1;
                Visible=FALSE;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              BalanceDrillDown;
                            END;
                             }

  }
  CODE
  {
    VAR
      AccountingPeriod@1000 : Record "Accounting Period";
      GLAcc@1001 : Record "G/L Account";
      PeriodFormMgt@1002 : Codeunit PeriodFormManagement;
      GLPeriodLength@1003 : 'Day,Week,Month,Quarter,Year,"Accounting Period"';
      AmountType@1004 : '"Net Change","Balance at Date"';
      ClosingEntryFilter@1005 : 'Include,Exclude';
      DebitCreditTotals@1006 : Boolean;

    [External]
    PROCEDURE Set@1(VAR NewGLAcc@1000 : Record "G/L Account";NewGLPeriodLength@1001 : Integer;NewAmountType@1002 : '"Net Change",Balance';NewClosingEntryFilter@1003 : 'Include,Exclude';NewDebitCreditTotals@1004 : Boolean);
    BEGIN
      GLAcc.COPY(NewGLAcc);
      GLPeriodLength := NewGLPeriodLength;
      AmountType := NewAmountType;
      ClosingEntryFilter := NewClosingEntryFilter;
      DebitCreditTotals := NewDebitCreditTotals;
      CurrPage.UPDATE(false);
    END;

    LOCAL PROCEDURE BalanceDrillDown@3();
    VAR
      GLEntry@1000 : Record "G/L Entry";
    BEGIN
      SetDateFilter;
      GLEntry.RESET;
      GLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
      GLEntry.SETRANGE("G/L Account No.",GLAcc."No.");
      if GLAcc.Totaling <> '' then
        GLEntry.SETFILTER("G/L Account No.",GLAcc.Totaling);
      GLEntry.SETFILTER("Posting Date",GLAcc.GETFILTER("Date Filter"));
      GLEntry.SETFILTER("Global Dimension 1 Code",GLAcc.GETFILTER("Global Dimension 1 Filter"));
      GLEntry.SETFILTER("Global Dimension 2 Code",GLAcc.GETFILTER("Global Dimension 2 Filter"));
      GLEntry.SETFILTER("Business Unit Code",GLAcc.GETFILTER("Business Unit Filter"));
      PAGE.RUN(0,GLEntry);
    END;

    LOCAL PROCEDURE SetDateFilter@2();
    BEGIN
      if AmountType = AmountType::"Net Change" then
        GLAcc.SETRANGE("Date Filter","Period Start","Period End")
      else
        GLAcc.SETRANGE("Date Filter",0D,"Period End");
      if ClosingEntryFilter = ClosingEntryFilter::Exclude then begin
        AccountingPeriod.SETCURRENTKEY("New Fiscal Year");
        AccountingPeriod.SETRANGE("New Fiscal Year",true);
        if GLAcc.GETRANGEMIN("Date Filter") = 0D then
          AccountingPeriod.SETRANGE("Starting Date",0D,GLAcc.GETRANGEMAX("Date Filter"))
        else
          AccountingPeriod.SETRANGE(
            "Starting Date",
            GLAcc.GETRANGEMIN("Date Filter") + 1,
            GLAcc.GETRANGEMAX("Date Filter"));
        if AccountingPeriod.FIND('-') then
          repeat
            GLAcc.SETFILTER(
              "Date Filter",GLAcc.GETFILTER("Date Filter") + '&<>%1',
              CLOSINGDATE(AccountingPeriod."Starting Date" - 1));
          until AccountingPeriod.NEXT = 0;
      end else
        GLAcc.SETRANGE(
          "Date Filter",
          GLAcc.GETRANGEMIN("Date Filter"),
          CLOSINGDATE(GLAcc.GETRANGEMAX("Date Filter")));
    END;

    BEGIN
    END.
  }
}

