OBJECT Codeunit 1247 Process Gen. Journal  Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Gen. Journal Line";
    Permissions=TableData "Data Exch."=rimd;
    OnRun=VAR
            DataExch@1001 : Record "Data Exch.";
            ProcessDataExch@1002 : Codeunit "Process Data Exch.";
            RecRef@1000 : RecordRef;
          BEGIN
            DataExch.GET("Data Exch. Entry No.");
            RecRef.GETTABLE(Rec);
            ProcessDataExch.ProcessAllLinesColumnMapping(DataExch,RecRef);
          END;

  }
  CODE
  {
    VAR
      ProgressWindowMsg@1000 : TextConst 'ENU=Please wait while the operation is being completed.';

    [Internal]
    PROCEDURE ImportBankStatement@700(GenJnlLine@1004 : Record "Gen. Journal Line");
    VAR
      GenJnlBatch@1005 : Record "Gen. Journal Batch";
      BankAcc@1000 : Record "Bank Account";
      BankExportImportSetup@1009 : Record "Bank Export/Import Setup";
      DataExchDef@1001 : Record "Data Exch. Def";
      DataExchMapping@1002 : Record "Data Exch. Mapping";
      DataExchLineDef@1007 : Record "Data Exch. Line Def";
      DataExch@1003 : Record "Data Exch.";
      GenJnlLineTemplate@1006 : Record "Gen. Journal Line";
      ProgressWindow@1008 : Dialog;
    BEGIN
      GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");

      case GenJnlBatch."Bal. Account Type" of
        GenJnlBatch."Bal. Account Type"::"Bank Account":
          begin
            GenJnlBatch.TESTFIELD("Bal. Account No.");
            BankAcc.GET(GenJnlBatch."Bal. Account No.");
            BankAcc.GetDataExchDef(DataExchDef);
          end;
        GenJnlBatch."Bal. Account Type"::"G/L Account":
          begin
            GenJnlBatch.TESTFIELD("Bank Statement Import Format");
            BankExportImportSetup.GET(GenJnlBatch."Bank Statement Import Format");
            BankExportImportSetup.TESTFIELD("Data Exch. Def. Code");
            DataExchDef.GET(BankExportImportSetup."Data Exch. Def. Code");
            DataExchDef.TESTFIELD(Type,DataExchDef.Type::"Bank Statement Import");
          end;
        else
          GenJnlBatch.FIELDERROR("Bal. Account Type");
      end;
      CreateGeneralJournalLineTemplate(GenJnlLineTemplate,GenJnlLine);

      if not DataExch.ImportToDataExch(DataExchDef)then
        exit;

      GenJnlLineTemplate."Data Exch. Entry No." := DataExch."Entry No.";

      ProgressWindow.OPEN(ProgressWindowMsg);

      DataExchLineDef.SETRANGE("Data Exch. Def Code",DataExchDef.Code);
      DataExchLineDef.FINDFIRST;

      DataExchMapping.GET(DataExchDef.Code,DataExchLineDef.Code,DATABASE::"Gen. Journal Line");
      DataExchMapping.TESTFIELD("Mapping Codeunit");
      CODEUNIT.RUN(DataExchMapping."Mapping Codeunit",GenJnlLineTemplate);

      UpdateGenJournalLines(GenJnlLineTemplate);

      ProgressWindow.CLOSE;
    END;

    PROCEDURE CreateGeneralJournalLineTemplate@701(VAR GenJournalLineTemplate@1003 : Record "Gen. Journal Line";GenJournalLine@1002 : Record "Gen. Journal Line");
    BEGIN
      with GenJournalLineTemplate do begin
        "Journal Template Name" := GenJournalLine."Journal Template Name";
        "Journal Batch Name" := GenJournalLine."Journal Batch Name";
        SetUpNewLine(GenJournalLine,GenJournalLine."Balance (LCY)",true);
        "Account Type" := "Account Type"::"G/L Account";

        GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");
        GenJournalLine.SETRANGE("Journal Batch Name",GenJournalLine."Journal Batch Name");
        if GenJournalLine.FINDLAST then begin
          "Line No." := GenJournalLine."Line No.";
          "Document No." := INCSTR(GenJournalLine."Document No.");
        end else
          "Document No." := GenJournalLine."Document No.";
      end;
    END;

    PROCEDURE UpdateGenJournalLines@702(VAR GenJournalLineTemplate@1000 : Record "Gen. Journal Line");
    VAR
      GenJournalLine@1002 : Record "Gen. Journal Line";
      DocNo@1001 : Code[20];
    BEGIN
      OnBeforeUpdateGenJnlLines(GenJournalLineTemplate);

      GenJournalLine.SETRANGE("Journal Template Name",GenJournalLineTemplate."Journal Template Name");
      GenJournalLine.SETRANGE("Journal Batch Name",GenJournalLineTemplate."Journal Batch Name");
      GenJournalLine.SETFILTER("Line No.",'>%1',GenJournalLineTemplate."Line No.");
      if GenJournalLine.FINDSET then begin
        DocNo := GenJournalLineTemplate."Document No.";
        repeat
          GenJournalLine.VALIDATE("Document No.",DocNo);
          GenJournalLine.MODIFY(true);
          DocNo := INCSTR(DocNo);
        until GenJournalLine.NEXT = 0;
      end;
    END;

    [Integration]
    PROCEDURE OnBeforeUpdateGenJnlLines@1215(VAR GenJournalLineTemplate@1213 : Record "Gen. Journal Line");
    BEGIN
    END;

    BEGIN
    END.
  }
}

