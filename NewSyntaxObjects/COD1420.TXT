OBJECT Codeunit 1420 Doc. Exch. Serv.- Doc. Status
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            CheckPostedInvoices;
            CheckPostedCrMemos;
            CheckPostedServiceInvoices;
            CheckPostedServiceCrMemos;
          END;

  }
  CODE
  {
    VAR
      DocExchLinks@1000 : Codeunit "Doc. Exch. Links";
      UnSupportedTableTypeErr@1002 : TextConst '@@@=%1 is the table.;ENU=The %1 table is not supported.';
      CheckLatestQst@1001 : TextConst '@@@=%1 is Document Exchange Status;ENU=Do you want to check the latest status of the electronic document?';

    LOCAL PROCEDURE CheckPostedInvoices@1();
    VAR
      SalesInvoiceHeader@1002 : Record "Sales Invoice Header";
    BEGIN
      with SalesInvoiceHeader do begin
        SETFILTER("Document Exchange Status",STRSUBSTNO('%1|%2',
            "Document Exchange Status"::"Sent to Document Exchange Service",
            "Document Exchange Status"::"Pending Connection to Recipient"));
        if FINDSET then
          repeat
            DocExchLinks.CheckAndUpdateDocExchInvoiceStatus(SalesInvoiceHeader);
            COMMIT;
          until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CheckPostedCrMemos@2();
    VAR
      SalesCrMemoHeader@1000 : Record "Sales Cr.Memo Header";
    BEGIN
      with SalesCrMemoHeader do begin
        SETFILTER("Document Exchange Status",STRSUBSTNO('%1|%2',
            "Document Exchange Status"::"Sent to Document Exchange Service",
            "Document Exchange Status"::"Pending Connection to Recipient"));
        if FINDSET then
          repeat
            DocExchLinks.CheckAndUpdateDocExchCrMemoStatus(SalesCrMemoHeader);
            COMMIT;
          until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CheckPostedServiceInvoices@3();
    VAR
      ServiceInvoiceHeader@1002 : Record "Service Invoice Header";
    BEGIN
      with ServiceInvoiceHeader do begin
        SETFILTER("Document Exchange Status",STRSUBSTNO('%1|%2',
            "Document Exchange Status"::"Sent to Document Exchange Service",
            "Document Exchange Status"::"Pending Connection to Recipient"));
        if FINDSET then
          repeat
            DocExchLinks.CheckAndUpdateDocExchServiceInvoiceStatus(ServiceInvoiceHeader);
            COMMIT;
          until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CheckPostedServiceCrMemos@4();
    VAR
      ServiceCrMemoHeader@1000 : Record "Service Cr.Memo Header";
    BEGIN
      with ServiceCrMemoHeader do begin
        SETFILTER("Document Exchange Status",STRSUBSTNO('%1|%2',
            "Document Exchange Status"::"Sent to Document Exchange Service",
            "Document Exchange Status"::"Pending Connection to Recipient"));
        if FINDSET then
          repeat
            DocExchLinks.CheckAndUpdateDocExchServiceCrMemoStatus(ServiceCrMemoHeader);
            COMMIT;
          until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CheckAndUpdateDocExchStatus@57(DocRecRef@1002 : RecordRef);
    VAR
      SalesInvoiceHeader@1006 : Record "Sales Invoice Header";
      SalesCrMemoHeader@1005 : Record "Sales Cr.Memo Header";
      ServiceInvoiceHeader@1004 : Record "Service Invoice Header";
      ServiceCrMemoHeader@1001 : Record "Service Cr.Memo Header";
      DocExchLinks@1000 : Codeunit "Doc. Exch. Links";
    BEGIN
      case DocRecRef.NUMBER of
        DATABASE::"Sales Invoice Header":
          begin
            DocRecRef.SETTABLE(SalesInvoiceHeader);
            DocExchLinks.CheckAndUpdateDocExchInvoiceStatus(SalesInvoiceHeader);
          end;
        DATABASE::"Sales Cr.Memo Header":
          begin
            DocRecRef.SETTABLE(SalesCrMemoHeader);
            DocExchLinks.CheckAndUpdateDocExchCrMemoStatus(SalesCrMemoHeader);
          end;
        DATABASE::"Service Invoice Header":
          begin
            DocRecRef.SETTABLE(ServiceInvoiceHeader);
            DocExchLinks.CheckAndUpdateDocExchServiceInvoiceStatus(ServiceInvoiceHeader);
          end;
        DATABASE::"Service Cr.Memo Header":
          begin
            DocRecRef.SETTABLE(ServiceCrMemoHeader);
            DocExchLinks.CheckAndUpdateDocExchServiceCrMemoStatus(ServiceCrMemoHeader);
          end;
        else
          ERROR(UnSupportedTableTypeErr,DocRecRef.NUMBER);
      end;
    END;

    [External]
    PROCEDURE DocExchStatusDrillDown@58(DocRecVariant@1000 : Variant);
    VAR
      DataTypeManagement@1002 : Codeunit "Data Type Management";
      DocRecRef@1001 : RecordRef;
      Handled@1014 : Boolean;
    BEGIN
      if not DataTypeManagement.GetRecordRef(DocRecVariant,DocRecRef) then
        exit;
      OnDocExchStatusDrillDown(DocRecRef,Handled);
      if not Handled then
        DefaultDocExchStatusDrillDown(DocRecRef);
    END;

    LOCAL PROCEDURE DefaultDocExchStatusDrillDown@76(VAR DocRecRef@1002 : RecordRef);
    VAR
      SalesInvoiceHeader@1005 : Record "Sales Invoice Header";
      ActivityLog@1000 : Record "Activity Log";
      DataTypeManagement@1007 : Codeunit "Data Type Management";
      TypeHelper@1009 : Codeunit "Type Helper";
      DocExchServiceMgt@1001 : Codeunit "Doc. Exch. Service Mgt.";
      StatusFieldRef@1006 : FieldRef;
      IdentifierFieldRef@1008 : FieldRef;
      Options@1010 : Text;
      DocExchIdentifier@1013 : Text;
      DocExchStatus@1012 : Integer;
    BEGIN
      if not IsSupportedByDefaultDocExchStatusDrillDown(DocRecRef) then
        exit;
      DataTypeManagement.FindFieldByName(DocRecRef,StatusFieldRef,'Document Exchange Status');
      Options := StatusFieldRef.OPTIONSTRING;
      DocExchStatus := StatusFieldRef.VALUE;
      case DocExchStatus of
        TypeHelper.GetOptionNo(FORMAT(SalesInvoiceHeader."Document Exchange Status"::"Not Sent"),Options):
          exit;
        TypeHelper.GetOptionNo(FORMAT(SalesInvoiceHeader."Document Exchange Status"::"Sent to Document Exchange Service"),Options),
        TypeHelper.GetOptionNo(FORMAT(SalesInvoiceHeader."Document Exchange Status"::"Pending Connection to Recipient"),Options):
          if CONFIRM(CheckLatestQst,true) then
            CheckAndUpdateDocExchStatus(DocRecRef);
        TypeHelper.GetOptionNo(FORMAT(SalesInvoiceHeader."Document Exchange Status"::"Delivered to Recipient"),Options):
          begin
            DataTypeManagement.FindFieldByName(DocRecRef,IdentifierFieldRef,'Document Exchange Identifier');
            DocExchIdentifier := IdentifierFieldRef.VALUE;
            HYPERLINK(DocExchServiceMgt.GetExternalDocURL(DocExchIdentifier));
            exit;
          end;
      end;
      ActivityLog.ShowEntries(DocRecRef.RECORDID);
    END;

    LOCAL PROCEDURE IsSupportedByDefaultDocExchStatusDrillDown@52(DocRecRef@1000 : RecordRef) : Boolean;
    BEGIN
      exit(DocRecRef.NUMBER in [DATABASE::"Sales Invoice Header",DATABASE::"Sales Cr.Memo Header",
                                DATABASE::"Service Invoice Header",DATABASE::"Service Cr.Memo Header"]);
    END;

    [Integration]
    [External]
    PROCEDURE OnDocExchStatusDrillDown@55(VAR DocRecRef@1000 : RecordRef;VAR Handled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

