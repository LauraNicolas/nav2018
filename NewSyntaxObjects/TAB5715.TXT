OBJECT Table 5715 Item Substitution
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ItemSubstitution@1001 : Record "Item Substitution";
             BEGIN
               if Interchangeable then
                 if CONFIRM(Text001 + Text002) then
                   DeleteInterchangeableItem(Type,"No.","Variant Code","Substitute Type","Substitute No.","Substitute Variant Code")
                 else
                   if ItemSubstitution.GET(
                        "Substitute Type",
                        "Substitute No.",
                        "Substitute Variant Code",
                        Type,
                        "No.",
                        "Variant Code")
                   then begin
                     ItemSubstitution.Interchangeable := false;
                     ItemSubstitution.MODIFY;
                   end;

               if Condition then begin
                 SubCondition.SETRANGE(Type,Type);
                 SubCondition.SETRANGE("No.","No.");
                 SubCondition.SETRANGE("Variant Code","Variant Code");
                 SubCondition.SETRANGE("Substitute Type","Substitute Type");
                 SubCondition.SETRANGE("Substitute No.","Substitute No.");
                 SubCondition.SETRANGE("Substitute Variant Code","Substitute Variant Code");
                 SubCondition.DELETEALL;
               end;
             END;

    CaptionML=ENU=Item Substitution;
    LookupPageID="Item Substitution Entry";
    DrillDownPageID="Item Substitution Entry";
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item."No."
                                                                 ELSE IF (Type=CONST("Nonstock Item")) "Nonstock Item"."Entry No.";
                                                   OnValidate=BEGIN
                                                                if "No." <> xRec."No." then begin
                                                                  "Variant Code" := '';
                                                                  if Interchangeable then
                                                                    DeleteInterchangeableItem(
                                                                      Type,
                                                                      xRec."No.",
                                                                      "Variant Code",
                                                                      "Substitute Type",
                                                                      "Substitute No.",
                                                                      "Substitute Variant Code");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No.;
                                                   NotBlank=true }
    { 2   ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE ("Item No."=FIELD("No."));
                                                   OnValidate=BEGIN
                                                                if "Variant Code" <> xRec."Variant Code" then
                                                                  RecreateSubstEntry(xRec."Variant Code","Substitute Variant Code");
                                                              END;

                                                   CaptionML=ENU=Variant Code }
    { 3   ;   ;Substitute No.      ;Code20        ;TableRelation=IF ("Substitute Type"=CONST(Item)) Item."No."
                                                                 ELSE IF ("Substitute Type"=CONST("Nonstock Item")) "Nonstock Item"."Entry No.";
                                                   OnValidate=BEGIN
                                                                if (Type = "Substitute Type") and
                                                                   ("No." = "Substitute No.") and
                                                                   ("Variant Code" = "Substitute Variant Code")
                                                                then
                                                                  ERROR(Text000);

                                                                if "Substitute No." <> xRec."Substitute No." then
                                                                  if Interchangeable then
                                                                    DeleteInterchangeableItem(
                                                                      Type,
                                                                      "No.",
                                                                      "Variant Code",
                                                                      "Substitute Type",
                                                                      xRec."Substitute No.",
                                                                      "Substitute Variant Code");

                                                                SetItemVariantDescription("Substitute Type","Substitute No.","Substitute Variant Code",Description);
                                                              END;

                                                   CaptionML=ENU=Substitute No.;
                                                   NotBlank=true }
    { 4   ;   ;Substitute Variant Code;Code10     ;TableRelation="Item Variant".Code WHERE ("Item No."=FIELD("Substitute No."));
                                                   OnValidate=BEGIN
                                                                if (Type = "Substitute Type") and
                                                                   ("No." = "Substitute No.") and
                                                                   ("Variant Code" = "Substitute Variant Code")
                                                                then
                                                                  ERROR(Text000);

                                                                if "Substitute Variant Code" <> xRec."Substitute Variant Code" then
                                                                  RecreateSubstEntry("Variant Code",xRec."Substitute Variant Code");

                                                                Description := GetItemVariantDescription;
                                                              END;

                                                   CaptionML=ENU=Substitute Variant Code }
    { 5   ;   ;Description         ;Text50        ;CaptionML=ENU=Description;
                                                   Editable=false }
    { 6   ;   ;Inventory           ;Decimal       ;CaptionML=ENU=Inventory;
                                                   DecimalPlaces=0:5 }
    { 7   ;   ;Interchangeable     ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                                TESTFIELD("Substitute No.");
                                                                if not Interchangeable then
                                                                  DeleteInterchangeableItem(Type,"No.","Variant Code","Substitute Type","Substitute No.","Substitute Variant Code")
                                                                else
                                                                  CreateInterchangeableItem;
                                                              END;

                                                   CaptionML=ENU=Interchangeable }
    { 8   ;   ;Condition           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Substitution Condition" WHERE (Type=FIELD(Type),
                                                                                                     "No."=FIELD("No."),
                                                                                                     "Variant Code"=FIELD("Variant Code"),
                                                                                                     "Substitute Type"=FIELD("Substitute Type"),
                                                                                                     "Substitute No."=FIELD("Substitute No."),
                                                                                                     "Substitute Variant Code"=FIELD("Substitute Variant Code")));
                                                   CaptionML=ENU=Condition;
                                                   Editable=false }
    { 9   ;   ;Location Filter     ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Location;
                                                   CaptionML=ENU=Location Filter }
    { 100 ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                if Type <> xRec.Type then begin
                                                                  if Interchangeable then
                                                                    DeleteInterchangeableItem(
                                                                      xRec.Type,
                                                                      "No.",
                                                                      "Variant Code",
                                                                      "Substitute Type",
                                                                      "Substitute No.",
                                                                      "Substitute Variant Code");
                                                                  if xRec."No." <> '' then
                                                                    VALIDATE("No.",'');
                                                                  VALIDATE("Substitute No.",'');
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Item,Nonstock Item;
                                                   OptionString=Item,"Nonstock Item" }
    { 101 ;   ;Substitute Type     ;Option        ;OnValidate=BEGIN
                                                                if (Type = "Substitute Type") and
                                                                   ("No." = "Substitute No.") and
                                                                   ("Variant Code" = "Substitute Variant Code")
                                                                then
                                                                  ERROR(Text000);

                                                                if "Substitute Type" <> xRec."Substitute Type" then begin
                                                                  if Interchangeable then
                                                                    DeleteInterchangeableItem(
                                                                      Type,
                                                                      "No.",
                                                                      "Variant Code",
                                                                      xRec."Substitute Type",
                                                                      "Substitute No.",
                                                                      "Substitute Variant Code");
                                                                  Description := '';
                                                                  "Variant Code" := '';
                                                                  "Substitute No." := '';
                                                                  Interchangeable := false;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Substitute Type;
                                                   OptionCaptionML=ENU=Item,Nonstock Item;
                                                   OptionString=Item,"Nonstock Item" }
    { 102 ;   ;Sub. Item No.       ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Nonstock Item"."Item No." WHERE ("Entry No."=FIELD("Substitute No.")));
                                                   CaptionML=ENU=Sub. Item No.;
                                                   Editable=false }
    { 103 ;   ;Relations Level     ;Integer       ;CaptionML=ENU=Relations Level;
                                                   Editable=false }
    { 104 ;   ;Quantity Avail. on Shpt. Date;Decimal;
                                                   CaptionML=ENU=Quantity Avail. on Shpt. Date;
                                                   DecimalPlaces=0:5 }
    { 105 ;   ;Shipment Date       ;Date          ;CaptionML=ENU=Shipment Date }
  }
  KEYS
  {
    {    ;Type,"No.","Variant Code","Substitute Type","Substitute No.","Substitute Variant Code";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You can not set up an item to be substituted by itself.';
      Text001@1001 : TextConst 'ENU=This substitute is interchangeable. \';
      Text002@1002 : TextConst 'ENU=Do you want to delete the corresponding substitute?';
      SubCondition@1007 : Record "Substitution Condition";
      ItemVariant@1003 : Record "Item Variant";

    LOCAL PROCEDURE CreateSubstitution@6(ItemNo1@1000 : Code[20];Variant1@1001 : Code[10];ItemNo2@1002 : Code[20];Variant2@1003 : Code[10];Substitutable@1004 : Boolean);
    BEGIN
      INIT;
      Type := Type::Item;
      "No." := ItemNo1;
      "Variant Code" := Variant1;
      "Substitute Type" := "Substitute Type"::Item;
      "Substitute No." := ItemNo2;
      "Substitute Variant Code" := Variant2;
      Interchangeable := Substitutable;
      SetItemVariantDescription(Type,"No.","Variant Code",Description);
    END;

    [External]
    PROCEDURE CreateSubstitutionItem2Item@5(ItemNo1@1000 : Code[20];Variant1@1001 : Code[10];ItemNo2@1002 : Code[20];Variant2@1003 : Code[10];Substitutable@1004 : Boolean);
    BEGIN
      CreateSubstitution(ItemNo1,Variant1,ItemNo2,Variant2,Substitutable);
      INSERT;
      if Substitutable then
        CreateSubstitution(ItemNo2,Variant2,ItemNo1,Variant1,Substitutable);
    END;

    LOCAL PROCEDURE CreateInterchangeableItem@1();
    VAR
      ItemSubstitution@1001 : Record "Item Substitution";
    BEGIN
      ItemSubstitution.Type := "Substitute Type";
      ItemSubstitution."No." := "Substitute No.";
      ItemSubstitution."Variant Code" := "Substitute Variant Code";
      ItemSubstitution."Substitute Type" := Type;
      ItemSubstitution."Substitute No." := "No.";
      ItemSubstitution."Substitute Variant Code" := "Variant Code";
      SetDescription(Type,"No.",ItemSubstitution.Description);
      ItemSubstitution.Interchangeable := true;
      if ItemSubstitution.FIND then
        ItemSubstitution.MODIFY
      else
        ItemSubstitution.INSERT;
    END;

    LOCAL PROCEDURE DeleteInterchangeableItem@2(XType@1000 : Integer;XNo@1001 : Code[20];XVariantCode@1002 : Code[10];XSubstType@1003 : Integer;XSubstNo@1004 : Code[20];XSubstVariantCode@1005 : Code[10]);
    VAR
      ItemSubstitution@1007 : Record "Item Substitution";
    BEGIN
      ItemSubstitution.Type := XSubstType;
      ItemSubstitution."No." := XSubstNo;
      ItemSubstitution."Variant Code" := XSubstVariantCode;
      ItemSubstitution."Substitute Type" := XType;
      ItemSubstitution."Substitute No." := XNo;
      ItemSubstitution."Substitute Variant Code" := XVariantCode;
      if ItemSubstitution.FIND then begin
        ItemSubstitution.CALCFIELDS(Condition);
        if ItemSubstitution.Condition then begin
          SubCondition.SETRANGE(Type,XType);
          SubCondition.SETRANGE("No.",XNo);
          SubCondition.SETRANGE("Variant Code",XVariantCode);
          SubCondition.SETRANGE("Substitute Type",XSubstType);
          SubCondition.SETRANGE("Substitute No.",XSubstNo);
          SubCondition.SETRANGE("Substitute Variant Code",XSubstVariantCode);
          SubCondition.DELETEALL;
        end;
        ItemSubstitution.DELETE;
        Interchangeable := false;
      end;
    END;

    LOCAL PROCEDURE RecreateSubstEntry@3(XVariantCode@1000 : Code[10];XSubstVariantCode@1001 : Code[10]);
    BEGIN
      if xRec.Interchangeable then
        DeleteInterchangeableItem(
          xRec.Type,
          xRec."No.",
          XVariantCode,
          xRec."Substitute Type",
          xRec."Substitute No.",
          XSubstVariantCode);
    END;

    LOCAL PROCEDURE GetItemVariantDescription@4() : Text[50];
    VAR
      Item@1002 : Record Item;
    BEGIN
      if "Substitute Variant Code" <> '' then
        if ItemVariant.GET("Substitute No.","Substitute Variant Code") then
          if ItemVariant.Description <> '' then
            exit(ItemVariant.Description);

      if Item.GET("Substitute No.") then
        exit(Item.Description);
    END;

    [External]
    PROCEDURE SetDescription@8(Type@1000 : Integer;Number@1001 : Code[20];VAR Description@1002 : Text[50]);
    VAR
      Item@1003 : Record Item;
      NonstockItem@1004 : Record "Nonstock Item";
    BEGIN
      Description := '';
      if Type = 1 then begin
        if NonstockItem.GET(Number) then
          Description := NonstockItem.Description;
      end else
        if Item.GET(Number) then
          Description := Item.Description;
    END;

    [External]
    PROCEDURE SetItemVariantDescription@11(Type@1002 : Integer;Number@1001 : Code[20];Variant@1003 : Code[10];VAR Description@1000 : Text[50]);
    VAR
      Item@1004 : Record Item;
      NonstockItem@1005 : Record "Nonstock Item";
      ItemVariant@1006 : Record "Item Variant";
    BEGIN
      Description := '';
      if Type = 1 then begin
        if NonstockItem.GET(Number) then
          Description := NonstockItem.Description;
      end else begin
        if Variant <> '' then
          if ItemVariant.GET(Number,Variant) then
            if ItemVariant.Description <> '' then begin
              Description := ItemVariant.Description;
              exit;
            end;
        if Item.GET(Number) then
          Description := Item.Description;
      end;
    END;

    BEGIN
    END.
  }
}

