OBJECT Codeunit 1708 Exp. Writing Pos. Pay
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Data Exch."=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [Internal]
    PROCEDURE ExportPositivePay@1(DataExchEntryCodeDetail@1001 : Integer;DataExchEntryCodeFooter@1000 : Integer;Filename@1003 : Text;DataExchEntryCodeFooterArray@1011 : ARRAY [100] OF Integer);
    VAR
      DataExchFooter@1004 : Record "Data Exch.";
      DataExchDetail@1005 : Record "Data Exch.";
      TempBlob@1010 : Record TempBlob;
      TempBlob2@1006 : Record TempBlob;
      ExportFile@1008 : File;
      OutStream@1007 : OutStream;
      InStream@1009 : InStream;
      Filename2@1002 : Text[250];
      RecordCount@1015 : Integer;
      ArrayLength@1014 : Integer;
    BEGIN
      // Need to copy the File Name and File from the footer to the Detail record.
      ExportFile.WRITEMODE := true;
      ExportFile.TEXTMODE := true;
      ExportFile.OPEN(Filename);

      // Copy current file contents to TempBlob
      ExportFile.CREATEINSTREAM(InStream);
      TempBlob.Blob.CREATEOUTSTREAM(OutStream);
      COPYSTREAM(OutStream,InStream);

      Filename2 := COPYSTR(Filename,1,250);

      DataExchDetail.SETRANGE("Entry No.",DataExchEntryCodeDetail);
      if DataExchDetail.FINDFIRST then begin
        DataExchDetail."File Name" := Filename2;
        DataExchDetail."File Content" := TempBlob.Blob;
        DataExchDetail.MODIFY;
      end;
      ExportFile.CLOSE;

      // Need to clear out the File Name and blob (File Content) for the footer record(s)
      DataExchFooter.SETRANGE("Entry No.",DataExchEntryCodeFooter);
      if DataExchFooter.FINDFIRST then begin
        ArrayLength := ARRAYLEN(DataExchEntryCodeFooterArray);
        RecordCount := 1;
        while (DataExchEntryCodeFooterArray[RecordCount] > 0) and (RecordCount < ArrayLength) do begin
          DataExchFooter."Entry No." := DataExchEntryCodeFooterArray[RecordCount];
          DataExchFooter."File Name" := '';
          TempBlob2.Blob.CREATEOUTSTREAM(OutStream);
          DataExchFooter."File Content" := TempBlob2.Blob;
          DataExchFooter.MODIFY;
          RecordCount := RecordCount + 1;
        end;
      end;
    END;

    [External]
    PROCEDURE CleanUpPositivePayWorkTables@2(DataExchEntryCodeHeaderArray@1005 : ARRAY [100] OF Integer;DataExchEntryCodeDetailArray@1004 : ARRAY [100] OF Integer;DataExchEntryCodeFooterArray@1003 : ARRAY [100] OF Integer);
    VAR
      PositivePayHeader@1009 : Record "Positive Pay Header";
      PositivePayDetail@1008 : Record "Positive Pay Detail";
      PositivePayFooter@1007 : Record "Positive Pay Footer";
      RecordCount@1002 : Integer;
      ArrayLength@1006 : Integer;
    BEGIN
      ArrayLength := ARRAYLEN(DataExchEntryCodeHeaderArray);
      RecordCount := 1;
      while (DataExchEntryCodeHeaderArray[RecordCount] > 0) and (RecordCount < ArrayLength) do begin
        PositivePayHeader.SETRANGE("Data Exch. Entry No.",DataExchEntryCodeHeaderArray[RecordCount]);
        PositivePayHeader.DELETEALL;
        RecordCount := RecordCount + 1;
      end;

      ArrayLength := ARRAYLEN(DataExchEntryCodeDetailArray);
      RecordCount := 1;
      while (DataExchEntryCodeDetailArray[RecordCount] > 0) and (RecordCount < ArrayLength) do begin
        PositivePayDetail.SETRANGE("Data Exch. Entry No.",DataExchEntryCodeDetailArray[RecordCount]);
        PositivePayDetail.DELETEALL;
        RecordCount := RecordCount + 1;
      end;

      ArrayLength := ARRAYLEN(DataExchEntryCodeFooterArray);
      RecordCount := 1;
      while (DataExchEntryCodeFooterArray[RecordCount] > 0) and (RecordCount < ArrayLength) do begin
        PositivePayFooter.SETRANGE("Data Exch. Entry No.",DataExchEntryCodeFooterArray[RecordCount]);
        PositivePayFooter.DELETEALL;
        RecordCount := RecordCount + 1;
      end;
    END;

    BEGIN
    END.
  }
}

