OBJECT Page 6013 Resource Capacity Settings
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Resource Capacity Settings;
    SourceTable=Resource;
    PageType=Card;
    OnOpenPage=BEGIN
                 StartDate := 0D;
                 EndDate := 0D;
                 WorkTemplateCode := '';
               END;

    OnAfterGetRecord=BEGIN
                       if not WorkTemplateRec.GET(WorkTemplateCode) and ("No." <> xRec."No.") then
                         CLEAR(WorkTemplateRec);
                       SumWeekTotal;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      Name=ActionContainer1900000004;
                      ActionContainerType=ActionItems }
      { 50      ;1   ;Action    ;
                      Name=UpdateCapacity;
                      CaptionML=ENU=Update &Capacity;
                      ApplicationArea=#Advanced;
                      Promoted=true;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 NewCapacity@1000 : Decimal;
                               BEGIN
                                 if StartDate = 0D then
                                   ERROR(Text002);

                                 if EndDate = 0D then
                                   ERROR(Text003);

                                 if not CONFIRM(Text004,false,TABLECAPTION,"No.") then
                                   exit;

                                 if CompanyInformation.GET then
                                   if CompanyInformation."Base Calendar Code" <> '' then
                                     CalendarCustomized :=
                                       CalendarMgmt.CustomizedChangesExist(CalChange."Source Type"::Company,'','',CompanyInformation."Base Calendar Code");

                                 ResCapacityEntry.RESET;
                                 ResCapacityEntry.SETCURRENTKEY("Resource No.",Date);
                                 ResCapacityEntry.SETRANGE("Resource No.","No.");
                                 TempDate := StartDate;
                                 ChangedDays := 0;
                                 repeat
                                   if CalendarCustomized then
                                     Holiday :=
                                       CalendarMgmt.CheckCustomizedDateStatus(
                                         CalChange."Source Type"::Company,'','',CompanyInformation."Base Calendar Code",TempDate,NewDescription)
                                   else
                                     Holiday := CalendarMgmt.CheckDateStatus(CompanyInformation."Base Calendar Code",TempDate,NewDescription);

                                   ResCapacityEntry.SETRANGE(Date,TempDate);
                                   ResCapacityEntry.CALCSUMS(Capacity);
                                   TempCapacity := ResCapacityEntry.Capacity;

                                   if Holiday then
                                     if TempCapacity = 0 then
                                       NewCapacity := 0
                                     else begin
                                       // post reverse capacity entry to have zero balance
                                       NewCapacity := SelectCapacity;
                                       if NewCapacity > TempCapacity then
                                         NewCapacity := TempCapacity;
                                     end
                                   else
                                     NewCapacity := TempCapacity - SelectCapacity;

                                   if NewCapacity <> 0 then begin
                                     ResCapacityEntry2.RESET;
                                     if ResCapacityEntry2.FINDLAST then;
                                     LastEntry := ResCapacityEntry2."Entry No." + 1;
                                     ResCapacityEntry2.RESET;
                                     ResCapacityEntry2."Entry No." := LastEntry;
                                     ResCapacityEntry2.Capacity := -NewCapacity;
                                     ResCapacityEntry2."Resource No." := "No.";
                                     ResCapacityEntry2."Resource Group No." := "Resource Group No.";
                                     ResCapacityEntry2.Date := TempDate;
                                     if ResCapacityEntry2.INSERT(true) then;
                                     ChangedDays := ChangedDays + 1;
                                   end;
                                   TempDate := TempDate + 1;
                                 until TempDate > EndDate;
                                 COMMIT;
                                 if ChangedDays > 1 then
                                   MESSAGE(Text006,ChangedDays)
                                 else
                                   if ChangedDays = 1 then
                                     MESSAGE(Text007,ChangedDays)
                                   else
                                     MESSAGE(Text008);
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General }

    { 41  ;2   ;Field     ;
                Name=StartDate;
                CaptionML=ENU=Starting Date;
                ToolTipML=ENU=Specifies the starting date for the time period for which you want to change capacity.;
                ApplicationArea=#Jobs;
                SourceExpr=StartDate;
                ImplicitType=Date }

    { 45  ;2   ;Field     ;
                Name=EndDate;
                CaptionML=ENU=Ending Date;
                ToolTipML=ENU=Specifies the end date relating to the resource capacity.;
                ApplicationArea=#Jobs;
                SourceExpr=EndDate;
                OnValidate=BEGIN
                             if StartDate > EndDate then
                               ERROR(Text000);
                           END;

                ImplicitType=Date }

    { 16  ;2   ;Field     ;
                Name=WorkTemplateCode;
                CaptionML=ENU=Work-Hour Template;
                ToolTipML=ENU=Specifies the number of hours in the work week: 30, 36, or 40.;
                ApplicationArea=#Jobs;
                SourceExpr=WorkTemplateCode;
                TableRelation="Work-Hour Template";
                LookupPageID="Work-Hour Templates";
                OnValidate=BEGIN
                             WorkTemplateCodeOnAfterValidat;
                           END;

                ImplicitType=Code10 }

    { 23  ;2   ;Field     ;
                Name=Monday;
                CaptionML=ENU=Monday;
                ToolTipML=ENU=Specifies the number of work-hours on Monday.;
                ApplicationArea=#Jobs;
                SourceExpr=WorkTemplateRec.Monday;
                MinValue=0;
                MaxValue=24;
                OnValidate=BEGIN
                             WorkTemplateRecMondayOnAfterVa;
                           END;
                            }

    { 26  ;2   ;Field     ;
                Name=Tuesday;
                CaptionML=ENU=Tuesday;
                ToolTipML=ENU=Specifies the number of work-hours on Tuesday.;
                ApplicationArea=#Jobs;
                SourceExpr=WorkTemplateRec.Tuesday;
                MinValue=0;
                MaxValue=24;
                OnValidate=BEGIN
                             WorkTemplateRecTuesdayOnAfterV;
                           END;
                            }

    { 29  ;2   ;Field     ;
                Name=Wednesday;
                CaptionML=ENU=Wednesday;
                ToolTipML=ENU=Specifies the number of work-hours on Wednesday.;
                ApplicationArea=#Jobs;
                SourceExpr=WorkTemplateRec.Wednesday;
                MinValue=0;
                MaxValue=24;
                OnValidate=BEGIN
                             WorkTemplateRecWednesdayOnAfte;
                           END;
                            }

    { 31  ;2   ;Field     ;
                Name=Thursday;
                CaptionML=ENU=Thursday;
                ToolTipML=ENU=Specifies the number of work-hours on Thursday.;
                ApplicationArea=#Jobs;
                SourceExpr=WorkTemplateRec.Thursday;
                MinValue=0;
                MaxValue=24;
                OnValidate=BEGIN
                             WorkTemplateRecThursdayOnAfter;
                           END;
                            }

    { 33  ;2   ;Field     ;
                Name=Friday;
                CaptionML=ENU=Friday;
                ToolTipML=ENU=Specifies the work-hour schedule for Friday.;
                ApplicationArea=#Jobs;
                SourceExpr=WorkTemplateRec.Friday;
                MinValue=0;
                MaxValue=24;
                OnValidate=BEGIN
                             WorkTemplateRecFridayOnAfterVa;
                           END;
                            }

    { 35  ;2   ;Field     ;
                Name=Saturday;
                CaptionML=ENU=Saturday;
                ToolTipML=ENU=Specifies the number of work-hours on Friday.;
                ApplicationArea=#Jobs;
                SourceExpr=WorkTemplateRec.Saturday;
                MinValue=0;
                MaxValue=24;
                OnValidate=BEGIN
                             WorkTemplateRecSaturdayOnAfter;
                           END;
                            }

    { 37  ;2   ;Field     ;
                Name=Sunday;
                CaptionML=ENU=Sunday;
                ToolTipML=ENU=Specifies the number of work-hours on Saturday.;
                ApplicationArea=#Jobs;
                SourceExpr=WorkTemplateRec.Sunday;
                MinValue=0;
                MaxValue=24;
                OnValidate=BEGIN
                             WorkTemplateRecSundayOnAfterVa;
                           END;
                            }

    { 39  ;2   ;Field     ;
                Name=WeekTotal;
                CaptionML=ENU=Week Total;
                ToolTipML=ENU=Specifies the total number of hours for the week. The total is calculated automatically.;
                ApplicationArea=#Jobs;
                DecimalPlaces=0:5;
                SourceExpr=WeekTotal;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 1900000007;0;Container;
                Name=Container1900000007;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Name=Part1900383207;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Name=Part1905767507;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The starting date is later than the ending date.';
      Text002@1002 : TextConst 'ENU=You must fill in the Starting Date field.';
      Text003@1003 : TextConst 'ENU=You must fill in the Ending Date field.';
      Text004@1004 : TextConst '@@@=Do you want to change the capacity for NO No.?;ENU=Do you want to change the capacity for %1 %2?';
      Text006@1006 : TextConst 'ENU=The capacity for %1 days was changed successfully.';
      Text007@1007 : TextConst 'ENU=The capacity for %1 day was changed successfully.';
      Text008@1008 : TextConst 'ENU=The capacity change was unsuccessful.';
      CalChange@1023 : Record "Customized Calendar Change";
      WorkTemplateRec@1009 : Record "Work-Hour Template";
      ResCapacityEntry@1010 : Record "Res. Capacity Entry";
      CompanyInformation@1102601000 : Record "Company Information";
      ResCapacityEntry2@1011 : Record "Res. Capacity Entry";
      CalendarMgmt@1022 : Codeunit "Calendar Management";
      WorkTemplateCode@1013 : Code[10];
      StartDate@1014 : Date;
      EndDate@1015 : Date;
      WeekTotal@1016 : Decimal;
      TempDate@1017 : Date;
      TempCapacity@1018 : Decimal;
      ChangedDays@1019 : Integer;
      LastEntry@1020 : Decimal;
      CalendarCustomized@1012 : Boolean;
      Holiday@1024 : Boolean;
      NewDescription@1025 : Text[50];

    LOCAL PROCEDURE SelectCapacity@1() Hours@1000 : Decimal;
    BEGIN
      case DATE2DWY(TempDate,1) of
        1:
          Hours := WorkTemplateRec.Monday;
        2:
          Hours := WorkTemplateRec.Tuesday;
        3:
          Hours := WorkTemplateRec.Wednesday;
        4:
          Hours := WorkTemplateRec.Thursday;
        5:
          Hours := WorkTemplateRec.Friday;
        6:
          Hours := WorkTemplateRec.Saturday;
        7:
          Hours := WorkTemplateRec.Sunday;
      end;
    END;

    LOCAL PROCEDURE SumWeekTotal@2();
    BEGIN
      WeekTotal := WorkTemplateRec.Monday + WorkTemplateRec.Tuesday + WorkTemplateRec.Wednesday +
        WorkTemplateRec.Thursday + WorkTemplateRec.Friday + WorkTemplateRec.Saturday + WorkTemplateRec.Sunday;
    END;

    LOCAL PROCEDURE WorkTemplateCodeOnAfterValidat@19020447();
    BEGIN
      if WorkTemplateRec.GET(WorkTemplateCode) then;
      SumWeekTotal;
    END;

    LOCAL PROCEDURE WorkTemplateRecMondayOnAfterVa@19038558();
    BEGIN
      SumWeekTotal;
    END;

    LOCAL PROCEDURE WorkTemplateRecTuesdayOnAfterV@19038753();
    BEGIN
      SumWeekTotal;
    END;

    LOCAL PROCEDURE WorkTemplateRecWednesdayOnAfte@19032807();
    BEGIN
      SumWeekTotal;
    END;

    LOCAL PROCEDURE WorkTemplateRecThursdayOnAfter@19058780();
    BEGIN
      SumWeekTotal;
    END;

    LOCAL PROCEDURE WorkTemplateRecFridayOnAfterVa@19036770();
    BEGIN
      SumWeekTotal;
    END;

    LOCAL PROCEDURE WorkTemplateRecSaturdayOnAfter@19074542();
    BEGIN
      SumWeekTotal;
    END;

    LOCAL PROCEDURE WorkTemplateRecSundayOnAfterVa@19029469();
    BEGIN
      SumWeekTotal;
    END;

    BEGIN
    END.
  }
}

