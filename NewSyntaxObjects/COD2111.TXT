OBJECT Codeunit 2111 O365 Sales Background Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Marketing Setup"=rimd;
    OnRun=BEGIN
            InitializeGraphSync;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE InitializeGraphSync@31();
    VAR
      MarketingSetup@1000 : Record "Marketing Setup";
      CompanyInformation@1002 : Record "Company Information";
      AzureADUserManagement@1003 : Codeunit "Azure AD User Management";
      WebhookManagement@1001 : Codeunit "Webhook Management";
      GraphSyncRunner@1005 : Codeunit "Graph Sync. Runner";
      TenantDetail@1004 : DotNet "'Microsoft.Azure.ActiveDirectory.GraphClient, Version=1.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Azure.ActiveDirectory.GraphClient.TenantDetail";
    BEGIN
      MarketingSetup.LOCKTABLE;
      if not MarketingSetup.GET then
        MarketingSetup.INSERT(true);

      if not MarketingSetup."Sync with Microsoft Graph" then
        MarketingSetup.VALIDATE("Sync with Microsoft Graph",true);
      MarketingSetup.MODIFY(true);

      if not WebhookManagement.IsCurrentClientTypeAllowed then
        exit;

      if not WebhookManagement.IsSyncAllowed then
        exit;

      if not GraphSyncRunner.IsGraphSyncEnabled then
        exit;

      TASKSCHEDULER.CREATETASK(CODEUNIT::"Graph Subscription Management",CODEUNIT::"Graph Delta Sync",
        true,COMPANYNAME,CURRENTDATETIME + 200); // Add 200 ms

      CompanyInformation.GET;
      if CompanyInformation."Sync with O365 Bus. profile" then
        TASKSCHEDULER.CREATETASK(CODEUNIT::"Business Profile Sync. Runner",0,true,COMPANYNAME,0DT)
      else
        if AzureADUserManagement.GetTenantDetail(TenantDetail) then begin
          CompanyInformation.LOCKTABLE;

          CompanyInformation.Name := COPYSTR(TenantDetail.DisplayName,1,MAXSTRLEN(CompanyInformation.Name));
          CompanyInformation.Address := COPYSTR(TenantDetail.Street,1,MAXSTRLEN(CompanyInformation.Address));
          CompanyInformation.City := COPYSTR(TenantDetail.City,1,MAXSTRLEN(CompanyInformation.City));
          CompanyInformation."Post Code" := COPYSTR(TenantDetail.PostalCode,1,MAXSTRLEN(CompanyInformation."Post Code"));
          CompanyInformation.County := COPYSTR(TenantDetail.State,1,MAXSTRLEN(CompanyInformation.County));
          CompanyInformation."Country/Region Code" :=
            COPYSTR(TenantDetail.CountryLetterCode,1,MAXSTRLEN(CompanyInformation."Country/Region Code"));
          CompanyInformation.MODIFY(false);
        end;
    END;

    BEGIN
    END.
  }
}

