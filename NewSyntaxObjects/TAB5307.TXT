OBJECT Table 5307 Outlook Synch. Option Correl.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Outlook Value");

               CheckDuplicatedRecords;
             END;

    OnModify=BEGIN
               CheckDuplicatedRecords;
             END;

    CaptionML=ENU=Outlook Synch. Option Correl.;
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                SetDefaults;
                                                              END;

                                                   CaptionML=ENU=Synch. Entity Code;
                                                   NotBlank=true }
    { 2   ;   ;Element No.         ;Integer       ;CaptionML=ENU=Element No. }
    { 3   ;   ;Field Line No.      ;Integer       ;CaptionML=ENU=Field Line No. }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 5   ;   ;Outlook Object      ;Text80        ;CaptionML=ENU=Outlook Object }
    { 6   ;   ;Outlook Property    ;Text80        ;CaptionML=ENU=Outlook Property }
    { 7   ;   ;Outlook Value       ;Text80        ;OnValidate=VAR
                                                                IntVar@1000 : Integer;
                                                              BEGIN
                                                                OSynchField.RESET;
                                                                OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");

                                                                if OSynchSetupMgt.CheckOEnumeration(OSynchField) then begin
                                                                  if "Element No." = 0 then
                                                                    OSynchSetupMgt.ValidateEnumerationValue(
                                                                      "Outlook Value",
                                                                      "Enumeration No.",
                                                                      OSynchField."Outlook Object",
                                                                      '',
                                                                      OSynchField."Outlook Property")
                                                                  else begin
                                                                    OSynchEntity.GET("Synch. Entity Code");
                                                                    OSynchSetupMgt.ValidateEnumerationValue(
                                                                      "Outlook Value",
                                                                      "Enumeration No.",
                                                                      OSynchEntity."Outlook Item",
                                                                      OSynchField."Outlook Object",
                                                                      OSynchField."Outlook Property");
                                                                  end;
                                                                end else begin
                                                                  if not EVALUATE(IntVar,"Outlook Value") then
                                                                    ERROR(Text002);

                                                                  "Enumeration No." := IntVar;
                                                                end;
                                                              END;

                                                   OnLookup=VAR
                                                              OutlookValue@1000 : Text[80];
                                                              EnumerationNo@1001 : Integer;
                                                            BEGIN
                                                              OSynchField.RESET;
                                                              OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");
                                                              if not OSynchSetupMgt.CheckOEnumeration(OSynchField) then
                                                                ERROR(Text003);

                                                              if "Element No." = 0 then
                                                                OutlookValue := OSynchSetupMgt.ShowEnumerationsLookup("Outlook Object",'',"Outlook Property",EnumerationNo)
                                                              else begin
                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                OutlookValue :=
                                                                  OSynchSetupMgt.ShowEnumerationsLookup(
                                                                    OSynchEntity."Outlook Item",
                                                                    "Outlook Object",
                                                                    "Outlook Property",
                                                                    EnumerationNo);
                                                              end;

                                                              if OutlookValue = '' then
                                                                exit;

                                                              "Outlook Value" := OutlookValue;
                                                              "Enumeration No." := EnumerationNo;
                                                            END;

                                                   CaptionML=ENU=Outlook Value }
    { 8   ;   ;Table No.           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Table));
                                                   CaptionML=ENU=Table No. }
    { 9   ;   ;Field No.           ;Integer       ;TableRelation=Field."No." WHERE (TableNo=FIELD("Table No."));
                                                   CaptionML=ENU=Field No. }
    { 11  ;   ;Option No.          ;Integer       ;CaptionML=ENU=Option No.;
                                                   Editable=false }
    { 12  ;   ;Enumeration No.     ;Integer       ;CaptionML=ENU=Enumeration No.;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Synch. Entity Code","Element No.","Field Line No.","Line No.";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntity@1000 : Record "Outlook Synch. Entity";
      OSynchEntityElement@1001 : Record "Outlook Synch. Entity Element";
      OSynchField@1002 : Record "Outlook Synch. Field";
      OSynchSetupMgt@1003 : Codeunit "Outlook Synch. Setup Mgt.";
      Text001@1006 : TextConst 'ENU=The line you are trying to create already exists.';
      Text002@1007 : TextConst 'ENU=This value is not valid. It must be either an integer or an enumeration element.';
      Text003@1008 : TextConst 'ENU=The look up window table cannot be opened because this Outlook property is not of the enumeration type.';

    [External]
    PROCEDURE SetDefaults@1();
    BEGIN
      if "Element No." = 0 then begin
        OSynchEntity.GET("Synch. Entity Code");
        "Outlook Object" := OSynchEntity."Outlook Item";
      end else begin
        OSynchEntityElement.GET("Synch. Entity Code","Element No.");
        "Outlook Object" := OSynchEntityElement."Outlook Collection";
      end;

      OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");
      "Outlook Property" := OSynchField."Outlook Property";
      "Field No." := OSynchField."Field No.";

      if OSynchField."Table No." = 0 then
        "Table No." := OSynchField."Master Table No."
      else
        "Table No." := OSynchField."Table No.";
    END;

    LOCAL PROCEDURE CheckDuplicatedRecords@2();
    VAR
      OSynchOptionCorrel@1000 : Record "Outlook Synch. Option Correl.";
    BEGIN
      OSynchOptionCorrel.RESET;
      OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
      OSynchOptionCorrel.SETRANGE("Field Line No.","Field Line No.");
      OSynchOptionCorrel.SETFILTER("Line No.",'<>%1',"Line No.");
      OSynchOptionCorrel.SETRANGE("Option No.","Option No.");
      OSynchOptionCorrel.SETRANGE("Enumeration No.","Enumeration No.");
      if not OSynchOptionCorrel.ISEMPTY then
        ERROR(Text001);
    END;

    [Internal]
    PROCEDURE GetFieldValue@3() FieldValue : Text;
    VAR
      OutlookSynchTypeConv@1002 : Codeunit "Outlook Synch. Type Conv";
      LookupRecRef@1000 : RecordRef;
      LookupFieldRef@1001 : FieldRef;
    BEGIN
      LookupRecRef.OPEN("Table No.");
      LookupFieldRef := LookupRecRef.FIELD("Field No.");
      FieldValue := OutlookSynchTypeConv.OptionValueToText("Option No.",LookupFieldRef.OPTIONCAPTION);
      LookupRecRef.CLOSE;
    END;

    BEGIN
    END.
  }
}

