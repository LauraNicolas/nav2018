OBJECT Codeunit 7317 Whse. Integration Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1003 : TextConst 'ENU=%1 must not be the Adjustment Bin Code of the location %2.';
      Text001@1002 : TextConst 'ENU=The bin %1 is Dedicated.\Do you still want to use this bin?';
      Text002@1001 : TextConst 'ENU=The update has been interrupted.';
      Text003@1000 : TextConst '@@@="%2 = Object No., %3 = Object No.";ENU=Location %1 must be set up with Bin Mandatory if the %2 %3 uses it.';
      Text004@1004 : TextConst '@@@=You cannot enter a bin code of bin type Receive, Ship, or Pick.;ENU=You cannot enter a bin code of bin type %1, %2, or %3.';
      Text005@1005 : TextConst 'ENU=You cannot enter a bin code of bin type %1 or %2.';

    [External]
    PROCEDURE CheckBinTypeCode@59(SourceTable@1000 : Integer;BinCodeFieldCaption@1001 : Text[30];LocationCode@1002 : Code[10];BinCode@1003 : Code[20];AdditionalIdentifier@1008 : Option);
    VAR
      BinType@1005 : Record "Bin Type";
      MachineCenter@1004 : Record "Machine Center";
      WorkCenter@1006 : Record "Work Center";
      ItemJournalLine@1007 : Record "Item Journal Line";
      Location@1009 : Record Location;
      Bin@1010 : Record Bin;
      ServiceLine@1011 : Record "Service Line";
    BEGIN
      Location.GET(LocationCode);
      Location.TESTFIELD("Bin Mandatory");

      if not Location."Directed Put-away and Pick" then
        exit;

      if BinCode = Location."Adjustment Bin Code" then
        ERROR(Text000,BinCodeFieldCaption,LocationCode);

      Bin.GET(LocationCode,BinCode);
      Bin.TESTFIELD("Bin Type Code");
      BinType.GET(Bin."Bin Type Code");
      case SourceTable of
        DATABASE::"Warehouse Shipment Header",
        DATABASE::"Warehouse Shipment Line":
          BinType.TESTFIELD(Ship,true);
        DATABASE::"Warehouse Receipt Header",
        DATABASE::"Warehouse Receipt Line":
          BinType.TESTFIELD(Receive,true);
        DATABASE::"Production Order",
        DATABASE::"Prod. Order Line",
        DATABASE::"Assembly Header":
          AllowPutawayPickOrQCBinsOnly(BinType);
        DATABASE::"Prod. Order Component",
        DATABASE::"Assembly Line":
          AllowPutawayOrQCBinsOnly(BinType);
        DATABASE::"Machine Center":
          case BinCodeFieldCaption of
            MachineCenter.FIELDCAPTION("Open Shop Floor Bin Code"),
            MachineCenter.FIELDCAPTION("To-Production Bin Code"):
              AllowPutawayOrQCBinsOnly(BinType);
            MachineCenter.FIELDCAPTION("From-Production Bin Code"):
              AllowPutawayPickOrQCBinsOnly(BinType);
          end;
        DATABASE::"Work Center":
          case BinCodeFieldCaption of
            WorkCenter.FIELDCAPTION("Open Shop Floor Bin Code"),
            WorkCenter.FIELDCAPTION("To-Production Bin Code"):
              AllowPutawayOrQCBinsOnly(BinType);
            WorkCenter.FIELDCAPTION("From-Production Bin Code"):
              AllowPutawayPickOrQCBinsOnly(BinType);
          end;
        DATABASE::Location:
          case BinCodeFieldCaption of
            Location.FIELDCAPTION("Open Shop Floor Bin Code"),
            Location.FIELDCAPTION("To-Production Bin Code"),
            Location.FIELDCAPTION("To-Assembly Bin Code"):
              AllowPutawayOrQCBinsOnly(BinType);
            Location.FIELDCAPTION("From-Production Bin Code"),
            Location.FIELDCAPTION("From-Assembly Bin Code"):
              AllowPutawayPickOrQCBinsOnly(BinType);
          end;
        DATABASE::"Item Journal Line":
          case AdditionalIdentifier of
            ItemJournalLine."Entry Type"::Output:
              AllowPutawayPickOrQCBinsOnly(BinType);
            ItemJournalLine."Entry Type"::Consumption:
              AllowPutawayOrQCBinsOnly(BinType);
          end;
        DATABASE::"Service Line":
          if AdditionalIdentifier = ServiceLine."Document Type"::Invoice then
            BinType.TESTFIELD(Pick,true);
      end;
    END;

    LOCAL PROCEDURE AllowPutawayOrQCBinsOnly@65(VAR BinType@1000 : Record "Bin Type");
    BEGIN
      if BinType.Receive or BinType.Ship or BinType.Pick then
        ERROR(Text004,BinType.FIELDCAPTION(Receive),BinType.FIELDCAPTION(Ship),BinType.FIELDCAPTION(Pick));
    END;

    LOCAL PROCEDURE AllowPutawayPickOrQCBinsOnly@66(VAR BinType@1000 : Record "Bin Type");
    BEGIN
      if BinType.Receive or BinType.Ship then
        ERROR(Text005,BinType.FIELDCAPTION(Receive),BinType.FIELDCAPTION(Ship));
    END;

    [External]
    PROCEDURE CheckIfBinDedicatedOnSrcDoc@69(LocationCode@1000 : Code[10];VAR BinCode@1001 : Code[20];IssueWarning@1002 : Boolean);
    VAR
      Bin@1003 : Record Bin;
    BEGIN
      if BinCode <> '' then begin
        Bin.GET(LocationCode,BinCode);
        if Bin.Dedicated then
          if IssueWarning then begin
            if not
               CONFIRM(
                 STRSUBSTNO(Text001,BinCode),false)
            then
              ERROR(Text002)
          end else
            BinCode := '';
      end;
    END;

    [External]
    PROCEDURE CheckBinCode@63(LocationCode@1000 : Code[10];BinCode@1001 : Code[20];BinCaption@1003 : Text[30];SourceTable@1005 : Integer;Number@1006 : Code[20]);
    VAR
      Bin@1007 : Record Bin;
      Location@1008 : Record Location;
    BEGIN
      if BinCode <> '' then begin
        Location.GET(LocationCode);
        CheckLocationCode(Location,SourceTable,Number);
        Bin.GET(LocationCode,BinCode);
        CheckBinTypeCode(SourceTable,
          BinCaption,
          LocationCode,
          BinCode,0);
      end;
    END;

    [External]
    PROCEDURE CheckLocationCode@62(Location@1000 : Record Location;SourceTable@1001 : Integer;Number@1002 : Code[20]);
    VAR
      WorkCenter@1004 : Record "Work Center";
      MachineCenter@1005 : Record "Machine Center";
      CaptionText@1003 : Text[30];
    BEGIN
      case SourceTable of
        DATABASE::"Work Center":
          CaptionText := WorkCenter.TABLECAPTION;
        DATABASE::"Machine Center":
          CaptionText := MachineCenter.TABLECAPTION;
        DATABASE::Location:
          CaptionText := Location.TABLECAPTION;
      end;
      if not Location."Bin Mandatory" then
        ERROR(Text003,
          Location.Code,
          CaptionText,
          Number);
    END;

    [External]
    PROCEDURE IsOpenShopFloorBin@61(LocationCode@1000 : Code[10];BinCode@1001 : Code[20]) : Boolean;
    VAR
      Location@1002 : Record Location;
      WorkCenter@1003 : Record "Work Center";
      MachineCenter@1004 : Record "Machine Center";
    BEGIN
      Location.GET(LocationCode);
      if BinCode = Location."Open Shop Floor Bin Code" then
        exit(true);

      WorkCenter.SETRANGE("Location Code",LocationCode);
      WorkCenter.SETRANGE("Open Shop Floor Bin Code",BinCode);
      if WorkCenter.COUNT > 0 then
        exit(true);

      MachineCenter.SETRANGE("Location Code",LocationCode);
      MachineCenter.SETRANGE("Open Shop Floor Bin Code",BinCode);
      if MachineCenter.COUNT > 0 then
        exit(true);

      exit(false);
    END;

    [External]
    PROCEDURE CheckLocationOnManufBins@9(Location@1001 : Record Location);
    VAR
      WorkCenter@1000 : Record "Work Center";
    BEGIN
      WorkCenter.SETRANGE("Location Code",Location.Code);
      if WorkCenter.FINDSET(false) then
        repeat
          CheckLocationCode(Location,DATABASE::"Work Center",WorkCenter."No.");
        until WorkCenter.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

