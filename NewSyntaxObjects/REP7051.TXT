OBJECT Report 7051 Suggest Item Price on Wksh.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Suggest Item Price on Wksh.;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);
                  if ToCurrency.Code = '' then begin
                    ToCurrency.InitRoundingPrecision;
                  end else begin
                    ToCurrency.FIND;
                    ToCurrency.TESTFIELD("Unit-Amount Rounding Precision");
                  end;

                  if (ToSalesCode = '') and (ToSalesType <> ToSalesType::"All Customers") then
                    ERROR(Text002,SalesPrice.FIELDCAPTION("Sales Code"));

                  if ToUnitofMeasure.Code <> '' then
                    ToUnitofMeasure.FIND;
                  with SalesPriceWksh do begin
                    VALIDATE("Sales Type",ToSalesType);
                    VALIDATE("Sales Code",ToSalesCode);
                    VALIDATE("Currency Code",ToCurrency.Code);
                    VALIDATE("Starting Date",ToStartDate);
                    VALIDATE("Ending Date",ToEndDate);
                    "Unit of Measure Code" := ToUnitofMeasure.Code;

                    case ToSalesType of
                      ToSalesType::Customer:
                        begin
                          ToCust."No." := ToSalesCode;
                          ToCust.FIND;
                          "Price Includes VAT" := ToCust."Prices Including VAT";
                          "Allow Line Disc." := ToCust."Allow Line Disc.";
                        end;
                      ToSalesType::"Customer Price Group":
                        begin
                          ToCustPriceGr.Code := ToSalesCode;
                          ToCustPriceGr.FIND;
                          "Price Includes VAT" := ToCustPriceGr."Price Includes VAT";
                          "Allow Line Disc." := ToCustPriceGr."Allow Line Disc.";
                          "Allow Invoice Disc." := ToCustPriceGr."Allow Invoice Disc.";
                        end;
                    end;
                  end;
                END;

  }
  DATASET
  {
    { 8129;    ;DataItem;Item                ;
               DataItemTable=Item;
               DataItemTableView=SORTING("No.");
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  CurrentUnitPrice@1000 : Decimal;
                                BEGIN
                                  Window.UPDATE(1,"No.");
                                  with SalesPriceWksh do begin
                                    INIT;
                                    VALIDATE("Item No.",Item."No.");

                                    if not ("Unit of Measure Code" in [Item."Base Unit of Measure",'']) then
                                      if not ItemUnitOfMeasure.GET("Item No.","Unit of Measure Code") then
                                        CurrReport.SKIP;

                                    VALIDATE("Unit of Measure Code",ToUnitofMeasure.Code);
                                    CurrentUnitPrice :=
                                      ROUND(
                                        CurrExchRate.ExchangeAmtLCYToFCY(
                                          WORKDATE,ToCurrency.Code,
                                          Item."Unit Price",
                                          CurrExchRate.ExchangeRate(
                                            WORKDATE,ToCurrency.Code)) *
                                        UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code"),
                                        ToCurrency."Unit-Amount Rounding Precision");

                                    if CurrentUnitPrice > PriceLowerLimit then
                                      "New Unit Price" := CurrentUnitPrice * UnitPriceFactor;

                                    if RoundingMethod.Code <> '' then begin
                                      RoundingMethod."Minimum Amount" := "New Unit Price";
                                      if RoundingMethod.FIND('=<') then begin
                                        "New Unit Price" := "New Unit Price" + RoundingMethod."Amount Added Before";
                                        if RoundingMethod.Precision > 0 then
                                          "New Unit Price" :=
                                            ROUND(
                                              "New Unit Price",
                                              RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                                        "New Unit Price" := "New Unit Price" + RoundingMethod."Amount Added After";
                                      end;
                                    end;

                                    CalcCurrentPrice(PriceAlreadyExists);

                                    if not PriceAlreadyExists then begin
                                      "Current Unit Price" := CurrentUnitPrice;
                                      "VAT Bus. Posting Gr. (Price)" := Item."VAT Bus. Posting Gr. (Price)";
                                    end;

                                    if PriceAlreadyExists or CreateNewPrices then begin
                                      SalesPriceWksh2 := SalesPriceWksh;
                                      if SalesPriceWksh2.FIND('=') then
                                        MODIFY
                                      else
                                        INSERT;
                                    end;
                                  end;
                                END;

               ReqFilterFields="No.","Vendor No.","Inventory Posting Group" }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnInit=BEGIN
               ToEndDateCtrlEnable := true;
               ToStartDateCtrlEnable := true;
               SalesCodeCtrlEnable := true;
             END;

      OnOpenPage=BEGIN
                   if UnitPriceFactor = 0 then
                     UnitPriceFactor := 1;

                   SalesCodeCtrlEnable := true;
                   if ToSalesType = ToSalesType::"All Customers" then
                     SalesCodeCtrlEnable := false;

                   SalesCodeCtrlEnable := ToSalesType <> ToSalesType::"All Customers";
                   ToStartDateCtrlEnable := ToSalesType <> ToSalesType::Campaign;
                   ToEndDateCtrlEnable := ToSalesType <> ToSalesType::Campaign;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 21  ;2   ;Group     ;
                  Name=Copy to Sales Price Worksheet...;
                  CaptionML=ENU=Copy to Sales Price Worksheet... }

      { 8   ;3   ;Field     ;
                  Name=Sales Type;
                  CaptionML=ENU=Sales Type;
                  ToolTipML=ENU=Specifies the sales type for the sales price agreement. To see the existing sales types, click the field.;
                  OptionCaptionML=ENU=Customer,Customer Price Group,All Customers,Campaign;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ToSalesType;
                  OnValidate=BEGIN
                               SalesCodeCtrlEnable := ToSalesType <> ToSalesType::"All Customers";
                               ToStartDateCtrlEnable := ToSalesType <> ToSalesType::Campaign;
                               ToEndDateCtrlEnable := ToSalesType <> ToSalesType::Campaign;

                               ToSalesCode := '';
                               ToStartDate := 0D;
                               ToEndDate := 0D;
                             END;
                              }

      { 1   ;3   ;Field     ;
                  Name=SalesCodeCtrl;
                  CaptionML=ENU=Sales Code;
                  ToolTipML=ENU=Specifies the code for the sales type that the sales price agreement will update. To see the existing sales codes, click the field.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ToSalesCode;
                  Enabled=SalesCodeCtrlEnable;
                  OnValidate=BEGIN
                               ToSalesCodeOnAfterValidate;
                             END;

                  OnLookup=VAR
                             CustList@1004 : Page "Customer List";
                             CustPriceGrList@1003 : Page "Customer Price Groups";
                             CampaignList@1002 : Page "Campaign List";
                           BEGIN
                             case ToSalesType of
                               ToSalesType::Customer:
                                 begin
                                   CustList.LOOKUPMODE := true;
                                   CustList.SETRECORD(ToCust);
                                   if CustList.RUNMODAL = ACTION::LookupOK then begin
                                     CustList.GETRECORD(ToCust);
                                     ToSalesCode := ToCust."No.";
                                   end;
                                 end;
                               ToSalesType::"Customer Price Group":
                                 begin
                                   CustPriceGrList.LOOKUPMODE := true;
                                   CustPriceGrList.SETRECORD(ToCustPriceGr);
                                   if CustPriceGrList.RUNMODAL = ACTION::LookupOK then begin
                                     CustPriceGrList.GETRECORD(ToCustPriceGr);
                                     ToSalesCode := ToCustPriceGr.Code;
                                   end;
                                 end;
                               ToSalesType::Campaign:
                                 begin
                                   CampaignList.LOOKUPMODE := true;
                                   CampaignList.SETRECORD(ToCampaign);
                                   if CampaignList.RUNMODAL = ACTION::LookupOK then begin
                                     CampaignList.GETRECORD(ToCampaign);
                                     ToSalesCode := ToCampaign."No.";
                                     ToStartDate := ToCampaign."Starting Date";
                                     ToEndDate := ToCampaign."Ending Date";
                                   end;
                                 end;
                             end;
                           END;
                            }

      { 9   ;3   ;Field     ;
                  Name=Unit of Measure Code;
                  CaptionML=ENU=Unit of Measure Code;
                  ToolTipML=ENU=Specifies the unit of measure that the item is shown in.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ToUnitofMeasure.Code;
                  TableRelation="Unit of Measure" }

      { 2   ;3   ;Field     ;
                  Name=Currency Code;
                  CaptionML=ENU=Currency Code;
                  ToolTipML=ENU=Specifies the code for the currency that amounts are shown in.;
                  ApplicationArea=#Advanced;
                  SourceExpr=ToCurrency.Code;
                  TableRelation=Currency }

      { 3   ;3   ;Field     ;
                  Name=ToStartDateCtrl;
                  CaptionML=ENU=Starting Date;
                  ToolTipML=ENU=Specifies the date when the price changes will take effect.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ToStartDate;
                  Enabled=ToStartDateCtrlEnable }

      { 12  ;3   ;Field     ;
                  Name=ToEndDateCtrl;
                  CaptionML=ENU=Ending Date;
                  ToolTipML=ENU=Specifies the date to which the price changes are valid.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ToEndDate;
                  Enabled=ToEndDateCtrlEnable }

      { 4   ;2   ;Field     ;
                  Name=Only Amounts Above;
                  CaptionML=ENU=Only Amounts Above;
                  ToolTipML=ENU=Specifies the lowest unit price that will be changed. Only prices that are higher than this price will be changed.;
                  ApplicationArea=#Basic,#Suite;
                  DecimalPlaces=2:5;
                  SourceExpr=PriceLowerLimit }

      { 5   ;2   ;Field     ;
                  Name=Adjustment Factor;
                  CaptionML=ENU=Adjustment Factor;
                  ToolTipML=ENU=Specifies an adjustment factor to multiply the item price that you want suggested. By entering an adjustment factor, you can increase or decrease the amounts that are suggested.;
                  ApplicationArea=#Basic,#Suite;
                  DecimalPlaces=0:5;
                  SourceExpr=UnitPriceFactor;
                  MinValue=0 }

      { 6   ;2   ;Field     ;
                  Name=Rounding Method;
                  CaptionML=ENU=Rounding Method;
                  ToolTipML=ENU=Specifies a code for the rounding method that you want applied to prices.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

      { 7   ;2   ;Field     ;
                  Name=Create New Prices;
                  CaptionML=ENU=Create New Prices;
                  ToolTipML=ENU=Specifies if you want the batch job to create new price suggestions (for example, a new combination of currency, sales code and time). Don't insert a check mark if you only want to adjust existing sales prices.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=CreateNewPrices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing items  #1##########';
      RoundingMethod@1002 : Record "Rounding Method";
      SalesPrice@1003 : Record "Sales Price";
      SalesPriceWksh2@1004 : Record "Sales Price Worksheet";
      SalesPriceWksh@1005 : Record "Sales Price Worksheet";
      ToCust@1024 : Record Customer;
      ToCustPriceGr@1006 : Record "Customer Price Group";
      ToCampaign@1022 : Record Campaign;
      ToCurrency@1007 : Record Currency;
      CurrExchRate@1008 : Record "Currency Exchange Rate";
      ToUnitofMeasure@1009 : Record "Unit of Measure";
      ItemUnitOfMeasure@1010 : Record "Item Unit of Measure";
      UOMMgt@1012 : Codeunit "Unit of Measure Management";
      Window@1013 : Dialog;
      PriceAlreadyExists@1014 : Boolean;
      CreateNewPrices@1015 : Boolean;
      UnitPriceFactor@1016 : Decimal;
      PriceLowerLimit@1017 : Decimal;
      ToSalesType@1019 : 'Customer,"Customer Price Group","All Customers",Campaign';
      ToSalesCode@1020 : Code[20];
      ToStartDate@1018 : Date;
      ToEndDate@1011 : Date;
      Text002@1021 : TextConst 'ENU=%1 must be specified.';
      SalesCodeCtrlEnable@19004995 : Boolean INDATASET;
      ToStartDateCtrlEnable@19060085 : Boolean INDATASET;
      ToEndDateCtrlEnable@19023823 : Boolean INDATASET;

    PROCEDURE InitializeRequest@3(NewToSalesType@1005 : Option;NewToSalesCode@1004 : Code[20];NewToStartDateText@1002 : Date;NewToEndDateText@1000 : Date;NewToCurrCode@1001 : Code[10];NewToUOMCode@1003 : Code[10]);
    BEGIN
      ToSalesType := NewToSalesType;
      ToSalesCode := NewToSalesCode;
      ToStartDate := NewToStartDateText;
      ToEndDate := NewToEndDateText;
      ToCurrency.Code := NewToCurrCode;
      ToUnitofMeasure.Code := NewToUOMCode;
    END;

    LOCAL PROCEDURE ToSalesCodeOnAfterValidate@19025830();
    BEGIN
      if ToSalesType = ToSalesType::Campaign then
        if ToCampaign.GET(ToSalesCode) then begin
          ToStartDate := ToCampaign."Starting Date";
          ToEndDate := ToCampaign."Ending Date";
        end else begin
          ToStartDate := 0D;
          ToEndDate := 0D;
        end;
    END;

    PROCEDURE InitializeRequest2@1(NewToSalesType@1005 : Option;NewToSalesCode@1004 : Code[20];NewToStartDateText@1002 : Date;NewToEndDateText@1000 : Date;NewToCurrCode@1001 : Code[10];NewToUOMCode@1003 : Code[10];NewPriceLowerLimit@1006 : Decimal;NewUnitPriceFactor@1007 : Decimal;NewRoundingMethodCode@1008 : Code[10];NewCreateNewPrices@1009 : Boolean);
    BEGIN
      InitializeRequest(NewToSalesType,NewToSalesCode,NewToStartDateText,NewToEndDateText,NewToCurrCode,NewToUOMCode);
      PriceLowerLimit := NewPriceLowerLimit;
      UnitPriceFactor := NewUnitPriceFactor;
      RoundingMethod.Code := NewRoundingMethodCode;
      CreateNewPrices := NewCreateNewPrices;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

