OBJECT Codeunit 274 Res. Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20010608D;
    Time=120000T;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo="Res. Journal Batch";
    OnRun=BEGIN
            ResJnlBatch.COPY(Rec);
            Code;
            Rec := ResJnlBatch;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.';
      ResJnlTemplate@1004 : Record "Res. Journal Template";
      ResJnlBatch@1005 : Record "Res. Journal Batch";
      ResJnlLine@1006 : Record "Res. Journal Line";
      ResReg@1007 : Record "Resource Register";
      ResJnlPostBatch@1008 : Codeunit "Res. Jnl.-Post Batch";
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with ResJnlBatch do begin
        ResJnlTemplate.GET("Journal Template Name");
        ResJnlTemplate.TESTFIELD("Posting Report ID");

        if not CONFIRM(Text000) then
          exit;

        FIND('-');
        repeat
          ResJnlLine."Journal Template Name" := "Journal Template Name";
          ResJnlLine."Journal Batch Name" := Name;
          ResJnlLine."Line No." := 1;
          CLEAR(ResJnlPostBatch);
          if ResJnlPostBatch.RUN(ResJnlLine) then begin
            MARK(false);
            if ResReg.GET(ResJnlLine."Line No.") then begin
              ResReg.SETRECFILTER;
              REPORT.RUN(ResJnlTemplate."Posting Report ID",false,false,ResReg);
            end;
          end else begin
            MARK(true);
            JnlWithErrors := true;
          end;
        until NEXT = 0;

        if not JnlWithErrors then
          MESSAGE(Text001)
        else
          MESSAGE(
            Text002 +
            Text003);

        if not FIND('=><') then begin
          RESET;
          Name := '';
        end;
      end;
    END;

    BEGIN
    END.
  }
}

