OBJECT Page 9191 Delete User Personalization
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Delete User Personalization;
    InsertAllowed=false;
    ModifyAllowed=false;
    SourceTable="User Metadata";
    PageType=List;
    SourceTableTemporary=true;
    OnOpenPage=VAR
                 UserMetadata@1001 : Record "User Metadata";
                 UserPageMetadata@1000 : Record "User Page Metadata";
                 EmptyGuid@1002 : GUID;
               BEGIN
                 RESET;

                 if not (FilterUserID = EmptyGuid) then begin
                   UserMetadata.SETFILTER("User SID",FilterUserID);
                   UserPageMetadata.SETFILTER("User SID",FilterUserID);
                 end;

                 if UserMetadata.FINDSET then
                   repeat
                     "User SID" := UserMetadata."User SID";
                     "Page ID" := UserMetadata."Page ID";
                     "Personalization ID" := UserMetadata."Personalization ID";
                     Date := UserMetadata.Date;
                     Time := UserMetadata.Time;
                     INSERT;
                   until UserMetadata.NEXT = 0;

                 if UserPageMetadata.FINDSET then
                   repeat
                     "User SID" := UserPageMetadata."User SID";
                     "Page ID" := UserPageMetadata."Page ID";
                     "Personalization ID" := 'EXTENSION METADATA';
                     INSERT;
                   until UserPageMetadata.NEXT = 0;
               END;

    OnAfterGetRecord=VAR
                       PageDefinition@1000 : Record "Page Metadata";
                     BEGIN
                       if "Personalization ID" = 'EXTENSION METADATA' then
                         LegacyPersonalization := false
                       else
                         LegacyPersonalization := true;

                       if PageDefinition.GET("Page ID") then
                         PageName := PageDefinition.Caption
                       else
                         PageName := '';
                     END;

    OnDeleteRecord=VAR
                     UserPageMetadata@1000 : Record "User Page Metadata";
                     UserMetadata@1001 : Record "User Metadata";
                   BEGIN
                     if "Personalization ID" = 'EXTENSION METADATA' then begin
                       UserPageMetadata.SETFILTER("User SID","User SID");
                       UserPageMetadata.SETFILTER("Page ID",FORMAT("Page ID"));

                       if UserPageMetadata.FINDFIRST then
                         UserPageMetadata.DELETE(true);
                     end else begin
                       UserMetadata.SETFILTER("User SID","User SID");
                       UserMetadata.SETFILTER("Page ID",FORMAT("Page ID"));
                       UserMetadata.SETFILTER("Personalization ID","Personalization ID");

                       if UserMetadata.FINDFIRST then
                         UserMetadata.DELETE(true);
                     end;

                     CurrPage.UPDATE(true);
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1106000000;1;Group  ;
                Name=Group1106000000;
                GroupType=Repeater }

    { 1106000001;2;Field  ;
                Name=User SID;
                CaptionML=ENU=User SID;
                ToolTipML=ENU=Specifies the security identifier (SID) of the user who did the personalization.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User SID";
                ImplicitType=GUID }

    { 1106000003;2;Field  ;
                Name=User ID;
                CaptionML=ENU=User ID;
                ToolTipML=ENU=Specifies the user ID of the user who performed the personalization.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User ID";
                ImplicitType=Code50 }

    { 1106000005;2;Field  ;
                Name=Page ID;
                CaptionML=ENU=Page ID;
                ToolTipML=ENU=Specifies the number of the page object that has been personalized.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Page ID";
                ImplicitType=Integer }

    { 2   ;2   ;Field     ;
                Name=Description;
                CaptionML=ENU=Description;
                ToolTipML=ENU=Specifies the Name of the page that has been personalized.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PageName;
                ImplicitType=Text }

    { 1   ;2   ;Field     ;
                Name=Legacy Personalization;
                CaptionML=ENU=Legacy Personalization;
                ToolTipML=ENU=Specifies if the personalization was made in the Windows client or the Web client.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=LegacyPersonalization;
                ImplicitType=Boolean }

    { 3   ;2   ;Field     ;
                Name=Date;
                CaptionML=ENU=Date;
                ToolTipML=ENU=Specifies the date of the personalization.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Date;
                Visible=False;
                ImplicitType=Date }

    { 4   ;2   ;Field     ;
                Name=Time;
                CaptionML=ENU=Time;
                ToolTipML=ENU=Specifies the timestamp for the personalization.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Time;
                Visible=False;
                ImplicitType=Time }

  }
  CODE
  {
    VAR
      LegacyPersonalization@1000 : Boolean;
      PageName@1001 : Text;
      FilterUserID@1002 : GUID;

    PROCEDURE SetUserID@1(UserID@1000 : GUID);
    BEGIN
      FilterUserID := UserID;
    END;

    BEGIN
    END.
  }
}

