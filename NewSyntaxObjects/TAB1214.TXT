OBJECT Table 1214 Intermediate Data Import
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Intermediate Data Import;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;AutoIncrement=true;
                                                   CaptionML=ENU=ID }
    { 2   ;   ;Data Exch. No.      ;Integer       ;TableRelation="Data Exch.";
                                                   CaptionML=ENU=Data Exch. No.;
                                                   NotBlank=true }
    { 3   ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Table));
                                                   CaptionML=ENU=Table ID;
                                                   NotBlank=true }
    { 4   ;   ;Record No.          ;Integer       ;CaptionML=ENU=Record No. }
    { 5   ;   ;Field ID            ;Integer       ;TableRelation=Field."No." WHERE (TableNo=FIELD("Table ID"));
                                                   CaptionML=ENU=Field ID }
    { 6   ;   ;Value               ;Text250       ;CaptionML=ENU=Value }
    { 7   ;   ;Validate Only       ;Boolean       ;CaptionML=ENU=Validate Only }
    { 8   ;   ;Parent Record No.   ;Integer       ;CaptionML=ENU=Parent Record No. }
  }
  KEYS
  {
    {    ;ID                                       }
    {    ;"Data Exch. No.","Table ID","Record No.","Field ID";
                                                   Clustered=true }
    {    ;"Data Exch. No.","Table ID","Field ID"   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE InsertOrUpdateEntry@2(EntryNo@1002 : Integer;TableID@1003 : Integer;FieldID@1004 : Integer;ParentRecordNo@1001 : Integer;RecordNo@1000 : Integer;NewValue@1005 : Text[250]);
    BEGIN
      if FindEntry(EntryNo,TableID,FieldID,ParentRecordNo,RecordNo) then begin
        Value := NewValue;
        MODIFY;
      end else begin
        CLEAR(Rec);
        "Data Exch. No." := EntryNo;
        "Table ID" := TableID;
        "Record No." := RecordNo;
        "Field ID" := FieldID;
        Value := NewValue;
        "Parent Record No." := ParentRecordNo;
        "Validate Only" := false;
        INSERT;
      end;
    END;

    [External]
    PROCEDURE FindEntry@1(EntryNo@1002 : Integer;TableID@1003 : Integer;FieldID@1004 : Integer;ParentRecordNo@1001 : Integer;RecordNo@1000 : Integer) : Boolean;
    BEGIN
      RESET;

      SETRANGE("Data Exch. No.",EntryNo);
      SETRANGE("Table ID",TableID);
      SETRANGE("Field ID",FieldID);
      SETRANGE("Parent Record No.",ParentRecordNo);
      SETRANGE("Record No.",RecordNo);

      exit(FINDFIRST);
    END;

    [External]
    PROCEDURE GetEntryValue@4(EntryNo@1002 : Integer;TableID@1003 : Integer;FieldID@1004 : Integer;ParentRecordNo@1001 : Integer;RecordNo@1000 : Integer) : Text[250];
    BEGIN
      if FindEntry(EntryNo,TableID,FieldID,ParentRecordNo,RecordNo) then
        exit(Value);

      exit('');
    END;

    BEGIN
    END.
  }
}

