OBJECT Table 112 Sales Invoice Header
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields="No.","Sell-to Customer Name";
    OnDelete=VAR
               PostedDeferralHeader@1002 : Record "Posted Deferral Header";
               PostSalesDelete@1003 : Codeunit "PostSales-Delete";
               DeferralUtilities@1001 : Codeunit "Deferral Utilities";
             BEGIN
               PostSalesDelete.IsDocumentDeletionAllowed("Posting Date");
               TESTFIELD("No. Printed");
               LOCKTABLE;
               PostSalesDelete.DeleteSalesInvLines(Rec);

               SalesCommentLine.SETRANGE("Document Type",SalesCommentLine."Document Type"::"Posted Invoice");
               SalesCommentLine.SETRANGE("No.","No.");
               SalesCommentLine.DELETEALL;

               ApprovalsMgmt.DeletePostedApprovalEntries(RECORDID);
               PostedDeferralHeader.DeleteForDoc(DeferralUtilities.GetSalesDeferralDocType,'','',
                 SalesCommentLine."Document Type"::"Posted Invoice","No.");
             END;

    CaptionML=ENU=Sales Invoice Header;
    LookupPageID="Posted Sales Invoices";
    DrillDownPageID="Posted Sales Invoices";
  }
  FIELDS
  {
    { 2   ;   ;Sell-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Sell-to Customer No.;
                                                   NotBlank=true }
    { 3   ;   ;No.                 ;Code20        ;CaptionML=ENU=No. }
    { 4   ;   ;Bill-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Bill-to Customer No.;
                                                   NotBlank=true }
    { 5   ;   ;Bill-to Name        ;Text50        ;CaptionML=ENU=Bill-to Name }
    { 6   ;   ;Bill-to Name 2      ;Text50        ;CaptionML=ENU=Bill-to Name 2 }
    { 7   ;   ;Bill-to Address     ;Text50        ;CaptionML=ENU=Bill-to Address }
    { 8   ;   ;Bill-to Address 2   ;Text50        ;CaptionML=ENU=Bill-to Address 2 }
    { 9   ;   ;Bill-to City        ;Text30        ;TableRelation="Post Code".City;
                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Bill-to City }
    { 10  ;   ;Bill-to Contact     ;Text50        ;CaptionML=ENU=Bill-to Contact }
    { 11  ;   ;Your Reference      ;Text35        ;CaptionML=ENU=Your Reference }
    { 12  ;   ;Ship-to Code        ;Code10        ;TableRelation="Ship-to Address".Code WHERE ("Customer No."=FIELD("Sell-to Customer No."));
                                                   CaptionML=ENU=Ship-to Code }
    { 13  ;   ;Ship-to Name        ;Text50        ;CaptionML=ENU=Ship-to Name }
    { 14  ;   ;Ship-to Name 2      ;Text50        ;CaptionML=ENU=Ship-to Name 2 }
    { 15  ;   ;Ship-to Address     ;Text50        ;CaptionML=ENU=Ship-to Address }
    { 16  ;   ;Ship-to Address 2   ;Text50        ;CaptionML=ENU=Ship-to Address 2 }
    { 17  ;   ;Ship-to City        ;Text30        ;TableRelation="Post Code".City;
                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Ship-to City }
    { 18  ;   ;Ship-to Contact     ;Text50        ;CaptionML=ENU=Ship-to Contact }
    { 19  ;   ;Order Date          ;Date          ;CaptionML=ENU=Order Date }
    { 20  ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 21  ;   ;Shipment Date       ;Date          ;CaptionML=ENU=Shipment Date }
    { 22  ;   ;Posting Description ;Text50        ;CaptionML=ENU=Posting Description }
    { 23  ;   ;Payment Terms Code  ;Code10        ;TableRelation="Payment Terms";
                                                   CaptionML=ENU=Payment Terms Code }
    { 24  ;   ;Due Date            ;Date          ;CaptionML=ENU=Due Date }
    { 25  ;   ;Payment Discount %  ;Decimal       ;CaptionML=ENU=Payment Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 26  ;   ;Pmt. Discount Date  ;Date          ;CaptionML=ENU=Pmt. Discount Date }
    { 27  ;   ;Shipment Method Code;Code10        ;TableRelation="Shipment Method";
                                                   CaptionML=ENU=Shipment Method Code }
    { 28  ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE ("Use As In-Transit"=CONST(false));
                                                   CaptionML=ENU=Location Code }
    { 29  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1));
                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 30  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2));
                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 31  ;   ;Customer Posting Group;Code20      ;TableRelation="Customer Posting Group";
                                                   CaptionML=ENU=Customer Posting Group;
                                                   Editable=false }
    { 32  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code;
                                                   Editable=false }
    { 33  ;   ;Currency Factor     ;Decimal       ;CaptionML=ENU=Currency Factor;
                                                   DecimalPlaces=0:15;
                                                   MinValue=0 }
    { 34  ;   ;Customer Price Group;Code10        ;TableRelation="Customer Price Group";
                                                   CaptionML=ENU=Customer Price Group }
    { 35  ;   ;Prices Including VAT;Boolean       ;CaptionML=ENU=Prices Including VAT }
    { 37  ;   ;Invoice Disc. Code  ;Code20        ;CaptionML=ENU=Invoice Disc. Code }
    { 40  ;   ;Customer Disc. Group;Code20        ;TableRelation="Customer Discount Group";
                                                   CaptionML=ENU=Customer Disc. Group }
    { 41  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=ENU=Language Code }
    { 43  ;   ;Salesperson Code    ;Code20        ;TableRelation="Salesperson/Purchaser";
                                                   CaptionML=ENU=Salesperson Code }
    { 44  ;   ;Order No.           ;Code20        ;AccessByPermission=TableData "Sales Shipment Header"=R;
                                                   CaptionML=ENU=Order No. }
    { 46  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Sales Comment Line" WHERE ("Document Type"=CONST("Posted Invoice"),
                                                                                                 "No."=FIELD("No."),
                                                                                                 "Document Line No."=CONST(0)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=false }
    { 47  ;   ;No. Printed         ;Integer       ;CaptionML=ENU=No. Printed;
                                                   Editable=false }
    { 51  ;   ;On Hold             ;Code3         ;CaptionML=ENU=On Hold }
    { 52  ;   ;Applies-to Doc. Type;Option        ;CaptionML=ENU=Applies-to Doc. Type;
                                                   OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                   OptionString=" ",Payment,Invoice,"Credit Memo","Finance Charge Memo",Reminder,Refund }
    { 53  ;   ;Applies-to Doc. No. ;Code20        ;OnLookup=BEGIN
                                                              CustLedgEntry.SETCURRENTKEY("Document No.");
                                                              CustLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                                                              CustLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
                                                              PAGE.RUN(0,CustLedgEntry);
                                                            END;

                                                   CaptionML=ENU=Applies-to Doc. No. }
    { 55  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF ("Bal. Account Type"=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF ("Bal. Account Type"=CONST("Bank Account")) "Bank Account";
                                                   CaptionML=ENU=Bal. Account No. }
    { 60  ;   ;Amount              ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Sales Invoice Line".Amount WHERE ("Document No."=FIELD("No.")));
                                                   CaptionML=ENU=Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 61  ;   ;Amount Including VAT;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Sales Invoice Line"."Amount Including VAT" WHERE ("Document No."=FIELD("No.")));
                                                   CaptionML=ENU=Amount Including VAT;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 70  ;   ;VAT Registration No.;Text20        ;CaptionML=ENU=VAT Registration No. }
    { 73  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=ENU=Reason Code }
    { 74  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 75  ;   ;EU 3-Party Trade    ;Boolean       ;CaptionML=ENU=EU 3-Party Trade }
    { 76  ;   ;Transaction Type    ;Code10        ;TableRelation="Transaction Type";
                                                   CaptionML=ENU=Transaction Type }
    { 77  ;   ;Transport Method    ;Code10        ;TableRelation="Transport Method";
                                                   CaptionML=ENU=Transport Method }
    { 78  ;   ;VAT Country/Region Code;Code10     ;TableRelation="Country/Region";
                                                   CaptionML=ENU=VAT Country/Region Code }
    { 79  ;   ;Sell-to Customer Name;Text50       ;CaptionML=ENU=Sell-to Customer Name }
    { 80  ;   ;Sell-to Customer Name 2;Text50     ;CaptionML=ENU=Sell-to Customer Name 2 }
    { 81  ;   ;Sell-to Address     ;Text50        ;CaptionML=ENU=Sell-to Address }
    { 82  ;   ;Sell-to Address 2   ;Text50        ;CaptionML=ENU=Sell-to Address 2 }
    { 83  ;   ;Sell-to City        ;Text30        ;TableRelation="Post Code".City;
                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Sell-to City }
    { 84  ;   ;Sell-to Contact     ;Text50        ;CaptionML=ENU=Sell-to Contact }
    { 85  ;   ;Bill-to Post Code   ;Code20        ;TableRelation="Post Code";
                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Bill-to Post Code }
    { 86  ;   ;Bill-to County      ;Text30        ;CaptionML=ENU=Bill-to County }
    { 87  ;   ;Bill-to Country/Region Code;Code10 ;TableRelation="Country/Region";
                                                   CaptionML=ENU=Bill-to Country/Region Code }
    { 88  ;   ;Sell-to Post Code   ;Code20        ;TableRelation="Post Code";
                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Sell-to Post Code }
    { 89  ;   ;Sell-to County      ;Text30        ;CaptionML=ENU=Sell-to County }
    { 90  ;   ;Sell-to Country/Region Code;Code10 ;TableRelation="Country/Region";
                                                   CaptionML=ENU=Sell-to Country/Region Code }
    { 91  ;   ;Ship-to Post Code   ;Code20        ;TableRelation="Post Code";
                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Ship-to Post Code }
    { 92  ;   ;Ship-to County      ;Text30        ;CaptionML=ENU=Ship-to County }
    { 93  ;   ;Ship-to Country/Region Code;Code10 ;TableRelation="Country/Region";
                                                   CaptionML=ENU=Ship-to Country/Region Code }
    { 94  ;   ;Bal. Account Type   ;Option        ;CaptionML=ENU=Bal. Account Type;
                                                   OptionCaptionML=ENU=G/L Account,Bank Account;
                                                   OptionString="G/L Account","Bank Account" }
    { 97  ;   ;Exit Point          ;Code10        ;TableRelation="Entry/Exit Point";
                                                   CaptionML=ENU=Exit Point }
    { 98  ;   ;Correction          ;Boolean       ;CaptionML=ENU=Correction }
    { 99  ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date }
    { 100 ;   ;External Document No.;Code35       ;CaptionML=ENU=External Document No. }
    { 101 ;   ;Area                ;Code10        ;TableRelation=Area;
                                                   CaptionML=ENU=Area }
    { 102 ;   ;Transaction Specification;Code10   ;TableRelation="Transaction Specification";
                                                   CaptionML=ENU=Transaction Specification }
    { 104 ;   ;Payment Method Code ;Code10        ;TableRelation="Payment Method";
                                                   CaptionML=ENU=Payment Method Code }
    { 105 ;   ;Shipping Agent Code ;Code10        ;TableRelation="Shipping Agent";
                                                   AccessByPermission=TableData "Shipping Agent Services"=R;
                                                   CaptionML=ENU=Shipping Agent Code }
    { 106 ;   ;Package Tracking No.;Text30        ;CaptionML=ENU=Package Tracking No. }
    { 107 ;   ;Pre-Assigned No. Series;Code20     ;TableRelation="No. Series";
                                                   CaptionML=ENU=Pre-Assigned No. Series }
    { 108 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=false }
    { 110 ;   ;Order No. Series    ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=Order No. Series }
    { 111 ;   ;Pre-Assigned No.    ;Code20        ;CaptionML=ENU=Pre-Assigned No. }
    { 112 ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit "User Management";
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=false;
                                                   CaptionML=ENU=User ID }
    { 113 ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=ENU=Source Code }
    { 114 ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=ENU=Tax Area Code }
    { 115 ;   ;Tax Liable          ;Boolean       ;CaptionML=ENU=Tax Liable }
    { 116 ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 119 ;   ;VAT Base Discount % ;Decimal       ;CaptionML=ENU=VAT Base Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 131 ;   ;Prepayment No. Series;Code20       ;TableRelation="No. Series";
                                                   CaptionML=ENU=Prepayment No. Series }
    { 136 ;   ;Prepayment Invoice  ;Boolean       ;CaptionML=ENU=Prepayment Invoice }
    { 137 ;   ;Prepayment Order No.;Code20        ;CaptionML=ENU=Prepayment Order No. }
    { 151 ;   ;Quote No.           ;Code20        ;CaptionML=ENU=Quote No.;
                                                   Editable=false }
    { 166 ;   ;Last Email Sent Time;DateTime      ;FieldClass=FlowField;
                                                   CalcFormula=Max("O365 Document Sent History"."Created Date-Time" WHERE ("Document Type"=CONST(Invoice),
                                                                                                                           "Document No."=FIELD("No."),
                                                                                                                           Posted=CONST(true)));
                                                   CaptionML=ENU=Last Email Sent Time }
    { 167 ;   ;Last Email Sent Status;Option      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("O365 Document Sent History"."Job Last Status" WHERE ("Document Type"=CONST(Invoice),
                                                                                                                            "Document No."=FIELD("No."),
                                                                                                                            Posted=CONST(true),
                                                                                                                            "Created Date-Time"=FIELD("Last Email Sent Time")));
                                                   CaptionML=ENU=Last Email Sent Status;
                                                   OptionCaptionML=ENU=,In Process,Finished,Error;
                                                   OptionString=,"In Process",Finished,Error }
    { 168 ;   ;Sent as Email       ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("O365 Document Sent History" WHERE ("Document Type"=CONST(Invoice),
                                                                                                         "Document No."=FIELD("No."),
                                                                                                         Posted=CONST(true),
                                                                                                         "Job Last Status"=CONST(Finished)));
                                                   CaptionML=ENU=Sent as Email }
    { 169 ;   ;Last Email Notif Cleared;Boolean   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("O365 Document Sent History".NotificationCleared WHERE ("Document Type"=CONST(Invoice),
                                                                                                                              "Document No."=FIELD("No."),
                                                                                                                              Posted=CONST(true),
                                                                                                                              "Created Date-Time"=FIELD("Last Email Sent Time")));
                                                   CaptionML=ENU=Last Email Notif Cleared }
    { 200 ;   ;Work Description    ;BLOB          ;CaptionML=ENU=Work Description }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
    { 600 ;   ;Payment Service Set ID;Integer     ;CaptionML=ENU=Payment Service Set ID }
    { 710 ;   ;Document Exchange Identifier;Text50;CaptionML=ENU=Document Exchange Identifier }
    { 711 ;   ;Document Exchange Status;Option    ;CaptionML=ENU=Document Exchange Status;
                                                   OptionCaptionML=ENU=Not Sent,Sent to Document Exchange Service,Delivered to Recipient,Delivery Failed,Pending Connection to Recipient;
                                                   OptionString="Not Sent","Sent to Document Exchange Service","Delivered to Recipient","Delivery Failed","Pending Connection to Recipient" }
    { 712 ;   ;Doc. Exch. Original Identifier;Text50;
                                                   CaptionML=ENU=Doc. Exch. Original Identifier }
    { 720 ;   ;Coupled to CRM      ;Boolean       ;CaptionML=ENU=Coupled to Dynamics 365 for Sales }
    { 1200;   ;Direct Debit Mandate ID;Code35     ;TableRelation="SEPA Direct Debit Mandate" WHERE ("Customer No."=FIELD("Bill-to Customer No."));
                                                   CaptionML=ENU=Direct Debit Mandate ID }
    { 1302;   ;Closed              ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=-Exist("Cust. Ledger Entry" WHERE ("Entry No."=FIELD("Cust. Ledger Entry No."),
                                                                                                  Open=FILTER(true)));
                                                   CaptionML=ENU=Closed;
                                                   Editable=false }
    { 1303;   ;Remaining Amount    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Detailed Cust. Ledg. Entry".Amount WHERE ("Cust. Ledger Entry No."=FIELD("Cust. Ledger Entry No.")));
                                                   CaptionML=ENU=Remaining Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 1304;   ;Cust. Ledger Entry No.;Integer     ;TableRelation="Cust. Ledger Entry"."Entry No.";
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Cust. Ledger Entry No.;
                                                   Editable=false }
    { 1305;   ;Invoice Discount Amount;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Sales Invoice Line"."Inv. Discount Amount" WHERE ("Document No."=FIELD("No.")));
                                                   CaptionML=ENU=Invoice Discount Amount;
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 1310;   ;Cancelled           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Cancelled Document" WHERE ("Source ID"=CONST(112),
                                                                                                 "Cancelled Doc. No."=FIELD("No.")));
                                                   CaptionML=ENU=Cancelled;
                                                   Editable=false }
    { 1311;   ;Corrective          ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Cancelled Document" WHERE ("Source ID"=CONST(114),
                                                                                                 "Cancelled By Doc. No."=FIELD("No.")));
                                                   CaptionML=ENU=Corrective;
                                                   Editable=false }
    { 5050;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   CaptionML=ENU=Campaign No. }
    { 5052;   ;Sell-to Contact No. ;Code20        ;TableRelation=Contact;
                                                   CaptionML=ENU=Sell-to Contact No. }
    { 5053;   ;Bill-to Contact No. ;Code20        ;TableRelation=Contact;
                                                   CaptionML=ENU=Bill-to Contact No. }
    { 5055;   ;Opportunity No.     ;Code20        ;TableRelation=Opportunity;
                                                   CaptionML=ENU=Opportunity No. }
    { 5700;   ;Responsibility Center;Code10       ;TableRelation="Responsibility Center";
                                                   CaptionML=ENU=Responsibility Center }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;CaptionML=ENU=Allow Line Disc. }
    { 7200;   ;Get Shipment Used   ;Boolean       ;CaptionML=ENU=Get Shipment Used }
    { 8000;   ;Id                  ;GUID          ;CaptionML=ENU=Id }
  }
  KEYS
  {
    {    ;"No."                                   ;Clustered=true }
    {    ;"Order No."                              }
    {    ;"Pre-Assigned No."                       }
    {    ;"Sell-to Customer No.","External Document No.";
                                                   MaintainSQLIndex=false }
    {    ;"Sell-to Customer No.","Order Date"     ;MaintainSQLIndex=false }
    {    ;"Sell-to Customer No."                   }
    {    ;"Prepayment Order No.","Prepayment Invoice" }
    {    ;"Bill-to Customer No."                   }
    {    ;"Posting Date"                           }
    {    ;"Document Exchange Status"               }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;"No.","Sell-to Customer No.","Bill-to Customer No.","Posting Date","Posting Description" }
    { 2   ;Brick               ;"No.","Sell-to Customer Name",Amount,"Due Date","Amount Including VAT" }
  }
  CODE
  {
    VAR
      SalesCommentLine@1001 : Record "Sales Comment Line";
      SalesSetup@1007 : Record "Sales & Receivables Setup";
      CustLedgEntry@1003 : Record "Cust. Ledger Entry";
      DimMgt@1005 : Codeunit DimensionManagement;
      ApprovalsMgmt@1008 : Codeunit "Approvals Mgmt.";
      UserSetupMgt@1002 : Codeunit "User Setup Management";
      DocTxt@1000 : TextConst 'ENU=Invoice';
      PaymentReference@1214 : Text;
      PaymentReferenceLbl@1215 : Text;

    [Internal]
    PROCEDURE SendRecords@12();
    VAR
      DocumentSendingProfile@1001 : Record "Document Sending Profile";
      DummyReportSelections@1000 : Record "Report Selections";
    BEGIN
      DocumentSendingProfile.SendCustomerRecords(
        DummyReportSelections.Usage::"S.Invoice",Rec,DocTxt,"Bill-to Customer No.","No.",
        FIELDNO("Bill-to Customer No."),FIELDNO("No."));
    END;

    [External]
    PROCEDURE SendProfile@72(VAR DocumentSendingProfile@1000 : Record "Document Sending Profile");
    VAR
      DummyReportSelections@1003 : Record "Report Selections";
    BEGIN
      DocumentSendingProfile.Send(
        DummyReportSelections.Usage::"S.Invoice",Rec,"No.","Bill-to Customer No.",
        DocTxt,FIELDNO("Bill-to Customer No."),FIELDNO("No."));
    END;

    [External]
    PROCEDURE PrintRecords@1(ShowRequestForm@1000 : Boolean);
    VAR
      DocumentSendingProfile@1002 : Record "Document Sending Profile";
      DummyReportSelections@1001 : Record "Report Selections";
    BEGIN
      DocumentSendingProfile.TrySendToPrinter(
        DummyReportSelections.Usage::"S.Invoice",Rec,"Bill-to Customer No.",ShowRequestForm);
    END;

    [Internal]
    PROCEDURE EmailRecords@17(ShowDialog@1000 : Boolean);
    VAR
      DocumentSendingProfile@1003 : Record "Document Sending Profile";
      DummyReportSelections@1001 : Record "Report Selections";
    BEGIN
      DocumentSendingProfile.TrySendToEMail(
        DummyReportSelections.Usage::"S.Invoice",Rec,FIELDNO("No."),DocTxt,FIELDNO("Bill-to Customer No."),ShowDialog);
    END;

    [External]
    PROCEDURE Navigate@2();
    VAR
      NavigateForm@1000 : Page Navigate;
    BEGIN
      NavigateForm.SetDoc("Posting Date","No.");
      NavigateForm.RUN;
    END;

    [External]
    PROCEDURE LookupAdjmtValueEntries@3();
    VAR
      ValueEntry@1000 : Record "Value Entry";
    BEGIN
      ValueEntry.SETCURRENTKEY("Document No.");
      ValueEntry.SETRANGE("Document No.","No.");
      ValueEntry.SETRANGE("Document Type",ValueEntry."Document Type"::"Sales Invoice");
      ValueEntry.SETRANGE(Adjustment,true);
      PAGE.RUNMODAL(0,ValueEntry);
    END;

    [External]
    PROCEDURE GetCustomerVATRegistrationNumber@14() : Text;
    BEGIN
      exit("VAT Registration No.");
    END;

    [External]
    PROCEDURE GetCustomerVATRegistrationNumberLbl@15() : Text;
    BEGIN
      exit(FIELDCAPTION("VAT Registration No."));
    END;

    [External]
    PROCEDURE GetCustomerGlobalLocationNumber@8() : Text;
    BEGIN
      exit('');
    END;

    [External]
    PROCEDURE GetCustomerGlobalLocationNumberLbl@10() : Text;
    BEGIN
      exit('');
    END;

    [External]
    PROCEDURE GetPaymentReference@9() : Text;
    BEGIN
      OnGetPaymentReference(PaymentReference);
      exit(PaymentReference);
    END;

    [External]
    PROCEDURE GetPaymentReferenceLbl@11() : Text;
    BEGIN
      OnGetPaymentReferenceLbl(PaymentReferenceLbl);
      exit(PaymentReferenceLbl);
    END;

    [External]
    PROCEDURE GetLegalStatement@60() : Text;
    BEGIN
      SalesSetup.GET;
      exit(SalesSetup.GetLegalStatement);
    END;

    [External]
    PROCEDURE GetRemainingAmount@6() : Decimal;
    VAR
      CustLedgerEntry@1000 : Record "Cust. Ledger Entry";
    BEGIN
      CustLedgerEntry.SETRANGE("Customer No.","Bill-to Customer No.");
      CustLedgerEntry.SETRANGE("Posting Date","Posting Date");
      CustLedgerEntry.SETRANGE("Document Type",CustLedgerEntry."Document Type"::Invoice);
      CustLedgerEntry.SETRANGE("Document No.","No.");
      if not CustLedgerEntry.FINDFIRST then
        exit(0);
      CustLedgerEntry.CALCFIELDS("Remaining Amount");
      exit(CustLedgerEntry."Remaining Amount");
    END;

    [External]
    PROCEDURE ShowDimensions@4();
    BEGIN
      DimMgt.ShowDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2',TABLECAPTION,"No."));
    END;

    [External]
    PROCEDURE SetSecurityFilterOnRespCenter@5();
    BEGIN
      if UserSetupMgt.GetSalesFilter <> '' then begin
        FILTERGROUP(2);
        SETRANGE("Responsibility Center",UserSetupMgt.GetSalesFilter);
        FILTERGROUP(0);
      end;
    END;

    [External]
    PROCEDURE GetDocExchStatusStyle@13() : Text;
    BEGIN
      case "Document Exchange Status" of
        "Document Exchange Status"::"Not Sent":
          exit('Standard');
        "Document Exchange Status"::"Sent to Document Exchange Service":
          exit('Ambiguous');
        "Document Exchange Status"::"Delivered to Recipient":
          exit('Favorable');
        else
          exit('Unfavorable');
      end;
    END;

    [External]
    PROCEDURE ShowActivityLog@116();
    VAR
      ActivityLog@1000 : Record "Activity Log";
    BEGIN
      ActivityLog.ShowEntries(RECORDID);
    END;

    [External]
    PROCEDURE GetSelectedPaymentsText@84() : Text;
    VAR
      PaymentServiceSetup@1000 : Record "Payment Service Setup";
    BEGIN
      exit(PaymentServiceSetup.GetSelectedPaymentsText("Payment Service Set ID"));
    END;

    [External]
    PROCEDURE GetWorkDescription@113() : Text;
    VAR
      TempBlob@1000 : TEMPORARY Record TempBlob;
      CR@1004 : Text[1];
    BEGIN
      CALCFIELDS("Work Description");
      if not "Work Description".HASVALUE then
        exit('');
      CR[1] := 10;
      TempBlob.Blob := "Work Description";
      exit(TempBlob.ReadAsText(CR,TEXTENCODING::Windows));
    END;

    [External]
    PROCEDURE DocExchangeStatusIsSent@18() : Boolean;
    BEGIN
      exit("Document Exchange Status" <> "Document Exchange Status"::"Not Sent");
    END;

    [External]
    PROCEDURE ShowCanceledOrCorrCrMemo@22();
    BEGIN
      CALCFIELDS(Cancelled,Corrective);
      case true of
        Cancelled:
          ShowCorrectiveCreditMemo;
        Corrective:
          ShowCancelledCreditMemo;
      end;
    END;

    [External]
    PROCEDURE ShowCorrectiveCreditMemo@19();
    VAR
      CancelledDocument@1000 : Record "Cancelled Document";
      SalesCrMemoHeader@1001 : Record "Sales Cr.Memo Header";
    BEGIN
      CALCFIELDS(Cancelled);
      if not Cancelled then
        exit;

      if CancelledDocument.FindSalesCancelledInvoice("No.") then begin
        SalesCrMemoHeader.GET(CancelledDocument."Cancelled By Doc. No.");
        PAGE.RUN(PAGE::"Posted Sales Credit Memo",SalesCrMemoHeader);
      end;
    END;

    [External]
    PROCEDURE ShowCancelledCreditMemo@20();
    VAR
      CancelledDocument@1000 : Record "Cancelled Document";
      SalesCrMemoHeader@1001 : Record "Sales Cr.Memo Header";
    BEGIN
      CALCFIELDS(Corrective);
      if not Corrective then
        exit;

      if CancelledDocument.FindSalesCorrectiveInvoice("No.") then begin
        SalesCrMemoHeader.GET(CancelledDocument."Cancelled Doc. No.");
        PAGE.RUN(PAGE::"Posted Sales Credit Memo",SalesCrMemoHeader);
      end;
    END;

    [External]
    PROCEDURE GetDefaultEmailDocumentName@21() : Text[150];
    BEGIN
      exit(DocTxt);
    END;

    [Integration(TRUE)]
    LOCAL PROCEDURE OnGetPaymentReference@1214(VAR PaymentReference@1213 : Text);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnGetPaymentReferenceLbl@1213(VAR PaymentReferenceLbl@1213 : Text);
    BEGIN
    END;

    BEGIN
    END.
  }
}

