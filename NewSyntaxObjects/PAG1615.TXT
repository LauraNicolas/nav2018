OBJECT Page 1615 Office Job Journal
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Job Journal;
    SourceTable="Office Job Journal";
    DataCaptionExpr=CaptionTxt;
    SourceTableTemporary=true;
    OnOpenPage=VAR
                 JobJournalLine@1001 : Record "Job Journal Line";
                 JobUsageLink@1000 : Record "Job Usage Link";
                 JobJournalTemplate@1004 : Record "Job Journal Template";
                 JobJournalBatch@1003 : Record "Job Journal Batch";
                 OfficeJobsHandler@1002 : Codeunit "Office Jobs Handler";
               BEGIN
                 TemplateEditable := true;

                 JobUsageLink.SETRANGE("Job No.","Job No.");
                 JobUsageLink.SETRANGE("Job Task No.","Job Task No.");
                 JobUsageLink.SETRANGE("Line No.","Job Planning Line No.");

                 JobPlanningLine.GET("Job No.","Job Task No.","Job Planning Line No.");
                 OfficeJobsHandler.SetJobJournalRange(JobJournalLine,JobPlanningLine);

                 if JobJournalLine.ISEMPTY and JobUsageLink.ISEMPTY then begin
                   IsEditable := true;
                   CaptionTxt := EnterJobInfoTxt;
                   DisplayQuantity := JobPlanningLine.Quantity;

                   JobJournalTemplate.SETRANGE("Page ID",PAGE::"Job Journal");
                   JobJournalTemplate.SETRANGE(Recurring,false);

                   if JobJournalTemplate.COUNT = 1 then begin
                     TemplateEditable := false;
                     JobJournalTemplate.FINDFIRST;
                     "Job Journal Template Name" := JobJournalTemplate.Name;
                     FindJobJournalBatch(JobJournalBatch);
                     if JobJournalBatch.COUNT = 1 then begin
                       JobJournalBatch.FINDFIRST;
                       "Job Journal Batch Name" := JobJournalBatch.Name;
                     end else
                       BatchEditable := true;
                   end;
                 end else begin
                   CaptionTxt := JobCompletedTxt;
                   if not JobUsageLink.ISEMPTY then
                     DisplayQuantity := JobPlanningLine."Qty. Posted"
                   else begin
                     JobJournalLine.FINDFIRST;
                     DisplayQuantity := JobJournalLine.Quantity;
                     "Job Journal Template Name" := JobJournalLine."Journal Template Name";
                     "Job Journal Batch Name" := JobJournalLine."Journal Batch Name";
                   end;
                 end;

                 CurrPage.EDITABLE(IsEditable);
                 CurrPage.UPDATE(true);
               END;

    ActionList=ACTIONS
    {
      { 11      ;    ;ActionContainer;
                      Name=ActionContainer11;
                      ActionContainerType=ActionItems }
      { 12      ;1   ;Action    ;
                      Name=Submit;
                      CaptionML=ENU=Submit;
                      ToolTipML=ENU=Submit the quantity for this completed planning line.;
                      ApplicationArea=#Jobs;
                      Promoted=true;
                      Visible=IsEditable;
                      Image=CompleteLine;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 OfficeJobsHandler@1000 : Codeunit "Office Jobs Handler";
                               BEGIN
                                 JobPlanningLine."Qty. to Transfer to Journal" := DisplayQuantity;
                                 OfficeJobsHandler.SubmitJobPlanningLine(JobPlanningLine,"Job Journal Template Name","Job Journal Batch Name");
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=<Control1>;
                ContainerType=ContentArea }

    { 3   ;1   ;Field     ;
                Name=Description;
                ToolTipML=ENU=Specifies the name of the resource, item, or general ledger account to which this entry applies. You can change the description.;
                ApplicationArea=#Jobs;
                SourceExpr=JobPlanningLine.Description;
                Editable=False }

    { 5   ;1   ;Field     ;
                Name=Document No.;
                ToolTipML=ENU=Specifies a document number for the journal line.;
                ApplicationArea=#Jobs;
                SourceExpr=JobPlanningLine."Document No.";
                Editable=False }

    { 6   ;1   ;Field     ;
                Name=Job No.;
                ToolTipML=ENU=Specifies the number of the related job.;
                ApplicationArea=#Jobs;
                SourceExpr="Job No.";
                Editable=False;
                ImplicitType=Code20 }

    { 7   ;1   ;Field     ;
                Name=Job Task No.;
                ToolTipML=ENU=Specifies the number of the related job task.;
                ApplicationArea=#Jobs;
                SourceExpr="Job Task No.";
                Editable=False;
                ImplicitType=Code20 }

    { 4   ;1   ;Field     ;
                Name=JobJournalTemplate;
                CaptionML=ENU=Job Journal Template;
                ToolTipML=ENU=Specifies the journal template that is used for the job journal.;
                ApplicationArea=#Jobs;
                SourceExpr="Job Journal Template Name";
                Editable=TemplateEditable AND IsEditable;
                OnValidate=VAR
                             JobJournalTemplate@1001 : Record "Job Journal Template";
                             JobJournalBatch@1000 : Record "Job Journal Batch";
                           BEGIN
                             JobJournalTemplate.GET("Job Journal Template Name");
                             FindJobJournalBatch(JobJournalBatch);
                             "Job Journal Batch Name" := '';
                             BatchEditable := false;
                             case JobJournalBatch.COUNT of
                               0:
                                 ERROR(NoBatchesErr);
                               1:
                                 begin
                                   JobJournalBatch.FINDFIRST;
                                   "Job Journal Batch Name" := JobJournalBatch.Name;
                                 end;
                               else
                                 BatchEditable := true;
                             end;
                           END;

                ImplicitType=Code10 }

    { 8   ;1   ;Field     ;
                Name=JobJournalBatch;
                CaptionML=ENU=Job Journal Batch;
                ToolTipML=ENU=Specifies the journal batch that is used for the job journal.;
                ApplicationArea=#Jobs;
                SourceExpr="Job Journal Batch Name";
                Editable=BatchEditable AND IsEditable;
                ImplicitType=Code10 }

    { 2   ;1   ;Field     ;
                Name=Date;
                CaptionML=ENU=Date;
                ToolTipML=ENU=Specifies the date of the planning line.;
                ApplicationArea=#Jobs;
                SourceExpr=JobPlanningLine."Planning Date";
                Editable=IsEditable }

    { 10  ;1   ;Field     ;
                Name=DisplayQuantity;
                CaptionML=ENU=Quantity;
                ToolTipML=ENU=Specifies the quantity you want to transfer to the job journal.;
                ApplicationArea=#Jobs;
                SourceExpr=DisplayQuantity;
                Editable=IsEditable;
                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      JobPlanningLine@1011 : Record "Job Planning Line";
      DisplayQuantity@1002 : Decimal;
      IsEditable@1009 : Boolean;
      TemplateEditable@1008 : Boolean;
      BatchEditable@1000 : Boolean;
      CaptionTxt@1001 : Text;
      EnterJobInfoTxt@1003 : TextConst 'ENU=Enter Job Information';
      JobCompletedTxt@1004 : TextConst 'ENU=Job Completed';
      NoBatchesErr@1010 : TextConst 'ENU=There are no batches available for the selected template.';

    LOCAL PROCEDURE FindJobJournalBatch@2(VAR JobJournalBatch@1000 : Record "Job Journal Batch");
    BEGIN
      JobJournalBatch.SETRANGE("Journal Template Name","Job Journal Template Name");
    END;

    BEGIN
    END.
  }
}

