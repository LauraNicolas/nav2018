OBJECT Report 1192 Suggest Res. Price Chg.(Price)
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Suggest Res. Price Chg.(Price);
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);
                  if ToCurrency.Code <> '' then
                    ToCurrency.FIND;
                END;

  }
  DATASET
  {
    { 9832;    ;DataItem;Resource Price      ;
               DataItemTable="Resource Price";
               DataItemTableView=SORTING(Type,Code,"Work Type Code","Currency Code");
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text001 +
                                 Text002 +
                                 Text003);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,Type);
                                  Window.UPDATE(2,Code);
                                  ResPriceChg.Type := Type;
                                  ResPriceChg.Code := Code;

                                  if ToCurrency.Code = '' then begin
                                    ResPriceChg."Currency Code" := "Currency Code";
                                    SETRANGE("Currency Code",'');
                                  end else
                                    ResPriceChg."Currency Code" := ToCurrency.Code;

                                  if ToWorkType.Code = '' then
                                    ResPriceChg."Work Type Code" := "Work Type Code"
                                  else
                                    ResPriceChg."Work Type Code" := ToWorkType.Code;

                                  ResPriceChg."New Unit Price" := "Unit Price";

                                  if ResPriceChg."Currency Code" <> "Currency Code" then begin
                                    if "Currency Code" <> '' then
                                      ResPriceChg."New Unit Price" :=
                                        CurrExchRate.ExchangeAmtFCYToLCY(
                                          WORKDATE,"Currency Code",ResPriceChg."New Unit Price",
                                          CurrExchRate.ExchangeRate(
                                            WORKDATE,"Currency Code"));
                                    if ResPriceChg."Currency Code" <> '' then
                                      ResPriceChg."New Unit Price" :=
                                        CurrExchRate.ExchangeAmtLCYToFCY(
                                          WORKDATE,ResPriceChg."Currency Code",
                                          ResPriceChg."New Unit Price",
                                          CurrExchRate.ExchangeRate(
                                            WORKDATE,ResPriceChg."Currency Code"));
                                  end;

                                  if ResPriceChg."Currency Code" = '' then
                                    Currency2.InitRoundingPrecision
                                  else begin
                                    Currency2.GET(ResPriceChg."Currency Code");
                                    Currency2.TESTFIELD("Unit-Amount Rounding Precision");
                                  end;

                                  ResPriceChg."New Unit Price" :=
                                    ROUND(ResPriceChg."New Unit Price",Currency2."Unit-Amount Rounding Precision");

                                  if ResPriceChg."New Unit Price" > PriceLowerLimit then
                                    ResPriceChg."New Unit Price" := ResPriceChg."New Unit Price" * UnitPricefactor;

                                  if RoundingMethod.Code <> '' then begin
                                    RoundingMethod."Minimum Amount" := ResPriceChg."New Unit Price";
                                    if RoundingMethod.FIND('=<') then begin
                                      ResPriceChg."New Unit Price" := ResPriceChg."New Unit Price" + RoundingMethod."Amount Added Before";
                                      if RoundingMethod.Precision > 0 then
                                        ResPriceChg."New Unit Price" :=
                                          ROUND(ResPriceChg."New Unit Price",
                                            RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                                      ResPriceChg."New Unit Price" := ResPriceChg."New Unit Price" + RoundingMethod."Amount Added After";
                                    end;
                                  end;

                                  ResPrice.SETRANGE("Work Type Code",ResPriceChg."Work Type Code");
                                  ResPrice.SETRANGE(Code,ResPriceChg.Code);
                                  ResPrice.SETRANGE(Type,ResPriceChg.Type);
                                  ResPrice.SETRANGE("Currency Code",ResPriceChg."Currency Code");
                                  if ResPrice.FINDLAST then begin
                                    ResPriceChg."Current Unit Price" := ResPrice."Unit Price";
                                    PriceAlreadyExists := true;
                                  end else begin
                                    ResPriceChg."Current Unit Price" := 0;
                                    PriceAlreadyExists := false;
                                  end;
                                  if PriceAlreadyExists or CreateNewPrices then begin
                                    ResPriceChg2 := ResPriceChg;
                                    if ResPriceChg2.FIND('=') then
                                      ResPriceChg.MODIFY
                                    else
                                      ResPriceChg.INSERT;
                                  end;
                                END;

               ReqFilterFields=Type,Code,"Work Type Code","Currency Code" }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   if UnitPricefactor = 0 then begin
                     UnitPricefactor := 1;
                     CreateNewPrices := true;
                   end;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 15  ;2   ;Group     ;
                  Name=Copy to Res. Price Change...;
                  CaptionML=ENU=Copy to Res. Price Change... }

      { 7   ;3   ;Field     ;
                  Name=Currency Code;
                  CaptionML=ENU=Currency Code;
                  ToolTipML=ENU=Specifies the code for the currency that amounts are shown in.;
                  ApplicationArea=#Jobs;
                  SourceExpr=ToCurrency.Code;
                  TableRelation=Currency }

      { 6   ;3   ;Field     ;
                  Name=Work Type;
                  CaptionML=ENU=Work Type;
                  ToolTipML=ENU=Specifies the work types to which the new prices apply. If you want the new price to be calculated for all work types, enter ALL.;
                  ApplicationArea=#Jobs;
                  SourceExpr=ToWorkType.Code;
                  TableRelation="Work Type" }

      { 16  ;2   ;Field     ;
                  Name=Only Prices Above;
                  CaptionML=ENU=Only Prices Above;
                  ToolTipML=ENU=Specifies an amount to determine the lowest unit price that is changed. Only prices that are higher than this are changed. If a price is lower than or equal to this amount, a line for it is created in the Resource Price Changes window, but with the same unit price as in the Resource Prices window.;
                  ApplicationArea=#Jobs;
                  DecimalPlaces=2:5;
                  SourceExpr=PriceLowerLimit }

      { 13  ;2   ;Field     ;
                  Name=Adjustment Factor;
                  CaptionML=ENU=Adjustment Factor;
                  ToolTipML=ENU=Specifies an adjustment factor to multiply the amounts that you want suggested. By entering an adjustment factor, you can increase or decrease the amounts that are suggested.;
                  ApplicationArea=#Jobs;
                  DecimalPlaces=0:5;
                  SourceExpr=UnitPricefactor;
                  MinValue=0 }

      { 9   ;2   ;Field     ;
                  Name=Rounding Method;
                  CaptionML=ENU=Rounding Method;
                  ToolTipML=ENU=Specifies a code for the rounding method that you want to apply to costs or prices that you adjust.;
                  ApplicationArea=#Jobs;
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

      { 1   ;2   ;Field     ;
                  Name=Create New Prices;
                  CaptionML=ENU=Create New Prices;
                  ToolTipML=ENU=Specifies if you want the batch job to create new price suggestions, such as a new combination of currency, job number, or work type. If you only want to adjust existing alternative prices, do not select.;
                  ApplicationArea=#Jobs;
                  SourceExpr=CreateNewPrices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Processing resources...\\';
      Text002@1002 : TextConst 'ENU=Type         #1##########\';
      Text003@1003 : TextConst 'ENU=Code         #2##########\';
      RoundingMethod@1008 : Record "Rounding Method";
      ToCurrency@1007 : Record Currency;
      CurrExchRate@1006 : Record "Currency Exchange Rate";
      ToWorkType@1016 : Record "Work Type";
      ResPriceChg@1015 : Record "Resource Price Change";
      ResPriceChg2@1014 : Record "Resource Price Change";
      Currency2@1013 : Record Currency;
      ResPrice@1009 : Record "Resource Price";
      Window@1005 : Dialog;
      CreateNewPrices@1010 : Boolean;
      UnitPricefactor@1011 : Decimal;
      PriceLowerLimit@1012 : Decimal;
      PriceAlreadyExists@1019 : Boolean;

    PROCEDURE InitializeCopyToResPrice@3(CurrencyCode@1000 : Code[10];WorkTypeCode@1002 : Code[10]);
    BEGIN
      ToCurrency.Code := CurrencyCode;
      ToWorkType.Code := WorkTypeCode;
    END;

    PROCEDURE InitializeRequest@2(PriceLowerLimitFrom@1003 : Decimal;UnitPriceFactorFrom@1004 : Decimal;RoundingMethodCode@1005 : Code[10];CreateNewPricesFrom@1006 : Boolean);
    BEGIN
      PriceLowerLimit := PriceLowerLimitFrom;
      UnitPricefactor := UnitPriceFactorFrom;
      RoundingMethod.Code := RoundingMethodCode;
      CreateNewPrices := CreateNewPricesFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

