OBJECT Codeunit 454 Job Queue - Send Notification
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Job Queue Entry";
    OnRun=VAR
            RecordLink@1000 : Record "Record Link";
          BEGIN
            RecordLink."Link ID" := 0;
            RecordLink."Record ID" := RECORDID;
            if Status = Status::Error then
              RecordLink.Description := COPYSTR(GetErrorMessage,1,MAXSTRLEN(RecordLink.Description))
            else
              RecordLink.Description := Description;
            SetURL(Rec,RecordLink);
            RecordLink.Type := RecordLink.Type::Note;
            RecordLink.Created := CURRENTDATETIME;
            RecordLink."User ID" := USERID;
            RecordLink.Company := COMPANYNAME;
            RecordLink.Notify := true;
            RecordLink."To User ID" := "User ID";
            SetText(Rec,RecordLink);
            RecordLink.INSERT;
          END;

  }
  CODE
  {
    VAR
      ErrorText1@1002 : TextConst 'ENU=Error when processing ''%1''.';
      ErrorText2@1000 : TextConst 'ENU=Error message:';
      Text001@1001 : TextConst '@@@="%1 = job description, e.g. ''Post Sales Order 1234''";ENU=''%1'' finished successfully.';

    PROCEDURE SetJobQueueEntryStatusToOnHold@1(ModifyOnlyWhenReadOnlyNotification@1001 : Notification);
    VAR
      JobQueueEntry@1000 : Record "Job Queue Entry";
    BEGIN
      if not ModifyOnlyWhenReadOnlyNotification.HASDATA(JobQueueEntry.FIELDNAME(ID)) then
        exit;

      if JobQueueEntry.GET(ModifyOnlyWhenReadOnlyNotification.GETDATA(JobQueueEntry.FIELDNAME(ID))) then
        JobQueueEntry.SetStatus(JobQueueEntry.Status::"On Hold");
    END;

    LOCAL PROCEDURE SetURL@3(VAR JobQueueEntry@1002 : Record "Job Queue Entry";VAR RecordLink@1000 : Record "Record Link");
    VAR
      Link@1001 : Text;
    BEGIN
      with JobQueueEntry do begin
        // Generates a URL such as dynamicsnav://host:port/instance/company/runpage?page=672&$filter=
        // The intent is to open up the Job Queue Entries page and show the list of "my errors".
        // TODO: Leverage parameters ",JobQueueEntry,TRUE)" to take into account the filters, which will add the
        // following to the Url: &$filter=JobQueueEntry.Status IS 2 AND JobQueueEntry."User ID" IS <UserID>.
        // This will also require setting the filters on the record, such as:
        // SETFILTER(Status,'=2');
        // SETFILTER("User ID",'=%1',"User ID");
        Link := GETURL(DEFAULTCLIENTTYPE,COMPANYNAME,OBJECTTYPE::Page,PAGE::"Job Queue Entries") +
          STRSUBSTNO('&$filter=''%1''.''%2''%20IS%20''2''%20AND%20''%1''.''%3''%20IS%20''%4''&mode=View',
            HtmlEncode(TABLENAME),HtmlEncode(FIELDNAME(Status)),HtmlEncode(FIELDNAME("User ID")),HtmlEncode("User ID"));

        RecordLink.URL1 := COPYSTR(Link,1,MAXSTRLEN(RecordLink.URL1));
        if STRLEN(Link) > MAXSTRLEN(RecordLink.URL1) then
          RecordLink.URL2 := COPYSTR(Link,MAXSTRLEN(RecordLink.URL1) + 1,MAXSTRLEN(RecordLink.URL2));
      end;
    END;

    LOCAL PROCEDURE SetText@4(VAR JobQueueEntry@1001 : Record "Job Queue Entry";VAR RecordLink@1000 : Record "Record Link");
    VAR
      TypeHelper@1002 : Codeunit "Type Helper";
      s@1007 : Text;
      lf@1006 : Text;
      c1@1005 : Byte;
    BEGIN
      with JobQueueEntry do begin
        c1 := 13;
        lf[1] := c1;

        if Status = Status::Error then
          s := STRSUBSTNO(ErrorText1,Description) + lf + ErrorText2 + ' ' + GetErrorMessage
        else
          s := STRSUBSTNO(Text001,Description);

        TypeHelper.WriteRecordLinkNote(RecordLink,s);
      end;
    END;

    LOCAL PROCEDURE HtmlEncode@20(InText@1000 : Text[1024]) : Text[1024];
    VAR
      SystemWebHttpUtility@1001 : DotNet "'System.Web, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Web.HttpUtility";
    BEGIN
      SystemWebHttpUtility := SystemWebHttpUtility.HttpUtility;
      exit(SystemWebHttpUtility.HtmlEncode(InText));
    END;

    BEGIN
    END.
  }
}

