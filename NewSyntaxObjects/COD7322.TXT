OBJECT Codeunit 7322 Create Inventory Pick/Movement
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Warehouse Activity Header";
    Permissions=TableData "Whse. Item Tracking Line"=rimd;
    OnRun=BEGIN
            WhseActivHeader := Rec;
            Code;
            Rec := WhseActivHeader;
          END;

  }
  CODE
  {
    VAR
      WhseRequest@1009 : Record "Warehouse Request";
      WhseActivHeader@1000 : Record "Warehouse Activity Header";
      Text000@1001 : TextConst 'ENU=There is nothing to handle.';
      TempHandlingSpecification@1006 : TEMPORARY Record "Tracking Specification";
      Location@1019 : Record Location;
      Item@1018 : Record Item;
      TempReservEntry@1028 : TEMPORARY Record "Reservation Entry";
      PurchHeader@1013 : Record "Purchase Header";
      SalesHeader@1012 : Record "Sales Header";
      TransferHeader@1011 : Record "Transfer Header";
      ProdHeader@1010 : Record "Production Order";
      AssemblyHeader@1035 : Record "Assembly Header";
      WMSMgt@1021 : Codeunit "WMS Management";
      ItemTrackingMgt@1032 : Codeunit "Item Tracking Management";
      PostingDate@1016 : Date;
      VendorDocNo@1017 : Code[35];
      NextLineNo@1002 : Integer;
      LastTempHandlingSpecNo@1024 : Integer;
      HideDialog@1008 : Boolean;
      SNRequired@1005 : Boolean;
      LNRequired@1004 : Boolean;
      Text001@1007 : TextConst 'ENU=Quantity available to pick is not sufficient to fulfill shipping advise %1 for sales line with Document Type %2, Document No. %3, Line No. %4.';
      CheckLineExist@1014 : Boolean;
      AutoCreation@1015 : Boolean;
      LineCreated@1020 : Boolean;
      CompleteShipment@1022 : Boolean;
      PrintDocument@1036 : Boolean;
      ShowError@1023 : Boolean;
      Text002@1025 : TextConst 'ENU=Quantity available to pick is not sufficient to fulfill shipping advise %1 for transfer line with Document No. %2, Line No. %3.';
      IsInvtMovement@1026 : Boolean;
      IsBlankInvtMovement@1027 : Boolean;
      Text003@1029 : TextConst 'ENU=%1 activity no. %2 has been created.';
      Text004@1030 : TextConst 'ENU=Do you want to create Inventory Movement?';
      FromBinCode@1031 : Code[20];
      HasExpiredItems@1033 : Boolean;
      ExpiredItemMessageText@1034 : Text[100];
      ATOInvtMovementsCreated@1037 : Integer;
      TotalATOInvtMovementsToBeCreated@1038 : Integer;

    LOCAL PROCEDURE Code@11();
    BEGIN
      WhseActivHeader.TESTFIELD("No.");
      WhseActivHeader.TESTFIELD("Location Code");

      if not HideDialog then
        if not GetWhseRequest(WhseRequest) then
          exit;

      GetSourceDocHeader;
      UpdateWhseActivHeader(WhseRequest);

      case WhseRequest."Source Document" of
        WhseRequest."Source Document"::"Purchase Order":
          CreatePickOrMoveFromPurchase(PurchHeader);
        WhseRequest."Source Document"::"Purchase Return Order":
          CreatePickOrMoveFromPurchase(PurchHeader);
        WhseRequest."Source Document"::"Sales Order":
          CreatePickOrMoveFromSales(SalesHeader);
        WhseRequest."Source Document"::"Sales Return Order":
          CreatePickOrMoveFromSales(SalesHeader);
        WhseRequest."Source Document"::"Outbound Transfer":
          CreatePickOrMoveFromTransfer(TransferHeader);
        WhseRequest."Source Document"::"Prod. Consumption":
          CreatePickOrMoveFromProduction(ProdHeader);
        WhseRequest."Source Document"::"Assembly Consumption":
          CreatePickOrMoveFromAssembly(AssemblyHeader);
      end;

      if LineCreated then
        WhseActivHeader.MODIFY
      else
        if not AutoCreation then
          MESSAGE(Text000 + ExpiredItemMessageText);
    END;

    LOCAL PROCEDURE GetWhseRequest@1(VAR WhseRequest@1000 : Record "Warehouse Request") : Boolean;
    BEGIN
      with WhseRequest do begin
        FILTERGROUP := 2;
        SETRANGE(Type,Type::Outbound);
        SETRANGE("Location Code",WhseActivHeader."Location Code");
        SETRANGE("Document Status","Document Status"::Released);
        if WhseActivHeader."Source Document" <> 0 then
          SETRANGE("Source Document",WhseActivHeader."Source Document")
        else
          if WhseActivHeader.Type = WhseActivHeader.Type::"Invt. Movement" then
            SETFILTER("Source Document",'%1|%2|%3',
              WhseActivHeader."Source Document"::"Prod. Consumption",
              WhseActivHeader."Source Document"::"Prod. Output",
              WhseActivHeader."Source Document"::"Assembly Consumption");
        if WhseActivHeader."Source No." <> '' then
          SETRANGE("Source No.",WhseActivHeader."Source No.");
        SETRANGE("Completely Handled",false);
        FILTERGROUP := 0;
        if PAGE.RUNMODAL(PAGE::"Source Documents",WhseRequest,"Source No.") = ACTION::LookupOK then
          exit(true);
      end;
    END;

    LOCAL PROCEDURE GetSourceDocHeader@13();
    BEGIN
      case WhseRequest."Source Document" of
        WhseRequest."Source Document"::"Purchase Order":
          begin
            PurchHeader.GET(PurchHeader."Document Type"::Order,WhseRequest."Source No.");
            PostingDate := PurchHeader."Posting Date";
            VendorDocNo := PurchHeader."Vendor Invoice No.";
          end;
        WhseRequest."Source Document"::"Purchase Return Order":
          begin
            PurchHeader.GET(PurchHeader."Document Type"::"Return Order",WhseRequest."Source No.");
            PostingDate := PurchHeader."Posting Date";
            VendorDocNo := PurchHeader."Vendor Cr. Memo No.";
          end;
        WhseRequest."Source Document"::"Sales Order":
          begin
            SalesHeader.GET(SalesHeader."Document Type"::Order,WhseRequest."Source No.");
            PostingDate := SalesHeader."Posting Date";
          end;
        WhseRequest."Source Document"::"Sales Return Order":
          begin
            SalesHeader.GET(SalesHeader."Document Type"::"Return Order",WhseRequest."Source No.");
            PostingDate := SalesHeader."Posting Date";
          end;
        WhseRequest."Source Document"::"Outbound Transfer":
          begin
            TransferHeader.GET(WhseRequest."Source No.");
            PostingDate := TransferHeader."Posting Date";
          end;
        WhseRequest."Source Document"::"Prod. Consumption":
          begin
            ProdHeader.GET(WhseRequest."Source Subtype",WhseRequest."Source No.");
            PostingDate := WORKDATE;
          end;
        WhseRequest."Source Document"::"Assembly Consumption":
          begin
            AssemblyHeader.GET(WhseRequest."Source Subtype",WhseRequest."Source No.");
            PostingDate := AssemblyHeader."Posting Date";
          end;
      end;
    END;

    LOCAL PROCEDURE UpdateWhseActivHeader@2(WhseRequest@1001 : Record "Warehouse Request");
    BEGIN
      with WhseRequest do begin
        if WhseActivHeader."Source Document" = 0 then begin
          WhseActivHeader."Source Document" := "Source Document";
          WhseActivHeader."Source Type" := "Source Type";
          WhseActivHeader."Source Subtype" := "Source Subtype";
        end else
          WhseActivHeader.TESTFIELD("Source Document","Source Document");
        if WhseActivHeader."Source No." = '' then
          WhseActivHeader."Source No." := "Source No."
        else
          WhseActivHeader.TESTFIELD("Source No.","Source No.");

        WhseActivHeader."Destination Type" := "Destination Type";
        WhseActivHeader."Destination No." := "Destination No.";
        WhseActivHeader."External Document No." := "External Document No.";
        WhseActivHeader."Shipment Date" := "Shipment Date";
        WhseActivHeader."Posting Date" := PostingDate;
        WhseActivHeader."External Document No.2" := VendorDocNo;
        GetLocation("Location Code");
      end;
    END;

    LOCAL PROCEDURE CreatePickOrMoveFromPurchase@7(PurchHeader@1000 : Record "Purchase Header");
    VAR
      PurchLine@1001 : Record "Purchase Line";
      NewWhseActivLine@1005 : Record "Warehouse Activity Line";
      RemQtyToPickBase@1007 : Decimal;
    BEGIN
      with PurchLine do begin
        if not SetFilterPurchLine(PurchLine,PurchHeader) then begin
          if not HideDialog then
            MESSAGE(Text000);
          exit;
        end;

        FindNextLineNo;

        repeat
          if not NewWhseActivLine.ActivityExists(DATABASE::"Purchase Line","Document Type","Document No.","Line No.",0,0) then begin
            NewWhseActivLine.INIT;
            NewWhseActivLine."Activity Type" := WhseActivHeader.Type;
            NewWhseActivLine."No." := WhseActivHeader."No.";
            if Location."Bin Mandatory" then
              NewWhseActivLine."Action Type" := NewWhseActivLine."Action Type"::Take;
            NewWhseActivLine.SetSource(DATABASE::"Purchase Line","Document Type","Document No.","Line No.",0);
            NewWhseActivLine."Location Code" := "Location Code";
            NewWhseActivLine."Bin Code" := "Bin Code";
            NewWhseActivLine."Item No." := "No.";
            NewWhseActivLine."Variant Code" := "Variant Code";
            NewWhseActivLine."Unit of Measure Code" := "Unit of Measure Code";
            NewWhseActivLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
            NewWhseActivLine.Description := Description;
            NewWhseActivLine."Description 2" := "Description 2";
            NewWhseActivLine."Due Date" := "Expected Receipt Date";
            NewWhseActivLine."Destination Type" := NewWhseActivLine."Destination Type"::Vendor;
            NewWhseActivLine."Destination No." := PurchHeader."Buy-from Vendor No.";
            if "Document Type" = "Document Type"::Order then begin
              NewWhseActivLine."Source Document" := NewWhseActivLine."Source Document"::"Purchase Order";
              RemQtyToPickBase := -"Qty. to Receive (Base)";
            end else begin
              NewWhseActivLine."Source Document" :=
                NewWhseActivLine."Source Document"::"Purchase Return Order";
              RemQtyToPickBase := "Return Qty. to Ship (Base)";
            end;

            CALCFIELDS("Reserved Quantity");
            CreatePickOrMoveLine(
              NewWhseActivLine,RemQtyToPickBase,"Outstanding Qty. (Base)","Reserved Quantity" <> 0);
          end;
        until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE SetFilterPurchLine@14(VAR PurchLine@1000 : Record "Purchase Line";PurchHeader@1001 : Record "Purchase Header") : Boolean;
    BEGIN
      with PurchLine do begin
        SETCURRENTKEY("Document Type","Document No.","Location Code");
        SETRANGE("Document Type",PurchHeader."Document Type");
        SETRANGE("Document No.",PurchHeader."No.");
        SETRANGE("Drop Shipment",false);
        if not CheckLineExist then
          SETRANGE("Location Code",WhseActivHeader."Location Code");
        SETRANGE(Type,Type::Item);
        if PurchHeader."Document Type" = PurchHeader."Document Type"::Order then
          SETFILTER("Qty. to Receive",'<%1',0)
        else
          SETFILTER("Return Qty. to Ship",'>%1',0);
        exit(FIND('-'));
      end;
    END;

    LOCAL PROCEDURE CreatePickOrMoveFromSales@8(SalesHeader@1000 : Record "Sales Header");
    VAR
      SalesLine@1006 : Record "Sales Line";
      NewWhseActivLine@1003 : Record "Warehouse Activity Line";
      RemQtyToPickBase@1001 : Decimal;
    BEGIN
      with SalesLine do begin
        if not SetFilterSalesLine(SalesLine,SalesHeader) then begin
          if not HideDialog then
            MESSAGE(Text000);
          exit;
        end;
        CompleteShipment := true;

        FindNextLineNo;

        repeat
          if not NewWhseActivLine.ActivityExists(DATABASE::"Sales Line","Document Type","Document No.","Line No.",0,0) then begin
            NewWhseActivLine.INIT;
            NewWhseActivLine."Activity Type" := WhseActivHeader.Type;
            NewWhseActivLine."No." := WhseActivHeader."No.";
            if Location."Bin Mandatory" then
              NewWhseActivLine."Action Type" := NewWhseActivLine."Action Type"::Take;
            NewWhseActivLine.SetSource(DATABASE::"Sales Line","Document Type","Document No.","Line No.",0);
            NewWhseActivLine."Location Code" := "Location Code";
            NewWhseActivLine."Bin Code" := "Bin Code";
            NewWhseActivLine."Item No." := "No.";
            NewWhseActivLine."Variant Code" := "Variant Code";
            NewWhseActivLine."Unit of Measure Code" := "Unit of Measure Code";
            NewWhseActivLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
            NewWhseActivLine.Description := Description;
            NewWhseActivLine."Description 2" := "Description 2";
            NewWhseActivLine."Due Date" := "Planned Shipment Date";
            NewWhseActivLine."Shipping Advice" := SalesHeader."Shipping Advice";
            NewWhseActivLine."Shipping Agent Code" := "Shipping Agent Code";
            NewWhseActivLine."Shipping Agent Service Code" := "Shipping Agent Service Code";
            NewWhseActivLine."Shipment Method Code" := SalesHeader."Shipment Method Code";
            NewWhseActivLine."Destination Type" := NewWhseActivLine."Destination Type"::Customer;
            NewWhseActivLine."Destination No." := SalesHeader."Sell-to Customer No.";

            if "Document Type" = "Document Type"::Order then begin
              NewWhseActivLine."Source Document" := NewWhseActivLine."Source Document"::"Sales Order";
              RemQtyToPickBase := "Qty. to Ship (Base)";
            end else begin
              NewWhseActivLine."Source Document" := NewWhseActivLine."Source Document"::"Sales Return Order";
              RemQtyToPickBase := -"Return Qty. to Receive (Base)";
            end;

            CALCFIELDS("Reserved Quantity");
            CreatePickOrMoveLine(
              NewWhseActivLine,RemQtyToPickBase,"Outstanding Qty. (Base)","Reserved Quantity" <> 0);

            if SalesHeader."Shipping Advice" = SalesHeader."Shipping Advice"::Complete then begin
              if RemQtyToPickBase < 0 then begin
                if AutoCreation then begin
                  if WhseActivHeader.DELETE(true) then
                    LineCreated := false;
                  exit;
                end;
                ERROR(Text001,SalesHeader."Shipping Advice","Document Type","Document No.","Line No.");
              end;
              if (RemQtyToPickBase = 0) and not CompleteShipment then begin
                if ShowError then
                  ERROR(Text001,SalesHeader."Shipping Advice","Document Type","Document No.","Line No.");
                if WhseActivHeader.DELETE(true) then ;
                LineCreated := false;
                exit;
              end;
            end;
          end;
        until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE SetFilterSalesLine@15(VAR SalesLine@1000 : Record "Sales Line";SalesHeader@1001 : Record "Sales Header") : Boolean;
    BEGIN
      with SalesLine do begin
        SETCURRENTKEY("Document Type","Document No.","Location Code");
        SETRANGE("Document Type",SalesHeader."Document Type");
        SETRANGE("Document No.",SalesHeader."No.");
        SETRANGE("Drop Shipment",false);
        if not CheckLineExist then
          SETRANGE("Location Code",WhseActivHeader."Location Code");
        SETRANGE(Type,Type::Item);
        if SalesHeader."Document Type" = SalesHeader."Document Type"::Order then
          SETFILTER("Qty. to Ship",'>%1',0)
        else
          SETFILTER("Return Qty. to Receive",'<%1',0);
        exit(FIND('-'));
      end;
    END;

    LOCAL PROCEDURE CreatePickOrMoveFromTransfer@9(TransferHeader@1000 : Record "Transfer Header");
    VAR
      TransferLine@1006 : Record "Transfer Line";
      NewWhseActivLine@1003 : Record "Warehouse Activity Line";
      RemQtyToPickBase@1001 : Decimal;
    BEGIN
      with TransferLine do begin
        if not SetFilterTransferLine(TransferLine,TransferHeader) then begin
          if not HideDialog then
            MESSAGE(Text000);
          exit;
        end;
        CompleteShipment := true;

        FindNextLineNo;

        repeat
          if not NewWhseActivLine.ActivityExists(DATABASE::"Transfer Line",0,"Document No.","Line No.",0,0) then begin
            NewWhseActivLine.INIT;
            NewWhseActivLine."Activity Type" := WhseActivHeader.Type;
            NewWhseActivLine."No." := WhseActivHeader."No.";
            if Location."Bin Mandatory" then
              NewWhseActivLine."Action Type" := NewWhseActivLine."Action Type"::Take;
            NewWhseActivLine.SetSource(DATABASE::"Transfer Line",0,"Document No.","Line No.",0);
            NewWhseActivLine."Source Document" := NewWhseActivLine."Source Document"::"Outbound Transfer";
            NewWhseActivLine."Location Code" := "Transfer-from Code";
            NewWhseActivLine."Bin Code" := "Transfer-from Bin Code";
            NewWhseActivLine."Item No." := "Item No.";
            NewWhseActivLine."Variant Code" := "Variant Code";
            NewWhseActivLine."Unit of Measure Code" := "Unit of Measure Code";
            NewWhseActivLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
            NewWhseActivLine.Description := Description;
            NewWhseActivLine."Description 2" := "Description 2";
            NewWhseActivLine."Due Date" := "Shipment Date";
            NewWhseActivLine."Shipping Advice" := TransferHeader."Shipping Advice";
            NewWhseActivLine."Shipping Agent Code" := "Shipping Agent Code";
            NewWhseActivLine."Shipping Agent Service Code" := "Shipping Agent Service Code";
            NewWhseActivLine."Shipment Method Code" := TransferHeader."Shipment Method Code";
            NewWhseActivLine."Destination Type" := NewWhseActivLine."Destination Type"::Location;
            NewWhseActivLine."Destination No." := TransferHeader."Transfer-to Code";
            RemQtyToPickBase := "Qty. to Ship (Base)";

            CALCFIELDS("Reserved Quantity Outbnd.");
            CreatePickOrMoveLine(
              NewWhseActivLine,RemQtyToPickBase,
              "Outstanding Qty. (Base)","Reserved Quantity Outbnd." <> 0);

            if TransferHeader."Shipping Advice" = TransferHeader."Shipping Advice"::Complete then begin
              if RemQtyToPickBase > 0 then begin
                if AutoCreation then begin
                  WhseActivHeader.DELETE(true);
                  LineCreated := false;
                  exit;
                end;
                ERROR(Text002,TransferHeader."Shipping Advice","Document No.","Line No.");
              end;
              if (RemQtyToPickBase = 0) and not CompleteShipment then begin
                if ShowError then
                  ERROR(Text002,TransferHeader."Shipping Advice","Document No.","Line No.");
                if WhseActivHeader.DELETE(true) then ;
                LineCreated := false;
                exit;
              end;
            end;
          end;
        until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE SetFilterTransferLine@16(VAR TransferLine@1000 : Record "Transfer Line";TransferHeader@1001 : Record "Transfer Header") : Boolean;
    BEGIN
      with TransferLine do begin
        SETRANGE("Document No.",TransferHeader."No.");
        SETRANGE("Derived From Line No.",0);
        if not CheckLineExist then
          SETRANGE("Transfer-from Code",WhseActivHeader."Location Code");
        SETFILTER("Qty. to Ship",'>%1',0);
        exit(FIND('-'));
      end;
    END;

    LOCAL PROCEDURE CreatePickOrMoveFromProduction@6(ProdOrder@1000 : Record "Production Order");
    VAR
      ProdOrderComp@1001 : Record "Prod. Order Component";
      NewWhseActivLine@1003 : Record "Warehouse Activity Line";
      RemQtyToPickBase@1005 : Decimal;
    BEGIN
      with ProdOrderComp do begin
        if not SetFilterProductionLine(ProdOrderComp,ProdOrder) then begin
          if not HideDialog then
            MESSAGE(Text000);
          exit;
        end;

        FindNextLineNo;

        repeat
          if not
             NewWhseActivLine.ActivityExists(
               DATABASE::"Prod. Order Component",Status,"Prod. Order No.","Prod. Order Line No.","Line No.",0)
          then begin
            NewWhseActivLine.INIT;
            NewWhseActivLine."Activity Type" := WhseActivHeader.Type;
            NewWhseActivLine."No." := WhseActivHeader."No.";
            if Location."Bin Mandatory" then
              NewWhseActivLine."Action Type" := NewWhseActivLine."Action Type"::Take;
            NewWhseActivLine.SetSource(DATABASE::"Prod. Order Component",Status,"Prod. Order No.","Prod. Order Line No.","Line No.");
            NewWhseActivLine."Location Code" := "Location Code";
            NewWhseActivLine."Bin Code" := "Bin Code";
            NewWhseActivLine."Item No." := "Item No.";
            NewWhseActivLine."Variant Code" := "Variant Code";
            NewWhseActivLine."Unit of Measure Code" := "Unit of Measure Code";
            NewWhseActivLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
            NewWhseActivLine.Description := Description;
            NewWhseActivLine."Source Document" := NewWhseActivLine."Source Document"::"Prod. Consumption";
            NewWhseActivLine."Due Date" := "Due Date";
            if WhseActivHeader.Type = WhseActivHeader.Type::"Invt. Pick" then
              RemQtyToPickBase := "Remaining Qty. (Base)"
            else
              RemQtyToPickBase := "Expected Qty. (Base)" - "Qty. Picked (Base)";

            CALCFIELDS("Reserved Quantity");
            CreatePickOrMoveLine(
              NewWhseActivLine,RemQtyToPickBase,RemQtyToPickBase,"Reserved Quantity" <> 0);
          end;
        until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CreatePickOrMoveFromAssembly@37(AsmHeader@1000 : Record "Assembly Header");
    VAR
      AssemblyLine@1001 : Record "Assembly Line";
      NewWhseActivLine@1003 : Record "Warehouse Activity Line";
      RemQtyToPickBase@1005 : Decimal;
    BEGIN
      with AssemblyLine do begin
        if not SetFilterAssemblyLine(AssemblyLine,AsmHeader) then begin
          if not HideDialog then
            MESSAGE(Text000);
          exit;
        end;

        if WhseActivHeader.Type = WhseActivHeader.Type::"Invt. Pick" then // no support for inventory pick on assembly
          exit;

        FindNextLineNo;

        repeat
          if not
             NewWhseActivLine.ActivityExists(DATABASE::"Assembly Line","Document Type","Document No.","Line No.",0,0)
          then begin
            NewWhseActivLine.INIT;
            NewWhseActivLine."Activity Type" := WhseActivHeader.Type;
            NewWhseActivLine."No." := WhseActivHeader."No.";
            if Location."Bin Mandatory" then
              NewWhseActivLine."Action Type" := NewWhseActivLine."Action Type"::Take;
            NewWhseActivLine.SetSource(DATABASE::"Assembly Line","Document Type","Document No.","Line No.",0);
            NewWhseActivLine."Location Code" := "Location Code";
            NewWhseActivLine."Bin Code" := "Bin Code";
            NewWhseActivLine."Item No." := "No.";
            NewWhseActivLine."Variant Code" := "Variant Code";
            NewWhseActivLine."Unit of Measure Code" := "Unit of Measure Code";
            NewWhseActivLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
            NewWhseActivLine.Description := Description;
            NewWhseActivLine."Source Document" := NewWhseActivLine."Source Document"::"Assembly Consumption";
            NewWhseActivLine."Due Date" := "Due Date";
            NewWhseActivLine."Destination Type" := NewWhseActivLine."Destination Type"::Item;
            NewWhseActivLine."Destination No." := AssemblyHeader."Item No.";
            RemQtyToPickBase := "Quantity (Base)" - "Remaining Quantity (Base)" +
              "Quantity to Consume (Base)" - "Qty. Picked (Base)";

            CALCFIELDS("Reserved Quantity");
            CreatePickOrMoveLine(
              NewWhseActivLine,RemQtyToPickBase,RemQtyToPickBase,"Reserved Quantity" <> 0);
          end;
        until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE SetFilterProductionLine@18(VAR ProdOrderComp@1000 : Record "Prod. Order Component";ProdOrder@1001 : Record "Production Order") : Boolean;
    BEGIN
      with ProdOrderComp do begin
        SETRANGE(Status,ProdOrder.Status);
        SETRANGE("Prod. Order No.",ProdOrder."No.");
        if not CheckLineExist then
          SETRANGE("Location Code",WhseActivHeader."Location Code");
        SETRANGE("Planning Level Code",0);
        if IsInvtMovement then begin
          SETFILTER("Bin Code",'<>%1','');
          SETFILTER("Flushing Method",'%1|%2|%3',
            "Flushing Method"::Manual,
            "Flushing Method"::"Pick + Forward",
            "Flushing Method"::"Pick + Backward");
        end else
          SETRANGE("Flushing Method","Flushing Method"::Manual);
        SETFILTER("Remaining Quantity",'>0');
        exit(FIND('-'));
      end;
    END;

    LOCAL PROCEDURE SetFilterAssemblyLine@38(VAR AssemblyLine@1000 : Record "Assembly Line";AsmHeader@1001 : Record "Assembly Header") : Boolean;
    BEGIN
      with AssemblyLine do begin
        SETRANGE("Document Type",AsmHeader."Document Type");
        SETRANGE("Document No.",AsmHeader."No.");
        SETRANGE(Type,Type::Item);
        if not CheckLineExist then
          SETRANGE("Location Code",WhseActivHeader."Location Code");
        if IsInvtMovement then
          SETFILTER("Bin Code",'<>%1','');
        SETFILTER("Remaining Quantity",'>0');
        exit(FIND('-'));
      end;
    END;

    LOCAL PROCEDURE FindNextLineNo@24();
    VAR
      WhseActivLine@1000 : Record "Warehouse Activity Line";
    BEGIN
      with WhseActivHeader do begin
        if IsInvtMovement then
          WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::"Invt. Movement")
        else
          WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::"Invt. Pick");
        WhseActivLine.SETRANGE("No.","No.");
        if WhseActivLine.FINDLAST then
          NextLineNo := WhseActivLine."Line No." + 10000
        else
          NextLineNo := 10000;
      end;
    END;

    LOCAL PROCEDURE CreatePickOrMoveLine@5(NewWhseActivLine@1003 : Record "Warehouse Activity Line";VAR RemQtyToPickBase@1004 : Decimal;OutstandingQtyBase@1006 : Decimal;ReservationExists@1007 : Boolean);
    VAR
      ReservationEntry@1009 : Record "Reservation Entry";
      ATOSalesLine@1010 : Record "Sales Line";
      ItemTrackingMgt@1002 : Codeunit "Item Tracking Management";
      QtyAvailToPickBase@1005 : Decimal;
      OriginalRemQtyToPickBase@1011 : Decimal;
      ITQtyToPickBase@1001 : Decimal;
      TotalITQtyToPickBase@1012 : Decimal;
      QtyToTrackBase@1000 : Decimal;
      EntriesExist@1008 : Boolean;
    BEGIN
      if ReservationExists then
        CalcRemQtyToPickOrMoveBase(NewWhseActivLine,OutstandingQtyBase,RemQtyToPickBase);
      if RemQtyToPickBase <= 0 then
        exit;

      OriginalRemQtyToPickBase := RemQtyToPickBase;

      QtyAvailToPickBase := CalcInvtAvailability(NewWhseActivLine,'','');
      if WMSMgt.GetATOSalesLine(
           NewWhseActivLine."Source Type",NewWhseActivLine."Source Subtype",NewWhseActivLine."Source No.",
           NewWhseActivLine."Source Line No.",ATOSalesLine)
      then
        QtyAvailToPickBase += ATOSalesLine.QtyToAsmBaseOnATO;

      if RemQtyToPickBase > QtyAvailToPickBase then begin
        RemQtyToPickBase := QtyAvailToPickBase;
        CompleteShipment := false;
      end;

      if RemQtyToPickBase > 0 then begin
        ItemTrackingMgt.CheckWhseItemTrkgSetup(NewWhseActivLine."Item No.",SNRequired,LNRequired,false);
        if SNRequired or LNRequired then begin
          if IsBlankInvtMovement then
            ItemTrackingMgt.SumUpItemTrackingOnlyInventoryOrATO(TempReservEntry,TempHandlingSpecification,true,true)
          else begin
            SetFilterReservEntry(ReservationEntry,NewWhseActivLine);
            ItemTrackingMgt.SumUpItemTrackingOnlyInventoryOrATO(ReservationEntry,TempHandlingSpecification,true,true);
          end;
          if PickOrMoveAccordingToFEFO(NewWhseActivLine."Location Code") or
             PickStrictExpirationPosting(NewWhseActivLine."Item No.")
          then begin
            QtyToTrackBase := RemQtyToPickBase;
            if UndefinedItemTrkg(QtyToTrackBase) then
              CreateTempHandlingSpec(NewWhseActivLine,QtyToTrackBase);
          end;

          TempHandlingSpecification.RESET;
          if TempHandlingSpecification.FIND('-') then
            repeat
              ITQtyToPickBase := ABS(TempHandlingSpecification."Qty. to Handle (Base)");
              TotalITQtyToPickBase += ITQtyToPickBase;
              if ITQtyToPickBase > 0 then begin
                NewWhseActivLine.CopyTrackingFromSpec(TempHandlingSpecification);
                if NewWhseActivLine.TrackingExists then
                  NewWhseActivLine."Expiration Date" :=
                    ItemTrackingMgt.ExistingExpirationDate(NewWhseActivLine."Item No.",
                      NewWhseActivLine."Variant Code",NewWhseActivLine."Lot No.",NewWhseActivLine."Serial No.",
                      false,EntriesExist);

                if Location."Bin Mandatory" then begin
                  // find Take qty. for bin code of source line
                  if (NewWhseActivLine."Bin Code" <> '') and (not IsInvtMovement or IsBlankInvtMovement) then
                    InsertPickOrMoveBinWhseActLine(
                      NewWhseActivLine,NewWhseActivLine."Bin Code",false,ITQtyToPickBase);

                  // Invt. movement without document has to be created
                  if IsBlankInvtMovement then
                    ITQtyToPickBase := 0;

                  // find Take qty. for default bin
                  if ITQtyToPickBase > 0 then
                    InsertPickOrMoveBinWhseActLine(NewWhseActivLine,'',true,ITQtyToPickBase);

                  // find Take qty. for other bins
                  if ITQtyToPickBase > 0 then
                    InsertPickOrMoveBinWhseActLine(NewWhseActivLine,'',false,ITQtyToPickBase);
                  if (ITQtyToPickBase = 0) and IsInvtMovement and not IsBlankInvtMovement then
                    SynchronizeWhseItemTracking(TempHandlingSpecification);
                end else
                  if ITQtyToPickBase > 0 then
                    InsertShelfWhseActivLine(NewWhseActivLine,ITQtyToPickBase);

                RemQtyToPickBase :=
                  RemQtyToPickBase + ITQtyToPickBase +
                  TempHandlingSpecification."Qty. to Handle (Base)";
              end;
              NewWhseActivLine.ClearTracking;
            until (TempHandlingSpecification.NEXT = 0) or (RemQtyToPickBase <= 0);

          RemQtyToPickBase := Minimum(RemQtyToPickBase,OriginalRemQtyToPickBase - TotalITQtyToPickBase);
        end;

        if Location."Bin Mandatory" then begin
          // find Take qty. for bin code of source line
          if (RemQtyToPickBase > 0) and
             (NewWhseActivLine."Bin Code" <> '') and
             (not IsInvtMovement or IsBlankInvtMovement) and
             (not HasExpiredItems)
          then
            InsertPickOrMoveBinWhseActLine(
              NewWhseActivLine,NewWhseActivLine."Bin Code",false,RemQtyToPickBase);

          // Invt. movement without document has to be created
          if IsBlankInvtMovement then
            RemQtyToPickBase := 0;

          // find Take qty. for default bin
          if (RemQtyToPickBase > 0) and (not HasExpiredItems) then
            InsertPickOrMoveBinWhseActLine(NewWhseActivLine,'',true,RemQtyToPickBase);

          // find Take qty. for other bins
          if (RemQtyToPickBase > 0) and (not HasExpiredItems) then
            InsertPickOrMoveBinWhseActLine(NewWhseActivLine,'',false,RemQtyToPickBase)
        end else
          if (RemQtyToPickBase > 0) and (not HasExpiredItems) then
            InsertShelfWhseActivLine(NewWhseActivLine,RemQtyToPickBase);
      end;
    END;

    LOCAL PROCEDURE CalcRemQtyToPickOrMoveBase@26(NewWhseActivLine@1001 : Record "Warehouse Activity Line";OutstandingQtyBase@1002 : Decimal;VAR RemQtyToPickBase@1000 : Decimal);
    VAR
      ATOSalesLine@1004 : Record "Sales Line";
      MaxQtyToPickBase@1003 : Decimal;
    BEGIN
      with NewWhseActivLine do begin
        MaxQtyToPickBase :=
          OutstandingQtyBase -
          WMSMgt.CalcLineReservedQtyNotonInvt(
            "Source Type","Source Subtype","Source No.",
            "Source Line No.","Source Subline No.");

        if WMSMgt.GetATOSalesLine("Source Type","Source Subtype","Source No.","Source Line No.",ATOSalesLine) then
          MaxQtyToPickBase += ATOSalesLine.QtyAsmRemainingBaseOnATO;

        if RemQtyToPickBase > MaxQtyToPickBase then begin
          RemQtyToPickBase := MaxQtyToPickBase;
          if "Shipping Advice" = "Shipping Advice"::Complete then
            CompleteShipment := false;
        end;
      end;
    END;

    LOCAL PROCEDURE InsertPickOrMoveBinWhseActLine@4(NewWhseActivLine@1004 : Record "Warehouse Activity Line";BinCode@1000 : Code[20];DefaultBin@1005 : Boolean;VAR RemQtyToPickBase@1003 : Decimal);
    VAR
      FromBinContent@1002 : Record "Bin Content";
      QtyToPickBase@1001 : Decimal;
      QtyAvailToPickBase@1006 : Decimal;
    BEGIN
      CreateATOPickLine(NewWhseActivLine,BinCode,RemQtyToPickBase);
      if RemQtyToPickBase = 0 then
        exit;

      with FromBinContent do begin
        SETCURRENTKEY(Default,"Location Code","Item No.","Variant Code","Bin Code");
        SETRANGE(Default,DefaultBin);
        SETRANGE("Location Code",NewWhseActivLine."Location Code");
        SETRANGE("Item No.",NewWhseActivLine."Item No.");
        SETRANGE("Variant Code",NewWhseActivLine."Variant Code");

        if (BinCode <> '') and not IsInvtMovement then
          SETRANGE("Bin Code",BinCode);

        if (NewWhseActivLine."Bin Code" <> '') and IsInvtMovement then
          // not movement within the same bin
          SETFILTER("Bin Code",'<>%1',NewWhseActivLine."Bin Code");

        if IsBlankInvtMovement then begin
          // inventory movement without source document, created from Internal Movement
          SETRANGE("Bin Code",FromBinCode);
          SETRANGE(Default);
        end;

        if NewWhseActivLine."Serial No." <> '' then
          SETRANGE("Serial No. Filter",NewWhseActivLine."Serial No.");
        if NewWhseActivLine."Lot No." <> '' then
          SETRANGE("Lot No. Filter",NewWhseActivLine."Lot No.");
        if FIND('-') then
          repeat
            QtyAvailToPickBase := CalcQtyAvailToPick(0);
            if RemQtyToPickBase < QtyAvailToPickBase then
              QtyAvailToPickBase := RemQtyToPickBase;
            if QtyAvailToPickBase > 0 then begin
              if SNRequired then begin
                QtyAvailToPickBase := ROUND(QtyAvailToPickBase,1,'<');
                QtyToPickBase := 1;
              end else
                QtyToPickBase := QtyAvailToPickBase;

              MakeHeader;

              repeat
                MakeLine(NewWhseActivLine,"Bin Code",QtyToPickBase,RemQtyToPickBase);
                QtyAvailToPickBase := QtyAvailToPickBase - QtyToPickBase;
              until QtyAvailToPickBase <= 0;
            end;
          until (NEXT = 0) or (RemQtyToPickBase = 0);
      end;
    END;

    LOCAL PROCEDURE InsertShelfWhseActivLine@25(NewWhseActivLine@1001 : Record "Warehouse Activity Line";VAR RemQtyToPickBase@1000 : Decimal);
    VAR
      QtyToPickBase@1002 : Decimal;
    BEGIN
      CreateATOPickLine(NewWhseActivLine,'',RemQtyToPickBase);
      if RemQtyToPickBase = 0 then
        exit;

      if SNRequired then begin
        RemQtyToPickBase := ROUND(RemQtyToPickBase,1,'<');
        QtyToPickBase := 1;
      end else
        QtyToPickBase := RemQtyToPickBase;

      MakeHeader;

      repeat
        MakeLine(NewWhseActivLine,'',QtyToPickBase,RemQtyToPickBase);
      until RemQtyToPickBase = 0;
    END;

    LOCAL PROCEDURE CalcInvtAvailability@28(WhseActivLine@1000 : Record "Warehouse Activity Line";LotNo@1010 : Code[20];SerialNo@1009 : Code[20]) : Decimal;
    VAR
      Item2@1002 : Record Item;
      TempWhseActivLine2@1004 : TEMPORARY Record "Warehouse Activity Line";
      WhseAvailMgt@1006 : Codeunit "Warehouse Availability Mgt.";
      QtyAssgndtoPick@1003 : Decimal;
      LineReservedQty@1001 : Decimal;
      QtyReservedOnPickShip@1005 : Decimal;
      QtyOnDedicatedBins@1007 : Decimal;
      QtyBlocked@1008 : Decimal;
    BEGIN
      with WhseActivLine do begin
        GetItem("Item No.");
        Item2 := Item;
        Item2.SETRANGE("Location Filter","Location Code");
        Item2.SETRANGE("Variant Filter","Variant Code");
        if SerialNo <> '' then
          Item2.SETRANGE("Serial No. Filter",SerialNo);
        if LotNo <> '' then
          Item2.SETRANGE("Lot No. Filter",LotNo);
        Item2.CALCFIELDS(Inventory);
        if not IsBlankInvtMovement then
          Item2.CALCFIELDS("Reserved Qty. on Inventory");

        QtyAssgndtoPick := WhseAvailMgt.CalcQtyAssgndtoPick(Location,"Item No.","Variant Code",'');
        QtyOnDedicatedBins := WhseAvailMgt.CalcQtyOnDedicatedBins("Location Code","Item No.","Variant Code",LotNo,SerialNo);
        QtyBlocked :=
          WhseAvailMgt.CalcQtyOnBlockedITOrOnBlockedOutbndBins("Location Code","Item No.","Variant Code",LotNo,SerialNo,false,false);
        LineReservedQty :=
          WhseAvailMgt.CalcLineReservedQtyOnInvt(
            "Source Type","Source Subtype","Source No.","Source Line No.","Source Subline No.",true,LotNo,SerialNo,TempWhseActivLine2);
        QtyReservedOnPickShip :=
          WhseAvailMgt.CalcReservQtyOnPicksShips("Location Code","Item No.","Variant Code",TempWhseActivLine2);
      end;
      exit(
        Item2.Inventory - ABS(Item2."Reserved Qty. on Inventory") - QtyAssgndtoPick - QtyOnDedicatedBins - QtyBlocked +
        LineReservedQty + QtyReservedOnPickShip);
    END;

    LOCAL PROCEDURE GetLocation@22(LocationCode@1000 : Code[10]);
    BEGIN
      if LocationCode <> Location.Code then begin
        if LocationCode = '' then
          Location.GetLocationSetup('',Location)
        else
          Location.GET(LocationCode);
      end;
    END;

    LOCAL PROCEDURE GetShelfNo@21(ItemNo@1000 : Code[20]) : Code[10];
    BEGIN
      GetItem(ItemNo);
      exit(Item."Shelf No.");
    END;

    LOCAL PROCEDURE GetItem@19(ItemNo@1000 : Code[20]);
    BEGIN
      if ItemNo <> Item."No." then
        Item.GET(ItemNo);
    END;

    [External]
    PROCEDURE SetWhseRequest@10(NewWhseRequest@1000 : Record "Warehouse Request";SetHideDialog@1001 : Boolean);
    BEGIN
      WhseRequest := NewWhseRequest;
      HideDialog := SetHideDialog;
      LineCreated := false;
    END;

    [External]
    PROCEDURE CheckSourceDoc@12(NewWhseRequest@1000 : Record "Warehouse Request") : Boolean;
    VAR
      PurchLine@1001 : Record "Purchase Line";
      SalesLine@1002 : Record "Sales Line";
      TransferLine@1003 : Record "Transfer Line";
      ProdOrderComp@1004 : Record "Prod. Order Component";
      AssemblyLine@1005 : Record "Assembly Line";
    BEGIN
      WhseRequest := NewWhseRequest;
      if Location.RequireShipment(WhseRequest."Location Code") then
        exit(false);

      GetSourceDocHeader;
      CheckLineExist := true;
      case WhseRequest."Source Document" of
        WhseRequest."Source Document"::"Purchase Order":
          exit(SetFilterPurchLine(PurchLine,PurchHeader));
        WhseRequest."Source Document"::"Purchase Return Order":
          exit(SetFilterPurchLine(PurchLine,PurchHeader));
        WhseRequest."Source Document"::"Sales Order":
          exit(SetFilterSalesLine(SalesLine,SalesHeader));
        WhseRequest."Source Document"::"Sales Return Order":
          exit(SetFilterSalesLine(SalesLine,SalesHeader));
        WhseRequest."Source Document"::"Outbound Transfer":
          exit(SetFilterTransferLine(TransferLine,TransferHeader));
        WhseRequest."Source Document"::"Prod. Consumption":
          exit(SetFilterProductionLine(ProdOrderComp,ProdHeader));
        WhseRequest."Source Document"::"Assembly Consumption":
          exit(SetFilterAssemblyLine(AssemblyLine,AssemblyHeader));
      end;
    END;

    [External]
    PROCEDURE AutoCreatePickOrMove@17(VAR WhseActivHeaderNew@1000 : Record "Warehouse Activity Header");
    BEGIN
      WhseActivHeader := WhseActivHeaderNew;
      CheckLineExist := false;
      AutoCreation := true;
      GetLocation(WhseRequest."Location Code");

      case WhseRequest."Source Document" of
        WhseRequest."Source Document"::"Purchase Order":
          CreatePickOrMoveFromPurchase(PurchHeader);
        WhseRequest."Source Document"::"Purchase Return Order":
          CreatePickOrMoveFromPurchase(PurchHeader);
        WhseRequest."Source Document"::"Sales Order":
          CreatePickOrMoveFromSales(SalesHeader);
        WhseRequest."Source Document"::"Sales Return Order":
          CreatePickOrMoveFromSales(SalesHeader);
        WhseRequest."Source Document"::"Outbound Transfer":
          CreatePickOrMoveFromTransfer(TransferHeader);
        WhseRequest."Source Document"::"Prod. Consumption":
          CreatePickOrMoveFromProduction(ProdHeader);
        WhseRequest."Source Document"::"Assembly Consumption":
          CreatePickOrMoveFromAssembly(AssemblyHeader);
      end;

      if LineCreated then begin
        WhseActivHeader.MODIFY;
        WhseActivHeaderNew := WhseActivHeader;
      end;
    END;

    [External]
    PROCEDURE SetReportGlobals@23(PrintDocument2@1000 : Boolean;ShowError2@1001 : Boolean);
    BEGIN
      PrintDocument := PrintDocument2;
      ShowError := ShowError2;
    END;

    LOCAL PROCEDURE SetFilterReservEntry@31(VAR ReservationEntry@1001 : Record "Reservation Entry";WhseActivLine@1000 : Record "Warehouse Activity Line");
    BEGIN
      with ReservationEntry do begin
        RESET;
        SETCURRENTKEY("Source ID","Source Ref. No.","Source Type","Source Subtype","Source Batch Name","Source Prod. Order Line");
        SETRANGE("Source ID",WhseActivLine."Source No.");
        if WhseActivLine."Source Type" = DATABASE::"Prod. Order Component" then
          SETRANGE("Source Ref. No.",WhseActivLine."Source Subline No.")
        else
          SETRANGE("Source Ref. No.",WhseActivLine."Source Line No.");
        SETRANGE("Source Type",WhseActivLine."Source Type");
        SETRANGE("Source Subtype",WhseActivLine."Source Subtype");
        if WhseActivLine."Source Type" = DATABASE::"Prod. Order Component" then
          SETRANGE("Source Prod. Order Line",WhseActivLine."Source Line No.");
        SETRANGE(Positive,false);
      end;
    END;

    [External]
    PROCEDURE SetInvtMovement@3(InvtMovement@1000 : Boolean);
    BEGIN
      IsInvtMovement := InvtMovement;
    END;

    LOCAL PROCEDURE PickOrMoveAccordingToFEFO@50(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      GetLocation(LocationCode);

      exit(Location."Pick According to FEFO" and (SNRequired or LNRequired));
    END;

    LOCAL PROCEDURE UndefinedItemTrkg@58(VAR QtyToTrackBase@1000 : Decimal) : Boolean;
    BEGIN
      QtyToTrackBase := QtyToTrackBase + ItemTrackedQuantity('','');

      exit(QtyToTrackBase > 0);
    END;

    LOCAL PROCEDURE ItemTrackedQuantity@63(LotNo@1003 : Code[20];SerialNo@1002 : Code[20]) : Decimal;
    BEGIN
      with TempHandlingSpecification do begin
        RESET;
        if (LotNo = '') and (SerialNo = '') then
          if ISEMPTY then
            exit(0);

        if SerialNo <> '' then begin
          SETCURRENTKEY("Lot No.","Serial No.");
          SETRANGE("Serial No.",SerialNo);
          if ISEMPTY then
            exit(0);

          exit(1);
        end;

        if LotNo <> '' then begin
          SETCURRENTKEY("Lot No.","Serial No.");
          SETRANGE("Lot No.",LotNo);
          if ISEMPTY then
            exit(0);
        end;

        SETCURRENTKEY(
          "Source ID","Source Type","Source Subtype","Source Batch Name",
          "Source Prod. Order Line","Source Ref. No.");
        if LotNo <> '' then
          SETRANGE("Lot No.",LotNo);
        CALCSUMS("Qty. to Handle (Base)");
        exit("Qty. to Handle (Base)");
      end;
    END;

    LOCAL PROCEDURE CreateTempHandlingSpec@51(WhseActivLine@1004 : Record "Warehouse Activity Line";TotalQtytoPickBase@1002 : Decimal);
    VAR
      EntrySummary@1007 : Record "Entry Summary";
      WhseItemTrackingFEFO@1001 : Codeunit "Whse. Item Tracking FEFO";
      TotalAvailQtyToPickBase@1006 : Decimal;
      RemQtyToPickBase@1009 : Decimal;
      QtyToPickBase@1008 : Decimal;
      QtyTracked@1000 : Decimal;
    BEGIN
      if Location."Bin Mandatory" then
        if not IsItemOnBins(WhseActivLine) then
          exit;

      WhseItemTrackingFEFO.CreateEntrySummaryFEFO(Location,WhseActivLine."Item No.",WhseActivLine."Variant Code",true);
      if WhseItemTrackingFEFO.FindFirstEntrySummaryFEFO(EntrySummary) then begin
        InitTempHandlingSpec;
        RemQtyToPickBase := TotalQtytoPickBase;

        repeat
          if EntrySummary."Expiration Date" <> 0D then begin
            QtyTracked := ItemTrackedQuantity(EntrySummary."Lot No.",EntrySummary."Serial No.");
            if not ((EntrySummary."Serial No." <> '') and (QtyTracked > 0)) then begin
              if Location."Bin Mandatory" then
                TotalAvailQtyToPickBase :=
                  CalcQtyAvailToPickOnBins(WhseActivLine,EntrySummary."Lot No.",EntrySummary."Serial No.",RemQtyToPickBase)
              else
                TotalAvailQtyToPickBase := CalcInvtAvailability(WhseActivLine,EntrySummary."Lot No.",EntrySummary."Serial No.");

              TotalAvailQtyToPickBase := TotalAvailQtyToPickBase - QtyTracked;
              QtyToPickBase := 0;

              if TotalAvailQtyToPickBase > 0 then
                if TotalAvailQtyToPickBase >= RemQtyToPickBase then begin
                  QtyToPickBase := RemQtyToPickBase;
                  RemQtyToPickBase := 0
                end else begin
                  QtyToPickBase := TotalAvailQtyToPickBase;
                  RemQtyToPickBase := RemQtyToPickBase - QtyToPickBase;
                end;

              if QtyToPickBase > 0 then
                InsertTempHandlingSpec(
                  Location.Code,WhseActivLine."Item No.",WhseActivLine."Variant Code",EntrySummary,QtyToPickBase);
            end;
          end;
        until not WhseItemTrackingFEFO.FindNextEntrySummaryFEFO(EntrySummary) or (RemQtyToPickBase = 0);
      end;
      HasExpiredItems := WhseItemTrackingFEFO.GetHasExpiredItems;
      ExpiredItemMessageText := WhseItemTrackingFEFO.GetResultMessageForExpiredItem;
    END;

    LOCAL PROCEDURE InitTempHandlingSpec@29();
    BEGIN
      with TempHandlingSpecification do begin
        RESET;
        if FINDLAST then
          LastTempHandlingSpecNo := "Entry No."
        else
          LastTempHandlingSpecNo := 0;
      end;
    END;

    LOCAL PROCEDURE InsertTempHandlingSpec@59(LocationCode@1003 : Code[10];ItemNo@1002 : Code[20];VariantCode@1000 : Code[10];EntrySummary@1004 : Record "Entry Summary";QuantityBase@1001 : Decimal);
    BEGIN
      with TempHandlingSpecification do begin
        INIT;
        "Entry No." := LastTempHandlingSpecNo + 1;
        "Location Code" := LocationCode;
        "Item No." := ItemNo;
        "Variant Code" := VariantCode;
        "Lot No." := EntrySummary."Lot No.";
        "Serial No." := EntrySummary."Serial No.";
        "Expiration Date" := EntrySummary."Expiration Date";
        VALIDATE("Quantity (Base)",-QuantityBase);
        INSERT;
        LastTempHandlingSpecNo := "Entry No.";
      end;
    END;

    LOCAL PROCEDURE SetFilterInternalMomement@30(VAR InternalMovementLine@1000 : Record "Internal Movement Line";InternalMovementHeader@1001 : Record "Internal Movement Header") : Boolean;
    BEGIN
      with InternalMovementLine do begin
        SETRANGE("No.",InternalMovementHeader."No.");
        SETFILTER("Qty. (Base)",'>0');
        exit(FIND('-'));
      end;
    END;

    [External]
    PROCEDURE CreateInvtMvntWithoutSource@32(VAR InternalMovementHeader@1000 : Record "Internal Movement Header");
    VAR
      InternalMovementLine@1001 : Record "Internal Movement Line";
      NewWhseActivLine@1002 : Record "Warehouse Activity Line";
      RemQtyToPickBase@1003 : Decimal;
    BEGIN
      if not CONFIRM(Text004,false) then
        exit;

      IsInvtMovement := true;
      IsBlankInvtMovement := true;

      InternalMovementHeader.TESTFIELD("Location Code");

      with InternalMovementLine do begin
        if not SetFilterInternalMomement(InternalMovementLine,InternalMovementHeader) then begin
          if not HideDialog then
            MESSAGE(Text000);
          exit;
        end;

        // creating Inventory Movement Header
        CLEAR(WhseActivHeader);
        WhseActivHeader.Type := WhseActivHeader.Type::"Invt. Movement";
        WhseActivHeader.INSERT(true);
        WhseActivHeader.VALIDATE("Location Code",InternalMovementHeader."Location Code");
        WhseActivHeader.VALIDATE("Posting Date",InternalMovementHeader."Due Date");
        WhseActivHeader.VALIDATE("Assigned User ID",InternalMovementHeader."Assigned User ID");
        WhseActivHeader.VALIDATE("Assignment Date",InternalMovementHeader."Assignment Date");
        WhseActivHeader.VALIDATE("Assignment Time",InternalMovementHeader."Assignment Time");
        WhseActivHeader.MODIFY;

        FindNextLineNo;

        repeat
          NewWhseActivLine.INIT;
          NewWhseActivLine."Activity Type" := WhseActivHeader.Type;
          NewWhseActivLine."No." := WhseActivHeader."No.";
          TESTFIELD("Location Code");
          GetLocation("Location Code");
          if Location."Bin Mandatory" then
            NewWhseActivLine."Action Type" := NewWhseActivLine."Action Type"::Take;
          NewWhseActivLine."Location Code" := "Location Code";
          TESTFIELD("From Bin Code");
          FromBinCode := "From Bin Code";
          TESTFIELD("To Bin Code");
          NewWhseActivLine."Bin Code" := "To Bin Code";
          NewWhseActivLine."Item No." := "Item No.";
          NewWhseActivLine."Variant Code" := "Variant Code";
          NewWhseActivLine."Unit of Measure Code" := "Unit of Measure Code";
          NewWhseActivLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
          NewWhseActivLine.Description := Description;
          NewWhseActivLine."Due Date" := "Due Date";
          RemQtyToPickBase := "Qty. (Base)";

          PrepareItemTrackingFromWhseIT(InternalMovementLine);
          CreatePickOrMoveLine(NewWhseActivLine,RemQtyToPickBase,RemQtyToPickBase,false);
        until NEXT = 0;
      end;

      if NextLineNo = 10000 then
        ERROR(Text000);

      MoveWhseComments(InternalMovementHeader,WhseActivHeader."No.");
      InternalMovementHeader.DELETE(true);

      MESSAGE(Text003,WhseActivHeader.Type,WhseActivHeader."No.");
    END;

    LOCAL PROCEDURE PrepareItemTrackingFromWhseIT@27(InternalMovementLine@1000 : Record "Internal Movement Line");
    VAR
      WhseItemTrackingLine@1001 : Record "Whse. Item Tracking Line";
      EntryNo@1002 : Integer;
    BEGIN
      // function recopies warehouse item tracking into temporary item tracking table
      // when Invt. Movement is created from Internal Movement
      TempReservEntry.RESET;
      TempReservEntry.DELETEALL;

      WhseItemTrackingLine.SETCURRENTKEY("Source ID","Source Type","Source Subtype","Source Batch Name");
      WhseItemTrackingLine.SETRANGE("Source Type",DATABASE::"Internal Movement Line");
      WhseItemTrackingLine.SETRANGE("Source ID",InternalMovementLine."No.");
      WhseItemTrackingLine.SETRANGE("Source Ref. No.",InternalMovementLine."Line No.");

      if WhseItemTrackingLine.FIND('-') then
        repeat
          TempReservEntry.TRANSFERFIELDS(WhseItemTrackingLine);
          EntryNo += 1;
          TempReservEntry."Entry No." := EntryNo;
          TempReservEntry.Positive := false;
          TempReservEntry."Reservation Status" := TempReservEntry."Reservation Status"::Surplus;
          TempReservEntry.VALIDATE("Quantity (Base)",-TempReservEntry."Quantity (Base)");
          TempReservEntry.UpdateItemTracking;
          TempReservEntry.INSERT;
        until WhseItemTrackingLine.NEXT = 0;
    END;

    LOCAL PROCEDURE SynchronizeWhseItemTracking@33(VAR TrackingSpecification@1000 : Record "Tracking Specification");
    VAR
      WhseItemTrackingLine@1001 : Record "Whse. Item Tracking Line";
      EntryNo@1002 : Integer;
    BEGIN
      // documents which have defined item tracking - table 337 will have to synchronize these records with 6550 table for invt. movement
      if WhseItemTrackingLine.FINDLAST then
        EntryNo := WhseItemTrackingLine."Entry No.";
      EntryNo += 1;
      CLEAR(WhseItemTrackingLine);
      WhseItemTrackingLine.TRANSFERFIELDS(TrackingSpecification);
      WhseItemTrackingLine.VALIDATE("Quantity (Base)",ABS(WhseItemTrackingLine."Quantity (Base)"));
      WhseItemTrackingLine.VALIDATE("Qty. to Invoice (Base)",ABS(WhseItemTrackingLine."Qty. to Invoice (Base)"));
      WhseItemTrackingLine."Entry No." := EntryNo;
      WhseItemTrackingLine.INSERT;
    END;

    LOCAL PROCEDURE MoveWhseComments@34(InternalMovementHeader@1000 : Record "Internal Movement Header";InvtMovementNo@1003 : Code[20]);
    VAR
      WhseCommentLine@1001 : Record "Warehouse Comment Line";
      WhseCommentLine2@1002 : Record "Warehouse Comment Line";
    BEGIN
      WhseCommentLine.SETRANGE("Table Name",WhseCommentLine."Table Name"::"Internal Movement");
      WhseCommentLine.SETRANGE("No.",InternalMovementHeader."No.");
      WhseCommentLine.LOCKTABLE;

      if WhseCommentLine.FIND('-') then begin
        repeat
          WhseCommentLine2.INIT;
          WhseCommentLine2 := WhseCommentLine;
          WhseCommentLine2."Table Name" := WhseCommentLine2."Table Name"::"Whse. Activity Header";
          WhseCommentLine2.Type := WhseCommentLine.Type::"Invt. Movement";
          WhseCommentLine2."No." := InvtMovementNo;
          WhseCommentLine2.INSERT;
        until WhseCommentLine.NEXT = 0;
        WhseCommentLine.DELETEALL;
      end;
    END;

    [External]
    PROCEDURE GetExpiredItemMessage@35() : Text[100];
    BEGIN
      exit(ExpiredItemMessageText);
    END;

    LOCAL PROCEDURE PickStrictExpirationPosting@36(ItemNo@1000 : Code[20]) : Boolean;
    BEGIN
      exit(ItemTrackingMgt.StrictExpirationPosting(ItemNo) and (SNRequired or LNRequired));
    END;

    LOCAL PROCEDURE MakeHeader@42();
    BEGIN
      if AutoCreation and not LineCreated then begin
        WhseActivHeader."No." := '';
        WhseActivHeader.INSERT(true);
        UpdateWhseActivHeader(WhseRequest);
        NextLineNo := 10000;
        COMMIT;
      end;
    END;

    LOCAL PROCEDURE MakeLine@43(VAR NewWhseActivLine@1000 : Record "Warehouse Activity Line";TakeBinCode@1001 : Code[20];QtyToPickBase@1003 : Decimal;VAR RemQtyToPickBase@1004 : Decimal);
    VAR
      PlaceBinCode@1002 : Code[20];
    BEGIN
      PlaceBinCode := NewWhseActivLine."Bin Code";

      NewWhseActivLine."No." := WhseActivHeader."No.";
      NewWhseActivLine."Line No." := NextLineNo;
      if Location."Bin Mandatory" then begin
        NewWhseActivLine."Action Type" := NewWhseActivLine."Action Type"::Take;
        NewWhseActivLine."Bin Code" := TakeBinCode;
      end else
        NewWhseActivLine."Shelf No." := GetShelfNo(NewWhseActivLine."Item No.");
      NewWhseActivLine.Quantity := NewWhseActivLine.CalcQty(QtyToPickBase);
      NewWhseActivLine."Qty. (Base)" := QtyToPickBase;
      NewWhseActivLine."Qty. Outstanding" := NewWhseActivLine.Quantity;
      NewWhseActivLine."Qty. Outstanding (Base)" := NewWhseActivLine."Qty. (Base)";
      NewWhseActivLine."Qty. to Handle" := 0;
      NewWhseActivLine."Qty. to Handle (Base)" := 0;
      RemQtyToPickBase := RemQtyToPickBase - QtyToPickBase;
      NewWhseActivLine.INSERT;

      if Location."Bin Mandatory" and IsInvtMovement then begin
        // Place Action for inventory movement
        NextLineNo := NextLineNo + 10000;
        NewWhseActivLine."Line No." := NextLineNo;
        NewWhseActivLine."Action Type" := NewWhseActivLine."Action Type"::Place;
        NewWhseActivLine."Bin Code" := PlaceBinCode;
        NewWhseActivLine.INSERT;
      end;

      LineCreated := true;
      NextLineNo := NextLineNo + 10000;
    END;

    LOCAL PROCEDURE CreateATOPickLine@39(NewWhseActivLine@1002 : Record "Warehouse Activity Line";BinCode@1001 : Code[20];VAR RemQtyToPickBase@1000 : Decimal);
    VAR
      ATOSalesLine@1005 : Record "Sales Line";
      AsmHeader@1004 : Record "Assembly Header";
      AssemblySetup@1003 : Record "Assembly Setup";
      ReservationEntry@1010 : Record "Reservation Entry";
      TempTrackingSpecification@1009 : TEMPORARY Record "Tracking Specification";
      AssemblyHeaderReserve@1012 : Codeunit "Assembly Header-Reserve";
      QtyToAsmBase@1011 : Decimal;
      QtyToPickBase@1006 : Decimal;
      MovementsCreated@1007 : Integer;
      TotalMovementsCreated@1008 : Integer;
    BEGIN
      if (not IsInvtMovement) and
         WMSMgt.GetATOSalesLine(NewWhseActivLine."Source Type",
           NewWhseActivLine."Source Subtype",
           NewWhseActivLine."Source No.",
           NewWhseActivLine."Source Line No.",
           ATOSalesLine)
      then begin
        ATOSalesLine.AsmToOrderExists(AsmHeader);
        if NewWhseActivLine.TrackingExists then begin
          AssemblyHeaderReserve.FilterReservFor(ReservationEntry,AsmHeader);
          ReservationEntry.SetTrackingFilter(NewWhseActivLine."Serial No.",NewWhseActivLine."Lot No.");
          ReservationEntry.SETRANGE(Positive,true);
          if ItemTrackingMgt.SumUpItemTracking(ReservationEntry,TempTrackingSpecification,true,true) then
            QtyToAsmBase := ABS(TempTrackingSpecification."Qty. to Handle (Base)");
        end else
          QtyToAsmBase := ATOSalesLine.QtyToAsmBaseOnATO;
        QtyToPickBase := QtyToAsmBase -
          WMSMgt.CalcQtyBaseOnATOInvtPick(ATOSalesLine,NewWhseActivLine."Serial No.",NewWhseActivLine."Lot No.");
        if QtyToPickBase > 0 then begin
          MakeHeader;
          if Location."Bin Mandatory" and (BinCode = '') then
            ATOSalesLine.GetATOBin(Location,BinCode);
          NewWhseActivLine."Assemble to Order" := true;
          MakeLine(NewWhseActivLine,BinCode,QtyToPickBase,RemQtyToPickBase);

          AssemblySetup.GET;
          if AssemblySetup."Create Movements Automatically" then begin
            AsmHeader.CreateInvtMovement(true,PrintDocument,ShowError,MovementsCreated,TotalMovementsCreated);
            ATOInvtMovementsCreated += MovementsCreated;
            TotalATOInvtMovementsToBeCreated += TotalMovementsCreated;
          end;
        end;
      end;
    END;

    [External]
    PROCEDURE GetATOMovementsCounters@40(VAR MovementsCreated@1000 : Integer;VAR TotalMovementsCreated@1001 : Integer);
    BEGIN
      MovementsCreated := ATOInvtMovementsCreated;
      TotalMovementsCreated := TotalATOInvtMovementsToBeCreated;
    END;

    LOCAL PROCEDURE Minimum@41(a@1000 : Decimal;b@1001 : Decimal) : Decimal;
    BEGIN
      if a < b then
        exit(a);

      exit(b);
    END;

    LOCAL PROCEDURE CalcQtyAvailToPickOnBins@45(WhseActivLine@1000 : Record "Warehouse Activity Line";LotNo@1002 : Code[20];SerialNo@1003 : Code[20];RemQtyToPickBase@1004 : Decimal) : Decimal;
    VAR
      BinContent@1005 : Record "Bin Content";
      TotalAvailQtyToPickBase@1006 : Decimal;
    BEGIN
      TotalAvailQtyToPickBase := 0;

      with BinContent do begin
        SETRANGE("Location Code",WhseActivLine."Location Code");
        SETRANGE("Item No.",WhseActivLine."Item No.");
        SETRANGE("Variant Code",WhseActivLine."Variant Code");
        SETRANGE("Serial No. Filter",SerialNo);
        SETRANGE("Lot No. Filter",LotNo);
        if FIND('-') then
          repeat
            TotalAvailQtyToPickBase += CalcQtyAvailToPick(0);
          until (NEXT = 0) or (TotalAvailQtyToPickBase >= RemQtyToPickBase);
      end;

      exit(TotalAvailQtyToPickBase);
    END;

    LOCAL PROCEDURE IsItemOnBins@44(WhseActivLine@1000 : Record "Warehouse Activity Line") : Boolean;
    VAR
      BinContent@1003 : Record "Bin Content";
    BEGIN
      with BinContent do begin
        SETRANGE("Location Code",WhseActivLine."Location Code");
        SETRANGE("Item No.",WhseActivLine."Item No.");
        SETRANGE("Variant Code",WhseActivLine."Variant Code");
        exit(not ISEMPTY);
      end;
    END;

    BEGIN
    END.
  }
}

