OBJECT Table 5071 Campaign
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields="No.",Description;
    OnInsert=BEGIN
               if "No." = '' then begin
                 RMSetup.GET;
                 RMSetup.TESTFIELD("Campaign Nos.");
                 NoSeriesMgt.InitSeries(RMSetup."Campaign Nos.",xRec."No. Series",0D,"No.","No. Series");
               end;

               if "Salesperson Code" = '' then
                 SetDefaultSalesperson;

               DimMgt.UpdateDefaultDim(
                 DATABASE::Campaign,"No.",
                 "Global Dimension 1 Code","Global Dimension 2 Code");
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;

               if ("Starting Date" <> xRec."Starting Date") or
                  ("Ending Date" <> xRec."Ending Date")
               then
                 UpdateDates;
             END;

    OnDelete=VAR
               SalesPrice@1001 : Record "Sales Price";
               SalesLineDisc@1000 : Record "Sales Line Discount";
             BEGIN
               DimMgt.DeleteDefaultDim(DATABASE::Campaign,"No.");

               RMCommentLine.SETRANGE("Table Name",RMCommentLine."Table Name"::Campaign);
               RMCommentLine.SETRANGE("No.","No.");
               RMCommentLine.DELETEALL;

               CampaignEntry.SETCURRENTKEY("Campaign No.");
               CampaignEntry.SETRANGE("Campaign No.","No.");
               CampaignEntry.DELETEALL;

               CampaignMgmt.DeactivateCampaign(Rec,false);

               SalesPrice.SETRANGE("Sales Type",SalesPrice."Sales Type"::Campaign);
               SalesPrice.SETRANGE("Sales Code","No.");
               SalesPrice.DELETEALL;

               SalesLineDisc.SETRANGE("Sales Type",SalesLineDisc."Sales Type"::Campaign);
               SalesLineDisc.SETRANGE("Sales Code","No.");
               SalesLineDisc.DELETEALL;
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
             END;

    CaptionML=ENU=Campaign;
    LookupPageID="Campaign List";
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                if "No." <> xRec."No." then begin
                                                                  RMSetup.GET;
                                                                  NoSeriesMgt.TestManual(RMSetup."Campaign Nos.");
                                                                  "No. Series" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 2   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 3   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                if ("Starting Date" > "Ending Date") and ("Ending Date" > 0D) then
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));
                                                              END;

                                                   CaptionML=ENU=Starting Date }
    { 4   ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                if ("Ending Date" < "Starting Date") and ("Ending Date" > 0D) then
                                                                  ERROR(Text001,FIELDCAPTION("Ending Date"),FIELDCAPTION("Starting Date"));
                                                              END;

                                                   CaptionML=ENU=Ending Date }
    { 5   ;   ;Salesperson Code    ;Code20        ;TableRelation="Salesperson/Purchaser";
                                                   CaptionML=ENU=Salesperson Code }
    { 6   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Rlshp. Mgt. Comment Line" WHERE ("Table Name"=CONST(Campaign),
                                                                                                       "No."=FIELD("No.")));
                                                   CaptionML=ENU=Comment;
                                                   Editable=false }
    { 7   ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=false }
    { 8   ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=false }
    { 9   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 1 Code;
                                                   CaptionClass='1,1,1' }
    { 10  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 2 Code;
                                                   CaptionClass='1,1,2' }
    { 11  ;   ;Status Code         ;Code10        ;TableRelation="Campaign Status";
                                                   CaptionML=ENU=Status Code }
    { 12  ;   ;Target Contacts Contacted;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Interaction Log Entry" WHERE ("Campaign No."=FIELD("No."),
                                                                                                    "Campaign Target"=CONST(true),
                                                                                                    Canceled=CONST(false),
                                                                                                    Date=FIELD("Date Filter"),
                                                                                                    Postponed=CONST(false)));
                                                   CaptionML=ENU=Target Contacts Contacted;
                                                   Editable=false }
    { 13  ;   ;Contacts Responded  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Interaction Log Entry" WHERE ("Campaign No."=FIELD("No."),
                                                                                                    "Campaign Response"=CONST(true),
                                                                                                    Canceled=CONST(false),
                                                                                                    Date=FIELD("Date Filter"),
                                                                                                    Postponed=CONST(false)));
                                                   CaptionML=ENU=Contacts Responded;
                                                   Editable=false }
    { 14  ;   ;Duration (Min.)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Interaction Log Entry"."Duration (Min.)" WHERE ("Campaign No."=FIELD("No."),
                                                                                                                    Canceled=CONST(false),
                                                                                                                    Date=FIELD("Date Filter"),
                                                                                                                    Postponed=CONST(false)));
                                                   CaptionML=ENU=Duration (Min.);
                                                   DecimalPlaces=0:0;
                                                   Editable=false }
    { 16  ;   ;Cost (LCY)          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Interaction Log Entry"."Cost (LCY)" WHERE ("Campaign No."=FIELD("No."),
                                                                                                               Canceled=CONST(false),
                                                                                                               Date=FIELD("Date Filter"),
                                                                                                               Postponed=CONST(false)));
                                                   CaptionML=ENU=Cost (LCY);
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 17  ;   ;No. of Opportunities;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Opportunity Entry" WHERE ("Campaign No."=FIELD("No."),
                                                                                                Active=CONST(true)));
                                                   CaptionML=ENU=No. of Opportunities;
                                                   Editable=false }
    { 18  ;   ;Estimated Value (LCY);Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Opportunity Entry"."Estimated Value (LCY)" WHERE ("Campaign No."=FIELD("No."),
                                                                                                                      Active=CONST(true)));
                                                   CaptionML=ENU=Estimated Value (LCY);
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 19  ;   ;Calcd. Current Value (LCY);Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Opportunity Entry"."Calcd. Current Value (LCY)" WHERE ("Campaign No."=FIELD("No."),
                                                                                                                           Active=CONST(true)));
                                                   CaptionML=ENU=Calcd. Current Value (LCY);
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 20  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
    { 23  ;   ;Action Taken Filter ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Action Taken Filter;
                                                   OptionCaptionML=ENU=" ,Next,Previous,Updated,Jumped,Won,Lost";
                                                   OptionString=" ",Next,Previous,Updated,Jumped,Won,Lost }
    { 24  ;   ;Sales Cycle Filter  ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Sales Cycle";
                                                   CaptionML=ENU=Sales Cycle Filter }
    { 25  ;   ;Sales Cycle Stage Filter;Integer   ;FieldClass=FlowFilter;
                                                   TableRelation="Sales Cycle Stage".Stage WHERE ("Sales Cycle Code"=FIELD("Sales Cycle Filter"));
                                                   CaptionML=ENU=Sales Cycle Stage Filter }
    { 26  ;   ;Probability % Filter;Decimal       ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Probability % Filter;
                                                   DecimalPlaces=1:1;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 27  ;   ;Completed % Filter  ;Decimal       ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Completed % Filter;
                                                   DecimalPlaces=1:1;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 28  ;   ;Contact Filter      ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Contact;
                                                   CaptionML=ENU=Contact Filter }
    { 29  ;   ;Contact Company Filter;Code20      ;FieldClass=FlowFilter;
                                                   TableRelation=Contact WHERE (Type=CONST(Company));
                                                   CaptionML=ENU=Contact Company Filter }
    { 30  ;   ;Estimated Value Filter;Decimal     ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Estimated Value Filter;
                                                   AutoFormatType=1 }
    { 31  ;   ;Calcd. Current Value Filter;Decimal;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Calcd. Current Value Filter;
                                                   AutoFormatType=1 }
    { 32  ;   ;Chances of Success % Filter;Decimal;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Chances of Success % Filter;
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 33  ;   ;Task Status Filter  ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Task Status Filter;
                                                   OptionCaptionML=ENU=Not Started,In Progress,Completed,Waiting,Postponed;
                                                   OptionString="Not Started","In Progress",Completed,Waiting,Postponed }
    { 34  ;   ;Task Closed Filter  ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Task Closed Filter }
    { 35  ;   ;Priority Filter     ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Priority Filter;
                                                   OptionCaptionML=ENU=Low,Normal,High;
                                                   OptionString=Low,Normal,High }
    { 36  ;   ;Team Filter         ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Team;
                                                   CaptionML=ENU=Team Filter }
    { 37  ;   ;Salesperson Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Salesperson/Purchaser";
                                                   CaptionML=ENU=Salesperson Filter }
    { 38  ;   ;Opportunity Entry Exists;Boolean   ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Opportunity Entry" WHERE ("Campaign No."=FIELD("No."),
                                                                                                Active=CONST(true),
                                                                                                "Salesperson Code"=FIELD("Salesperson Filter"),
                                                                                                "Contact No."=FIELD("Contact Filter"),
                                                                                                "Contact Company No."=FIELD("Contact Company Filter"),
                                                                                                "Sales Cycle Code"=FIELD("Sales Cycle Filter"),
                                                                                                "Sales Cycle Stage"=FIELD("Sales Cycle Stage Filter"),
                                                                                                "Action Taken"=FIELD("Action Taken Filter"),
                                                                                                "Estimated Value (LCY)"=FIELD("Estimated Value Filter"),
                                                                                                "Calcd. Current Value (LCY)"=FIELD("Calcd. Current Value Filter"),
                                                                                                "Completed %"=FIELD("Completed % Filter"),
                                                                                                "Chances of Success %"=FIELD("Chances of Success % Filter"),
                                                                                                "Probability %"=FIELD("Probability % Filter"),
                                                                                                "Estimated Close Date"=FIELD("Date Filter"),
                                                                                                "Close Opportunity Code"=FIELD("Close Opportunity Filter")));
                                                   CaptionML=ENU=Opportunity Entry Exists;
                                                   Editable=false }
    { 39  ;   ;Task Entry Exists   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("To-do" WHERE ("Campaign No."=FIELD("No."),
                                                                                    "Contact No."=FIELD("Contact Filter"),
                                                                                    "Contact Company No."=FIELD("Contact Company Filter"),
                                                                                    "Salesperson Code"=FIELD("Salesperson Filter"),
                                                                                    "Team Code"=FIELD("Team Filter"),
                                                                                    Status=FIELD("Task Status Filter"),
                                                                                    Closed=FIELD("Task Closed Filter"),
                                                                                    Priority=FIELD("Priority Filter"),
                                                                                    Date=FIELD("Date Filter")));
                                                   CaptionML=ENU=Task Entry Exists;
                                                   Editable=false }
    { 40  ;   ;Close Opportunity Filter;Code10    ;FieldClass=FlowFilter;
                                                   TableRelation="Close Opportunity Code";
                                                   CaptionML=ENU=Close Opportunity Filter }
    { 41  ;   ;Activated           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Campaign Target Group" WHERE ("Campaign No."=FIELD("No.")));
                                                   CaptionML=ENU=Activated;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"No."                                   ;Clustered=true }
    {    ;"Salesperson Code"                       }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;"No.",Description,"Starting Date","Ending Date","Status Code" }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be before %2.';
      Text001@1001 : TextConst 'ENU=%1 must be after %2.';
      RMSetup@1002 : Record "Marketing Setup";
      Campaign@1003 : Record Campaign;
      RMCommentLine@1004 : Record "Rlshp. Mgt. Comment Line";
      CampaignEntry@1005 : Record "Campaign Entry";
      NoSeriesMgt@1006 : Codeunit NoSeriesManagement;
      DimMgt@1007 : Codeunit DimensionManagement;
      CampaignMgmt@1008 : Codeunit "Campaign Target Group Mgt";

    [External]
    PROCEDURE AssistEdit@2(OldCampaign@1000 : Record Campaign) : Boolean;
    BEGIN
      with Campaign do begin
        Campaign := Rec;
        RMSetup.GET;
        RMSetup.TESTFIELD("Campaign Nos.");
        if NoSeriesMgt.SelectSeries(RMSetup."Campaign Nos.",OldCampaign."No. Series","No. Series") then begin
          RMSetup.GET;
          RMSetup.TESTFIELD("Campaign Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := Campaign;
          exit(true);
        end;
      end;
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::Campaign,"No.",FieldNumber,ShortcutDimCode);
      MODIFY;
    END;

    LOCAL PROCEDURE UpdateDates@1();
    VAR
      SalesPrice@1002 : Record "Sales Price";
      SalesLineDisc@1001 : Record "Sales Line Discount";
      SalesPrice2@1003 : Record "Sales Price";
      SalesLineDisc2@1004 : Record "Sales Line Discount";
    BEGIN
      MODIFY;
      SalesPrice.SETRANGE("Sales Type",SalesPrice."Sales Type"::Campaign);
      SalesPrice.SETRANGE("Sales Code","No.");
      SalesPrice.LOCKTABLE;
      if SalesPrice.FIND('-') then
        repeat
          SalesPrice2 := SalesPrice;
          SalesPrice.DELETE;
          SalesPrice2.VALIDATE("Starting Date","Starting Date");
          SalesPrice2.INSERT(true);
          SalesPrice2.VALIDATE("Ending Date","Ending Date");
          SalesPrice2.MODIFY;
        until SalesPrice.NEXT = 0;

      SalesLineDisc.SETRANGE("Sales Type",SalesLineDisc."Sales Type"::Campaign);
      SalesLineDisc.SETRANGE("Sales Code","No.");
      SalesLineDisc.LOCKTABLE;
      if SalesLineDisc.FIND('-') then
        repeat
          SalesLineDisc2 := SalesLineDisc;
          SalesLineDisc.DELETE;
          SalesLineDisc2.VALIDATE("Starting Date","Starting Date");
          SalesLineDisc2.INSERT(true);
          SalesLineDisc2.VALIDATE("Ending Date","Ending Date");
          SalesLineDisc2.MODIFY;
        until SalesLineDisc.NEXT = 0;
    END;

    LOCAL PROCEDURE SetDefaultSalesperson@124();
    VAR
      UserSetup@1000 : Record "User Setup";
    BEGIN
      if not UserSetup.GET(USERID) then
        exit;

      if UserSetup."Salespers./Purch. Code" <> '' then
        VALIDATE("Salesperson Code",UserSetup."Salespers./Purch. Code");
    END;

    BEGIN
    END.
  }
}

