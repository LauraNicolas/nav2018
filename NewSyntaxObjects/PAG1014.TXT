OBJECT Page 1014 Job Transfer Job Planning Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Job Transfer Job Planning Line;
    SaveValues=true;
    SourceTable="Job Journal Template";
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 InitializeValues;
               END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General }

    { 3   ;2   ;Field     ;
                Name=PostingDate;
                CaptionML=ENU=Posting Date;
                ToolTipML=ENU=Specifies the posting date for the document.;
                ApplicationArea=#Jobs;
                SourceExpr=PostingDate;
                ImplicitType=Date }

    { 4   ;2   ;Field     ;
                Name=JobJournalTemplateName;
                Lookup=true;
                CaptionML=ENU=Job Journal Template;
                ToolTipML=ENU=Specifies the journal template that is used for the job journal.;
                ApplicationArea=#Jobs;
                SourceExpr=JobJournalTemplateName;
                TableRelation="Job Journal Template".Name WHERE ("Page ID"=CONST(201),
                                                                 Recurring=CONST(false));
                OnLookup=BEGIN
                           SelectJobJournalTemplate;
                         END;

                ImplicitType=Code10 }

    { 5   ;2   ;Field     ;
                Name=JobJournalBatchName;
                Lookup=true;
                CaptionML=ENU=Job Journal Batch;
                ToolTipML=ENU=Specifies the journal batch that is used for the job journal.;
                ApplicationArea=#Jobs;
                SourceExpr=JobJournalBatchName;
                TableRelation="Job Journal Batch".Name WHERE ("Journal Template Name"=FIELD(Name));
                OnLookup=BEGIN
                           SelectJobJournalBatch;
                         END;

                ImplicitType=Code10 }

  }
  CODE
  {
    VAR
      JobJournalTemplateName@1002 : Code[10];
      JobJournalBatchName@1003 : Code[10];
      PostingDate@1000 : Date;

    [External]
    PROCEDURE InitializeValues@14();
    VAR
      JobJnlTemplate@1000 : Record "Job Journal Template";
      JobJnlBatch@1001 : Record "Job Journal Batch";
    BEGIN
      PostingDate := WORKDATE;

      JobJnlTemplate.SETRANGE("Page ID",PAGE::"Job Journal");
      JobJnlTemplate.SETRANGE(Recurring,false);

      if JobJnlTemplate.COUNT = 1 then begin
        JobJnlTemplate.FINDFIRST;
        JobJournalTemplateName := JobJnlTemplate.Name;

        JobJnlBatch.SETRANGE("Journal Template Name",JobJournalTemplateName);

        if JobJnlBatch.COUNT = 1 then begin
          JobJnlBatch.FINDFIRST;
          JobJournalBatchName := JobJnlBatch.Name;
        end;
      end;
    END;

    LOCAL PROCEDURE SelectJobJournalTemplate@3();
    VAR
      JobJnlTemplate@1001 : Record "Job Journal Template";
      JobJnlBatch@1000 : Record "Job Journal Batch";
    BEGIN
      JobJnlTemplate.SETRANGE("Page ID",PAGE::"Job Journal");
      JobJnlTemplate.SETRANGE(Recurring,false);

      if PAGE.RUNMODAL(0,JobJnlTemplate) = ACTION::LookupOK then begin
        JobJournalTemplateName := JobJnlTemplate.Name;

        JobJnlBatch.SETRANGE("Journal Template Name",JobJournalTemplateName);

        if JobJnlBatch.COUNT = 1 then begin
          JobJnlBatch.FINDFIRST;
          JobJournalBatchName := JobJnlBatch.Name;
        end else
          JobJournalBatchName := '';
      end;
    END;

    LOCAL PROCEDURE SelectJobJournalBatch@5();
    VAR
      JobJnlBatch@1000 : Record "Job Journal Batch";
    BEGIN
      JobJnlBatch.SETRANGE("Journal Template Name",JobJournalTemplateName);

      if PAGE.RUNMODAL(0,JobJnlBatch) = ACTION::LookupOK then
        JobJournalBatchName := JobJnlBatch.Name;
    END;

    [External]
    PROCEDURE GetPostingDate@2() : Date;
    BEGIN
      exit(PostingDate);
    END;

    [External]
    PROCEDURE GetJobJournalTemplateName@1() : Code[10];
    BEGIN
      exit(JobJournalTemplateName);
    END;

    [External]
    PROCEDURE GetJobJournalBatchName@4() : Code[10];
    BEGIN
      exit(JobJournalBatchName);
    END;

    BEGIN
    END.
  }
}

