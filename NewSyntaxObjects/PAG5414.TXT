OBJECT Page 5414 Item Availability by Variant
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Item Availability by Variant;
    InsertAllowed=false;
    DeleteAllowed=false;
    ModifyAllowed=false;
    LinksAllowed=false;
    SourceTable=Item;
    DataCaptionFields="No.",Description;
    PageType=ListPlus;
    RefreshOnActivate=true;
    OnOpenPage=BEGIN
                 FindPeriod('');
               END;

    OnClosePage=VAR
                  ItemVariant@1000 : Record "Item Variant";
                BEGIN
                  CurrPage.ItemAvailLocLines.PAGE.GETRECORD(ItemVariant);
                  LastVariant := ItemVariant.Code;
                END;

    OnAfterGetRecord=BEGIN
                       SETRANGE("Drop Shipment Filter",false);
                       FindPeriod('');
                       UpdateSubForm;
                     END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      Name=ActionContainer1900000003;
                      ActionContainerType=RelatedInformation }
      { 10      ;1   ;ActionGroup;
                      Name=&Item;
                      CaptionML=ENU=&Item;
                      Image=Item }
      { 11      ;2   ;ActionGroup;
                      Name=&Item Availability by;
                      CaptionML=ENU=&Item Availability by;
                      Image=ItemAvailability }
      { 3       ;3   ;Action    ;
                      Name=<Action4>;
                      CaptionML=ENU=Event;
                      ToolTipML=ENU=View how the actual and the projected available balance of an item will develop over time according to supply and demand events.;
                      ApplicationArea=#Advanced;
                      Image=Event;
                      OnAction=BEGIN
                                 ItemAvailFormsMgt.ShowItemAvailFromItem(Rec,ItemAvailFormsMgt.ByEvent);
                               END;
                                }
      { 14      ;3   ;Action    ;
                      Name=Period;
                      CaptionML=ENU=Period;
                      ToolTipML=ENU=Show the projected quantity of the item over time according to time periods, such as day, week, or month.;
                      ApplicationArea=#Advanced;
                      RunObject=Page "Item Availability by Periods";
                      RunPageLink="No."=FIELD("No."),
                                  "Global Dimension 1 Filter"=FIELD("Global Dimension 1 Filter"),
                                  "Global Dimension 2 Filter"=FIELD("Global Dimension 2 Filter"),
                                  "Location Filter"=FIELD("Location Filter"),
                                  "Drop Shipment Filter"=FIELD("Drop Shipment Filter"),
                                  "Variant Filter"=FIELD("Variant Filter");
                      Image=Period }
      { 16      ;3   ;Action    ;
                      Name=Location;
                      CaptionML=ENU=Location;
                      ToolTipML=ENU=View the actual and projected quantity of the item per location.;
                      ApplicationArea=#Advanced;
                      RunObject=Page "Item Availability by Location";
                      RunPageLink="No."=FIELD("No."),
                                  "Global Dimension 1 Filter"=FIELD("Global Dimension 1 Filter"),
                                  "Global Dimension 2 Filter"=FIELD("Global Dimension 2 Filter"),
                                  "Location Filter"=FIELD("Location Filter"),
                                  "Drop Shipment Filter"=FIELD("Drop Shipment Filter"),
                                  "Variant Filter"=FIELD("Variant Filter");
                      Image=Warehouse }
      { 5       ;3   ;Action    ;
                      Name=BOM Level;
                      CaptionML=ENU=BOM Level;
                      ToolTipML=ENU=View availability figures for items on bills of materials that show how many units of a parent item you can make based on the availability of child items.;
                      ApplicationArea=#Advanced;
                      Image=BOMLevel;
                      OnAction=BEGIN
                                 ItemAvailFormsMgt.ShowItemAvailFromItem(Rec,ItemAvailFormsMgt.ByBOM);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      Name=ActionContainer1900000004;
                      ActionContainerType=ActionItems }
      { 27      ;1   ;Action    ;
                      Name=PreviousPeriod;
                      CaptionML=ENU=Previous Period;
                      ToolTipML=ENU=Show the information based on the previous period. If you set the View by field to Day, the date filter changes to the day before.;
                      ApplicationArea=#Advanced;
                      Promoted=true;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      PromotedOnly=true;
                      OnAction=BEGIN
                                 FindPeriod('<=');
                                 UpdateSubForm;
                               END;
                                }
      { 26      ;1   ;Action    ;
                      Name=NextPeriod;
                      CaptionML=ENU=Next Period;
                      ToolTipML=ENU=Show the information based on the next period. If you set the View by field to Day, the date filter changes to the day before.;
                      ApplicationArea=#Advanced;
                      Promoted=true;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      PromotedOnly=true;
                      OnAction=BEGIN
                                 FindPeriod('>=');
                                 UpdateSubForm;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Options;
                CaptionML=ENU=Options }

    { 33  ;2   ;Field     ;
                Name=PeriodType;
                CaptionML=ENU=View by;
                ToolTipML=ENU=Specifies by which period amounts are displayed.;
                OptionCaptionML=ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                ApplicationArea=#Advanced;
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             FindPeriod('');
                             UpdateSubForm;
                           END;

                ImplicitType=Option }

    { 34  ;2   ;Field     ;
                Name=AmountType;
                CaptionML=ENU=View as;
                ToolTipML=ENU=Specifies how amounts are displayed. Net Change: The net change in the balance for the selected period. Balance at Date: The balance as of the last day in the selected period.;
                OptionCaptionML=ENU=Net Change,Balance at Date;
                ApplicationArea=#Advanced;
                SourceExpr=AmountType;
                OnValidate=BEGIN
                             FindPeriod('');
                             UpdateSubForm;
                           END;

                ImplicitType=Option }

    { 4   ;2   ;Field     ;
                Name=DateFilter;
                CaptionML=ENU=Date Filter;
                ToolTipML=ENU=Specifies the dates that will be used to filter the amounts in the window.;
                ApplicationArea=#Advanced;
                SourceExpr=DateFilter;
                Editable=FALSE;
                ImplicitType=Text }

    { 1   ;1   ;Part      ;
                Name=ItemAvailLocLines;
                ApplicationArea=#Advanced;
                SubPageLink="Item No."=FIELD("No.");
                PagePartID="Item Avail. by Variant Lines";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Calendar@1000 : Record Date;
      ItemAvailFormsMgt@1004 : Codeunit "Item Availability Forms Mgt";
      PeriodType@1001 : 'Day,Week,Month,Quarter,Year,"Accounting Period"';
      AmountType@1002 : '"Net Change","Balance at Date"';
      LastVariant@1003 : Code[10];
      DateFilter@1005 : Text;

    LOCAL PROCEDURE FindPeriod@2(SearchText@1000 : Text[3]);
    VAR
      PeriodFormMgt@1001 : Codeunit PeriodFormManagement;
    BEGIN
      if GETFILTER("Date Filter") <> '' then begin
        Calendar.SETFILTER("Period Start",GETFILTER("Date Filter"));
        if not PeriodFormMgt.FindDate('+',Calendar,PeriodType) then
          PeriodFormMgt.FindDate('+',Calendar,PeriodType::Day);
        Calendar.SETRANGE("Period Start");
      end;
      PeriodFormMgt.FindDate(SearchText,Calendar,PeriodType);
      if AmountType = AmountType::"Net Change" then begin
        SETRANGE("Date Filter",Calendar."Period Start",Calendar."Period End");
        if GETRANGEMIN("Date Filter") = GETRANGEMAX("Date Filter") then
          SETRANGE("Date Filter",GETRANGEMIN("Date Filter"));
      end else
        SETRANGE("Date Filter",0D,Calendar."Period End");
      DateFilter := GETFILTER("Date Filter");
    END;

    LOCAL PROCEDURE UpdateSubForm@1();
    BEGIN
      CurrPage.ItemAvailLocLines.PAGE.Set(Rec,AmountType);
    END;

    [External]
    PROCEDURE GetLastVariant@3() : Code[10];
    BEGIN
      exit(LastVariant);
    END;

    BEGIN
    END.
  }
}

