OBJECT Table 9651 Report Layout Selection
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataPerCompany=false;
    OnInsert=BEGIN
               TESTFIELD("Report ID");
               if "Company Name" = '' then
                 "Company Name" := COMPANYNAME;
             END;

    CaptionML=ENU=Report Layout Selection;
  }
  FIELDS
  {
    { 1   ;   ;Report ID           ;Integer       ;CaptionML=ENU=Report ID }
    { 2   ;   ;Report Name         ;Text80        ;CaptionML=ENU=Report Name;
                                                   Editable=false }
    { 3   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company Name }
    { 4   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("Report ID");
                                                                CALCFIELDS("Report Caption");
                                                                "Report Name" := "Report Caption";
                                                                case Type of
                                                                  Type::"RDLC (built-in)":
                                                                    begin
                                                                      if not HasRdlcLayout("Report ID") then
                                                                        ERROR(NoRdlcLayoutErr,"Report Name");
                                                                      "Custom Report Layout Code" := '';
                                                                    end;
                                                                  Type::"Word (built-in)":
                                                                    begin
                                                                      if not HasWordLayout("Report ID") then
                                                                        ERROR(NoWordLayoutErr,"Report Name");
                                                                      "Custom Report Layout Code" := '';
                                                                    end;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=RDLC (built-in),Word (built-in),Custom Layout;
                                                   OptionString="RDLC (built-in)","Word (built-in)","Custom Layout" }
    { 6   ;   ;Custom Report Layout Code;Code20   ;TableRelation="Custom Report Layout" WHERE ("Report ID"=FIELD("Report ID"));
                                                   OnValidate=BEGIN
                                                                if "Custom Report Layout Code" = '' then
                                                                  Type := GetDefaultType("Report ID")
                                                                else
                                                                  Type := Type::"Custom Layout";
                                                              END;

                                                   CaptionML=ENU=Custom Report Layout Code }
    { 7   ;   ;Report Layout Description;Text80   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (Code=FIELD("Custom Report Layout Code")));
                                                   CaptionML=ENU=Report Layout Description }
    { 8   ;   ;Report Caption      ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Report ID")));
                                                   CaptionML=ENU=Report Caption;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Report ID","Company Name"              ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoRdlcLayoutErr@1000 : TextConst '@@@="%1=a report name";ENU=Report ''%1'' has no built-in RDLC layout.';
      NoWordLayoutErr@1001 : TextConst '@@@="%1=a report name";ENU=Report ''%1'' has no built-in Word layout.';

    [External]
    PROCEDURE GetDefaultType@2(ReportID@1000 : Integer) : Integer;
    BEGIN
      if REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::Word then
        exit(Type::"Word (built-in)");
      exit(Type::"RDLC (built-in)");
    END;

    [External]
    PROCEDURE IsProcessingOnly@20(ReportID@1000 : Integer) : Boolean;
    BEGIN
      exit(REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::None);
    END;

    LOCAL PROCEDURE HasRdlcLayout@21(ReportID@1001 : Integer) : Boolean;
    VAR
      InStr@1000 : InStream;
    BEGIN
      if REPORT.RDLCLAYOUT(ReportID,InStr) then
        exit(not InStr.EOS);
      exit(false);
    END;

    [External]
    PROCEDURE HasWordLayout@23(ReportID@1001 : Integer) : Boolean;
    VAR
      InStr@1000 : InStream;
    BEGIN
      if REPORT.WORDLAYOUT(ReportID,InStr) then
        exit(not InStr.EOS);
      exit(false);
    END;

    [External]
    PROCEDURE HasCustomLayout@1(ReportID@1000 : Integer) : Integer;
    VAR
      CustomReportLayout@1001 : Record "Custom Report Layout";
    BEGIN
      // Temporarily selected layout for Design-time report execution?
      if GetTempLayoutSelected <> '' then
        if CustomReportLayout.GET(GetTempLayoutSelected) then begin
          if CustomReportLayout.Type = CustomReportLayout.Type::RDLC then
            exit(1);
          exit(2);
        end;

      // Normal selection
      if not GET(ReportID,COMPANYNAME) then
        exit(0);
      case Type of
        Type::"RDLC (built-in)":
          begin
            if REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::RDLC then
              exit(0);
            exit(1);
          end;
        Type::"Word (built-in)":
          exit(2);
        Type::"Custom Layout":
          begin
            if not CustomReportLayout.GET("Custom Report Layout Code") then
              exit(0);
            if CustomReportLayout.Type = CustomReportLayout.Type::RDLC then
              exit(1);
            exit(2);
          end;
      end;
    END;

    [External]
    PROCEDURE GetTempLayoutSelected@3() : Code[20];
    VAR
      DesignTimeReportSelection@1000 : Codeunit "Design-time Report Selection";
    BEGIN
      exit(DesignTimeReportSelection.GetSelectedCustomLayout);
    END;

    [External]
    PROCEDURE SetTempLayoutSelected@4(NewTempSelectedLayoutCode@1001 : Code[20]);
    VAR
      DesignTimeReportSelection@1000 : Codeunit "Design-time Report Selection";
    BEGIN
      DesignTimeReportSelection.SetSelectedCustomLayout(NewTempSelectedLayoutCode);
    END;

    BEGIN
    END.
  }
}

