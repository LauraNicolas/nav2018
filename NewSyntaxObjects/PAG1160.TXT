OBJECT Page 1160 Sales Documents
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Editable=false;
    CaptionML=ENU=Sales Documents;
    InsertAllowed=false;
    DeleteAllowed=false;
    ModifyAllowed=false;
    SourceTable="Cust. Ledger Entry";
    SourceTableView=SORTING("Entry No.")
                    ORDER(Descending);
    PageType=CardPart;
    OnOpenPage=BEGIN
                 SETRANGE("Document Type","Document Type"::Invoice);
                 SETRANGE(Open,true);
                 SETFILTER("Due Date",'<%1',WORKDATE);
                 SETFILTER("Remaining Amt. (LCY)",'<>0');
                 SETCURRENTKEY("Remaining Amt. (LCY)");
                 ASCENDING := false;
               END;

    OnAfterGetRecord=VAR
                       Customer@1000 : Record Customer;
                     BEGIN
                       Customer.GET("Customer No.");
                       CustomerName := Customer.Name;
                       StyleTxt := SetStyle;
                     END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      Name=ActionContainer7;
                      ActionContainerType=ActionItems }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=Due Date;
                ToolTipML=ENU=Specifies when the sales documents are due.;
                ApplicationArea=#All;
                SourceExpr="Due Date";
                StyleExpr=StyleTxt;
                ImplicitType=Date }

    { 8   ;2   ;Field     ;
                Name=Document Type;
                ToolTipML=ENU=Specifies the type of sales document.;
                ApplicationArea=#All;
                SourceExpr="Document Type";
                ImplicitType=Option }

    { 4   ;2   ;Field     ;
                Name=Document No.;
                ToolTipML=ENU=Specifies the sales document number.;
                ApplicationArea=#All;
                SourceExpr="Document No.";
                StyleExpr=StyleTxt;
                ImplicitType=Code20 }

    { 5   ;2   ;Field     ;
                Name=Customer Name;
                CaptionML=ENU=Customer Name;
                ToolTipML=ENU=Specifies customer name.;
                ApplicationArea=#All;
                SourceExpr=CustomerName;
                ImplicitType=Text250 }

    { 6   ;2   ;Field     ;
                Name=Remaining Amount;
                ToolTipML=ENU=Specifies the amount that remains to be paid on the sales documents.;
                ApplicationArea=#All;
                SourceExpr="Remaining Amount";
                OnDrillDown=VAR
                              Company@2000 : Record Company;
                              HyperLinkUrl@1000 : Text[500];
                            BEGIN
                              Company.GET(COMPANYNAME);
                              if Company."Evaluation Company" then
                                HyperLinkUrl := GETURL(CLIENTTYPE::Web,COMPANYNAME,OBJECTTYPE::Page,573) +
                                  '&profile=BUSINESS%20MANAGER' + STRSUBSTNO(FilterForRemAmtDrillDwnTxt,"Entry No.")
                              else
                                HyperLinkUrl := GETURL(CLIENTTYPE::Web,COMPANYNAME,OBJECTTYPE::Page,573) + STRSUBSTNO(FilterForRemAmtDrillDwnTxt,"Entry No.");
                              HYPERLINK(HyperLinkUrl);
                            END;

                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      StyleTxt@1000 : Text;
      CustomerName@1001 : Text[250];
      FilterForRemAmtDrillDwnTxt@1002 : TextConst '@@@="%1 - Entry No. for Vendor ledger entry. ";ENU="&filter=''Detailed Cust. Ledg. Entry''.''Cust. Ledger Entry No.'' IS ''%1''"';

    PROCEDURE SetFilterForOverdueSalesInvoiceAmount@1();
    BEGIN
      RESET;
      SETRANGE("Document Type","Document Type"::Invoice);
      SETRANGE(Open,true);
      SETFILTER("Due Date",'<%1',WORKDATE);
      SETFILTER("Remaining Amt. (LCY)",'<>0');
      SETCURRENTKEY("Remaining Amt. (LCY)");
      ASCENDING := false;
      CurrPage.UPDATE;
    END;

    PROCEDURE SetFilterForSalesDocsDueToday@2();
    BEGIN
      RESET;
      SETFILTER("Document Type",'Invoice|Credit Memo');
      SETFILTER("Due Date",'<=%1',WORKDATE);
      SETRANGE(Open,true);
      ASCENDING := false;
      CurrPage.UPDATE;
    END;

    PROCEDURE SetFilterForSalesDocsDueNextWeek@3();
    BEGIN
      RESET;
      SETFILTER("Document Type",'Invoice|Credit Memo');
      SETFILTER("Due Date",'%1..%2',CALCDATE('<1D>',WORKDATE),CALCDATE('<1W>',WORKDATE));
      SETRANGE(Open,true);
      ASCENDING := false;
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

