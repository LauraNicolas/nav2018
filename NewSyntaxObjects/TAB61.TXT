OBJECT Table 61 Electronic Document Format
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckCodeunitExist;
             END;

    OnModify=BEGIN
               CheckCodeunitExist;
             END;

    CaptionML=ENU=Electronic Document Format;
    LookupPageID="Electronic Document Format";
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 2   ;   ;Usage               ;Option        ;CaptionML=ENU=Usage;
                                                   OptionCaptionML=ENU=Sales Invoice,Sales Credit Memo,Sales Validation,Service Invoice,Service Credit Memo,Service Validation,Job Quote;
                                                   OptionString="Sales Invoice","Sales Credit Memo","Sales Validation","Service Invoice","Service Credit Memo","Service Validation","Job Quote" }
    { 4   ;   ;Description         ;Text250       ;CaptionML=ENU=Description }
    { 5   ;   ;Codeunit ID         ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID;
                                                   NotBlank=true;
                                                   BlankZero=true }
    { 6   ;   ;Codeunit Caption    ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Codeunit),
                                                                                                                "Object ID"=FIELD("Codeunit ID")));
                                                   CaptionML=ENU=Codeunit Caption;
                                                   Editable=false }
    { 7   ;   ;Delivery Codeunit ID;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Codeunit));
                                                   CaptionML=ENU=Delivery Codeunit ID;
                                                   BlankZero=true }
    { 8   ;   ;Delivery Codeunit Caption;Text250  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Codeunit),
                                                                                                                "Object ID"=FIELD("Codeunit ID")));
                                                   CaptionML=ENU=Delivery Codeunit Caption;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;Code,Usage                              ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      UnSupportedTableTypeErr@1000 : TextConst '@@@="%1 = Sales Document Type";ENU=The %1 table is not supported.';
      NonExistingDocumentFormatErr@1001 : TextConst '@@@=%1 : document format, %2 document use eq Invoice;ENU=The electronic document format %1 does not exist for the document type %2.';
      UnSupportedDocumentTypeErr@1002 : TextConst '@@@="%1 : document ytp eq Invocie ";ENU=The document type %1 is not supported.';
      ElectronicDocumentNotCreatedErr@1003 : TextConst 'ENU=The electronic document has not been created.';
      ElectronicFormatErr@1004 : TextConst '@@@="%1=Specified Electronic Format";ENU=The electronic format %1 does not exist.';
      FileManagement@1005 : Codeunit "File Management";

    [Internal]
    PROCEDURE SendElectronically@1(VAR ServerFilePath@1008 : Text[250];VAR ClientFileName@1009 : Text[250];DocumentVariant@1000 : Variant;ElectronicFormat@1003 : Code[20]);
    VAR
      RecordExportBuffer@1002 : Record "Record Export Buffer";
      TempErrorMessage@1011 : TEMPORARY Record "Error Message";
      ErrorMessage@1012 : Record "Error Message";
      RecRef@1005 : RecordRef;
      DocumentUsage@1004 : '"Sales Invoice","Sales Credit Memo"';
      StartID@1006 : Integer;
      EndID@1007 : Integer;
      IsMissingServerFile@1010 : Boolean;
    BEGIN
      GetDocumentUsage(DocumentUsage,DocumentVariant);

      if not GET(ElectronicFormat,DocumentUsage) then
        ERROR(STRSUBSTNO(NonExistingDocumentFormatErr,ElectronicFormat,FORMAT(DocumentUsage)));

      RecRef.GETTABLE(DocumentVariant);

      RecordExportBuffer.LOCKTABLE;
      if RecRef.FINDSET then
        repeat
          CLEAR(RecordExportBuffer);
          RecordExportBuffer.RecordID := RecRef.RECORDID;
          RecordExportBuffer.ClientFileName :=
            GetAttachmentFileName(GetDocumentNo(RecRef),GetDocumentType(RecRef),'xml');
          RecordExportBuffer.ZipFileName :=
            GetAttachmentFileName(GetDocumentNo(RecRef),GetDocumentType(RecRef),'zip');
          RecordExportBuffer.INSERT(true);
          if StartID = 0 then
            StartID := RecordExportBuffer.ID;
          EndID := RecordExportBuffer.ID;
        until RecRef.NEXT = 0;

      RecordExportBuffer.SETRANGE(ID,StartID,EndID);
      if RecordExportBuffer.FINDSET then
        repeat
          ErrorMessage.SetContext(RecordExportBuffer);
          ErrorMessage.ClearLog;

          CODEUNIT.RUN("Codeunit ID",RecordExportBuffer);

          TempErrorMessage.CopyFromContext(RecordExportBuffer);
          ErrorMessage.ClearLog; // Clean up

          if RecordExportBuffer.ServerFilePath = '' then
            IsMissingServerFile := true;
        until RecordExportBuffer.NEXT = 0;

      // Display errors in case anything went wrong.
      TempErrorMessage.ShowErrorMessages(true);
      if IsMissingServerFile then
        ERROR(ElectronicDocumentNotCreatedErr);

      if RecordExportBuffer.COUNT > 1 then begin
        ServerFilePath := COPYSTR(FileManagement.CreateZipArchiveObject,1,250);
        ClientFileName := COPYSTR(RecordExportBuffer.ZipFileName,1,250);
        RecordExportBuffer.FINDSET;
        repeat
          FileManagement.AddFileToZipArchive(RecordExportBuffer.ServerFilePath,RecordExportBuffer.ClientFileName);
        until RecordExportBuffer.NEXT = 0;
        FileManagement.CloseZipArchive;
      end else
        if RecordExportBuffer.FINDFIRST then begin
          ServerFilePath := RecordExportBuffer.ServerFilePath;
          ClientFileName := RecordExportBuffer.ClientFileName;
        end;

      RecordExportBuffer.DELETEALL;
    END;

    [External]
    PROCEDURE ValidateElectronicServiceDocument@11(ServiceHeader@1000 : Record "Service Header";ElectronicFormat@1001 : Code[20]);
    VAR
      ElectronicDocumentFormat@1002 : Record "Electronic Document Format";
    BEGIN
      if not ElectronicDocumentFormat.GET(ElectronicFormat,Usage::"Service Validation") then
        exit; // no validation required

      CODEUNIT.RUN(ElectronicDocumentFormat."Codeunit ID",ServiceHeader);
    END;

    [External]
    PROCEDURE ValidateElectronicSalesDocument@3(SalesHeader@1000 : Record "Sales Header";ElectronicFormat@1001 : Code[20]);
    VAR
      ElectronicDocumentFormat@1002 : Record "Electronic Document Format";
    BEGIN
      if not ElectronicDocumentFormat.GET(ElectronicFormat,Usage::"Sales Validation") then
        exit; // no validation required

      CODEUNIT.RUN(ElectronicDocumentFormat."Codeunit ID",SalesHeader);
    END;

    [External]
    PROCEDURE ValidateElectronicJobsDocument@6(Job@1000 : Record Job;ElectronicFormat@1001 : Code[20]);
    VAR
      ElectronicDocumentFormat@1002 : Record "Electronic Document Format";
    BEGIN
      if not ElectronicDocumentFormat.GET(ElectronicFormat,Usage::"Job Quote") then
        exit; // no validation required

      CODEUNIT.RUN(ElectronicDocumentFormat."Codeunit ID",Job);
    END;

    [External]
    PROCEDURE GetAttachmentFileName@30(DocumentNo@1003 : Code[20];DocumentType@1005 : Text;Extension@1001 : Code[3]) : Text[250];
    VAR
      FileMgt@1000 : Codeunit "File Management";
    BEGIN
      exit(
        COPYSTR(
          STRSUBSTNO('%1 - %2 %3.%4',FileMgt.StripNotsupportChrInFileName(COMPANYNAME),DocumentType,DocumentNo,Extension),1,250));
    END;

    [External]
    PROCEDURE GetDocumentUsage@4(VAR DocumentUsage@1000 : Option;DocumentVariant@1001 : Variant);
    VAR
      DocumentRecordRef@1003 : RecordRef;
    BEGIN
      DocumentRecordRef.GETTABLE(DocumentVariant);
      case DocumentRecordRef.NUMBER of
        DATABASE::"Sales Invoice Header":
          DocumentUsage := Usage::"Sales Invoice";
        DATABASE::"Sales Cr.Memo Header":
          DocumentUsage := Usage::"Sales Credit Memo";
        DATABASE::"Service Invoice Header":
          DocumentUsage := Usage::"Service Invoice";
        DATABASE::"Service Cr.Memo Header":
          DocumentUsage := Usage::"Service Credit Memo";
        DATABASE::"Sales Header":
          GetDocumentUsageForSalesHeader(DocumentUsage,DocumentVariant);
        DATABASE::"Service Header":
          GetDocumentUsageForServiceHeader(DocumentUsage,DocumentVariant);
        DATABASE::Job:
          DocumentUsage := Usage::"Job Quote";
        else
          ERROR(STRSUBSTNO(UnSupportedTableTypeErr,DocumentRecordRef.CAPTION));
      end;
    END;

    [External]
    PROCEDURE GetDocumentNo@7(DocumentVariant@1001 : Variant) : Code[20];
    VAR
      SalesInvoiceHeader@1000 : Record "Sales Invoice Header";
      SalesCrMemoHeader@1002 : Record "Sales Cr.Memo Header";
      SalesHeader@1004 : Record "Sales Header";
      ServiceInvoiceHeader@1005 : Record "Service Invoice Header";
      ServiceCrMemoHeader@1006 : Record "Service Cr.Memo Header";
      ServiceHeader@1007 : Record "Service Header";
      Job@1008 : Record Job;
      DocumentRecordRef@1003 : RecordRef;
    BEGIN
      if DocumentVariant.ISRECORD then
        DocumentRecordRef.GETTABLE(DocumentVariant)
      else
        if DocumentVariant.ISRECORDREF then
          DocumentRecordRef := DocumentVariant;

      case DocumentRecordRef.NUMBER of
        DATABASE::"Sales Invoice Header":
          begin
            SalesInvoiceHeader := DocumentVariant;
            exit(SalesInvoiceHeader."No.");
          end;
        DATABASE::"Sales Cr.Memo Header":
          begin
            SalesCrMemoHeader := DocumentVariant;
            exit(SalesCrMemoHeader."No.");
          end;
        DATABASE::"Service Invoice Header":
          begin
            ServiceInvoiceHeader := DocumentVariant;
            exit(ServiceInvoiceHeader."No.");
          end;
        DATABASE::"Service Cr.Memo Header":
          begin
            ServiceCrMemoHeader := DocumentVariant;
            exit(ServiceCrMemoHeader."No.");
          end;
        DATABASE::"Service Header":
          begin
            ServiceHeader := DocumentVariant;
            exit(ServiceHeader."No.");
          end;
        DATABASE::"Sales Header":
          begin
            SalesHeader := DocumentVariant;
            exit(SalesHeader."No.");
          end;
        DATABASE::Job:
          begin
            Job := DocumentVariant;
            exit(Job."No.");
          end;
        else
          ERROR(STRSUBSTNO(UnSupportedTableTypeErr,DocumentRecordRef.CAPTION));
      end;
    END;

    LOCAL PROCEDURE GetDocumentUsageForSalesHeader@2(VAR DocumentUsage@1001 : Option;SalesHeader@1000 : Record "Sales Header");
    BEGIN
      case SalesHeader."Document Type" of
        SalesHeader."Document Type"::Order:
          exit;
        SalesHeader."Document Type"::Invoice:
          DocumentUsage := Usage::"Sales Invoice";
        SalesHeader."Document Type"::"Credit Memo":
          DocumentUsage := Usage::"Sales Credit Memo";
        else
          ERROR(STRSUBSTNO(UnSupportedDocumentTypeErr,FORMAT(SalesHeader."Document Type")));
      end;
    END;

    LOCAL PROCEDURE GetDocumentUsageForServiceHeader@9(VAR DocumentUsage@1001 : Option;ServiceHeader@1000 : Record "Service Header");
    BEGIN
      case ServiceHeader."Document Type" of
        ServiceHeader."Document Type"::Invoice:
          DocumentUsage := Usage::"Service Invoice";
        ServiceHeader."Document Type"::"Credit Memo":
          DocumentUsage := Usage::"Service Credit Memo";
        else
          ERROR(STRSUBSTNO(UnSupportedDocumentTypeErr,FORMAT(ServiceHeader."Document Type")));
      end;
    END;

    LOCAL PROCEDURE CheckCodeunitExist@8();
    VAR
      AllObj@1000 : Record AllObj;
    BEGIN
      AllObj.GET(AllObj."Object Type"::Codeunit,"Codeunit ID");
    END;

    [External]
    PROCEDURE ValidateElectronicFormat@5(ElectronicFormat@1001 : Code[20]);
    VAR
      ElectronicDocumentFormat@1000 : Record "Electronic Document Format";
    BEGIN
      ElectronicDocumentFormat.SETRANGE(Code,ElectronicFormat);
      if ElectronicDocumentFormat.ISEMPTY then
        ERROR(ElectronicFormatErr,ElectronicFormat);
    END;

    LOCAL PROCEDURE GetDocumentType@60(DocumentVariant@1000 : Variant) : Text[50];
    VAR
      DummySalesHeader@1002 : Record "Sales Header";
      DummyServiceHeader@1003 : Record "Service Header";
      DocumentRecordRef@1001 : RecordRef;
    BEGIN
      if DocumentVariant.ISRECORD then
        DocumentRecordRef.GETTABLE(DocumentVariant)
      else
        if DocumentVariant.ISRECORDREF then
          DocumentRecordRef := DocumentVariant;
      case DocumentRecordRef.NUMBER of
        DATABASE::"Sales Invoice Header":
          exit(FORMAT(DummySalesHeader."Document Type"::Invoice));
        DATABASE::"Sales Cr.Memo Header":
          exit(FORMAT(DummySalesHeader."Document Type"::"Credit Memo"));
        DATABASE::"Service Invoice Header":
          exit(FORMAT(DummyServiceHeader."Document Type"::Invoice));
        DATABASE::"Service Cr.Memo Header":
          exit(FORMAT(DummyServiceHeader."Document Type"::"Credit Memo"));
        DATABASE::Job:
          exit(FORMAT(DummyServiceHeader."Document Type"::Quote));
        DATABASE::"Service Header":
          begin
            DummyServiceHeader := DocumentVariant;
            if DummyServiceHeader."Document Type" = DummyServiceHeader."Document Type"::Quote then
              exit(FORMAT(DummyServiceHeader."Document Type"::Quote));
          end;
        DATABASE::"Sales Header":
          begin
            DummySalesHeader := DocumentVariant;
            if DummySalesHeader."Document Type" = DummySalesHeader."Document Type"::Quote then
              exit(FORMAT(DummySalesHeader."Document Type"::Quote));
          end;
      end;
    END;

    [External]
    PROCEDURE InsertElectronicFormat@24(InsertElectronicFormatCode@1000 : Code[20];InsertElectronicFormatDescription@1001 : Text[250];CodeunitID@1002 : Integer;DeliveryCodeunitID@1005 : Integer;InsertElectronicFormatUsage@1004 : Option);
    VAR
      ElectronicDocumentFormat@1003 : Record "Electronic Document Format";
    BEGIN
      if ElectronicDocumentFormat.GET(InsertElectronicFormatCode,Usage) then
        exit;

      ElectronicDocumentFormat.INIT;
      ElectronicDocumentFormat.Code := InsertElectronicFormatCode;
      ElectronicDocumentFormat.Description := InsertElectronicFormatDescription;
      ElectronicDocumentFormat."Codeunit ID" := CodeunitID;
      ElectronicDocumentFormat."Delivery Codeunit ID" := DeliveryCodeunitID;
      ElectronicDocumentFormat.Usage := InsertElectronicFormatUsage;
      ElectronicDocumentFormat.INSERT;
    END;

    [Integration]
    PROCEDURE OnDiscoverElectronicFormat@10();
    BEGIN
    END;

    BEGIN
    END.
  }
}

