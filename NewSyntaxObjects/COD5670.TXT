OBJECT Codeunit 5670 FA. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="FA Journal Line";
    OnRun=BEGIN
            FAJnlLine.COPY(Rec);
            Code;
            COPY(FAJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.';
      FAJnlTemplate@1005 : Record "FA Journal Template";
      FAJnlLine@1006 : Record "FA Journal Line";
      FAReg@1007 : Record "FA Register";
      TempJnlBatchName@1009 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      with FAJnlLine do begin
        FAJnlTemplate.GET("Journal Template Name");
        FAJnlTemplate.TESTFIELD("Posting Report ID");
        FAJnlTemplate.TESTFIELD("Maint. Posting Report ID");
        if FAJnlTemplate.Recurring and (GETFILTER("FA Posting Date") <> '') then
          FIELDERROR("FA Posting Date",Text000);

        if not CONFIRM(Text001,false) then
          exit;

        TempJnlBatchName := "Journal Batch Name";

        CODEUNIT.RUN(CODEUNIT::"FA Jnl.-Post Batch",FAJnlLine);

        if FAReg.GET("Line No.") then begin
          FAReg.SETRECFILTER;
          if FAReg."From Entry No." > 0 then
            REPORT.RUN(FAJnlTemplate."Posting Report ID",false,false,FAReg);
          if FAReg."From Maintenance Entry No." > 0 then
            REPORT.RUN(FAJnlTemplate."Maint. Posting Report ID",false,false,FAReg);
        end;

        if "Line No." = 0 then
          MESSAGE(Text002)
        else
          if TempJnlBatchName = "Journal Batch Name" then
            MESSAGE(Text003)
          else
            MESSAGE(
              Text004,
              "Journal Batch Name");

        if not FIND('=><') or (TempJnlBatchName <> "Journal Batch Name") then begin
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP := 0;
          "Line No." := 1;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

