OBJECT Page 1626 Office OCR Incoming Documents
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Office Incoming Documents;
    InsertAllowed=false;
    DeleteAllowed=false;
    SourceTable="Exchange Object";
    DataCaptionExpr=PageCaptionTxt;
    SourceTableView=SORTING(Name)
                    ORDER(Ascending);
    PageType=StandardDialog;
    SourceTableTemporary=true;
    OnOpenPage=BEGIN
                 if COUNT = 1 then begin
                   Selected := true;
                   MODIFY;
                 end else
                   if IncomingDocumentAttachment."Document No. Filter" <> '' then
                     MODIFYALL(Selected,false);
               END;

    OnQueryClosePage=VAR
                       IncomingDocument@1001 : Record "Incoming Document";
                     BEGIN
                       if CloseAction in [ACTION::OK,ACTION::LookupOK] then begin
                         SETRANGE(Selected,true);
                         if FINDSET then begin
                           repeat
                             case InitiatedAction of
                               InitiatedAction::InitiateSendToIncomingDocuments:
                                 OfficeMgt.SendToIncomingDocument(Rec,IncomingDocument,IncomingDocumentAttachment);
                               InitiatedAction::InitiateSendToOCR:
                                 if OfficeMgt.SendToIncomingDocument(Rec,IncomingDocument,IncomingDocumentAttachment) then
                                   OfficeMgt.SendToOCR(IncomingDocument);
                               InitiatedAction::InitiateSendToWorkFlow:
                                 if OfficeMgt.SendToIncomingDocument(Rec,IncomingDocument,IncomingDocumentAttachment) then
                                   OfficeMgt.SendApprovalRequest(IncomingDocument);
                             end;
                           until NEXT = 0;
                           if InitiatedAction = InitiatedAction::InitiateSendToOCR then
                             OfficeMgt.DisplayOCRUploadSuccessMessage(COUNT);
                         end;
                       end;
                     END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                Name=Selected;
                CaptionML=ENU=Send;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Selected;
                OnValidate=BEGIN
                             if (IncomingDocumentAttachment."Document No. Filter" <> '') and (COUNT > 1) and Selected then begin
                               MODIFYALL(Selected,false);
                               Selected := true;
                             end;
                           END;

                ImplicitType=Boolean }

    { 3   ;2   ;Field     ;
                Name=Name;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Name;
                Editable=False;
                ImplicitType=Text250 }

  }
  CODE
  {
    VAR
      IncomingDocumentAttachment@1002 : Record "Incoming Document Attachment";
      OfficeMgt@1000 : Codeunit "Office Management";
      PageCaptionTxt@1001 : TextConst 'ENU=Select Attachment to Send';

    [External]
    PROCEDURE InitializeIncomingDocumentAttachment@1(LinkedIncomingDocumentAttachment@1000 : Record "Incoming Document Attachment");
    BEGIN
      IncomingDocumentAttachment := LinkedIncomingDocumentAttachment;
    END;

    [External]
    PROCEDURE InitializeExchangeObject@10(VAR TempExchangeObject@1001 : TEMPORARY Record "Exchange Object");
    BEGIN
      if TempExchangeObject.FINDSET then
        repeat
          TempExchangeObject.CALCFIELDS(Content);
          TRANSFERFIELDS(TempExchangeObject);
          INSERT;
        until TempExchangeObject.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

