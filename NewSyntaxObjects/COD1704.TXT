OBJECT Codeunit 1704 Exp. Pre-Mapping Det Pos. Pay
{
  OBJECT-PROPERTIES
  {
    Date=20150915D;
    Time=120000T;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo="Data Exch.";
    Permissions=TableData "Positive Pay Detail"=rimd;
    OnRun=VAR
            CheckLedgerEntry@1000 : Record "Check Ledger Entry";
            LineNo@1001 : Integer;
          BEGIN
            CheckLedgerEntry.SETRANGE("Data Exch. Entry No.","Entry No.");
            PreparePosPayDetails(CheckLedgerEntry,"Entry No.",LineNo);

            // Reset filters and set it on the Data Exch. Voided Entry No.
            CheckLedgerEntry.RESET;
            CheckLedgerEntry.SETRANGE("Data Exch. Voided Entry No.","Entry No.");
            PreparePosPayDetails(CheckLedgerEntry,"Entry No.",LineNo);
          END;

  }
  CODE
  {
    VAR
      ProgressMsg@1000 : TextConst 'ENU=Preprocessing line no. #1######.';

    LOCAL PROCEDURE PreparePosPayDetails@2(VAR CheckLedgerEntry@1000 : Record "Check Ledger Entry";DataExchangeEntryNo@1002 : Integer;VAR LineNo@1001 : Integer);
    VAR
      Window@1003 : Dialog;
    BEGIN
      if CheckLedgerEntry.FINDSET then begin
        Window.OPEN(ProgressMsg);
        repeat
          LineNo += 1;
          Window.UPDATE(1,LineNo);
          PreparePosPayDetail(CheckLedgerEntry,DataExchangeEntryNo,LineNo);
        until CheckLedgerEntry.NEXT = 0;
        Window.CLOSE;
      end;
    END;

    LOCAL PROCEDURE PreparePosPayDetail@1(CheckLedgerEntry@1000 : Record "Check Ledger Entry";DataExchangeEntryNo@1001 : Integer;LineNo@1002 : Integer);
    VAR
      BankAccount@1003 : Record "Bank Account";
      PosPayDetail@1004 : Record "Positive Pay Detail";
    BEGIN
      BankAccount.GET(CheckLedgerEntry."Bank Account No.");

      with PosPayDetail do begin
        INIT;
        "Data Exch. Entry No." := DataExchangeEntryNo;
        "Entry No." := LineNo;
        "Account Number" := BankAccount."Bank Account No.";
        if DataExchangeEntryNo = CheckLedgerEntry."Data Exch. Voided Entry No." then begin
          // V for Void legend
          "Record Type Code" := 'V';
          "Void Check Indicator" := 'V';
        end else begin
          // O for Open legend
          "Record Type Code" := 'O';
          "Void Check Indicator" := '';
        end;
        "Check Number" := CheckLedgerEntry."Check No.";
        Amount := CheckLedgerEntry.Amount;
        Payee := CheckLedgerEntry.GetPayee;
        "Issue Date" := CheckLedgerEntry."Check Date";
        if BankAccount."Currency Code" <> '' then
          "Currency Code" := BankAccount."Currency Code";

        INSERT(true);
      end;
    END;

    BEGIN
    END.
  }
}

