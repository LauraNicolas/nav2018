OBJECT Codeunit 5470 Graph Collection Mgt - Item
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ItemUOMDescriptionTxt@1000 : TextConst '@@@={Locked};ENU=Graph CDM - Unit of Measure complex type on Item Entity page';
      ItemUOMConversionsDescriptionTxt@1002 : TextConst '@@@={Locked};ENU=Graph CDM - Unit of Measure Conversions complex type on Item Entity page';
      ItemCategoryDescriptionTxt@1006 : TextConst '@@@={Locked};ENU=Graph CDM - Item Category complex type on Item Enity page';
      ValueMustBeEqualErr@1003 : TextConst '@@@={Locked};ENU=Conversions must be specified with %1 with value %2.';
      BaseUnitOfMeasureCannotHaveConversionsErr@1008 : TextConst '@@@={Locked};ENU=Base Unit Of Measure must be specified on the item first.';

    PROCEDURE InsertItemFromSalesDocument@16(VAR Item@1000 : Record Item;VAR TempFieldSet@1004 : TEMPORARY Record Field;UnitOfMeasureJSON@1001 : Text);
    VAR
      GraphMgtGeneralTools@1005 : Codeunit "Graph Mgt - General Tools";
      RecRef@1002 : RecordRef;
      ItemModified@1006 : Boolean;
    BEGIN
      Item.INSERT(true);

      UpdateOrCreateItemUnitOfMeasureFromSalesDocument(UnitOfMeasureJSON,Item,TempFieldSet,ItemModified);

      RecRef.GETTABLE(Item);
      GraphMgtGeneralTools.ProcessNewRecordFromAPI(RecRef,TempFieldSet,Item."Last DateTime Modified");
      RecRef.SETTABLE(Item);

      Item.MODIFY(true);
    END;

    PROCEDURE InsertItem@10(VAR Item@1000 : Record Item;VAR TempFieldSet@1004 : TEMPORARY Record Field;BaseUnitOfMeasureJSON@1001 : Text;ItemCategoryJSON@1002 : Text);
    VAR
      GraphMgtGeneralTools@1005 : Codeunit "Graph Mgt - General Tools";
      RecRef@1003 : RecordRef;
    BEGIN
      Item.INSERT(true);

      ProcessComplexTypes(
        Item,
        BaseUnitOfMeasureJSON,
        ItemCategoryJSON
        );

      RecRef.GETTABLE(Item);
      GraphMgtGeneralTools.ProcessNewRecordFromAPI(RecRef,TempFieldSet,Item."Last DateTime Modified");
      RecRef.SETTABLE(Item);

      Item.MODIFY(true);
    END;

    PROCEDURE ProcessComplexTypes@8(VAR Item@1000 : Record Item;BaseUOMJSON@1003 : Text;ItemCategoryJSON@1005 : Text);
    BEGIN
      UpdateUnitOfMeasure(BaseUOMJSON,Item.FIELDNO("Base Unit of Measure"),Item);
      UpdateItemCategory(ItemCategoryJSON,Item);
    END;

    [External]
    PROCEDURE ItemCategoryToJSON@1(ItemCategoryCode@1000 : Code[20]) : Text;
    VAR
      ItemCategory@1001 : Record "Item Category";
      JSONManagement@1002 : Codeunit "JSON Management";
      JsonObject@1003 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
    BEGIN
      if ItemCategoryCode = '' then
        exit('');

      if not ItemCategory.GET(ItemCategoryCode) then
        exit('');

      JSONManagement.InitializeEmptyObject;
      JSONManagement.GetJSONObject(JsonObject);
      JSONManagement.AddJPropertyToJObject(JsonObject,ItemCategoryComplexTypeCategoryId,ItemCategory.Code);
      JSONManagement.AddJPropertyToJObject(JsonObject,ItemCategoryComplexTypeDescription,ItemCategory.Description);

      exit(JSONManagement.WriteObjectToString);
    END;

    PROCEDURE ItemUnitOfMeasureToJSON@7(VAR Item@1000 : Record Item;UnitOfMeasureCode@1001 : Code[10]) : Text;
    VAR
      ItemUnitOfMeasure@1004 : Record "Item Unit of Measure";
      GraphMgtComplexTypes@1007 : Codeunit "Graph Mgt - Complex Types";
      JSONManagement@1003 : Codeunit "JSON Management";
      JsonObject@1002 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      ItemUOMConversionJObject@1005 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      UnitOfMeasureJSON@1008 : Text;
    BEGIN
      UnitOfMeasureJSON := GraphMgtComplexTypes.GetUnitOfMeasureJSON(UnitOfMeasureCode);

      if UnitOfMeasureCode = Item."Base Unit of Measure" then
        exit(UnitOfMeasureJSON);

      if not ItemUnitOfMeasure.GET(Item."No.",UnitOfMeasureCode) then
        exit(UnitOfMeasureJSON);

      JSONManagement.InitializeObject(UnitOfMeasureJSON);
      JSONManagement.GetJSONObject(JsonObject);

      ItemUOMConversionJObject := ItemUOMConversionJObject.JObject;
      JSONManagement.AddJPropertyToJObject(
        ItemUOMConversionJObject,UOMConversionComplexTypeToUnitOfMeasure,Item."Base Unit of Measure");
      JSONManagement.AddJPropertyToJObject(
        ItemUOMConversionJObject,UOMConversionComplexTypeFromToConversionRate,ItemUnitOfMeasure."Qty. per Unit of Measure");
      JSONManagement.AddJObjectToJObject(JsonObject,UOMConversionComplexTypeName,ItemUOMConversionJObject);
      exit(JSONManagement.WriteObjectToString);
    END;

    PROCEDURE UpdateIntegrationRecords@11(OnlyItemsWithoutId@1000 : Boolean);
    VAR
      Item@1002 : Record Item;
      GraphMgtGeneralTools@1001 : Codeunit "Graph Mgt - General Tools";
      ItemRecordRef@1019 : RecordRef;
    BEGIN
      ItemRecordRef.OPEN(DATABASE::Item);
      GraphMgtGeneralTools.UpdateIntegrationRecords(ItemRecordRef,Item.FIELDNO(Id),OnlyItemsWithoutId);
    END;

    PROCEDURE UpdateOrCreateItemUnitOfMeasureFromSalesDocument@6(UnitOfMeasureJSONString@1000 : Text;VAR Item@1004 : Record Item;VAR TempFieldSet@1002 : TEMPORARY Record Field;VAR ItemModified@1007 : Boolean);
    VAR
      UnitOfMeasure@1005 : Record "Unit of Measure";
      TempUnitOfMeasure@1022 : TEMPORARY Record "Unit of Measure";
      ItemUnitOfMeasure@1006 : Record "Item Unit of Measure";
      TempItemUnitOfMeasure@1023 : TEMPORARY Record "Item Unit of Measure";
      ExpectedBaseUOMCode@1001 : Code[10];
      HasUOMConversions@1024 : Boolean;
    BEGIN
      if UnitOfMeasureJSONString = '' then
        exit;

      ParseJSONToUnitOfMeasure(UnitOfMeasureJSONString,TempUnitOfMeasure);
      HasUOMConversions :=
        ParseJSONToItemUnitOfMeasure(UnitOfMeasureJSONString,Item,TempItemUnitOfMeasure,TempUnitOfMeasure,ExpectedBaseUOMCode);

      if HasUOMConversions then begin
        VerifyBaseUOMIsSet(Item,ExpectedBaseUOMCode);
        if not UnitOfMeasure.GET(ExpectedBaseUOMCode) then begin
          UnitOfMeasure.Code := ExpectedBaseUOMCode;
          UnitOfMeasure.INSERT;
        end;

        if Item."Base Unit of Measure" = '' then begin
          Item.VALIDATE("Base Unit of Measure",UnitOfMeasure.Code);
          RegisterFieldSet(TempFieldSet,Item.FIELDNO("Base Unit of Measure"));
          ItemModified := true;
        end;

        InsertOrUpdateItemUnitOfMeasureRecord(ItemUnitOfMeasure,TempItemUnitOfMeasure);
      end else begin
        InsertOrUpdateUnitOfMeasureRecord(UnitOfMeasure,TempUnitOfMeasure);

        if Item."Base Unit of Measure" = '' then begin
          Item.VALIDATE("Base Unit of Measure",UnitOfMeasure.Code);
          RegisterFieldSet(TempFieldSet,Item.FIELDNO("Base Unit of Measure"));
          ItemModified := true;
        end else begin
          // Create on the fly if it does not exist
          if ItemUnitOfMeasure.GET(Item."No.",UnitOfMeasure.Code) then
            exit;

          ItemUnitOfMeasure."Item No." := Item."No.";
          ItemUnitOfMeasure.Code := UnitOfMeasure.Code;
          ItemUnitOfMeasure."Qty. per Unit of Measure" := 1;
          ItemUnitOfMeasure.INSERT(true);
        end;
      end;
    END;

    PROCEDURE UpdateUnitOfMeasure@23(UnitOfMeasureJSONString@1000 : Text;UnitOfMeasureFieldNo@1018 : Integer;VAR Item@1004 : Record Item);
    VAR
      UnitOfMeasure@1005 : Record "Unit of Measure";
      TempUnitOfMeasure@1022 : TEMPORARY Record "Unit of Measure";
      ItemUnitOfMeasure@1006 : Record "Item Unit of Measure";
      TempItemUnitOfMeasure@1023 : TEMPORARY Record "Item Unit of Measure";
      ItemRecordRef@1019 : RecordRef;
      UnitOfMeasureFieldRef@1020 : FieldRef;
      PreviousUnitOfMeasureJSONString@1013 : Text;
      PreviousUOMCode@1021 : Code[10];
      HasUOMConversions@1024 : Boolean;
      BaseUnitOfMeasureCode@1001 : Code[20];
    BEGIN
      if UnitOfMeasureJSONString = '' then
        exit;

      ItemRecordRef.GETTABLE(Item);
      UnitOfMeasureFieldRef := ItemRecordRef.FIELD(UnitOfMeasureFieldNo);

      if UnitOfMeasureJSONString = 'null' then begin
        UnitOfMeasureFieldRef.VALIDATE('');
        ItemRecordRef.SETTABLE(Item);
        exit;
      end;

      PreviousUOMCode := UnitOfMeasureFieldRef.VALUE;
      PreviousUnitOfMeasureJSONString := ItemUnitOfMeasureToJSON(Item,PreviousUOMCode);

      if UnitOfMeasureJSONString = PreviousUnitOfMeasureJSONString then
        exit;

      ParseJSONToUnitOfMeasure(UnitOfMeasureJSONString,TempUnitOfMeasure);
      HasUOMConversions :=
        ParseJSONToItemUnitOfMeasure(UnitOfMeasureJSONString,Item,TempItemUnitOfMeasure,TempUnitOfMeasure,BaseUnitOfMeasureCode);
      if HasUOMConversions and (UnitOfMeasureFieldNo = Item.FIELDNO("Base Unit of Measure")) then
        ERROR(BaseUnitOfMeasureCannotHaveConversionsErr);

      InsertOrUpdateUnitOfMeasureRecord(UnitOfMeasure,TempUnitOfMeasure);

      if HasUOMConversions then
        InsertOrUpdateItemUnitOfMeasureRecord(ItemUnitOfMeasure,TempItemUnitOfMeasure);

      if PreviousUOMCode <> UnitOfMeasure.Code then
        UnitOfMeasureFieldRef.VALIDATE(UnitOfMeasure.Code);

      ItemRecordRef.SETTABLE(Item);
    END;

    LOCAL PROCEDURE InsertOrUpdateUnitOfMeasureRecord@22(VAR UnitOfMeasure@1003 : Record "Unit of Measure";VAR TempUnitOfMeasure@1002 : TEMPORARY Record "Unit of Measure");
    VAR
      Modify@1000 : Boolean;
    BEGIN
      if TempUnitOfMeasure.Code = '' then
        exit;

      if not UnitOfMeasure.GET(TempUnitOfMeasure.Code) then begin
        UnitOfMeasure.TRANSFERFIELDS(TempUnitOfMeasure,true);
        UnitOfMeasure.INSERT(true);
        exit;
      end;

      if not (TempUnitOfMeasure.Description in [UnitOfMeasure.Description,'']) then begin
        UnitOfMeasure.VALIDATE(Description,TempUnitOfMeasure.Description);
        Modify := true;
      end;

      if not (TempUnitOfMeasure.Symbol in [UnitOfMeasure.Symbol,'']) then begin
        UnitOfMeasure.VALIDATE(Symbol,TempUnitOfMeasure.Symbol);
        Modify := true;
      end;

      if Modify then
        UnitOfMeasure.MODIFY(true);
    END;

    LOCAL PROCEDURE InsertOrUpdateItemUnitOfMeasureRecord@28(VAR ItemUnitOfMeasure@1003 : Record "Item Unit of Measure";VAR TempItemUnitOfMeasure@1002 : TEMPORARY Record "Item Unit of Measure");
    BEGIN
      if not ItemUnitOfMeasure.GET(TempItemUnitOfMeasure."Item No.",TempItemUnitOfMeasure.Code) then begin
        ItemUnitOfMeasure.TRANSFERFIELDS(TempItemUnitOfMeasure,true);
        ItemUnitOfMeasure.INSERT(true);
      end else begin
        if TempItemUnitOfMeasure."Qty. per Unit of Measure" = ItemUnitOfMeasure."Qty. per Unit of Measure" then
          exit;

        ItemUnitOfMeasure.VALIDATE("Qty. per Unit of Measure",TempItemUnitOfMeasure."Qty. per Unit of Measure");
        ItemUnitOfMeasure.MODIFY(true);
      end;
    END;

    PROCEDURE ParseJSONToUnitOfMeasure@5(UnitOfMeasureJSONString@1000 : Text;VAR UnitOfMeasure@1005 : Record "Unit of Measure");
    VAR
      GraphMgtGeneralTools@1001 : Codeunit "Graph Mgt - General Tools";
      JSONManagement@1010 : Codeunit "JSON Management";
      JsonObject@1009 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      UnitCode@1003 : Text;
    BEGIN
      JSONManagement.InitializeObject(UnitOfMeasureJSONString);
      JSONManagement.GetJSONObject(JsonObject);

      GraphMgtGeneralTools.GetMandatoryStringPropertyFromJObject(JsonObject,UOMComplexTypeUnitCode,UnitCode);
      UnitOfMeasure.Code := COPYSTR(UnitCode,1,MAXSTRLEN(UnitOfMeasure.Code));
      JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,UOMComplexTypeUnitName,UnitOfMeasure.Description);
      JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,UOMComplexTypeSymbol,UnitOfMeasure.Symbol);
    END;

    LOCAL PROCEDURE ParseJSONToItemUnitOfMeasure@9(UnitOfMeasureJSONString@1000 : Text;VAR Item@1006 : Record Item;VAR TempItemUnitOfMeasure@1004 : TEMPORARY Record "Item Unit of Measure";VAR UnitOfMeasure@1018 : Record "Unit of Measure";VAR BaseUnitOfMeasureCode@1002 : Code[20]) : Boolean;
    VAR
      GraphMgtGeneralTools@1001 : Codeunit "Graph Mgt - General Tools";
      JSONManagement@1010 : Codeunit "JSON Management";
      JsonObject@1009 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      ConversionsTxt@1016 : Text;
      FromToConversionRateTxt@1008 : Text;
      BaseUnitOfMeasureTxt@1003 : Text;
    BEGIN
      JSONManagement.InitializeObject(UnitOfMeasureJSONString);
      JSONManagement.GetJSONObject(JsonObject);

      if not JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,UOMConversionComplexTypeName,ConversionsTxt) then
        exit(false);

      if ConversionsTxt = '' then
        exit(false);

      if ConversionsTxt = 'null' then
        exit(false);

      JSONManagement.InitializeObject(ConversionsTxt);
      JSONManagement.GetJSONObject(JsonObject);

      GraphMgtGeneralTools.GetMandatoryStringPropertyFromJObject(
        JsonObject,UOMConversionComplexTypeToUnitOfMeasure,BaseUnitOfMeasureTxt);
      BaseUnitOfMeasureCode := COPYSTR(BaseUnitOfMeasureTxt,1,20);

      GraphMgtGeneralTools.GetMandatoryStringPropertyFromJObject(
        JsonObject,UOMConversionComplexTypeFromToConversionRate,FromToConversionRateTxt);
      EVALUATE(TempItemUnitOfMeasure."Qty. per Unit of Measure",FromToConversionRateTxt);
      TempItemUnitOfMeasure."Item No." := Item."No.";
      TempItemUnitOfMeasure.Code := UnitOfMeasure.Code;
      TempItemUnitOfMeasure.INSERT;

      exit(true);
    END;

    LOCAL PROCEDURE UpdateItemCategory@29(ItemCategoryJSONString@1001 : Text;VAR Item@1002 : Record Item);
    VAR
      ItemCategory@1008 : Record "Item Category";
      PreviousItemCategoryJSONString@1000 : Text;
    BEGIN
      if ItemCategoryJSONString = '' then
        exit;

      if ItemCategoryJSONString = 'null' then begin
        Item.VALIDATE("Item Category Code",'');
        exit;
      end;

      PreviousItemCategoryJSONString := ItemCategoryToJSON(Item."Item Category Code");
      if ItemCategoryJSONString = PreviousItemCategoryJSONString then
        exit;

      UpdateOrCreateItemCategory(ItemCategoryJSONString,ItemCategory);

      if Item."Item Category Code" <> ItemCategory.Code then
        Item.VALIDATE("Item Category Code",ItemCategory.Code);
    END;

    LOCAL PROCEDURE UpdateOrCreateItemCategory@31(ItemCategoryJSONString@1001 : Text;VAR ItemCategory@1002 : Record "Item Category");
    VAR
      TempItemCategory@1009 : TEMPORARY Record "Item Category";
    BEGIN
      ParseJSONToItemCategory(ItemCategoryJSONString,TempItemCategory);

      if TempItemCategory.Code = '' then
        exit;

      if not ItemCategory.GET(TempItemCategory.Code) then begin
        ItemCategory.TRANSFERFIELDS(TempItemCategory,true);
        ItemCategory.INSERT(true);
        exit;
      end;

      if ItemCategory.Description = TempItemCategory.Description then
        exit;

      ItemCategory.VALIDATE(Description,TempItemCategory.Description);
      ItemCategory.MODIFY(true);
    END;

    LOCAL PROCEDURE ParseJSONToItemCategory@44(ItemCategoryJSONString@1006 : Text;VAR TempItemCategory@1005 : TEMPORARY Record "Item Category");
    VAR
      GraphMgtGeneralTools@1000 : Codeunit "Graph Mgt - General Tools";
      JSONManagement@1004 : Codeunit "JSON Management";
      JsonObject@1003 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      CategoryCode@1002 : Text;
    BEGIN
      JSONManagement.InitializeObject(ItemCategoryJSONString);
      JSONManagement.GetJSONObject(JsonObject);
      TempItemCategory.INIT;
      GraphMgtGeneralTools.GetMandatoryStringPropertyFromJObject(JsonObject,ItemCategoryComplexTypeCategoryId,CategoryCode);
      TempItemCategory.Code := COPYSTR(CategoryCode,1,MAXSTRLEN(TempItemCategory.Code));
      JSONManagement.GetStringPropertyValueFromJObjectByName(
        JsonObject,ItemCategoryComplexTypeDescription,TempItemCategory.Description);
    END;

    [Internal]
    PROCEDURE WriteItemEDMComplexTypes@3();
    VAR
      GraphMgtGeneralTools@1000 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      GraphMgtGeneralTools.InsertOrUpdateODataType('ITEM-UOM',ItemUOMDescriptionTxt,GetItemUOMEDM('ITEM-UOM-CONVERSION'));
      GraphMgtGeneralTools.InsertOrUpdateODataType('ITEM-UOM-CONVERSION',ItemUOMConversionsDescriptionTxt,GetItemUOMConversionEDM);
      GraphMgtGeneralTools.InsertOrUpdateODataType('ITEM-CATEGORY',ItemCategoryDescriptionTxt,GetItemCategoryEDM);
    END;

    LOCAL PROCEDURE GetItemUOMEDM@4(UOMConversionDefinitionCode@1000 : Code[50]) : Text;
    VAR
      DummyUnitOfMeasure@1001 : Record "Unit of Measure";
    BEGIN
      exit(
        '<ComplexType Name="UnitOfMeasure">' +
        STRSUBSTNO('<Property Name="%1" Type="Edm.String" Nullable="true" MaxLength="%2" />',
          UOMComplexTypeUnitCode,MAXSTRLEN(DummyUnitOfMeasure.Code)) +
        STRSUBSTNO('<Property Name="%1" Type="Edm.String" Nullable="true" MaxLength="%2" />',
          UOMComplexTypeUnitName,MAXSTRLEN(DummyUnitOfMeasure.Description)) +
        STRSUBSTNO('<Property Name="%1" Type="Edm.String" Nullable="true" MaxLength="%2" />',
          UOMComplexTypeSymbol,MAXSTRLEN(DummyUnitOfMeasure.Symbol)) +
        STRSUBSTNO('<Property Name="%1" Type="%2" Nullable="true" />',
          UOMConversionComplexTypeName,UOMConversionDefinitionCode) +
        '</ComplexType>');
    END;

    LOCAL PROCEDURE GetItemUOMConversionEDM@13() : Text;
    VAR
      DummyItem@1000 : Record Item;
    BEGIN
      exit(
        '<ComplexType Name="ItemUnitOfMeasureConversion">' +
        STRSUBSTNO('<Property Name="%1" Type="Edm.String" Nullable="true" MaxLength="%2" />',
          UOMConversionComplexTypeToUnitOfMeasure,MAXSTRLEN(DummyItem."Base Unit of Measure")) +
        STRSUBSTNO('<Property Name="%1" Type="Edm.Decimal" Nullable="true" />',
          UOMConversionComplexTypeFromToConversionRate) +
        '</ComplexType>');
    END;

    LOCAL PROCEDURE GetItemCategoryEDM@20() : Text;
    VAR
      DummyItemCategory@1000 : Record "Item Category";
    BEGIN
      exit(
        '<ComplexType Name="ItemCategory">' +
        STRSUBSTNO('<Property Name="%1" Type="Edm.String" Nullable="true" MaxLength="%2" />',
          ItemCategoryComplexTypeCategoryId,MAXSTRLEN(DummyItemCategory.Code)) +
        STRSUBSTNO('<Property Name="%1" Type="Edm.String" Nullable="true" MaxLength="%2" />',
          ItemCategoryComplexTypeDescription,MAXSTRLEN(DummyItemCategory.Description)) +
        '</ComplexType>'
        );
    END;

    LOCAL PROCEDURE EnableItemODataWebService@34();
    BEGIN
      WriteItemEDMComplexTypes;
      UpdateIntegrationRecords(false);
    END;

    PROCEDURE SetLastDateTimeModified@37();
    VAR
      Item@1001 : Record Item;
      DateFilterCalc@1000 : Codeunit "DateFilter-Calc";
      CombinedDateTime@1002 : DateTime;
    BEGIN
      Item.SETRANGE("Last DateTime Modified",0DT);
      if not Item.FINDSET then
        exit;

      repeat
        CombinedDateTime := CREATEDATETIME(Item."Last Date Modified",Item."Last Time Modified");
        Item."Last DateTime Modified" := DateFilterCalc.ConvertToUtcDateTime(CombinedDateTime);
        Item.MODIFY;
      until Item.NEXT = 0;
    END;

    PROCEDURE ItemCategoryComplexTypeCategoryId@39() : Text;
    BEGIN
      exit('categoryId');
    END;

    [External]
    PROCEDURE ItemCategoryComplexTypeDescription@40() : Text;
    BEGIN
      exit('description');
    END;

    LOCAL PROCEDURE RegisterFieldSet@12(VAR TempFieldSet@1001 : TEMPORARY Record Field;FieldNo@1000 : Integer);
    BEGIN
      if TempFieldSet.GET(DATABASE::Item,FieldNo) then
        exit;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::Item;
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(true);
    END;

    PROCEDURE UOMComplexTypeUnitCode@27() : Text;
    BEGIN
      exit('code');
    END;

    [External]
    PROCEDURE UOMComplexTypeUnitName@30() : Text;
    BEGIN
      exit('displayName');
    END;

    PROCEDURE UOMComplexTypeSymbol@32() : Text;
    BEGIN
      exit('symbol');
    END;

    [External]
    PROCEDURE UOMConversionComplexTypeName@15() : Text;
    BEGIN
      exit('unitConversion');
    END;

    [External]
    PROCEDURE UOMConversionComplexTypeToUnitOfMeasure@42() : Text;
    BEGIN
      exit('toUnitOfMeasure');
    END;

    PROCEDURE UOMConversionComplexTypeFromToConversionRate@43() : Text;
    BEGIN
      exit('fromToConversionRate');
    END;

    LOCAL PROCEDURE VerifyBaseUOMIsSet@14(VAR Item@1000 : Record Item;BaseUnitOfMeasure@1001 : Code[20]);
    BEGIN
      if Item."Base Unit of Measure" = '' then
        exit;

      if Item."Base Unit of Measure" <> UPPERCASE(BaseUnitOfMeasure) then
        ERROR(ValueMustBeEqualErr,UOMConversionComplexTypeToUnitOfMeasure,Item."Base Unit of Measure");
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnItem@19(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummyItem@1004 : Record Item;
      GraphMgtGeneralTools@1006 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(RecRef,NewId,Handled,DATABASE::Item,DummyItem.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@21(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummyItem@1004 : Record Item;
      GraphMgtGeneralTools@1007 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      GraphMgtGeneralTools.HandleGetPredefinedIdValue(Id,RecRef,Handled,DATABASE::Item,DummyItem.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@2();
    BEGIN
      EnableItemODataWebService;
      UpdateIntegrationRecords(false);
      UpdateIds;
    END;

    PROCEDURE UpdateIds@24();
    VAR
      Item@1000 : Record Item;
    BEGIN
      with Item do begin
        if FINDSET then
          repeat
            UpdateUnitOfMeasureId;
            UpdateTaxGroupId;
            MODIFY(false);
          until NEXT = 0;
      end;
    END;

    BEGIN
    END.
  }
}

