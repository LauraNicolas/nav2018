OBJECT Page 501 Available - Purchase Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Purchase Line"=rm;
    Editable=false;
    CaptionML=ENU=Available - Purchase Lines;
    InsertAllowed=false;
    DeleteAllowed=false;
    ModifyAllowed=false;
    LinksAllowed=false;
    SourceTable="Purchase Line";
    DataCaptionExpr=CaptionText;
    SourceTableView=SORTING("Document Type",Type,"No.","Variant Code","Drop Shipment","Location Code","Expected Receipt Date");
    PageType=List;
    OnOpenPage=BEGIN
                 ReservEntry.TESTFIELD("Source Type");

                 SETRANGE("Document Type",CurrentSubType);
                 SETRANGE(Type,Type::Item);
                 SETRANGE("No.",ReservEntry."Item No.");
                 SETRANGE("Variant Code",ReservEntry."Variant Code");
                 SETRANGE("Job No.",'');
                 SETRANGE("Drop Shipment",false);
                 SETRANGE("Location Code",ReservEntry."Location Code");

                 SETFILTER("Expected Receipt Date",ReservMgt.GetAvailabilityFilter(ReservEntry."Shipment Date"));

                 case CurrentSubType of
                   0,1,2,4:
                     if ReservMgt.IsPositive then
                       SETFILTER("Quantity (Base)",'>0')
                     else
                       SETFILTER("Quantity (Base)",'<0');
                   3,5:
                     if ReservMgt.IsPositive then
                       SETFILTER("Quantity (Base)",'<0')
                     else
                       SETFILTER("Quantity (Base)",'>0');
                 end;
               END;

    OnAfterGetRecord=BEGIN
                       ReservMgt.PurchLineUpdateValues(Rec,QtyToReserve,QtyToReserveBase,QtyReservedThisLine,QtyReservedThisLineBase);
                     END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      Name=ActionContainer1900000003;
                      ActionContainerType=RelatedInformation }
      { 22      ;1   ;ActionGroup;
                      Name=&Line;
                      CaptionML=ENU=&Line;
                      Image=Line }
      { 6500    ;2   ;Action    ;
                      Name=Item &Tracking Lines;
                      ShortCutKey=Shift+Ctrl+I;
                      CaptionML=ENU=Item &Tracking Lines;
                      ToolTipML=ENU=View or edit serial numbers and lot numbers that are assigned to the item on the document or journal line.;
                      ApplicationArea=#ItemTracking;
                      Image=ItemTrackingLines;
                      OnAction=BEGIN
                                 OpenItemTrackingLines;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      Name=ActionContainer1900000004;
                      ActionContainerType=ActionItems }
      { 23      ;1   ;ActionGroup;
                      Name=F&unctions;
                      CaptionML=ENU=F&unctions;
                      Image=Action }
      { 24      ;2   ;Action    ;
                      Name=Reserve;
                      CaptionML=ENU=&Reserve;
                      ToolTipML=ENU=Reserve the quantity that is required on the document line that you opened this window for.;
                      ApplicationArea=#Advanced;
                      Image=Reserve;
                      OnAction=BEGIN
                                 ReservEntry.LOCKTABLE;
                                 UpdateReservMgt;
                                 ReservMgt.PurchLineUpdateValues(Rec,QtyToReserve,QtyToReserveBase,QtyReservedThisLine,QtyReservedThisLineBase);
                                 ReservMgt.CalculateRemainingQty(NewQtyReservedThisLine,NewQtyReservedThisLineBase);
                                 ReservMgt.CopySign(NewQtyReservedThisLineBase,QtyToReserveBase);
                                 ReservMgt.CopySign(NewQtyReservedThisLine,QtyToReserve);
                                 if NewQtyReservedThisLineBase <> 0 then
                                   if ABS(NewQtyReservedThisLineBase) > ABS(QtyToReserveBase) then
                                     CreateReservation(QtyToReserve,QtyToReserveBase)
                                   else
                                     CreateReservation(NewQtyReservedThisLine,NewQtyReservedThisLineBase)
                                 else
                                   ERROR(Text000);
                               END;
                                }
      { 25      ;2   ;Action    ;
                      Name=CancelReservation;
                      AccessByPermission=TableData Item=R;
                      CaptionML=ENU=&Cancel Reservation;
                      ToolTipML=ENU=Cancel the reservation that exists for the document line that you opened this window for.;
                      ApplicationArea=#Advanced;
                      Image=Cancel;
                      OnAction=BEGIN
                                 if not CONFIRM(Text001,false) then
                                   exit;

                                 ReservEntry2.COPY(ReservEntry);
                                 ReservePurchLine.FilterReservFor(ReservEntry2,Rec);

                                 if ReservEntry2.FIND('-') then begin
                                   UpdateReservMgt;
                                   repeat
                                     ReservEngineMgt.CancelReservation(ReservEntry2);
                                   until ReservEntry2.NEXT = 0;

                                   UpdateReservFrom;
                                 end;
                               END;
                                }
      { 21      ;2   ;Action    ;
                      Name=ShowDocument;
                      ShortCutKey=Shift+F7;
                      CaptionML=ENU=&Show Document;
                      ToolTipML=ENU=Open the document that the information on the line comes from.;
                      ApplicationArea=#Planning;
                      Image=View;
                      OnAction=VAR
                                 PageManagement@1000 : Codeunit "Page Management";
                               BEGIN
                                 PurchHeader.GET("Document Type","Document No.");
                                 PageManagement.PageRun(PurchHeader);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Name=Document Type;
                ToolTipML=ENU=Specifies the type of document that you are about to create.;
                ApplicationArea=#Advanced;
                SourceExpr="Document Type";
                ImplicitType=Option }

    { 4   ;2   ;Field     ;
                Name=Document No.;
                ToolTipML=ENU=Specifies the document number.;
                ApplicationArea=#Advanced;
                SourceExpr="Document No.";
                ImplicitType=Code20 }

    { 6   ;2   ;Field     ;
                Name=Location Code;
                ToolTipML=ENU=Specifies the code for the location where the items on the line will be located.;
                ApplicationArea=#Advanced;
                SourceExpr="Location Code";
                ImplicitType=Code10 }

    { 8   ;2   ;Field     ;
                Name=Expected Receipt Date;
                ToolTipML=ENU=Specifies the date you expect the items to be available in your warehouse.;
                ApplicationArea=#Advanced;
                SourceExpr="Expected Receipt Date";
                ImplicitType=Date }

    { 10  ;2   ;Field     ;
                Name=Outstanding Qty. (Base);
                ToolTipML=ENU=Specifies the outstanding quantity expressed in the base units of measure.;
                ApplicationArea=#Advanced;
                SourceExpr="Outstanding Qty. (Base)";
                ImplicitType=Decimal }

    { 12  ;2   ;Field     ;
                Name=Reserved Qty. (Base);
                ToolTipML=ENU=Specifies the reserved quantity of the item expressed in base units of measure.;
                ApplicationArea=#Advanced;
                SourceExpr="Reserved Qty. (Base)";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 17  ;2   ;Field     ;
                Name=QtyToReserveBase;
                CaptionML=ENU=Available Quantity;
                ToolTipML=ENU=Specifies the quantity of the item that is available for reservation.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=QtyToReserveBase;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 19  ;2   ;Field     ;
                Name=ReservedThisLine;
                CaptionML=ENU=Current Reserved Quantity;
                ToolTipML=ENU=Specifies the quantity of the item that is reserved from the purchase line, for the current line or entry.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=ReservedThisLine;
                OnDrillDown=BEGIN
                              ReservEntry2.RESET;
                              ReservePurchLine.FilterReservFor(ReservEntry2,Rec);
                              ReservEntry2.SETRANGE("Reservation Status",ReservEntry2."Reservation Status"::Reservation);
                              ReservMgt.MarkReservConnection(ReservEntry2,ReservEntry);
                              PAGE.RUNMODAL(PAGE::"Reservation Entries",ReservEntry2);
                              UpdateReservFrom;
                              CurrPage.UPDATE;
                            END;
                             }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Fully reserved.';
      Text001@1001 : TextConst 'ENU=Do you want to cancel the reservation?';
      Text003@1002 : TextConst 'ENU=Available Quantity is %1.';
      ReservEntry@1003 : Record "Reservation Entry";
      ReservEntry2@1004 : Record "Reservation Entry";
      PurchHeader@1005 : Record "Purchase Header";
      SalesLine@1006 : Record "Sales Line";
      PurchLine@1007 : Record "Purchase Line";
      ReqLine@1009 : Record "Requisition Line";
      ProdOrderLine@1010 : Record "Prod. Order Line";
      ProdOrderComp@1011 : Record "Prod. Order Component";
      PlanningComponent@1012 : Record "Planning Component";
      TransLine@1013 : Record "Transfer Line";
      ServiceInvLine@1014 : Record "Service Line";
      JobPlanningLine@1033 : Record "Job Planning Line";
      AssemblyLine@1037 : Record "Assembly Line";
      AssemblyHeader@1036 : Record "Assembly Header";
      AssemblyLineReserve@1035 : Codeunit "Assembly Line-Reserve";
      AssemblyHeaderReserve@1034 : Codeunit "Assembly Header-Reserve";
      ReservMgt@1015 : Codeunit "Reservation Management";
      ReservEngineMgt@1016 : Codeunit "Reservation Engine Mgt.";
      ReserveSalesLine@1017 : Codeunit "Sales Line-Reserve";
      ReserveReqLine@1018 : Codeunit "Req. Line-Reserve";
      ReservePurchLine@1019 : Codeunit "Purch. Line-Reserve";
      ReserveProdOrderLine@1021 : Codeunit "Prod. Order Line-Reserve";
      ReserveProdOrderComp@1022 : Codeunit "Prod. Order Comp.-Reserve";
      ReservePlanningComponent@1023 : Codeunit "Plng. Component-Reserve";
      ReserveTransLine@1024 : Codeunit "Transfer Line-Reserve";
      ReserveServiceInvLine@1025 : Codeunit "Service Line-Reserve";
      JobPlanningLineReserve@1032 : Codeunit "Job Planning Line-Reserve";
      QtyToReserve@1020 : Decimal;
      QtyToReserveBase@1026 : Decimal;
      QtyReservedThisLine@1038 : Decimal;
      QtyReservedThisLineBase@1027 : Decimal;
      NewQtyReservedThisLine@1008 : Decimal;
      NewQtyReservedThisLineBase@1028 : Decimal;
      CaptionText@1029 : Text[80];
      Direction@1030 : 'Outbound,Inbound';
      CurrentSubType@1031 : Option;

    [External]
    PROCEDURE SetSalesLine@24(VAR CurrentSalesLine@1000 : Record "Sales Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentSalesLine.TESTFIELD(Type,CurrentSalesLine.Type::Item);
      SalesLine := CurrentSalesLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetSalesLine(SalesLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveSalesLine.FilterReservFor(ReservEntry,SalesLine);
      CaptionText := ReserveSalesLine.Caption(SalesLine);
    END;

    [External]
    PROCEDURE SetReqLine@23(VAR CurrentReqLine@1000 : Record "Requisition Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      ReqLine := CurrentReqLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetReqLine(ReqLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveReqLine.FilterReservFor(ReservEntry,ReqLine);
      CaptionText := ReserveReqLine.Caption(ReqLine);
    END;

    [External]
    PROCEDURE SetPurchLine@22(VAR CurrentPurchLine@1000 : Record "Purchase Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentPurchLine.TESTFIELD(Type,CurrentPurchLine.Type::Item);
      PurchLine := CurrentPurchLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetPurchLine(PurchLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReservePurchLine.FilterReservFor(ReservEntry,PurchLine);
      CaptionText := ReservePurchLine.Caption(PurchLine);
    END;

    [External]
    PROCEDURE SetProdOrderLine@19(VAR CurrentProdOrderLine@1000 : Record "Prod. Order Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      ProdOrderLine := CurrentProdOrderLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetProdOrderLine(ProdOrderLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveProdOrderLine.FilterReservFor(ReservEntry,ProdOrderLine);
      CaptionText := ReserveProdOrderLine.Caption(ProdOrderLine);
    END;

    [External]
    PROCEDURE SetProdOrderComponent@1(VAR CurrentProdOrderComp@1000 : Record "Prod. Order Component";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      ProdOrderComp := CurrentProdOrderComp;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetProdOrderComponent(ProdOrderComp);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveProdOrderComp.FilterReservFor(ReservEntry,ProdOrderComp);
      CaptionText := ReserveProdOrderComp.Caption(ProdOrderComp);
    END;

    [External]
    PROCEDURE SetPlanningComponent@15(VAR CurrentPlanningComponent@1000 : Record "Planning Component";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      PlanningComponent := CurrentPlanningComponent;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetPlanningComponent(PlanningComponent);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReservePlanningComponent.FilterReservFor(ReservEntry,PlanningComponent);
      CaptionText := ReservePlanningComponent.Caption(PlanningComponent);
    END;

    [External]
    PROCEDURE SetTransferLine@16(VAR CurrentTransLine@1000 : Record "Transfer Line";CurrentReservEntry@1001 : Record "Reservation Entry";CurrDirection@1002 : 'Outbound,Inbound');
    BEGIN
      TransLine := CurrentTransLine;
      ReservEntry := CurrentReservEntry;
      Direction := CurrDirection;

      CLEAR(ReservMgt);
      ReservMgt.SetTransferLine(TransLine,Direction);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveTransLine.FilterReservFor(ReservEntry,TransLine,Direction);
      CaptionText := ReserveTransLine.Caption(TransLine);
    END;

    [External]
    PROCEDURE SetServiceInvLine@8(VAR CurrentServiceInvLine@1000 : Record "Service Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentServiceInvLine.TESTFIELD(Type,CurrentServiceInvLine.Type::Item);
      ServiceInvLine := CurrentServiceInvLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetServLine(ServiceInvLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveServiceInvLine.FilterReservFor(ReservEntry,ServiceInvLine);
      CaptionText := ReserveServiceInvLine.Caption(ServiceInvLine);
    END;

    [External]
    PROCEDURE SetJobPlanningLine@3(VAR CurrentJobPlanningLine@1000 : Record "Job Planning Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentJobPlanningLine.TESTFIELD(Type,CurrentJobPlanningLine.Type::Item);
      JobPlanningLine := CurrentJobPlanningLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetJobPlanningLine(JobPlanningLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      JobPlanningLineReserve.FilterReservFor(ReservEntry,JobPlanningLine);
      CaptionText := JobPlanningLineReserve.Caption(JobPlanningLine);
    END;

    LOCAL PROCEDURE CreateReservation@14(ReservedQuantity@1001 : Decimal;ReserveQuantityBase@1000 : Decimal);
    VAR
      TrackingSpecification@1002 : Record "Tracking Specification";
    BEGIN
      CALCFIELDS("Reserved Qty. (Base)");
      if (ABS("Outstanding Qty. (Base)") - ABS("Reserved Qty. (Base)")) < ReserveQuantityBase then
        ERROR(Text003,ABS("Outstanding Qty. (Base)") - "Reserved Qty. (Base)");

      TESTFIELD("Job No.",'');
      TESTFIELD("Drop Shipment",false);
      TESTFIELD("No.",ReservEntry."Item No.");
      TESTFIELD("Variant Code",ReservEntry."Variant Code");
      TESTFIELD("Location Code",ReservEntry."Location Code");

      UpdateReservMgt;
      TrackingSpecification.InitTrackingSpecification2(
        DATABASE::"Purchase Line","Document Type","Document No.",'',0,"Line No.",
        "Variant Code","Location Code","Qty. per Unit of Measure");
      ReservMgt.CreateReservation(
        ReservEntry.Description,"Expected Receipt Date",ReservedQuantity,ReserveQuantityBase,TrackingSpecification);
      UpdateReservFrom;
    END;

    LOCAL PROCEDURE UpdateReservFrom@17();
    BEGIN
      case ReservEntry."Source Type" of
        DATABASE::"Sales Line":
          begin
            SalesLine.FIND;
            SetSalesLine(SalesLine,ReservEntry);
          end;
        DATABASE::"Requisition Line":
          begin
            ReqLine.FIND;
            SetReqLine(ReqLine,ReservEntry);
          end;
        DATABASE::"Purchase Line":
          begin
            PurchLine.FIND;
            SetPurchLine(PurchLine,ReservEntry);
          end;
        DATABASE::"Prod. Order Line":
          begin
            ProdOrderLine.FIND;
            SetProdOrderLine(ProdOrderLine,ReservEntry);
          end;
        DATABASE::"Prod. Order Component":
          begin
            ProdOrderComp.FIND;
            SetProdOrderComponent(ProdOrderComp,ReservEntry);
          end;
        DATABASE::"Planning Component":
          begin
            PlanningComponent.FIND;
            SetPlanningComponent(PlanningComponent,ReservEntry);
          end;
        DATABASE::"Transfer Line":
          begin
            TransLine.FIND;
            SetTransferLine(TransLine,ReservEntry,ReservEntry."Source Subtype");
          end;
        DATABASE::"Service Line":
          begin
            ServiceInvLine.FIND;
            SetServiceInvLine(ServiceInvLine,ReservEntry);
          end;
        DATABASE::"Job Planning Line":
          begin
            JobPlanningLine.FIND;
            SetJobPlanningLine(JobPlanningLine,ReservEntry);
          end;
      end;
    END;

    LOCAL PROCEDURE UpdateReservMgt@13();
    BEGIN
      CLEAR(ReservMgt);
      case ReservEntry."Source Type" of
        DATABASE::"Sales Line":
          ReservMgt.SetSalesLine(SalesLine);
        DATABASE::"Requisition Line":
          ReservMgt.SetReqLine(ReqLine);
        DATABASE::"Purchase Line":
          ReservMgt.SetPurchLine(PurchLine);
        DATABASE::"Prod. Order Line":
          ReservMgt.SetProdOrderLine(ProdOrderLine);
        DATABASE::"Prod. Order Component":
          ReservMgt.SetProdOrderComponent(ProdOrderComp);
        DATABASE::"Planning Component":
          ReservMgt.SetPlanningComponent(PlanningComponent);
        DATABASE::"Transfer Line":
          ReservMgt.SetTransferLine(TransLine,ReservEntry."Source Subtype");
        DATABASE::"Service Line":
          ReservMgt.SetServLine(ServiceInvLine);
        DATABASE::"Job Planning Line":
          ReservMgt.SetJobPlanningLine(JobPlanningLine);
      end;
    END;

    LOCAL PROCEDURE ReservedThisLine@2() : Decimal;
    BEGIN
      ReservEntry2.RESET;
      if ReservEntry."Source Type" = DATABASE::"Transfer Line" then
        ReservEntry."Source Subtype" := Direction;
      ReservePurchLine.FilterReservFor(ReservEntry2,Rec);
      ReservEntry2.SETRANGE("Reservation Status",ReservEntry2."Reservation Status"::Reservation);
      exit(ReservMgt.MarkReservConnection(ReservEntry2,ReservEntry));
    END;

    [External]
    PROCEDURE SetCurrentSubType@9(SubType@1000 : Option);
    BEGIN
      CurrentSubType := SubType;
    END;

    [External]
    PROCEDURE SetAssemblyLine@4(VAR CurrentAsmLine@1002 : Record "Assembly Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentAsmLine.TESTFIELD(Type,CurrentAsmLine.Type::Item);
      AssemblyLine := CurrentAsmLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetAssemblyLine(AssemblyLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      AssemblyLineReserve.FilterReservFor(ReservEntry,AssemblyLine);
      CaptionText := AssemblyLineReserve.Caption(AssemblyLine);
    END;

    [External]
    PROCEDURE SetAssemblyHeader@7(VAR CurrentAsmHeader@1000 : Record "Assembly Header";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      AssemblyHeader := CurrentAsmHeader;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetAssemblyHeader(AssemblyHeader);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      AssemblyHeaderReserve.FilterReservFor(ReservEntry,AssemblyHeader);
      CaptionText := AssemblyHeaderReserve.Caption(AssemblyHeader);
    END;

    BEGIN
    END.
  }
}

