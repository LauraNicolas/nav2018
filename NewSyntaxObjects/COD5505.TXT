OBJECT Codeunit 5505 Graph Mgt - Sales Quote
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ProcessComplexTypes@1(VAR SalesQuoteEntityBuffer@1000 : Record "Sales Quote Entity Buffer";BillToAddressJSON@1001 : Text);
    BEGIN
      ParseBillToCustomerAddressFromJSON(BillToAddressJSON,SalesQuoteEntityBuffer);
    END;

    PROCEDURE ParseBillToCustomerAddressFromJSON@10(BillToAddressJSON@1000 : Text;VAR SalesQuoteEntityBuffer@1002 : Record "Sales Quote Entity Buffer");
    VAR
      GraphMgtComplexTypes@1003 : Codeunit "Graph Mgt - Complex Types";
      RecRef@1001 : RecordRef;
    BEGIN
      if BillToAddressJSON <> '' then
        with SalesQuoteEntityBuffer do begin
          RecRef.GETTABLE(SalesQuoteEntityBuffer);
          GraphMgtComplexTypes.ApplyPostalAddressFromJSON(BillToAddressJSON,RecRef,
            FIELDNO("Sell-to Address"),FIELDNO("Sell-to Address 2"),FIELDNO("Sell-to City"),FIELDNO("Sell-to County"),
            FIELDNO("Sell-to Country/Region Code"),FIELDNO("Sell-to Post Code"));
          RecRef.SETTABLE(SalesQuoteEntityBuffer);
        end;
    END;

    PROCEDURE BillToCustomerAddressToJSON@5(SalesQuoteEntityBuffer@1000 : Record "Sales Quote Entity Buffer") JSON : Text;
    VAR
      GraphMgtComplexTypes@1001 : Codeunit "Graph Mgt - Complex Types";
    BEGIN
      with SalesQuoteEntityBuffer do
        GraphMgtComplexTypes.GetPostalAddressJSON("Sell-to Address","Sell-to Address 2",
          "Sell-to City","Sell-to County","Sell-to Country/Region Code","Sell-to Post Code",JSON);
    END;

    PROCEDURE UpdateIntegrationRecordIds@34(OnlyRecordsWithoutID@1007 : Boolean);
    VAR
      DummySalesQuoteEntityBuffer@1001 : Record "Sales Quote Entity Buffer";
      DummyCustomer@1005 : Record Customer;
      GraphMgtGeneralTools@1008 : Codeunit "Graph Mgt - General Tools";
      SalesQuoteEntityBufferRecordRef@1000 : RecordRef;
      CustomerRecordRef@1004 : RecordRef;
    BEGIN
      CustomerRecordRef.OPEN(DATABASE::Customer);
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        CustomerRecordRef,DummyCustomer.FIELDNO(Id),true);

      SalesQuoteEntityBufferRecordRef.OPEN(DATABASE::"Sales Header");
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        SalesQuoteEntityBufferRecordRef,DummySalesQuoteEntityBuffer.FIELDNO(Id),OnlyRecordsWithoutID);
    END;

    LOCAL PROCEDURE CheckSupportedRcords@3(VAR RecRef@1000 : RecordRef) : Boolean;
    VAR
      SalesHeader@1002 : Record "Sales Header";
    BEGIN
      case RecRef.NUMBER of
        DATABASE::"Sales Header":
          begin
            RecRef.SETTABLE(SalesHeader);
            exit(SalesHeader."Document Type" = SalesHeader."Document Type"::Quote);
          end;
        else
          exit(false);
      end;
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnSalesQuoteHeader@19(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummySalesQuoteEntityBuffer@1004 : Record "Sales Quote Entity Buffer";
      GraphMgtGeneralTools@1003 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      if not CheckSupportedRcords(RecRef) then
        exit;

      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(
        RecRef,NewId,Handled,RecRef.NUMBER,DummySalesQuoteEntityBuffer.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@21(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummySalesQuoteEntityBuffer@1004 : Record "Sales Quote Entity Buffer";
      GraphMgtGeneralTools@1007 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      if not CheckSupportedRcords(RecRef) then
        exit;

      GraphMgtGeneralTools.HandleGetPredefinedIdValue(
        Id,RecRef,Handled,RecRef.NUMBER,DummySalesQuoteEntityBuffer.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@16();
    VAR
      GraphMgtSalesQuoteBuffer@1000 : Codeunit "Graph Mgt - Sales Quote Buffer";
    BEGIN
      UpdateIntegrationRecordIds(false);
      GraphMgtSalesQuoteBuffer.UpdateBufferTableRecords;
    END;

    BEGIN
    END.
  }
}

