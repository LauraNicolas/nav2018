OBJECT Codeunit 242 Item Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo="Item Journal Line";
    OnRun=BEGIN
            ItemJnlLine.COPY(Rec);
            Code;
            COPY(ItemJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU="The journal lines were successfully posted. "';
      Text005@1005 : TextConst 'ENU=You are now in the %1 journal.';
      ItemJnlTemplate@1006 : Record "Item Journal Template";
      ItemJnlLine@1007 : Record "Item Journal Line";
      ItemReg@1008 : Record "Item Register";
      WhseReg@1011 : Record "Warehouse Register";
      ItemJnlPostBatch@1009 : Codeunit "Item Jnl.-Post Batch";
      TempJnlBatchName@1010 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      with ItemJnlLine do begin
        ItemJnlTemplate.GET("Journal Template Name");
        ItemJnlTemplate.TESTFIELD("Posting Report ID");
        if ItemJnlTemplate.Recurring and (GETFILTER("Posting Date") <> '') then
          FIELDERROR("Posting Date",Text000);

        if not CONFIRM(Text001,false) then
          exit;

        TempJnlBatchName := "Journal Batch Name";

        ItemJnlPostBatch.RUN(ItemJnlLine);

        if ItemReg.GET(ItemJnlPostBatch.GetItemRegNo) then begin
          ItemReg.SETRECFILTER;
          REPORT.RUN(ItemJnlTemplate."Posting Report ID",false,false,ItemReg);
        end;

        if WhseReg.GET(ItemJnlPostBatch.GetWhseRegNo) then begin
          WhseReg.SETRECFILTER;
          REPORT.RUN(ItemJnlTemplate."Whse. Register Report ID",false,false,WhseReg);
        end;

        if (ItemJnlPostBatch.GetItemRegNo = 0) and
           (ItemJnlPostBatch.GetWhseRegNo = 0)
        then
          MESSAGE(Text002)
        else
          if TempJnlBatchName = "Journal Batch Name" then
            MESSAGE(Text003)
          else
            MESSAGE(
              Text004 +
              Text005,
              "Journal Batch Name");

        if not FIND('=><') or (TempJnlBatchName <> "Journal Batch Name") then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

