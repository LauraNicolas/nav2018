OBJECT Table 2105 O365 Payment History Buffer
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=O365 Payment History Buffer;
  }
  FIELDS
  {
    { 1   ;   ;Ledger Entry No.    ;Integer       ;TableRelation="G/L Entry";
                                                   CaptionML=ENU=Ledger Entry No. }
    { 2   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                   OptionString=" ",Payment,Invoice,"Credit Memo","Finance Charge Memo",Reminder,Refund }
    { 3   ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   AutoFormatType=10;
                                                   AutoFormatExpr='1' }
    { 4   ;   ;Date Received       ;Date          ;CaptionML=ENU=Date Received }
    { 5   ;   ;Payment Method      ;Code10        ;CaptionML=ENU=Payment Method }
  }
  KEYS
  {
    {    ;"Ledger Entry No."                      ;Clustered=true }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;"Date Received",Type,Amount,"Payment Method" }
  }
  CODE
  {
    VAR
      CanOnlyCancelPaymentsErr@1002 : TextConst 'ENU=Only payment registrations can be canceled.';
      CanOnlyCancelLastPaymentErr@1001 : TextConst 'ENU=Only the last payment registration can be canceled.';
      DevMsgNotTemporaryErr@1000 : TextConst 'ENU=This function can only be used when the record is temporary.';
      O365SalesInvoicePayment@1003 : Codeunit "O365 Sales Invoice Payment";
      MarkAsUnpaidConfirmQst@1004 : TextConst 'ENU=Cancel this payment registration?';

    [External]
    PROCEDURE FillPaymentHistory@1(SalesInvoiceDocumentNo@1000 : Code[20];IncludeSalesInvoice@1001 : Boolean);
    VAR
      InvoiceCustLedgerEntry@1002 : Record "Cust. Ledger Entry";
      PaymentCustLedgerEntry@1003 : Record "Cust. Ledger Entry";
    BEGIN
      if not ISTEMPORARY then
        ERROR(DevMsgNotTemporaryErr);

      RESET;
      DELETEALL;
      InvoiceCustLedgerEntry.SETRANGE("Document Type",InvoiceCustLedgerEntry."Document Type"::Invoice);
      InvoiceCustLedgerEntry.SETRANGE("Document No.",SalesInvoiceDocumentNo);
      if not InvoiceCustLedgerEntry.FINDFIRST then
        exit;

      if IncludeSalesInvoice then
        CopyFromCustomerLedgerEntry(InvoiceCustLedgerEntry);

      if PaymentCustLedgerEntry.GET(InvoiceCustLedgerEntry."Closed by Entry No.") then
        CopyFromCustomerLedgerEntry(PaymentCustLedgerEntry);

      PaymentCustLedgerEntry.SETCURRENTKEY("Closed by Entry No.");
      PaymentCustLedgerEntry.SETRANGE("Closed by Entry No.",InvoiceCustLedgerEntry."Entry No.");
      if PaymentCustLedgerEntry.FINDSET then
        repeat
          CopyFromCustomerLedgerEntry(PaymentCustLedgerEntry);
        until PaymentCustLedgerEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyFromCustomerLedgerEntry@6(CustLedgerEntry@1000 : Record "Cust. Ledger Entry");
    BEGIN
      CustLedgerEntry.CALCFIELDS("Amount (LCY)");
      INIT;
      "Ledger Entry No." := CustLedgerEntry."Entry No.";
      Type := CustLedgerEntry."Document Type";
      Amount := CustLedgerEntry."Amount (LCY)";
      if Type = Type::Payment then
        Amount := -Amount;
      "Date Received" := CustLedgerEntry."Posting Date";
      "Payment Method" := CustLedgerEntry."Payment Method Code";
      INSERT(true);
    END;

    [Internal]
    PROCEDURE CancelPayment@2() : Boolean;
    VAR
      TempO365PaymentHistoryBuffer@1000 : TEMPORARY Record "O365 Payment History Buffer";
    BEGIN
      if Type <> Type::Payment then
        ERROR(CanOnlyCancelPaymentsErr);
      TempO365PaymentHistoryBuffer.COPY(Rec,true);
      TempO365PaymentHistoryBuffer.SETFILTER("Ledger Entry No.",'>%1',"Ledger Entry No.");
      if not TempO365PaymentHistoryBuffer.ISEMPTY then
        ERROR(CanOnlyCancelLastPaymentErr);
      if not CONFIRM(MarkAsUnpaidConfirmQst) then
        exit(false);

      O365SalesInvoicePayment.CancelCustLedgerEntry("Ledger Entry No.");
      exit(true);
    END;

    BEGIN
    END.
  }
}

