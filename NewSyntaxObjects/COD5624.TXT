OBJECT Codeunit 5624 Cancel FA Ledger Entries
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=must be the same in all canceled ledger entries';
      Text002@1001 : TextConst 'ENU="%1 = %2 has already been canceled."';
      Text003@1002 : TextConst 'ENU=The ledger entries have been transferred to the journal.';
      Text004@1003 : TextConst 'ENU="%1 = %2 cannot be canceled. Use %3 = %4."';
      FAJnlSetup@1005 : Record "FA Journal Setup";
      DeprBook@1006 : Record "Depreciation Book";
      GenJnlLine@1007 : Record "Gen. Journal Line";
      FAJnlLine@1008 : Record "FA Journal Line";
      FA@1009 : Record "Fixed Asset";
      GLIntegration@1010 : ARRAY [9] OF Boolean;
      FAJnlNextLineNo@1011 : Integer;
      GenJnlNextLineNo@1012 : Integer;
      DeprBookCode@1013 : Code[10];
      GenJnlUsedOnce@1014 : Boolean;
      FAJnlUsedOnce@1015 : Boolean;
      FAJnlDocumentNo@1016 : Code[20];
      GenJnlDocumentNo@1017 : Code[20];

    [External]
    PROCEDURE TransferLine@1(VAR FALedgEntry@1000 : Record "FA Ledger Entry";BalAccount@1001 : Boolean;NewPostingDate@1002 : Date);
    BEGIN
      CLEARALL;
      with FALedgEntry do begin
        if FIND('+') then
          repeat
            if DeprBookCode = '' then
              DeprBookCode := "Depreciation Book Code";
            if DeprBookCode <> "Depreciation Book Code" then
              FIELDERROR("Depreciation Book Code",Text001);
            if "FA No." = '' then
              ERROR(Text002,FIELDCAPTION("Entry No."),"Entry No.");
            FA.GET("FA No.");
            DeprBook.GET("Depreciation Book Code");
            DeprBook.IndexGLIntegration(GLIntegration);
            CheckType(FALedgEntry);
            if NewPostingDate > 0D then begin
              "Posting Date" := NewPostingDate;
              DeprBook.TESTFIELD("Use Same FA+G/L Posting Dates",false);
            end;
            if GLIntegration[ConvertPostingType + 1] and not FA."Budgeted Asset" then
              InsertGenJnlLine(FALedgEntry,BalAccount)
            else
              InsertFAJnlLine(FALedgEntry);
          until NEXT(-1) = 0;
      end;
      MESSAGE(Text003);
    END;

    LOCAL PROCEDURE CheckType@4(VAR FALedgEntry@1000 : Record "FA Ledger Entry");
    BEGIN
      with FALedgEntry do
        if ("FA Posting Type" > "FA Posting Type"::"Salvage Value") or
           ("FA Posting Category" <> "FA Posting Category"::" ")
        then begin
          "FA Posting Type" := "FA Posting Type"::"Proceeds on Disposal";
          ERROR(
            Text004,
            FIELDCAPTION("Entry No."),"Entry No.",FIELDCAPTION("FA Posting Type"),"FA Posting Type");
        end;
    END;

    LOCAL PROCEDURE InsertFAJnlLine@6(VAR FALedgEntry@1000 : Record "FA Ledger Entry");
    BEGIN
      if not FAJnlUsedOnce then begin;
        FAJnlLine.LOCKTABLE;
        FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
        FAJnlUsedOnce := true;
        FAJnlDocumentNo :=
          FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,FALedgEntry."FA Posting Date",false);
      end;

      FALedgEntry.MoveToFAJnl(FAJnlLine);
      with FAJnlLine do begin
        "Document No." := FAJnlDocumentNo;
        "Document Type" := "Document Type"::" ";
        "External Document No." := '';
        "Shortcut Dimension 1 Code" := FALedgEntry."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := FALedgEntry."Global Dimension 2 Code";
        "Dimension Set ID" := FALedgEntry."Dimension Set ID";
        "FA Error Entry No." := FALedgEntry."Entry No.";
        "Posting No. Series" := FAJnlSetup.GetFANoSeries(FAJnlLine);
        VALIDATE(Amount,-Amount);
        VALIDATE(Correction,DeprBook."Mark Errors as Corrections");
        "Line No." := "Line No." + 10000;
        INSERT(true);
      end;
    END;

    LOCAL PROCEDURE InsertGenJnlLine@7(VAR FALedgEntry@1000 : Record "FA Ledger Entry";BalAccount@1001 : Boolean);
    VAR
      FAInsertGLAcc@1002 : Codeunit "FA Insert G/L Account";
    BEGIN
      if not GenJnlUsedOnce then begin;
        GenJnlLine.LOCKTABLE;
        FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
        GenJnlUsedOnce := true;
        GenJnlDocumentNo :=
          FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,FALedgEntry."FA Posting Date",false);
      end;

      FALedgEntry.MoveToGenJnl(GenJnlLine);
      with GenJnlLine do begin
        "Document No." := GenJnlDocumentNo;
        "Document Type" := "Document Type"::" ";
        "External Document No." := '';
        "Shortcut Dimension 1 Code" := FALedgEntry."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := FALedgEntry."Global Dimension 2 Code";
        "Dimension Set ID" := FALedgEntry."Dimension Set ID";
        "FA Error Entry No." := FALedgEntry."Entry No.";
        VALIDATE(Amount,-Amount);
        VALIDATE(Correction,DeprBook."Mark Errors as Corrections");
        "Posting No. Series" := FAJnlSetup.GetGenNoSeries(GenJnlLine);
        "Line No." := "Line No." + 10000;
        INSERT(true);
        if BalAccount then
          FAInsertGLAcc.GetBalAcc(GenJnlLine);
      end;
    END;

    BEGIN
    END.
  }
}

