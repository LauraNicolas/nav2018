OBJECT Page 1604 Office New Contact Dlg
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Do you want to add a new contact?;
    InsertAllowed=false;
    DeleteAllowed=false;
    SourceTable=Contact;
    SourceTableTemporary=true;
    OnOpenPage=VAR
                 OfficeMgt@1000 : Codeunit "Office Management";
               BEGIN
                 OfficeMgt.GetContext(TempOfficeAddinContext);
               END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Dialog;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group2;
                GroupType=Group;
                InstructionalTextML=ENU=The sender of this email is not among your contacts.;
                Layout=Rows }

    { 5   ;2   ;Field     ;
                Name=NewPersonContact;
                ToolTipML=ENU=Specifies a new person contact.;
                ApplicationArea=#All;
                SourceExpr=STRSUBSTNO(CreatePersonContactLbl,TempOfficeAddinContext.Name);
                Editable=FALSE;
                OnDrillDown=BEGIN
                              CreateNewContact(Type::Person);
                            END;

                ShowCaption=false }

    { 4   ;2   ;Field     ;
                Name=LinkContact;
                ToolTipML=ENU=Specifies the contacts in your company.;
                ApplicationArea=#All;
                SourceExpr=LinkContactLbl;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              PAGE.RUN(PAGE::"Contact List");
                            END;

                ImplicitType=TextConst;
                ShowCaption=false }

  }
  CODE
  {
    VAR
      CreatePersonContactLbl@1001 : TextConst '@@@="%1 = Contact name";ENU=Add %1 as a contact';
      LinkContactLbl@1002 : TextConst 'ENU=View existing contacts';
      TempOfficeAddinContext@1000 : TEMPORARY Record "Office Add-in Context";

    LOCAL PROCEDURE NotLinked@1(Contact@1000 : Record Contact) : Boolean;
    VAR
      ContBusRel@1002 : Record "Contact Business Relation";
    BEGIN
      // Person could be linked directly or through Company No.
      ContBusRel.SETFILTER("Contact No.",'%1|%2',Contact."No.",Contact."Company No.");
      ContBusRel.SETFILTER("No.",'<>''''');
      exit(ContBusRel.ISEMPTY);
    END;

    LOCAL PROCEDURE CreateNewContact@3(ContactType@1003 : Option);
    VAR
      TempContact@1002 : TEMPORARY Record Contact;
      Contact@1001 : Record Contact;
      NameLength@1000 : Integer;
    BEGIN
      Contact.SETRANGE("Search E-Mail",TempOfficeAddinContext.Email);
      if not Contact.FINDFIRST then begin
        NameLength := 50;
        if STRPOS(TempOfficeAddinContext.Name,' ') = 0 then
          NameLength := 30;
        TempContact.INIT;
        TempContact.VALIDATE(Type,ContactType);
        TempContact.VALIDATE(Name,COPYSTR(TempOfficeAddinContext.Name,1,NameLength));
        TempContact.VALIDATE("E-Mail",TempOfficeAddinContext.Email);
        TempContact.INSERT;
        COMMIT;
      end;

      if ACTION::LookupOK = PAGE.RUNMODAL(PAGE::"Office Contact Details Dlg",TempContact) then begin
        CLEAR(Contact);
        Contact.TRANSFERFIELDS(TempContact);
        Contact.INSERT(true);
        COMMIT;
        if NotLinked(Contact) then
          PAGE.RUN(PAGE::"Contact Card",Contact)
        else
          Contact.ShowCustVendBank;
        CurrPage.CLOSE;
      end;
    END;

    BEGIN
    END.
  }
}

