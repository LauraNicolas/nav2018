OBJECT Report 750 Save as Standard Gen. Journal
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Save as Standard Gen. Journal;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  if Code = '' then
                    ERROR(Text000);

                  StdJournalCreated := false;
                  SaveGenJnlAsStandardJnl;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 2   ;2   ;Field     ;
                  Name=Code;
                  Lookup=true;
                  CaptionML=ENU=Code;
                  ToolTipML=ENU=Specifies the code for the journal.;
                  ApplicationArea=#Suite;
                  NotBlank=true;
                  SourceExpr=Code;
                  OnLookup=VAR
                             StdGenJnl@1001 : Record "Standard General Journal";
                             StdGenJnls@1000 : Page "Standard General Journals";
                           BEGIN
                             StdGenJnl.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
                             StdGenJnls.SETTABLEVIEW(StdGenJnl);

                             StdGenJnls.LOOKUPMODE := true;
                             StdGenJnls.EDITABLE := false;
                             if StdGenJnls.RUNMODAL = ACTION::LookupOK then begin
                               StdGenJnls.GETRECORD(StdGenJnl);
                               Code := StdGenJnl.Code;
                               Description := StdGenJnl.Description;
                             end;
                           END;
                            }

      { 4   ;2   ;Field     ;
                  Name=Description;
                  CaptionML=ENU=Description;
                  ToolTipML=ENU=Specifies a description of the journal.;
                  ApplicationArea=#Suite;
                  SourceExpr=Description }

      { 8   ;2   ;Field     ;
                  Name=Save Amount;
                  CaptionML=ENU=Save Amount;
                  ToolTipML=ENU=Specifies if you want to save the values in the Amount field of the standard journal.;
                  ApplicationArea=#Suite;
                  SourceExpr=SaveAmount }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GenJnlLine@1008 : Record "Gen. Journal Line";
      GenJnlBatch@1002 : Record "Gen. Journal Batch";
      StdGenJnl@1007 : Record "Standard General Journal";
      Code@1000 : Code[10];
      Description@1001 : Text[50];
      Text000@1004 : TextConst 'ENU=Enter a code for Standard General Journal.';
      Text001@1005 : TextConst 'ENU=Standard General Journal %1 already exists. Do you want to overwrite?';
      SaveAmount@1006 : Boolean;
      StdJournalCreated@1003 : Boolean;
      NextLineNo@1010 : Integer;

    [External]
    PROCEDURE Initialise@2(VAR SelectedGenJnlLines@1000 : Record "Gen. Journal Line";SelectedGenJnlBatch@1001 : Record "Gen. Journal Batch");
    BEGIN
      GenJnlLine.COPY(SelectedGenJnlLines);
      GenJnlBatch := SelectedGenJnlBatch;
    END;

    [External]
    PROCEDURE InitializeRequest@7(NewCode@1002 : Code[10];NewDescription@1001 : Text[50];NewSaveAmount@1000 : Boolean);
    BEGIN
      Code := NewCode;
      Description := NewDescription;
      SaveAmount := NewSaveAmount;
    END;

    LOCAL PROCEDURE SaveGenJnlAsStandardJnl@6();
    VAR
      StdGenJnlLine@1000 : Record "Standard General Journal Line";
    BEGIN
      StdGenJnl.INIT;
      StdGenJnl."Journal Template Name" := GenJnlBatch."Journal Template Name";
      StdGenJnl.Code := Code;
      StdGenJnl.Description := Description;

      if StdGenJnlExists then
        if not CONFIRM(Text001,false,StdGenJnl.Code) then
          exit;

      StdGenJnlLine.LOCKTABLE;
      StdGenJnl.LOCKTABLE;

      if StdGenJnlExists then begin
        StdGenJnl.MODIFY(true);
        StdGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
        StdGenJnlLine.SETRANGE("Standard Journal Code",StdGenJnl.Code);
        StdGenJnlLine.DELETEALL(true);
      end else
        StdGenJnl.INSERT(true);

      NextLineNo := 10000;
      if GenJnlLine.FIND('-') then
        repeat
          StdGenJnlLine."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 10000;
          StdGenJnlLine.INIT;
          StdGenJnlLine."Journal Template Name" := StdGenJnl."Journal Template Name";
          StdGenJnlLine."Standard Journal Code" := StdGenJnl.Code;
          StdGenJnlLine.TRANSFERFIELDS(GenJnlLine,false);
          StdGenJnlLine."Shortcut Dimension 1 Code" := '';
          StdGenJnlLine."Shortcut Dimension 2 Code" := '';
          if not SaveAmount then begin
            StdGenJnlLine.Amount := 0;
            StdGenJnlLine."Debit Amount" := 0;
            StdGenJnlLine."Credit Amount" := 0;
            StdGenJnlLine."Amount (LCY)" := 0;
            StdGenJnlLine."VAT Amount" := 0;
            StdGenJnlLine."VAT Base Amount" := 0;
            StdGenJnlLine."VAT Difference" := 0;
            StdGenJnlLine."Bal. VAT Amount" := 0;
            StdGenJnlLine."Bal. VAT Base Amount" := 0;
            StdGenJnlLine."Bal. VAT Difference" := 0;
            StdGenJnlLine."Balance (LCY)" := 0;
          end;
          StdGenJnlLine.INSERT(true);
          CopyGenJnlLineDims(GenJnlLine,StdGenJnlLine);
        until GenJnlLine.NEXT = 0;

      StdJournalCreated := true;
    END;

    LOCAL PROCEDURE CopyGenJnlLineDims@3(GenJnlLine@1000 : Record "Gen. Journal Line";StdGenJnlLine@1001 : Record "Standard General Journal Line");
    VAR
      DimMgt@1004 : Codeunit DimensionManagement;
    BEGIN
      DimMgt.UpdateGlobalDimFromDimSetID(GenJnlLine."Dimension Set ID",StdGenJnlLine."Shortcut Dimension 1 Code",
        StdGenJnlLine."Shortcut Dimension 2 Code");
      StdGenJnlLine."Dimension Set ID" := GenJnlLine."Dimension Set ID";

      StdGenJnlLine.MODIFY;
    END;

    LOCAL PROCEDURE StdGenJnlExists@10() : Boolean;
    VAR
      StdGenJnl@1000 : Record "Standard General Journal";
    BEGIN
      StdGenJnl.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      StdGenJnl.SETRANGE(Code,Code);

      exit(StdGenJnl.FINDFIRST);
    END;

    [External]
    PROCEDURE GetStdGeneralJournal@1(VAR StdGenJnl1@1000 : Record "Standard General Journal") : Boolean;
    BEGIN
      if StdJournalCreated then
        StdGenJnl1.COPY(StdGenJnl);

      exit(StdJournalCreated);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

