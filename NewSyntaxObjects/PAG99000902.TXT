OBJECT Page 99000902 Item Availability Line List
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Editable=false;
    CaptionML=ENU=Item Availability Line List;
    SourceTable="Item Availability Line";
    PageType=List;
    SourceTableTemporary=true;
    OnOpenPage=BEGIN
                 DELETEALL;
                 MakeWhat;
               END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Name=Name;
                ToolTipML=ENU=Specifies the name for this entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Name;
                ImplicitType=Text50 }

    { 4   ;2   ;Field     ;
                Name=Quantity;
                ToolTipML=ENU=Specifies the quantity for this entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Quantity;
                OnDrillDown=BEGIN
                              LookupEntries;
                            END;

                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 Receipt';
      Text001@1001 : TextConst 'ENU=%1 Release';
      Text002@1002 : TextConst 'ENU=Firm planned %1';
      Text003@1003 : TextConst 'ENU=Released %1';
      Item@1004 : Record Item;
      ItemLedgerEntry@1005 : Record "Item Ledger Entry";
      SalesLine@1006 : Record "Sales Line";
      ServLine@1007 : Record "Service Line";
      JobPlanningLine@1018 : Record "Job Planning Line";
      PurchLine@1008 : Record "Purchase Line";
      TransLine@1009 : Record "Transfer Line";
      ReqLine@1010 : Record "Requisition Line";
      ProdOrderLine@1011 : Record "Prod. Order Line";
      ProdOrderComp@1012 : Record "Prod. Order Component";
      PlanningComponent@1013 : Record "Planning Component";
      AssemblyHeader@1020 : Record "Assembly Header";
      AssemblyLine@1019 : Record "Assembly Line";
      AvailType@1015 : '"Gross Requirement","Planned Order Receipt","Scheduled Order Receipt","Planned Order Release",All';
      Sign@1016 : Integer;

    [External]
    PROCEDURE Init@1(NewType@1000 : '"Gross Requirement","Planned Order Receipt","Scheduled Order Receipt","Planned Order Release",All';VAR NewItem@1001 : Record Item);
    BEGIN
      AvailType := NewType;
      Item.COPY(NewItem);
    END;

    LOCAL PROCEDURE MakeEntries@2();
    BEGIN
      case AvailType of
        AvailType::"Gross Requirement":
          begin
            InsertEntry(
              DATABASE::"Sales Line",
              Item.FIELDNO("Qty. on Sales Order"),
              SalesLine.TABLECAPTION,
              Item."Qty. on Sales Order");
            InsertEntry(
              DATABASE::"Service Line",
              Item.FIELDNO("Qty. on Service Order"),
              ServLine.TABLECAPTION,
              Item."Qty. on Service Order");
            InsertEntry(
              DATABASE::"Job Planning Line",
              Item.FIELDNO("Qty. on Job Order"),
              JobPlanningLine.TABLECAPTION,
              Item."Qty. on Job Order");
            InsertEntry(
              DATABASE::"Prod. Order Component",
              Item.FIELDNO("Scheduled Need (Qty.)"),
              ProdOrderComp.TABLECAPTION,
              Item."Scheduled Need (Qty.)");
            InsertEntry(
              DATABASE::"Planning Component",
              Item.FIELDNO("Planning Issues (Qty.)"),
              PlanningComponent.TABLECAPTION,
              Item."Planning Issues (Qty.)");
            InsertEntry(
              DATABASE::"Transfer Line",
              Item.FIELDNO("Trans. Ord. Shipment (Qty.)"),
              Item.FIELDCAPTION("Trans. Ord. Shipment (Qty.)"),
              Item."Trans. Ord. Shipment (Qty.)");
            InsertEntry(
              DATABASE::"Purchase Line",
              0,
              PurchLine.TABLECAPTION,
              Item."Qty. on Purch. Return");
            InsertEntry(
              DATABASE::"Assembly Line",
              Item.FIELDNO("Qty. on Asm. Component"),
              AssemblyLine.TABLECAPTION,
              Item."Qty. on Asm. Component");
          end;
        AvailType::"Planned Order Receipt":
          begin
            InsertEntry(
              DATABASE::"Requisition Line",
              Item.FIELDNO("Purch. Req. Receipt (Qty.)"),
              ReqLine.TABLECAPTION,
              Item."Purch. Req. Receipt (Qty.)");
            InsertEntry(
              DATABASE::"Prod. Order Line",
              Item.FIELDNO("Planned Order Receipt (Qty.)"),
              STRSUBSTNO(Text000,ProdOrderLine.TABLECAPTION),
              Item."Planned Order Receipt (Qty.)");
          end;
        AvailType::"Planned Order Release":
          begin
            InsertEntry(
              DATABASE::"Requisition Line",
              Item.FIELDNO("Purch. Req. Release (Qty.)"),
              ReqLine.TABLECAPTION,
              Item."Purch. Req. Release (Qty.)");
            InsertEntry(
              DATABASE::"Prod. Order Line",
              Item.FIELDNO("Planned Order Release (Qty.)"),
              STRSUBSTNO(Text001,ProdOrderLine.TABLECAPTION),
              Item."Planned Order Release (Qty.)");
            InsertEntry(
              DATABASE::"Requisition Line",
              Item.FIELDNO("Planning Release (Qty.)"),
              ReqLine.TABLECAPTION,
              Item."Planning Release (Qty.)");
          end;
        AvailType::"Scheduled Order Receipt":
          begin
            InsertEntry(
              DATABASE::"Purchase Line",
              Item.FIELDNO("Qty. on Purch. Order"),
              PurchLine.TABLECAPTION,
              Item."Qty. on Purch. Order");
            InsertEntry(
              DATABASE::"Prod. Order Line",
              Item.FIELDNO("FP Order Receipt (Qty.)"),
              STRSUBSTNO(Text002,ProdOrderLine.TABLECAPTION),
              Item."FP Order Receipt (Qty.)");
            InsertEntry(
              DATABASE::"Prod. Order Line",
              Item.FIELDNO("Rel. Order Receipt (Qty.)"),
              STRSUBSTNO(Text003,ProdOrderLine.TABLECAPTION),
              Item."Rel. Order Receipt (Qty.)");
            InsertEntry(
              DATABASE::"Transfer Line",
              Item.FIELDNO("Qty. in Transit"),
              Item.FIELDCAPTION("Qty. in Transit"),
              Item."Qty. in Transit");
            InsertEntry(
              DATABASE::"Transfer Line",
              Item.FIELDNO("Trans. Ord. Receipt (Qty.)"),
              Item.FIELDCAPTION("Trans. Ord. Receipt (Qty.)"),
              Item."Trans. Ord. Receipt (Qty.)");
            InsertEntry(
              DATABASE::"Sales Line",
              0,
              SalesLine.TABLECAPTION,
              Item."Qty. on Sales Return");
            InsertEntry(
              DATABASE::"Assembly Header",
              Item.FIELDNO("Qty. on Assembly Order"),
              AssemblyHeader.TABLECAPTION,
              Item."Qty. on Assembly Order");
          end;
      end;
    END;

    LOCAL PROCEDURE MakeWhat@4();
    BEGIN
      Sign := 1;
      if AvailType <> AvailType::All then
        MakeEntries
      else begin
        Item.SETRANGE("Date Filter",0D,Item.GETRANGEMAX("Date Filter"));
        Item.CALCFIELDS(
          "Qty. on Purch. Order",
          "Qty. on Sales Order",
          "Qty. on Service Order",
          "Qty. on Job Order",
          "Net Change",
          "Scheduled Receipt (Qty.)",
          "Scheduled Need (Qty.)",
          "Planned Order Receipt (Qty.)",
          "FP Order Receipt (Qty.)",
          "Rel. Order Receipt (Qty.)",
          "Planned Order Release (Qty.)",
          "Purch. Req. Receipt (Qty.)",
          "Planning Issues (Qty.)",
          "Purch. Req. Release (Qty.)",
          "Qty. in Transit");
        Item.CALCFIELDS(
          "Trans. Ord. Shipment (Qty.)",
          "Trans. Ord. Receipt (Qty.)",
          "Qty. on Assembly Order",
          "Qty. on Asm. Component",
          "Qty. on Purch. Return",
          "Qty. on Sales Return");
        if Item.Inventory <> 0 then begin
          "Table No." := DATABASE::"Item Ledger Entry";
          QuerySource := Item.FIELDNO(Inventory);
          Name := ItemLedgerEntry.TABLECAPTION;
          Quantity := Item.Inventory;
          INSERT;
        end;
        AvailType := AvailType::"Gross Requirement";
        Sign := -1;
        MakeEntries;
        AvailType := AvailType::"Planned Order Receipt";
        Sign := 1;
        MakeEntries;
        AvailType := AvailType::"Scheduled Order Receipt";
        Sign := 1;
        MakeEntries;
        AvailType := AvailType::All;
      end;
    END;

    LOCAL PROCEDURE LookupEntries@3();
    BEGIN
      case "Table No." of
        DATABASE::"Item Ledger Entry":
          begin
            ItemLedgerEntry.SETCURRENTKEY("Item No.","Entry Type","Variant Code","Drop Shipment","Location Code","Posting Date");
            ItemLedgerEntry.SETRANGE("Item No.",Item."No.");
            ItemLedgerEntry.SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
            ItemLedgerEntry.SETFILTER("Drop Shipment",Item.GETFILTER("Drop Shipment Filter"));
            ItemLedgerEntry.SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
            ItemLedgerEntry.SETFILTER("Global Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
            ItemLedgerEntry.SETFILTER("Global Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
            PAGE.RUNMODAL(0,ItemLedgerEntry);
          end;
        DATABASE::"Sales Line":
          begin
            if QuerySource > 0 then
              SalesLine.FindLinesWithItemToPlan(Item,SalesLine."Document Type"::Order)
            else
              SalesLine.FindLinesWithItemToPlan(Item,SalesLine."Document Type"::"Return Order");
            SalesLine.SETRANGE("Drop Shipment",false);
            PAGE.RUNMODAL(0,SalesLine);
          end;
        DATABASE::"Service Line":
          begin
            ServLine.FindLinesWithItemToPlan(Item);
            PAGE.RUNMODAL(0,ServLine);
          end;
        DATABASE::"Job Planning Line":
          begin
            JobPlanningLine.FindLinesWithItemToPlan(Item);
            PAGE.RUNMODAL(0,JobPlanningLine);
          end;
        DATABASE::"Purchase Line":
          begin
            PurchLine.SETCURRENTKEY("Document Type",Type,"No.");
            if QuerySource > 0 then
              PurchLine.FindLinesWithItemToPlan(Item,PurchLine."Document Type"::Order)
            else
              PurchLine.FindLinesWithItemToPlan(Item,PurchLine."Document Type"::"Return Order");
            PurchLine.SETRANGE("Drop Shipment",false);
            PAGE.RUNMODAL(0,PurchLine);
          end;
        DATABASE::"Transfer Line":
          begin
            case QuerySource of
              Item.FIELDNO("Trans. Ord. Shipment (Qty.)"):
                TransLine.FindLinesWithItemToPlan(Item,false,false);
              Item.FIELDNO("Trans. Ord. Receipt (Qty.)"),Item.FIELDNO("Qty. in Transit"):
                TransLine.FindLinesWithItemToPlan(Item,true,false);
            end;
            PAGE.RUNMODAL(0,TransLine);
          end;
        DATABASE::"Planning Component":
          begin
            PlanningComponent.FindLinesWithItemToPlan(Item);
            PAGE.RUNMODAL(0,PlanningComponent);
          end;
        DATABASE::"Prod. Order Component":
          begin
            ProdOrderComp.FindLinesWithItemToPlan(Item,true);
            PAGE.RUNMODAL(0,ProdOrderComp);
          end;
        DATABASE::"Requisition Line":
          begin
            ReqLine.FindLinesWithItemToPlan(Item);
            case QuerySource of
              Item.FIELDNO("Purch. Req. Receipt (Qty.)"):
                Item.COPYFILTER("Date Filter",ReqLine."Due Date");
              Item.FIELDNO("Purch. Req. Release (Qty.)"):
                begin
                  Item.COPYFILTER("Date Filter",ReqLine."Order Date");
                  ReqLine.SETFILTER("Planning Line Origin",'%1|%2',
                    ReqLine."Planning Line Origin"::" ",ReqLine."Planning Line Origin"::Planning);
                end;
            end;
            PAGE.RUNMODAL(0,ReqLine);
          end;
        DATABASE::"Prod. Order Line":
          begin
            ProdOrderLine.RESET;
            ProdOrderLine.SETCURRENTKEY(Status,"Item No.");
            case QuerySource of
              Item.FIELDNO("Planned Order Receipt (Qty.)"):
                begin
                  ProdOrderLine.SETRANGE(Status,ProdOrderLine.Status::Planned);
                  Item.COPYFILTER("Date Filter",ProdOrderLine."Due Date");
                end;
              Item.FIELDNO("Planned Order Release (Qty.)"):
                begin
                  ProdOrderLine.SETRANGE(Status,ProdOrderLine.Status::Planned);
                  Item.COPYFILTER("Date Filter",ProdOrderLine."Starting Date");
                end;
              Item.FIELDNO("FP Order Receipt (Qty.)"):
                begin
                  ProdOrderLine.SETRANGE(Status,ProdOrderLine.Status::"Firm Planned");
                  Item.COPYFILTER("Date Filter",ProdOrderLine."Due Date");
                end;
              Item.FIELDNO("Rel. Order Receipt (Qty.)"):
                begin
                  ProdOrderLine.SETRANGE(Status,ProdOrderLine.Status::Released);
                  Item.COPYFILTER("Date Filter",ProdOrderLine."Due Date");
                end;
            end;
            ProdOrderLine.SETRANGE("Item No.",Item."No.");
            Item.COPYFILTER("Variant Filter",ProdOrderLine."Variant Code");
            Item.COPYFILTER("Location Filter",ProdOrderLine."Location Code");
            Item.COPYFILTER("Global Dimension 1 Filter",ProdOrderLine."Shortcut Dimension 1 Code");
            Item.COPYFILTER("Global Dimension 2 Filter",ProdOrderLine."Shortcut Dimension 2 Code");
            PAGE.RUNMODAL(0,ProdOrderLine);
          end;
        DATABASE::"Assembly Header":
          begin
            AssemblyHeader.FindLinesWithItemToPlan(Item,AssemblyHeader."Document Type"::Order);
            PAGE.RUNMODAL(0,AssemblyHeader);
          end;
        DATABASE::"Assembly Line":
          begin
            AssemblyLine.FindLinesWithItemToPlan(Item,AssemblyHeader."Document Type"::Order);
            PAGE.RUNMODAL(0,AssemblyLine);
          end;
      end;
    END;

    LOCAL PROCEDURE InsertEntry@5(Table@1000 : Integer;Field@1001 : Integer;TableName@1002 : Text[100];Qty@1003 : Decimal);
    BEGIN
      if Qty = 0 then
        exit;

      "Table No." := Table;
      QuerySource := Field;
      Name := COPYSTR(TableName,1,MAXSTRLEN(Name));
      Quantity := Qty * Sign;
      INSERT;
    END;

    BEGIN
    END.
  }
}

