OBJECT Codeunit 1802 Data Migration Notifier
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ListEmptyMsg@1000 : TextConst 'ENU=Want to import entries?';
      OpenDataMigrationTxt@1001 : TextConst 'ENU=Open Data Migration';
      DataTypeManagement@1003 : Codeunit "Data Type Management";

    [EventSubscriber(Page,22,OnOpenPageEvent)]
    LOCAL PROCEDURE OnCustomerListOpen@1(VAR Rec@1000 : Record Customer);
    BEGIN
      ShowListEmptyNotification(Rec);
    END;

    [EventSubscriber(Page,27,OnOpenPageEvent)]
    LOCAL PROCEDURE OnVendorListOpen@6(VAR Rec@1000 : Record Vendor);
    BEGIN
      ShowListEmptyNotification(Rec);
    END;

    [EventSubscriber(Page,31,OnOpenPageEvent)]
    LOCAL PROCEDURE OnItemListOpen@7(VAR Rec@1000 : Record Item);
    BEGIN
      ShowListEmptyNotification(Rec);
    END;

    LOCAL PROCEDURE ShowListEmptyNotification@3(RecordVariant@1001 : Variant);
    VAR
      ListEmptyNotification@1000 : Notification;
      RecRef@1002 : RecordRef;
    BEGIN
      DataTypeManagement.GetRecordRef(RecordVariant,RecRef);

      if not RecRef.ISEMPTY then
        exit;

      ListEmptyNotification.MESSAGE := ListEmptyMsg;
      ListEmptyNotification.SCOPE := NOTIFICATIONSCOPE::LocalScope;
      ListEmptyNotification.ADDACTION(OpenDataMigrationTxt,CODEUNIT::"Data Migration Notifier",'OpenDataMigrationWizard');
      ListEmptyNotification.SEND;
    END;

    [External]
    PROCEDURE OpenDataMigrationWizard@5(ListEmptyNotification@1000 : Notification);
    BEGIN
      PAGE.RUN(PAGE::"Data Migration Wizard");
    END;

    [EventSubscriber(Table,1800,OnGetInstructions,"",Skip,Skip)]
    LOCAL PROCEDURE OnGetInstructionsSubscriber@2(VAR Sender@1000 : Record "Data Migrator Registration";VAR Instructions@1001 : Text;VAR Handled@1002 : Boolean);
    BEGIN
      SENDTRACETAG('00001DB',Sender.TABLENAME,VERBOSITY::Normal,Sender.Description);
    END;

    BEGIN
    END.
  }
}

