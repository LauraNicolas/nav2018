OBJECT Codeunit 1023 Job Jnl.-B.Post
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo="Job Journal Batch";
    OnRun=BEGIN
            JobJnlBatch.COPY(Rec);
            Code;
            COPY(JobJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.';
      JobJnlTemplate@1004 : Record "Job Journal Template";
      JobJnlBatch@1005 : Record "Job Journal Batch";
      JobJnlLine@1006 : Record "Job Journal Line";
      JobJnlPostbatch@1007 : Codeunit "Job Jnl.-Post Batch";
      JnlWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with JobJnlBatch do begin
        JobJnlTemplate.GET("Journal Template Name");
        JobJnlTemplate.TESTFIELD("Force Posting Report",false);

        if not CONFIRM(Text000) then
          exit;

        FIND('-');
        repeat
          JobJnlLine."Journal Template Name" := "Journal Template Name";
          JobJnlLine."Journal Batch Name" := Name;
          JobJnlLine."Line No." := 1;
          CLEAR(JobJnlPostbatch);
          if JobJnlPostbatch.RUN(JobJnlLine) then
            MARK(false)
          else begin
            MARK(true);
            JnlWithErrors := true;
          end;
        until NEXT = 0;

        if not JnlWithErrors then
          MESSAGE(Text001)
        else
          MESSAGE(
            Text002 +
            Text003);

        if not FIND('=><') then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP(0);
          Name := '';
        end;
      end;
    END;

    BEGIN
    END.
  }
}

