OBJECT Table 99000786 Routing Version
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               if "Version Code" = '' then begin
                 RoutingHeader.GET("Routing No.");
                 RoutingHeader.TESTFIELD("Version Nos.");
                 NoSeriesMgt.InitSeries(RoutingHeader."Version Nos.",xRec."No. Series",0D,VersionCode,"No. Series");
                 if STRLEN(VersionCode) > MAXSTRLEN("Version Code") then
                   ERROR(Text000,
                     FIELDCAPTION("Version Code"),
                     NoSeriesLine.FIELDCAPTION("Starting No."),
                     RoutingHeader."Version Nos.",
                     NoSeries.TABLECAPTION,
                     MAXSTRLEN("Version Code"));

                 "Version Code" := VersionCode;
               end;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               RtngLine@1000 : Record "Routing Line";
             BEGIN
               RtngLine.LOCKTABLE;
               RtngLine.SETRANGE("Routing No.","Routing No.");
               RtngLine.SETRANGE("Version Code","Version Code");
               RtngLine.DELETEALL(true);
             END;

    OnRename=BEGIN
               if Status = Status::Certified then
                 ERROR(Text001,TABLECAPTION,FIELDCAPTION(Status),FORMAT(Status));
             END;

    CaptionML=ENU=Routing Version;
    LookupPageID="Routing Version List";
    DrillDownPageID="Routing Version List";
  }
  FIELDS
  {
    { 1   ;   ;Routing No.         ;Code20        ;TableRelation="Routing Header";
                                                   CaptionML=ENU=Routing No.;
                                                   NotBlank=true }
    { 2   ;   ;Version Code        ;Code20        ;CaptionML=ENU=Version Code }
    { 3   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 10  ;   ;Starting Date       ;Date          ;CaptionML=ENU=Starting Date }
    { 20  ;   ;Status              ;Option        ;OnValidate=VAR
                                                                RtngHeader@1000 : Record "Routing Header";
                                                              BEGIN
                                                                if (Status <> xRec.Status) and (Status = Status::Certified) then begin
                                                                  RtngHeader.GET("Routing No.");
                                                                  CheckRouting.Calculate(RtngHeader,"Version Code");
                                                                end;
                                                                MODIFY(true);
                                                                COMMIT;
                                                              END;

                                                   CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=New,Certified,Under Development,Closed;
                                                   OptionString=New,Certified,"Under Development",Closed }
    { 21  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                if Status = Status::Certified then
                                                                  FIELDERROR(Status);
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Serial,Parallel;
                                                   OptionString=Serial,Parallel }
    { 22  ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=false }
    { 50  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Routing No.","Version Code"            ;Clustered=true }
    {    ;"Routing No.","Starting Date"            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoSeries@1007 : Record "No. Series";
      NoSeriesLine@1006 : Record "No. Series Line";
      RoutingHeader@1000 : Record "Routing Header";
      RtngVersion@1001 : Record "Routing Version";
      CheckRouting@1002 : Codeunit "Check Routing Lines";
      NoSeriesMgt@1003 : Codeunit NoSeriesManagement;
      Text000@1008 : TextConst 'ENU=The new %1 cannot be generated by default\because the %2 for %3 %4 contains more than %5 characters.';
      VersionCode@1005 : Code[20];
      Text001@1004 : TextConst 'ENU=You cannot rename the %1 when %2 is %3.';

    [External]
    PROCEDURE AssistEdit@2(OldRoutVersion@1000 : Record "Routing Version") : Boolean;
    BEGIN
      with RtngVersion do begin
        RtngVersion := Rec;
        RoutingHeader.GET("Routing No.");
        RoutingHeader.TESTFIELD("Version Nos.");
        if NoSeriesMgt.SelectSeries(RoutingHeader."Version Nos.",OldRoutVersion."No. Series","No. Series") then begin
          NoSeriesMgt.SetSeries(VersionCode);
          if STRLEN(VersionCode) > MAXSTRLEN("Version Code") then
            ERROR(Text000,
              FIELDCAPTION("Version Code"),
              NoSeriesLine.FIELDCAPTION("Starting No."),
              RoutingHeader."Version Nos.",
              NoSeries.TABLECAPTION,
              MAXSTRLEN("Version Code"));

          "Version Code" := VersionCode;
          Rec := RtngVersion;
          exit(true);
        end;
      end;
    END;

    [External]
    PROCEDURE Caption@1() : Text[100];
    VAR
      RtngHeader@1000 : Record "Routing Header";
    BEGIN
      if GETFILTERS = '' then
        exit('');

      if "Routing No." = '' then
        exit('');

      RtngHeader.GET("Routing No.");
      exit(
        STRSUBSTNO(
          '%1 %2 %3',RtngHeader."No.",RtngHeader.Description,"Version Code"));
    END;

    BEGIN
    END.
  }
}

