OBJECT Codeunit 1001 Job Post-Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Job Ledger Entry"=rm,
                TableData "Job Planning Line"=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempSalesLineJob@1005 : TEMPORARY Record "Sales Line";
      TempPurchaseLineJob@1002 : TEMPORARY Record "Purchase Line";
      TempJobJournalLine@1004 : TEMPORARY Record "Job Journal Line";
      JobJnlPostLine@1003 : Codeunit "Job Jnl.-Post Line";
      JobTransferLine@1001 : Codeunit "Job Transfer Line";
      Text000@1011 : TextConst 'ENU="has been changed (initial a %1: %2= %3, %4= %5)"';
      Text003@1000 : TextConst 'ENU=You cannot change the sales line because it is linked to\';
      Text004@1014 : TextConst 'ENU=" %1: %2= %3, %4= %5."';
      Text005@1015 : TextConst 'ENU="You must post more usage or credit the sale of %1 %2 in %3 %4 before you can post purchase credit memo %5 %6 = %7."';

    [External]
    PROCEDURE InsertPlLineFromLedgEntry@2(JobLedgEntry@1003 : Record "Job Ledger Entry");
    VAR
      JobPlanningLine@1002 : Record "Job Planning Line";
    BEGIN
      if JobLedgEntry."Line Type" = JobLedgEntry."Line Type"::" " then
        exit;
      CLEARALL;
      JobPlanningLine."Job No." := JobLedgEntry."Job No.";
      JobPlanningLine."Job Task No." := JobLedgEntry."Job Task No.";
      JobPlanningLine.SETRANGE("Job No.",JobPlanningLine."Job No.");
      JobPlanningLine.SETRANGE("Job Task No.",JobPlanningLine."Job Task No.");
      if JobPlanningLine.FINDLAST then;
      JobPlanningLine."Line No." := JobPlanningLine."Line No." + 10000;
      JobPlanningLine.INIT;
      JobPlanningLine.RESET;
      CLEAR(JobTransferLine);
      JobTransferLine.FromJobLedgEntryToPlanningLine(JobLedgEntry,JobPlanningLine);
      PostPlanningLine(JobPlanningLine);
    END;

    LOCAL PROCEDURE PostPlanningLine@9(JobPlanningLine@1000 : Record "Job Planning Line");
    VAR
      Job@1001 : Record Job;
    BEGIN
      if JobPlanningLine."Line Type" = JobPlanningLine."Line Type"::"Both Budget and Billable" then begin
        Job.GET(JobPlanningLine."Job No.");
        if not Job."Allow Schedule/Contract Lines" or
           (JobPlanningLine.Type = JobPlanningLine.Type::"G/L Account")
        then begin
          JobPlanningLine.VALIDATE("Line Type",JobPlanningLine."Line Type"::Budget);
          JobPlanningLine.INSERT(true);
          InsertJobUsageLink(JobPlanningLine);
          JobPlanningLine.VALIDATE("Qty. to Transfer to Journal",0);
          JobPlanningLine.MODIFY(true);
          JobPlanningLine."Job Contract Entry No." := 0;
          JobPlanningLine."Line No." := JobPlanningLine."Line No." + 10000;
          JobPlanningLine.VALIDATE("Line Type",JobPlanningLine."Line Type"::Billable);
        end;
      end;
      if (JobPlanningLine.Type = JobPlanningLine.Type::"G/L Account") and
         (JobPlanningLine."Line Type" = JobPlanningLine."Line Type"::Billable)
      then
        ChangeGLNo(JobPlanningLine);
      JobPlanningLine.INSERT(true);
      JobPlanningLine.VALIDATE("Qty. to Transfer to Journal",0);
      JobPlanningLine.MODIFY(true);
      if JobPlanningLine."Line Type" in
         [JobPlanningLine."Line Type"::Budget,JobPlanningLine."Line Type"::"Both Budget and Billable"]
      then
        InsertJobUsageLink(JobPlanningLine);
    END;

    LOCAL PROCEDURE InsertJobUsageLink@4(JobPlanningLine@1001 : Record "Job Planning Line");
    VAR
      JobUsageLink@1000 : Record "Job Usage Link";
      JobLedgerEntry@1002 : Record "Job Ledger Entry";
    BEGIN
      if not JobPlanningLine."Usage Link" then
        exit;
      JobLedgerEntry.GET(JobPlanningLine."Job Ledger Entry No.");
      JobUsageLink.Create(JobPlanningLine,JobLedgerEntry);
    END;

    [Internal]
    PROCEDURE PostInvoiceContractLine@14(SalesHeader@1006 : Record "Sales Header";SalesLine@1000 : Record "Sales Line");
    VAR
      Job@1003 : Record Job;
      JobLedgEntry@1007 : Record "Job Ledger Entry";
      JobPlanningLine@1001 : Record "Job Planning Line";
      JobPlanningLineInvoice@1009 : Record "Job Planning Line Invoice";
      EntryType@1010 : 'Usage,Sale';
    BEGIN
      JobPlanningLine.SETCURRENTKEY("Job Contract Entry No.");
      JobPlanningLine.SETRANGE("Job Contract Entry No.",SalesLine."Job Contract Entry No.");
      JobPlanningLine.FINDFIRST;
      Job.GET(JobPlanningLine."Job No.");

      if Job."Invoice Currency Code" = '' then begin
        Job.TESTFIELD("Currency Code",SalesHeader."Currency Code");
        Job.TESTFIELD("Currency Code",JobPlanningLine."Currency Code");
        SalesHeader.TESTFIELD("Currency Code",JobPlanningLine."Currency Code");
        SalesHeader.TESTFIELD("Currency Factor",JobPlanningLine."Currency Factor");
      end else begin
        Job.TESTFIELD("Currency Code",'');
        JobPlanningLine.TESTFIELD("Currency Code",'');
      end;

      SalesHeader.TESTFIELD("Bill-to Customer No.",Job."Bill-to Customer No.");
      JobPlanningLine.CALCFIELDS("Qty. Transferred to Invoice");
      if JobPlanningLine.Type <> JobPlanningLine.Type::Text then
        JobPlanningLine.TESTFIELD("Qty. Transferred to Invoice");

      ValidateRelationship(SalesHeader,SalesLine,JobPlanningLine);

      case SalesHeader."Document Type" of
        SalesHeader."Document Type"::Invoice:
          if JobPlanningLineInvoice.GET(JobPlanningLine."Job No.",JobPlanningLine."Job Task No.",JobPlanningLine."Line No.",
               JobPlanningLineInvoice."Document Type"::Invoice,SalesHeader."No.",SalesLine."Line No.")
          then begin
            JobPlanningLineInvoice.DELETE(true);
            JobPlanningLineInvoice."Document Type" := JobPlanningLineInvoice."Document Type"::"Posted Invoice";
            JobPlanningLineInvoice."Document No." := SalesLine."Document No.";
            JobPlanningLineInvoice.INSERT(true);

            JobPlanningLineInvoice."Invoiced Date" := SalesHeader."Posting Date";
            JobPlanningLineInvoice."Invoiced Amount (LCY)" :=
              CalcLineAmountLCY(JobPlanningLine,JobPlanningLineInvoice."Quantity Transferred");
            JobPlanningLineInvoice."Invoiced Cost Amount (LCY)" :=
              JobPlanningLineInvoice."Quantity Transferred" * JobPlanningLine."Unit Cost (LCY)";
            if JobLedgEntry.FINDLAST then
              JobPlanningLineInvoice."Job Ledger Entry No." := JobLedgEntry."Entry No." + 1
            else
              JobPlanningLineInvoice."Job Ledger Entry No." := 1;
            JobPlanningLineInvoice.MODIFY;
          end;
        SalesHeader."Document Type"::"Credit Memo":
          if JobPlanningLineInvoice.GET(JobPlanningLine."Job No.",JobPlanningLine."Job Task No.",JobPlanningLine."Line No.",
               JobPlanningLineInvoice."Document Type"::"Credit Memo",SalesHeader."No.",SalesLine."Line No.")
          then begin
            JobPlanningLineInvoice.DELETE(true);
            JobPlanningLineInvoice."Document Type" := JobPlanningLineInvoice."Document Type"::"Posted Credit Memo";
            JobPlanningLineInvoice."Document No." := SalesLine."Document No.";
            JobPlanningLineInvoice.INSERT(true);

            JobPlanningLineInvoice."Invoiced Date" := SalesHeader."Posting Date";
            JobPlanningLineInvoice."Invoiced Amount (LCY)" :=
              CalcLineAmountLCY(JobPlanningLine,JobPlanningLineInvoice."Quantity Transferred");
            JobPlanningLineInvoice."Invoiced Cost Amount (LCY)" :=
              JobPlanningLineInvoice."Quantity Transferred" * JobPlanningLine."Unit Cost (LCY)";
            if JobLedgEntry.FINDLAST then
              JobPlanningLineInvoice."Job Ledger Entry No." := JobLedgEntry."Entry No." + 1
            else
              JobPlanningLineInvoice."Job Ledger Entry No." := 1;
            JobPlanningLineInvoice.MODIFY;
          end;
      end;

      JobPlanningLine.UpdateQtyToInvoice;
      JobPlanningLine.MODIFY;

      if JobPlanningLine.Type <> JobPlanningLine.Type::Text then
        PostJobOnSalesLine(JobPlanningLine,SalesHeader,SalesLine,EntryType::Sale);
    END;

    LOCAL PROCEDURE ValidateRelationship@7(SalesHeader@1006 : Record "Sales Header";SalesLine@1000 : Record "Sales Line";JobPlanningLine@1005 : Record "Job Planning Line");
    VAR
      JobTask@1002 : Record "Job Task";
      Txt@1001 : Text[500];
    BEGIN
      JobTask.GET(JobPlanningLine."Job No.",JobPlanningLine."Job Task No.");
      Txt := STRSUBSTNO(Text000,
          JobTask.TABLECAPTION,JobTask.FIELDCAPTION("Job No."),JobTask."Job No.",
          JobTask.FIELDCAPTION("Job Task No."),JobTask."Job Task No.");

      if JobPlanningLine.Type = JobPlanningLine.Type::Text then
        if SalesLine.Type <> SalesLine.Type::" " then
          SalesLine.FIELDERROR(Type,Txt);
      if JobPlanningLine.Type = JobPlanningLine.Type::Resource then
        if SalesLine.Type <> SalesLine.Type::Resource then
          SalesLine.FIELDERROR(Type,Txt);
      if JobPlanningLine.Type = JobPlanningLine.Type::Item then
        if SalesLine.Type <> SalesLine.Type::Item then
          SalesLine.FIELDERROR(Type,Txt);
      if JobPlanningLine.Type = JobPlanningLine.Type::"G/L Account" then
        if SalesLine.Type <> SalesLine.Type::"G/L Account" then
          SalesLine.FIELDERROR(Type,Txt);

      if SalesLine."No." <> JobPlanningLine."No." then
        SalesLine.FIELDERROR("No.",Txt);
      if SalesLine."Location Code" <> JobPlanningLine."Location Code" then
        SalesLine.FIELDERROR("Location Code",Txt);
      if SalesLine."Work Type Code" <> JobPlanningLine."Work Type Code" then
        SalesLine.FIELDERROR("Work Type Code",Txt);
      if SalesLine."Unit of Measure Code" <> JobPlanningLine."Unit of Measure Code" then
        SalesLine.FIELDERROR("Unit of Measure Code",Txt);
      if SalesLine."Variant Code" <> JobPlanningLine."Variant Code" then
        SalesLine.FIELDERROR("Variant Code",Txt);
      if SalesLine."Gen. Prod. Posting Group" <> JobPlanningLine."Gen. Prod. Posting Group" then
        SalesLine.FIELDERROR("Gen. Prod. Posting Group",Txt);
      if SalesLine."Line Discount %" <> JobPlanningLine."Line Discount %" then
        SalesLine.FIELDERROR("Line Discount %",Txt);
      if JobPlanningLine."Unit Cost (LCY)" <> SalesLine."Unit Cost (LCY)" then
        SalesLine.FIELDERROR("Unit Cost (LCY)",Txt);
      if SalesLine.Type = SalesLine.Type::" " then begin
        if SalesLine."Line Amount" <> 0 then
          SalesLine.FIELDERROR("Line Amount",Txt);
      end;
      if SalesHeader."Prices Including VAT" then begin
        if JobPlanningLine."VAT %" <> SalesLine."VAT %" then
          SalesLine.FIELDERROR("VAT %",Txt);
      end;
    END;

    LOCAL PROCEDURE PostJobOnSalesLine@21(JobPlanningLine@1000 : Record "Job Planning Line";SalesHeader@1003 : Record "Sales Header";SalesLine@1002 : Record "Sales Line";EntryType@1004 : 'Usage,Sale');
    VAR
      JobJnlLine@1001 : Record "Job Journal Line";
    BEGIN
      JobTransferLine.FromPlanningSalesLineToJnlLine(JobPlanningLine,SalesHeader,SalesLine,JobJnlLine,EntryType);
      if SalesLine.Type = SalesLine.Type::"G/L Account" then begin
        TempSalesLineJob := SalesLine;
        TempSalesLineJob.INSERT;
        InsertTempJobJournalLine(JobJnlLine,TempSalesLineJob."Line No.");
      end else
        JobJnlPostLine.RunWithCheck(JobJnlLine);
    END;

    LOCAL PROCEDURE CalcLineAmountLCY@19(JobPlanningLine@1002 : Record "Job Planning Line";Qty@1000 : Decimal) : Decimal;
    VAR
      TotalPrice@1001 : Decimal;
    BEGIN
      TotalPrice := ROUND(Qty * JobPlanningLine."Unit Price (LCY)",0.01);
      exit(TotalPrice - ROUND(TotalPrice * JobPlanningLine."Line Discount %" / 100,0.01));
    END;

    [Internal]
    PROCEDURE PostGenJnlLine@1(GenJnlLine@1000 : Record "Gen. Journal Line";GLEntry@1002 : Record "G/L Entry");
    VAR
      JobJnlLine@1001 : Record "Job Journal Line";
      Job@1004 : Record Job;
      JT@1003 : Record "Job Task";
      SourceCodeSetup@1007 : Record "Source Code Setup";
      JobTransferLine@1005 : Codeunit "Job Transfer Line";
    BEGIN
      if GenJnlLine."System-Created Entry" then
        exit;
      if GenJnlLine."Job No." = '' then
        exit;
      SourceCodeSetup.GET;
      if GenJnlLine."Source Code" = SourceCodeSetup."Job G/L WIP" then
        exit;
      GenJnlLine.TESTFIELD("Job Task No.");
      GenJnlLine.TESTFIELD("Job Quantity");
      Job.LOCKTABLE;
      JT.LOCKTABLE;
      Job.GET(GenJnlLine."Job No.");
      GenJnlLine.TESTFIELD("Job Currency Code",Job."Currency Code");
      JT.GET(GenJnlLine."Job No.",GenJnlLine."Job Task No.");
      JT.TESTFIELD("Job Task Type",JT."Job Task Type"::Posting);
      JobTransferLine.FromGenJnlLineToJnlLine(GenJnlLine,JobJnlLine);

      JobJnlPostLine.SetGLEntryNo(GLEntry."Entry No.");
      JobJnlPostLine.RunWithCheck(JobJnlLine);
      JobJnlPostLine.SetGLEntryNo(0);
    END;

    [Internal]
    PROCEDURE PostJobOnPurchaseLine@12(VAR PurchHeader@1006 : Record "Purchase Header";VAR PurchInvHeader@1004 : Record "Purch. Inv. Header";VAR PurchCrMemoHdr@1005 : Record "Purch. Cr. Memo Hdr.";PurchLine@1000 : Record "Purchase Line";Sourcecode@1007 : Code[10]);
    VAR
      JobJnlLine@1003 : Record "Job Journal Line";
      Job@1001 : Record Job;
      JobTask@1002 : Record "Job Task";
    BEGIN
      if (PurchLine.Type <> PurchLine.Type::Item) and (PurchLine.Type <> PurchLine.Type::"G/L Account") then
        exit;
      CLEAR(JobJnlLine);
      PurchLine.TESTFIELD("Job No.");
      PurchLine.TESTFIELD("Job Task No.");
      Job.LOCKTABLE;
      JobTask.LOCKTABLE;
      Job.GET(PurchLine."Job No.");
      PurchLine.TESTFIELD("Job Currency Code",Job."Currency Code");
      JobTask.GET(PurchLine."Job No.",PurchLine."Job Task No.");
      JobTransferLine.FromPurchaseLineToJnlLine(
        PurchHeader,PurchInvHeader,PurchCrMemoHdr,PurchLine,Sourcecode,JobJnlLine);
      JobJnlLine."Job Posting Only" := true;

      if PurchLine.Type = PurchLine.Type::"G/L Account" then begin
        TempPurchaseLineJob := PurchLine;
        TempPurchaseLineJob.INSERT;
        InsertTempJobJournalLine(JobJnlLine,TempPurchaseLineJob."Line No.");
      end else
        JobJnlPostLine.RunWithCheck(JobJnlLine);
    END;

    [External]
    PROCEDURE TestSalesLine@11(VAR SalesLine@1000 : Record "Sales Line");
    VAR
      JT@1003 : Record "Job Task";
      JobPlanningLine@1001 : Record "Job Planning Line";
      Txt@1002 : Text[250];
    BEGIN
      if SalesLine."Job Contract Entry No." = 0 then
        exit;
      JobPlanningLine.SETCURRENTKEY("Job Contract Entry No.");
      JobPlanningLine.SETRANGE("Job Contract Entry No.",SalesLine."Job Contract Entry No.");
      if JobPlanningLine.FINDFIRST then begin
        JT.GET(JobPlanningLine."Job No.",JobPlanningLine."Job Task No.");
        Txt := Text003 + STRSUBSTNO(Text004,
            JT.TABLECAPTION,JT.FIELDCAPTION("Job No."),JT."Job No.",
            JT.FIELDCAPTION("Job Task No."),JT."Job Task No.");
        ERROR(Txt);
      end;
    END;

    LOCAL PROCEDURE ChangeGLNo@13(VAR JobPlanningLine@1000 : Record "Job Planning Line");
    VAR
      GLAcc@1003 : Record "G/L Account";
      Job@1001 : Record Job;
      JT@1002 : Record "Job Task";
      JobPostingGr@1004 : Record "Job Posting Group";
      Cust@1005 : Record Customer;
    BEGIN
      JT.GET(JobPlanningLine."Job No.",JobPlanningLine."Job Task No.");
      Job.GET(JobPlanningLine."Job No.");
      Cust.GET(Job."Bill-to Customer No.");
      if JT."Job Posting Group" <> '' then
        JobPostingGr.GET(JT."Job Posting Group")
      else begin
        Job.TESTFIELD("Job Posting Group");
        JobPostingGr.GET(Job."Job Posting Group");
      end;
      if JobPostingGr."G/L Expense Acc. (Contract)" = '' then
        exit;
      GLAcc.GET(JobPostingGr."G/L Expense Acc. (Contract)");
      GLAcc.CheckGLAcc;
      JobPlanningLine."No." := GLAcc."No.";
      JobPlanningLine."Gen. Bus. Posting Group" := Cust."Gen. Bus. Posting Group";
      JobPlanningLine."Gen. Prod. Posting Group" := GLAcc."Gen. Prod. Posting Group";
    END;

    [External]
    PROCEDURE CheckItemQuantityPurchCredit@15(VAR PurchaseHeader@1006 : Record "Purchase Header";VAR PurchaseLine@1001 : Record "Purchase Line");
    VAR
      Item@1003 : Record Item;
      Job@1007 : Record Job;
    BEGIN
      Job.GET(PurchaseLine."Job No.");
      if Job.GetQuantityAvailable(PurchaseLine."No.",PurchaseLine."Location Code",PurchaseLine."Variant Code",0,2) <
         -PurchaseLine."Return Qty. to Ship (Base)"
      then
        ERROR(
          Text005,Item.TABLECAPTION,PurchaseLine."No.",Job.TABLECAPTION,
          PurchaseLine."Job No.",PurchaseHeader."No.",
          PurchaseLine.FIELDCAPTION("Line No."),PurchaseLine."Line No.");
    END;

    [Internal]
    PROCEDURE PostPurchaseGLAccounts@5(TempInvoicePostBuffer@1000 : TEMPORARY Record "Invoice Post. Buffer";GLEntryNo@1001 : Integer);
    BEGIN
      with TempPurchaseLineJob do begin
        RESET;
        SETRANGE("Job No.",TempInvoicePostBuffer."Job No.");
        SETRANGE("No.",TempInvoicePostBuffer."G/L Account");
        SETRANGE("Gen. Bus. Posting Group",TempInvoicePostBuffer."Gen. Bus. Posting Group");
        SETRANGE("Gen. Prod. Posting Group",TempInvoicePostBuffer."Gen. Prod. Posting Group");
        SETRANGE("VAT Bus. Posting Group",TempInvoicePostBuffer."VAT Bus. Posting Group");
        SETRANGE("VAT Prod. Posting Group",TempInvoicePostBuffer."VAT Prod. Posting Group");
        if FINDSET then begin
          repeat
            TempJobJournalLine.RESET;
            TempJobJournalLine.SETRANGE("Line No.","Line No.");
            TempJobJournalLine.FINDFIRST;
            JobJnlPostLine.SetGLEntryNo(GLEntryNo);
            JobJnlPostLine.RunWithCheck(TempJobJournalLine);
          until NEXT = 0;
          DELETEALL;
        end;
      end;
    END;

    [Internal]
    PROCEDURE PostSalesGLAccounts@8(TempInvoicePostBuffer@1005 : TEMPORARY Record "Invoice Post. Buffer";GLEntryNo@1000 : Integer);
    BEGIN
      with TempSalesLineJob do begin
        RESET;
        SETRANGE("Job No.",TempInvoicePostBuffer."Job No.");
        SETRANGE("No.",TempInvoicePostBuffer."G/L Account");
        SETRANGE("Gen. Bus. Posting Group",TempInvoicePostBuffer."Gen. Bus. Posting Group");
        SETRANGE("Gen. Prod. Posting Group",TempInvoicePostBuffer."Gen. Prod. Posting Group");
        SETRANGE("VAT Bus. Posting Group",TempInvoicePostBuffer."VAT Bus. Posting Group");
        SETRANGE("VAT Prod. Posting Group",TempInvoicePostBuffer."VAT Prod. Posting Group");
        if FINDSET then begin
          repeat
            TempJobJournalLine.RESET;
            TempJobJournalLine.SETRANGE("Line No.","Line No.");
            TempJobJournalLine.FINDFIRST;
            JobJnlPostLine.SetGLEntryNo(GLEntryNo);
            JobJnlPostLine.RunWithCheck(TempJobJournalLine);
          until NEXT = 0;
          DELETEALL;
        end;
      end;
    END;

    LOCAL PROCEDURE InsertTempJobJournalLine@23(JobJournalLine@1000 : Record "Job Journal Line";LineNo@1001 : Integer);
    BEGIN
      TempJobJournalLine := JobJournalLine;
      TempJobJournalLine."Line No." := LineNo;
      TempJobJournalLine.INSERT;
    END;

    BEGIN
    END.
  }
}

