OBJECT Report 5692 Calculate Depreciation
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Calculate Depreciation;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  DeprBook.GET(DeprBookCode);
                  if DeprUntilDate = 0D then
                    ERROR(Text000,FAJnlLine.FIELDCAPTION("FA Posting Date"));
                  if PostingDate = 0D then
                    PostingDate := DeprUntilDate;
                  if UseForceNoOfDays and (DaysInPeriod = 0) then
                    ERROR(Text001);

                  if DeprBook."Use Same FA+G/L Posting Dates" and (DeprUntilDate <> PostingDate) then
                    ERROR(
                      Text002,
                      FAJnlLine.FIELDCAPTION("FA Posting Date"),
                      FAJnlLine.FIELDCAPTION("Posting Date"),
                      DeprBook.FIELDCAPTION("Use Same FA+G/L Posting Dates"),
                      false,
                      DeprBook.TABLECAPTION,
                      DeprBook.FIELDCAPTION(Code),
                      DeprBook.Code);

                  Window.OPEN(
                    Text003 +
                    Text004 +
                    Text005);
                END;

    OnPostReport=VAR
                   PageGenJnlLine@1000 : Record "Gen. Journal Line";
                   PageFAJnlLine@1001 : Record "FA Journal Line";
                 BEGIN
                   if (FAJnlLineCreatedCount = 0) and (GenJnlLineCreatedCount = 0) then begin
                     MESSAGE(CompletionStatsMsg);
                     exit;
                   end;

                   if FAJnlLineCreatedCount > 0 then
                     if CONFIRM(CompletionStatsFAJnlQst,true,FAJnlLineCreatedCount)
                     then begin
                       PageFAJnlLine.SETRANGE("Journal Template Name",FAJnlLine."Journal Template Name");
                       PageFAJnlLine.SETRANGE("Journal Batch Name",FAJnlLine."Journal Batch Name");
                       PageFAJnlLine.FINDFIRST;
                       PAGE.RUN(PAGE::"Fixed Asset Journal",PageFAJnlLine);
                     end;

                   if GenJnlLineCreatedCount > 0 then
                     if CONFIRM(CompletionStatsGenJnlQst,true,GenJnlLineCreatedCount)
                     then begin
                       PageGenJnlLine.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                       PageGenJnlLine.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
                       PageGenJnlLine.FINDFIRST;
                       PAGE.RUN(PAGE::"Fixed Asset G/L Journal",PageGenJnlLine);
                     end;
                 END;

  }
  DATASET
  {
    { 3794;    ;DataItem;Fixed Asset         ;
               DataItemTable="Fixed Asset";
               OnAfterGetRecord=BEGIN
                                  if Inactive or Blocked then
                                    CurrReport.SKIP;

                                  CalculateDepr.Calculate(
                                    DeprAmount,Custom1Amount,NumberOfDays,Custom1NumberOfDays,
                                    "No.",DeprBookCode,DeprUntilDate,EntryAmounts,0D,DaysInPeriod);
                                  if (DeprAmount <> 0) or (Custom1Amount <> 0) then
                                    Window.UPDATE(1,"No.")
                                  else
                                    Window.UPDATE(2,"No.");

                                  if Custom1Amount <> 0 then
                                    if not DeprBook."G/L Integration - Custom 1" or "Budgeted Asset" then begin
                                      FAJnlLineTmp."FA No." := "No.";
                                      FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::"Custom 1";
                                      FAJnlLineTmp.Amount := Custom1Amount;
                                      FAJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                                      FAJnlLineTmp."FA Error Entry No." := Custom1ErrorNo;
                                      FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                      FAJnlLineTmp.INSERT;
                                    end else begin
                                      GenJnlLineTmp."Account No." := "No.";
                                      GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::"Custom 1";
                                      GenJnlLineTmp.Amount := Custom1Amount;
                                      GenJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                                      GenJnlLineTmp."FA Error Entry No." := Custom1ErrorNo;
                                      GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                      GenJnlLineTmp.INSERT;
                                    end;

                                  if DeprAmount <> 0 then
                                    if not DeprBook."G/L Integration - Depreciation" or "Budgeted Asset" then begin
                                      FAJnlLineTmp."FA No." := "No.";
                                      FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::Depreciation;
                                      FAJnlLineTmp.Amount := DeprAmount;
                                      FAJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      FAJnlLineTmp."FA Error Entry No." := ErrorNo;
                                      FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                      FAJnlLineTmp.INSERT;
                                    end else begin
                                      GenJnlLineTmp."Account No." := "No.";
                                      GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::Depreciation;
                                      GenJnlLineTmp.Amount := DeprAmount;
                                      GenJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      GenJnlLineTmp."FA Error Entry No." := ErrorNo;
                                      GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                      GenJnlLineTmp.INSERT;
                                    end;
                                END;

               OnPostDataItem=BEGIN
                                with FAJnlLine do begin
                                  if FAJnlLineTmp.FIND('-') then begin
                                    LOCKTABLE;
                                    FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
                                    NoSeries := FAJnlSetup.GetFANoSeries(FAJnlLine);
                                    if DocumentNo = '' then
                                      DocumentNo2 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,DeprUntilDate,true)
                                    else
                                      DocumentNo2 := DocumentNo;
                                  end;
                                  if FAJnlLineTmp.FIND('-') then
                                    repeat
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      if "Posting Date" = "FA Posting Date" then
                                        "Posting Date" := 0D;
                                      "FA Posting Type" := FAJnlLineTmp."FA Posting Type";
                                      VALIDATE("FA No.",FAJnlLineTmp."FA No.");
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      Description := PostingDescription;
                                      VALIDATE("Depreciation Book Code",DeprBookCode);
                                      VALIDATE(Amount,FAJnlLineTmp.Amount);
                                      "No. of Depreciation Days" := FAJnlLineTmp."No. of Depreciation Days";
                                      "FA Error Entry No." := FAJnlLineTmp."FA Error Entry No.";
                                      FAJnlNextLineNo := FAJnlNextLineNo + 10000;
                                      "Line No." := FAJnlNextLineNo;
                                      INSERT(true);
                                      FAJnlLineCreatedCount += 1;
                                    until FAJnlLineTmp.NEXT = 0;
                                end;

                                with GenJnlLine do begin
                                  if GenJnlLineTmp.FIND('-') then begin
                                    LOCKTABLE;
                                    FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
                                    NoSeries := FAJnlSetup.GetGenNoSeries(GenJnlLine);
                                    if DocumentNo = '' then
                                      DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,DeprUntilDate,true)
                                    else
                                      DocumentNo2 := DocumentNo;
                                  end;
                                  if GenJnlLineTmp.FIND('-') then
                                    repeat
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      if "Posting Date" = "FA Posting Date" then
                                        "FA Posting Date" := 0D;
                                      "FA Posting Type" := GenJnlLineTmp."FA Posting Type";
                                      "Account Type" := "Account Type"::"Fixed Asset";
                                      VALIDATE("Account No.",GenJnlLineTmp."Account No.");
                                      Description := PostingDescription;
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      VALIDATE("Depreciation Book Code",DeprBookCode);
                                      VALIDATE(Amount,GenJnlLineTmp.Amount);
                                      "No. of Depreciation Days" := GenJnlLineTmp."No. of Depreciation Days";
                                      "FA Error Entry No." := GenJnlLineTmp."FA Error Entry No.";
                                      GenJnlNextLineNo := GenJnlNextLineNo + 1000;
                                      "Line No." := GenJnlNextLineNo;
                                      INSERT(true);
                                      GenJnlLineCreatedCount += 1;
                                      if BalAccount then
                                        FAInsertGLAcc.GetBalAcc2(GenJnlLine,GenJnlNextLineNo);
                                    until GenJnlLineTmp.NEXT = 0;
                                end;
                              END;

               ReqFilterFields="No.","FA Class Code","FA Subclass Code","Budgeted Asset" }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   BalAccount := true;
                   PostingDate := WORKDATE;
                   DeprUntilDate := WORKDATE;
                   if DeprBookCode = '' then begin
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   end;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 15  ;2   ;Field     ;
                  Name=DepreciationBook;
                  CaptionML=ENU=Depreciation Book;
                  ToolTipML=ENU=Specifies the code for the depreciation book to be included in the report or batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 9   ;2   ;Field     ;
                  Name=FAPostingDate;
                  CaptionML=ENU=FA Posting Date;
                  ToolTipML=ENU=Specifies the fixed asset posting date to be used by the batch job. The batch job includes ledger entries up to this date. This date appears in the FA Posting Date field in the resulting journal lines. If the Use Same FA+G/L Posting Dates field has been activated in the depreciation book that is used in the batch job, then this date must be the same as the posting date entered in the Posting Date field.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DeprUntilDate;
                  Importance=Additional;
                  OnValidate=BEGIN
                               DeprUntilDateModified := true;
                             END;
                              }

      { 8   ;2   ;Field     ;
                  Name=UseForceNoOfDays;
                  CaptionML=ENU=Use Force No. of Days;
                  ToolTipML=ENU=Specifies if you want the program to use the number of days, as specified in the field below, in the depreciation calculation.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=UseForceNoOfDays;
                  Importance=Additional;
                  OnValidate=BEGIN
                               if not UseForceNoOfDays then
                                 DaysInPeriod := 0;
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=ForceNoOfDays;
                  CaptionML=ENU=Force No. of Days;
                  ToolTipML=ENU=Specifies if you want the program to use the number of days, as specified in the field below, in the depreciation calculation.;
                  ApplicationArea=#FixedAssets;
                  BlankZero=true;
                  SourceExpr=DaysInPeriod;
                  MinValue=0;
                  Importance=Additional;
                  OnValidate=BEGIN
                               if not UseForceNoOfDays and (DaysInPeriod <> 0) then
                                 ERROR(Text006);
                             END;
                              }

      { 11  ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=ENU=Posting Date;
                  ToolTipML=ENU=Specifies the posting date to be used by the batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               if not DeprUntilDateModified then
                                 DeprUntilDate := PostingDate;
                             END;
                              }

      { 1   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=ENU=Document No.;
                  ToolTipML=ENU=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  Name=Posting Description;
                  CaptionML=ENU=Posting Description;
                  ToolTipML=ENU=Specifies the posting date to be used by the batch job as a filter.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDescription }

      { 3   ;2   ;Field     ;
                  Name=InsertBalAccount;
                  CaptionML=ENU=Insert Bal. Account;
                  ToolTipML=ENU=Specifies if you want the batch job to automatically insert fixed asset entries with balancing accounts.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=BalAccount;
                  Importance=Additional }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.';
      Text001@1001 : TextConst 'ENU=Force No. of Days must be activated.';
      Text002@1002 : TextConst 'ENU="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7."';
      Text003@1003 : TextConst 'ENU=Depreciating fixed asset      #1##########\';
      Text004@1004 : TextConst 'ENU=Not depreciating fixed asset  #2##########\';
      Text005@1005 : TextConst 'ENU=Inserting journal lines       #3##########';
      Text006@1006 : TextConst 'ENU=Use Force No. of Days must be activated.';
      GenJnlLine@1007 : Record "Gen. Journal Line";
      GenJnlLineTmp@1008 : TEMPORARY Record "Gen. Journal Line";
      FASetup@1009 : Record "FA Setup";
      FAJnlLine@1010 : Record "FA Journal Line";
      FAJnlLineTmp@1011 : TEMPORARY Record "FA Journal Line";
      DeprBook@1012 : Record "Depreciation Book";
      FAJnlSetup@1013 : Record "FA Journal Setup";
      CalculateDepr@1014 : Codeunit "Calculate Depreciation";
      FAInsertGLAcc@1015 : Codeunit "FA Insert G/L Account";
      Window@1016 : Dialog;
      DeprAmount@1017 : Decimal;
      Custom1Amount@1018 : Decimal;
      NumberOfDays@1019 : Integer;
      Custom1NumberOfDays@1020 : Integer;
      DeprUntilDate@1021 : Date;
      UseForceNoOfDays@1022 : Boolean;
      DaysInPeriod@1023 : Integer;
      PostingDate@1024 : Date;
      DocumentNo@1025 : Code[20];
      DocumentNo2@1026 : Code[20];
      NoSeries@1027 : Code[10];
      PostingDescription@1028 : Text[50];
      DeprBookCode@1029 : Code[10];
      BalAccount@1030 : Boolean;
      ErrorNo@1031 : Integer;
      Custom1ErrorNo@1032 : Integer;
      FAJnlNextLineNo@1033 : Integer;
      GenJnlNextLineNo@1034 : Integer;
      EntryAmounts@1035 : ARRAY [4] OF Decimal;
      LineNo@1036 : Integer;
      CompletionStatsMsg@1037 : TextConst 'ENU=The depreciation has been calculated.\\No journal lines were created.';
      FAJnlLineCreatedCount@1038 : Integer;
      GenJnlLineCreatedCount@1039 : Integer;
      CompletionStatsFAJnlQst@1042 : TextConst '@@@=The depreciation has been calculated.\\5 fixed asset journal lines were created.\\Do you want to open the Fixed Asset Journal window?;ENU=The depreciation has been calculated.\\%1 fixed asset journal lines were created.\\Do you want to open the Fixed Asset Journal window?';
      CompletionStatsGenJnlQst@1043 : TextConst '@@@=The depreciation has been calculated.\\2 fixed asset G/L  journal lines were created.\\Do you want to open the Fixed Asset G/L Journal window?;ENU=The depreciation has been calculated.\\%1 fixed asset G/L journal lines were created.\\Do you want to open the Fixed Asset G/L Journal window?';
      DeprUntilDateModified@1040 : Boolean;

    PROCEDURE InitializeRequest@1(DeprBookCodeFrom@1007 : Code[10];DeprUntilDateFrom@1000 : Date;UseForceNoOfDaysFrom@1001 : Boolean;DaysInPeriodFrom@1002 : Integer;PostingDateFrom@1003 : Date;DocumentNoFrom@1004 : Code[20];PostingDescriptionFrom@1005 : Text[50];BalAccountFrom@1006 : Boolean);
    BEGIN
      DeprBookCode := DeprBookCodeFrom;
      DeprUntilDate := DeprUntilDateFrom;
      UseForceNoOfDays := UseForceNoOfDaysFrom;
      DaysInPeriod := DaysInPeriodFrom;
      PostingDate := PostingDateFrom;
      DocumentNo := DocumentNoFrom;
      PostingDescription := PostingDescriptionFrom;
      BalAccount := BalAccountFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

