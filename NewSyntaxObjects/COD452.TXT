OBJECT Codeunit 452 Report Distribution Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    EventSubscriberInstance=Manual;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      HideDialog@1008 : Boolean;

    [Internal]
    PROCEDURE VANDocumentReport@5(HeaderDoc@1000 : Variant;TempDocumentSendingProfile@1002 : TEMPORARY Record "Document Sending Profile");
    VAR
      ElectronicDocumentFormat@1003 : Record "Electronic Document Format";
      RecordExportBuffer@1008 : Record "Record Export Buffer";
      DocExchServiceMgt@1005 : Codeunit "Doc. Exch. Service Mgt.";
      RecordRef@1006 : RecordRef;
      SpecificRecordRef@1007 : RecordRef;
      XMLPath@1004 : Text[250];
      ClientFileName@1001 : Text[250];
    BEGIN
      RecordRef.GETTABLE(HeaderDoc);
      if RecordRef.FINDSET then
        repeat
          SpecificRecordRef.GET(RecordRef.RECORDID);
          SpecificRecordRef.SETRECFILTER;
          ElectronicDocumentFormat.SendElectronically(
            XMLPath,ClientFileName,SpecificRecordRef,TempDocumentSendingProfile."Electronic Format");
          if ElectronicDocumentFormat."Delivery Codeunit ID" = 0 then
            DocExchServiceMgt.SendDocument(SpecificRecordRef,XMLPath)
          else begin
            RecordExportBuffer.RecordID := SpecificRecordRef.RECORDID;
            RecordExportBuffer.ClientFileName := ClientFileName;
            RecordExportBuffer.ServerFilePath := XMLPath;
            CODEUNIT.RUN(ElectronicDocumentFormat."Delivery Codeunit ID",RecordExportBuffer);
          end;
        until RecordRef.NEXT = 0;
    END;

    [External]
    PROCEDURE DownloadPdfOnClient@12(ServerPdfFilePath@1000 : Text) : Text;
    VAR
      FileManagement@1001 : Codeunit "File Management";
      ClientPdfFilePath@1002 : Text;
    BEGIN
      ClientPdfFilePath := FileManagement.DownloadTempFile(ServerPdfFilePath);
      ERASE(ServerPdfFilePath);
      exit(ClientPdfFilePath);
    END;

    [External]
    PROCEDURE InitializeFrom@10(NewHideDialog@1001 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    LOCAL PROCEDURE GetDocumentType@7(DocumentVariant@1000 : Variant) : Text[50];
    VAR
      DummySalesHeader@1002 : Record "Sales Header";
      DummyServiceHeader@1003 : Record "Service Header";
      DummyPurchaseHeader@1004 : Record "Purchase Header";
      DocumentRecordRef@1005 : RecordRef;
    BEGIN
      DocumentRecordRef.GETTABLE(DocumentVariant);
      case DocumentRecordRef.NUMBER of
        DATABASE::"Sales Invoice Header":
          exit(FORMAT(DummySalesHeader."Document Type"::Invoice));
        DATABASE::"Sales Cr.Memo Header":
          exit(FORMAT(DummySalesHeader."Document Type"::"Credit Memo"));
        DATABASE::"Service Invoice Header":
          exit(FORMAT(DummyServiceHeader."Document Type"::Invoice));
        DATABASE::"Service Cr.Memo Header":
          exit(FORMAT(DummyServiceHeader."Document Type"::"Credit Memo"));
        DATABASE::"Purchase Header":
          exit(FORMAT(DummyPurchaseHeader."Document Type"::Order));
        DATABASE::"Service Header":
          begin
            DummyServiceHeader := DocumentVariant;
            if DummyServiceHeader."Document Type" = DummyServiceHeader."Document Type"::Quote then
              exit(FORMAT(DummyServiceHeader."Document Type"::Quote));
          end;
        DATABASE::"Sales Header":
          begin
            DummySalesHeader := DocumentVariant;
            if DummySalesHeader."Document Type" = DummySalesHeader."Document Type"::Quote then
              exit(FORMAT(DummySalesHeader."Document Type"::Quote));
          end;
      end;
    END;

    LOCAL PROCEDURE GetBillToCustomer@16(VAR Customer@1000 : Record Customer;DocumentVariant@1001 : Variant);
    VAR
      SalesInvoiceHeader@1003 : Record "Sales Invoice Header";
      SalesCrMemoHeader@1002 : Record "Sales Cr.Memo Header";
      SalesHeader@1006 : Record "Sales Header";
      ServiceInvoiceHeader@1004 : Record "Service Invoice Header";
      ServiceCrMemoHeader@1007 : Record "Service Cr.Memo Header";
      ServiceHeader@1008 : Record "Service Header";
      Job@1009 : Record Job;
      DocumentRecordRef@1005 : RecordRef;
    BEGIN
      DocumentRecordRef.GETTABLE(DocumentVariant);
      case DocumentRecordRef.NUMBER of
        DATABASE::"Sales Invoice Header":
          begin
            SalesInvoiceHeader := DocumentVariant;
            Customer.GET(SalesInvoiceHeader."Bill-to Customer No.");
          end;
        DATABASE::"Sales Cr.Memo Header":
          begin
            SalesCrMemoHeader := DocumentVariant;
            Customer.GET(SalesCrMemoHeader."Bill-to Customer No.");
          end;
        DATABASE::"Service Invoice Header":
          begin
            ServiceInvoiceHeader := DocumentVariant;
            Customer.GET(ServiceInvoiceHeader."Bill-to Customer No.");
          end;
        DATABASE::"Service Cr.Memo Header":
          begin
            ServiceCrMemoHeader := DocumentVariant;
            Customer.GET(ServiceCrMemoHeader."Bill-to Customer No.");
          end;
        DATABASE::"Service Header":
          begin
            ServiceHeader := DocumentVariant;
            Customer.GET(ServiceHeader."Bill-to Customer No.");
          end;
        DATABASE::"Sales Header":
          begin
            SalesHeader := DocumentVariant;
            Customer.GET(SalesHeader."Bill-to Customer No.");
          end;
        DATABASE::Job:
          begin
            Job := DocumentVariant;
            Customer.GET(Job."Bill-to Customer No.");
          end;
      end;
    END;

    LOCAL PROCEDURE GetBuyFromVendor@2(VAR Vendor@1001 : Record Vendor;DocumentVariant@1000 : Variant);
    VAR
      PurchaseHeader@1009 : Record "Purchase Header";
      DocumentRecordRef@1002 : RecordRef;
    BEGIN
      DocumentRecordRef.GETTABLE(DocumentVariant);
      case DocumentRecordRef.NUMBER of
        DATABASE::"Purchase Header":
          begin
            PurchaseHeader := DocumentVariant;
            Vendor.GET(PurchaseHeader."Buy-from Vendor No.");
          end;
      end;
    END;

    [Internal]
    PROCEDURE SaveFileOnClient@14(ServerFilePath@1000 : Text;ClientFileName@1005 : Text);
    VAR
      FileManagement@1004 : Codeunit "File Management";
    BEGIN
      FileManagement.DownloadHandler(
        ServerFilePath,
        '',
        '',
        FileManagement.GetToFilterText('',ClientFileName),
        ClientFileName);
    END;

    LOCAL PROCEDURE SendAttachment@6(PostedDocumentNo@1000 : Code[20];SendEmailAddress@1001 : Text[250];AttachmentFilePath@1002 : Text[250];AttachmentFileName@1004 : Text[250];DocumentType@1005 : Text[50];SendTo@1009 : Option;ServerEmailBodyFilePath@1003 : Text[250];ReportUsage@1006 : Integer);
    VAR
      DocumentSendingProfile@1007 : Record "Document Sending Profile";
      DocumentMailing@1008 : Codeunit "Document-Mailing";
    BEGIN
      if SendTo = DocumentSendingProfile."Send To"::Disk then begin
        SaveFileOnClient(AttachmentFilePath,AttachmentFileName);
        exit;
      end;

      DocumentMailing.EmailFile(
        AttachmentFilePath,AttachmentFileName,ServerEmailBodyFilePath,PostedDocumentNo,
        SendEmailAddress,DocumentType,HideDialog,ReportUsage);
    END;

    [Internal]
    PROCEDURE SendXmlEmailAttachment@4(DocumentVariant@1000 : Variant;DocumentFormat@1008 : Code[20];ServerEmailBodyFilePath@1004 : Text[250];SendToEmailAddress@1009 : Text[250]);
    VAR
      ElectronicDocumentFormat@1001 : Record "Electronic Document Format";
      Customer@1002 : Record Customer;
      DocumentSendingProfile@1007 : Record "Document Sending Profile";
      ReportSelections@1011 : Record "Report Selections";
      DocumentMailing@1005 : Codeunit "Document-Mailing";
      XMLPath@1006 : Text[250];
      ClientFileName@1003 : Text[250];
      ReportUsage@1010 : Integer;
    BEGIN
      GetBillToCustomer(Customer,DocumentVariant);

      if SendToEmailAddress = '' then
        SendToEmailAddress := DocumentMailing.GetToAddressFromCustomer(Customer."No.");

      DocumentSendingProfile.GET(Customer."Document Sending Profile");
      if DocumentSendingProfile.Usage = DocumentSendingProfile.Usage::"Job Quote" then
        ReportUsage := ReportSelections.Usage::JQ;

      ElectronicDocumentFormat.SendElectronically(XMLPath,ClientFileName,DocumentVariant,DocumentFormat);
      COMMIT;
      SendAttachment(
        ElectronicDocumentFormat.GetDocumentNo(DocumentVariant),
        SendToEmailAddress,
        XMLPath,
        ClientFileName,
        GetDocumentType(DocumentVariant),
        DocumentSendingProfile."Send To"::"Electronic Document",
        ServerEmailBodyFilePath,ReportUsage);
    END;

    [Internal]
    PROCEDURE SendXmlEmailAttachmentVendor@1(DocumentVariant@1003 : Variant;DocumentFormat@1002 : Code[20];ServerEmailBodyFilePath@1001 : Text[250];SendToEmailAddress@1000 : Text[250]);
    VAR
      ElectronicDocumentFormat@1011 : Record "Electronic Document Format";
      Vendor@1010 : Record Vendor;
      DocumentSendingProfile@1009 : Record "Document Sending Profile";
      ReportSelections@1008 : Record "Report Selections";
      DocumentMailing@1007 : Codeunit "Document-Mailing";
      XMLPath@1006 : Text[250];
      ClientFileName@1005 : Text[250];
      ReportUsage@1004 : Integer;
    BEGIN
      GetBuyFromVendor(Vendor,DocumentVariant);

      if SendToEmailAddress = '' then
        SendToEmailAddress := DocumentMailing.GetToAddressFromVendor(Vendor."No.");

      DocumentSendingProfile.GET(Vendor."Document Sending Profile");

      if DocumentSendingProfile.Usage = DocumentSendingProfile.Usage::"Job Quote" then
        ReportUsage := ReportSelections.Usage::JQ;

      ElectronicDocumentFormat.SendElectronically(XMLPath,ClientFileName,DocumentVariant,DocumentFormat);
      COMMIT;
      SendAttachment(
        ElectronicDocumentFormat.GetDocumentNo(DocumentVariant),
        SendToEmailAddress,
        XMLPath,
        ClientFileName,
        GetDocumentType(DocumentVariant),
        DocumentSendingProfile."Send To"::"Electronic Document",
        ServerEmailBodyFilePath,ReportUsage);
    END;

    [Internal]
    PROCEDURE CreateOrAppendZipFile@17(VAR FileManagement@1000 : Codeunit "File Management";ServerFilePath@1001 : Text[250];ClientFileName@1004 : Text[250];VAR ZipPath@1003 : Text[250];VAR ClientZipFileName@1002 : Text[250]);
    BEGIN
      if FileManagement.IsGZip(ServerFilePath) then begin
        ZipPath := ServerFilePath;
        FileManagement.OpenZipFile(ZipPath);
        ClientZipFileName := ClientFileName;
      end else begin
        ZipPath := COPYSTR(FileManagement.CreateZipArchiveObject,1,250);
        FileManagement.AddFileToZipArchive(ServerFilePath,ClientFileName);
        ClientZipFileName := COPYSTR(FileManagement.GetFileNameWithoutExtension(ClientFileName) + '.zip',1,250);
      end;
    END;

    [EventSubscriber(Report,1304,OnBeforeArchiving)]
    LOCAL PROCEDURE SetCancelOnBeforeArchiving@8(VAR CancelArchiving@1000 : Boolean);
    BEGIN
      CancelArchiving := true;
    END;

    BEGIN
    END.
  }
}

