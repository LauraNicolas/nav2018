OBJECT Codeunit 13 Gen. Jnl.-Post Batch
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Gen. Journal Line";
    Permissions=TableData "Gen. Journal Batch"=imd;
    OnRun=VAR
            GenJnlLine@1000 : Record "Gen. Journal Line";
          BEGIN
            GenJnlLine.COPY(Rec);
            Code(GenJnlLine);
            Rec := GenJnlLine;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot exceed %1 characters';
      PostingStateMsg@1001 : TextConst '@@@=This is a message for dialog window. Parameters do not require translation.;ENU=Journal Batch Name    #1##########\\Posting @2@@@@@@@@@@@@@\#3#############';
      CheckingLinesMsg@1003 : TextConst 'ENU=Checking lines';
      CheckingBalanceMsg@1004 : TextConst 'ENU=Checking balance';
      UpdatingBalLinesMsg@1005 : TextConst 'ENU=Updating bal. lines';
      PostingLinesMsg@1006 : TextConst 'ENU=Posting lines';
      PostingReversLinesMsg@1007 : TextConst 'ENU=Posting revers. lines';
      UpdatingLinesMsg@1036 : TextConst 'ENU=Updating lines';
      Text008@1008 : TextConst 'ENU=must be the same on all lines for the same document';
      Text009@1009 : TextConst 'ENU="%1 %2 posted on %3 includes more than one customer or vendor. "';
      Text010@1010 : TextConst 'ENU=In order for the program to calculate VAT, the entries must be separated by another document number or by an empty line.';
      Text012@1012 : TextConst 'ENU="%5 %2 is out of balance by %1. "';
      Text013@1013 : TextConst 'ENU=Please check that %3, %4, %5 and %6 are correct for each line.';
      Text014@1014 : TextConst 'ENU="The lines in %1 are out of balance by %2. "';
      Text015@1015 : TextConst 'ENU=Check that %3 and %4 are correct for each line.';
      Text016@1016 : TextConst 'ENU="Your reversing entries in %4 %2 are out of balance by %1. "';
      Text017@1017 : TextConst 'ENU=Please check whether %3 is correct for each line for this %4.';
      Text018@1018 : TextConst 'ENU="Your reversing entries for %1 are out of balance by %2. "';
      Text019@1019 : TextConst 'ENU="%3 %1 is out of balance due to the additional reporting currency. "';
      Text020@1020 : TextConst 'ENU=Please check that %2 is correct for each line.';
      Text021@1021 : TextConst 'ENU=cannot be specified when using recurring journals.';
      Text022@1022 : TextConst 'ENU=The Balance and Reversing Balance recurring methods can be used only for G/L accounts.';
      Text023@1023 : TextConst 'ENU=Allocations can only be used with recurring journals.';
      Text024@1024 : TextConst 'ENU=<Month Text>';
      Text025@1025 : TextConst 'ENU=A maximum of %1 posting number series can be used in each journal.';
      Text026@1026 : TextConst 'ENU="%5 %2 is out of balance by %1 %7. "';
      Text027@1027 : TextConst 'ENU="The lines in %1 are out of balance by %2 %5. "';
      Text028@1028 : TextConst 'ENU=The Balance and Reversing Balance recurring methods can be used only with Allocations.';
      GenJnlTemplate@1029 : Record "Gen. Journal Template";
      GenJnlBatch@1030 : Record "Gen. Journal Batch";
      GenJnlLine2@1032 : Record "Gen. Journal Line";
      GenJnlLine3@1033 : Record "Gen. Journal Line";
      TempGenJnlLine4@1034 : TEMPORARY Record "Gen. Journal Line";
      GenJnlLine5@1035 : Record "Gen. Journal Line";
      GLEntry@1037 : Record "G/L Entry";
      GLReg@1038 : Record "G/L Register";
      GLAcc@1039 : Record "G/L Account";
      GenJnlAlloc@1042 : Record "Gen. Jnl. Allocation";
      AccountingPeriod@1043 : Record "Accounting Period";
      NoSeries@1044 : TEMPORARY Record "No. Series";
      GLSetup@1045 : Record "General Ledger Setup";
      FAJnlSetup@1046 : Record "FA Journal Setup";
      GenJnlLineTemp@1102601000 : TEMPORARY Record "Gen. Journal Line";
      GenJnlCheckLine@1047 : Codeunit "Gen. Jnl.-Check Line";
      GenJnlPostLine@1048 : Codeunit "Gen. Jnl.-Post Line";
      GenJnlPostPreview@1080 : Codeunit "Gen. Jnl.-Post Preview";
      NoSeriesMgt@1049 : Codeunit NoSeriesManagement;
      NoSeriesMgt2@1050 : ARRAY [10] OF Codeunit NoSeriesManagement;
      ICOutboxMgt@1078 : Codeunit ICInboxOutboxMgt;
      InstructionMgt@1072 : Codeunit "Instruction Mgt.";
      Window@1052 : Dialog;
      GLRegNo@1053 : Integer;
      StartLineNo@1054 : Integer;
      StartLineNoReverse@1055 : Integer;
      LastDate@1056 : Date;
      LastDocType@1057 : Integer;
      LastDocNo@1058 : Code[20];
      LastPostedDocNo@1059 : Code[20];
      CurrentBalance@1060 : Decimal;
      CurrentBalanceReverse@1061 : Decimal;
      Day@1062 : Integer;
      Week@1063 : Integer;
      Month@1064 : Integer;
      MonthText@1065 : Text[30];
      NoOfRecords@1066 : Integer;
      NoOfReversingRecords@1067 : Integer;
      LineCount@1068 : Integer;
      NoOfPostingNoSeries@1069 : Integer;
      PostingNoSeriesNo@1070 : Integer;
      DocCorrection@1071 : Boolean;
      VATEntryCreated@1073 : Boolean;
      LastFAAddCurrExchRate@1074 : Decimal;
      LastCurrencyCode@1075 : Code[10];
      CurrencyBalance@1076 : Decimal;
      Text029@1041 : TextConst '@@@="%1 = Document Type;%2 = Document No.;%3=Posting Date";ENU=%1 %2 posted on %3 includes more than one customer, vendor or IC Partner.';
      Text030@1011 : TextConst 'ENU=You cannot enter G/L Account or Bank Account in both %1 and %2.';
      Text031@1040 : TextConst 'ENU=Line No. %1 does not contain a G/L Account or Bank Account. When the %2 field contains an account number, either the %3 field or the %4 field must contain a G/L Account or Bank Account.';
      RefPostingState@1002 : '"Checking lines","Checking balance","Updating bal. lines","Posting Lines","Posting revers. lines","Updating lines"';
      PreviewMode@1051 : Boolean;
      SkippedLineMsg@1092 : TextConst 'ENU=One or more lines has not been posted because the amount is zero.';
      ConfirmPostingOutsidePeriodQst@1079 : TextConst 'ENU=The posting date of one or more journal lines is outside the current fiscal year. Do you want to continue?';

    LOCAL PROCEDURE Code@7(VAR GenJnlLine@1000 : Record "Gen. Journal Line");
    VAR
      TempMarkedGenJnlLine@1001 : TEMPORARY Record "Gen. Journal Line";
    BEGIN
      with GenJnlLine do begin
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");

        LOCKTABLE;
        GenJnlAlloc.LOCKTABLE;

        GenJnlTemplate.GET("Journal Template Name");
        GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
        if STRLEN(INCSTR(GenJnlBatch.Name)) > MAXSTRLEN(GenJnlBatch.Name) then
          GenJnlBatch.FIELDERROR(
            Name,
            STRSUBSTNO(Text000,MAXSTRLEN(GenJnlBatch.Name)));

        if GenJnlTemplate.Recurring then begin
          TempMarkedGenJnlLine.COPY(GenJnlLine);
          CheckGenJnlLineDates(TempMarkedGenJnlLine,GenJnlLine);
          TempMarkedGenJnlLine.SETRANGE("Posting Date",0D,WORKDATE);
          GLSetup.GET;
        end;

        if GenJnlTemplate.Recurring then
          ProcessLines(TempMarkedGenJnlLine)
        else
          ProcessLines(GenJnlLine);
      end;
    END;

    LOCAL PROCEDURE ProcessLines@43(VAR GenJnlLine@1000 : Record "Gen. Journal Line");
    VAR
      TempGenJnlLine@1015 : TEMPORARY Record "Gen. Journal Line";
      GenJnlLineVATInfoSource@1014 : Record "Gen. Journal Line";
      UpdateAnalysisView@1010 : Codeunit "Update Analysis View";
      ICLastDocNo@1009 : Code[20];
      CurrentICPartner@1005 : Code[20];
      LastLineNo@1008 : Integer;
      ICTransactionNo@1007 : Integer;
      ICLastDocType@1004 : Integer;
      ICLastDate@1006 : Date;
      VATInfoSourceLineIsInserted@1003 : Boolean;
      SkippedLine@1002 : Boolean;
      PostingOutsideFiscalYearConfirmed@1001 : Boolean;
    BEGIN
      with GenJnlLine do begin
        if not FIND('=><') then begin
          "Line No." := 0;
          if PreviewMode then
            GenJnlPostPreview.ThrowError;
          COMMIT;
          exit;
        end;

        Window.OPEN(PostingStateMsg);
        Window.UPDATE(1,"Journal Batch Name");

        // Check lines
        LineCount := 0;
        StartLineNo := "Line No.";
        NoOfRecords := COUNT;
        GenJnlCheckLine.SetBatchMode(true);
        repeat
          LineCount := LineCount + 1;
          UpdateDialog(RefPostingState::"Checking lines",LineCount,NoOfRecords);
          CheckLine(GenJnlLine,PostingOutsideFiscalYearConfirmed);
          TempGenJnlLine := GenJnlLine5;
          TempGenJnlLine.INSERT;
          if NEXT = 0 then
            FINDFIRST;
        until "Line No." = StartLineNo;
        if GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany then
          CheckICDocument(TempGenJnlLine);

        ProcessBalanceOfLines(GenJnlLine,GenJnlLineVATInfoSource,VATInfoSourceLineIsInserted,LastLineNo,CurrentICPartner);

        // Find next register no.
        GLEntry.LOCKTABLE;
        if GLEntry.FINDLAST then;
        FindNextGLRegisterNo;

        // Post lines
        LineCount := 0;
        LastDocNo := '';
        LastPostedDocNo := '';
        TempGenJnlLine4.DELETEALL;
        NoOfReversingRecords := 0;
        FINDSET(true,false);
        repeat
          ProcessICLines(CurrentICPartner,ICTransactionNo,ICLastDocNo,ICLastDate,ICLastDocType,GenJnlLine,TempGenJnlLine);
          GenJnlLine3 := GenJnlLine;
          if not PostGenJournalLine(GenJnlLine3,CurrentICPartner,ICTransactionNo) then
            SkippedLine := true;
        until NEXT = 0;

        // Post reversing lines
        PostReversingLines(TempGenJnlLine4);

        if PreviewMode then
          GenJnlPostPreview.ThrowError;

        // Copy register no. and current journal batch name to general journal
        if not GLReg.FINDLAST or (GLReg."No." <> GLRegNo) then
          GLRegNo := 0;

        INIT;
        "Line No." := GLRegNo;

        // Update/delete lines
        if GLRegNo <> 0 then
          UpdateAndDeleteLines(GenJnlLine);

        if GenJnlBatch."No. Series" <> '' then
          NoSeriesMgt.SaveNoSeries;
        if NoSeries.FINDSET then
          repeat
            EVALUATE(PostingNoSeriesNo,NoSeries.Description);
            NoSeriesMgt2[PostingNoSeriesNo].SaveNoSeries;
          until NoSeries.NEXT = 0;

        COMMIT;
        CLEAR(GenJnlCheckLine);
        CLEAR(GenJnlPostLine);
        CLEARMARKS;
      end;
      UpdateAnalysisView.UpdateAll(0,true);
      GenJnlBatch.OnMoveGenJournalBatch(GLReg.RECORDID);
      COMMIT;

      if SkippedLine and GUIALLOWED then
        MESSAGE(SkippedLineMsg);
    END;

    LOCAL PROCEDURE ProcessBalanceOfLines@42(VAR GenJnlLine@1000 : Record "Gen. Journal Line";VAR GenJnlLineVATInfoSource@1003 : Record "Gen. Journal Line";VAR VATInfoSourceLineIsInserted@1002 : Boolean;VAR LastLineNo@1004 : Integer;CurrentICPartner@1001 : Code[20]);
    VAR
      VATPostingSetup@1006 : Record "VAT Posting Setup";
      BalVatPostingSetup@1005 : Record "VAT Posting Setup";
      ErrorMessage@1007 : Text;
    BEGIN
      with GenJnlLine do begin
        if (GenJnlBatch."No. Series" = '') and (GenJnlBatch."Posting No. Series" = '') and GenJnlTemplate."Force Doc. Balance" then
          SETCURRENTKEY("Document No.");
        LineCount := 0;
        LastDate := 0D;
        LastDocType := 0;
        LastDocNo := '';
        LastFAAddCurrExchRate := 0;
        GenJnlLineTemp.RESET;
        GenJnlLineTemp.DELETEALL;
        VATEntryCreated := false;
        CurrentBalance := 0;
        CurrentBalanceReverse := 0;
        CurrencyBalance := 0;

        FINDSET(true,false);
        LastCurrencyCode := "Currency Code";

        repeat
          LineCount := LineCount + 1;
          UpdateDialog(RefPostingState::"Checking balance",LineCount,NoOfRecords);

          if not EmptyLine then begin
            if not PreviewMode then
              CheckDocNoBasedOnNoSeries(LastDocNo,GenJnlBatch."No. Series",NoSeriesMgt);
            if "Posting No. Series" <> '' then
              TESTFIELD("Posting No. Series",GenJnlBatch."Posting No. Series");
            if ("Posting Date" <> LastDate) or
               ("Document Type" <> LastDocType) or ("Document No." <> LastDocNo)
            then begin
              if Correction then
                GenJnlTemplate.TESTFIELD("Force Doc. Balance",true);
              DocCorrection := Correction;
            end else
              if Correction <> DocCorrection then
                FIELDERROR(Correction,Text008);
          end;
          if ("Posting Date" <> LastDate) or
             GenJnlTemplate."Force Doc. Balance" and
             (("Document Type" <> LastDocType) or ("Document No." <> LastDocNo))
          then begin
            CheckBalance(GenJnlLine);
            CurrencyBalance := 0;
            LastCurrencyCode := "Currency Code";
            GenJnlLineTemp.RESET;
            GenJnlLineTemp.DELETEALL;
          end;

          if Amount <> 0 then begin
            if LastFAAddCurrExchRate <> "FA Add.-Currency Factor" then
              CheckAddExchRateBalance(GenJnlLine);
            if (CurrentBalance = 0) and (CurrentICPartner = '') then begin
              GenJnlLineTemp.RESET;
              GenJnlLineTemp.DELETEALL;
              if VATEntryCreated and VATInfoSourceLineIsInserted then
                UpdateGenJnlLineWithVATInfo(GenJnlLine,GenJnlLineVATInfoSource,StartLineNo,LastLineNo);
              VATEntryCreated := false;
              VATInfoSourceLineIsInserted := false;
              StartLineNo := "Line No.";
            end;
            if CurrentBalanceReverse = 0 then
              StartLineNoReverse := "Line No.";
            UpdateLineBalance;
            CurrentBalance := CurrentBalance + "Balance (LCY)";
            if "Recurring Method" >= "Recurring Method"::"RF Reversing Fixed" then
              CurrentBalanceReverse := CurrentBalanceReverse + "Balance (LCY)";

            UpdateCurrencyBalanceForRecurringLine(GenJnlLine);
          end;

          LastDate := "Posting Date";
          LastDocType := "Document Type";
          if not EmptyLine then
            LastDocNo := "Document No.";
          LastFAAddCurrExchRate := "FA Add.-Currency Factor";
          if GenJnlTemplate."Force Doc. Balance" then begin
            if not VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group") then
              CLEAR(VATPostingSetup);
            if not BalVatPostingSetup.GET("Bal. VAT Bus. Posting Group","Bal. VAT Prod. Posting Group") then
              CLEAR(BalVatPostingSetup);
            VATEntryCreated :=
              VATEntryCreated or
              (("Account Type" = "Account Type"::"G/L Account") and ("Account No." <> '') and
               ("Gen. Posting Type" in ["Gen. Posting Type"::Purchase,"Gen. Posting Type"::Sale]) and
               (VATPostingSetup."VAT %" <> 0)) or
              (("Bal. Account Type" = "Bal. Account Type"::"G/L Account") and ("Bal. Account No." <> '') and
               ("Bal. Gen. Posting Type" in ["Bal. Gen. Posting Type"::Purchase,"Bal. Gen. Posting Type"::Sale]) and
               (BalVatPostingSetup."VAT %" <> 0));
            if IsCustVendICAdded(GenJnlLine) then begin
              GenJnlLineVATInfoSource := GenJnlLine;
              VATInfoSourceLineIsInserted := true;
            end;
            if (GenJnlLineTemp.COUNT > 1) and VATEntryCreated then begin
              ErrorMessage := Text009 + Text010;
              ERROR(ErrorMessage,"Document Type","Document No.","Posting Date");
            end;
            if (GenJnlLineTemp.COUNT > 1) and (CurrentICPartner <> '') and
               (GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany)
            then
              ERROR(
                Text029,
                "Document Type","Document No.","Posting Date");
            LastLineNo := "Line No.";
          end;
        until NEXT = 0;
        CheckBalance(GenJnlLine);
        CopyFields(GenJnlLine);
        if VATEntryCreated and VATInfoSourceLineIsInserted then
          UpdateGenJnlLineWithVATInfo(GenJnlLine,GenJnlLineVATInfoSource,StartLineNo,LastLineNo);
      end;
    END;

    LOCAL PROCEDURE ProcessICLines@49(VAR CurrentICPartner@1001 : Code[20];VAR ICTransactionNo@1002 : Integer;VAR ICLastDocNo@1006 : Code[20];VAR ICLastDate@1005 : Date;VAR ICLastDocType@1003 : Integer;VAR GenJnlLine@1000 : Record "Gen. Journal Line";VAR TempGenJnlLine@1004 : TEMPORARY Record "Gen. Journal Line");
    VAR
      HandledICInboxTrans@1007 : Record "Handled IC Inbox Trans.";
    BEGIN
      with GenJnlLine do
        if (GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany) and not EmptyLine and
           (("Posting Date" <> ICLastDate) or ("Document Type" <> ICLastDocType) or ("Document No." <> ICLastDocNo))
        then begin
          CurrentICPartner := '';
          ICLastDate := "Posting Date";
          ICLastDocType := "Document Type";
          ICLastDocNo := "Document No.";
          TempGenJnlLine.RESET;
          TempGenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
          TempGenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
          TempGenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
          TempGenJnlLine.SETRANGE("Posting Date","Posting Date");
          TempGenJnlLine.SETRANGE("Document No.","Document No.");
          TempGenJnlLine.SETFILTER("IC Partner Code",'<>%1','');
          if TempGenJnlLine.FINDFIRST and (TempGenJnlLine."IC Partner Code" <> '') then begin
            CurrentICPartner := TempGenJnlLine."IC Partner Code";
            if TempGenJnlLine."IC Direction" = TempGenJnlLine."IC Direction"::Outgoing then
              ICTransactionNo := ICOutboxMgt.CreateOutboxJnlTransaction(TempGenJnlLine,false)
            else
              if HandledICInboxTrans.GET(
                   TempGenJnlLine."IC Partner Transaction No.",TempGenJnlLine."IC Partner Code",
                   HandledICInboxTrans."Transaction Source"::"Created by Partner",TempGenJnlLine."Document Type")
              then begin
                HandledICInboxTrans.LOCKTABLE;
                HandledICInboxTrans.Status := HandledICInboxTrans.Status::Posted;
                HandledICInboxTrans.MODIFY;
              end
          end
        end;
    END;

    LOCAL PROCEDURE CheckBalance@8(VAR GenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      with GenJnlLine do begin
        if CurrentBalance <> 0 then begin
          GET("Journal Template Name","Journal Batch Name",StartLineNo);
          if GenJnlTemplate."Force Doc. Balance" then
            ERROR(
              Text012 +
              Text013,
              CurrentBalance,LastDocNo,FIELDCAPTION("Posting Date"),FIELDCAPTION("Document Type"),
              FIELDCAPTION("Document No."),FIELDCAPTION(Amount));
          ERROR(
            Text014 +
            Text015,
            LastDate,CurrentBalance,FIELDCAPTION("Posting Date"),FIELDCAPTION(Amount));
        end;
        if CurrentBalanceReverse <> 0 then begin
          GET("Journal Template Name","Journal Batch Name",StartLineNoReverse);
          if GenJnlTemplate."Force Doc. Balance" then
            ERROR(
              Text016 +
              Text017,
              CurrentBalanceReverse,LastDocNo,FIELDCAPTION("Recurring Method"),FIELDCAPTION("Document No."));
          ERROR(
            Text018 +
            Text017,
            LastDate,CurrentBalanceReverse,FIELDCAPTION("Recurring Method"),FIELDCAPTION("Posting Date"));
        end;
        if (LastCurrencyCode <> '') and (CurrencyBalance <> 0) then begin
          GET("Journal Template Name","Journal Batch Name",StartLineNo);
          if GenJnlTemplate."Force Doc. Balance" then
            ERROR(
              Text026 +
              Text013,
              CurrencyBalance,LastDocNo,FIELDCAPTION("Posting Date"),FIELDCAPTION("Document Type"),
              FIELDCAPTION("Document No."),FIELDCAPTION(Amount),
              LastCurrencyCode);
          ERROR(
            Text027 +
            Text015,
            LastDate,CurrencyBalance,FIELDCAPTION("Posting Date"),FIELDCAPTION(Amount),LastCurrencyCode);
        end;
      end;
    END;

    LOCAL PROCEDURE CheckAddExchRateBalance@9(GenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      with GenJnlLine do
        if CurrentBalance <> 0 then
          ERROR(
            Text019 +
            Text020,
            LastDocNo,FIELDCAPTION("FA Add.-Currency Factor"),FIELDCAPTION("Document No."));
    END;

    LOCAL PROCEDURE CheckRecurringLine@1(VAR GenJnlLine2@1000 : Record "Gen. Journal Line");
    VAR
      DummyDateFormula@1001 : DateFormula;
    BEGIN
      with GenJnlLine2 do begin
        if "Account No." <> '' then
          if GenJnlTemplate.Recurring then begin
            TESTFIELD("Recurring Method");
            TESTFIELD("Recurring Frequency");
            if "Bal. Account No." <> '' then
              FIELDERROR("Bal. Account No.",Text021);
            case "Recurring Method" of
              "Recurring Method"::"V  Variable","Recurring Method"::"RV Reversing Variable",
              "Recurring Method"::"F  Fixed","Recurring Method"::"RF Reversing Fixed":
                TESTFIELD(Amount);
              "Recurring Method"::"B  Balance","Recurring Method"::"RB Reversing Balance":
                TESTFIELD(Amount,0);
            end;
          end else begin
            TESTFIELD("Recurring Method",0);
            TESTFIELD("Recurring Frequency",DummyDateFormula);
          end;
      end;
    END;

    LOCAL PROCEDURE UpdateRecurringAmt@2(VAR GenJnlLine2@1000 : Record "Gen. Journal Line");
    BEGIN
      with GenJnlLine2 do begin
        if ("Account No." <> '') and
           ("Recurring Method" in
            ["Recurring Method"::"B  Balance","Recurring Method"::"RB Reversing Balance"])
        then begin
          GLEntry.LOCKTABLE;
          if "Account Type" = "Account Type"::"G/L Account" then begin
            GLAcc."No." := "Account No.";
            GLAcc.SETRANGE("Date Filter",0D,"Posting Date");
            if GLSetup."Additional Reporting Currency" <> '' then begin
              "Source Currency Code" := GLSetup."Additional Reporting Currency";
              GLAcc.CALCFIELDS("Additional-Currency Net Change");
              "Source Currency Amount" := -GLAcc."Additional-Currency Net Change";
              GenJnlAlloc.UpdateAllocationsAddCurr(GenJnlLine2,"Source Currency Amount");
            end;
            GLAcc.CALCFIELDS("Net Change");
            VALIDATE(Amount,-GLAcc."Net Change");
          end else
            ERROR(
              Text022);
        end;
      end;
    END;

    LOCAL PROCEDURE CheckAllocations@3(VAR GenJnlLine2@1000 : Record "Gen. Journal Line");
    BEGIN
      with GenJnlLine2 do begin
        if "Account No." <> '' then begin
          if "Recurring Method" in
             ["Recurring Method"::"B  Balance",
              "Recurring Method"::"RB Reversing Balance"]
          then begin
            GenJnlAlloc.RESET;
            GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
            GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
            GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
            if GenJnlAlloc.ISEMPTY then
              ERROR(
                Text028);
          end;

          GenJnlAlloc.RESET;
          GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
          GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
          GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
          GenJnlAlloc.SETFILTER(Amount,'<>0');
          if not GenJnlAlloc.ISEMPTY then begin
            if not GenJnlTemplate.Recurring then
              ERROR(Text023);
            GenJnlAlloc.SETRANGE("Account No.",'');
            if GenJnlAlloc.FINDFIRST then
              GenJnlAlloc.TESTFIELD("Account No.");
          end;
        end;
      end;
    END;

    LOCAL PROCEDURE MakeRecurringTexts@4(VAR GenJnlLine2@1000 : Record "Gen. Journal Line");
    BEGIN
      with GenJnlLine2 do begin
        if ("Account No." <> '') and ("Recurring Method" <> 0) then begin
          Day := DATE2DMY("Posting Date",1);
          Week := DATE2DWY("Posting Date",2);
          Month := DATE2DMY("Posting Date",2);
          MonthText := FORMAT("Posting Date",0,Text024);
          AccountingPeriod.SETRANGE("Starting Date",0D,"Posting Date");
          if not AccountingPeriod.FINDLAST then
            AccountingPeriod.Name := '';
          "Document No." :=
            DELCHR(
              PADSTR(
                STRSUBSTNO("Document No.",Day,Week,Month,MonthText,AccountingPeriod.Name),
                MAXSTRLEN("Document No.")),
              '>');
          Description :=
            DELCHR(
              PADSTR(
                STRSUBSTNO(Description,Day,Week,Month,MonthText,AccountingPeriod.Name),
                MAXSTRLEN(Description)),
              '>');
        end;
      end;
    END;

    LOCAL PROCEDURE PostAllocations@5(VAR AllocateGenJnlLine@1000 : Record "Gen. Journal Line";Reversing@1001 : Boolean);
    BEGIN
      with AllocateGenJnlLine do
        if "Account No." <> '' then begin
          GenJnlAlloc.RESET;
          GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
          GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
          GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
          GenJnlAlloc.SETFILTER("Account No.",'<>%1','');
          if GenJnlAlloc.FINDSET(true,false) then begin
            GenJnlLine2.INIT;
            GenJnlLine2."Account Type" := GenJnlLine2."Account Type"::"G/L Account";
            GenJnlLine2."Posting Date" := "Posting Date";
            GenJnlLine2."Document Type" := "Document Type";
            GenJnlLine2."Document No." := "Document No.";
            GenJnlLine2.Description := Description;
            GenJnlLine2."Source Code" := "Source Code";
            GenJnlLine2."Journal Batch Name" := "Journal Batch Name";
            GenJnlLine2."Line No." := "Line No.";
            GenJnlLine2."Reason Code" := "Reason Code";
            GenJnlLine2.Correction := Correction;
            GenJnlLine2."Recurring Method" := "Recurring Method";
            if "Account Type" in ["Account Type"::Customer,"Account Type"::Vendor] then begin
              GenJnlLine2."Bill-to/Pay-to No." := "Bill-to/Pay-to No.";
              GenJnlLine2."Ship-to/Order Address Code" := "Ship-to/Order Address Code";
            end;
            repeat
              GenJnlLine2.CopyFromGenJnlAllocation(GenJnlAlloc);
              GenJnlLine2."Shortcut Dimension 1 Code" := GenJnlAlloc."Shortcut Dimension 1 Code";
              GenJnlLine2."Shortcut Dimension 2 Code" := GenJnlAlloc."Shortcut Dimension 2 Code";
              GenJnlLine2."Dimension Set ID" := GenJnlAlloc."Dimension Set ID";
              GenJnlLine2."Allow Zero-Amount Posting" := true;
              PrepareGenJnlLineAddCurr(GenJnlLine2);
              if not Reversing then begin
                GenJnlPostLine.RunWithCheck(GenJnlLine2);
                if "Recurring Method" in
                   ["Recurring Method"::"V  Variable","Recurring Method"::"B  Balance"]
                then begin
                  GenJnlAlloc.Amount := 0;
                  GenJnlAlloc."Additional-Currency Amount" := 0;
                  GenJnlAlloc.MODIFY;
                end;
              end else begin
                MultiplyAmounts(GenJnlLine2,-1);
                GenJnlLine2."Reversing Entry" := true;
                GenJnlPostLine.RunWithCheck(GenJnlLine2);
                if "Recurring Method" in
                   ["Recurring Method"::"RV Reversing Variable",
                    "Recurring Method"::"RB Reversing Balance"]
                then begin
                  GenJnlAlloc.Amount := 0;
                  GenJnlAlloc."Additional-Currency Amount" := 0;
                  GenJnlAlloc.MODIFY;
                end;
              end;
            until GenJnlAlloc.NEXT = 0;
          end;
        end;

      OnAfterPostAllocations(AllocateGenJnlLine,Reversing);
    END;

    LOCAL PROCEDURE MultiplyAmounts@6(VAR GenJnlLine2@1000 : Record "Gen. Journal Line";Factor@1001 : Decimal);
    BEGIN
      with GenJnlLine2 do
        if "Account No." <> '' then begin
          Amount := Amount * Factor;
          "Debit Amount" := "Debit Amount" * Factor;
          "Credit Amount" := "Credit Amount" * Factor;
          "Amount (LCY)" := "Amount (LCY)" * Factor;
          "Balance (LCY)" := "Balance (LCY)" * Factor;
          "Sales/Purch. (LCY)" := "Sales/Purch. (LCY)" * Factor;
          "Profit (LCY)" := "Profit (LCY)" * Factor;
          "Inv. Discount (LCY)" := "Inv. Discount (LCY)" * Factor;
          Quantity := Quantity * Factor;
          "VAT Amount" := "VAT Amount" * Factor;
          "VAT Base Amount" := "VAT Base Amount" * Factor;
          "VAT Amount (LCY)" := "VAT Amount (LCY)" * Factor;
          "VAT Base Amount (LCY)" := "VAT Base Amount (LCY)" * Factor;
          "Source Currency Amount" := "Source Currency Amount" * Factor;
          if "Job No." <> '' then begin
            "Job Quantity" := "Job Quantity" * Factor;
            "Job Total Cost (LCY)" := "Job Total Cost (LCY)" * Factor;
            "Job Total Price (LCY)" := "Job Total Price (LCY)" * Factor;
            "Job Line Amount (LCY)" := "Job Line Amount (LCY)" * Factor;
            "Job Total Cost" := "Job Total Cost" * Factor;
            "Job Total Price" := "Job Total Price" * Factor;
            "Job Line Amount" := "Job Line Amount" * Factor;
            "Job Line Discount Amount" := "Job Line Discount Amount" * Factor;
            "Job Line Disc. Amount (LCY)" := "Job Line Disc. Amount (LCY)" * Factor;
          end;
        end;

      OnAfterMultiplyAmounts(GenJnlLine2,Factor);
    END;

    LOCAL PROCEDURE CheckDocumentNo@11(VAR GenJnlLine2@1000 : Record "Gen. Journal Line");
    BEGIN
      with GenJnlLine2 do begin
        if "Posting No. Series" = '' then
          "Posting No. Series" := GenJnlBatch."No. Series"
        else
          if not EmptyLine then
            if "Document No." = LastDocNo then
              "Document No." := LastPostedDocNo
            else begin
              if not NoSeries.GET("Posting No. Series") then begin
                NoOfPostingNoSeries := NoOfPostingNoSeries + 1;
                if NoOfPostingNoSeries > ARRAYLEN(NoSeriesMgt2) then
                  ERROR(
                    Text025,
                    ARRAYLEN(NoSeriesMgt2));
                NoSeries.Code := "Posting No. Series";
                NoSeries.Description := FORMAT(NoOfPostingNoSeries);
                NoSeries.INSERT;
              end;
              LastDocNo := "Document No.";
              EVALUATE(PostingNoSeriesNo,NoSeries.Description);
              "Document No." :=
                NoSeriesMgt2[PostingNoSeriesNo].GetNextNo("Posting No. Series","Posting Date",true);
              LastPostedDocNo := "Document No.";
            end;
      end;
    END;

    LOCAL PROCEDURE PrepareGenJnlLineAddCurr@10(VAR GenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      if (GLSetup."Additional Reporting Currency" <> '') and
         (GenJnlLine."Recurring Method" in
          [GenJnlLine."Recurring Method"::"B  Balance",
           GenJnlLine."Recurring Method"::"RB Reversing Balance"])
      then begin
        GenJnlLine."Source Currency Code" := GLSetup."Additional Reporting Currency";
        if (GenJnlLine.Amount = 0) and
           (GenJnlLine."Source Currency Amount" <> 0)
        then begin
          GenJnlLine."Additional-Currency Posting" :=
            GenJnlLine."Additional-Currency Posting"::"Additional-Currency Amount Only";
          GenJnlLine.Amount := GenJnlLine."Source Currency Amount";
          GenJnlLine."Source Currency Amount" := 0;
        end;
      end;
    END;

    LOCAL PROCEDURE CopyFields@12(VAR GenJnlLine@1004 : Record "Gen. Journal Line");
    VAR
      GenJnlLine4@1000 : Record "Gen. Journal Line";
      GenJnlLine6@1001 : Record "Gen. Journal Line";
      TempGenJnlLine@1007 : TEMPORARY Record "Gen. Journal Line";
      JnlLineTotalQty@1002 : Integer;
      RefPostingSubState@1003 : '"Check account","Check bal. account","Update lines"';
    BEGIN
      GenJnlLine6.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
      GenJnlLine4.FILTERGROUP(2);
      GenJnlLine4.COPY(GenJnlLine);
      GenJnlLine4.FILTERGROUP(0);
      GenJnlLine6.FILTERGROUP(2);
      GenJnlLine6.COPY(GenJnlLine);
      GenJnlLine6.FILTERGROUP(0);
      GenJnlLine6.SETRANGE("Bill-to/Pay-to No.",'');
      GenJnlLine4.SETFILTER(
        "Account Type",'%1|%2',GenJnlLine4."Account Type"::Customer,GenJnlLine4."Account Type"::Vendor);
      GenJnlLine4.SETRANGE("Bal. Account No.",'');
      CheckAndCopyBalancingData(GenJnlLine4,GenJnlLine6,TempGenJnlLine,false);

      GenJnlLine4.SETRANGE("Account Type");
      GenJnlLine4.SETRANGE("Bal. Account No.");
      GenJnlLine4.SETFILTER(
        "Bal. Account Type",'%1|%2',GenJnlLine4."Bal. Account Type"::Customer,GenJnlLine4."Bal. Account Type"::Vendor);
      GenJnlLine4.SETRANGE("Account No.",'');
      CheckAndCopyBalancingData(GenJnlLine4,GenJnlLine6,TempGenJnlLine,true);

      JnlLineTotalQty := TempGenJnlLine.COUNT;
      LineCount := 0;
      if TempGenJnlLine.FINDSET then
        repeat
          LineCount := LineCount + 1;
          UpdateDialogUpdateBalLines(RefPostingSubState::"Update lines",LineCount,JnlLineTotalQty);
          GenJnlLine4.GET(TempGenJnlLine."Journal Template Name",TempGenJnlLine."Journal Batch Name",TempGenJnlLine."Line No.");
          CopyGenJnlLineBalancingData(GenJnlLine4,TempGenJnlLine);
          GenJnlLine4.MODIFY;
        until TempGenJnlLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckICDocument@13(VAR TempGenJnlLine1@1001 : TEMPORARY Record "Gen. Journal Line");
    VAR
      TempGenJnlLine2@1002 : TEMPORARY Record "Gen. Journal Line";
      CurrentICPartner@1000 : Code[20];
    BEGIN
      with TempGenJnlLine1 do begin
        SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");
        FIND('-');
        repeat
          if ("Posting Date" <> LastDate) or ("Document Type" <> LastDocType) or ("Document No." <> LastDocNo) then begin
            TempGenJnlLine2 := TempGenJnlLine1;
            SETRANGE("Posting Date","Posting Date");
            SETRANGE("Document No.","Document No.");
            SETFILTER("IC Partner Code",'<>%1','');
            if FIND('-') then
              CurrentICPartner := "IC Partner Code"
            else
              CurrentICPartner := '';
            SETRANGE("Posting Date");
            SETRANGE("Document No.");
            SETRANGE("IC Partner Code");
            LastDate := "Posting Date";
            LastDocType := "Document Type";
            LastDocNo := "Document No.";
            TempGenJnlLine1 := TempGenJnlLine2;
          end;
          if (CurrentICPartner <> '') and ("IC Direction" = "IC Direction"::Outgoing) then begin
            if ("Account Type" in ["Account Type"::"G/L Account","Account Type"::"Bank Account"]) and
               ("Bal. Account Type" in ["Bal. Account Type"::"G/L Account","Account Type"::"Bank Account"]) and
               ("Account No." <> '') and
               ("Bal. Account No." <> '')
            then
              ERROR(Text030,FIELDCAPTION("Account No."),FIELDCAPTION("Bal. Account No."));
            if (("Account Type" in ["Account Type"::"G/L Account","Account Type"::"Bank Account"]) and ("Account No." <> '')) xor
               (("Bal. Account Type" in ["Bal. Account Type"::"G/L Account","Account Type"::"Bank Account"]) and
                ("Bal. Account No." <> ''))
            then
              TESTFIELD("IC Partner G/L Acc. No.")
            else
              if "IC Partner G/L Acc. No." <> '' then
                ERROR(Text031,
                  "Line No.",FIELDCAPTION("IC Partner G/L Acc. No."),FIELDCAPTION("Account No."),
                  FIELDCAPTION("Bal. Account No."));
          end else
            TESTFIELD("IC Partner G/L Acc. No.",'');
        until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE UpdateIncomingDocument@15(VAR GenJnlLine@1000 : Record "Gen. Journal Line");
    VAR
      IncomingDocument@1001 : Record "Incoming Document";
    BEGIN
      with GenJnlLine do
        IncomingDocument.UpdateIncomingDocumentFromPosting("Incoming Document Entry No.","Posting Date","Document No.");
    END;

    LOCAL PROCEDURE AddCustVendIC@14(AccountType@1000 : Option;AccountNo@1001 : Code[20]) : Boolean;
    BEGIN
      GenJnlLineTemp.SETRANGE("Account Type",AccountType);
      GenJnlLineTemp.SETRANGE("Account No.",AccountNo);
      if GenJnlLineTemp.FINDFIRST then
        exit(false);

      GenJnlLineTemp.RESET;
      if GenJnlLineTemp.FINDLAST then
        GenJnlLineTemp."Line No." := GenJnlLineTemp."Line No." + 10000
      else
        GenJnlLineTemp."Line No." := 10000;

      GenJnlLineTemp."Account Type" := AccountType;
      GenJnlLineTemp."Account No." := AccountNo;
      GenJnlLineTemp.INSERT;
      exit(true);
    END;

    LOCAL PROCEDURE CopyGenJnlLineBalancingData@18(VAR GenJnlLineTo@1000 : Record "Gen. Journal Line";VAR GenJnlLineFrom@1002 : Record "Gen. Journal Line");
    BEGIN
      GenJnlLineTo."Bill-to/Pay-to No." := GenJnlLineFrom."Bill-to/Pay-to No.";
      GenJnlLineTo."Ship-to/Order Address Code" := GenJnlLineFrom."Ship-to/Order Address Code";
      GenJnlLineTo."VAT Registration No." := GenJnlLineFrom."VAT Registration No.";
      GenJnlLineTo."Country/Region Code" := GenJnlLineFrom."Country/Region Code";
    END;

    LOCAL PROCEDURE CheckGenPostingType@19(GenJnlLine6@1000 : Record "Gen. Journal Line";AccountType@1001 : '"G/L Account",Customer,Vendor,"Bank Account","Fixed Asset","IC Partner"');
    BEGIN
      if (AccountType = AccountType::Customer) and
         (GenJnlLine6."Gen. Posting Type" = GenJnlLine6."Gen. Posting Type"::Purchase) or
         (AccountType = AccountType::Vendor) and
         (GenJnlLine6."Gen. Posting Type" = GenJnlLine6."Gen. Posting Type"::Sale)
      then
        GenJnlLine6.FIELDERROR("Gen. Posting Type");
      if (AccountType = AccountType::Customer) and
         (GenJnlLine6."Bal. Gen. Posting Type" = GenJnlLine6."Bal. Gen. Posting Type"::Purchase) or
         (AccountType = AccountType::Vendor) and
         (GenJnlLine6."Bal. Gen. Posting Type" = GenJnlLine6."Bal. Gen. Posting Type"::Sale)
      then
        GenJnlLine6.FIELDERROR("Bal. Gen. Posting Type");
    END;

    LOCAL PROCEDURE CheckAndCopyBalancingData@16(VAR GenJnlLine4@1002 : Record "Gen. Journal Line";VAR GenJnlLine6@1001 : Record "Gen. Journal Line";VAR TempGenJnlLine@1004 : TEMPORARY Record "Gen. Journal Line";CheckBalAcount@1003 : Boolean);
    VAR
      AccountType@1005 : '"G/L Account",Customer,Vendor,"Bank Account","Fixed Asset","IC Partner"';
      CheckAmount@1000 : Decimal;
      JnlLineTotalQty@1007 : Integer;
      RefPostingSubState@1009 : '"Check account","Check bal. account","Update lines"';
    BEGIN
      JnlLineTotalQty := GenJnlLine4.COUNT;
      LineCount := 0;
      if CheckBalAcount then
        RefPostingSubState := RefPostingSubState::"Check bal. account"
      else
        RefPostingSubState := RefPostingSubState::"Check account";
      if GenJnlLine4.FINDSET then
        repeat
          LineCount := LineCount + 1;
          UpdateDialogUpdateBalLines(RefPostingSubState,LineCount,JnlLineTotalQty);

          GenJnlLine6.SETRANGE("Posting Date",GenJnlLine4."Posting Date");
          GenJnlLine6.SETRANGE("Document No.",GenJnlLine4."Document No.");
          AccountType := GetPostingTypeFilter(GenJnlLine4,CheckBalAcount);
          CheckAmount := 0;
          if GenJnlLine6.FINDSET then
            repeat
              if (GenJnlLine6."Account No." = '') <> (GenJnlLine6."Bal. Account No." = '') then begin
                CheckGenPostingType(GenJnlLine6,AccountType);
                TempGenJnlLine := GenJnlLine6;
                CopyGenJnlLineBalancingData(TempGenJnlLine,GenJnlLine4);
                if TempGenJnlLine.INSERT then;
                CheckAmount := CheckAmount + GenJnlLine6.Amount;
              end;
            until (GenJnlLine6.NEXT = 0) or (-GenJnlLine4.Amount = CheckAmount);
        until GenJnlLine4.NEXT = 0;
    END;

    LOCAL PROCEDURE IsCustVendICAdded@30(GenJournalLine@1000 : Record "Gen. Journal Line") : Boolean;
    BEGIN
      with GenJournalLine do begin
        if ("Account No." <> '') and
           ("Account Type" in ["Account Type"::Customer,"Account Type"::Vendor,
                               "Account Type"::"IC Partner"])
        then
          exit(AddCustVendIC("Account Type","Account No."));

        if ("Bal. Account No." <> '') and
           ("Bal. Account Type" in ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,
                                    "Bal. Account Type"::"IC Partner"])
        then
          exit(AddCustVendIC("Bal. Account Type","Bal. Account No."));
      end;

      exit(false);
    END;

    LOCAL PROCEDURE UpdateGenJnlLineWithVATInfo@26(VAR GenJournalLineFiltersSource@1005 : Record "Gen. Journal Line";GenJournalLineVATInfoSource@1000 : Record "Gen. Journal Line";StartLineNo@1003 : Integer;LastLineNo@1004 : Integer);
    VAR
      GenJournalLine@1001 : Record "Gen. Journal Line";
      Finish@1002 : Boolean;
    BEGIN
      with GenJournalLine do begin
        COPY(GenJournalLineFiltersSource);
        "Line No." := StartLineNo;
        Finish := false;
        if GET("Journal Template Name","Journal Batch Name","Line No.") then
          repeat
            if "Line No." <> GenJournalLineVATInfoSource."Line No." then begin
              "Bill-to/Pay-to No." := GenJournalLineVATInfoSource."Bill-to/Pay-to No.";
              "Country/Region Code" := GenJournalLineVATInfoSource."Country/Region Code";
              "VAT Registration No." := GenJournalLineVATInfoSource."VAT Registration No.";
              MODIFY;
            end;
            Finish := "Line No." = LastLineNo;
          until (NEXT = 0) or Finish;
      end;
    END;

    LOCAL PROCEDURE GetPostingTypeFilter@17(VAR GenJnlLine4@1002 : Record "Gen. Journal Line";CheckBalAcount@1000 : Boolean) : Integer;
    BEGIN
      if CheckBalAcount then
        exit(GenJnlLine4."Bal. Account Type");
      exit(GenJnlLine4."Account Type");
    END;

    LOCAL PROCEDURE UpdateDialog@23(PostingState@1000 : Integer;LineNo@1001 : Integer;TotalLinesQty@1002 : Integer);
    BEGIN
      UpdatePostingState(PostingState,LineNo);
      Window.UPDATE(2,GetProgressBarValue(PostingState,LineNo,TotalLinesQty));
    END;

    LOCAL PROCEDURE UpdateDialogUpdateBalLines@22(PostingSubState@1003 : Integer;LineNo@1001 : Integer;TotalLinesQty@1002 : Integer);
    BEGIN
      UpdatePostingState(RefPostingState::"Updating bal. lines",LineNo);
      Window.UPDATE(
        2,
        GetProgressBarUpdateBalLinesValue(
          CalcProgressPercent(PostingSubState,3,LineCount,TotalLinesQty)));
    END;

    LOCAL PROCEDURE UpdatePostingState@25(PostingState@1000 : Integer;LineNo@1002 : Integer);
    BEGIN
      Window.UPDATE(3,STRSUBSTNO('%1 (%2)',GetPostingStateMsg(PostingState),LineNo));
    END;

    LOCAL PROCEDURE UpdateCurrencyBalanceForRecurringLine@44(VAR GenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      with GenJnlLine do begin
        if "Recurring Method" <> "Recurring Method"::" " then
          CALCFIELDS("Allocated Amt. (LCY)");
        if ("Recurring Method" = "Recurring Method"::" ") or ("Amount (LCY)" <> -"Allocated Amt. (LCY)") then
          if "Currency Code" <> LastCurrencyCode then
            LastCurrencyCode := ''
          else
            if ("Currency Code" <> '') and (("Account No." = '') xor ("Bal. Account No." = '')) then
              if "Account No." <> '' then
                CurrencyBalance := CurrencyBalance + Amount
              else
                CurrencyBalance := CurrencyBalance - Amount;
      end;
    END;

    LOCAL PROCEDURE GetPostingStateMsg@29(PostingState@1000 : Integer) : Text;
    BEGIN
      case PostingState of
        RefPostingState::"Checking lines":
          exit(CheckingLinesMsg);
        RefPostingState::"Checking balance":
          exit(CheckingBalanceMsg);
        RefPostingState::"Updating bal. lines":
          exit(UpdatingBalLinesMsg);
        RefPostingState::"Posting Lines":
          exit(PostingLinesMsg);
        RefPostingState::"Posting revers. lines":
          exit(PostingReversLinesMsg);
        RefPostingState::"Updating lines":
          exit(UpdatingLinesMsg);
      end;
    END;

    LOCAL PROCEDURE GetProgressBarValue@21(PostingState@1002 : Integer;LineNo@1001 : Integer;TotalLinesQty@1000 : Integer) : Integer;
    BEGIN
      exit(ROUND(100 * CalcProgressPercent(PostingState,GetNumberOfPostingStages,LineNo,TotalLinesQty),1));
    END;

    LOCAL PROCEDURE GetProgressBarUpdateBalLinesValue@34(PostingStatePercent@1000 : Decimal) : Integer;
    BEGIN
      exit(ROUND((RefPostingState::"Updating bal. lines" * 100 + PostingStatePercent) / GetNumberOfPostingStages * 100,1));
    END;

    LOCAL PROCEDURE CalcProgressPercent@20(PostingState@1001 : Integer;NumberOfPostingStates@1000 : Integer;LineNo@1002 : Integer;TotalLinesQty@1003 : Integer) : Decimal;
    BEGIN
      exit(100 / NumberOfPostingStates * (PostingState + LineNo / TotalLinesQty));
    END;

    LOCAL PROCEDURE GetNumberOfPostingStages@33() : Integer;
    BEGIN
      if GenJnlTemplate.Recurring then
        exit(6);

      exit(4);
    END;

    LOCAL PROCEDURE FindNextGLRegisterNo@24();
    BEGIN
      GLReg.LOCKTABLE;
      if GLReg.FINDLAST then
        GLRegNo := GLReg."No." + 1
      else
        GLRegNo := 1;
    END;

    LOCAL PROCEDURE CheckGenJnlLineDates@36(VAR MarkedGenJnlLine@1001 : Record "Gen. Journal Line";VAR GenJournalLine@1000 : Record "Gen. Journal Line");
    BEGIN
      with GenJournalLine do begin
        if not FIND then
          FINDSET;
        SETRANGE("Posting Date",0D,WORKDATE);
        if FINDSET then begin
          StartLineNo := "Line No.";
          repeat
            if IsNotExpired(GenJournalLine) and IsPostingDateAllowed(GenJournalLine) then begin
              MarkedGenJnlLine := GenJournalLine;
              MarkedGenJnlLine.INSERT;
            end;
            if NEXT = 0 then
              FINDFIRST;
          until "Line No." = StartLineNo
        end;
        MarkedGenJnlLine := GenJournalLine;
      end;
    END;

    LOCAL PROCEDURE IsNotExpired@50(GenJournalLine@1000 : Record "Gen. Journal Line") : Boolean;
    BEGIN
      exit((GenJournalLine."Expiration Date" = 0D) or (GenJournalLine."Expiration Date" >= GenJournalLine."Posting Date"));
    END;

    LOCAL PROCEDURE IsPostingDateAllowed@48(GenJournalLine@1000 : Record "Gen. Journal Line") : Boolean;
    BEGIN
      exit(not GenJnlCheckLine.DateNotAllowed(GenJournalLine."Posting Date"));
    END;

    [External]
    PROCEDURE SetPreviewMode@27(NewPreviewMode@1000 : Boolean);
    BEGIN
      PreviewMode := NewPreviewMode;
    END;

    LOCAL PROCEDURE PostReversingLines@28(VAR TempGenJnlLine@1000 : TEMPORARY Record "Gen. Journal Line");
    VAR
      GenJournalLine1@1001 : Record "Gen. Journal Line";
      GenJournalLine2@1002 : Record "Gen. Journal Line";
    BEGIN
      LineCount := 0;
      LastDocNo := '';
      LastPostedDocNo := '';
      if TempGenJnlLine.FIND('-') then
        repeat
          GenJournalLine1 := TempGenJnlLine;
          with GenJournalLine1 do begin
            LineCount := LineCount + 1;
            UpdateDialog(RefPostingState::"Posting revers. lines",LineCount,NoOfReversingRecords);
            CheckDocumentNo(GenJournalLine1);
            GenJournalLine2.COPY(GenJournalLine1);
            PrepareGenJnlLineAddCurr(GenJournalLine2);
            GenJnlPostLine.RunWithCheck(GenJournalLine2);
            PostAllocations(GenJournalLine1,true);
          end;
        until TempGenJnlLine.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateAndDeleteLines@31(VAR GenJnlLine@1003 : Record "Gen. Journal Line");
    VAR
      TempGenJnlLine2@1002 : TEMPORARY Record "Gen. Journal Line";
      OldVATAmount@1000 : Decimal;
      OldVATPct@1001 : Decimal;
    BEGIN
      ClearDataExchEntries(GenJnlLine);
      if GenJnlTemplate.Recurring then begin
        // Recurring journal
        LineCount := 0;
        GenJnlLine2.COPY(GenJnlLine);
        GenJnlLine2.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Line No.");
        GenJnlLine2.FINDSET(true,false);
        repeat
          LineCount := LineCount + 1;
          UpdateDialog(RefPostingState::"Updating lines",LineCount,NoOfRecords);
          OldVATAmount := GenJnlLine2."VAT Amount";
          OldVATPct := GenJnlLine2."VAT %";
          if GenJnlLine2."Posting Date" <> 0D then
            GenJnlLine2.VALIDATE(
              "Posting Date",CALCDATE(GenJnlLine2."Recurring Frequency",GenJnlLine2."Posting Date"));
          if not
             (GenJnlLine2."Recurring Method" in
              [GenJnlLine2."Recurring Method"::"F  Fixed",
               GenJnlLine2."Recurring Method"::"RF Reversing Fixed"])
          then
            MultiplyAmounts(GenJnlLine2,0)
          else
            if (GenJnlLine2."VAT %" = OldVATPct) and (GenJnlLine2."VAT Amount" <> OldVATAmount) then
              GenJnlLine2.VALIDATE("VAT Amount",OldVATAmount);
          GenJnlLine2.MODIFY;
        until GenJnlLine2.NEXT = 0;
      end else begin
        // Not a recurring journal
        GenJnlLine2.COPY(GenJnlLine);
        GenJnlLine2.SETFILTER("Account No.",'<>%1','');
        if GenJnlLine2.FINDLAST then; // Remember the last line
        GenJnlLine3.COPY(GenJnlLine);
        GenJnlLine3.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Line No.");
        GenJnlLine3.DELETEALL;
        GenJnlLine3.RESET;
        GenJnlLine3.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
        GenJnlLine3.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        if not GenJnlLine3.FINDLAST then
          if INCSTR(GenJnlLine."Journal Batch Name") <> '' then begin
            GenJnlBatch.DELETE;
            if GenJnlTemplate.Type = GenJnlTemplate.Type::Assets then
              FAJnlSetup.IncGenJnlBatchName(GenJnlBatch);
            GenJnlBatch.Name := INCSTR(GenJnlLine."Journal Batch Name");
            if GenJnlBatch.INSERT then;
            GenJnlLine."Journal Batch Name" := GenJnlBatch.Name;
          end;

        GenJnlLine3.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        if (GenJnlBatch."No. Series" = '') and not GenJnlLine3.FINDLAST then begin
          GenJnlLine3.INIT;
          GenJnlLine3."Journal Template Name" := GenJnlLine."Journal Template Name";
          GenJnlLine3."Journal Batch Name" := GenJnlLine."Journal Batch Name";
          GenJnlLine3."Line No." := 10000;
          GenJnlLine3.INSERT;
          TempGenJnlLine2 := GenJnlLine2;
          TempGenJnlLine2."Balance (LCY)" := 0;
          GenJnlLine3.SetUpNewLine(TempGenJnlLine2,0,true);
          GenJnlLine3.MODIFY;
        end;
      end;
    END;

    [Internal]
    PROCEDURE Preview@32(VAR GenJournalLine@1000 : Record "Gen. Journal Line");
    VAR
      GenJnlLine@1001 : Record "Gen. Journal Line";
    BEGIN
      PreviewMode := true;
      GenJnlLine.COPY(GenJournalLine);
      Code(GenJnlLine);
    END;

    LOCAL PROCEDURE CheckRestrictions@35(VAR GenJournalLine@1000 : Record "Gen. Journal Line");
    BEGIN
      if not PreviewMode then
        GenJournalLine.OnCheckGenJournalLinePostRestrictions;
    END;

    LOCAL PROCEDURE ClearDataExchEntries@37(VAR PassedGenJnlLine@1000 : Record "Gen. Journal Line");
    VAR
      GenJnlLine@1001 : Record "Gen. Journal Line";
    BEGIN
      GenJnlLine.COPY(PassedGenJnlLine);
      if GenJnlLine.FINDSET then
        repeat
          GenJnlLine.ClearDataExchangeEntries(true);
        until GenJnlLine.NEXT = 0;
    END;

    LOCAL PROCEDURE PostGenJournalLine@39(VAR GenJournalLine@1000 : Record "Gen. Journal Line";CurrentICPartner@1001 : Code[20];ICTransactionNo@1002 : Integer) : Boolean;
    BEGIN
      with GenJournalLine do begin
        if NeedCheckZeroAmount and (Amount = 0) and IsRecurring then
          exit(false);

        LineCount := LineCount + 1;
        if CurrentICPartner <> '' then
          "IC Partner Code" := CurrentICPartner;
        UpdateDialog(RefPostingState::"Posting Lines",LineCount,NoOfRecords);
        MakeRecurringTexts(GenJournalLine);
        CheckDocumentNo(GenJournalLine);
        GenJnlLine5.COPY(GenJournalLine);
        PrepareGenJnlLineAddCurr(GenJnlLine5);
        UpdateIncomingDocument(GenJnlLine5);
        OnBeforePostGenJnlLine(GenJnlLine5);
        GenJnlPostLine.RunWithoutCheck(GenJnlLine5);
        OnAfterPostGenJnlLine(GenJnlLine5);
        if (GenJnlTemplate.Type = GenJnlTemplate.Type::Intercompany) and (CurrentICPartner <> '') and
           ("IC Direction" = "IC Direction"::Outgoing) and (ICTransactionNo > 0)
        then
          ICOutboxMgt.CreateOutboxJnlLine(ICTransactionNo,1,GenJnlLine5);
        if ("Recurring Method" >= "Recurring Method"::"RF Reversing Fixed") and ("Posting Date" <> 0D) then begin
          "Posting Date" := "Posting Date" + 1;
          "Document Date" := "Posting Date";
          MultiplyAmounts(GenJournalLine,-1);
          TempGenJnlLine4 := GenJournalLine;
          TempGenJnlLine4."Reversing Entry" := true;
          TempGenJnlLine4.INSERT;
          NoOfReversingRecords := NoOfReversingRecords + 1;
          "Posting Date" := "Posting Date" - 1;
          "Document Date" := "Posting Date";
        end;
        PostAllocations(GenJournalLine,false);
      end;
      exit(true);
    END;

    LOCAL PROCEDURE CheckLine@38(VAR GenJnlLine@1000 : Record "Gen. Journal Line";VAR PostingOutsideFiscalYearConfirmed@1002 : Boolean);
    BEGIN
      CheckRecurringLine(GenJnlLine);
      UpdateRecurringAmt(GenJnlLine);
      CheckAllocations(GenJnlLine);
      GenJnlLine5.COPY(GenJnlLine);
      if not PostingOutsideFiscalYearConfirmed then
        PostingOutsideFiscalYearConfirmed :=
          InstructionMgt.ConfirmPostingOutsideFiscalYear(
            ConfirmPostingOutsidePeriodQst,GenJnlLine5."Posting Date",InstructionMgt.PostingOutsideFiscalYearNotAllowedCode);
      PrepareGenJnlLineAddCurr(GenJnlLine5);
      GenJnlCheckLine.RunCheck(GenJnlLine5);
      CheckRestrictions(GenJnlLine5);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostGenJnlLine@41(VAR GenJournalLine@1000 : Record "Gen. Journal Line");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostGenJnlLine@40(VAR GenJournalLine@1000 : Record "Gen. Journal Line");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostAllocations@46(GenJournalLine@1000 : Record "Gen. Journal Line";Reversing@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterMultiplyAmounts@45(VAR GenJournalLine@1000 : Record "Gen. Journal Line";Factor@1001 : Decimal);
    BEGIN
    END;

    BEGIN
    END.
  }
}

