OBJECT Table 5305 Outlook Synch. User Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               if ISNULLGUID("Record GUID") then
                 "Record GUID" := CREATEGUID;
             END;

    OnDelete=BEGIN
               if not CheckSetupDetail(Rec) then
                 ERROR('');

               OSynchSetupDetail.RESET;
               OSynchSetupDetail.SETRANGE("User ID","User ID");
               OSynchSetupDetail.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchSetupDetail.DELETEALL;

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;
             END;

    OnRename=BEGIN
               if not CheckSetupDetail(xRec) then
                 ERROR('');

               if xRec."Synch. Entity Code" = "Synch. Entity Code" then
                 exit;

               Condition := '';
               "Synch. Direction" := "Synch. Direction"::Bidirectional;
               "Last Synch. Time" := 0DT;

               OSynchSetupDetail.RESET;
               OSynchSetupDetail.SETRANGE("User ID","User ID");
               OSynchSetupDetail.SETRANGE("Synch. Entity Code",xRec."Synch. Entity Code");
               OSynchSetupDetail.DELETEALL;

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;
             END;

    CaptionML=ENU=Outlook Synch. User Setup;
    PasteIsValid=false;
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=BEGIN
                                                                UserMgt.ValidateUserID("User ID");
                                                              END;

                                                   OnLookup=BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=User ID;
                                                   NotBlank=true }
    { 2   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                if "Synch. Entity Code" = xRec."Synch. Entity Code" then
                                                                  exit;

                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                OSynchEntity.TESTFIELD(Description);
                                                                OSynchEntity.TESTFIELD("Table No.");
                                                                OSynchEntity.TESTFIELD("Outlook Item");

                                                                CALCFIELDS(Description,"No. of Elements");
                                                              END;

                                                   CaptionML=ENU=Synch. Entity Code;
                                                   NotBlank=true }
    { 3   ;   ;Description         ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Outlook Synch. Entity".Description WHERE (Code=FIELD("Synch. Entity Code")));
                                                   CaptionML=ENU=Description;
                                                   Editable=false }
    { 4   ;   ;Condition           ;Text250       ;CaptionML=ENU=Condition;
                                                   Editable=false }
    { 5   ;   ;Synch. Direction    ;Option        ;OnValidate=VAR
                                                                OSynchDependency@1000 : Record "Outlook Synch. Dependency";
                                                                RecRef@1002 : RecordRef;
                                                                FldRef@1001 : FieldRef;
                                                              BEGIN
                                                                if "Synch. Direction" = xRec."Synch. Direction" then
                                                                  exit;

                                                                if "Synch. Direction" = "Synch. Direction"::Bidirectional then
                                                                  exit;

                                                                CALCFIELDS("No. of Elements");
                                                                if "No. of Elements" <> 0 then begin
                                                                  OSynchSetupDetail.RESET;
                                                                  OSynchSetupDetail.SETRANGE("User ID","User ID");
                                                                  OSynchSetupDetail.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                  if OSynchSetupDetail.FIND('-') then
                                                                    repeat
                                                                      OSynchEntityElement.GET(OSynchSetupDetail."Synch. Entity Code",OSynchSetupDetail."Element No.");
                                                                      MODIFY;
                                                                      OSynchEntityElement.CALCFIELDS("No. of Dependencies");
                                                                      if OSynchEntityElement."No. of Dependencies" > 0 then
                                                                        if not OSynchSetupMgt.CheckOCollectionAvailability(OSynchEntityElement,"User ID") then
                                                                          "Synch. Direction" := xRec."Synch. Direction";
                                                                    until OSynchSetupDetail.NEXT = 0;
                                                                end;

                                                                OSynchDependency.RESET;
                                                                OSynchDependency.SETRANGE("Depend. Synch. Entity Code","Synch. Entity Code");
                                                                if OSynchDependency.FIND('-') then
                                                                  repeat
                                                                    if OSynchUserSetup.GET("User ID",OSynchDependency."Synch. Entity Code") then
                                                                      if OSynchSetupDetail.GET(
                                                                           OSynchUserSetup."User ID",
                                                                           OSynchUserSetup."Synch. Entity Code",
                                                                           OSynchDependency."Element No.")
                                                                      then
                                                                        if "Synch. Direction" <> OSynchUserSetup."Synch. Direction" then begin
                                                                          CLEAR(RecRef);
                                                                          CLEAR(FldRef);
                                                                          RecRef.GETTABLE(Rec);
                                                                          FldRef := RecRef.FIELD(FIELDNO("Synch. Direction"));
                                                                          ERROR(
                                                                            Text001,
                                                                            FIELDCAPTION("Synch. Direction"),
                                                                            SELECTSTR(OSynchUserSetup."Synch. Direction"::Bidirectional + 1,FldRef.OPTIONSTRING),
                                                                            OSynchDependency."Synch. Entity Code");
                                                                        end;
                                                                  until OSynchDependency.NEXT = 0;
                                                              END;

                                                   CaptionML=ENU=Synch. Direction;
                                                   OptionCaptionML=ENU=Bidirectional,Microsoft Dynamics NAV to Outlook,Outlook to Microsoft Dynamics NAV;
                                                   OptionString=Bidirectional,"Microsoft Dynamics NAV to Outlook","Outlook to Microsoft Dynamics NAV" }
    { 6   ;   ;Last Synch. Time    ;DateTime      ;CaptionML=ENU=Last Synch. Time }
    { 7   ;   ;Record GUID         ;GUID          ;CaptionML=ENU=Record GUID;
                                                   Editable=false }
    { 8   ;   ;No. of Elements     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Outlook Synch. Setup Detail" WHERE ("User ID"=FIELD("User ID"),
                                                                                                          "Synch. Entity Code"=FIELD("Synch. Entity Code"),
                                                                                                          "Outlook Collection"=FILTER(<>'')));
                                                   CaptionML=ENU=No. of Elements;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"User ID","Synch. Entity Code"          ;Clustered=true }
    {    ;"Record GUID"                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntity@1004 : Record "Outlook Synch. Entity";
      OSynchEntityElement@1002 : Record "Outlook Synch. Entity Element";
      OSynchFilter@1000 : Record "Outlook Synch. Filter";
      OSynchUserSetup@1005 : Record "Outlook Synch. User Setup";
      OSynchSetupDetail@1003 : Record "Outlook Synch. Setup Detail";
      UserMgt@1001 : Codeunit "User Management";
      OSynchSetupMgt@1008 : Codeunit "Outlook Synch. Setup Mgt.";
      Text001@1007 : TextConst 'ENU=The value of the %1 field must either be %2 or match the synchronization direction of the %3 entity because these entities are dependent.';
      Text002@1009 : TextConst 'ENU=The %1 entity is used for the synchronization of one or more Outlook item collections.\If you delete this entity, all collections will be removed from synchronization. Do you want to proceed?';

    [External]
    PROCEDURE CheckSetupDetail@2(OSynchUserSetup1@1002 : Record "Outlook Synch. User Setup") : Boolean;
    VAR
      OSynchDependency@1003 : Record "Outlook Synch. Dependency";
    BEGIN
      OSynchSetupDetail.RESET;
      OSynchSetupDetail.SETRANGE("User ID",OSynchUserSetup1."User ID");
      if OSynchSetupDetail.FIND('-') then
        repeat
          if OSynchDependency.GET(
               OSynchSetupDetail."Synch. Entity Code",
               OSynchSetupDetail."Element No.",
               OSynchUserSetup1."Synch. Entity Code")
          then
            OSynchSetupDetail.MARK(true);
        until OSynchSetupDetail.NEXT = 0;

      OSynchSetupDetail.MARKEDONLY(true);
      if OSynchSetupDetail.COUNT > 0 then begin
        if CONFIRM(Text002,false,OSynchUserSetup1."Synch. Entity Code") then begin
          OSynchSetupDetail.DELETEALL;
          exit(true);
        end;
        exit(false);
      end;
      exit(true);
    END;

    BEGIN
    END.
  }
}

