OBJECT Codeunit 130440 Library - Random
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    SingleInstance=true;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Seed@1000 : Integer;

    [External]
    PROCEDURE RandDec@3(Range@1000 : Integer;Decimals@1001 : Integer) : Decimal;
    BEGIN
      exit(RandInt(Range * POWER(10,Decimals)) / POWER(10,Decimals));
    END;

    [External]
    PROCEDURE RandDecInRange@5(Min@1000 : Integer;Max@1002 : Integer;Decimals@1001 : Integer) : Decimal;
    BEGIN
      // Returns a pseudo random decimal in the interval (Min,Max]
      exit(Min + RandDec(Max - Min,Decimals));
    END;

    [External]
    PROCEDURE RandDecInDecimalRange@56(Min@1000 : Decimal;Max@1001 : Decimal;Precision@1002 : Integer) : Decimal;
    VAR
      Min2@1003 : Integer;
      Max2@1004 : Integer;
      Pow@1005 : Integer;
    BEGIN
      Pow := POWER(10,Precision);
      Min2 := ROUND(Min * Pow,1,'>');
      Max2 := ROUND(Max * Pow,1,'<');
      exit(RandIntInRange(Min2,Max2) / Pow);
    END;

    [External]
    PROCEDURE RandInt@1(Range@1000 : Integer) : Integer;
    BEGIN
      // Returns a pseudo random integer in the interval [1,Range]
      if Range < 1 then
        exit(1);
      exit(1 + ROUND(Uniform * (Range - 1),1));
    END;

    [External]
    PROCEDURE RandIntInRange@4(Min@1000 : Integer;Max@1001 : Integer) : Integer;
    BEGIN
      exit(Min - 1 + RandInt(Max - Min + 1));
    END;

    [External]
    PROCEDURE RandDate@16(Delta@1000 : Integer) : Date;
    BEGIN
      if Delta = 0 then
        exit(WORKDATE);
      exit(CALCDATE(STRSUBSTNO('<%1D>',Delta / ABS(Delta) * RandInt(ABS(Delta))),WORKDATE));
    END;

    [External]
    PROCEDURE RandDateFrom@11(FromDate@1000 : Date;Range@1001 : Integer) : Date;
    BEGIN
      if Range = 0 then
        exit(FromDate);
      exit(CALCDATE(STRSUBSTNO('<%1D>',Range / ABS(Range) * RandInt(Range)),FromDate));
    END;

    [External]
    PROCEDURE RandDateFromInRange@12(FromDate@1000 : Date;FromRange@1001 : Integer;ToRange@1002 : Integer) : Date;
    BEGIN
      if FromRange >= ToRange then
        exit(FromDate);
      exit(CALCDATE(STRSUBSTNO('<+%1D>',RandIntInRange(FromRange,ToRange)),FromDate));
    END;

    [External]
    PROCEDURE RandPrecision@6() : Decimal;
    BEGIN
      exit(1 / POWER(10,RandInt(5)));
    END;

    [External]
    PROCEDURE Init@28() : Integer;
    BEGIN
      // Updates the seed from the current time
      exit(SetSeed(TIME - 000000T));
    END;

    [External]
    PROCEDURE SetSeed@26(Val@1000 : Integer) : Integer;
    BEGIN
      // Set the random seed to reproduce pseudo random sequence
      Seed := Val;
      Seed := Seed mod 10000;  // Overflow protection
      exit(Seed);
    END;

    LOCAL PROCEDURE UpdateSeed@48();
    BEGIN
      // Generates a new seed value and
      Seed := Seed + 3;
      Seed := Seed * 3;
      Seed := Seed * Seed;
      Seed := Seed mod 10000;  // Overflow protection
    END;

    LOCAL PROCEDURE Uniform@2() : Decimal;
    BEGIN
      // Generates a pseudo random uniform number
      UpdateSeed;

      exit((Seed mod 137) / 137);
    END;

    BEGIN
    {
      Pseudo random number generator.
    }
    END.
  }
}

