OBJECT Codeunit 5945 CreateCreditfromContractLines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Service Contract Line";
    SingleInstance=true;
    OnRun=BEGIN
            ServContractHeader.GET("Contract Type","Contract No.");
            if not Credited and
               not "New Line"
            then begin
              if "Line Amount" > 0 then
                if ServContractHeader."Automatic Credit Memos" then
                  if "Credit Memo Date" > 0D then
                    CreditNoteNo := ServContractMgt.CreateContractLineCreditMemo(Rec,true);
              ServItemLine.RESET;
              ServItemLine.SETCURRENTKEY("Contract No.");
              ServItemLine.SETRANGE("Contract No.","Contract No.");
              ServItemLine.SETRANGE("Contract Line No.","Line No.");
              ServItemLineExist := ServItemLine.FINDFIRST;
            end;

            if LinesToDelete = 1 then begin
              LinesToDelete := 0;
              if CreditNoteNo <> '' then
                MESSAGE(Text000,CreditNoteNo);
              if ServItemLineExist then begin
                if LinesToDelete = 1 then begin
                  MESSAGE(Text002,LOWERCASE(TABLECAPTION));
                end else
                  MESSAGE(Text001);
              end;
              ServItemLineExist := false;
              CreditNoteNo := '';
            end;
          END;

  }
  CODE
  {
    VAR
      ServContractHeader@1000 : Record "Service Contract Header";
      ServItemLine@1008 : Record "Service Item Line";
      ServContractMgt@1002 : Codeunit ServContractManagement;
      CreditNoteNo@1001 : Code[20];
      Text000@1005 : TextConst 'ENU=Credit Memo %1 was created.';
      ServItemLineExist@1011 : Boolean;
      LinesToDelete@1006 : Integer;
      Text001@1009 : TextConst 'ENU=Some service contract lines are part of a service order/s.\You have to update this service order/s manually.';
      Text002@1010 : TextConst 'ENU=This %1 is part of a service order/s.\You have to update this service order/s manually.';

    [External]
    PROCEDURE SetSelectionFilterNo@1(LinesSelected@1000 : Integer);
    BEGIN
      LinesToDelete := LinesSelected;
    END;

    [External]
    PROCEDURE InitVariables@2();
    BEGIN
      ServItemLineExist := false;
      CreditNoteNo := '';
      LinesToDelete := 0;
    END;

    BEGIN
    END.
  }
}

