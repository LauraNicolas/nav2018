OBJECT Codeunit 5305 Outlook Synch. Process Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            case OSynchActionType of
              OSynchActionType::Insert:
                InsertItem;
              OSynchActionType::Modify:
                ProcessItem;
              OSynchActionType::Delete:
                OSynchDeletionMgt.ProcessItem(OSynchUserSetup,EntityRecID,ErrorLogXMLWriter,StartDateTime);
            end;
          END;

  }
  CODE
  {
    VAR
      OSynchEntity@1007 : Record "Outlook Synch. Entity";
      OSynchEntityElement@1009 : Record "Outlook Synch. Entity Element";
      OSynchUserSetup@1008 : Record "Outlook Synch. User Setup";
      ErrorConflictBuffer@1025 : TEMPORARY Record "Outlook Synch. Link";
      Field@1011 : Record Field;
      OSynchSetupMgt@1019 : Codeunit "Outlook Synch. Setup Mgt.";
      OSynchNAVMgt@1018 : Codeunit "Outlook Synch. NAV Mgt";
      OSynchTypeConversion@1017 : Codeunit "Outlook Synch. Type Conv";
      OSynchDeletionMgt@1016 : Codeunit "Outlook Synch. Deletion Mgt.";
      OSynchOutlookMgt@1005 : Codeunit "Outlook Synch. Outlook Mgt.";
      XMLTextReader@1001 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextReader";
      ErrorLogXMLWriter@1014 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextWriter";
      EntityRecID@1006 : RecordID;
      OSynchActionType@1002 : 'Insert,Modify,Delete,Undefined';
      StartDateTime@1037 : DateTime;
      Container@1026 : Text;
      OEntryIDHash@1012 : Text[32];
      RootIterator@1003 : Text[38];
      SkipCheckForConflicts@1004 : Boolean;
      Text001@1013 : TextConst 'ENU=An Outlook item cannot be synchronized because the %1 field of the %2 entity cannot be processed. Try again later and if the problem persists contact your system administrator.';
      Text002@1015 : TextConst 'ENU=An Outlook item cannot be synchronized because the %1 collection of the %2 entity cannot be found. Try again later and if the problem persists contact your system administrator.';
      Text003@1027 : TextConst 'ENU=An Outlook item cannot be synchronized because a conflict has occurred when processing the %1 entity.';
      Text004@1020 : TextConst 'ENU=An Outlook item cannot be synchronized because a conflict has occurred when processing the %1 collection in the %2 entity.';
      Text005@1022 : TextConst 'ENU=An Outlook item of the %1 entity cannot be synchronized because the %2 collection depends on an Outlook item that could not be found in the synchronization folders.';
      Text006@1023 : TextConst 'ENU=An Outlook item of the %1 entity cannot be synchronized because the %2 collection has a dependency that does not exist. Try again later and if the problem persists contact your system administrator.';
      Text007@1030 : TextConst 'ENU=An Outlook item of the %1 entity cannot be synchronized because the %2 collection depends on an Outlook item that has not been synchronized. Try again later and if the problem persists contact your system administrator.';
      Text008@1031 : TextConst 'ENU=An Outlook item of the %1 entity cannot be synchronized because an error occurred when processing the %2 collection. Try again later and if the problem persists contact your system administrator.';
      Text009@1021 : TextConst 'ENU=An Outlook item of the %1 entity cannot be synchronized because a conflict occurred that could not be logged. Please contact your system administrator to change your synchronization settings.';
      Text010@1010 : TextConst 'ENU=An Outlook item cannot be synchronized because the %1 field of the %2 collection in the %3 entity cannot be processed. Try again later and if the problem persists contact your system administrator.';

    [External]
    PROCEDURE SetGlobalParameters@5(VAR OSynchEntityIn@1006 : Record "Outlook Synch. Entity";VAR OSynchUserSetupIn@1002 : Record "Outlook Synch. User Setup";VAR ErrorConflictBufferIn@1009 : TEMPORARY Record "Outlook Synch. Link";VAR XMLTextReaderIn@1000 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextReader";VAR ErrorLogXMLWriterIn@1008 : DotNet "'Microsoft.Dynamics.Nav.OLSync.Common, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.NAV.OLSync.Common.XmlTextWriter";RootIteratorIn@1001 : Text[38];OSynchActionTypeIn@1005 : Integer;SearchRecID@1004 : Code[250];ContainerIn@1003 : Text;OEntryIDHashIn@1007 : Text[32];StartDateTimeIn@1010 : DateTime;SkipCheckForConflictsIn@1011 : Boolean);
    BEGIN
      OSynchEntity := OSynchEntityIn;
      OSynchUserSetup := OSynchUserSetupIn;

      if ErrorConflictBufferIn.FIND('-') then
        repeat
          ErrorConflictBuffer.INIT;
          ErrorConflictBuffer := ErrorConflictBufferIn;
          ErrorConflictBuffer.INSERT;
        until ErrorConflictBufferIn.NEXT = 0;

      XMLTextReader := XMLTextReaderIn;
      ErrorLogXMLWriter := ErrorLogXMLWriterIn;
      RootIterator := RootIteratorIn;
      OSynchActionType := OSynchActionTypeIn;
      CLEAR(EntityRecID);
      if SearchRecID <> '' then
        EVALUATE(EntityRecID,SearchRecID);

      Container := ContainerIn;
      OEntryIDHash := OEntryIDHashIn;
      StartDateTime := StartDateTimeIn;
      SkipCheckForConflicts := SkipCheckForConflictsIn;
    END;

    LOCAL PROCEDURE ProcessItem@2();
    VAR
      OSynchLink@1002 : Record "Outlook Synch. Link";
      OSynchField@1001 : Record "Outlook Synch. Field";
      EntityRecRef@1003 : RecordRef;
      ModifiedEntityRecRef@1006 : RecordRef;
      RecID@1000 : RecordID;
    BEGIN
      OSynchLink.GET(OSynchUserSetup."User ID",EntityRecID);
      if not EntityRecRef.GET(EntityRecID) then begin
        if SkipCheckForConflicts then begin
          OSynchLink.DELETE;
          InsertItem;
          exit;
        end;
        OSynchOutlookMgt.WriteErrorLog(
          OSynchUserSetup."User ID",
          EntityRecID,
          'Conflict',
          OSynchEntity.Code,
          STRSUBSTNO(Text003,OSynchEntity.Code),
          ErrorLogXMLWriter,
          Container);
        ERROR('');
      end;

      if not CheckEntityIdentity(EntityRecID,OSynchUserSetup."Synch. Entity Code") then
        exit;

      if not SkipCheckForConflicts then
        if ConflictDetected(EntityRecRef,OSynchUserSetup."Last Synch. Time") then begin
          OSynchOutlookMgt.WriteErrorLog(
            OSynchUserSetup."User ID",
            EntityRecID,
            'Conflict',
            OSynchEntity.Code,
            STRSUBSTNO(Text003,OSynchEntity.Code),
            ErrorLogXMLWriter,
            Container);
          ERROR('');
        end;

      ModifiedEntityRecRef := EntityRecRef.DUPLICATE;

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchEntity.Code);
      OSynchField.SETRANGE("Element No.",0);
      ProcessProperties(OSynchField,ModifiedEntityRecRef,RootIterator,0,false);

      ModifyRecRef(ModifiedEntityRecRef,EntityRecRef,0);
      ProcessCollections(ModifiedEntityRecRef);

      EVALUATE(RecID,FORMAT(EntityRecRef.RECORDID));
      UpdateSynchronizationDate(OSynchUserSetup."User ID",RecID);
    END;

    LOCAL PROCEDURE InsertItem@22();
    VAR
      OSynchLink@1003 : Record "Outlook Synch. Link";
      OSynchField@1000 : Record "Outlook Synch. Field";
      EntityRecRef@1012 : RecordRef;
      RecID@1002 : RecordID;
    BEGIN
      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchEntity.Code) ;
      OSynchField.SETRANGE("Element No.",0);

      EntityRecRef.OPEN(OSynchEntity."Table No.");
      EntityRecRef.LOCKTABLE;
      EntityRecRef.INIT;
      ProcessFields(OSynchField,EntityRecRef,RootIterator,0,false);
      EntityRecRef.INSERT(true);

      OSynchLink.InsertOSynchLink(OSynchUserSetup."User ID",Container,EntityRecRef,OEntryIDHash);
      ProcessCollections(EntityRecRef);

      EVALUATE(RecID,FORMAT(EntityRecRef.RECORDID));
      UpdateSynchronizationDate(OSynchUserSetup."User ID",RecID);

      EntityRecRef.CLOSE;
    END;

    LOCAL PROCEDURE ProcessConstants@30(VAR OSynchField@1002 : Record "Outlook Synch. Field";VAR RecRef@1000 : RecordRef);
    VAR
      KeyFieldsBuffer@1001 : TEMPORARY Record "Outlook Synch. Lookup Name";
      FieldRef@1003 : FieldRef;
    BEGIN
      KeyFieldsBuffer.RESET;
      KeyFieldsBuffer.DELETEALL;
      OSynchField.CLEARMARKS;

      if OSynchField.FIND('-') then
        repeat
          if CheckKeyField(OSynchField."Master Table No.",OSynchField."Field No.") then begin
            KeyFieldsBuffer.INIT;
            KeyFieldsBuffer."Entry No." := OSynchField."Field No.";
            KeyFieldsBuffer.Name := COPYSTR(OSynchField.DefaultValueExpression,1,MAXSTRLEN(KeyFieldsBuffer.Name));
            if KeyFieldsBuffer.INSERT then;
          end else
            OSynchField.MARK(true);
        until OSynchField.NEXT = 0;

      if OSynchField.FIND('-') then;
      KeyFieldsBuffer.RESET;
      if KeyFieldsBuffer.FIND('-') then
        repeat
          FieldRef := RecRef.FIELD(KeyFieldsBuffer."Entry No.");
          if not
             OSynchTypeConversion.EvaluateTextToFieldRef(
               OSynchTypeConversion.SetValueFormat(KeyFieldsBuffer.Name,FieldRef),
               FieldRef,
               true)
          then
            if OSynchField."Element No." = 0 then
              ERROR(Text001,FieldRef.CAPTION,OSynchField."Synch. Entity Code")
            else
              ERROR(Text010,FieldRef.CAPTION,OSynchField."Outlook Object",OSynchField."Synch. Entity Code");
        until KeyFieldsBuffer.NEXT = 0;

      OSynchField.MARKEDONLY(true);
      if OSynchField.FIND('-') then
        repeat
          FieldRef := RecRef.FIELD(OSynchField."Field No.");
          if not
             OSynchTypeConversion.EvaluateTextToFieldRef(
               OSynchTypeConversion.SetValueFormat(OSynchField.DefaultValueExpression,FieldRef),
               FieldRef,
               true)
          then
            if OSynchField."Element No." = 0 then
              ERROR(Text001,FieldRef.CAPTION,OSynchField."Synch. Entity Code")
            else
              ERROR(Text010,FieldRef.CAPTION,OSynchField."Outlook Object",OSynchField."Synch. Entity Code");
        until OSynchField.NEXT = 0;
    END;

    LOCAL PROCEDURE ProcessProperties@10(VAR OSynchField@1001 : Record "Outlook Synch. Field";VAR SynchRecRef@1009 : RecordRef;Iterator@1021 : Text[38];ElementNo@1020 : Integer;ProcessOnlySearchFields@1022 : Boolean);
    VAR
      OSynchFilter@1004 : Record "Outlook Synch. Filter";
      TempOSynchFilter@1017 : TEMPORARY Record "Outlook Synch. Filter";
      OSynchOptionCorrel@1018 : Record "Outlook Synch. Option Correl.";
      RelatedRecRef@1013 : RecordRef;
      NullRecRef@1012 : RecordRef;
      FldRef@1006 : FieldRef;
      RelatedFldRef@1016 : FieldRef;
      ChildIterator@1000 : Text[38];
      TagName@1002 : Text[250];
      OProperty@1003 : Text[80];
      OPropertyValue@1007 : Text[1024];
      OLOptionValue@1019 : Integer;
      ValidateFieldValue@1005 : Boolean;
    BEGIN
      if XMLTextReader.GetAllCurrentChildNodes(Iterator,ChildIterator) > 0 then begin
        ValidateFieldValue := not ProcessOnlySearchFields;
        if ProcessOnlySearchFields then
          OSynchField.SETRANGE("Search Field",true);
        OSynchField.SETFILTER("Read-Only Status",'<>%1',OSynchField."Read-Only Status"::"Read-Only in Microsoft Dynamics NAV");
        repeat
          TagName := XMLTextReader.GetName(ChildIterator);
          if TagName = 'Field' then begin
            OProperty := XMLTextReader.GetCurrentNodeAttribute(ChildIterator,'Name');
            OSynchField.SETRANGE("Outlook Property",OProperty);
            if OSynchField.FIND('-') then
              repeat
                if OSynchNAVMgt.CheckSynchFieldCondition(SynchRecRef,OSynchField) then begin
                  OPropertyValue := COPYSTR(XMLTextReader.GetValue(ChildIterator),1,MAXSTRLEN(OPropertyValue));
                  if OSynchField."Table No." <> 0 then begin
                    TempOSynchFilter.RESET;
                    TempOSynchFilter.DELETEALL;

                    OSynchFilter.RESET;
                    OSynchFilter.SETRANGE("Record GUID",OSynchField."Record GUID");
                    OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
                    OSynchFilter.SETFILTER(Type,'<>%1',OSynchFilter.Type::FIELD);
                    OSynchSetupMgt.CopyFilterRecords(OSynchFilter,TempOSynchFilter);

                    OSynchSetupMgt.CreateFilterCondition(
                      TempOSynchFilter,
                      OSynchField."Table No.",
                      OSynchField."Field No.",
                      TempOSynchFilter.Type::FILTER,
                      OPropertyValue);

                    RelatedRecRef.OPEN(OSynchField."Table No.");
                    RelatedRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(TempOSynchFilter,NullRecRef));
                    OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
                    if RelatedRecRef.FIND('-') then begin
                      if OSynchFilter.FIND('-') then
                        repeat
                          FldRef := SynchRecRef.FIELD(OSynchFilter."Master Table Field No.");
                          RelatedFldRef := RelatedRecRef.FIELD(OSynchFilter."Field No.");
                          if (FORMAT(RelatedFldRef.TYPE) = 'DATE') or (FORMAT(RelatedFldRef.TYPE) = 'TIME') then
                            ProcessDateTimeProperty(SynchRecRef,OSynchField,OPropertyValue)
                          else begin
                            if not
                               OSynchTypeConversion.EvaluateTextToFieldRef(
                                 OSynchTypeConversion.SetValueFormat(FORMAT(RelatedFldRef),RelatedFldRef),
                                 FldRef,
                                 ValidateFieldValue)
                            then
                              if ElementNo = 0 then
                                ERROR(Text001,FldRef.CAPTION,OSynchEntity.Code)
                              else
                                ERROR(Text010,FldRef.CAPTION,OSynchField."Outlook Object",OSynchEntity.Code);
                          end;
                        until OSynchFilter.NEXT = 0;
                    end else
                      if OPropertyValue = '' then begin
                        if OSynchFilter.FIND('-') then
                          repeat
                            FldRef := SynchRecRef.FIELD(OSynchFilter."Master Table Field No.");
                            if not OSynchTypeConversion.EvaluateTextToFieldRef('',FldRef,ValidateFieldValue) then
                              if ElementNo = 0 then
                                ERROR(Text001,FldRef.CAPTION,OSynchEntity.Code)
                              else
                                ERROR(Text010,FldRef.CAPTION,OSynchField."Outlook Object",OSynchEntity.Code);
                          until OSynchFilter.NEXT = 0;
                      end else
                        if OSynchFilter.FINDFIRST then begin
                          FldRef := SynchRecRef.FIELD(OSynchFilter."Master Table Field No.");
                          RelatedFldRef := RelatedRecRef.FIELD(OSynchFilter."Field No.");
                          if ElementNo = 0 then
                            ERROR(Text001,FldRef.CAPTION,OSynchField."Synch. Entity Code");

                          ERROR(Text010,FldRef.CAPTION,OSynchField."Outlook Object",OSynchField."Synch. Entity Code");
                        end;
                    RelatedRecRef.CLOSE;
                  end else begin
                    FldRef := SynchRecRef.FIELD(OSynchField."Field No.");
                    // EVALUATE(Field.Type,FORMAT(FldRef.TYPE));

                    // CASE Field.Type OF
                    case FORMAT(FldRef.TYPE) of
                      'Date','Time':
                        ProcessDateTimeProperty(SynchRecRef,OSynchField,OPropertyValue);
                      'BLOB':
                        ;
                      'Option':
                        if EVALUATE(OLOptionValue,OPropertyValue) then begin
                          OSynchOptionCorrel.RESET;
                          OSynchOptionCorrel.SETRANGE("Synch. Entity Code",OSynchField."Synch. Entity Code");
                          OSynchOptionCorrel.SETRANGE("Element No.",OSynchField."Element No.");
                          OSynchOptionCorrel.SETRANGE("Field Line No.",OSynchField."Line No.");
                          OSynchOptionCorrel.SETRANGE("Enumeration No.",OLOptionValue);
                          if OSynchOptionCorrel.FINDFIRST then begin
                            if not
                               OSynchTypeConversion.EvaluateTextToFieldRef(
                                 FORMAT(OSynchOptionCorrel."Option No."),
                                 FldRef,
                                 ValidateFieldValue)
                            then
                              if ElementNo = 0 then
                                ERROR(Text001,FldRef.CAPTION,OSynchEntity.Code)
                              else
                                ERROR(Text010,FldRef.CAPTION,OSynchField."Outlook Object",OSynchEntity.Code);
                          end else begin
                            if not OSynchSetupMgt.CheckOEnumeration(OSynchField) then begin
                              if not OSynchTypeConversion.EvaluateTextToFieldRef(OPropertyValue,FldRef,ValidateFieldValue) then
                                if ElementNo = 0 then
                                  ERROR(Text001,FldRef.CAPTION,OSynchEntity.Code)
                                else
                                  ERROR(Text010,FldRef.CAPTION,OSynchField."Outlook Object",OSynchEntity.Code);
                            end else
                              if ElementNo = 0 then
                                ERROR(Text001,FldRef.CAPTION,OSynchField."Synch. Entity Code")
                              else
                                ERROR(Text010,FldRef.CAPTION,OSynchField."Outlook Object",OSynchField."Synch. Entity Code");
                          end;
                        end else begin
                          if not OSynchTypeConversion.EvaluateTextToFieldRef(OPropertyValue,FldRef,ValidateFieldValue) then
                            if ElementNo = 0 then
                              ERROR(Text001,FldRef.CAPTION,OSynchField."Synch. Entity Code")
                            else
                              ERROR(Text010,FldRef.CAPTION,OSynchField."Outlook Object",OSynchField."Synch. Entity Code");
                        end;
                      else begin
                        if not OSynchTypeConversion.EvaluateTextToFieldRef(OPropertyValue,FldRef,ValidateFieldValue) then
                          if ElementNo = 0 then
                            ERROR(Text001,FldRef.CAPTION,OSynchField."Synch. Entity Code")
                          else
                            ERROR(Text010,FldRef.CAPTION,OSynchField."Outlook Object",OSynchField."Synch. Entity Code");
                      end;
                    end;
                  end;
                end;
              until OSynchField.NEXT = 0;
          end;
        until not XMLTextReader.MoveNext(ChildIterator);
      end;
      XMLTextReader.RemoveIterator(ChildIterator);
    END;

    LOCAL PROCEDURE ProcessRelationsAndConditions@26(VAR OSynchEntityElementIn@1002 : Record "Outlook Synch. Entity Element";EntityRecRef@1006 : RecordRef;VAR CollectionRecRef@1000 : RecordRef;DependentRecRef@1008 : RecordRef);
    VAR
      OSynchEntity1@1011 : Record "Outlook Synch. Entity";
      OSynchFilter@1004 : Record "Outlook Synch. Filter";
      TempOSynchFilter@1005 : TEMPORARY Record "Outlook Synch. Filter";
      KeyFieldsBuffer@1001 : TEMPORARY Record "Outlook Synch. Lookup Name";
      OSynchDependency@1010 : Record "Outlook Synch. Dependency";
      TempDependentRecRef@1009 : RecordRef;
      NullRecRef@1012 : RecordRef;
      FieldRef@1003 : FieldRef;
      RecID@1007 : RecordID;
    BEGIN
      TempOSynchFilter.RESET;
      TempOSynchFilter.DELETEALL;

      OSynchFilter.RESET;
      OSynchFilter.SETRANGE("Record GUID",OSynchEntityElementIn."Record GUID");
      OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
      OSynchSetupMgt.ComposeFilterRecords(OSynchFilter,TempOSynchFilter,EntityRecRef,TempOSynchFilter.Type::CONST);

      if OSynchEntityElementIn."No. of Dependencies" > 0 then begin
        EVALUATE(RecID,FORMAT(DependentRecRef.RECORDID));
        TempDependentRecRef.OPEN(RecID.TABLENO,true);
        OSynchNAVMgt.CopyRecordReference(DependentRecRef,TempDependentRecRef,false);

        OSynchDependency.RESET;
        OSynchDependency.SETRANGE("Synch. Entity Code",OSynchEntityElementIn."Synch. Entity Code");
        OSynchDependency.SETRANGE("Element No.",OSynchEntityElementIn."Element No.");
        OSynchDependency.CALCFIELDS("Depend. Synch. Entity Tab. No.");
        OSynchDependency.SETRANGE("Depend. Synch. Entity Tab. No.",RecID.TABLENO);
        if OSynchDependency.FIND('-') then
          repeat
            OSynchEntity1.GET(OSynchDependency."Depend. Synch. Entity Code");
            OSynchFilter.RESET;
            OSynchFilter.SETRANGE("Record GUID",OSynchEntity1."Record GUID");
            if OSynchFilter.FINDFIRST then
              TempDependentRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(OSynchFilter,NullRecRef));

            if TempDependentRecRef.FIND('-') then begin
              OSynchFilter.RESET;
              OSynchFilter.SETRANGE("Record GUID",OSynchDependency."Record GUID");
              OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::Condition);
              OSynchFilter.SETRANGE(Type,OSynchFilter.Type::CONST);
              OSynchSetupMgt.CopyFilterRecords(OSynchFilter,TempOSynchFilter);

              OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
              OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
              OSynchSetupMgt.ComposeFilterRecords(OSynchFilter,TempOSynchFilter,TempDependentRecRef,TempOSynchFilter.Type::CONST);
            end;
          until OSynchDependency.NEXT = 0;
        TempDependentRecRef.CLOSE;
      end;

      KeyFieldsBuffer.RESET;
      KeyFieldsBuffer.DELETEALL;
      TempOSynchFilter.CLEARMARKS;

      if TempOSynchFilter.FIND('-') then
        repeat
          if CheckKeyField(TempOSynchFilter."Table No.",TempOSynchFilter."Field No.") then begin
            KeyFieldsBuffer.INIT;
            KeyFieldsBuffer."Entry No." := TempOSynchFilter."Field No.";
            KeyFieldsBuffer.Name := COPYSTR(TempOSynchFilter.GetFilterExpressionValue,1,MAXSTRLEN(KeyFieldsBuffer.Name));
            if KeyFieldsBuffer.INSERT then;
          end else
            TempOSynchFilter.MARK(true);
        until TempOSynchFilter.NEXT = 0;

      KeyFieldsBuffer.RESET;
      if KeyFieldsBuffer.FIND('-') then
        repeat
          FieldRef := CollectionRecRef.FIELD(KeyFieldsBuffer."Entry No.");
          if not
             OSynchTypeConversion.EvaluateTextToFieldRef(
               OSynchTypeConversion.SetValueFormat(KeyFieldsBuffer.Name,FieldRef),
               FieldRef,
               true)
          then
            if OSynchEntityElementIn."Element No." = 0 then
              ERROR(Text001,FieldRef.CAPTION,OSynchEntityElementIn."Synch. Entity Code")
            else
              ERROR(
                Text010,
                FieldRef.CAPTION,
                OSynchEntityElementIn."Outlook Collection",
                OSynchEntityElementIn."Synch. Entity Code");
        until KeyFieldsBuffer.NEXT = 0;

      TempOSynchFilter.MARKEDONLY(true);
      TempOSynchFilter.SETCURRENTKEY("Table No.","Field No."); // Defines validation order

      if TempOSynchFilter.FIND('-') then
        repeat
          FieldRef := CollectionRecRef.FIELD(TempOSynchFilter."Field No.");
          if not
             OSynchTypeConversion.EvaluateTextToFieldRef(
               OSynchTypeConversion.SetValueFormat(TempOSynchFilter.GetFilterExpressionValue,FieldRef),
               FieldRef,
               true)
          then
            if OSynchEntityElementIn."Element No." = 0 then
              ERROR(Text001,FieldRef.CAPTION,OSynchEntityElementIn."Synch. Entity Code")
            else
              ERROR(
                Text010,
                FieldRef.CAPTION,
                OSynchEntityElementIn."Outlook Collection",
                OSynchEntityElementIn."Synch. Entity Code");
        until TempOSynchFilter.NEXT = 0;
    END;

    LOCAL PROCEDURE ProcessFields@7(VAR OSynchField@1005 : Record "Outlook Synch. Field";VAR SynchRecRef@1003 : RecordRef;Iterator@1002 : Text[38];ElementNo@1001 : Integer;ProcessOnlySearchFields@1000 : Boolean);
    VAR
      OSynchFieldBuffer@1004 : TEMPORARY Record "Outlook Synch. Field";
      IsConst@1006 : Boolean;
    BEGIN
      if not OSynchField.FIND('-') then
        exit;

      repeat
        if OSynchField."Outlook Property" = '' then begin
          if not IsConst then begin
            ProcessProperties(OSynchFieldBuffer,SynchRecRef,Iterator,ElementNo,ProcessOnlySearchFields);
            OSynchFieldBuffer.RESET;
            OSynchFieldBuffer.DELETEALL;
          end;

          if (OSynchField."Table No." = 0) and
             (OSynchField."Read-Only Status" = OSynchField."Read-Only Status"::"Read-Only in Microsoft Dynamics NAV") and
             (OSynchField.Condition = '')
          then begin
            OSynchFieldBuffer.INIT;
            OSynchFieldBuffer.COPY(OSynchField);
            OSynchFieldBuffer.INSERT;
          end;

          IsConst := true;
        end else begin
          if IsConst then begin
            ProcessConstants(OSynchFieldBuffer,SynchRecRef);
            OSynchFieldBuffer.RESET;
            OSynchFieldBuffer.DELETEALL;
          end;

          OSynchFieldBuffer.INIT;
          OSynchFieldBuffer.COPY(OSynchField);
          OSynchFieldBuffer.INSERT;

          IsConst := false;
        end;
      until OSynchField.NEXT = 0;

      if IsConst then
        ProcessConstants(OSynchFieldBuffer,SynchRecRef)
      else
        ProcessProperties(OSynchFieldBuffer,SynchRecRef,Iterator,ElementNo,ProcessOnlySearchFields);
    END;

    LOCAL PROCEDURE ProcessDateTimeProperty@45(VAR SynchRecRef@1003 : RecordRef;OSynchFieldIn@1001 : Record "Outlook Synch. Field";OPropertyValue@1007 : Text[1024]);
    VAR
      OSynchField@1000 : Record "Outlook Synch. Field";
      OSynchFilter@1009 : Record "Outlook Synch. Filter";
      TempOSynchFilter@1008 : TEMPORARY Record "Outlook Synch. Filter";
      Field1@1002 : Record Field;
      RelatedRecRef@1010 : RecordRef;
      NullRecRef@1006 : RecordRef;
      FldRef@1005 : FieldRef;
      RelatedFldRef@1004 : FieldRef;
      DateTimeVar@1011 : DateTime;
    BEGIN
      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchFieldIn."Synch. Entity Code");
      OSynchField.SETRANGE("Element No.",OSynchFieldIn."Element No.");
      OSynchField.SETRANGE("Outlook Property",OSynchFieldIn."Outlook Property");
      if OSynchField.FIND('-') then
        repeat
          if OSynchField."Table No." = 0 then
            Field1.GET(OSynchField."Master Table No.",OSynchField."Field No.")
          else
            Field1.GET(OSynchField."Table No.",OSynchField."Field No.");

          if (Field1.Type = Field1.Type::Time) or (Field1.Type = Field1.Type::Date) then begin
            if DateTimeVar = 0DT then
              OSynchTypeConversion.TextToDateTime(OPropertyValue,DateTimeVar);
            if OSynchNAVMgt.CheckSynchFieldCondition(SynchRecRef,OSynchField) then
              if OSynchField."Table No." = 0 then begin
                FldRef := SynchRecRef.FIELD(OSynchField."Field No.");
                if Field1.Type = Field1.Type::Time then
                  FldRef.VALIDATE(DT2TIME(DateTimeVar))
                else
                  FldRef.VALIDATE(DT2DATE(DateTimeVar));
              end else begin
                TempOSynchFilter.RESET;
                TempOSynchFilter.DELETEALL;

                OSynchFilter.RESET;
                OSynchFilter.SETRANGE("Record GUID",OSynchField."Record GUID");
                OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
                OSynchFilter.SETFILTER(Type,'<>%1',OSynchFilter.Type::FIELD);
                OSynchSetupMgt.CopyFilterRecords(OSynchFilter,TempOSynchFilter);

                OSynchSetupMgt.CreateFilterCondition(
                  TempOSynchFilter,
                  OSynchField."Table No.",
                  OSynchField."Field No.",
                  TempOSynchFilter.Type::FILTER,
                  OSynchTypeConversion.SetDateTimeFormat(DateTimeVar));

                RelatedRecRef.OPEN(OSynchField."Table No.");
                RelatedRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(TempOSynchFilter,NullRecRef));
                if RelatedRecRef.FIND('-') then begin
                  OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
                  if OSynchFilter.FIND('-') then
                    repeat
                      FldRef := SynchRecRef.FIELD(OSynchFilter."Master Table Field No.");
                      RelatedFldRef := RelatedRecRef.FIELD(OSynchFilter."Field No.");
                      if not OSynchTypeConversion.EvaluateTextToFieldRef(FORMAT(RelatedFldRef),FldRef,true) then
                        if OSynchField."Element No." = 0 then
                          ERROR(Text001,FldRef.CAPTION,OSynchField."Synch. Entity Code")
                        else
                          ERROR(Text010,FldRef.CAPTION,OSynchField."Outlook Object",OSynchField."Synch. Entity Code");
                    until OSynchFilter.NEXT = 0;
                end;
                RelatedRecRef.CLOSE;
              end;
          end;
        until OSynchField.NEXT = 0;
    END;

    LOCAL PROCEDURE ProcessCollections@6(EntityRecRef@1000 : RecordRef);
    VAR
      CollectionElementsBuffer@1005 : TEMPORARY Record "Outlook Synch. Link";
      CollectionsBuffer@1007 : RecordRef;
      ChildIterator@1001 : Text[38];
      ElementIterator@1003 : Text[38];
      ChildTagName@1002 : Text[250];
      OCollectionName@1004 : Text[80];
      CollectionTableID@1006 : Integer;
      CollectionElementNo@1008 : Integer;
      IsBufferUsed@1009 : Boolean;
    BEGIN
      if XMLTextReader.GetAllCurrentChildNodes(RootIterator,ChildIterator) = 0 then begin
        XMLTextReader.RemoveIterator(ChildIterator);
        exit;
      end;

      CollectionTableID := 0;
      OSynchEntityElement.RESET;
      OSynchEntityElement.SETRANGE("Synch. Entity Code",OSynchEntity.Code);
      repeat
        ChildTagName := XMLTextReader.GetName(ChildIterator);
        if ChildTagName = 'Collection' then begin
          CollectionElementsBuffer.RESET;
          CollectionElementsBuffer.DELETEALL;

          OCollectionName := XMLTextReader.GetCurrentNodeAttribute(ChildIterator,'Name');
          OSynchEntityElement.SETRANGE("Outlook Collection",OCollectionName);
          if OSynchEntityElement.FINDFIRST then begin
            if XMLTextReader.GetAllCurrentChildNodes(ChildIterator,ElementIterator) > 0 then
              repeat
                ChildTagName := XMLTextReader.GetName(ElementIterator);
                if ChildTagName = 'Element' then
                  CheckCollectionElement(OSynchEntityElement,ElementIterator);
              until not XMLTextReader.MoveNext(ElementIterator);
            XMLTextReader.RemoveIterator(ElementIterator);
          end else
            ERROR(Text002,OCollectionName,OSynchEntityElement."Synch. Entity Code");

          if not SkipCheckForConflicts then
            if CollectionConflictDetected(OSynchEntityElement,EntityRecRef,OSynchUserSetup."Last Synch. Time") then begin
              OSynchOutlookMgt.WriteErrorLog(
                OSynchUserSetup."User ID",
                EntityRecRef.RECORDID,
                'Conflict',
                OSynchEntityElement."Synch. Entity Code",
                STRSUBSTNO(
                  Text004,
                  OSynchEntityElement."Outlook Collection",
                  OSynchEntityElement."Synch. Entity Code"),
                ErrorLogXMLWriter,
                Container);
              ERROR('');
            end;

          if XMLTextReader.GetAllCurrentChildNodes(ChildIterator,ElementIterator) > 0 then begin
            if CollectionTableID <> OSynchEntityElement."Table No." then begin
              CollectionElementNo := 0;
              if CollectionTableID = 0 then
                CollectionsBuffer.OPEN(OSynchEntityElement."Table No.",true);

              if CollectionsBuffer.FIND('-') then
                ProcessCollectiosBuffer(CollectionElementsBuffer,CollectionsBuffer,CollectionTableID);

              CollectionTableID := OSynchEntityElement."Table No.";
              CollectionsBuffer.CLOSE;
              CollectionsBuffer.OPEN(CollectionTableID,true);
              IsBufferUsed := true;
            end;

            repeat
              ChildTagName := XMLTextReader.GetName(ElementIterator);
              if ChildTagName = 'Element' then begin
                CollectionElementNo := CollectionElementNo + 1;
                ProcessCollectionElement(
                  OSynchEntityElement,
                  ElementIterator,
                  EntityRecRef,
                  CollectionElementsBuffer,
                  CollectionsBuffer,
                  CollectionElementNo);
              end;
            until not XMLTextReader.MoveNext(ElementIterator);
          end;

          XMLTextReader.RemoveIterator(ElementIterator);
          DeletedCollectionElements(CollectionElementsBuffer,OSynchEntityElement,EntityRecRef);
        end;
      until not XMLTextReader.MoveNext(ChildIterator);
      XMLTextReader.RemoveIterator(ChildIterator);

      if IsBufferUsed then
        if CollectionsBuffer.FIND('-') then
          ProcessCollectiosBuffer(CollectionElementsBuffer,CollectionsBuffer,CollectionTableID);
    END;

    [Internal]
    PROCEDURE ProcessCollectiosBuffer@18(VAR CollectionElementsBuffer@1005 : Record "Outlook Synch. Link";CollectionsBuffer@1000 : RecordRef;TableID@1001 : Integer);
    VAR
      CollectionRecRef@1002 : RecordRef;
      RecID@1004 : RecordID;
    BEGIN
      CollectionRecRef.OPEN(TableID);

      repeat
        OSynchNAVMgt.CopyRecordReference(CollectionsBuffer,CollectionRecRef,true);

        EVALUATE(RecID,FORMAT(CollectionRecRef.RECORDID));

        CollectionElementsBuffer.INIT;
        CollectionElementsBuffer."User ID" := OSynchUserSetup."User ID";
        CollectionElementsBuffer."Record ID" := RecID;
        if CollectionElementsBuffer.INSERT then;
      until CollectionsBuffer.NEXT = 0;

      CollectionRecRef.CLOSE;
    END;

    LOCAL PROCEDURE ProcessCollectionElement@20(OSynchEntityElementIn@1015 : Record "Outlook Synch. Entity Element";ElementIterator@1000 : Text[38];EntityRecRef@1001 : RecordRef;VAR CollectionElementsBuffer@1013 : Record "Outlook Synch. Link";VAR CollectionsBuffer@1022 : RecordRef;CollectionElementNo@1023 : Integer);
    VAR
      OSynchEntity1@1014 : Record "Outlook Synch. Entity";
      OSynchLink@1005 : Record "Outlook Synch. Link";
      OSynchFilter@1008 : Record "Outlook Synch. Filter";
      OSynchFilter1@1011 : Record "Outlook Synch. Filter";
      OSynchField@1019 : Record "Outlook Synch. Field";
      OSynchDependency@1004 : Record "Outlook Synch. Dependency";
      DependentRecRef@1006 : RecordRef;
      TempDependentRecRef@1018 : RecordRef;
      CollectionRecRef@1007 : RecordRef;
      TempCollectionRecRef@1009 : RecordRef;
      OriginalCollectionRecRef@1012 : RecordRef;
      NullRecRef@1010 : RecordRef;
      RecID@1016 : RecordID;
      ContainerLocal@1021 : Text;
      EntryIDHash@1002 : Text[32];
      ChildrenIterator@1003 : Text[38];
      IsFound@1017 : Boolean;
      IsOneToOneEntity@1020 : Boolean;
    BEGIN
      if XMLTextReader.GetAllCurrentChildNodes(ElementIterator,ChildrenIterator) = 0 then
        exit;

      OSynchEntityElementIn.CALCFIELDS("Table Caption","Master Table No.","No. of Dependencies");
      if OSynchEntityElementIn."No. of Dependencies" = 0 then begin
        OSynchField.RESET;
        OSynchField.SETRANGE("Synch. Entity Code",OSynchEntityElementIn."Synch. Entity Code");
        OSynchField.SETRANGE("Element No.",OSynchEntityElementIn."Element No.");

        TempCollectionRecRef.OPEN(OSynchEntityElementIn."Table No.",true);
        TempCollectionRecRef.INIT;
        ProcessProperties(OSynchField,TempCollectionRecRef,ElementIterator,OSynchEntityElementIn."Element No.",true);
        TempCollectionRecRef.INSERT;

        FindEntityElementBySearchField(
          OSynchEntityElementIn."Synch. Entity Code",
          OSynchEntityElementIn."Element No.",
          EntityRecRef,
          TempCollectionRecRef,
          RecID);
        TempCollectionRecRef.CLOSE;

        if FORMAT(RecID) = '' then begin
          if OSynchEntityElementIn."Master Table No." = OSynchEntityElementIn."Table No." then
            ModifyCollectionElement(EntityRecRef.RECORDID,OSynchEntityElementIn."Element No.",ElementIterator,CollectionElementsBuffer)
          else
            PutCollectionElementToBuffer(
              EntityRecRef,
              NullRecRef,
              OSynchEntityElementIn,
              ElementIterator,
              CollectionsBuffer,
              CollectionElementNo);
        end else
          ModifyCollectionElement(RecID,OSynchEntityElementIn."Element No.",ElementIterator,CollectionElementsBuffer);
      end else begin
        CLEAR(ContainerLocal);
        IsOneToOneEntity := false;

        if XMLTextReader.GetName(ChildrenIterator) = 'EntryID' then begin
          ContainerLocal := OSynchOutlookMgt.ConvertValueFromBase64(XMLTextReader.GetValue(ChildrenIterator));
          EntryIDHash := OSynchOutlookMgt.ComputeHash(ContainerLocal);
        end;

        OSynchLink.RESET;
        OSynchLink.SETRANGE("User ID",OSynchUserSetup."User ID");
        OSynchLink.SETRANGE("Outlook Entry ID Hash",EntryIDHash);
        OSynchLink.FINDFIRST;

        DependentRecRef.GET(OSynchLink."Record ID");
        EVALUATE(RecID,FORMAT(DependentRecRef.RECORDID));

        OSynchFilter.RESET;
        OSynchFilter.SETRANGE("Record GUID",OSynchEntityElementIn."Record GUID");
        OSynchFilter.FINDFIRST;

        CollectionRecRef.OPEN(OSynchEntityElementIn."Table No.");
        CollectionRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(OSynchFilter,EntityRecRef));
        if not CollectionRecRef.FIND('-') then
          if OSynchEntityElementIn."Master Table No." <> OSynchEntityElementIn."Table No." then begin
            PutCollectionElementToBuffer(
              EntityRecRef,
              DependentRecRef,
              OSynchEntityElementIn,
              ElementIterator,
              CollectionsBuffer,
              CollectionElementNo);

            exit;
          end else begin
            CollectionRecRef := EntityRecRef.DUPLICATE;
            IsOneToOneEntity := true;
          end;

        TempDependentRecRef.OPEN(RecID.TABLENO,true);
        OSynchNAVMgt.CopyRecordReference(DependentRecRef,TempDependentRecRef,false);

        OSynchDependency.RESET;
        OSynchDependency.SETRANGE("Synch. Entity Code",OSynchEntityElementIn."Synch. Entity Code");
        OSynchDependency.SETRANGE("Element No.",OSynchEntityElementIn."Element No.");
        OSynchDependency.CALCFIELDS("Depend. Synch. Entity Tab. No.");
        OSynchDependency.SETRANGE("Depend. Synch. Entity Tab. No.",RecID.TABLENO);
        if OSynchDependency.FIND('-') then
          repeat
            OSynchEntity1.GET(OSynchDependency."Depend. Synch. Entity Code");
            OSynchFilter.RESET;
            OSynchFilter.SETRANGE("Record GUID",OSynchEntity1."Record GUID");
            if OSynchFilter.FINDFIRST then
              TempDependentRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(OSynchFilter,NullRecRef));

            if TempDependentRecRef.FIND('-') then
              IsFound := true;
          until (OSynchDependency.NEXT = 0) or IsFound;

        OSynchFilter.RESET;
        OSynchFilter.SETRANGE("Record GUID",OSynchDependency."Record GUID");
        OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::Condition);

        OSynchFilter1.RESET;
        OSynchFilter1.SETRANGE("Record GUID",OSynchDependency."Record GUID");
        OSynchFilter1.SETRANGE("Filter Type",OSynchFilter1."Filter Type"::"Table Relation");
        OSynchFilter1.SETRANGE(Type,OSynchFilter1.Type::FIELD);

        if not IsOneToOneEntity then begin
          TempCollectionRecRef.OPEN(OSynchEntityElementIn."Table No.",true);
          repeat
            OSynchNAVMgt.CopyRecordReference(CollectionRecRef,TempCollectionRecRef,false);
          until CollectionRecRef.NEXT = 0;

          TempCollectionRecRef.SETVIEW(OSynchSetupMgt.ComposeTableView(OSynchFilter,OSynchFilter1,DependentRecRef));
          if TempCollectionRecRef.FIND('-') then begin
            EVALUATE(RecID,FORMAT(TempCollectionRecRef.RECORDID));
            ModifyCollectionElement(RecID,OSynchEntityElementIn."Element No.",ElementIterator,CollectionElementsBuffer);
            exit;
          end;
          TempCollectionRecRef.CLOSE;
        end;

        if OSynchEntityElementIn."Table No." = OSynchEntityElementIn."Master Table No." then begin
          EVALUATE(RecID,FORMAT(CollectionRecRef.RECORDID));
          OSynchFilter.RESET;
          OSynchFilter.SETRANGE("Record GUID",OSynchEntityElementIn."Record GUID");
          OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
          if not OneToOneRelation(OSynchFilter) then
            ERROR(
              Text008,
              OSynchEntityElementIn."Synch. Entity Code",
              OSynchEntityElementIn."Outlook Collection");

          OriginalCollectionRecRef := CollectionRecRef.DUPLICATE;
          if not SetRelation(CollectionRecRef,DependentRecRef,OSynchDependency) then
            ERROR(
              Text008,
              OSynchDependency."Synch. Entity Code",
              OSynchEntityElementIn."Outlook Collection");

          OSynchField.RESET;
          OSynchField.SETRANGE("Synch. Entity Code",OSynchEntityElementIn."Synch. Entity Code");
          OSynchField.SETRANGE("Element No.",OSynchEntityElementIn."Element No.");
          ProcessProperties(
            OSynchField,
            CollectionRecRef,
            ElementIterator,
            OSynchEntityElementIn."Element No.",
            false);

          ModifyRecRef(CollectionRecRef,OriginalCollectionRecRef,OSynchEntityElementIn."Element No.");
          CollectionElementsBuffer.INIT;
          CollectionElementsBuffer."User ID" := OSynchUserSetup."User ID";
          CollectionElementsBuffer."Record ID" := RecID;
          if CollectionElementsBuffer.INSERT then;
          exit;
        end;

        PutCollectionElementToBuffer(
          EntityRecRef,
          DependentRecRef,
          OSynchEntityElementIn,
          ElementIterator,
          CollectionsBuffer,
          CollectionElementNo);

        DependentRecRef.CLOSE;
        TempDependentRecRef.CLOSE;
        CollectionRecRef.CLOSE;
      end;
    END;

    LOCAL PROCEDURE DeletedCollectionElements@76(VAR CollectionElementsBuffer@1000 : Record "Outlook Synch. Link";OSynchEntityElementIn@1002 : Record "Outlook Synch. Entity Element";EntityRecRef@1001 : RecordRef);
    VAR
      OSynchFilter@1003 : Record "Outlook Synch. Filter";
      DeletedCollectionElementsBuf@1005 : TEMPORARY Record "Outlook Synch. Link";
      CollectionRecRef@1004 : RecordRef;
      RecID@1006 : RecordID;
    BEGIN
      OSynchFilter.RESET;
      OSynchFilter.SETRANGE("Record GUID",OSynchEntityElementIn."Record GUID");
      CollectionRecRef.OPEN(OSynchEntityElementIn."Table No.");
      CollectionRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(OSynchFilter,EntityRecRef));

      if not CollectionElementsBuffer.FIND('-') then begin
        if not CollectionRecRef.FIND('-') then
          exit;
      end;

      DeletedCollectionElementsBuf.RESET;
      DeletedCollectionElementsBuf.DELETEALL;

      repeat
        EVALUATE(RecID,FORMAT(CollectionRecRef.RECORDID));
        if not CollectionElementsBuffer.GET(OSynchUserSetup."User ID",RecID) then begin
          DeletedCollectionElementsBuf.INIT;
          DeletedCollectionElementsBuf."User ID" := OSynchUserSetup."User ID";
          DeletedCollectionElementsBuf."Record ID" := RecID;
          DeletedCollectionElementsBuf.INSERT;
        end;
      until CollectionRecRef.NEXT = 0;

      EVALUATE(RecID,FORMAT(EntityRecRef.RECORDID));
      OSynchDeletionMgt.ProcessCollectionElements(DeletedCollectionElementsBuf,OSynchEntityElementIn,RecID);
      CollectionRecRef.CLOSE;
    END;

    LOCAL PROCEDURE ModifyCollectionElement@3(CollectionElementRecID@1002 : RecordID;ElementNo@1005 : Integer;ElementIterator@1000 : Text[38];VAR CollectionElementsBuffer@1004 : Record "Outlook Synch. Link");
    VAR
      OSynchField@1006 : Record "Outlook Synch. Field";
      CollectionRecRef@1003 : RecordRef;
      ModifiedCollectionRecRef@1001 : RecordRef;
    BEGIN
      CollectionRecRef.GET(CollectionElementRecID);

      ModifiedCollectionRecRef := CollectionRecRef.DUPLICATE;

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchEntity.Code);
      OSynchField.SETRANGE("Element No.",ElementNo);
      ProcessProperties(OSynchField,ModifiedCollectionRecRef,ElementIterator,ElementNo,false);

      ModifyRecRef(ModifiedCollectionRecRef,CollectionRecRef,ElementNo);

      CollectionRecRef.CLOSE;

      CollectionElementsBuffer.INIT;
      CollectionElementsBuffer."User ID" := OSynchUserSetup."User ID";
      CollectionElementsBuffer."Record ID" := CollectionElementRecID;
      if CollectionElementsBuffer.INSERT then;
    END;

    LOCAL PROCEDURE PutCollectionElementToBuffer@28(EntityRecRef@1002 : RecordRef;DependentRecRef@1001 : RecordRef;OSynchEntityElementIn@1003 : Record "Outlook Synch. Entity Element";ElementIterator@1010 : Text[38];VAR CollectionElementRecRef@1020 : RecordRef;CollectionElementNo@1000 : Integer);
    VAR
      OSynchFilter@1004 : Record "Outlook Synch. Filter";
      OSynchField@1008 : Record "Outlook Synch. Field";
      CollectionElementRecRef1@1018 : RecordRef;
      FldRef@1005 : FieldRef;
      KeyRef@1015 : KeyRef;
      IntVar@1016 : Integer;
      BigIntVar@1014 : BigInteger;
      DecimaVar@1017 : Decimal;
    BEGIN
      OSynchEntityElementIn.CALCFIELDS("Master Table No.");
      if OSynchEntityElementIn."Master Table No." = OSynchEntityElementIn."Table No." then
        ERROR(Text008,OSynchEntityElementIn."Synch. Entity Code",OSynchEntityElementIn."Outlook Collection");

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchEntityElementIn."Synch. Entity Code");
      OSynchField.SETRANGE("Element No.",OSynchEntityElementIn."Element No.");

      CollectionElementRecRef.INIT;
      ProcessRelationsAndConditions(
        OSynchEntityElementIn,
        EntityRecRef,
        CollectionElementRecRef,
        DependentRecRef);
      ProcessFields(
        OSynchField,
        CollectionElementRecRef,
        ElementIterator,
        OSynchEntityElementIn."Element No.",
        false);

      OSynchFilter.RESET;
      OSynchFilter.SETRANGE("Record GUID",OSynchEntityElementIn."Record GUID");
      OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);

      CollectionElementRecRef1.OPEN(OSynchEntityElementIn."Table No.");
      CollectionElementRecRef1.SETVIEW(OSynchSetupMgt.ComposeTableFilter(OSynchFilter,EntityRecRef));
      if CollectionElementRecRef1.FIND('+') then begin
        KeyRef := CollectionElementRecRef1.KEYINDEX(1);
        FldRef := KeyRef.FIELDINDEX(KeyRef.FIELDCOUNT);
        EVALUATE(Field.Type,FORMAT(FldRef.TYPE));

        if Field.Type in [Field.Type::Integer,Field.Type::BigInteger,Field.Type::Decimal] then
          case Field.Type of
            Field.Type::Integer:
              begin
                EVALUATE(IntVar,FORMAT(CollectionElementRecRef1.FIELDINDEX(KeyRef.FIELDCOUNT).VALUE));
                IntVar := IntVar + CollectionElementNo * 10000;
              end;
            Field.Type::BigInteger:
              begin
                EVALUATE(BigIntVar,FORMAT(CollectionElementRecRef1.FIELDINDEX(KeyRef.FIELDCOUNT).VALUE));
                BigIntVar := BigIntVar + CollectionElementNo * 10000;
              end;
            Field.Type::Decimal:
              begin
                EVALUATE(DecimaVar,FORMAT(CollectionElementRecRef1.FIELDINDEX(KeyRef.FIELDCOUNT).VALUE));
                DecimaVar := DecimaVar + CollectionElementNo * 10000;
              end;
            Field.Type::GUID:
              FldRef.VALUE := CREATEGUID;
          end;
      end else begin
        IntVar := CollectionElementNo * 10000;
        BigIntVar := CollectionElementNo * 10000;
        DecimaVar := CollectionElementNo * 10000;
      end;

      KeyRef := CollectionElementRecRef.KEYINDEX(1);
      FldRef := KeyRef.FIELDINDEX(KeyRef.FIELDCOUNT);
      EVALUATE(Field.Type,FORMAT(FldRef.TYPE));

      if Field.Type in [Field.Type::Integer,Field.Type::BigInteger,Field.Type::Decimal,Field.Type::GUID] then
        case Field.Type of
          Field.Type::Integer:
            FldRef.VALIDATE(IntVar);
          Field.Type::BigInteger:
            FldRef.VALIDATE(BigIntVar);
          Field.Type::Decimal:
            FldRef.VALIDATE(DecimaVar);
          Field.Type::GUID:
            FldRef.VALUE := CREATEGUID;
        end;

      CollectionElementRecRef.INSERT;
    END;

    LOCAL PROCEDURE SetRelation@19(CollectionElementRecRef@1008 : RecordRef;DependentRecRef@1000 : RecordRef;OSynchDependency@1002 : Record "Outlook Synch. Dependency") : Boolean;
    VAR
      OSynchFilter@1003 : Record "Outlook Synch. Filter";
      SourceField@1006 : Record Field;
      TargetField@1007 : Record Field;
      SourceFieldRef@1004 : FieldRef;
      TargetFieldRef@1005 : FieldRef;
    BEGIN
      OSynchFilter.RESET;
      OSynchFilter.SETRANGE("Record GUID",OSynchDependency."Record GUID");
      OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
      OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
      if OSynchFilter.FIND('-') then
        repeat
          if (CollectionElementRecRef.NUMBER <> OSynchFilter."Master Table No.") or
             (DependentRecRef.NUMBER <> OSynchFilter."Table No.")
          then
            exit(false);

          SourceFieldRef := DependentRecRef.FIELD(OSynchFilter."Field No.");
          TargetFieldRef := CollectionElementRecRef.FIELD(OSynchFilter."Master Table Field No.");
          EVALUATE(SourceField.Type,FORMAT(SourceFieldRef.TYPE));
          EVALUATE(TargetField.Type,FORMAT(TargetFieldRef.TYPE));

          if SourceField.Type <> TargetField.Type then
            exit(false);

          TargetFieldRef.VALIDATE(SourceFieldRef.VALUE);
        until OSynchFilter.NEXT = 0;

      exit(true);
    END;

    LOCAL PROCEDURE ModifyRecRef@16(VAR RecRef@1000 : RecordRef;xRecRef@1001 : RecordRef;ElementNo@1006 : Integer);
    VAR
      OSynchEntityElement1@1012 : Record "Outlook Synch. Entity Element";
      FldRef@1002 : FieldRef;
      xFldRef@1003 : FieldRef;
      ArrayPKFldRef@1010 : ARRAY [3] OF FieldRef;
      xArrayPKFldRef@1011 : ARRAY [3] OF FieldRef;
      PKeyRef@1009 : KeyRef;
      i@1004 : Integer;
      IsChanged@1008 : Boolean;
    BEGIN
      for i := 1 to RecRef.FIELDCOUNT do begin
        FldRef := RecRef.FIELDINDEX(i);
        xFldRef := xRecRef.FIELDINDEX(i);
        // EVALUATE(Field.Class,FORMAT(FldRef.CLASS));
        // IF Field.Class = Field.Class::Normal THEN
        if FORMAT(FldRef.CLASS) = 'Normal' then
          if FORMAT(FldRef.VALUE) <> FORMAT(xFldRef.VALUE) then
            IsChanged := true;
      end;

      if IsChanged then begin
        PKeyRef := RecRef.KEYINDEX(1);
        for i := 1 to PKeyRef.FIELDCOUNT do begin
          ArrayPKFldRef[i] := PKeyRef.FIELDINDEX(i);
          xArrayPKFldRef[i] := xRecRef.KEYINDEX(1).FIELDINDEX(i);
          if FORMAT(ArrayPKFldRef[i].VALUE) <> FORMAT(xArrayPKFldRef[i].VALUE) then
            if ElementNo = 0 then
              ERROR(Text001,ArrayPKFldRef[i].CAPTION,OSynchEntity.Code)
            else begin
              OSynchEntityElement1.GET(OSynchEntity.Code,ElementNo);
              ERROR(Text010,ArrayPKFldRef[i].CAPTION,OSynchEntityElement1."Outlook Collection",OSynchEntity.Code);
            end;
        end;

        RecRef.MODIFY(true);
      end;
    END;

    [External]
    PROCEDURE UpdateSynchronizationDate@1(UserID@1002 : Code[50];RecID@1000 : RecordID);
    VAR
      OSynchLink@1001 : Record "Outlook Synch. Link";
    BEGIN
      if not OSynchLink.GET(UserID,RecID) then
        exit;

      OSynchLink."Synchronization Date" := CURRENTDATETIME;
      OSynchLink.MODIFY;
    END;

    LOCAL PROCEDURE CheckCollectionElement@59(OSynchEntityElementIn@1015 : Record "Outlook Synch. Entity Element";CollectionIterator@1000 : Text[38]);
    VAR
      OSynchEntity1@1009 : Record "Outlook Synch. Entity";
      OSynchLink@1005 : Record "Outlook Synch. Link";
      OSynchFilter@1010 : Record "Outlook Synch. Filter";
      OSynchDependency@1008 : Record "Outlook Synch. Dependency";
      DependentRecRef@1006 : RecordRef;
      TempDependentRecRef@1012 : RecordRef;
      NullRecRef@1011 : RecordRef;
      RecID@1007 : RecordID;
      ContainerLocal@1014 : Text;
      DependentEntityIDHash@1013 : Text[32];
      ElementChildIterator@1002 : Text[38];
      IsEmptyEntryID@1004 : Boolean;
      DependencyFound@1001 : Boolean;
    BEGIN
      if XMLTextReader.GetAllCurrentChildNodes(CollectionIterator,ElementChildIterator) <= 0 then begin
        XMLTextReader.RemoveIterator(ElementChildIterator);
        exit;
      end;

      CLEAR(ContainerLocal);
      IsEmptyEntryID := true;

      if  XMLTextReader.GetName(ElementChildIterator) = 'EntryID' then begin
        ContainerLocal := OSynchOutlookMgt.ConvertValueFromBase64(XMLTextReader.GetValue(ElementChildIterator));
        IsEmptyEntryID := ContainerLocal = '';
      end;

      OSynchEntityElementIn.CALCFIELDS("No. of Dependencies");

      if IsEmptyEntryID and (OSynchEntityElementIn."No. of Dependencies" > 0) then
        ERROR(
          Text005,
          OSynchEntityElementIn."Synch. Entity Code",
          OSynchEntityElementIn."Outlook Collection");

      if not IsEmptyEntryID and (OSynchEntityElementIn."No. of Dependencies" = 0) then
        ERROR(Text006,OSynchEntityElementIn."Synch. Entity Code",OSynchEntityElementIn."Outlook Collection");

      if not IsEmptyEntryID then begin
        ContainerLocal := OSynchOutlookMgt.ConvertValueFromBase64(XMLTextReader.GetValue(ElementChildIterator));
        DependentEntityIDHash := OSynchOutlookMgt.ComputeHash(ContainerLocal);
        if DependentEntityIDHash = '' then
          ERROR(Text007,OSynchEntityElementIn."Synch. Entity Code",OSynchEntityElementIn."Outlook Collection");

        ErrorConflictBuffer.RESET;
        ErrorConflictBuffer.SETRANGE("Outlook Entry ID Hash",DependentEntityIDHash);
        if ErrorConflictBuffer.FINDFIRST then
          ERROR(
            Text008,
            OSynchEntityElementIn."Synch. Entity Code",
            OSynchEntityElementIn."Outlook Collection");

        OSynchLink.RESET;
        OSynchLink.SETRANGE("User ID",OSynchUserSetup."User ID");
        OSynchLink.SETRANGE("Outlook Entry ID Hash",DependentEntityIDHash);
        if not OSynchLink.FINDFIRST then
          ERROR(
            Text008,
            OSynchEntityElementIn."Synch. Entity Code",
            OSynchEntityElementIn."Outlook Collection");

        EVALUATE(RecID,FORMAT(OSynchLink."Record ID"));
        DependentRecRef.GET(RecID);
        TempDependentRecRef.OPEN(RecID.TABLENO,true);
        OSynchNAVMgt.CopyRecordReference(DependentRecRef,TempDependentRecRef,false);

        OSynchDependency.RESET;
        OSynchDependency.SETRANGE("Synch. Entity Code",OSynchEntityElementIn."Synch. Entity Code");
        OSynchDependency.SETRANGE("Element No.",OSynchEntityElementIn."Element No.");
        OSynchDependency.CALCFIELDS("Depend. Synch. Entity Tab. No.");
        OSynchDependency.SETRANGE("Depend. Synch. Entity Tab. No.",RecID.TABLENO);
        if not OSynchDependency.FIND('-') then
          ERROR(Text006,OSynchEntityElementIn."Synch. Entity Code",OSynchEntityElementIn."Outlook Collection");

        DependencyFound := false;
        repeat
          OSynchEntity1.GET(OSynchDependency."Depend. Synch. Entity Code");
          OSynchFilter.RESET;
          OSynchFilter.SETRANGE("Record GUID",OSynchEntity1."Record GUID");
          if OSynchFilter.FINDFIRST then
            TempDependentRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(OSynchFilter,NullRecRef));

          if TempDependentRecRef.FIND('-') then
            DependencyFound := true;
        until OSynchDependency.NEXT = 0;

        if not DependencyFound then
          ERROR(Text006,OSynchEntityElementIn."Synch. Entity Code",OSynchEntityElementIn."Outlook Collection");
      end;
      XMLTextReader.RemoveIterator(ElementChildIterator);
    END;

    [Internal]
    PROCEDURE CheckEntityIdentity@12(RecID@1000 : RecordID;SynchEntityCode@1001 : Code[10]) : Boolean;
    VAR
      OSynchEntity1@1003 : Record "Outlook Synch. Entity";
      OSynchFilter@1006 : Record "Outlook Synch. Filter";
      EntityRecRef@1002 : RecordRef;
      TempEntityRecRef@1004 : RecordRef;
      NullRecRef@1005 : RecordRef;
    BEGIN
      OSynchEntity1.GET(SynchEntityCode);
      OSynchFilter.RESET;
      OSynchFilter.SETRANGE("Record GUID",OSynchEntity1."Record GUID");
      OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::Condition);

      EntityRecRef.GET(RecID);
      TempEntityRecRef.OPEN(RecID.TABLENO,true);
      OSynchNAVMgt.CopyRecordReference(EntityRecRef,TempEntityRecRef,false);
      TempEntityRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(OSynchFilter,NullRecRef));
      if not TempEntityRecRef.FIND('-') then
        exit(false);

      exit(true);
    END;

    [Internal]
    PROCEDURE CheckUserSettingsForConflicts@13(VAR SynchRecRef@1004 : RecordRef;TableID@1003 : Integer);
    VAR
      OSynchFilter@1001 : Record "Outlook Synch. Filter";
      TempRecRef@1002 : RecordRef;
      NullRecRef@1000 : RecordRef;
    BEGIN
      if OSynchUserSetup."Synch. Direction" = OSynchUserSetup."Synch. Direction"::"Outlook to Microsoft Dynamics NAV" then
        ERROR(Text009,OSynchUserSetup."Synch. Entity Code");

      OSynchFilter.RESET;
      OSynchFilter.SETFILTER(
        "Record GUID",
        '%1|%2',
        OSynchEntity."Record GUID",
        OSynchUserSetup."Record GUID");

      TempRecRef.OPEN(TableID,true);
      OSynchNAVMgt.CopyRecordReference(SynchRecRef,TempRecRef,false);
      TempRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(OSynchFilter,NullRecRef));
      if not TempRecRef.FIND('-') then
        ERROR(Text009,OSynchUserSetup."Synch. Entity Code");
      TempRecRef.CLOSE;
    END;

    [Internal]
    PROCEDURE CheckKeyField@14(TableID@1000 : Integer;FieldID@1001 : Integer) : Boolean;
    VAR
      CheckRecordRef@1002 : RecordRef;
      CheckKeyRef@1004 : KeyRef;
      CheckFieldRef@1003 : FieldRef;
      Counter@1005 : Integer;
    BEGIN
      CheckRecordRef.OPEN(TableID,true);
      CheckKeyRef := CheckRecordRef.KEYINDEX(1);

      for Counter := 1 to CheckKeyRef.FIELDCOUNT do begin
        CheckFieldRef := CheckKeyRef.FIELDINDEX(Counter);
        if CheckFieldRef.NUMBER = FieldID then
          exit(true);
      end;

      CheckRecordRef.CLOSE;
    END;

    LOCAL PROCEDURE ConflictDetected@21(SynchRecRef@1009 : RecordRef;LastSynchTime@1002 : DateTime) IsConflict : Boolean;
    VAR
      ChangeLogEntry@1000 : Record "Change Log Entry";
      OSynchLink@1005 : Record "Outlook Synch. Link";
      RecID@1004 : RecordID;
    BEGIN
      EVALUATE(RecID,FORMAT(SynchRecRef.RECORDID));

      if not OSynchLink.GET(OSynchUserSetup."User ID",RecID) then
        exit;

      ChangeLogEntry.SETCURRENTKEY("Table No.","Primary Key Field 1 Value");
      FilterChangeLog(RecID,ChangeLogEntry);
      ChangeLogEntry.SETRANGE("Type of Change",ChangeLogEntry."Type of Change"::Modification);

      if OSynchLink."Synchronization Date" >= LastSynchTime then begin
        ChangeLogEntry.SETFILTER("Date and Time",'>=%1',LastSynchTime);
        if not ChangeLogEntry.FINDLAST then
          exit;

        if ChangeLogEntry."Date and Time" <= OSynchLink."Synchronization Date" then
          exit;

        ChangeLogEntry.SETFILTER("Date and Time",'>=%1',OSynchLink."Synchronization Date");
      end else
        ChangeLogEntry.SETFILTER("Date and Time",'>=%1',LastSynchTime);

      if not ChangeLogEntry.FINDFIRST then
        exit;

      IsConflict := IsConflictDetected(ChangeLogEntry,SynchRecRef,RecID.TABLENO);
    END;

    [Internal]
    PROCEDURE IsConflictDetected@4(VAR ChangeLogEntry@1000 : Record "Change Log Entry";SynchRecRef@1009 : RecordRef;TableNo@1003 : Integer) IsConflict : Boolean;
    VAR
      OutlookSynchField@1001 : Record "Outlook Synch. Field";
    BEGIN
      OutlookSynchField.RESET;
      OutlookSynchField.SETRANGE("Synch. Entity Code",OSynchEntity.Code);
      OutlookSynchField.SETRANGE("Element No.",0);
      OutlookSynchField.SETFILTER("Read-Only Status",'<>%1',
        OutlookSynchField."Read-Only Status"::"Read-Only in Microsoft Dynamics NAV");
      IsConflict := SetupFieldsModified(ChangeLogEntry,OutlookSynchField);

      if IsConflict then
        CheckUserSettingsForConflicts(SynchRecRef,TableNo);
    END;

    LOCAL PROCEDURE CollectionConflictDetected@65(OSynchEntityElementIn@1000 : Record "Outlook Synch. Entity Element";EntityRecRef@1001 : RecordRef;LastSynchTime@1005 : DateTime) IsConflict : Boolean;
    VAR
      ChangeLogEntry@1004 : Record "Change Log Entry";
      TempChangeLogEntry@1006 : TEMPORARY Record "Change Log Entry";
      OSynchLink@1011 : Record "Outlook Synch. Link";
      OSynchFilter@1003 : Record "Outlook Synch. Filter";
      OSynchField@1007 : Record "Outlook Synch. Field";
      CollectionRecRef@1002 : RecordRef;
      CollectionFieldRef@1009 : FieldRef;
      RecID@1012 : RecordID;
      FilteringExpression@1008 : Text;
    BEGIN
      EVALUATE(RecID,FORMAT(EntityRecRef.RECORDID));
      if not OSynchLink.GET(OSynchUserSetup."User ID",RecID) then
        exit;

      OSynchFilter.RESET;
      OSynchFilter.SETRANGE("Record GUID",OSynchEntityElementIn."Record GUID");
      FilteringExpression := OSynchSetupMgt.ComposeTableFilter(OSynchFilter,EntityRecRef);

      ChangeLogEntry.SETCURRENTKEY("Table No.","Date and Time");
      ChangeLogEntry.SETRANGE("Table No.",OSynchEntityElementIn."Table No.");

      if OSynchLink."Synchronization Date" >= LastSynchTime then begin
        ChangeLogEntry.SETFILTER("Date and Time",'>=%1',LastSynchTime);
        if not ChangeLogEntry.FINDLAST then
          exit;
        if ChangeLogEntry."Date and Time" <= OSynchLink."Synchronization Date" then
          exit;
        ChangeLogEntry.SETFILTER("Date and Time",'>=%1',OSynchLink."Synchronization Date");
      end else
        ChangeLogEntry.SETFILTER("Date and Time",'>=%1&<=%2',LastSynchTime,StartDateTime);

      ChangeLogEntry.SETFILTER("Type of Change",'<>%1',ChangeLogEntry."Type of Change"::Deletion);
      if not ChangeLogEntry.ISEMPTY then begin
        ChangeLogEntry.SETCURRENTKEY("Table No.","Primary Key Field 1 Value");
        CollectionRecRef.OPEN(OSynchEntityElementIn."Table No.");
        CollectionRecRef.SETVIEW(FilteringExpression);
        if CollectionRecRef.FIND('-') then
          repeat
            EVALUATE(RecID,FORMAT(CollectionRecRef.RECORDID));
            FilterChangeLog(RecID,ChangeLogEntry);
            if ChangeLogEntry.FINDFIRST then begin
              OSynchField.RESET;
              OSynchField.SETRANGE("Synch. Entity Code",OSynchEntityElementIn."Synch. Entity Code");
              OSynchField.SETRANGE("Element No.",OSynchEntityElementIn."Element No.");
              OSynchField.SETFILTER("Read-Only Status",'<>%1',OSynchField."Read-Only Status"::"Read-Only in Microsoft Dynamics NAV");

              IsConflict := SetupFieldsModified(ChangeLogEntry,OSynchField);
              if IsConflict then begin
                EVALUATE(RecID,FORMAT(EntityRecRef.RECORDID));
                CheckUserSettingsForConflicts(EntityRecRef,RecID.TABLENO);
                exit;
              end;
            end;
          until CollectionRecRef.NEXT = 0;
        CollectionRecRef.CLOSE;
      end;

      ChangeLogEntry.SETRANGE("Primary Key Field 1 Value");
      ChangeLogEntry.SETRANGE("Primary Key Field 1 No.");
      ChangeLogEntry.SETRANGE("Primary Key Field 2 No.");
      ChangeLogEntry.SETRANGE("Primary Key Field 2 Value");
      ChangeLogEntry.SETRANGE("Primary Key Field 3 No.");
      ChangeLogEntry.SETRANGE("Primary Key Field 3 Value");

      ChangeLogEntry.SETRANGE("Type of Change",ChangeLogEntry."Type of Change"::Deletion);
      ChangeLogEntry.SETCURRENTKEY("Table No.","Date and Time");
      if ChangeLogEntry.ISEMPTY then
        exit;

      TempChangeLogEntry.RESET;
      TempChangeLogEntry.DELETEALL;

      OSynchNAVMgt.RemoveChangeLogDuplicates(ChangeLogEntry,TempChangeLogEntry);
      ChangeLogEntry.SETCURRENTKEY("Table No.","Primary Key Field 1 Value");
      if TempChangeLogEntry.FIND('-') then
        repeat
          CollectionRecRef.OPEN(OSynchEntityElementIn."Table No.",true);
          CollectionRecRef.INIT;
          ChangeLogEntry.SETRANGE("Primary Key",TempChangeLogEntry."Primary Key");
          ChangeLogEntry.SETRANGE("Primary Key Field 1 Value",TempChangeLogEntry."Primary Key Field 1 Value");
          if ChangeLogEntry.FINDSET then
            repeat
              OSynchFilter.SETRANGE("Field No.",ChangeLogEntry."Field No.");
              if OSynchFilter.FINDFIRST then begin
                CollectionFieldRef := CollectionRecRef.FIELD(ChangeLogEntry."Field No.");
                if not
                   OSynchTypeConversion.EvaluateTextToFieldRef(
                     OSynchTypeConversion.SetValueFormat(ChangeLogEntry."Old Value",CollectionFieldRef),
                     CollectionFieldRef,
                     false)
                then
                  ERROR(
                    Text010,
                    CollectionFieldRef.CAPTION,
                    OSynchEntityElementIn."Outlook Collection",
                    OSynchEntityElementIn."Synch. Entity Code");
              end;
            until ChangeLogEntry.NEXT = 0;
          CollectionRecRef.INSERT;

          CollectionRecRef.SETVIEW(FilteringExpression);
          IsConflict := CollectionRecRef.FIND('-');
          if IsConflict then begin
            EVALUATE(RecID,FORMAT(EntityRecRef.RECORDID));
            CheckUserSettingsForConflicts(EntityRecRef,RecID.TABLENO);
            exit;
          end;
          CollectionRecRef.CLOSE;
        until TempChangeLogEntry.NEXT = 0;
    END;

    [Internal]
    PROCEDURE FindEntityElementBySearchField@11(SynchEntityCode@1000 : Code[10];ElementNo@1004 : Integer;EntityRecRef@1013 : RecordRef;TemplateEntityElementRecRef@1001 : RecordRef;VAR RecID@1006 : RecordID);
    VAR
      OSynchEntityElement1@1014 : Record "Outlook Synch. Entity Element";
      OSynchFilter@1009 : Record "Outlook Synch. Filter";
      TempOSynchFilter@1005 : TEMPORARY Record "Outlook Synch. Filter";
      TempOSynchFilter1@1011 : TEMPORARY Record "Outlook Synch. Filter";
      OSynchField@1002 : Record "Outlook Synch. Field";
      CollectionElementRecRef@1003 : RecordRef;
      TempCollectionElementRecRef@1015 : RecordRef;
      RelatedRecRef@1008 : RecordRef;
      NullRecRef@1010 : RecordRef;
      FieldRef@1007 : FieldRef;
      RelatedFieldRef@1012 : FieldRef;
    BEGIN
      CLEAR(RecID);
      OSynchEntityElement1.GET(SynchEntityCode,ElementNo);
      TempCollectionElementRecRef.OPEN(OSynchEntityElement1."Table No.",true);

      OSynchFilter.RESET;
      OSynchFilter.SETRANGE("Record GUID",OSynchEntityElement1."Record GUID");
      if OSynchFilter.FINDFIRST then begin
        CollectionElementRecRef.OPEN(OSynchEntityElement1."Table No.");
        CollectionElementRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(OSynchFilter,EntityRecRef));
        if CollectionElementRecRef.FIND('-') then
          repeat
            OSynchNAVMgt.CopyRecordReference(CollectionElementRecRef,TempCollectionElementRecRef,false);
          until CollectionElementRecRef.NEXT = 0;
        CollectionElementRecRef.CLOSE;
      end;

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",SynchEntityCode);
      OSynchField.SETRANGE("Element No.",ElementNo);
      OSynchField.SETRANGE("Search Field",true);
      if OSynchField.FIND('-') then
        repeat
          if OSynchField."Table No." = 0 then begin
            FieldRef := TemplateEntityElementRecRef.FIELD(OSynchField."Field No.");
            OSynchSetupMgt.CreateFilterCondition(
              TempOSynchFilter,
              OSynchField."Master Table No.",
              OSynchField."Field No.",
              TempOSynchFilter.Type::FILTER,
              FORMAT(FieldRef.VALUE));
          end else begin
            TempOSynchFilter1.RESET;
            TempOSynchFilter1.DELETEALL;

            OSynchFilter.RESET;
            OSynchFilter.SETRANGE("Record GUID",OSynchField."Record GUID");
            OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
            OSynchFilter.SETRANGE(Type,OSynchFilter.Type::CONST);
            OSynchSetupMgt.CopyFilterRecords(OSynchFilter,TempOSynchFilter1);

            RelatedFieldRef := TemplateEntityElementRecRef.FIELD(OSynchField."Field No.");
            OSynchSetupMgt.CreateFilterCondition(
              TempOSynchFilter1,
              OSynchField."Table No.",
              OSynchField."Field No.",
              TempOSynchFilter1.Type::FILTER,
              FORMAT(RelatedFieldRef.VALUE));

            RelatedRecRef.OPEN(OSynchField."Table No.");
            RelatedRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(TempOSynchFilter1,NullRecRef));
            if RelatedRecRef.FIND('-') then begin
              OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
              if OSynchFilter.FIND('-') then
                repeat
                  FieldRef := TemplateEntityElementRecRef.FIELD(OSynchFilter."Master Table Field No.");
                  RelatedFieldRef := RelatedRecRef.FIELD(OSynchFilter."Field No.");

                  TempOSynchFilter.RESET;
                  TempOSynchFilter.SETRANGE("Field No.",OSynchFilter."Master Table Field No.");
                  if not TempOSynchFilter.FIND('-') then
                    OSynchSetupMgt.CreateFilterCondition(
                      TempOSynchFilter,
                      OSynchFilter."Master Table No.",
                      OSynchFilter."Master Table Field No.",
                      TempOSynchFilter.Type::FILTER,
                      FORMAT(RelatedFieldRef.VALUE));
                until OSynchFilter.NEXT = 0;
            end;
            RelatedRecRef.CLOSE;
          end;
        until OSynchField.NEXT = 0;

      TempOSynchFilter.RESET;
      if TempOSynchFilter.FIND('-') then begin
        TempCollectionElementRecRef.SETVIEW(OSynchSetupMgt.ComposeTableFilter(TempOSynchFilter,NullRecRef));

        if TempCollectionElementRecRef.FIND('-') then
          EVALUATE(RecID,FORMAT(TempCollectionElementRecRef.RECORDID));
      end;
    END;

    [External]
    PROCEDURE FilterChangeLog@98(RecID@1000 : RecordID;VAR ChangeLogEntry@1001 : Record "Change Log Entry");
    VAR
      SynchRecRef@1003 : RecordRef;
      KeyRef@1004 : KeyRef;
      KeyFldRef@1005 : FieldRef;
      i@1006 : Integer;
      MaxKeyCount@1007 : Integer;
    BEGIN
      SynchRecRef := RecID.GETRECORD;

      ChangeLogEntry.SETRANGE("Table No.",RecID.TABLENO);

      KeyRef := SynchRecRef.KEYINDEX(1);
      MaxKeyCount := KeyRef.FIELDCOUNT;
      if MaxKeyCount > 3 then
        MaxKeyCount := 3;
      for i := 1 to MaxKeyCount do begin
        KeyFldRef := KeyRef.FIELDINDEX(i);
        case i of
          1:
            begin
              ChangeLogEntry.SETRANGE("Primary Key Field 1 No.",KeyFldRef.NUMBER);
              ChangeLogEntry.SETRANGE("Primary Key Field 1 Value",COPYSTR(FORMAT(KeyFldRef.VALUE,0,9),1,50));
            end;
          2:
            begin
              ChangeLogEntry.SETRANGE("Primary Key Field 2 No.",KeyFldRef.NUMBER);
              ChangeLogEntry.SETRANGE("Primary Key Field 2 Value",COPYSTR(FORMAT(KeyFldRef.VALUE,0,9),1,50));
            end;
          3:
            begin
              ChangeLogEntry.SETRANGE("Primary Key Field 3 No.",KeyFldRef.NUMBER);
              ChangeLogEntry.SETRANGE("Primary Key Field 3 Value",COPYSTR(FORMAT(KeyFldRef.VALUE,0,9),1,50));
            end;
        end;
      end;
    END;

    [External]
    PROCEDURE SetupFieldsModified@9(VAR ChangeLogEntry@1000 : Record "Change Log Entry";VAR OSynchField@1001 : Record "Outlook Synch. Field") : Boolean;
    VAR
      OSynchFilter@1003 : Record "Outlook Synch. Filter";
    BEGIN
      if OSynchField.FIND('-') then
        repeat
          if OSynchField."Table No." = 0 then begin
            ChangeLogEntry.SETRANGE("Field No.",OSynchField."Field No.");
            if ChangeLogEntry.FIND('-') then begin
              ChangeLogEntry.SETRANGE("Field No.");
              exit(true);
            end;
          end else begin
            OSynchFilter.RESET;
            OSynchFilter.SETRANGE("Record GUID",OSynchField."Record GUID");
            OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
            OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
            if OSynchFilter.FIND('-') then
              repeat
                ChangeLogEntry.SETRANGE("Field No.",OSynchFilter."Master Table Field No.");
                if ChangeLogEntry.FIND('-') then begin
                  ChangeLogEntry.SETRANGE("Field No.");
                  exit(true);
                end;
              until OSynchFilter.NEXT = 0;
          end;
        until OSynchField.NEXT = 0;

      ChangeLogEntry.SETRANGE("Field No.");
    END;

    [Internal]
    PROCEDURE OneToOneRelation@17(VAR OSynchFilter@1000 : Record "Outlook Synch. Filter") : Boolean;
    VAR
      RecRef@1001 : RecordRef;
      KeyRef@1002 : KeyRef;
      FieldRef@1004 : FieldRef;
      Counter@1003 : Integer;
    BEGIN
      OSynchFilter.FINDFIRST;

      RecRef.OPEN(OSynchFilter."Table No.",true);
      KeyRef := RecRef.KEYINDEX(1);

      for Counter := 1 to KeyRef.FIELDCOUNT do begin
        FieldRef := KeyRef.FIELDINDEX(Counter);
        OSynchFilter.SETRANGE("Field No.",FieldRef.NUMBER);
        if not OSynchFilter.FINDFIRST then
          exit(false);
      end;

      RecRef.CLOSE;
      exit(true);
    END;

    BEGIN
    END.
  }
}

