OBJECT Codeunit 5200 Employee/Resource Update
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData Resource=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Res@1000 : Record Resource;

    [External]
    PROCEDURE HumanResToRes@1(OldEmployee@1000 : Record Employee;Employee@1001 : Record Employee);
    BEGIN
      if (Employee."Resource No." <> '') and
         ((OldEmployee."Resource No." <> Employee."Resource No.") or
          (OldEmployee."Job Title" <> Employee."Job Title") or
          (OldEmployee."First Name" <> Employee."First Name") or
          (OldEmployee."Last Name" <> Employee."Last Name") or
          (OldEmployee.Address <> Employee.Address) or
          (OldEmployee."Address 2" <> Employee."Address 2") or
          (OldEmployee."Post Code" <> Employee."Post Code") or
          (OldEmployee."Social Security No." <> Employee."Social Security No.") or
          (OldEmployee."Employment Date" <> Employee."Employment Date"))
      then
        ResUpdate(Employee)
      else
        exit;
    END;

    [External]
    PROCEDURE ResUpdate@4(Employee@1000 : Record Employee);
    BEGIN
      Res.GET(Employee."Resource No.");
      Res."Job Title" := Employee."Job Title";
      Res.Name := COPYSTR(Employee.FullName,1,30);
      Res.Address := Employee.Address;
      Res."Address 2" := Employee."Address 2";
      Res.VALIDATE("Post Code",Employee."Post Code");
      Res."Social Security No." := Employee."Social Security No.";
      Res."Employment Date" := Employee."Employment Date";
      Res.MODIFY(true)
    END;

    BEGIN
    END.
  }
}

