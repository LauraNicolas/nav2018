OBJECT Report 6004 Batch Post Service Invoices
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Batch Post Service Invoices;
    ProcessingOnly=true;
  }
  DATASET
  {
    { 1634;    ;DataItem;Service Header      ;
               DataItemTable="Service Header";
               DataItemTableView=SORTING("Document Type","No.") WHERE("Document Type"=CONST(Invoice));
               OnPreDataItem=BEGIN
                               if ReplacePostingDate and (PostingDateReq = 0D) then
                                 ERROR(Text000);
                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=BEGIN
                                  if CalcInvDisc then
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  CLEAR(ServPost);
                                  ServPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  ServPost.SetPostingOptions(true,false,true); // ship, consume, invoice
                                  if ServPost.RUN("Service Header") then begin
                                    CounterOK := CounterOK + 1;
                                    if MARKEDONLY then
                                      MARK(false);
                                  end;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields="No.",Status,Priority }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                   ReplacePostingDate := false;
                   ReplaceDocumentDate := false;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=ENU=Posting Date;
                  ToolTipML=ENU=Specifies the date that the program uses as the document or posting date if you select the Replace Posting Date check box or the Replace Document Date check box or both check boxes.;
                  ApplicationArea=#Service;
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  Name=ReplacePostingDate;
                  CaptionML=ENU=Replace Posting Date;
                  ToolTipML=ENU=Specifies if you want to replace the posting date of the service invoices with the date that you entered in the Posting Date field.;
                  ApplicationArea=#Service;
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               if ReplacePostingDate then
                                 MESSAGE(Text003);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  Name=Replace Document Date;
                  CaptionML=ENU=Replace Document Date;
                  ToolTipML=ENU=Specifies if you want to replace the document date of the service invoices with the date in the Posting Date field.;
                  ApplicationArea=#Service;
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  Name=Calc. Inv. Discount;
                  CaptionML=ENU=Calc. Inv. Discount;
                  ToolTipML=ENU=Specifies if you want the invoice discount amount to be automatically calculated on the invoices before posting.;
                  ApplicationArea=#Service;
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               SalesSetup.GET;
                               SalesSetup.TESTFIELD("Calc. Inv. Discount",false);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the posting date.';
      Text001@1001 : TextConst 'ENU=Posting invoices   #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 invoices out of a total of %2 have now been posted.';
      Text003@1003 : TextConst 'ENU=The exchange rate associated with the new posting date on the service header will not apply to the service lines.';
      ServLine@1005 : Record "Service Line";
      SalesSetup@1006 : Record "Sales & Receivables Setup";
      ServCalcDisc@1007 : Codeunit "Service-Calc. Discount";
      ServPost@1008 : Codeunit "Service-Post";
      Window@1009 : Dialog;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocumentDate@1015 : Boolean;
      CalcInvDisc@1016 : Boolean;

    LOCAL PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      ServLine.RESET;
      ServLine.SETRANGE("Document Type","Service Header"."Document Type");
      ServLine.SETRANGE("Document No.","Service Header"."No.");
      if ServLine.FINDFIRST then
        if ServCalcDisc.RUN(ServLine) then begin
          "Service Header".GET("Service Header"."Document Type","Service Header"."No.");
          COMMIT;
        end;
    END;

    PROCEDURE InitializeRequest@2(PostingDateReqFrom@1000 : Date;ReplacePostingDateFrom@1003 : Boolean;ReplaceDocumentDateFrom@1002 : Boolean;CalcInvDiscFrom@1001 : Boolean);
    BEGIN
      PostingDateReq := PostingDateReqFrom;
      ReplacePostingDate := ReplacePostingDateFrom;
      ReplaceDocumentDate := ReplaceDocumentDateFrom;
      CalcInvDisc := CalcInvDiscFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

