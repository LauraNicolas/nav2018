OBJECT Codeunit 1330 Instruction Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "My Notifications"=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NotAllowedToPostOutsideFiscalYearErr@1000 : TextConst 'ENU=You cannot post when one or more dates is outside the current fiscal year.';
      WarnUnpostedDocumentsTxt@1006 : TextConst 'ENU=Warn about unposted documents.';
      WarnUnpostedDocumentsDescriptionTxt@1005 : TextConst 'ENU=Show warning when you close a document that you have not posted.';
      ConfirmAfterPostingDocumentsTxt@1004 : TextConst 'ENU=Confirm after posting documents.';
      ConfirmAfterPostingDocumentsDescriptionTxt@1003 : TextConst 'ENU=Show warning when you post a document where you can choose to view the posted document.';
      ConfirmPostingOutsideFiscalYearTxt@1002 : TextConst 'ENU=Confirm posting outside the fiscal year.';
      ConfirmPostingOutsideFiscalYearDescriptionTxt@1001 : TextConst 'ENU=Show warning when you post entries where the posting date is in another fiscal year.';
      MarkBookingAsInvoicedWarningTxt@1007 : TextConst 'ENU=Confirm marking booking as invoiced.';
      MarkBookingAsInvoicedWarningDescriptionTxt@1008 : TextConst 'ENU=Show warning when you mark a Booking appointment as invoiced.';
      OfficeUpdateNotificationTxt@1009 : TextConst 'ENU=Notify user of Outlook add-in update.';
      OfficeUpdateNotificationDescriptionTxt@1010 : TextConst 'ENU=Ask user to update their Outlook add-in when an update is available.';

    [External]
    PROCEDURE ShowConfirm@8(ConfirmQst@1000 : Text;InstructionType@1001 : Code[50]) : Boolean;
    BEGIN
      if GUIALLOWED and IsEnabled(InstructionType) then
        exit(CONFIRM(ConfirmQst));

      exit(true);
    END;

    [External]
    PROCEDURE ConfirmPostingOutsideFiscalYear@9(ConfirmQst@1003 : Text;PostingDate@1001 : Date;InstructionType@1000 : Code[50]) : Boolean;
    VAR
      AccountingPeriod@1002 : Record "Accounting Period";
    BEGIN
      if GUIALLOWED and not IsEnabled(InstructionType) then begin
        FindNextFYStartingDate(AccountingPeriod);
        if PostingDate >= AccountingPeriod."Starting Date" then begin
          if CONFIRM(ConfirmQst,false) then
            exit(true);
          ERROR(NotAllowedToPostOutsideFiscalYearErr);
        end;
      end;
    END;

    [External]
    PROCEDURE DisableMessageForCurrentUser@4(InstructionType@1002 : Code[50]);
    VAR
      UserPreference@1000 : Record "User Preference";
    BEGIN
      UserPreference.DisableInstruction(InstructionType);
    END;

    [External]
    PROCEDURE EnableMessageForCurrentUser@10(InstructionType@1002 : Code[50]);
    VAR
      UserPreference@1000 : Record "User Preference";
    BEGIN
      UserPreference.EnableInstruction(InstructionType);
    END;

    [External]
    PROCEDURE IsEnabled@1(InstructionType@1003 : Code[50]) : Boolean;
    VAR
      UserPreference@1000 : Record "User Preference";
    BEGIN
      exit(not UserPreference.GET(USERID,InstructionType));
    END;

    [External]
    PROCEDURE ShowPostedConfirmationMessageCode@6() : Code[50];
    BEGIN
      exit(UPPERCASE('ShowPostedConfirmationMessage'));
    END;

    [External]
    PROCEDURE QueryPostOnCloseCode@2() : Code[50];
    BEGIN
      exit(UPPERCASE('QueryPostOnClose'));
    END;

    [External]
    PROCEDURE OfficeUpdateNotificationCode@3() : Code[50];
    BEGIN
      exit(UPPERCASE('OfficeUpdateNotification'));
    END;

    [External]
    PROCEDURE PostingOutsideFiscalYearNotAllowedCode@5() : Code[50];
    BEGIN
      exit(UPPERCASE('PostingOutsideFiscalYearNotAllowed'));
    END;

    [External]
    PROCEDURE MarkBookingAsInvoicedWarningCode@15() : Code[50];
    BEGIN
      exit(UPPERCASE('MarkBookingAsInvoicedWarning'));
    END;

    LOCAL PROCEDURE FindNextFYStartingDate@11(VAR AccountingPeriod@1000 : Record "Accounting Period");
    BEGIN
      AccountingPeriod.SETRANGE(Closed,false);
      AccountingPeriod.SETRANGE("New Fiscal Year",true);
      AccountingPeriod.FINDSET;
      // Try to get starting period of next opened fiscal year
      if AccountingPeriod.NEXT = 0 then begin
        // If not exists then get the day after the last period of opened fiscal year
        AccountingPeriod.SETRANGE("New Fiscal Year");
        AccountingPeriod.FINDLAST;
        AccountingPeriod."Starting Date" := CALCDATE('<CM+1D>',AccountingPeriod."Starting Date");
      end;
    END;

    [External]
    PROCEDURE GetClosingUnpostedDocumentNotificationId@16() : GUID;
    BEGIN
      exit('612A2701-4BBB-4C5B-B4C0-629D96B60644');
    END;

    [External]
    PROCEDURE GetOpeningPostedDocumentNotificationId@17() : GUID;
    BEGIN
      exit('0C6ED8F1-7408-4352-8DD1-B9F17332607D');
    END;

    [External]
    PROCEDURE GetMarkBookingAsInvoicedWarningNotificationId@18() : GUID;
    BEGIN
      exit('413A3221-D47F-4FBF-8822-0029AB41F9A6');
    END;

    [External]
    PROCEDURE GetOfficeUpdateNotificationId@25() : GUID;
    BEGIN
      exit('882980DE-C2F6-4D4F-BF39-BB3A9FE3D7DA');
    END;

    [External]
    PROCEDURE GetPostingOutsideFiscalYeartNotificationId@14() : GUID;
    BEGIN
      exit('F76D6004-5EC5-4DEA-B14D-71B2AEB53ACF');
    END;

    [EventSubscriber(Page,1518,OnInitializingNotificationWithDefaultState)]
    LOCAL PROCEDURE OnInitializingNotificationWithDefaultState@13();
    VAR
      MyNotifications@1000 : Record "My Notifications";
    BEGIN
      MyNotifications.InsertDefault(GetClosingUnpostedDocumentNotificationId,
        WarnUnpostedDocumentsTxt,
        WarnUnpostedDocumentsDescriptionTxt,
        IsEnabled(QueryPostOnCloseCode));
      MyNotifications.InsertDefault(GetOpeningPostedDocumentNotificationId,
        ConfirmAfterPostingDocumentsTxt,
        ConfirmAfterPostingDocumentsDescriptionTxt,
        IsEnabled(ShowPostedConfirmationMessageCode));
      MyNotifications.InsertDefault(GetPostingOutsideFiscalYeartNotificationId,
        ConfirmPostingOutsideFiscalYearTxt,
        ConfirmPostingOutsideFiscalYearDescriptionTxt,
        IsEnabled(PostingOutsideFiscalYearNotAllowedCode));
      MyNotifications.InsertDefault(GetMarkBookingAsInvoicedWarningNotificationId,
        MarkBookingAsInvoicedWarningTxt,
        MarkBookingAsInvoicedWarningDescriptionTxt,
        IsEnabled(MarkBookingAsInvoicedWarningCode));
      MyNotifications.InsertDefault(GetOfficeUpdateNotificationId,
        OfficeUpdateNotificationTxt,
        OfficeUpdateNotificationDescriptionTxt,
        IsEnabled(OfficeUpdateNotificationCode));
    END;

    [EventSubscriber(Table,1518,OnStateChanged)]
    LOCAL PROCEDURE OnStateChanged@12(NotificationId@1000 : GUID;NewEnabledState@1001 : Boolean);
    BEGIN
      case NotificationId of
        GetClosingUnpostedDocumentNotificationId:
          if NewEnabledState then
            EnableMessageForCurrentUser(QueryPostOnCloseCode)
          else
            DisableMessageForCurrentUser(QueryPostOnCloseCode);
        GetOpeningPostedDocumentNotificationId:
          if NewEnabledState then
            EnableMessageForCurrentUser(ShowPostedConfirmationMessageCode)
          else
            DisableMessageForCurrentUser(ShowPostedConfirmationMessageCode);
        GetPostingOutsideFiscalYeartNotificationId:
          if NewEnabledState then
            EnableMessageForCurrentUser(PostingOutsideFiscalYearNotAllowedCode)
          else
            DisableMessageForCurrentUser(PostingOutsideFiscalYearNotAllowedCode);
      end;
    END;

    BEGIN
    END.
  }
}

