OBJECT Table 8618 Config. Template Header
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ConfigTmplSelectionRules@1000 : Record "Config. Tmpl. Selection Rules";
             BEGIN
               CALCFIELDS("Used In Hierarchy");
               if not "Used In Hierarchy" then begin
                 ConfigTemplateLine.SETRANGE("Data Template Code",Code);
                 ConfigTemplateLine.DELETEALL;
               end;

               ConfigTmplSelectionRules.SETRANGE("Template Code",Code);
               ConfigTmplSelectionRules.DELETEALL;
             END;

    OnRename=BEGIN
               CALCFIELDS("Used In Hierarchy");
               if not "Used In Hierarchy" then begin
                 ConfigTemplateLine.SETRANGE("Data Template Code",xRec.Code);
                 ConfigTemplateLine.FIND('-');
                 repeat
                   ConfigTemplateLine.RENAME(Code,ConfigTemplateLine."Line No.");
                 until ConfigTemplateLine.NEXT = 0;
               end;
             END;

    CaptionML=ENU=Config. Template Header;
    LookupPageID="Config. Template List";
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 2   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 3   ;   ;Table ID            ;Integer       ;OnValidate=BEGIN
                                                                TestXRec;
                                                                CALCFIELDS("Table Name");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ConfigValidateMgt.LookupTable("Table ID");
                                                              if "Table ID" <> 0 then
                                                                VALIDATE("Table ID");
                                                            END;

                                                   CaptionML=ENU=Table ID }
    { 4   ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE ("Object Type"=CONST(Table),
                                                                                                             "Object ID"=FIELD("Table ID")));
                                                   CaptionML=ENU=Table Name;
                                                   Editable=false }
    { 5   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Table),
                                                                                                                "Object ID"=FIELD("Table ID")));
                                                   CaptionML=ENU=Table Caption;
                                                   Editable=false }
    { 6   ;   ;Used In Hierarchy   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Config. Template Line" WHERE ("Data Template Code"=FIELD(Code),
                                                                                                    Type=CONST(Template)));
                                                   CaptionML=ENU=Used In Hierarchy;
                                                   Editable=false }
    { 7   ;   ;Enabled             ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Enabled }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Template lines that relate to %1 exists. Delete the lines to change the Table ID.';
      ConfigTemplateLine@1001 : Record "Config. Template Line";
      Text001@1002 : TextConst '@@@="%2 = Table ID, %3 = Table Caption";ENU=A new instance %1 has been created in table %2 %3.';
      ConfigValidateMgt@1003 : Codeunit "Config. Validate Management";

    LOCAL PROCEDURE TestXRec@1();
    VAR
      ConfigTemplateLine@1000 : Record "Config. Template Line";
    BEGIN
      ConfigTemplateLine.SETRANGE("Data Template Code",Code);
      if ConfigTemplateLine.FINDFIRST then
        if xRec."Table ID" <> "Table ID" then
          ERROR(Text000,xRec."Table ID");
    END;

    [External]
    PROCEDURE ConfirmNewInstance@2(VAR RecRef@1000 : RecordRef);
    VAR
      KeyRef@1004 : KeyRef;
      FieldRef@1003 : FieldRef;
      KeyFieldCount@1002 : Integer;
      MessageString@1001 : Text[1024];
    BEGIN
      KeyRef := RecRef.KEYINDEX(1);
      for KeyFieldCount := 1 to KeyRef.FIELDCOUNT do begin
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        MessageString := MessageString + ' ' + FORMAT(FieldRef.VALUE);
        MessageString := DELCHR(MessageString,'<');
        MESSAGE(STRSUBSTNO(Text001,MessageString,RecRef.NUMBER,RecRef.CAPTION));
      end;
    END;

    [External]
    PROCEDURE SetTemplateEnabled@3(IsEnabled@1000 : Boolean);
    BEGIN
      VALIDATE(Enabled,IsEnabled);
      MODIFY(true);
    END;

    BEGIN
    END.
  }
}

