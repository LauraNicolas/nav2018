OBJECT Table 8619 Config. Template Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ConfigTemplateHeader@1000 : Record "Config. Template Header";
             BEGIN
               if ConfigTemplateHeader.GET("Data Template Code") then begin
                 "Table ID" := ConfigTemplateHeader."Table ID";
                 if "Language ID" = 0 then
                   "Language ID" := GLOBALLANGUAGE;
               end;
             END;

    CaptionML=ENU=Config. Template Line;
  }
  FIELDS
  {
    { 1   ;   ;Data Template Code  ;Code10        ;TableRelation="Config. Template Header";
                                                   CaptionML=ENU=Data Template Code;
                                                   Editable=false }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   Editable=false }
    { 3   ;   ;Type                ;Option        ;InitValue=Field;
                                                   OnValidate=BEGIN
                                                                case Type of
                                                                  Type::Field:
                                                                    CLEAR("Template Code");
                                                                  Type::Template:
                                                                    begin
                                                                      CLEAR("Field Name");
                                                                      CLEAR("Field ID");
                                                                    end;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Field,Template,Related Template;
                                                   OptionString=Field,Template,"Related Template" }
    { 4   ;   ;Field ID            ;Integer       ;TableRelation=IF (Type=CONST(Field)) Field."No." WHERE (TableNo=FIELD("Table ID"),
                                                                                                           Class=CONST(Normal));
                                                   CaptionML=ENU=Field ID }
    { 5   ;   ;Field Name          ;Text30        ;FieldClass=Normal;
                                                   OnValidate=VAR
                                                                ConfigTemplateLine@1000 : Record "Config. Template Line";
                                                                ConfigTemplateMgt@1001 : Codeunit "Config. Template Management";
                                                              BEGIN
                                                                ConfigTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                ConfigTemplateLine.SETRANGE("Field Name","Field Name");
                                                                if not ConfigTemplateLine.ISEMPTY then
                                                                  ERROR(STRSUBSTNO(Text004,"Field Name"));

                                                                ConfigTemplateMgt.TestHierarchy(Rec);
                                                              END;

                                                   OnLookup=BEGIN
                                                              SelectFieldName;
                                                            END;

                                                   CaptionML=ENU=Field Name;
                                                   Editable=false }
    { 6   ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID }
    { 7   ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE ("Object Type"=FILTER(Table),
                                                                                                             "Object ID"=FIELD("Table ID")));
                                                   CaptionML=ENU=Table Name;
                                                   Editable=false }
    { 8   ;   ;Template Code       ;Code10        ;TableRelation="Config. Template Header";
                                                   OnValidate=VAR
                                                                ConfigTemplateHeader@1000 : Record "Config. Template Header";
                                                                ConfigTemplateHeader2@1001 : Record "Config. Template Header";
                                                                ConfigTemplateLine@1003 : Record "Config. Template Line";
                                                                ConfigTemplateMgt@1002 : Codeunit "Config. Template Management";
                                                              BEGIN
                                                                if Type = Type::Field then
                                                                  ERROR(Text005);

                                                                if "Template Code" = "Data Template Code" then
                                                                  ERROR(Text000);

                                                                if ConfigTemplateHeader.GET("Template Code") then
                                                                  if ConfigTemplateHeader2.GET("Data Template Code") then
                                                                    if ConfigTemplateHeader."Table ID" <> ConfigTemplateHeader2."Table ID" then
                                                                      ERROR(STRSUBSTNO(Text002,ConfigTemplateHeader.Code,ConfigTemplateHeader2."Table ID"));

                                                                ConfigTemplateMgt.TestHierarchy(Rec);

                                                                ConfigTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                ConfigTemplateLine.SETRANGE("Template Code","Template Code");
                                                                if not ConfigTemplateLine.ISEMPTY then
                                                                  ERROR(STRSUBSTNO(Text003,"Template Code"));
                                                              END;

                                                   OnLookup=VAR
                                                              ConfigTemplateHeader@1002 : Record "Config. Template Header";
                                                              ConfigTemplateList@1000 : Page "Config. Template List";
                                                            BEGIN
                                                              if Type = Type::Field then
                                                                exit;

                                                              ConfigTemplateHeader.GET("Data Template Code");
                                                              if ConfigTemplateHeader."Table ID" = 0 then
                                                                exit;

                                                              ConfigTemplateHeader.SETRANGE("Table ID",ConfigTemplateHeader."Table ID");
                                                              ConfigTemplateList.SETTABLEVIEW(ConfigTemplateHeader);
                                                              ConfigTemplateList.LOOKUPMODE := true;
                                                              ConfigTemplateList.EDITABLE := false;
                                                              if ConfigTemplateList.RUNMODAL = ACTION::LookupOK then begin
                                                                ConfigTemplateList.GETRECORD(ConfigTemplateHeader);
                                                                if ConfigTemplateHeader.Code = "Data Template Code" then
                                                                  ERROR(Text000);
                                                                CALCFIELDS("Template Description");
                                                                VALIDATE("Template Code",ConfigTemplateHeader.Code);
                                                              end;
                                                            END;

                                                   CaptionML=ENU=Template Code }
    { 9   ;   ;Template Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Config. Template Header".Description WHERE (Code=FIELD("Data Template Code")));
                                                   CaptionML=ENU=Template Description;
                                                   Editable=false }
    { 10  ;   ;Mandatory           ;Boolean       ;CaptionML=ENU=Mandatory }
    { 11  ;   ;Reference           ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=ENU=Reference }
    { 12  ;   ;Default Value       ;Text250       ;OnValidate=VAR
                                                                TempConfigPackageField@1005 : TEMPORARY Record "Config. Package Field";
                                                                ConfigPackageManagement@1004 : Codeunit "Config. Package Management";
                                                                ConfigValidateMgt@1003 : Codeunit "Config. Validate Management";
                                                                RecRef@1002 : RecordRef;
                                                                FieldRef@1001 : FieldRef;
                                                                ValidationError@1000 : Text[250];
                                                              BEGIN
                                                                if ("Field ID" <> 0) and ("Default Value" <> '') then begin
                                                                  RecRef.OPEN("Table ID",true);
                                                                  FieldRef := RecRef.FIELD("Field ID");
                                                                  ValidationError := ConfigValidateMgt.EvaluateValue(FieldRef,"Default Value",false);
                                                                  if ValidationError <> '' then
                                                                    ERROR(ValidationError);

                                                                  "Default Value" := FORMAT(FieldRef.VALUE);

                                                                  if not "Skip Relation Check" then begin
                                                                    ConfigPackageManagement.GetFieldsOrder(RecRef,'',TempConfigPackageField);
                                                                    ConfigValidateMgt.TransferRecordDefaultValues("Data Template Code",RecRef,"Field ID","Default Value");
                                                                    ValidationError := ConfigValidateMgt.ValidateFieldRefRelationAgainstCompanyData(FieldRef,TempConfigPackageField);

                                                                    if ValidationError <> '' then
                                                                      ERROR(ValidationError);
                                                                  end;

                                                                  if GLOBALLANGUAGE <> "Language ID" then
                                                                    VALIDATE("Language ID",GLOBALLANGUAGE);
                                                                end
                                                              END;

                                                   CaptionML=ENU=Default Value }
    { 13  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=FILTER(Table),
                                                                                                                "Object ID"=FIELD("Table ID")));
                                                   CaptionML=ENU=Table Caption;
                                                   Editable=false }
    { 14  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD("Table ID"),
                                                                                                   "No."=FIELD("Field ID")));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=false }
    { 15  ;   ;Skip Relation Check ;Boolean       ;CaptionML=ENU=Skip Relation Check }
    { 16  ;   ;Language ID         ;Integer       ;InitValue=0;
                                                   CaptionML=ENU=Language ID }
  }
  KEYS
  {
    {    ;"Data Template Code","Line No."         ;Clustered=true }
    {    ;"Data Template Code",Type                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=A template cannot relate to itself. Specify a different template.';
      Text002@1002 : TextConst 'ENU=The template %1 does not relate to table %2.';
      Text003@1003 : TextConst 'ENU=The template %1 is already in this hierarchy.';
      Text004@1004 : TextConst 'ENU=Field %1 is already in the template.';
      Text005@1005 : TextConst 'ENU=The template line cannot be edited if type is Field.';

    PROCEDURE SelectFieldName@2();
    VAR
      ConfigTemplateHeader@1003 : Record "Config. Template Header";
      Field@1002 : Record Field;
      FieldList@1001 : Page "Field List";
    BEGIN
      if Type = Type::Template then
        exit;

      ConfigTemplateHeader.GET("Data Template Code");

      if ConfigTemplateHeader."Table ID" = 0 then
        exit;

      CLEAR(FieldList);
      Field.SETRANGE(TableNo,ConfigTemplateHeader."Table ID");
      FieldList.SETTABLEVIEW(Field);
      FieldList.LOOKUPMODE := true;
      if FieldList.RUNMODAL = ACTION::LookupOK then begin
        FieldList.GETRECORD(Field);
        "Table ID" := Field.TableNo;
        VALIDATE("Field ID",Field."No.");
        VALIDATE("Field Name",Field.FieldName);
      end;
    END;

    PROCEDURE GetLine@1(VAR ConfigTemplateLine@1000 : Record "Config. Template Line";DataTemplateCode@1001 : Code[10];FieldID@1002 : Integer) : Boolean;
    BEGIN
      ConfigTemplateLine.SETRANGE("Data Template Code",DataTemplateCode);
      ConfigTemplateLine.SETRANGE("Field ID",FieldID);
      if not ConfigTemplateLine.FINDFIRST then
        exit(false);
      exit(true)
    END;

    BEGIN
    END.
  }
}

