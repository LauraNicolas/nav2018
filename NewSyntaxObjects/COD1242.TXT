OBJECT Codeunit 1242 Set Up Curr Exch Rate Service
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=VAR
            Currency@1003 : Record Currency;
            CurrExchRateUpdateSetup@1001 : Record "Curr. Exch. Rate Update Setup";
            GLSetup@1002 : Record "General Ledger Setup";
          BEGIN
            if Currency.ISEMPTY then
              exit;
            if not CurrExchRateUpdateSetup.ISEMPTY then
              exit;

            GLSetup.GET;
            if GLSetup."LCY Code" <> '' then
              SetupYahooDataExchange(CurrExchRateUpdateSetup,GetYahooURI);
            if GLSetup."LCY Code" = 'EUR' then
              SetupECBDataExchange(CurrExchRateUpdateSetup,GetECB_URI);
            COMMIT;
          END;

  }
  CODE
  {
    VAR
      DummyDataExchColumnDef@1000 : Record "Data Exch. Column Def";
      DummyCurrExchRate@1001 : Record "Currency Exchange Rate";
      YAHOO_EXCH_RATESTxt@1003 : TextConst '@@@=Yahoo Currency Exchange Rate Code;ENU=YAHOO-EXCHANGE-RATES';
      ECB_EXCH_RATESTxt@1004 : TextConst '@@@=European Central Bank Currency Exchange Rate Code;ENU=ECB-EXCHANGE-RATES';
      YAHOO_EXCH_RATESDescTxt@1006 : TextConst 'ENU=Yahoo Currency Exchange Rates Setup';
      ECB_EXCH_RATESDescTxt@1005 : TextConst 'ENU=European Central Bank Currency Exchange Rates Setup';
      YahooFinanceURLTxt@1007 : TextConst '@@@={Locked};ENU="http://query.yahooapis.com/v1/public/yql?%1&env=store://datatables.org/alltableswithkeys"';
      ECB_URLTxt@1008 : TextConst '@@@={Locked};ENU=http://www.ecb.europa.eu/stats/eurofxref/eurofxref-daily.xml';
      YahooFinanceTermsOfUseTxt@1009 : TextConst '@@@={Locked};ENU=https://policies.yahoo.com/us/en/yahoo/terms/product-atos/yql/index.htm';
      YahooServiceProviderTxt@1010 : TextConst '@@@={Locked};ENU=Yahoo!';
      ECBServiceProviderTxt@1011 : TextConst 'ENU=European Central Bank';

    [Internal]
    PROCEDURE SetupYahooDataExchange@4(VAR CurrExchRateUpdateSetup@1003 : Record "Curr. Exch. Rate Update Setup";PathToYahooService@1002 : Text);
    VAR
      DataExchLineDef@1000 : Record "Data Exch. Line Def";
      SuggestColDefinitionXML@1001 : Codeunit "Suggest Col. Definition - XML";
    BEGIN
      DataExchLineDef.SETRANGE("Data Exch. Def Code",YAHOO_EXCH_RATESTxt);
      if DataExchLineDef.FINDFIRST then;

      CreateCurrencyExchangeSetup(
        CurrExchRateUpdateSetup,YAHOO_EXCH_RATESTxt,YAHOO_EXCH_RATESDescTxt,
        DataExchLineDef."Data Exch. Def Code",YahooServiceProviderTxt,YahooFinanceTermsOfUseTxt);

      if STRPOS(PathToYahooService,'http') = 1 then
        CurrExchRateUpdateSetup.SetWebServiceURL(PathToYahooService);

      if DataExchLineDef."Data Exch. Def Code" = '' then begin
        CreateExchLineDef(DataExchLineDef,CurrExchRateUpdateSetup."Data Exch. Def Code",GetYahooRepeaterPath);
        SuggestColDefinitionXML.GenerateDataExchColDef(PathToYahooService,DataExchLineDef);

        MapYahooDataExch(DataExchLineDef);
      end;
      COMMIT;
    END;

    [Internal]
    PROCEDURE SetupECBDataExchange@5(VAR CurrExchRateUpdateSetup@1001 : Record "Curr. Exch. Rate Update Setup";PathToECBService@1002 : Text);
    VAR
      DataExchLineDef@1003 : Record "Data Exch. Line Def";
      SuggestColDefinitionXML@1000 : Codeunit "Suggest Col. Definition - XML";
    BEGIN
      DataExchLineDef.SETRANGE("Data Exch. Def Code",ECB_EXCH_RATESTxt);
      if DataExchLineDef.FINDFIRST then;

      CreateCurrencyExchangeSetup(
        CurrExchRateUpdateSetup,ECB_EXCH_RATESTxt,ECB_EXCH_RATESDescTxt,
        DataExchLineDef."Data Exch. Def Code",ECBServiceProviderTxt,'');

      if STRPOS(PathToECBService,'http') = 1 then
        CurrExchRateUpdateSetup.SetWebServiceURL(PathToECBService);

      if DataExchLineDef."Data Exch. Def Code" = '' then begin
        CreateExchLineDef(DataExchLineDef,CurrExchRateUpdateSetup."Data Exch. Def Code",GetECBRepeaterPath);
        SuggestColDefinitionXML.GenerateDataExchColDef(PathToECBService,DataExchLineDef);

        MapECBDataExch(DataExchLineDef);
      end;
      COMMIT;
    END;

    LOCAL PROCEDURE CreateCurrencyExchangeSetup@29(VAR CurrExchRateUpdateSetup@1002 : Record "Curr. Exch. Rate Update Setup";NewCode@1000 : Code[20];NewDesc@1001 : Text[250];NewDataExchCode@1005 : Code[20];NewServiceProvider@1004 : Text[30];NewTermOfUse@1003 : Text[250]);
    BEGIN
      CurrExchRateUpdateSetup.INIT;
      CurrExchRateUpdateSetup.VALIDATE("Data Exch. Def Code",NewDataExchCode);
      CurrExchRateUpdateSetup.VALIDATE(Code,NewCode);
      CurrExchRateUpdateSetup.VALIDATE(Description,NewDesc);
      CurrExchRateUpdateSetup.VALIDATE("Service Provider",NewServiceProvider);
      CurrExchRateUpdateSetup.VALIDATE("Terms of Service",NewTermOfUse);
      CurrExchRateUpdateSetup.INSERT(true);
    END;

    [External]
    PROCEDURE GetYahooURI@6() : Text;
    VAR
      Currency@1005 : Record Currency;
      GLSetup@1007 : Record "General Ledger Setup";
      QueryText@1002 : Text;
      CurrencyPairText@1001 : Text;
    BEGIN
      GLSetup.GET;
      GLSetup.TESTFIELD("LCY Code");
      Currency.SETFILTER(Code,'<>%1',GLSetup."LCY Code");
      Currency.FINDSET;
      repeat
        if CurrencyPairText <> '' then
          CurrencyPairText := CurrencyPairText + ',';
        CurrencyPairText := CurrencyPairText + STRSUBSTNO('"%1%2"',GLSetup."LCY Code",Currency.Code);
      until Currency.NEXT = 0;

      QueryText :=
        STRSUBSTNO(
          'q=select * from yahoo.finance.xchange where pair in (%1)',CurrencyPairText);

      exit(STRSUBSTNO(YahooFinanceURLTxt,QueryText));
    END;

    [External]
    PROCEDURE GetECB_URI@9() : Text;
    VAR
      GLSetup@1000 : Record "General Ledger Setup";
    BEGIN
      GLSetup.GET;
      GLSetup.TESTFIELD("LCY Code",'EUR');
      exit(ECB_URLTxt);
    END;

    LOCAL PROCEDURE CreateExchLineDef@14(VAR DataExchLineDef@1000 : Record "Data Exch. Line Def";DataExchDefCode@1003 : Code[20];RepeaterPath@1005 : Text[250]);
    BEGIN
      DataExchLineDef.SETRANGE("Data Exch. Def Code",DataExchDefCode);
      DataExchLineDef.FINDFIRST;
      DataExchLineDef.VALIDATE("Data Line Tag",RepeaterPath);
      DataExchLineDef.MODIFY(true);
    END;

    LOCAL PROCEDURE CreateExchMappingLine@8(DataExchMapping@1000 : Record "Data Exch. Mapping";FromColumnName@1001 : Text[250];ToFieldNo@1003 : Integer;DataType@1005 : Option;NewMultiplier@1006 : Decimal;NewDataFormat@1007 : Text[10];NewTransformationRule@1008 : Code[20];NewDefaultValue@1009 : Text[250]);
    VAR
      DataExchFieldMapping@1002 : Record "Data Exch. Field Mapping";
      DataExchColumnDef@1004 : Record "Data Exch. Column Def";
    BEGIN
      with DataExchColumnDef do begin
        SETRANGE("Data Exch. Def Code",DataExchMapping."Data Exch. Def Code");
        SETRANGE("Data Exch. Line Def Code",DataExchMapping."Data Exch. Line Def Code");
        if NewDefaultValue <> '' then begin
          if FINDLAST then begin
            INIT;
            "Column No." += 10000;
            INSERT;
          end
        end else begin
          SETRANGE(Name,FromColumnName);
          FINDFIRST;
        end;
        VALIDATE("Data Type",DataType);
        VALIDATE("Data Format",NewDataFormat);
        MODIFY(true);
      end;

      with DataExchFieldMapping do begin
        INIT;
        VALIDATE("Data Exch. Def Code",DataExchMapping."Data Exch. Def Code");
        VALIDATE("Data Exch. Line Def Code",DataExchMapping."Data Exch. Line Def Code");
        VALIDATE("Table ID",DataExchMapping."Table ID");
        VALIDATE("Column No.",DataExchColumnDef."Column No.");
        VALIDATE("Field ID",ToFieldNo);
        VALIDATE(Multiplier,NewMultiplier);
        VALIDATE("Transformation Rule",NewTransformationRule);
        VALIDATE("Default Value",NewDefaultValue);
        INSERT(true);
      end;
    END;

    LOCAL PROCEDURE MapYahooDataExch@53(VAR DataExchLineDef@1001 : Record "Data Exch. Line Def");
    VAR
      DataExchMapping@1000 : Record "Data Exch. Mapping";
      TransformationRule@1002 : Record "Transformation Rule";
    BEGIN
      DataExchMapping.GET(DataExchLineDef."Data Exch. Def Code",DataExchLineDef.Code,GetMappingTable);

      TransformationRule.CreateDefaultTransformations;
      CreateExchMappingLine(
        DataExchMapping,GetYahooCurrencyCodeXMLElement,GetCurrencyCodeFieldNo,
        DummyDataExchColumnDef."Data Type"::Text,1,'',TransformationRule.GetFourthToSixthSubstringCode,'');
      CreateExchMappingLine(
        DataExchMapping,GetYahooStartingDateXMLElement,GetStartingDateFieldNo,
        DummyDataExchColumnDef."Data Type"::Date,1,'',TransformationRule.GetUSDateFormatCode,'');

      CreateExchMappingLine(
        DataExchMapping,GetYahooExchRateAmtXMLElement,GetExchRateAmtFieldNo,
        DummyDataExchColumnDef."Data Type"::Decimal,1,'','','');
      CreateExchMappingLine(
        DataExchMapping,'',GetRelationalExchRateFieldNo,
        DummyDataExchColumnDef."Data Type"::Decimal,1,'','','1');
    END;

    LOCAL PROCEDURE MapECBDataExch@52(VAR DataExchLineDef@1001 : Record "Data Exch. Line Def");
    VAR
      DataExchMapping@1000 : Record "Data Exch. Mapping";
    BEGIN
      DataExchMapping.GET(DataExchLineDef."Data Exch. Def Code",DataExchLineDef.Code,GetMappingTable);

      CreateExchMappingLine(
        DataExchMapping,GetECBCurrencyCodeXMLElement,GetCurrencyCodeFieldNo,
        DummyDataExchColumnDef."Data Type"::Text,1,'','','');
      CreateExchMappingLine(
        DataExchMapping,GetECBStartingDateXMLElement,GetStartingDateFieldNo,
        DummyDataExchColumnDef."Data Type"::Date,1,'','','');

      CreateExchMappingLine(
        DataExchMapping,GetECBExchRateXMLElement,GetExchRateAmtFieldNo,
        DummyDataExchColumnDef."Data Type"::Decimal,1,'','','');
      CreateExchMappingLine(
        DataExchMapping,'',GetRelationalExchRateFieldNo,
        DummyDataExchColumnDef."Data Type"::Decimal,1,'','','1');
    END;

    LOCAL PROCEDURE GetYahooRepeaterPath@56() : Text[250];
    BEGIN
      exit('/query/results/rate');
    END;

    LOCAL PROCEDURE GetECBRepeaterPath@44() : Text[250];
    BEGIN
      exit('/gesmes:Envelope/Cube/Cube/Cube');
    END;

    LOCAL PROCEDURE GetMappingTable@16() : Integer;
    BEGIN
      exit(DATABASE::"Currency Exchange Rate")
    END;

    LOCAL PROCEDURE GetYahooCurrencyCodeXMLElement@63() : Text[250];
    BEGIN
      exit('id');
    END;

    LOCAL PROCEDURE GetYahooExchRateAmtXMLElement@60() : Text[250];
    BEGIN
      exit('Rate');
    END;

    LOCAL PROCEDURE GetYahooStartingDateXMLElement@58() : Text[250];
    BEGIN
      exit('Date');
    END;

    LOCAL PROCEDURE GetECBCurrencyCodeXMLElement@69() : Text[250];
    BEGIN
      exit('currency');
    END;

    LOCAL PROCEDURE GetECBExchRateXMLElement@68() : Text[250];
    BEGIN
      exit('rate');
    END;

    LOCAL PROCEDURE GetECBStartingDateXMLElement@1() : Text[250];
    BEGIN
      exit('time');
    END;

    LOCAL PROCEDURE GetCurrencyCodeFieldNo@20() : Integer;
    BEGIN
      exit(DummyCurrExchRate.FIELDNO("Currency Code"));
    END;

    LOCAL PROCEDURE GetRelationalExchRateFieldNo@21() : Integer;
    BEGIN
      exit(DummyCurrExchRate.FIELDNO("Relational Exch. Rate Amount"));
    END;

    LOCAL PROCEDURE GetExchRateAmtFieldNo@24() : Integer;
    BEGIN
      exit(DummyCurrExchRate.FIELDNO("Exchange Rate Amount"));
    END;

    LOCAL PROCEDURE GetStartingDateFieldNo@22() : Integer;
    BEGIN
      exit(DummyCurrExchRate.FIELDNO("Starting Date"));
    END;

    BEGIN
    END.
  }
}

