OBJECT Codeunit 6502 Late Binding Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempCurrSupplyReservEntry@1009 : TEMPORARY Record "Reservation Entry";
      TempCurrDemandReservEntry@1008 : TEMPORARY Record "Reservation Entry";
      TempSupplyReservEntry@1007 : TEMPORARY Record "Reservation Entry";
      TempReservEntryDelete@1002 : TEMPORARY Record "Reservation Entry";
      TempReservEntryModify@1003 : TEMPORARY Record "Reservation Entry";
      TempReservEntryInsert@1004 : TEMPORARY Record "Reservation Entry";
      ReservMgt@1000 : Codeunit "Reservation Management";
      LastEntryNo@1005 : Integer;
      Text001@1010 : TextConst 'ENU=Not enough free supply available for reallocation.';

    LOCAL PROCEDURE CleanUpVariables@4();
    BEGIN
      CLEARALL;
      TempReservEntryDelete.RESET;
      TempReservEntryDelete.DELETEALL;
      TempReservEntryModify.RESET;
      TempReservEntryModify.DELETEALL;
      TempReservEntryInsert.RESET;
      TempReservEntryInsert.DELETEALL;
      TempCurrSupplyReservEntry.RESET;
      TempCurrSupplyReservEntry.DELETEALL;
      TempCurrDemandReservEntry.RESET;
      TempCurrDemandReservEntry.DELETEALL;
      TempSupplyReservEntry.RESET;
      TempSupplyReservEntry.DELETEALL;
    END;

    [External]
    PROCEDURE ReallocateTrkgSpecification@3(VAR TempTrackingSpecification@1000 : TEMPORARY Record "Tracking Specification");
    BEGIN
      // Go through the tracking specification and calculate what is available/reserved/can be reallocated
      // The buffer fields on TempTrackingSpecification are used as follows:
      // "Buffer Value1" : Non-allocated item tracking
      // "Buffer Value2" : Total inventory
      // "Buffer Value3" : Total reserved inventory
      // "Buffer Value4" : Qty for reallocation (negative = need for reallocation)
      // "Buffer Value5" : Total non-specific reserved inventory (can be un-reserved through reallocation)

      CleanUpVariables;

      TempTrackingSpecification.CALCSUMS("Buffer Value1"); // Non-allocated item tracking

      if TempTrackingSpecification."Buffer Value1" = 0 then
        exit; // Item tracking is fully allocated => no basis for reallocation

      if not CalcInventory(TempTrackingSpecification) then
        exit; // No reservations exist => no basis for reallocation

      TempTrackingSpecification.SETFILTER("Buffer Value4",'< %1',0);
      if TempTrackingSpecification.ISEMPTY then begin
        TempTrackingSpecification.RESET;
        exit; // Supply is available - no need for reallocation
      end;

      TempTrackingSpecification.RESET;

      // Try to free sufficient supply by reallocation within the tracking specification
      CalcAllocations(TempTrackingSpecification);

      TempTrackingSpecification.RESET;
      TempTrackingSpecification.CALCSUMS("Buffer Value4");

      if TempTrackingSpecification."Buffer Value4" < 0 then
        if not PrepareTempDataSet(TempTrackingSpecification,ABS(TempTrackingSpecification."Buffer Value4")) then
          exit; // There is not sufficient free supply to cover reallocation

      TempTrackingSpecification.RESET;
      Reallocate(TempTrackingSpecification);

      // Write to database in the end
      WriteToDatabase;
    END;

    LOCAL PROCEDURE Reallocate@10(VAR TempTrackingSpecification@1000 : TEMPORARY Record "Tracking Specification") AllocationsChanged : Boolean;
    VAR
      TempTrackingSpecification2@1002 : TEMPORARY Record "Tracking Specification";
      QtyToReallocate@1001 : Decimal;
    BEGIN
      TempTrackingSpecification.RESET;
      TempTrackingSpecification.SETFILTER("Buffer Value4",'< %1',0);
      if TempTrackingSpecification.FINDSET then
        repeat
          TempTrackingSpecification2 := TempTrackingSpecification;
          TempTrackingSpecification2.INSERT;
        until TempTrackingSpecification.NEXT = 0;
      TempTrackingSpecification.RESET;

      TempCurrSupplyReservEntry.RESET;
      if TempTrackingSpecification2.FINDSET then
        repeat
          TempCurrSupplyReservEntry.SetTrackingFilter(
            TempTrackingSpecification2."Serial No.",TempTrackingSpecification2."Lot No.");
          QtyToReallocate := ABS(TempTrackingSpecification2."Buffer Value4");
          if TempCurrSupplyReservEntry.FINDSET then
            repeat
              QtyToReallocate := ReshuffleReservEntry(TempCurrSupplyReservEntry,QtyToReallocate,TempTrackingSpecification);
            until (TempCurrSupplyReservEntry.NEXT = 0) or (QtyToReallocate = 0);
          AllocationsChanged := AllocationsChanged or (QtyToReallocate <> ABS(TempTrackingSpecification2."Buffer Value4"));
        until TempTrackingSpecification2.NEXT = 0;
    END;

    LOCAL PROCEDURE PrepareTempDataSet@12(VAR TempTrackingSpecification@1000 : TEMPORARY Record "Tracking Specification";QtyToPrepare@1001 : Decimal) : Boolean;
    VAR
      ItemLedgEntry@1007 : Record "Item Ledger Entry";
      ReservEntry2@1006 : Record "Reservation Entry";
    BEGIN
      if QtyToPrepare <= 0 then
        exit(true);

      TempTrackingSpecification.RESET;

      ItemLedgEntry.SETCURRENTKEY("Item No.",Open,"Variant Code",Positive,"Location Code");
      ItemLedgEntry.SETRANGE("Item No.",TempTrackingSpecification."Item No.");
      ItemLedgEntry.SETRANGE("Variant Code",TempTrackingSpecification."Variant Code");
      ItemLedgEntry.SETRANGE("Location Code",TempTrackingSpecification."Location Code");
      ItemLedgEntry.SETRANGE(Positive,true);
      ItemLedgEntry.SETRANGE(Open,true);

      ReservEntry2.SetSourceFilter(DATABASE::"Item Ledger Entry",0,'',-1,true);
      if ItemLedgEntry.FINDSET then
        repeat
          TempTrackingSpecification.SetTrackingFilter(ItemLedgEntry."Serial No.",ItemLedgEntry."Lot No.");
          if TempTrackingSpecification.ISEMPTY then begin
            InsertTempSupplyReservEntry(ItemLedgEntry);
            // GET record
            QtyToPrepare -= ItemLedgEntry."Remaining Quantity";
            TempSupplyReservEntry.GET(-ItemLedgEntry."Entry No.",true);
            ReservEntry2.SETRANGE("Source Ref. No.",ItemLedgEntry."Entry No.");
            if ReservEntry2.FINDSET then
              repeat
                TempSupplyReservEntry."Quantity (Base)" -= ReservEntry2."Quantity (Base)";
                if TempSupplyReservEntry."Quantity (Base)" = 0 then
                  TempSupplyReservEntry.DELETE
                else
                  TempSupplyReservEntry.MODIFY;

                if ReservEntry2."Reservation Status" = ReservEntry2."Reservation Status"::Surplus then begin
                  TempSupplyReservEntry := ReservEntry2;
                  TempSupplyReservEntry.INSERT;
                end else
                  QtyToPrepare += ReservEntry2."Quantity (Base)";
              until ReservEntry2.NEXT = 0;
          end;
        until (ItemLedgEntry.NEXT = 0) or (QtyToPrepare <= 0);

      TempTrackingSpecification.RESET;
      exit(QtyToPrepare <= 0);
    END;

    LOCAL PROCEDURE ReshuffleReservEntry@9(SupplyReservEntry@1000 : Record "Reservation Entry";QtyToReshuffle@1006 : Decimal;VAR TempTrackingSpecification@1008 : TEMPORARY Record "Tracking Specification") RemainingQty : Decimal;
    VAR
      TotalAvailable@1003 : Decimal;
      QtyToReshuffleThisLine@1004 : Decimal;
      xQtyToReshuffleThisLine@1005 : Decimal;
      AdjustmentQty@1013 : Decimal;
      NewQty@1007 : Decimal;
      xQty@1014 : Decimal;
    BEGIN
      if SupplyReservEntry."Reservation Status" > SupplyReservEntry."Reservation Status"::Tracking then
        exit; // The entry is neither reservation nor tracking and cannot be reshuffled

      if not SupplyReservEntry.Positive then
        exit; // The entry is not supply and cannot be reshuffled

      TempCurrDemandReservEntry.GET(SupplyReservEntry."Entry No.",not SupplyReservEntry.Positive); // Demand

      if TempCurrDemandReservEntry.TrackingExists then // The reservation is not open
        exit; // The entry is a specific allocation and cannot be reshuffled

      if QtyToReshuffle <= 0 then
        exit;

      TempSupplyReservEntry.CALCSUMS("Quantity (Base)");
      TotalAvailable := TempSupplyReservEntry."Quantity (Base)";

      if TotalAvailable < QtyToReshuffle then
        ERROR(Text001);

      if SupplyReservEntry."Quantity (Base)" > QtyToReshuffle then
        QtyToReshuffleThisLine := QtyToReshuffle
      else
        QtyToReshuffleThisLine := SupplyReservEntry."Quantity (Base)";

      xQtyToReshuffleThisLine := QtyToReshuffleThisLine;

      TempSupplyReservEntry.SETRANGE("Reservation Status",TempSupplyReservEntry."Reservation Status"::Surplus);
      if TempSupplyReservEntry.FINDSET then
        repeat
          TempTrackingSpecification.SetTrackingFilter(TempSupplyReservEntry."Serial No.",TempSupplyReservEntry."Lot No.");
          if TempTrackingSpecification.FINDFIRST then begin
            if TempTrackingSpecification."Buffer Value4" > 0 then begin
              if TempTrackingSpecification."Buffer Value4" < QtyToReshuffleThisLine then begin
                AdjustmentQty := QtyToReshuffleThisLine - TempTrackingSpecification."Buffer Value4";
                QtyToReshuffleThisLine := TempTrackingSpecification."Buffer Value4";
              end else
                AdjustmentQty := 0;

              xQty := QtyToReshuffleThisLine;
              QtyToReshuffleThisLine := MakeConnection(TempSupplyReservEntry,TempCurrDemandReservEntry,QtyToReshuffleThisLine);
              TempTrackingSpecification."Buffer Value4" -= (xQty - QtyToReshuffleThisLine);
              TempTrackingSpecification.MODIFY;
              QtyToReshuffleThisLine += AdjustmentQty;
            end;
          end else
            QtyToReshuffleThisLine := MakeConnection(TempSupplyReservEntry,TempCurrDemandReservEntry,QtyToReshuffleThisLine);

        until (TempSupplyReservEntry.NEXT = 0) or (QtyToReshuffleThisLine = 0);

      RemainingQty := QtyToReshuffle - xQtyToReshuffleThisLine + QtyToReshuffleThisLine;

      // Modify the original demand/supply entries

      NewQty := SupplyReservEntry."Quantity (Base)" - xQtyToReshuffleThisLine + QtyToReshuffleThisLine;
      if NewQty = 0 then begin
        TempReservEntryDelete := SupplyReservEntry;
        TempReservEntryDelete.INSERT;
        TempReservEntryDelete := TempCurrDemandReservEntry;
        TempReservEntryDelete.INSERT;
      end else begin
        TempReservEntryModify := SupplyReservEntry;
        TempReservEntryModify."Quantity (Base)" := NewQty;
        TempReservEntryModify.INSERT;
        TempReservEntryModify := TempCurrDemandReservEntry;
        TempReservEntryModify."Quantity (Base)" := -NewQty;
        TempReservEntryModify.INSERT;
      end;

      TempTrackingSpecification.RESET;
    END;

    LOCAL PROCEDURE MakeConnection@6(VAR SupplySurplusEntry@1000 : Record "Reservation Entry";VAR DemandReservEntry@1001 : Record "Reservation Entry";QtyToReshuffle@1002 : Decimal) RemainingQty : Decimal;
    VAR
      NewEntryNo@1003 : Integer;
    BEGIN
      if SupplySurplusEntry."Quantity (Base)" = 0 then
        exit(QtyToReshuffle);

      if SupplySurplusEntry."Quantity (Base)" <= QtyToReshuffle then begin
        // Convert supply surplus fully
        if SupplySurplusEntry."Entry No." < 0 then begin // Item Ledger Entry temporary record
          LastEntryNo := LastEntryNo + 1;
          NewEntryNo := -LastEntryNo;
        end else
          NewEntryNo := SupplySurplusEntry."Entry No.";

        TempReservEntryInsert := DemandReservEntry;
        TempReservEntryInsert."Entry No." := NewEntryNo;
        TempReservEntryInsert."Expected Receipt Date" := SupplySurplusEntry."Expected Receipt Date";
        TempReservEntryInsert."Quantity (Base)" := -SupplySurplusEntry."Quantity (Base)";
        TempReservEntryInsert.INSERT;

        TempReservEntryModify := SupplySurplusEntry;
        TempReservEntryModify."Entry No." := NewEntryNo;
        TempReservEntryModify."Reservation Status" := DemandReservEntry."Reservation Status";
        TempReservEntryModify."Shipment Date" := DemandReservEntry."Shipment Date";

        if SupplySurplusEntry."Entry No." < 0 then begin // Entry does not really exist
          TempReservEntryInsert := TempReservEntryModify;
          TempReservEntryInsert.INSERT;
        end else
          TempReservEntryModify.INSERT;

        RemainingQty := QtyToReshuffle - SupplySurplusEntry."Quantity (Base)";
        SupplySurplusEntry."Quantity (Base)" := 0;
        SupplySurplusEntry.MODIFY;
      end else begin
        if SupplySurplusEntry."Entry No." > 0 then begin
          TempReservEntryModify := SupplySurplusEntry;
          TempReservEntryModify."Quantity (Base)" -= QtyToReshuffle;
          TempReservEntryModify.INSERT;
        end;

        LastEntryNo := LastEntryNo + 1;
        NewEntryNo := -LastEntryNo;
        TempReservEntryInsert := SupplySurplusEntry;
        TempReservEntryInsert."Entry No." := NewEntryNo;
        TempReservEntryInsert."Reservation Status" := DemandReservEntry."Reservation Status";
        TempReservEntryInsert.VALIDATE("Quantity (Base)",QtyToReshuffle);
        TempReservEntryInsert."Shipment Date" := DemandReservEntry."Shipment Date";
        TempReservEntryInsert.INSERT;

        TempReservEntryInsert := DemandReservEntry;
        TempReservEntryInsert."Entry No." := NewEntryNo;
        TempReservEntryInsert."Expected Receipt Date" := SupplySurplusEntry."Expected Receipt Date";
        TempReservEntryInsert.VALIDATE("Quantity (Base)",-QtyToReshuffle);
        TempReservEntryInsert.INSERT;

        SupplySurplusEntry."Quantity (Base)" -= QtyToReshuffle;
        SupplySurplusEntry.MODIFY;
        RemainingQty := 0;
      end;
    END;

    LOCAL PROCEDURE WriteToDatabase@13();
    VAR
      ReservEntry@1000 : Record "Reservation Entry";
      PrevNegEntryNo@1001 : Integer;
      LastInsertedEntryNo@1002 : Integer;
    BEGIN
      TempReservEntryDelete.RESET;
      TempReservEntryModify.RESET;
      TempReservEntryInsert.RESET;

      if TempReservEntryDelete.FINDSET then
        repeat
          ReservEntry := TempReservEntryDelete;
          ReservEntry.DELETE;
        until TempReservEntryDelete.NEXT = 0;

      if TempReservEntryModify.FINDSET then
        repeat
          ReservEntry := TempReservEntryModify;
          ReservEntry.VALIDATE("Quantity (Base)");
          ReservEntry.MODIFY;
        until TempReservEntryModify.NEXT = 0;

      if TempReservEntryInsert.FINDSET then
        repeat
          ReservEntry := TempReservEntryInsert;
          if ReservEntry."Entry No." < 0 then
            if ReservEntry."Entry No." = PrevNegEntryNo then
              ReservEntry."Entry No." := LastInsertedEntryNo
            else begin
              PrevNegEntryNo := ReservEntry."Entry No.";
              ReservEntry."Entry No." := 0;
            end;
          ReservEntry.VALIDATE("Quantity (Base)");
          ReservEntry.UpdateItemTracking;
          ReservEntry.INSERT;
          LastInsertedEntryNo := ReservEntry."Entry No.";
        until TempReservEntryInsert.NEXT = 0;
    END;

    LOCAL PROCEDURE CalcInventory@7(VAR TempTrackingSpecification@1000 : TEMPORARY Record "Tracking Specification") : Boolean;
    VAR
      ItemLedgEntry@1006 : Record "Item Ledger Entry";
      ReservEntry@1002 : Record "Reservation Entry";
      TotalReservedQty@1001 : Decimal;
    BEGIN
      ReservEntry.LOCKTABLE;
      ReservEntry.SETCURRENTKEY("Item No.","Source Type","Source Subtype","Reservation Status","Location Code","Variant Code");
      ReservEntry.SETRANGE("Item No.",TempTrackingSpecification."Item No.");
      ReservEntry.SETRANGE("Variant Code",TempTrackingSpecification."Variant Code");
      ReservEntry.SETRANGE("Location Code",TempTrackingSpecification."Location Code");
      ReservEntry.SETRANGE("Source Type",DATABASE::"Item Ledger Entry");
      ReservEntry.SETRANGE("Source Subtype",0);
      ReservEntry.SETRANGE("Reservation Status",ReservEntry."Reservation Status"::Reservation);
      ReservEntry.SETFILTER("Item Tracking",'<> %1',ReservEntry."Item Tracking"::None);

      if ReservEntry.ISEMPTY then  // No reservations with Item Tracking exist against inventory - no basis for reallocation.
        exit(false);

      ItemLedgEntry.SETCURRENTKEY("Item No.",Open,"Variant Code",Positive,"Location Code");
      ItemLedgEntry.SETRANGE("Item No.",TempTrackingSpecification."Item No.");
      ItemLedgEntry.SETRANGE("Variant Code",TempTrackingSpecification."Variant Code");
      ItemLedgEntry.SETRANGE("Location Code",TempTrackingSpecification."Location Code");
      ItemLedgEntry.SETRANGE(Positive,true);
      ItemLedgEntry.SETRANGE(Open,true);

      if TempTrackingSpecification.FINDSET then
        repeat
          ItemLedgEntry.SETRANGE("Serial No.",TempTrackingSpecification."Serial No.");
          ItemLedgEntry.SETRANGE("Lot No.",TempTrackingSpecification."Lot No.");

          if ItemLedgEntry.FINDSET then
            repeat
              TempTrackingSpecification."Buffer Value2" += ItemLedgEntry."Remaining Quantity";
              ItemLedgEntry.CALCFIELDS("Reserved Quantity");
              TempTrackingSpecification."Buffer Value3" += ItemLedgEntry."Reserved Quantity";
              InsertTempSupplyReservEntry(ItemLedgEntry);
            until ItemLedgEntry.NEXT = 0;

          TempTrackingSpecification."Buffer Value4" :=
            TempTrackingSpecification."Buffer Value2" - // Total Inventory
            TempTrackingSpecification."Buffer Value3" + // Reserved Inventory
            TempTrackingSpecification."Buffer Value1";  // Non-allocated lot/sn demand (signed negatively)
          TempTrackingSpecification.MODIFY;
          TotalReservedQty += TempTrackingSpecification."Buffer Value3";
        until TempTrackingSpecification.NEXT = 0;

      if TotalReservedQty = 0 then
        exit(false); // No need to consider reallocation if no reservations exist.

      exit(true);
    END;

    LOCAL PROCEDURE CalcAllocations@11(VAR TempTrackingSpecification@1000 : TEMPORARY Record "Tracking Specification") : Boolean;
    VAR
      ReservEntry@1005 : Record "Reservation Entry";
      ReservEntry2@1001 : Record "Reservation Entry";
      QtyNeededForReallocation@1002 : Decimal;
    BEGIN
      ReservEntry.SETCURRENTKEY("Item No.","Source Type","Source Subtype","Reservation Status","Location Code","Variant Code");
      ReservEntry.SETRANGE("Item No.",TempTrackingSpecification."Item No.");
      ReservEntry.SETRANGE("Variant Code",TempTrackingSpecification."Variant Code");
      ReservEntry.SETRANGE("Location Code",TempTrackingSpecification."Location Code");
      ReservEntry.SETRANGE("Source Type",DATABASE::"Item Ledger Entry"); // (ILE)
      ReservEntry.SETRANGE("Source Subtype",0);
      ReservEntry.SETRANGE(Positive,true);
      ReservEntry.SETRANGE("Reservation Status",
        ReservEntry."Reservation Status"::Reservation,ReservEntry."Reservation Status"::Tracking);

      TempTrackingSpecification.SETFILTER("Buffer Value4",'< %1',0);

      if TempTrackingSpecification.FINDSET then
        repeat
          if TempTrackingSpecification."Serial No." <> '' then
            ReservEntry.SETRANGE("Serial No.",TempTrackingSpecification."Serial No.")
          else
            ReservEntry.SETRANGE("Serial No.");
          if TempTrackingSpecification."Lot No." <> '' then
            ReservEntry.SETRANGE("Lot No.",TempTrackingSpecification."Lot No.")
          else
            ReservEntry.SETRANGE("Lot No.");
          if ReservEntry.FINDSET(true,true) then
            repeat
              ReservEntry2.GET(ReservEntry."Entry No.",not ReservEntry.Positive); // Get demand
              if not ReservEntry2.TrackingExists then begin
                TempCurrSupplyReservEntry := ReservEntry;
                TempCurrSupplyReservEntry.INSERT;
                TempTrackingSpecification."Buffer Value5" += ReservEntry."Quantity (Base)";
                TempCurrDemandReservEntry := ReservEntry2;
                TempCurrDemandReservEntry.INSERT;
              end;
            until (ReservEntry.NEXT = 0) or
                  (TempTrackingSpecification."Buffer Value4" + TempTrackingSpecification."Buffer Value5" >= 0);
          if TempTrackingSpecification."Buffer Value4" + TempTrackingSpecification."Buffer Value5" < 0 then // Not sufficient qty
            exit(false);
          TempTrackingSpecification.MODIFY;
          QtyNeededForReallocation += ABS(TempTrackingSpecification."Buffer Value4");
        until TempTrackingSpecification.NEXT = 0;

      TempTrackingSpecification.SETFILTER("Buffer Value4",'>= %1',0);
      ReservEntry.SETRANGE("Reservation Status");

      // The quantity temporary records representing Item Ledger Entries are adjusted according to the
      // reservation entries actually existing in the database pointing at those entries. Otherwise these
      // would be counted twice.
      if TempTrackingSpecification.FINDSET then
        repeat
          if TempTrackingSpecification."Serial No." <> '' then
            ReservEntry.SETRANGE("Serial No.",TempTrackingSpecification."Serial No.")
          else
            ReservEntry.SETRANGE("Serial No.");
          if TempTrackingSpecification."Lot No." <> '' then
            ReservEntry.SETRANGE("Lot No.",TempTrackingSpecification."Lot No.")
          else
            ReservEntry.SETRANGE("Lot No.");

          if ReservEntry.FINDSET then
            repeat
              TempSupplyReservEntry.GET(-ReservEntry."Source Ref. No.",true);
              TempSupplyReservEntry."Quantity (Base)" -= ReservEntry."Quantity (Base)";
              if TempSupplyReservEntry."Quantity (Base)" = 0 then
                TempSupplyReservEntry.DELETE
              else
                TempSupplyReservEntry.MODIFY;

              if ReservEntry."Reservation Status" = ReservEntry."Reservation Status"::Surplus then begin
                TempSupplyReservEntry := ReservEntry;
                TempSupplyReservEntry.INSERT;
              end;
            until (ReservEntry.NEXT = 0);
          QtyNeededForReallocation -= TempTrackingSpecification."Buffer Value4";
        until (TempTrackingSpecification.NEXT = 0);
      exit(QtyNeededForReallocation <= 0);
    END;

    LOCAL PROCEDURE InsertTempSupplyReservEntry@8(ItemLedgEntry@1000 : Record "Item Ledger Entry");
    BEGIN
      TempSupplyReservEntry.INIT;
      TempSupplyReservEntry."Entry No." := -ItemLedgEntry."Entry No.";
      TempSupplyReservEntry.Positive := true;

      TempSupplyReservEntry."Source Type" := DATABASE::"Item Ledger Entry";
      TempSupplyReservEntry."Source Ref. No." := ItemLedgEntry."Entry No.";

      TempSupplyReservEntry."Item No." := ItemLedgEntry."Item No.";
      TempSupplyReservEntry."Variant Code" := ItemLedgEntry."Variant Code";
      TempSupplyReservEntry."Location Code" := ItemLedgEntry."Location Code";
      TempSupplyReservEntry."Qty. per Unit of Measure" := ItemLedgEntry."Qty. per Unit of Measure";
      TempSupplyReservEntry.Description := ItemLedgEntry.Description;
      TempSupplyReservEntry."Serial No." := ItemLedgEntry."Serial No.";
      TempSupplyReservEntry."Lot No." := ItemLedgEntry."Lot No.";
      TempSupplyReservEntry."Quantity (Base)" := ItemLedgEntry."Remaining Quantity";
      TempSupplyReservEntry."Reservation Status" := TempSupplyReservEntry."Reservation Status"::Surplus;
      TempSupplyReservEntry."Expected Receipt Date" := 0D;
      TempSupplyReservEntry."Shipment Date" := 0D;
      TempSupplyReservEntry.INSERT;
    END;

    [External]
    PROCEDURE NonspecificReservedQty@5(VAR ItemLedgEntry@1000 : Record "Item Ledger Entry") UnspecificQty : Decimal;
    VAR
      ReservEntry@1001 : Record "Reservation Entry";
      ReservEntry2@1002 : Record "Reservation Entry";
    BEGIN
      if not ItemLedgEntry.FINDSET then
        exit;

      ReservEntry.SETCURRENTKEY("Item No.","Source Type","Source Subtype","Reservation Status","Location Code","Variant Code");
      ReservEntry.SETRANGE("Item No.",ItemLedgEntry."Item No.");
      ReservEntry.SETRANGE("Variant Code",ItemLedgEntry."Variant Code");
      ReservEntry.SETRANGE("Location Code",ItemLedgEntry."Location Code");
      ReservEntry.SETRANGE("Source Type",DATABASE::"Item Ledger Entry");
      ReservEntry.SETRANGE("Source Subtype",0);
      ReservEntry.SETRANGE("Reservation Status",ReservEntry."Reservation Status"::Reservation);
      ReservEntry.SETRANGE(Positive,true);
      ItemLedgEntry.COPYFILTER("Serial No.",ReservEntry."Serial No.");
      ItemLedgEntry.COPYFILTER("Lot No.",ReservEntry."Lot No.");

      if not ReservEntry.FINDSET then
        exit;

      repeat
        ReservEntry2.GET(ReservEntry."Entry No.",not ReservEntry.Positive);  // Get demand
        if not ReservEntry2.TrackingExists then
          UnspecificQty -= ReservEntry2."Quantity (Base)"; // Sum up negative entries to a positive value
      until ReservEntry.NEXT = 0;
    END;

    [External]
    PROCEDURE ReleaseForReservation@14(ItemNo@1006 : Code[20];VariantCode@1005 : Code[10];LocationCode@1001 : Code[10];SerialNo@1004 : Code[20];LotNo@1002 : Code[20];QtyToRelease@1000 : Decimal) AllocationsChanged : Boolean;
    VAR
      TempTrackingSpecification@1003 : TEMPORARY Record "Tracking Specification";
    BEGIN
      // Local procedure used when doing item tracking specific reservations
      // "Buffer Value4" : Qty for reallocation (negative = need for reallocation)

      CleanUpVariables;
      TempTrackingSpecification."Item No." := ItemNo;
      TempTrackingSpecification."Variant Code" := VariantCode;
      TempTrackingSpecification."Location Code" := LocationCode;
      TempTrackingSpecification."Serial No." := SerialNo;
      TempTrackingSpecification."Lot No." := LotNo;
      TempTrackingSpecification."Quantity (Base)" := QtyToRelease;
      TempTrackingSpecification."Buffer Value4" := -QtyToRelease;
      TempTrackingSpecification.INSERT;

      PrepareTempDataSet(TempTrackingSpecification,QtyToRelease);
      CalcAllocations(TempTrackingSpecification);
      AllocationsChanged := Reallocate(TempTrackingSpecification);
      WriteToDatabase;
    END;

    [External]
    PROCEDURE ReleaseForReservation3@16(VAR CalcItemLedgEntry@1000 : Record "Item Ledger Entry";CalcReservEntry@1004 : Record "Reservation Entry";RemainingQtyToReserve@1003 : Decimal) AllocationsChanged : Boolean;
    VAR
      AvailableToReserve@1005 : Decimal;
    BEGIN
      // Used when doing item tracking specific reservations on reservation form.
      // "Buffer Value4" : Qty for reallocation (negative = need for reallocation)

      if CalcItemLedgEntry.FINDSET then
        repeat
          CalcItemLedgEntry.CALCFIELDS("Reserved Quantity");
          AvailableToReserve +=
            CalcItemLedgEntry."Remaining Quantity" - CalcItemLedgEntry."Reserved Quantity";
        until (CalcItemLedgEntry.NEXT = 0) or (AvailableToReserve >= RemainingQtyToReserve);

      if AvailableToReserve < RemainingQtyToReserve then
        AllocationsChanged := ReleaseForReservation(
            CalcReservEntry."Item No.",CalcReservEntry."Variant Code",CalcReservEntry."Location Code",
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",RemainingQtyToReserve - AvailableToReserve);
    END;

    [External]
    PROCEDURE ReserveItemTrackingLine@17(TrackingSpecification@1001 : Record "Tracking Specification");
    VAR
      ReservEntry@1003 : Record "Reservation Entry";
      QtyToReserveBase@1000 : Decimal;
      QtyToReserve@1002 : Decimal;
      UnreservedQty@1006 : Decimal;
      AvailabilityDate@1004 : Date;
    BEGIN
      // Used when fully reserving an item tracking line
      QtyToReserveBase := TrackingSpecification."Quantity (Base)" - TrackingSpecification."Quantity Handled (Base)";

      if QtyToReserveBase <= 0 then
        exit;

      ReservMgt.SetCalcReservEntry(TrackingSpecification,ReservEntry);

      if ReservEntry."Quantity (Base)" < 0 then
        AvailabilityDate := ReservEntry."Shipment Date"
      else
        AvailabilityDate := ReservEntry."Expected Receipt Date";

      UnreservedQty :=
        TrackingSpecification."Quantity (Base)" - TrackingSpecification."Quantity Handled (Base)";

      ReservEntry.SETCURRENTKEY(
        "Source ID","Source Ref. No.","Source Type","Source Subtype",
        "Source Batch Name","Source Prod. Order Line","Reservation Status");
      ReservEntry.SetPointerFilter;
      ReservEntry.SetTrackingFilter(ReservEntry."Serial No.",ReservEntry."Lot No.");
      ReservEntry.SETRANGE("Reservation Status",ReservEntry."Reservation Status"::Reservation);
      if ReservEntry.FINDSET then
        repeat
          UnreservedQty -= ABS(ReservEntry."Quantity (Base)");
        until ReservEntry.NEXT = 0;

      if QtyToReserveBase > UnreservedQty then
        QtyToReserveBase := UnreservedQty;

      ReservMgt.AutoReserveOneLine(1,QtyToReserve,QtyToReserveBase,'',AvailabilityDate);
    END;

    [External]
    PROCEDURE ReserveItemTrackingLine2@1(TrackingSpecification@1001 : Record "Tracking Specification";QtyToReserve@1002 : Decimal;QtyToReserveBase@1000 : Decimal);
    VAR
      ReservEntry@1003 : Record "Reservation Entry";
      AvailabilityDate@1004 : Date;
    BEGIN
      // Used when reserving a specific quantity on an item tracking line
      if QtyToReserveBase <= 0 then
        exit;

      ReservMgt.SetCalcReservEntry(TrackingSpecification,ReservEntry);

      if ReservEntry."Quantity (Base)" < 0 then
        AvailabilityDate := ReservEntry."Shipment Date"
      else
        AvailabilityDate := ReservEntry."Expected Receipt Date";

      ReservMgt.AutoReserveOneLine(1,QtyToReserve,QtyToReserveBase,'',AvailabilityDate);
    END;

    BEGIN
    END.
  }
}

