OBJECT Codeunit 399 Copy Tax Setup From Company
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Tax Groups        @1@@@@@@@@@@@\';
      Text001@1001 : TextConst 'ENU=Tax Jurisdictions @2@@@@@@@@@@@\';
      Text002@1002 : TextConst 'ENU=Tax Areas         @3@@@@@@@@@@@\';
      Text003@1003 : TextConst 'ENU=Tax Detail        @4@@@@@@@@@@@';
      Window@1004 : Dialog;
      CurrentRecord@1005 : Integer;
      RecordCount@1006 : Integer;

    [External]
    PROCEDURE CopyTaxInfo@5(SourceCompany@1000 : Record Company;CopyTable@1001 : ARRAY [4] OF Boolean);
    BEGIN
      Window.OPEN(Text000 +
        Text001 +
        Text002 +
        Text003);

      if CopyTable[1] then
        CopyTaxGroup(SourceCompany);

      if CopyTable[2] then
        CopyTaxJurisdiction(SourceCompany);

      if CopyTable[3] then
        CopyTaxArea(SourceCompany);

      if CopyTable[4] then
        CopyTaxDetail(SourceCompany);

      Window.CLOSE;
    END;

    LOCAL PROCEDURE CopyTaxJurisdiction@1(SourceCompany@1000 : Record Company);
    VAR
      TaxJurisdiction@1001 : Record "Tax Jurisdiction";
      SourceTaxJurisdiction@1002 : Record "Tax Jurisdiction";
    BEGIN
      SourceTaxJurisdiction.CHANGECOMPANY(SourceCompany.Name);
      if not SourceTaxJurisdiction.FIND('-') then
        exit;
      RecordCount := SourceTaxJurisdiction.COUNT;
      CurrentRecord := 0;
      repeat
        CurrentRecord := CurrentRecord + 1;
        Window.UPDATE(2,ROUND(CurrentRecord / RecordCount * 10000,1));
        TaxJurisdiction.INIT;
        TaxJurisdiction.TRANSFERFIELDS(SourceTaxJurisdiction);
        if TaxJurisdiction.INSERT then;
      until SourceTaxJurisdiction.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyTaxGroup@2(SourceCompany@1000 : Record Company);
    VAR
      SourceTaxGroup@1001 : Record "Tax Group";
      TaxGroup@1002 : Record "Tax Group";
    BEGIN
      SourceTaxGroup.CHANGECOMPANY(SourceCompany.Name);
      if not SourceTaxGroup.FIND('-') then
        exit;
      RecordCount := SourceTaxGroup.COUNT;
      CurrentRecord := 0;
      repeat
        CurrentRecord := CurrentRecord + 1;
        Window.UPDATE(1,ROUND(CurrentRecord / RecordCount * 10000,1));
        TaxGroup.INIT;
        TaxGroup.TRANSFERFIELDS(SourceTaxGroup);
        if TaxGroup.INSERT then;
      until SourceTaxGroup.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyTaxArea@3(SourceCompany@1000 : Record Company);
    VAR
      TaxAreaLine@1001 : Record "Tax Area Line";
      TaxArea@1002 : Record "Tax Area";
      SourceTaxAreaLine@1003 : Record "Tax Area Line";
      SourceTaxArea@1004 : Record "Tax Area";
    BEGIN
      SourceTaxArea.CHANGECOMPANY(SourceCompany.Name);
      SourceTaxAreaLine.CHANGECOMPANY(SourceCompany.Name);
      if not SourceTaxArea.FIND('-') then
        exit;
      RecordCount := SourceTaxArea.COUNT;
      CurrentRecord := 0;
      repeat
        CurrentRecord := CurrentRecord + 1;
        Window.UPDATE(3,ROUND(CurrentRecord / RecordCount * 10000,1));
        TaxArea.INIT;
        TaxArea.TRANSFERFIELDS(SourceTaxArea);
        if TaxArea.INSERT then;
        SourceTaxAreaLine.SETRANGE("Tax Area",SourceTaxArea.Code);
        if SourceTaxAreaLine.FIND('-') then
          repeat
            TaxAreaLine.INIT;
            TaxAreaLine.TRANSFERFIELDS(SourceTaxAreaLine);
            if TaxAreaLine.INSERT then;
          until SourceTaxAreaLine.NEXT = 0;
      until SourceTaxArea.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyTaxDetail@4(SourceCompany@1000 : Record Company);
    VAR
      SourceTaxDetail@1001 : Record "Tax Detail";
      TaxDetail@1002 : Record "Tax Detail";
    BEGIN
      SourceTaxDetail.CHANGECOMPANY(SourceCompany.Name);
      if not SourceTaxDetail.FIND('-') then
        exit;
      RecordCount := SourceTaxDetail.COUNT;
      CurrentRecord := 0;
      repeat
        CurrentRecord := CurrentRecord + 1;
        Window.UPDATE(4,ROUND(CurrentRecord / RecordCount * 10000,1));
        TaxDetail.INIT;
        TaxDetail.TRANSFERFIELDS(SourceTaxDetail);
        if TaxDetail.INSERT then;
      until SourceTaxDetail.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

