OBJECT Table 1470 Product Video Buffer
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Product Video Buffer;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;AutoIncrement=true;
                                                   CaptionML=ENU=ID;
                                                   Editable=false }
    { 2   ;   ;Title               ;Text250       ;TableRelation="Assisted Setup".Name;
                                                   CaptionML=ENU=Title }
    { 3   ;   ;Video Url           ;Text250       ;TableRelation="Assisted Setup"."Video Url";
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Video Url }
    { 4   ;   ;Assisted Setup ID   ;Integer       ;CaptionML=ENU=Assisted Setup ID }
    { 5   ;   ;Indentation         ;Integer       ;CaptionML=ENU=Indentation }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EntryNo@1001 : Integer;

    PROCEDURE InitBuffer@4(VAR TempProductVideoBuffer@1000 : TEMPORARY Record "Product Video Buffer";Category@1003 : Option);
    BEGIN
      TempProductVideoBuffer.DELETEALL;

      InitVideoTree(TempProductVideoBuffer,Category);
      TempProductVideoBuffer.SETCURRENTKEY(ID);
      if TempProductVideoBuffer.FINDFIRST then;
    END;

    LOCAL PROCEDURE InitVideoTree@7(VAR TempProductVideoBuffer@1000 : TEMPORARY Record "Product Video Buffer";Category@1003 : Option);
    VAR
      ProductVideoCategory@1001 : Record "Product Video Category";
    BEGIN
      case Category of
        ProductVideoCategory.Category::" ":
          AddAllVideos(TempProductVideoBuffer);
        else
          AddVideosToCategory(TempProductVideoBuffer,Category);
      end;
    END;

    LOCAL PROCEDURE AddAllVideos@3(VAR TempProductVideoBuffer@1000 : TEMPORARY Record "Product Video Buffer");
    VAR
      ProductVideoCategory@1006 : Record "Product Video Category";
      TypeHelper@1005 : Codeunit "Type Helper";
      RecRef@1001 : RecordRef;
      FieldRef@1002 : FieldRef;
      Index@1004 : Integer;
    BEGIN
      RecRef.OPEN(DATABASE::"Product Video Category");
      FieldRef := RecRef.FIELD(ProductVideoCategory.FIELDNO(Category));

      for Index := 1 to TypeHelper.GetNumberOfOptions(FieldRef.OPTIONSTRING) do
        InitVideoTree(TempProductVideoBuffer,Index);
    END;

    LOCAL PROCEDURE AddVideosToCategory@2(VAR TempProductVideoBuffer@1000 : TEMPORARY Record "Product Video Buffer";Category@1003 : Option);
    VAR
      ProductVideoswithCategory@1004 : Query "Product Videos with Category";
    BEGIN
      ProductVideoswithCategory.SETRANGE(Category,Category);
      ProductVideoswithCategory.OPEN;
      if ProductVideoswithCategory.READ then begin
        AddCategory(TempProductVideoBuffer,FORMAT(ProductVideoswithCategory.Category));
        repeat
          if ProductVideoswithCategory.Alternate_Title <> '' then
            AddVideoToCategory(TempProductVideoBuffer,ProductVideoswithCategory.Assisted_Setup_ID,
              ProductVideoswithCategory.Alternate_Title,ProductVideoswithCategory.Video_Url)
          else
            AddVideoToCategory(TempProductVideoBuffer,ProductVideoswithCategory.Assisted_Setup_ID,
              ProductVideoswithCategory.Name,ProductVideoswithCategory.Video_Url);
        until ProductVideoswithCategory.READ = false;
      end;
      ProductVideoswithCategory.CLOSE;
    END;

    LOCAL PROCEDURE AddCategory@1(VAR TempProductVideoBuffer@1002 : TEMPORARY Record "Product Video Buffer";CategoryName@1004 : Text[250]);
    BEGIN
      InsertRec(TempProductVideoBuffer,0,CategoryName,'',0);
    END;

    LOCAL PROCEDURE AddVideoToCategory@5(VAR TempProductVideoBuffer@1002 : TEMPORARY Record "Product Video Buffer";Id@1004 : Integer;VideoName@1000 : Text[250];VideoUrl@1001 : Text[250]);
    BEGIN
      InsertRec(TempProductVideoBuffer,Id,VideoName,VideoUrl,1);
    END;

    LOCAL PROCEDURE InsertRec@6(VAR TempProductVideoBuffer@1003 : TEMPORARY Record "Product Video Buffer";Id@1004 : Integer;VideoName@1000 : Text[250];VideoUrl@1001 : Text[250];Indent@1002 : Integer);
    BEGIN
      EntryNo := EntryNo + 1;
      TempProductVideoBuffer.INIT;
      TempProductVideoBuffer.ID := EntryNo;
      TempProductVideoBuffer.Title := VideoName;
      TempProductVideoBuffer."Video Url" := VideoUrl;
      TempProductVideoBuffer."Assisted Setup ID" := Id;
      TempProductVideoBuffer.Indentation := Indent;
      TempProductVideoBuffer.INSERT;
    END;

    BEGIN
    END.
  }
}

