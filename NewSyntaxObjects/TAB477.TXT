OBJECT Table 477 Report Inbox
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Report Inbox;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=true;
                                                   CaptionML=ENU=Entry No. }
    { 2   ;   ;User ID             ;Text65        ;CaptionML=ENU=User ID;
                                                   Editable=false }
    { 3   ;   ;Report Output       ;BLOB          ;CaptionML=ENU=Report Output }
    { 4   ;   ;Created Date-Time   ;DateTime      ;CaptionML=ENU=Created Date-Time;
                                                   Editable=false }
    { 5   ;   ;Job Queue Log Entry ID;GUID        ;CaptionML=ENU=Job Queue Log Entry ID;
                                                   Editable=false }
    { 6   ;   ;Output Type         ;Option        ;CaptionML=ENU=Output Type;
                                                   OptionCaptionML=ENU=PDF,Word,Excel;
                                                   OptionString=PDF,Word,Excel;
                                                   Editable=false }
    { 7   ;   ;Description         ;Text250       ;CaptionML=ENU=Description;
                                                   Editable=false }
    { 8   ;   ;Report ID           ;Integer       ;CaptionML=ENU=Report ID;
                                                   Editable=false }
    { 9   ;   ;Report Name         ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Report ID")));
                                                   CaptionML=ENU=Report Name;
                                                   Editable=false }
    { 10  ;   ;Read                ;Boolean       ;CaptionML=ENU=Read }
  }
  KEYS
  {
    {    ;"Entry No."                             ;Clustered=true }
    {    ;"User ID","Created Date-Time"            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FileDownLoadTxt@1000 : TextConst 'ENU=Export';
      ReportIsEmptyMsg@1001 : TextConst 'ENU=The report is empty.';
      NoReportsToShowErr@1002 : TextConst 'ENU=There are no reports in the list.';

    [External]
    PROCEDURE ShowReport@1();
    VAR
      Instr@1003 : InStream;
      Downloaded@1002 : Boolean;
      FileName@1004 : Text;
    BEGIN
      if "Entry No." = 0 then
        ERROR(NoReportsToShowErr);

      CALCFIELDS("Report Output","Report Name");
      if not "Report Output".HASVALUE then begin
        Read := true;
        MODIFY;
        COMMIT;
        MESSAGE(ReportIsEmptyMsg);
        exit;
      end;
      FileName := DELCHR("Report Name",'=','\/:*?"<>|') + Suffix;
      "Report Output".CREATEINSTREAM(Instr);
      Downloaded := DOWNLOADFROMSTREAM(Instr,FileDownLoadTxt,'','',FileName);

      if not Read and Downloaded then begin
        Read := true;
        MODIFY;
      end;
    END;

    LOCAL PROCEDURE Suffix@5() : Text;
    BEGIN
      case "Output Type" of
        "Output Type"::PDF:
          exit('.pdf');
        "Output Type"::Word:
          exit('.docx');
        "Output Type"::Excel:
          exit('.xlsx');
      end;
    END;

    BEGIN
    END.
  }
}

