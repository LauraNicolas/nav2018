OBJECT Codeunit 7181 Purchases Info-Pane Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Item@1001 : Record Item;
      PurchHeader@1003 : Record "Purchase Header";
      PurchPriceCalcMgt@1002 : Codeunit "Purch. Price Calc. Mgt.";

    [External]
    PROCEDURE CalcAvailability@18(VAR PurchLine@1000 : Record "Purchase Line") : Decimal;
    VAR
      AvailableToPromise@1001 : Codeunit "Available to Promise";
      GrossRequirement@1002 : Decimal;
      ScheduledReceipt@1003 : Decimal;
      PeriodType@1006 : 'Day,Week,Month,Quarter,Year';
      AvailabilityDate@1004 : Date;
      LookaheadDateformula@1005 : DateFormula;
    BEGIN
      if GetItem(PurchLine) then begin
        if PurchLine."Expected Receipt Date" <> 0D then
          AvailabilityDate := PurchLine."Expected Receipt Date"
        else
          AvailabilityDate := WORKDATE;

        Item.RESET;
        Item.SETRANGE("Date Filter",0D,AvailabilityDate);
        Item.SETRANGE("Variant Filter",PurchLine."Variant Code");
        Item.SETRANGE("Location Filter",PurchLine."Location Code");
        Item.SETRANGE("Drop Shipment Filter",false);

        exit(
          AvailableToPromise.QtyAvailabletoPromise(
            Item,
            GrossRequirement,
            ScheduledReceipt,
            AvailabilityDate,
            PeriodType,
            LookaheadDateformula));
      end;
    END;

    [External]
    PROCEDURE CalcNoOfPurchasePrices@19(VAR PurchLine@1102628000 : Record "Purchase Line") : Integer;
    BEGIN
      if GetItem(PurchLine) then begin
        GetPurchHeader(PurchLine);
        exit(PurchPriceCalcMgt.NoOfPurchLinePrice(PurchHeader,PurchLine,true));
      end;
    END;

    [External]
    PROCEDURE CalcNoOfPurchLineDisc@22(VAR PurchLine@1102628000 : Record "Purchase Line") : Integer;
    BEGIN
      if GetItem(PurchLine) then begin
        GetPurchHeader(PurchLine);
        exit(PurchPriceCalcMgt.NoOfPurchLineLineDisc(PurchHeader,PurchLine,true));
      end;
    END;

    LOCAL PROCEDURE GetItem@24(VAR PurchLine@1000 : Record "Purchase Line") : Boolean;
    BEGIN
      with Item do begin
        if (PurchLine.Type <> PurchLine.Type::Item) or (PurchLine."No." = '') then
          exit(false);

        if PurchLine."No." <> "No." then
          GET(PurchLine."No.");
        exit(true);
      end;
    END;

    LOCAL PROCEDURE GetPurchHeader@13(PurchLine@1000 : Record "Purchase Line");
    BEGIN
      if (PurchLine."Document Type" <> PurchHeader."Document Type") or
         (PurchLine."Document No." <> PurchHeader."No.")
      then
        PurchHeader.GET(PurchLine."Document Type",PurchLine."Document No.");
    END;

    BEGIN
    END.
  }
}

