OBJECT Codeunit 7 GLBudget-Open
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="G/L Account";
    OnRun=BEGIN
            if GETFILTER("Budget Filter") = '' then
              SearchForName := true
            else begin
              GLBudgetName.SETFILTER(Name,GETFILTER("Budget Filter"));
              SearchForName := not GLBudgetName.FINDFIRST;
              GLBudgetName.SETRANGE(Name);
            end;
            if SearchForName then begin
              if not GLBudgetName.FINDFIRST then begin
                GLBudgetName.INIT;
                GLBudgetName.Name := Text000;
                GLBudgetName.Description := Text001;
                GLBudgetName.INSERT;
              end;
              SETFILTER("Budget Filter",GLBudgetName.Name);
            end;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=DEFAULT';
      Text001@1001 : TextConst 'ENU=Default Budget';
      GLBudgetName@1002 : Record "G/L Budget Name";
      SearchForName@1003 : Boolean;

    PROCEDURE SetupFiltersOnGLAccBudgetPage@43(VAR GlobalDim1Filter@1001 : Text;VAR GlobalDim2Filter@1002 : Text;VAR GlobalDim1FilterEnable@1003 : Boolean;VAR GlobalDim2FilterEnable@1004 : Boolean;VAR PeriodType@1006 : 'Day,Week,Month,Quarter,Year,"Accounting Period"';VAR DateFilter@1007 : Text;VAR GLAccount@1000 : Record "G/L Account");
    VAR
      GLSetup@1005 : Record "General Ledger Setup";
    BEGIN
      with GLAccount do begin
        GlobalDim1Filter := GETFILTER("Global Dimension 1 Filter");
        GlobalDim2Filter := GETFILTER("Global Dimension 2 Filter");
        GLSetup.GET;
        GlobalDim1FilterEnable :=
          (GLSetup."Global Dimension 1 Code" <> '') and
          (GlobalDim1Filter = '');
        GlobalDim2FilterEnable :=
          (GLSetup."Global Dimension 2 Code" <> '') and
          (GlobalDim2Filter = '');
        PeriodType := PeriodType::Month;
        DateFilter := GETFILTER("Date Filter");
        if DateFilter = '' then begin
          DateFilter := FORMAT(CALCDATE('<-CY>',TODAY)) + '..' + FORMAT(CALCDATE('<CY>',TODAY));
          SETFILTER("Date Filter",DateFilter);
        end;
      end;
    END;

    BEGIN
    END.
  }
}

