OBJECT Codeunit 358 DateFilter-Calc
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Fiscal Year %1';
      AccountingPeriod@1001 : Record "Accounting Period";
      StartDate@1002 : Date;

    [External]
    PROCEDURE CreateFiscalYearFilter@3(VAR Filter@1000 : Text[30];VAR Name@1001 : Text[30];Date@1002 : Date;NextStep@1003 : Integer);
    BEGIN
      CreateAccountingDateFilter(Filter,Name,true,Date,NextStep);
    END;

    [External]
    PROCEDURE CreateAccountingPeriodFilter@2(VAR Filter@1000 : Text[30];VAR Name@1001 : Text[30];Date@1002 : Date;NextStep@1003 : Integer);
    BEGIN
      CreateAccountingDateFilter(Filter,Name,false,Date,NextStep);
    END;

    [External]
    PROCEDURE ConvertToUtcDateTime@5(LocalDateTime@1000 : DateTime) : DateTime;
    VAR
      DotNetDateTimeOffset@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.DateTimeOffset";
      DotNetDateTimeOffsetNow@1001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.DateTimeOffset";
    BEGIN
      if LocalDateTime = CREATEDATETIME(0D,000000T) then
        exit(CREATEDATETIME(0D,000000T));

      DotNetDateTimeOffset := DotNetDateTimeOffset.DateTimeOffset(LocalDateTime);
      DotNetDateTimeOffsetNow := DotNetDateTimeOffset.Now;
      exit(DotNetDateTimeOffset.LocalDateTime - DotNetDateTimeOffsetNow.Offset);
    END;

    LOCAL PROCEDURE CreateAccountingDateFilter@1(VAR Filter@1000 : Text[30];VAR Name@1001 : Text[30];FiscalYear@1002 : Boolean;Date@1003 : Date;NextStep@1004 : Integer);
    BEGIN
      AccountingPeriod.RESET;
      if FiscalYear then
        AccountingPeriod.SETRANGE("New Fiscal Year",true);
      AccountingPeriod."Starting Date" := Date;
      AccountingPeriod.FIND('=<>');
      if AccountingPeriod."Starting Date" > Date then
        NextStep := NextStep - 1;
      if NextStep <> 0 then
        if AccountingPeriod.NEXT(NextStep) <> NextStep then begin
          if NextStep < 0 then
            Filter := '..' + FORMAT(AccountingPeriod."Starting Date" - 1)
          else
            Filter := FORMAT(AccountingPeriod."Starting Date") + '..' + FORMAT(DMY2DATE(31,12,9999));
          Name := '...';
          exit;
        end;
      StartDate := AccountingPeriod."Starting Date";
      if FiscalYear then
        Name := STRSUBSTNO(Text000,FORMAT(DATE2DMY(StartDate,3)))
      else
        Name := AccountingPeriod.Name;
      if AccountingPeriod.NEXT <> 0 then
        Filter := FORMAT(StartDate) + '..' + FORMAT(AccountingPeriod."Starting Date" - 1)
      else begin
        Filter := FORMAT(StartDate) + '..' + FORMAT(DMY2DATE(31,12,9999));
        Name := Name + '...';
      end;
    END;

    BEGIN
    END.
  }
}

