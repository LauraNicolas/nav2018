OBJECT Table 5620 FA Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               FAJnlTemplate.GET("Journal Template Name");
             END;

    OnDelete=BEGIN
               FAJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               FAJnlLine.SETRANGE("Journal Batch Name",Name);
               FAJnlLine.DELETEALL(true);
             END;

    OnRename=BEGIN
               FAJnlLine.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               FAJnlLine.SETRANGE("Journal Batch Name",xRec.Name);
               while FAJnlLine.FINDFIRST do
                 FAJnlLine.RENAME("Journal Template Name",Name,FAJnlLine."Line No.");
             END;

    CaptionML=ENU=FA Journal Batch;
    LookupPageID="FA Journal Batches";
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="FA Journal Template";
                                                   CaptionML=ENU=Journal Template Name;
                                                   NotBlank=true }
    { 2   ;   ;Name                ;Code10        ;CaptionML=ENU=Name;
                                                   NotBlank=true }
    { 3   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                if "Reason Code" <> xRec."Reason Code" then begin
                                                                  FAJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  FAJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                  FAJnlLine.MODIFYALL("Reason Code","Reason Code");
                                                                  MODIFY;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Reason Code }
    { 5   ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if "No. Series" <> '' then begin
                                                                  FAJnlTemplate.GET("Journal Template Name");
                                                                  if FAJnlTemplate.Recurring then
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  if "No. Series" = "Posting No. Series" then
                                                                    VALIDATE("Posting No. Series",'');
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No. Series }
    { 6   ;   ;Posting No. Series  ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if ("Posting No. Series" = "No. Series") and ("Posting No. Series" <> '') then
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                                FAJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                FAJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                FAJnlLine.MODIFYALL("Posting No. Series","Posting No. Series");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Posting No. Series }
    { 22  ;   ;Recurring           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("FA Journal Template".Recurring WHERE (Name=FIELD("Journal Template Name")));
                                                   CaptionML=ENU=Recurring;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Journal Template Name",Name            ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'ENU=must not be %1';
      FAJnlTemplate@1002 : Record "FA Journal Template";
      FAJnlLine@1003 : Record "FA Journal Line";

    [External]
    PROCEDURE SetupNewBatch@3();
    BEGIN
      FAJnlTemplate.GET("Journal Template Name");
      "No. Series" := FAJnlTemplate."No. Series";
      "Posting No. Series" := FAJnlTemplate."Posting No. Series";
      "Reason Code" := FAJnlTemplate."Reason Code";
    END;

    BEGIN
    END.
  }
}

