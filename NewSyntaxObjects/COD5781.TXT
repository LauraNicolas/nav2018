OBJECT Codeunit 5781 Whse. Validate Source Header
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE SalesHeaderVerifyChange@1(VAR NewSalesHeader@1000 : Record "Sales Header";VAR OldSalesHeader@1001 : Record "Sales Header");
    VAR
      SalesLine@1002 : Record "Sales Line";
    BEGIN
      with NewSalesHeader do begin
        if "Shipping Advice" = OldSalesHeader."Shipping Advice" then
          exit;

        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type",OldSalesHeader."Document Type");
        SalesLine.SETRANGE("Document No.",OldSalesHeader."No.");
        if SalesLine.FINDSET then
          repeat
            ChangeWhseLines(
              DATABASE::"Sales Line",
              SalesLine."Document Type",
              SalesLine."Document No.",
              SalesLine."Line No.",
              0,
              "Shipping Advice");
          until SalesLine.NEXT = 0;
      end;
    END;

    [External]
    PROCEDURE ServiceHeaderVerifyChange@3(VAR NewServiceHeader@1000 : Record "Service Header";VAR OldServiceHeader@1001 : Record "Service Header");
    VAR
      ServiceLine@1002 : Record "Service Line";
    BEGIN
      with NewServiceHeader do begin
        if "Shipping Advice" = OldServiceHeader."Shipping Advice" then
          exit;

        ServiceLine.RESET;
        ServiceLine.SETRANGE("Document Type",OldServiceHeader."Document Type");
        ServiceLine.SETRANGE("Document No.",OldServiceHeader."No.");
        if ServiceLine.FIND('-') then
          repeat
            ChangeWhseLines(
              DATABASE::"Service Line",
              ServiceLine."Document Type",
              ServiceLine."Document No.",
              ServiceLine."Line No.",
              0,
              "Shipping Advice");
          until ServiceLine.NEXT = 0;
      end;
    END;

    [External]
    PROCEDURE TransHeaderVerifyChange@2(VAR NewTransHeader@1000 : Record "Transfer Header";VAR OldTransHeader@1001 : Record "Transfer Header");
    VAR
      TransLine@1002 : Record "Transfer Line";
    BEGIN
      with NewTransHeader do begin
        if "Shipping Advice" = OldTransHeader."Shipping Advice" then
          exit;

        TransLine.RESET;
        TransLine.SETRANGE("Document No.",OldTransHeader."No.");
        if TransLine.FIND('-') then
          repeat
            ChangeWhseLines(
              DATABASE::"Transfer Line",
              0,// Outbound Transfer
              TransLine."Document No.",
              TransLine."Line No.",
              0,
              "Shipping Advice");
          until TransLine.NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE ChangeWhseLines@6(SourceType@1000 : Integer;SourceSubType@1001 : Option;SourceNo@1002 : Code[20];SourceLineNo@1003 : Integer;SourceSublineNo@1004 : Integer;ShipAdvice@1005 : Integer);
    VAR
      WhseActivLine@1010 : Record "Warehouse Activity Line";
      WhseShptLine@1008 : Record "Warehouse Shipment Line";
      WhseWkshLine@1007 : Record "Whse. Worksheet Line";
    BEGIN
      WhseShptLine.RESET;
      WhseShptLine.SetSourceFilter(SourceType,SourceSubType,SourceNo,SourceLineNo,false);
      WhseShptLine.MODIFYALL("Shipping Advice",ShipAdvice);

      WhseActivLine.RESET;
      WhseActivLine.SetSourceFilter(SourceType,SourceSubType,SourceNo,SourceLineNo,SourceSublineNo,false);
      WhseActivLine.MODIFYALL("Shipping Advice",ShipAdvice);

      WhseWkshLine.RESET;
      WhseWkshLine.SetSourceFilter(SourceType,SourceSubType,SourceNo,SourceLineNo,false);
      WhseWkshLine.MODIFYALL("Shipping Advice",ShipAdvice);
    END;

    BEGIN
    END.
  }
}

