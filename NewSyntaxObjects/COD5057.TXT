OBJECT Codeunit 5057 VendCont-Update
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      RMSetup@1000 : Record "Marketing Setup";

    [External]
    PROCEDURE OnInsert@3(VAR Vend@1000 : Record Vendor);
    BEGIN
      RMSetup.GET;
      if RMSetup."Bus. Rel. Code for Vendors" = '' then
        exit;

      InsertNewContact(Vend,true);
    END;

    [External]
    PROCEDURE OnModify@1(VAR Vend@1000 : Record Vendor);
    VAR
      Cont@1002 : Record Contact;
      OldCont@1001 : Record Contact;
      ContBusRel@1006 : Record "Contact Business Relation";
      ContNo@1004 : Code[20];
      NoSeries@1003 : Code[20];
      SalespersonCode@1005 : Code[20];
    BEGIN
      with ContBusRel do begin
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::Vendor);
        SETRANGE("No.",Vend."No.");
        if not FINDFIRST then
          exit;
        Cont.GET("Contact No.");
        OldCont := Cont;
      end;

      ContNo := Cont."No.";
      NoSeries := Cont."No. Series";
      SalespersonCode := Cont."Salesperson Code";
      Cont.VALIDATE("E-Mail",Vend."E-Mail");
      Cont.TRANSFERFIELDS(Vend);
      Cont."No." := ContNo ;
      Cont."No. Series" := NoSeries;
      Cont."Salesperson Code" := SalespersonCode;
      Cont.VALIDATE(Name);
      Cont.OnModify(OldCont);
      Cont.MODIFY(true);

      Vend.GET(Vend."No.");
    END;

    [External]
    PROCEDURE OnDelete@2(VAR Vend@1000 : Record Vendor);
    VAR
      ContBusRel@1001 : Record "Contact Business Relation";
    BEGIN
      with ContBusRel do begin
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::Vendor);
        SETRANGE("No.",Vend."No.");
        DELETEALL(true);
      end;
    END;

    [External]
    PROCEDURE InsertNewContact@4(VAR Vend@1000 : Record Vendor;LocalCall@1001 : Boolean);
    VAR
      Cont@1002 : Record Contact;
      ContBusRel@1004 : Record "Contact Business Relation";
      NoSeriesMgt@1003 : Codeunit NoSeriesManagement;
    BEGIN
      if not LocalCall then begin
        RMSetup.GET;
        RMSetup.TESTFIELD("Bus. Rel. Code for Vendors");
      end;

      with Cont do begin
        INIT;
        TRANSFERFIELDS(Vend);
        VALIDATE(Name);
        VALIDATE("E-Mail");
        "No." := '';
        "No. Series" := '';
        RMSetup.TESTFIELD("Contact Nos.");
        NoSeriesMgt.InitSeries(RMSetup."Contact Nos.",'',0D,"No.","No. Series");
        Type := Type::Company;
        TypeChange;
        SetSkipDefault;
        INSERT(true);
      end;

      with ContBusRel do begin
        INIT;
        "Contact No." := Cont."No.";
        "Business Relation Code" := RMSetup."Bus. Rel. Code for Vendors";
        "Link to Table" := "Link to Table"::Vendor;
        "No." := Vend."No.";
        INSERT(true);
      end;
    END;

    [Internal]
    PROCEDURE InsertNewContactPerson@5(VAR Vend@1001 : Record Vendor;LocalCall@1003 : Boolean);
    VAR
      Cont@1004 : Record Contact;
      ContComp@1000 : Record Contact;
      ContBusRel@1002 : Record "Contact Business Relation";
    BEGIN
      if not LocalCall then begin
        RMSetup.GET;
        RMSetup.TESTFIELD("Bus. Rel. Code for Vendors");
      end;

      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Vendor);
      ContBusRel.SETRANGE("No.",Vend."No.");
      if ContBusRel.FINDFIRST then
        if ContComp.GET(ContBusRel."Contact No.") then
          with Cont do begin
            INIT;
            "No." := '';
            INSERT(true);
            "Company No." := ContComp."No.";
            Type := Type::Person;
            VALIDATE(Name,Vend.Contact);
            InheritCompanyToPersonData(ContComp);
            MODIFY(true);
            Vend."Primary Contact No." := "No.";
          end
    END;

    BEGIN
    END.
  }
}

