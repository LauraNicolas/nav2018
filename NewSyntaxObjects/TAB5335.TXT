OBJECT Table 5335 Integration Table Mapping
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=VAR
               CRMOptionMapping@1001 : Record "CRM Option Mapping";
               IntegrationFieldMapping@1000 : Record "Integration Field Mapping";
             BEGIN
               IntegrationFieldMapping.SETRANGE("Integration Table Mapping Name",Name);
               IntegrationFieldMapping.DELETEALL;

               CRMOptionMapping.SETRANGE("Table ID","Table ID");
               CRMOptionMapping.SETRANGE("Integration Table ID","Integration Table ID");
               CRMOptionMapping.SETRANGE("Integration Field ID","Integration Table UID Fld. No.");
               CRMOptionMapping.DELETEALL;
             END;

    CaptionML=ENU=Integration Table Mapping;
    LookupPageID="Integration Table Mapping List";
    DrillDownPageID="Integration Table Mapping List";
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code20        ;CaptionML=ENU=Name }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation="Table Metadata".ID;
                                                   CaptionML=ENU=Table ID }
    { 3   ;   ;Integration Table ID;Integer       ;TableRelation="Table Metadata".ID;
                                                   CaptionML=ENU=Integration Table ID }
    { 4   ;   ;Synch. Codeunit ID  ;Integer       ;TableRelation="Table Metadata".ID;
                                                   CaptionML=ENU=Synch. Codeunit ID }
    { 5   ;   ;Integration Table UID Fld. No.;Integer;
                                                   OnValidate=VAR
                                                                Field@1000 : Record Field;
                                                              BEGIN
                                                                Field.GET("Integration Table ID","Integration Table UID Fld. No.");
                                                                "Int. Table UID Field Type" := Field.Type;
                                                              END;

                                                   CaptionML=ENU=Integration Table UID Fld. No.;
                                                   Description=Integration Table Unique Identifier Field No. }
    { 6   ;   ;Int. Tbl. Modified On Fld. No.;Integer;
                                                   CaptionML=ENU=Int. Tbl. Modified On Fld. No.;
                                                   Description=Integration Table Modified On Field No. }
    { 7   ;   ;Int. Table UID Field Type;Integer  ;CaptionML=ENU=Int. Table UID Field Type;
                                                   Editable=false }
    { 8   ;   ;Table Config Template Code;Code10  ;TableRelation="Config. Template Header".Code WHERE ("Table ID"=FIELD("Table ID"));
                                                   CaptionML=ENU=Table Config Template Code }
    { 9   ;   ;Int. Tbl. Config Template Code;Code10;
                                                   TableRelation="Config. Template Header".Code WHERE ("Table ID"=FIELD("Integration Table ID"));
                                                   CaptionML=ENU=Int. Tbl. Config Template Code }
    { 10  ;   ;Direction           ;Option        ;CaptionML=ENU=Direction;
                                                   OptionCaptionML=ENU=Bidirectional,ToIntegrationTable,FromIntegrationTable;
                                                   OptionString=Bidirectional,ToIntegrationTable,FromIntegrationTable }
    { 11  ;   ;Int. Tbl. Caption Prefix;Text30    ;CaptionML=ENU=Int. Tbl. Caption Prefix }
    { 12  ;   ;Synch. Int. Tbl. Mod. On Fltr.;DateTime;
                                                   CaptionML=ENU=Synch. Int. Tbl. Mod. On Fltr.;
                                                   Description=Scheduled synch. Integration Table Modified On Filter }
    { 13  ;   ;Synch. Modified On Filter;DateTime ;CaptionML=ENU=Synch. Modified On Filter;
                                                   Description=Scheduled synch. Modified On Filter }
    { 14  ;   ;Table Filter        ;BLOB          ;CaptionML=ENU=Table Filter }
    { 15  ;   ;Integration Table Filter;BLOB      ;CaptionML=ENU=Integration Table Filter }
    { 16  ;   ;Synch. Only Coupled Records;Boolean;InitValue=true;
                                                   CaptionML=ENU=Synch. Only Coupled Records }
    { 17  ;   ;Parent Name         ;Code20        ;CaptionML=ENU=Parent Name }
    { 18  ;   ;Graph Delta Token   ;Text250       ;CaptionML=ENU=Graph Delta Token }
    { 19  ;   ;Int. Tbl. Delta Token Fld. No.;Integer;
                                                   CaptionML=ENU=Int. Tbl. Delta Token Fld. No. }
    { 20  ;   ;Int. Tbl. ChangeKey Fld. No.;Integer;
                                                   CaptionML=ENU=Int. Tbl. ChangeKey Fld. No. }
    { 21  ;   ;Int. Tbl. State Fld. No.;Integer   ;CaptionML=ENU=Int. Tbl. State Fld. No. }
    { 22  ;   ;Delete After Synchronization;Boolean;
                                                   CaptionML=ENU=Delete After Synchronization }
    { 30  ;   ;Dependency Filter   ;Text250       ;CaptionML=ENU=Dependency Filter }
    { 100 ;   ;Full Sync is Running;Boolean       ;OnValidate=BEGIN
                                                                if xRec.GET(Name) then;
                                                                if (not xRec."Full Sync is Running") and "Full Sync is Running" then begin
                                                                  "Last Full Sync Start DateTime" := CURRENTDATETIME;
                                                                  "Full Sync Session ID" := SESSIONID;
                                                                end;
                                                                if not "Full Sync is Running" then
                                                                  "Full Sync Session ID" := 0;
                                                              END;

                                                   CaptionML=ENU=Full Sync is Running;
                                                   Description=This is set to TRUE when FullSync starts, and to FALSE when FullSync completes. }
    { 101 ;   ;Full Sync Session ID;Integer       ;CaptionML=ENU=Full Sync Session ID;
                                                   Description=The ID of the session running the FullSync must be 0 if FullSync is not running. }
    { 102 ;   ;Last Full Sync Start DateTime;DateTime;
                                                   CaptionML=ENU=Last Full Sync Start DateTime;
                                                   Description=The starting date and time of the last time FullSync was run. This is used to re-run in case FullSync failed to reset these fields. }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      JobLogEntryNo@1000 : Integer;

    [External]
    PROCEDURE FindMapping@16(TableNo@1001 : Integer;PrimaryKey@1000 : Variant) : Boolean;
    BEGIN
      if PrimaryKey.ISRECORDID then
        exit(FindMappingForTable(TableNo));
      if PrimaryKey.ISGUID then
        exit(FindMappingForIntegrationTable(TableNo));
    END;

    LOCAL PROCEDURE FindMappingForIntegrationTable@13(TableId@1000 : Integer) : Boolean;
    BEGIN
      SETRANGE("Integration Table ID",TableId);
      exit(FINDFIRST);
    END;

    [External]
    PROCEDURE FindMappingForTable@12(TableId@1000 : Integer) : Boolean;
    BEGIN
      SETRANGE("Table ID",TableId);
      exit(FINDFIRST);
    END;

    PROCEDURE IsFullSynch@27() : Boolean;
    BEGIN
      exit("Full Sync is Running" and "Delete After Synchronization");
    END;

    [External]
    PROCEDURE GetName@22() Result : Code[20];
    BEGIN
      if "Delete After Synchronization" then
        Result := "Parent Name";
      if Result = '' then
        Result := Name;
    END;

    [External]
    PROCEDURE GetDirection@23() : Integer;
    VAR
      IntegrationTableMapping@1000 : Record "Integration Table Mapping";
    BEGIN
      IntegrationTableMapping.GET(GetName);
      exit(IntegrationTableMapping.Direction);
    END;

    PROCEDURE GetJobLogEntryNo@26() : Integer;
    BEGIN
      exit(JobLogEntryNo)
    END;

    [External]
    PROCEDURE GetTempDescription@20() : Text;
    VAR
      Separator@1000 : Text;
    BEGIN
      case Direction of
        Direction::Bidirectional:
          Separator := '<->';
        Direction::ToIntegrationTable:
          Separator := '->';
        Direction::FromIntegrationTable:
          Separator := '<-';
      end;
      exit(
        STRSUBSTNO(
          '%1 %2 %3',GetTableCaption("Table ID"),Separator,GetTableCaption("Integration Table ID")));
    END;

    [External]
    PROCEDURE GetExtendedIntegrationTableCaption@1() : Text;
    VAR
      TableCaption@1001 : Text;
    BEGIN
      TableCaption := GetTableCaption("Integration Table ID");
      if TableCaption <> '' then
        if "Int. Tbl. Caption Prefix" <> '' then
          exit(STRSUBSTNO('%1 %2',"Int. Tbl. Caption Prefix",TableCaption));
      exit(TableCaption);
    END;

    LOCAL PROCEDURE GetTableCaption@21(ID@1000 : Integer) : Text;
    VAR
      TableMetadata@1001 : Record "Table Metadata";
    BEGIN
      if TableMetadata.GET(ID) then
        exit(TableMetadata.Caption);
      exit('');
    END;

    [External]
    PROCEDURE SetTableFilter@5(Filter@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Table Filter".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(Filter);
    END;

    [External]
    PROCEDURE GetTableFilter@6() Value : Text;
    VAR
      InStream@1000 : InStream;
    BEGIN
      CALCFIELDS("Table Filter");
      "Table Filter".CREATEINSTREAM(InStream);
      InStream.READ(Value);
    END;

    [External]
    PROCEDURE SetIntegrationTableFilter@7(Filter@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Integration Table Filter".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(Filter);
    END;

    [External]
    PROCEDURE GetIntegrationTableFilter@8() Value : Text;
    VAR
      InStream@1000 : InStream;
    BEGIN
      CALCFIELDS("Integration Table Filter");
      "Integration Table Filter".CREATEINSTREAM(InStream);
      InStream.READ(Value);
    END;

    [External]
    PROCEDURE SetIntTableModifiedOn@4(ModifiedOn@1000 : DateTime);
    BEGIN
      if (ModifiedOn <> 0DT) and (ModifiedOn > "Synch. Int. Tbl. Mod. On Fltr.") then begin
        "Synch. Int. Tbl. Mod. On Fltr." := ModifiedOn;
        MODIFY(true);
      end;
    END;

    [External]
    PROCEDURE SetTableModifiedOn@9(ModifiedOn@1000 : DateTime);
    BEGIN
      if (ModifiedOn <> 0DT) and (ModifiedOn > "Synch. Modified On Filter") then begin
        "Synch. Modified On Filter" := ModifiedOn;
        MODIFY(true);
      end;
    END;

    PROCEDURE SetJobLogEntryNo@25(NewJobLogEntryNo@1000 : Integer);
    BEGIN
      JobLogEntryNo := NewJobLogEntryNo;
    END;

    PROCEDURE ShowLog@24(JobIDFilter@1001 : Text);
    VAR
      IntegrationSynchJob@1000 : Record "Integration Synch. Job";
    BEGIN
      if Name = '' then
        exit;

      IntegrationSynchJob.SETCURRENTKEY("Start Date/Time",ID);
      IntegrationSynchJob.ASCENDING := false;
      IntegrationSynchJob.FILTERGROUP(2);
      IntegrationSynchJob.SETRANGE("Integration Table Mapping Name",Name);
      IntegrationSynchJob.FILTERGROUP(0);
      if JobIDFilter <> '' then
        IntegrationSynchJob.SETFILTER(ID,JobIDFilter);
      if IntegrationSynchJob.FINDFIRST then;
      PAGE.RUN(PAGE::"Integration Synch. Job List",IntegrationSynchJob);
    END;

    [External]
    PROCEDURE SynchronizeNow@2(ResetLastSynchModifiedOnDateTime@1000 : Boolean);
    BEGIN
      if ResetLastSynchModifiedOnDateTime then begin
        CLEAR("Synch. Modified On Filter");
        CLEAR("Synch. Int. Tbl. Mod. On Fltr.");
        MODIFY;
        COMMIT;
      end;
      CODEUNIT.RUN("Synch. Codeunit ID",Rec);
    END;

    [External]
    PROCEDURE GetRecordRef@3(ID@1000 : Variant;VAR IntegrationRecordRef@1001 : RecordRef) : Boolean;
    VAR
      IDFieldRef@1002 : FieldRef;
      RecordID@1003 : RecordID;
      TextKey@1004 : Text;
    BEGIN
      IntegrationRecordRef.CLOSE;
      if ID.ISGUID then begin
        IntegrationRecordRef.OPEN("Integration Table ID");
        IDFieldRef := IntegrationRecordRef.FIELD("Integration Table UID Fld. No.");
        IDFieldRef.SETFILTER(ID);
        exit(IntegrationRecordRef.FINDFIRST);
      end;

      if ID.ISRECORDID then begin
        RecordID := ID;
        if RecordID.TABLENO = "Table ID" then
          exit(IntegrationRecordRef.GET(ID));
      end;

      if ID.ISTEXT then begin
        IntegrationRecordRef.OPEN("Integration Table ID");
        IDFieldRef := IntegrationRecordRef.FIELD("Integration Table UID Fld. No.");
        TextKey := ID;
        IDFieldRef.SETFILTER('%1',TextKey);
        exit(IntegrationRecordRef.FINDFIRST);
      end;
    END;

    [External]
    PROCEDURE SetIntRecordRefFilter@10(VAR IntRecordRef@1000 : RecordRef);
    VAR
      ModifiedOnFieldRef@1003 : FieldRef;
      TableFilter@1001 : Text;
    BEGIN
      TableFilter := GetIntegrationTableFilter;
      if TableFilter <> '' then
        IntRecordRef.SETVIEW(TableFilter);

      if "Synch. Int. Tbl. Mod. On Fltr." <> 0DT then begin
        ModifiedOnFieldRef := IntRecordRef.FIELD("Int. Tbl. Modified On Fld. No.");
        ModifiedOnFieldRef.SETFILTER('>%1',"Synch. Int. Tbl. Mod. On Fltr.");
      end;
    END;

    [External]
    PROCEDURE SetRecordRefFilter@11(VAR RecordRef@1000 : RecordRef);
    VAR
      TableFilter@1001 : Text;
    BEGIN
      TableFilter := GetTableFilter;
      if TableFilter <> '' then
        RecordRef.SETVIEW(TableFilter);
    END;

    PROCEDURE CopyModifiedOnFilters@28(FromIntegrationTableMapping@1000 : Record "Integration Table Mapping");
    BEGIN
      "Synch. Modified On Filter" := FromIntegrationTableMapping."Synch. Modified On Filter";
      "Synch. Int. Tbl. Mod. On Fltr." := FromIntegrationTableMapping."Synch. Int. Tbl. Mod. On Fltr.";
      MODIFY;
    END;

    [External]
    PROCEDURE CreateRecord@14(MappingName@1000 : Code[20];TableNo@1001 : Integer;IntegrationTableNo@1002 : Integer;IntegrationTableUIDFieldNo@1005 : Integer;IntegrationTableModifiedFieldNo@1006 : Integer;TableConfigTemplateCode@1003 : Code[10];IntegrationTableConfigTemplateCode@1007 : Code[10];SynchOnlyCoupledRecords@1009 : Boolean;DirectionArg@1004 : Option;Prefix@1010 : Text[30]);
    BEGIN
      if GET(MappingName) then
        DELETE(true);
      INIT;
      Name := MappingName;
      "Table ID" := TableNo;
      "Integration Table ID" := IntegrationTableNo;
      "Synch. Codeunit ID" := CODEUNIT::"CRM Integration Table Synch.";
      VALIDATE("Integration Table UID Fld. No.",IntegrationTableUIDFieldNo);
      "Int. Tbl. Modified On Fld. No." := IntegrationTableModifiedFieldNo;
      "Table Config Template Code" := TableConfigTemplateCode;
      "Int. Tbl. Config Template Code" := IntegrationTableConfigTemplateCode;
      Direction := DirectionArg;
      "Int. Tbl. Caption Prefix" := Prefix;
      "Synch. Only Coupled Records" := SynchOnlyCoupledRecords;
      INSERT;
    END;

    [External]
    PROCEDURE SetFullSyncStartAndCommit@15();
    BEGIN
      VALIDATE("Full Sync is Running",true);
      MODIFY;
      COMMIT;
      GET(Name);
    END;

    [External]
    PROCEDURE SetFullSyncEndAndCommit@17();
    BEGIN
      VALIDATE("Full Sync is Running",false);
      MODIFY;
      COMMIT;
      GET(Name);
    END;

    [External]
    PROCEDURE IsFullSyncAllowed@18() : Boolean;
    BEGIN
      GET(Name);
      if not "Full Sync is Running" then
        exit(true);

      if not ISSESSIONACTIVE("Full Sync Session ID") then begin
        SetFullSyncEndAndCommit;
        exit(true);
      end;
      if ABS(CURRENTDATETIME - "Last Full Sync Start DateTime") >= OneDayInMiliseconds then
        exit(true);
      exit(false)
    END;

    LOCAL PROCEDURE OneDayInMiliseconds@19() : Integer;
    BEGIN
      exit(24 * 60 * 60 * 1000)
    END;

    BEGIN
    END.
  }
}

