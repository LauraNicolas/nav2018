OBJECT Page 345 Reconciliation
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Editable=false;
    CaptionML=ENU=Reconciliation;
    SourceTable="G/L Account Net Change";
    DataCaptionExpr=Heading;
    PageType=List;
    SourceTableTemporary=true;
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                Name=Group6;
                GroupType=Repeater }

    { 8   ;2   ;Field     ;
                Name=No.;
                CaptionML=ENU=Account;
                ToolTipML=ENU=Specifies the bank account that is being reconciled.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="No.";
                ImplicitType=Code20 }

    { 9   ;2   ;Field     ;
                Name=Name;
                ToolTipML=ENU=Specifies the name of the record.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Name;
                ImplicitType=Text50 }

    { 10  ;2   ;Field     ;
                Name=Net Change in Jnl.;
                ToolTipML=ENU=Specifies the net change that will occur on the bank when you post the journal.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Net Change in Jnl.";
                ImplicitType=Decimal }

    { 11  ;2   ;Field     ;
                Name=Balance after Posting;
                CaptionML=ENU=Balance after Posting;
                ToolTipML=ENU=Specifies the current balance on the bank account.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Balance after Posting";
                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      GenJnlLine@1000 : Record "Gen. Journal Line";
      GLAcc@1001 : Record "G/L Account";
      BankAccPostingGr@1002 : Record "Bank Account Posting Group";
      BankAcc@1003 : Record "Bank Account";
      Heading@1005 : Code[10];

    [External]
    PROCEDURE SetGenJnlLine@1(VAR NewGenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      GenJnlLine.COPY(NewGenJnlLine);
      Heading := GenJnlLine."Journal Batch Name";
      DELETEALL;
      GLAcc.SETCURRENTKEY("Reconciliation Account");
      GLAcc.SETRANGE("Reconciliation Account",true);
      if GLAcc.FIND('-') then
        repeat
          InsertGLAccNetChange;
        until GLAcc.NEXT = 0;

      if GenJnlLine.FIND('-') then
        repeat
          SaveNetChange(
            GenJnlLine."Account Type",GenJnlLine."Account No.",
            ROUND(GenJnlLine."Amount (LCY)" / (1 + GenJnlLine."VAT %" / 100)));
          SaveNetChange(
            GenJnlLine."Bal. Account Type",GenJnlLine."Bal. Account No.",
            -ROUND(GenJnlLine."Amount (LCY)" / (1 + GenJnlLine."Bal. VAT %" / 100)));
        until GenJnlLine.NEXT = 0;
      if FIND('-') then;
    END;

    LOCAL PROCEDURE SaveNetChange@2(AccType@1000 : Integer;AccNo@1001 : Code[20];NetChange@1002 : Decimal);
    BEGIN
      if AccNo = '' then
        exit;
      case  AccType of
        GenJnlLine."Account Type"::"G/L Account":
          if not GET(AccNo) then
            exit;
        GenJnlLine."Account Type"::"Bank Account":
          begin
            if AccNo <> BankAcc."No." then begin
              BankAcc.GET(AccNo);
              BankAcc.TESTFIELD("Bank Acc. Posting Group");
              BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
              BankAccPostingGr.TESTFIELD("G/L Bank Account No.");
            end;
            AccNo := BankAccPostingGr."G/L Bank Account No.";
            if not GET(AccNo) then begin
              GLAcc.GET(AccNo);
              InsertGLAccNetChange;
            end;
          end;
        else
          exit;
      end;

      "Net Change in Jnl." := "Net Change in Jnl." + NetChange;
      "Balance after Posting" := "Balance after Posting" + NetChange;
      MODIFY;
    END;

    LOCAL PROCEDURE InsertGLAccNetChange@6();
    BEGIN
      GLAcc.CALCFIELDS("Balance at Date");
      INIT;
      "No." := GLAcc."No.";
      Name := GLAcc.Name;
      "Balance after Posting" := GLAcc."Balance at Date";
      INSERT;
    END;

    [External]
    PROCEDURE ReturnGLAccountNetChange@25(VAR GLAccountNetChange@1000 : Record "G/L Account Net Change");
    VAR
      OldGLAccountNetChange@1001 : Record "G/L Account Net Change";
    BEGIN
      OldGLAccountNetChange := Rec;
      FINDSET;
      repeat
        GLAccountNetChange.INIT;
        GLAccountNetChange := Rec;
        GLAccountNetChange.INSERT;
      until NEXT = 0;

      Rec := OldGLAccountNetChange;
    END;

    BEGIN
    END.
  }
}

