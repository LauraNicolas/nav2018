OBJECT Codeunit 1262 Pre & Post Process XML Import
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DiffCurrQst@1001 : TextConst '@@@="%1 %2 = Currency Code EUR; %3 %4 = LCY Code DKK.";ENU=The bank statement that you are importing contains transactions in %1 %2. This conflicts with the %3 %4.\\Do you want to continue?';
      MissingStmtDateInDataMsg@1012 : TextConst 'ENU=The statement date was not found in the data to be imported.';
      MissingCrdDbtIndInDataMsg@1013 : TextConst 'ENU=The credit/debit indicator was not found in the data to be imported.';
      MissingBalTypeInDataMsg@1014 : TextConst 'ENU=The balance type was not found in the data to be imported.';
      MissingClosingBalInDataMsg@1015 : TextConst 'ENU=The closing balance was not found in the data to be imported.';
      MissingBankAccNoQst@1022 : TextConst 'ENU=Bank account %1 does not have a bank account number.\\Do you want to continue?';
      BankAccCurrErr@1020 : TextConst '@@@="%1 %2 = Currency Code EUR; %3 = Bank Account No.";ENU=The bank statement that you are importing contains transactions in currencies other than the %1 %2 of bank account %3.';
      MultipleStmtErr@1019 : TextConst 'ENU=The file that you are trying to import contains more than one bank statement.';
      MissingBankAccNoInDataErr@1018 : TextConst 'ENU=The bank account number was not found in the data to be imported.';
      BankAccMismatchQst@1017 : TextConst '@@@="%1=Value; %2 = Bank account no.";ENU=Bank account %1 does not have the bank account number %2, as specified in the bank statement file.\\Do you want to continue?';

    [Internal]
    PROCEDURE PostProcessStatementDate@3(DataExch@1002 : Record "Data Exch.";VAR RecRef@1000 : RecordRef;FieldNo@1001 : Integer;StmtDatePathFilter@1004 : Text);
    VAR
      DataExchFieldDetails@1007 : Query "Data Exch. Field Details";
    BEGIN
      SetValueFromDataExchField(DataExchFieldDetails,DataExch,StmtDatePathFilter,MissingStmtDateInDataMsg,RecRef,FieldNo);
    END;

    [Internal]
    PROCEDURE PostProcessStatementEndingBalance@11(DataExch@1004 : Record "Data Exch.";VAR RecRef@1000 : RecordRef;FieldNo@1001 : Integer;BalTypeDescriptor@1009 : Text;BalTypePathFilter@1007 : Text;BalAmtPathFilter@1010 : Text;CrdDbtIndPathFilter@1011 : Text;ParentNodeOffset@1012 : Integer);
    VAR
      DataExchFieldDetails@1008 : Query "Data Exch. Field Details";
      GroupNodeID@1002 : Text;
    BEGIN
      DataExchFieldDetails.SETRANGE(FieldValue,BalTypeDescriptor);
      if not HasDataExchFieldValue(DataExchFieldDetails,DataExch."Entry No.",BalTypePathFilter) then begin
        MESSAGE(MissingBalTypeInDataMsg);
        exit;
      end;

      GroupNodeID := GetSubTreeRoot(DataExchFieldDetails.Node_ID,ParentNodeOffset);
      CLEAR(DataExchFieldDetails);
      DataExchFieldDetails.SETFILTER(Node_ID,'%1',GroupNodeID + '*');
      if not SetValueFromDataExchField(DataExchFieldDetails,DataExch,BalAmtPathFilter,MissingClosingBalInDataMsg,RecRef,FieldNo) then
        exit;

      if CrdDbtIndPathFilter = '' then
        exit;

      SetValueFromDataExchField(DataExchFieldDetails,DataExch,CrdDbtIndPathFilter,MissingCrdDbtIndInDataMsg,RecRef,FieldNo);
    END;

    [External]
    PROCEDURE PreProcessBankAccount@5(DataExch@1005 : Record "Data Exch.";BankAccNo@1004 : Code[20];BankAccNoPathFilter@1006 : Text;CurrCodePathFilter@1007 : Text);
    VAR
      BankAccount@1001 : Record "Bank Account";
    BEGIN
      BankAccount.GET(BankAccNo);
      CheckBankAccNo(DataExch,BankAccount,BankAccNoPathFilter);
      CheckBankAccCurrency(DataExch,BankAccount,CurrCodePathFilter);
    END;

    [External]
    PROCEDURE PreProcessGLAccount@1(DataExch@1005 : Record "Data Exch.";VAR GenJournalLineTemplate@1004 : Record "Gen. Journal Line";CurrencyCodePathFilter@1001 : Text);
    VAR
      GLSetup@1000 : Record "General Ledger Setup";
      DataExchFieldDetails@1007 : Query "Data Exch. Field Details";
      StatementCurrencyCode@1006 : Code[10];
    BEGIN
      GLSetup.GET;

      DataExchFieldDetails.SETFILTER(FieldValue,'<>%1&<>%2','',GLSetup."LCY Code");
      if HasDataExchFieldValue(DataExchFieldDetails,DataExch."Entry No.",CurrencyCodePathFilter) then
        if not CONFIRM(STRSUBSTNO(DiffCurrQst,GenJournalLineTemplate.FIELDCAPTION("Currency Code"),
               DataExchFieldDetails.FieldValue,GLSetup.FIELDCAPTION("LCY Code"),GLSetup."LCY Code"))
        then
          ERROR('');

      DataExchFieldDetails.SETRANGE(FieldValue);
      if HasDataExchFieldValue(DataExchFieldDetails,DataExch."Entry No.",CurrencyCodePathFilter) then begin
        StatementCurrencyCode := FORMAT(DataExchFieldDetails.FieldValue,-MAXSTRLEN(GenJournalLineTemplate."Currency Code"));
        GenJournalLineTemplate.VALIDATE("Currency Code",GLSetup.GetCurrencyCode(StatementCurrencyCode));
      end;
    END;

    [External]
    PROCEDURE PreProcessFile@6(DataExch@1000 : Record "Data Exch.";StatementIdPathFilter@1001 : Text);
    BEGIN
      CheckMultipleStatements(DataExch,StatementIdPathFilter);
    END;

    LOCAL PROCEDURE GetSubTreeRoot@13(Node@1002 : Text;Distance@1001 : Integer) : Text;
    BEGIN
      exit(COPYSTR(Node,1,STRLEN(Node) - Distance * 4));
    END;

    LOCAL PROCEDURE CheckBankAccNo@4(DataExch@1003 : Record "Data Exch.";BankAccount@1000 : Record "Bank Account";BankAccNoPathFilter@1001 : Text);
    VAR
      DataExchFieldDetails@1002 : Query "Data Exch. Field Details";
    BEGIN
      if BankAccount.GetBankAccountNo = '' then begin
        if not CONFIRM(STRSUBSTNO(MissingBankAccNoQst,BankAccount."No.")) then
          ERROR('');
        exit;
      end;

      if not HasDataExchFieldValue(DataExchFieldDetails,DataExch."Entry No.",BankAccNoPathFilter) then
        ERROR(MissingBankAccNoInDataErr);

      if (DELCHR(DataExchFieldDetails.FieldValue,'=','- ') <> DELCHR(BankAccount."Bank Account No.",'=','- ')) and
         (DELCHR(DataExchFieldDetails.FieldValue,'=','- ') <> DELCHR(BankAccount.IBAN,'=','- '))
      then
        if not CONFIRM(STRSUBSTNO(BankAccMismatchQst,BankAccount."No.",DataExchFieldDetails.FieldValue)) then
          ERROR('');
    END;

    LOCAL PROCEDURE CheckBankAccCurrency@9(DataExch@1004 : Record "Data Exch.";BankAccount@1000 : Record "Bank Account";CurrCodePathFilter@1001 : Text);
    VAR
      GeneralLedgerSetup@1003 : Record "General Ledger Setup";
      DataExchFieldDetails@1002 : Query "Data Exch. Field Details";
    BEGIN
      GeneralLedgerSetup.GET;

      DataExchFieldDetails.SETFILTER(FieldValue,'<>%1&<>%2','',GeneralLedgerSetup.GetCurrencyCode(BankAccount."Currency Code"));
      if HasDataExchFieldValue(DataExchFieldDetails,DataExch."Entry No.",CurrCodePathFilter) then
        ERROR(BankAccCurrErr,BankAccount.FIELDCAPTION("Currency Code"),
          GeneralLedgerSetup.GetCurrencyCode(BankAccount."Currency Code"),BankAccount."No.");
    END;

    LOCAL PROCEDURE CheckMultipleStatements@2(DataExch@1003 : Record "Data Exch.";StatementIdPathFilter@1000 : Text);
    VAR
      DataExchFieldDetails@1001 : Query "Data Exch. Field Details";
      StmtCount@1002 : Integer;
    BEGIN
      if HasDataExchFieldValue(DataExchFieldDetails,DataExch."Entry No.",StatementIdPathFilter) then begin
        StmtCount := 1;
        while DataExchFieldDetails.READ do
          StmtCount += 1;
      end;

      if StmtCount > 1 then
        ERROR(MultipleStmtErr);
    END;

    [External]
    PROCEDURE HasDataExchFieldValue@7(VAR DataExchFieldDetails@1003 : Query "Data Exch. Field Details";DataExchEntryNo@1001 : Integer;PathFilter@1002 : Text) : Boolean;
    BEGIN
      DataExchFieldDetails.SETRANGE(Data_Exch_No,DataExchEntryNo);
      DataExchFieldDetails.SETFILTER(Path,PathFilter);
      DataExchFieldDetails.OPEN;
      exit(DataExchFieldDetails.READ);
    END;

    LOCAL PROCEDURE SetValueFromDataExchField@8(VAR DataExchFieldDetails@1000 : Query "Data Exch. Field Details";DataExch@1001 : Record "Data Exch.";PathFilter@1005 : Text;NotFoundMessage@1006 : Text;RecRef@1003 : RecordRef;FieldNo@1004 : Integer) : Boolean;
    VAR
      DataExchField@1002 : Record "Data Exch. Field";
      TempFieldIdsToNegate@1009 : TEMPORARY Record Integer;
      DummyDataExchFieldMapping@1008 : Record "Data Exch. Field Mapping";
      ProcessDataExch@1007 : Codeunit "Process Data Exch.";
    BEGIN
      if not HasDataExchFieldValue(DataExchFieldDetails,DataExch."Entry No.",PathFilter) then begin
        MESSAGE(NotFoundMessage);
        exit(false);
      end;

      DataExchField.GET(
        DataExchFieldDetails.Data_Exch_No,
        DataExchFieldDetails.Line_No,
        DataExchFieldDetails.Column_No,
        DataExchFieldDetails.Node_ID);

      DummyDataExchFieldMapping."Data Exch. Def Code" := DataExch."Data Exch. Def Code";
      DummyDataExchFieldMapping."Data Exch. Line Def Code" := DataExch."Data Exch. Line Def Code";
      DummyDataExchFieldMapping."Column No." := DataExchFieldDetails.Column_No;
      DummyDataExchFieldMapping."Field ID" := FieldNo;
      DummyDataExchFieldMapping."Table ID" := RecRef.NUMBER;

      ProcessDataExch.SetField(RecRef,DummyDataExchFieldMapping,DataExchField,TempFieldIdsToNegate);
      ProcessDataExch.NegateAmounts(RecRef,TempFieldIdsToNegate);

      RecRef.MODIFY(true);
      exit(true);
    END;

    BEGIN
    END.
  }
}

