OBJECT Codeunit 442 Sales-Post Prepayments
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Sales Line"=imd,
                TableData "Invoice Post. Buffer"=imd,
                TableData "Sales Invoice Header"=imd,
                TableData "Sales Invoice Line"=imd,
                TableData "Sales Cr.Memo Header"=imd,
                TableData "Sales Cr.Memo Line"=imd,
                TableData "General Posting Setup"=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=is not within your range of allowed posting dates';
      GLSetup@1024 : Record "General Ledger Setup";
      GenPostingSetup@1002 : Record "General Posting Setup";
      Text001@1003 : TextConst 'ENU=There is nothing to post.';
      Text002@1006 : TextConst 'ENU=Posting Prepayment Lines   #2######\';
      Text003@1004 : TextConst 'ENU=%1 %2 -> Invoice %3';
      Text004@1005 : TextConst 'ENU=Posting sales and VAT      #3######\';
      Text005@1007 : TextConst 'ENU=Posting to customers       #4######\';
      Text006@1022 : TextConst 'ENU=Posting to bal. account    #5######';
      Text007@1009 : TextConst 'ENU=The combination of dimensions that is used in the document of type %1 with the number %2 is blocked. %3.';
      Text008@1008 : TextConst 'ENU=The combination of dimensions that is used in the document of type %1 with the number %2, line no. %3 is blocked. %4.';
      Text009@1011 : TextConst 'ENU=The dimensions that are used in the document of type %1 with the number %2 are not valid. %3.';
      Text010@1010 : TextConst 'ENU=The dimensions that are used in the document of type %1 with the number %2, line no. %3 are not valid. %4.';
      TempGlobalPrepmtInvLineBuf@1025 : TEMPORARY Record "Prepayment Inv. Line Buffer";
      GenJnlPostLine@1013 : Codeunit "Gen. Jnl.-Post Line";
      Text011@1001 : TextConst 'ENU=%1 %2 -> Credit Memo %3';
      Text012@1015 : TextConst 'ENU=Prepayment %1, %2 %3.';
      Text013@1016 : TextConst 'ENU=It is not possible to assign a prepayment amount of %1 to the sales lines.';
      Text014@1018 : TextConst 'ENU=VAT Amount';
      Text015@1017 : TextConst 'ENU=%1% VAT';
      Text016@1019 : TextConst 'ENU=The new prepayment amount must be between %1 and %2.';
      Text017@1020 : TextConst 'ENU=At least one line must have %1 > 0 to distribute prepayment amount.';
      Text018@1021 : TextConst 'ENU=must be positive when %1 is not 0';
      Text019@1014 : TextConst 'ENU=Invoice,Credit Memo';

    [Internal]
    PROCEDURE Invoice@9(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      Code(SalesHeader,0);
    END;

    [Internal]
    PROCEDURE CreditMemo@10(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      Code(SalesHeader,1);
    END;

    LOCAL PROCEDURE Code@2(VAR SalesHeader2@1013 : Record "Sales Header";DocumentType@1022 : 'Invoice,"Credit Memo"');
    VAR
      SalesSetup@1010 : Record "Sales & Receivables Setup";
      SourceCodeSetup@1027 : Record "Source Code Setup";
      Cust@1029 : Record Customer;
      SalesHeader@1000 : Record "Sales Header";
      SalesLine@1001 : Record "Sales Line";
      SalesInvHeader@1002 : Record "Sales Invoice Header";
      SalesCrMemoHeader@1023 : Record "Sales Cr.Memo Header";
      TempPrepmtInvLineBuffer@1021 : TEMPORARY Record "Prepayment Inv. Line Buffer";
      TotalPrepmtInvLineBuffer@1019 : Record "Prepayment Inv. Line Buffer";
      TotalPrepmtInvLineBufferLCY@1020 : Record "Prepayment Inv. Line Buffer";
      GenJnlLine@1012 : Record "Gen. Journal Line";
      TempVATAmountLine@1000000002 : TEMPORARY Record "VAT Amount Line";
      TempVATAmountLineDeduct@1015 : TEMPORARY Record "VAT Amount Line";
      CustLedgEntry@1031 : Record "Cust. Ledger Entry";
      TempSalesLines@1032 : TEMPORARY Record "Sales Line";
      GenJnlCheckLine@1009 : Codeunit "Gen. Jnl.-Check Line";
      Window@1000000003 : Dialog;
      GenJnlLineDocNo@1006 : Code[20];
      GenJnlLineExtDocNo@1005 : Code[35];
      SrcCode@1004 : Code[10];
      PostingNoSeriesCode@1007 : Code[20];
      CalcPmtDiscOnCrMemos@1011 : Boolean;
      PostingDescription@1028 : Text[50];
      GenJnlLineDocType@1003 : Integer;
      PrevLineNo@1016 : Integer;
      LineCount@1000000004 : Integer;
      PostedDocTabNo@1025 : Integer;
      LineNo@1026 : Integer;
    BEGIN
      SalesHeader := SalesHeader2;
      GLSetup.GET;
      SalesSetup.GET;
      with SalesHeader do begin
        TESTFIELD("Document Type","Document Type"::Order);
        TESTFIELD("Sell-to Customer No.");
        TESTFIELD("Bill-to Customer No.");
        TESTFIELD("Posting Date");
        TESTFIELD("Document Date");
        if GenJnlCheckLine.DateNotAllowed("Posting Date") then
          FIELDERROR("Posting Date",Text000);

        if not CheckOpenPrepaymentLines(SalesHeader,DocumentType) then
          ERROR(Text001);

        CheckDim(SalesHeader);
        OnCheckSalesPostRestrictions;
        Cust.GET("Sell-to Customer No.");
        Cust.CheckBlockedCustOnDocs(Cust,PrepmtDocTypeToDocType("Document Type"),false,true);
        if "Bill-to Customer No." <> "Sell-to Customer No." then begin
          Cust.GET("Bill-to Customer No.");
          Cust.CheckBlockedCustOnDocs(Cust,PrepmtDocTypeToDocType("Document Type"),false,true);
        end;

        UpdateDocNos(SalesHeader,DocumentType,GenJnlLineDocNo,PostingNoSeriesCode);

        Window.OPEN(
          '#1#################################\\' +
          Text002 +
          Text004 +
          Text005 +
          Text006);
        Window.UPDATE(1,STRSUBSTNO('%1 %2',SELECTSTR(1 + DocumentType,Text019),"No."));

        SourceCodeSetup.GET;
        SrcCode := SourceCodeSetup.Sales;
        if "Prepmt. Posting Description" <> '' then
          PostingDescription := "Prepmt. Posting Description"
        else
          PostingDescription :=
            COPYSTR(
              STRSUBSTNO(Text012,SELECTSTR(1 + DocumentType,Text019),"Document Type","No."),
              1,MAXSTRLEN("Posting Description"));

        // Create posted header
        if SalesSetup."Ext. Doc. No. Mandatory" then
          TESTFIELD("External Document No.");
        case DocumentType of
          DocumentType::Invoice:
            begin
              InsertSalesInvHeader(SalesInvHeader,SalesHeader,PostingDescription,GenJnlLineDocNo,SrcCode,PostingNoSeriesCode);
              GenJnlLineDocType := GenJnlLine."Document Type"::Invoice;
              PostedDocTabNo := DATABASE::"Sales Invoice Header";
              Window.UPDATE(1,STRSUBSTNO(Text003,"Document Type","No.",SalesInvHeader."No."));
            end;
          DocumentType::"Credit Memo":
            begin
              CalcPmtDiscOnCrMemos := GetCalcPmtDiscOnCrMemos("Prepmt. Payment Terms Code");
              InsertSalesCrMemoHeader(
                SalesCrMemoHeader,SalesHeader,PostingDescription,GenJnlLineDocNo,SrcCode,PostingNoSeriesCode,
                CalcPmtDiscOnCrMemos);
              GenJnlLineDocType := GenJnlLine."Document Type"::"Credit Memo";
              PostedDocTabNo := DATABASE::"Sales Cr.Memo Header";
              Window.UPDATE(1,STRSUBSTNO(Text011,"Document Type","No.",SalesCrMemoHeader."No."));
            end;
        end;
        if SalesSetup."Copy Comments Order to Invoice" then
          CopyCommentLines("No.",PostedDocTabNo,GenJnlLineDocNo);
        GenJnlLineExtDocNo := "External Document No.";
        // Reverse old lines
        if DocumentType = DocumentType::Invoice then begin
          GetSalesLinesToDeduct(SalesHeader,TempSalesLines);
          if not TempSalesLines.ISEMPTY then
            CalcVATAmountLines(SalesHeader,TempSalesLines,TempVATAmountLineDeduct,DocumentType::"Credit Memo");
        end;

        // Create Lines
        TempPrepmtInvLineBuffer.DELETEALL;
        CalcVATAmountLines(SalesHeader,SalesLine,TempVATAmountLine,DocumentType);
        TempVATAmountLine.DeductVATAmountLine(TempVATAmountLineDeduct);
        UpdateVATOnLines(SalesHeader,SalesLine,TempVATAmountLine,DocumentType);
        BuildInvLineBuffer(SalesHeader,SalesLine,DocumentType,TempPrepmtInvLineBuffer,true);
        TempPrepmtInvLineBuffer.FIND('-');
        repeat
          LineCount := LineCount + 1;
          Window.UPDATE(2,LineCount);
          if TempPrepmtInvLineBuffer."Line No." <> 0 then
            LineNo := PrevLineNo + TempPrepmtInvLineBuffer."Line No."
          else
            LineNo := PrevLineNo + 10000;
          case DocumentType of
            DocumentType::Invoice:
              begin
                InsertSalesInvLine(SalesInvHeader,LineNo,TempPrepmtInvLineBuffer);
                PostedDocTabNo := DATABASE::"Sales Invoice Line";
              end;
            DocumentType::"Credit Memo":
              begin
                InsertSalesCrMemoLine(SalesCrMemoHeader,LineNo,TempPrepmtInvLineBuffer);
                PostedDocTabNo := DATABASE::"Sales Cr.Memo Line";
              end;
          end;
          PrevLineNo := LineNo;
          InsertExtendedText(
            PostedDocTabNo,GenJnlLineDocNo,TempPrepmtInvLineBuffer."G/L Account No.","Document Date","Language Code",PrevLineNo);
        until TempPrepmtInvLineBuffer.NEXT = 0;

        // G/L Posting
        LineCount := 0;
        if not "Compress Prepayment" then
          TempPrepmtInvLineBuffer.CompressBuffer;
        TempPrepmtInvLineBuffer.SETRANGE(Adjustment,false);
        TempPrepmtInvLineBuffer.FINDSET(true);
        repeat
          if DocumentType = DocumentType::Invoice then
            TempPrepmtInvLineBuffer.ReverseAmounts;
          RoundAmounts(SalesHeader,TempPrepmtInvLineBuffer,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY);
          if "Currency Code" = '' then begin
            AdjustInvLineBuffers(SalesHeader,TempPrepmtInvLineBuffer,TotalPrepmtInvLineBuffer,DocumentType);
            TotalPrepmtInvLineBufferLCY := TotalPrepmtInvLineBuffer;
          end else
            AdjustInvLineBuffers(SalesHeader,TempPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,DocumentType);
          TempPrepmtInvLineBuffer.MODIFY;
        until TempPrepmtInvLineBuffer.NEXT = 0;

        TempPrepmtInvLineBuffer.RESET;
        TempPrepmtInvLineBuffer.SETCURRENTKEY(Adjustment);
        TempPrepmtInvLineBuffer.FIND('+');
        repeat
          LineCount := LineCount + 1;
          Window.UPDATE(3,LineCount);

          PostPrepmtInvLineBuffer(
            SalesHeader,TempPrepmtInvLineBuffer,DocumentType,PostingDescription,
            GenJnlLineDocType,GenJnlLineDocNo,GenJnlLineExtDocNo,SrcCode,PostingNoSeriesCode);
        until TempPrepmtInvLineBuffer.NEXT(-1) = 0;

        // Post customer entry
        Window.UPDATE(4,1);
        PostCustomerEntry(
          SalesHeader,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,DocumentType,PostingDescription,
          GenJnlLineDocType,GenJnlLineDocNo,GenJnlLineExtDocNo,SrcCode,PostingNoSeriesCode,CalcPmtDiscOnCrMemos);

        UpdatePostedSalesDocument(DocumentType,GenJnlLineDocNo);

        // Balancing account
        if "Bal. Account No." <> '' then begin
          Window.UPDATE(5,1);
          CustLedgEntry.FINDLAST;
          PostBalancingEntry(
            SalesHeader,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,CustLedgEntry,DocumentType,
            PostingDescription,GenJnlLineDocType,GenJnlLineDocNo,GenJnlLineExtDocNo,SrcCode,PostingNoSeriesCode);
        end;

        // Update lines & header
        UpdateSalesDocument(SalesHeader,SalesLine,DocumentType,GenJnlLineDocNo);
        if Status <> Status::"Pending Prepayment" then
          Status := Status::"Pending Prepayment";
        MODIFY;
      end;

      SalesHeader2 := SalesHeader;
    END;

    LOCAL PROCEDURE UpdateDocNos@31(VAR SalesHeader@1001 : Record "Sales Header";DocumentType@1000 : 'Invoice,"Credit Memo"';VAR DocNo@1002 : Code[20];VAR NoSeriesCode@1003 : Code[20]);
    VAR
      NoSeriesMgt@1004 : Codeunit NoSeriesManagement;
    BEGIN
      with SalesHeader do
        case DocumentType of
          DocumentType::Invoice:
            begin
              TESTFIELD("Prepayment Due Date");
              TESTFIELD("Prepmt. Cr. Memo No.",'');
              if "Prepayment No." = '' then begin
                TESTFIELD("Prepayment No. Series");
                "Prepayment No." := NoSeriesMgt.GetNextNo("Prepayment No. Series","Posting Date",true);
                MODIFY;
                COMMIT;
              end;
              DocNo := "Prepayment No.";
              NoSeriesCode := "Prepayment No. Series";
            end;
          DocumentType::"Credit Memo":
            begin
              TESTFIELD("Prepayment No.",'');
              if "Prepmt. Cr. Memo No." = '' then begin
                TESTFIELD("Prepmt. Cr. Memo No. Series");
                "Prepmt. Cr. Memo No." := NoSeriesMgt.GetNextNo("Prepmt. Cr. Memo No. Series","Posting Date",true);
                MODIFY;
                COMMIT;
              end;
              DocNo := "Prepmt. Cr. Memo No.";
              NoSeriesCode := "Prepmt. Cr. Memo No. Series";
            end;
        end;
    END;

    [External]
    PROCEDURE CheckOpenPrepaymentLines@1(SalesHeader@1000 : Record "Sales Header";DocumentType@1003 : Option) Found : Boolean;
    VAR
      SalesLine@1001 : Record "Sales Line";
    BEGIN
      with SalesLine do begin
        ApplyFilter(SalesHeader,DocumentType,SalesLine);
        if FIND('-') then
          repeat
            if not Found then
              Found := PrepmtAmount(SalesLine,DocumentType) <> 0;
            if ("Prepayment VAT Identifier" = '') and ("Prepmt. Amt. Inv." = 0) then begin
              UpdatePrepmtSetupFields;
              MODIFY;
            end;
          until NEXT = 0;
      end;
      exit(Found);
    END;

    LOCAL PROCEDURE RoundAmounts@5(SalesHeader@1001 : Record "Sales Header";VAR PrepmtInvLineBuf@1000 : Record "Prepayment Inv. Line Buffer";VAR TotalPrepmtInvLineBuf@1002 : Record "Prepayment Inv. Line Buffer";VAR TotalPrepmtInvLineBufLCY@1003 : Record "Prepayment Inv. Line Buffer");
    VAR
      VAT@1004 : Boolean;
    BEGIN
      TotalPrepmtInvLineBuf.IncrAmounts(PrepmtInvLineBuf);

      if SalesHeader."Currency Code" <> '' then begin
        VAT := PrepmtInvLineBuf.Amount <> PrepmtInvLineBuf."Amount Incl. VAT";

        PrepmtInvLineBuf."Amount Incl. VAT" :=
          AmountToLCY(
            SalesHeader,TotalPrepmtInvLineBuf."Amount Incl. VAT",TotalPrepmtInvLineBufLCY."Amount Incl. VAT");
        if VAT then
          PrepmtInvLineBuf.Amount :=
            AmountToLCY(
              SalesHeader,TotalPrepmtInvLineBuf.Amount,TotalPrepmtInvLineBufLCY.Amount)
        else
          PrepmtInvLineBuf.Amount := PrepmtInvLineBuf."Amount Incl. VAT";
        PrepmtInvLineBuf."VAT Amount" := PrepmtInvLineBuf."Amount Incl. VAT" - PrepmtInvLineBuf.Amount;
        if PrepmtInvLineBuf."VAT Base Amount" <> 0 then
          PrepmtInvLineBuf."VAT Base Amount" := PrepmtInvLineBuf.Amount;
      end;

      TotalPrepmtInvLineBufLCY.IncrAmounts(PrepmtInvLineBuf);
    END;

    LOCAL PROCEDURE AmountToLCY@6(SalesHeader@1001 : Record "Sales Header";TotalAmt@1000 : Decimal;PrevTotalAmt@1002 : Decimal) : Decimal;
    VAR
      CurrExchRate@1003 : Record "Currency Exchange Rate";
    BEGIN
      CurrExchRate.INIT;
      with SalesHeader do
        exit(
          ROUND(
            CurrExchRate.ExchangeAmtFCYToLCY("Posting Date","Currency Code",TotalAmt,"Currency Factor")) -
          PrevTotalAmt);
    END;

    LOCAL PROCEDURE BuildInvLineBuffer@3(SalesHeader@1000 : Record "Sales Header";VAR SalesLine@1001 : Record "Sales Line";DocumentType@1005 : Option;VAR PrepmtInvBuf@1002 : Record "Prepayment Inv. Line Buffer";UpdateLines@1010 : Boolean);
    VAR
      PrepmtInvBuf2@1003 : Record "Prepayment Inv. Line Buffer";
      TotalPrepmtInvLineBuffer@1009 : Record "Prepayment Inv. Line Buffer";
      TotalPrepmtInvLineBufferDummy@1008 : Record "Prepayment Inv. Line Buffer";
      SalesSetup@1011 : Record "Sales & Receivables Setup";
    BEGIN
      with SalesHeader do begin
        TempGlobalPrepmtInvLineBuf.RESET;
        TempGlobalPrepmtInvLineBuf.DELETEALL;
        SalesSetup.GET;
        ApplyFilter(SalesHeader,DocumentType,SalesLine);
        SalesLine.SETRANGE("System-Created Entry",false);
        if SalesLine.FIND('-') then
          repeat
            if PrepmtAmount(SalesLine,DocumentType) <> 0 then begin
              if SalesLine.Quantity < 0 then
                SalesLine.FIELDERROR(Quantity,STRSUBSTNO(Text018,FIELDCAPTION("Prepayment %")));
              if SalesLine."Unit Price" < 0 then
                SalesLine.FIELDERROR("Unit Price",STRSUBSTNO(Text018,FIELDCAPTION("Prepayment %")));

              FillInvLineBuffer(SalesHeader,SalesLine,PrepmtInvBuf2);
              if UpdateLines then
                TempGlobalPrepmtInvLineBuf.CopyWithLineNo(PrepmtInvBuf2,SalesLine."Line No.");
              PrepmtInvBuf.InsertInvLineBuffer(PrepmtInvBuf2);
              if SalesSetup."Invoice Rounding" then
                RoundAmounts(
                  SalesHeader,PrepmtInvBuf2,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferDummy);
            end;
          until SalesLine.NEXT = 0;
        if SalesSetup."Invoice Rounding" then
          if InsertInvoiceRounding(
               SalesHeader,PrepmtInvBuf2,TotalPrepmtInvLineBuffer,SalesLine."Line No.")
          then
            PrepmtInvBuf.InsertInvLineBuffer(PrepmtInvBuf2);
      end;
    END;

    [External]
    PROCEDURE BuildInvLineBuffer2@19(SalesHeader@1000 : Record "Sales Header";VAR SalesLine@1001 : Record "Sales Line";DocumentType@1005 : 'Invoice,"Credit Memo",Statistic';VAR PrepmtInvBuf@1002 : Record "Prepayment Inv. Line Buffer");
    BEGIN
      BuildInvLineBuffer(
        SalesHeader,SalesLine,DocumentType,PrepmtInvBuf,false);
    END;

    LOCAL PROCEDURE AdjustInvLineBuffers@41(SalesHeader@1003 : Record "Sales Header";VAR PrepmtInvLineBuf@1002 : Record "Prepayment Inv. Line Buffer";VAR TotalPrepmtInvLineBuf@1001 : Record "Prepayment Inv. Line Buffer";DocumentType@1000 : 'Invoice,"Credit Memo"');
    VAR
      VATAdjustment@1004 : ARRAY [2] OF Decimal;
      VAT@1005 : ',Base,Amount';
    BEGIN
      CalcPrepmtAmtInvLCYInLines(SalesHeader,PrepmtInvLineBuf,DocumentType,VATAdjustment);
      if ABS(VATAdjustment[VAT::Base]) > GLSetup."Amount Rounding Precision" then
        InsertCorrInvLineBuffer(PrepmtInvLineBuf,SalesHeader,VATAdjustment[VAT::Base])
      else
        if (VATAdjustment[VAT::Base] <> 0) or (VATAdjustment[VAT::Amount] <> 0) then begin
          PrepmtInvLineBuf.AdjustVATBase(VATAdjustment);
          TotalPrepmtInvLineBuf.AdjustVATBase(VATAdjustment);
        end;
    END;

    LOCAL PROCEDURE CalcPrepmtAmtInvLCYInLines@42(SalesHeader@1002 : Record "Sales Header";VAR PrepmtInvLineBuf@1003 : Record "Prepayment Inv. Line Buffer";DocumentType@1001 : 'Invoice,"Credit Memo"';VAR VATAdjustment@1006 : ARRAY [2] OF Decimal);
    VAR
      SalesLine@1000 : Record "Sales Line";
      PrepmtInvBufAmount@1004 : ARRAY [2] OF Decimal;
      TotalAmount@1005 : ARRAY [2] OF Decimal;
      LineAmount@1007 : ARRAY [2] OF Decimal;
      Ratio@1010 : ARRAY [2] OF Decimal;
      PrepmtAmtReminder@1008 : ARRAY [2] OF Decimal;
      PrepmtAmountRnded@1009 : ARRAY [2] OF Decimal;
      VAT@1011 : ',Base,Amount';
    BEGIN
      PrepmtInvLineBuf.AmountsToArray(PrepmtInvBufAmount);
      if DocumentType = DocumentType::Invoice then
        ReverseDecArray(PrepmtInvBufAmount);

      TempGlobalPrepmtInvLineBuf.SetFilterOnPKey(PrepmtInvLineBuf);
      TempGlobalPrepmtInvLineBuf.CALCSUMS(Amount,"Amount Incl. VAT");
      TempGlobalPrepmtInvLineBuf.AmountsToArray(TotalAmount);
      for VAT := VAT::Base to VAT::Amount do
        if TotalAmount[VAT] = 0 then
          Ratio[VAT] := 0
        else
          Ratio[VAT] := PrepmtInvBufAmount[VAT] / TotalAmount[VAT];
      if TempGlobalPrepmtInvLineBuf.FINDSET then
        repeat
          TempGlobalPrepmtInvLineBuf.AmountsToArray(LineAmount);
          PrepmtAmountRnded[VAT::Base] :=
            CalcRoundedAmount(LineAmount[VAT::Base],Ratio[VAT::Base],PrepmtAmtReminder[VAT::Base]);
          PrepmtAmountRnded[VAT::Amount] :=
            CalcRoundedAmount(LineAmount[VAT::Amount],Ratio[VAT::Amount],PrepmtAmtReminder[VAT::Amount]);

          SalesLine.GET(SalesHeader."Document Type",SalesHeader."No.",TempGlobalPrepmtInvLineBuf."Line No.");
          if DocumentType = DocumentType::"Credit Memo" then begin
            VATAdjustment[VAT::Base] += SalesLine."Prepmt. Amount Inv. (LCY)" - PrepmtAmountRnded[VAT::Base];
            SalesLine."Prepmt. Amount Inv. (LCY)" := 0;
            VATAdjustment[VAT::Amount] += SalesLine."Prepmt. VAT Amount Inv. (LCY)" - PrepmtAmountRnded[VAT::Amount];
            SalesLine."Prepmt. VAT Amount Inv. (LCY)" := 0;
          end else begin
            SalesLine."Prepmt. Amount Inv. (LCY)" += PrepmtAmountRnded[VAT::Base];
            SalesLine."Prepmt. VAT Amount Inv. (LCY)" += PrepmtAmountRnded[VAT::Amount];
          end;
          SalesLine.MODIFY;
        until TempGlobalPrepmtInvLineBuf.NEXT = 0;
      TempGlobalPrepmtInvLineBuf.DELETEALL;
    END;

    LOCAL PROCEDURE CalcRoundedAmount@51(LineAmount@1000 : Decimal;Ratio@1001 : Decimal;VAR Reminder@1002 : Decimal) RoundedAmount : Decimal;
    VAR
      Amount@1003 : Decimal;
    BEGIN
      Amount := Reminder + LineAmount * Ratio;
      RoundedAmount := ROUND(Amount);
      Reminder := Amount - RoundedAmount;
    END;

    LOCAL PROCEDURE ReverseDecArray@53(VAR DecArray@1000 : ARRAY [2] OF Decimal);
    VAR
      Idx@1001 : Integer;
    BEGIN
      for Idx := 1 to ARRAYLEN(DecArray) do
        DecArray[Idx] := -DecArray[Idx];
    END;

    LOCAL PROCEDURE InsertCorrInvLineBuffer@43(VAR PrepmtInvLineBuf@1001 : Record "Prepayment Inv. Line Buffer";SalesHeader@1002 : Record "Sales Header";VATBaseAdjustment@1003 : Decimal);
    VAR
      NewPrepmtInvLineBuf@1000 : Record "Prepayment Inv. Line Buffer";
      SavedPrepmtInvLineBuf@1004 : Record "Prepayment Inv. Line Buffer";
      AdjmtAmountACY@1005 : Decimal;
    BEGIN
      SavedPrepmtInvLineBuf := PrepmtInvLineBuf;

      if SalesHeader."Currency Code" = '' then
        AdjmtAmountACY := VATBaseAdjustment
      else
        AdjmtAmountACY := 0;

      NewPrepmtInvLineBuf.FillAdjInvLineBuffer(
        PrepmtInvLineBuf,
        GetPrepmtAccNo(PrepmtInvLineBuf."Gen. Bus. Posting Group",PrepmtInvLineBuf."Gen. Prod. Posting Group"),
        VATBaseAdjustment,AdjmtAmountACY);
      PrepmtInvLineBuf.InsertInvLineBuffer(NewPrepmtInvLineBuf);

      NewPrepmtInvLineBuf.FillAdjInvLineBuffer(
        PrepmtInvLineBuf,
        GetCorrBalAccNo(SalesHeader,VATBaseAdjustment > 0),
        -VATBaseAdjustment,-AdjmtAmountACY);
      PrepmtInvLineBuf.InsertInvLineBuffer(NewPrepmtInvLineBuf);

      PrepmtInvLineBuf := SavedPrepmtInvLineBuf;
    END;

    LOCAL PROCEDURE GetPrepmtAccNo@47(GenBusPostingGroup@1000 : Code[20];GenProdPostingGroup@1001 : Code[20]) : Code[20];
    BEGIN
      if (GenBusPostingGroup <> GenPostingSetup."Gen. Bus. Posting Group") or
         (GenProdPostingGroup <> GenPostingSetup."Gen. Prod. Posting Group")
      then
        GenPostingSetup.GET(GenBusPostingGroup,GenProdPostingGroup);
      exit(GenPostingSetup.GetSalesPrepmtAccount);
    END;

    [External]
    PROCEDURE GetCorrBalAccNo@48(SalesHeader@1000 : Record "Sales Header";PositiveAmount@1001 : Boolean) : Code[20];
    VAR
      BalAccNo@1002 : Code[20];
    BEGIN
      if SalesHeader."Currency Code" = '' then
        BalAccNo := GetInvRoundingAccNo(SalesHeader."Customer Posting Group")
      else
        BalAccNo := GetGainLossGLAcc(SalesHeader."Currency Code",PositiveAmount);
      exit(BalAccNo);
    END;

    [External]
    PROCEDURE GetInvRoundingAccNo@49(CustomerPostingGroup@1000 : Code[20]) : Code[20];
    VAR
      CustPostingGr@1002 : Record "Customer Posting Group";
      GLAcc@1001 : Record "G/L Account";
    BEGIN
      CustPostingGr.GET(CustomerPostingGroup);
      GLAcc.GET(CustPostingGr.GetInvRoundingAccount);
      exit(CustPostingGr."Invoice Rounding Account");
    END;

    LOCAL PROCEDURE GetGainLossGLAcc@50(CurrencyCode@1000 : Code[10];PositiveAmount@1002 : Boolean) : Code[20];
    VAR
      Currency@1001 : Record Currency;
    BEGIN
      Currency.GET(CurrencyCode);
      if PositiveAmount then
        exit(Currency.GetRealizedGainsAccount);
      exit(Currency.GetRealizedLossesAccount);
    END;

    LOCAL PROCEDURE GetCurrencyAmountRoundingPrecision@7(CurrencyCode@1000 : Code[10]) : Decimal;
    VAR
      Currency@1001 : Record Currency;
    BEGIN
      Currency.Initialize(CurrencyCode);
      Currency.TESTFIELD("Amount Rounding Precision");
      exit(Currency."Amount Rounding Precision");
    END;

    [External]
    PROCEDURE FillInvLineBuffer@4(SalesHeader@1001 : Record "Sales Header";SalesLine@1002 : Record "Sales Line";VAR PrepmtInvBuf@1000 : Record "Prepayment Inv. Line Buffer");
    BEGIN
      with PrepmtInvBuf do begin
        INIT;
        "G/L Account No." := GetPrepmtAccNo(SalesLine."Gen. Bus. Posting Group",SalesLine."Gen. Prod. Posting Group");

        if not SalesHeader."Compress Prepayment" then begin
          "Line No." := SalesLine."Line No.";
          Description := SalesLine.Description;
        end;

        CopyFromSalesLine(SalesLine);
        FillFromGLAcc(SalesHeader."Compress Prepayment");

        SetAmounts(
          SalesLine."Prepayment Amount",SalesLine."Prepmt. Amt. Incl. VAT",SalesLine."Prepayment Amount",
          SalesLine."Prepayment Amount",SalesLine."Prepayment Amount",SalesLine."Prepayment VAT Difference");

        "VAT Amount" := SalesLine."Prepmt. Amt. Incl. VAT" - SalesLine."Prepayment Amount";
        "VAT Amount (ACY)" := SalesLine."Prepmt. Amt. Incl. VAT" - SalesLine."Prepayment Amount";
      end;
    END;

    LOCAL PROCEDURE InsertInvoiceRounding@25(SalesHeader@1002 : Record "Sales Header";VAR PrepmtInvBuf@1000 : Record "Prepayment Inv. Line Buffer";TotalPrepmtInvBuf@1001 : Record "Prepayment Inv. Line Buffer";PrevLineNo@1008 : Integer) : Boolean;
    VAR
      SalesLine@1007 : Record "Sales Line";
    BEGIN
      if InitInvoiceRoundingLine(SalesHeader,TotalPrepmtInvBuf."Amount Incl. VAT",SalesLine) then begin
        CreateDimensions(SalesLine);
        with PrepmtInvBuf do begin
          INIT;
          "Line No." := PrevLineNo + 10000;
          "Invoice Rounding" := true;
          "G/L Account No." := SalesLine."No.";

          CopyFromSalesLine(SalesLine);
          "Gen. Bus. Posting Group" := SalesHeader."Gen. Bus. Posting Group";
          "VAT Bus. Posting Group" := SalesHeader."VAT Bus. Posting Group";

          SetAmounts(
            SalesLine."Line Amount",SalesLine."Amount Including VAT",SalesLine."Line Amount",
            SalesLine."Prepayment Amount",SalesLine."Line Amount",0);

          "VAT Amount" := SalesLine."Amount Including VAT" - SalesLine."Line Amount";
          "VAT Amount (ACY)" := SalesLine."Amount Including VAT" - SalesLine."Line Amount";
        end;
        exit(true);
      end;
    END;

    LOCAL PROCEDURE InitInvoiceRoundingLine@29(SalesHeader@1000 : Record "Sales Header";TotalAmount@1004 : Decimal;VAR SalesLine@1001 : Record "Sales Line") : Boolean;
    VAR
      Currency@1007 : Record Currency;
      InvoiceRoundingAmount@1002 : Decimal;
    BEGIN
      Currency.Initialize(SalesHeader."Currency Code");
      Currency.TESTFIELD("Invoice Rounding Precision");
      InvoiceRoundingAmount :=
        -ROUND(
          TotalAmount -
          ROUND(
            TotalAmount,
            Currency."Invoice Rounding Precision",
            Currency.InvoiceRoundingDirection),
          Currency."Amount Rounding Precision");

      if InvoiceRoundingAmount = 0 then
        exit(false);

      with SalesLine do begin
        SetHideValidationDialog(true);
        "Document Type" := SalesHeader."Document Type";
        "Document No." := SalesHeader."No.";
        "System-Created Entry" := true;
        Type := Type::"G/L Account";
        VALIDATE("No.",GetInvRoundingAccNo(SalesHeader."Customer Posting Group"));
        VALIDATE(Quantity,1);
        if SalesHeader."Prices Including VAT" then
          VALIDATE("Unit Price",InvoiceRoundingAmount)
        else
          VALIDATE(
            "Unit Price",
            ROUND(
              InvoiceRoundingAmount /
              (1 + (1 - SalesHeader."VAT Base Discount %" / 100) * "VAT %" / 100),
              Currency."Amount Rounding Precision"));
        "Prepayment Amount" := "Unit Price";
        VALIDATE("Amount Including VAT",InvoiceRoundingAmount);
      end;
      exit(true);
    END;

    LOCAL PROCEDURE CopyCommentLines@22(FromNumber@1002 : Code[20];ToDocType@1000 : Integer;ToNumber@1003 : Code[20]);
    VAR
      SalesCommentLine@1004 : Record "Sales Comment Line";
    BEGIN
      with SalesCommentLine do
        case ToDocType of
          DATABASE::"Sales Invoice Header":
            CopyComments("Document Type"::Order,"Document Type"::"Posted Invoice",FromNumber,ToNumber);
          DATABASE::"Sales Cr.Memo Header":
            CopyComments("Document Type"::Order,"Document Type"::"Posted Credit Memo",FromNumber,ToNumber);
        end;
    END;

    LOCAL PROCEDURE InsertExtendedText@17(TabNo@1000 : Integer;DocNo@1003 : Code[20];GLAccNo@1006 : Code[20];DocDate@1001 : Date;LanguageCode@1009 : Code[10];VAR PrevLineNo@1002 : Integer);
    VAR
      TempExtTextLine@1005 : TEMPORARY Record "Extended Text Line";
      SalesInvLine@1007 : Record "Sales Invoice Line";
      SalesCrMemoLine@1008 : Record "Sales Cr.Memo Line";
      TransferExtText@1004 : Codeunit "Transfer Extended Text";
      NextLineNo@1010 : Integer;
    BEGIN
      TransferExtText.PrepmtGetAnyExtText(GLAccNo,TabNo,DocDate,LanguageCode,TempExtTextLine);
      if TempExtTextLine.FIND('-') then begin
        NextLineNo := PrevLineNo + 10000;
        repeat
          case TabNo of
            DATABASE::"Sales Invoice Line":
              begin
                SalesInvLine.INIT;
                SalesInvLine."Document No." := DocNo;
                SalesInvLine."Line No." := NextLineNo;
                SalesInvLine.Description := TempExtTextLine.Text;
                SalesInvLine.INSERT;
              end;
            DATABASE::"Sales Cr.Memo Line":
              begin
                SalesCrMemoLine.INIT;
                SalesCrMemoLine."Document No." := DocNo;
                SalesCrMemoLine."Line No." := NextLineNo;
                SalesCrMemoLine.Description := TempExtTextLine.Text;
                SalesCrMemoLine.INSERT;
              end;
          end;
          PrevLineNo := NextLineNo;
          NextLineNo := NextLineNo + 10000;
        until TempExtTextLine.NEXT = 0;
      end;
    END;

    [External]
    PROCEDURE UpdateVATOnLines@36(SalesHeader@1001 : Record "Sales Header";VAR SalesLine@1011 : Record "Sales Line";VAR VATAmountLine@1003 : Record "VAT Amount Line";DocumentType@1000 : 'Invoice,"Credit Memo",Statistic');
    VAR
      TempVATAmountLineRemainder@1004 : TEMPORARY Record "VAT Amount Line";
      Currency@1005 : Record Currency;
      PrepmtAmt@1002 : Decimal;
      NewAmount@1006 : Decimal;
      NewAmountIncludingVAT@1007 : Decimal;
      NewVATBaseAmount@1008 : Decimal;
      VATAmount@1009 : Decimal;
      VATDifference@1010 : Decimal;
      PrepmtAmtToInvTotal@1013 : Decimal;
    BEGIN
      Currency.Initialize(SalesHeader."Currency Code");

      with SalesLine do begin
        ApplyFilter(SalesHeader,DocumentType,SalesLine);
        LOCKTABLE;
        CALCSUMS("Prepmt. Line Amount","Prepmt. Amt. Inv.");
        PrepmtAmtToInvTotal := "Prepmt. Line Amount" - "Prepmt. Amt. Inv.";
        if FINDSET then
          repeat
            PrepmtAmt := PrepmtAmount(SalesLine,DocumentType);
            if PrepmtAmt <> 0 then begin
              VATAmountLine.GET(
                "Prepayment VAT Identifier",
                "Prepmt. VAT Calc. Type",
                "Prepayment Tax Group Code",
                false,
                PrepmtAmt >= 0);
              if VATAmountLine.Modified then begin
                if not TempVATAmountLineRemainder.GET(
                     "Prepayment VAT Identifier",
                     "Prepmt. VAT Calc. Type",
                     "Prepayment Tax Group Code",
                     false,
                     PrepmtAmt >= 0)
                then begin
                  TempVATAmountLineRemainder := VATAmountLine;
                  TempVATAmountLineRemainder.INIT;
                  TempVATAmountLineRemainder.INSERT;
                end;

                if SalesHeader."Prices Including VAT" then begin
                  if PrepmtAmt = 0 then begin
                    VATAmount := 0;
                    NewAmountIncludingVAT := 0;
                  end else begin
                    VATAmount :=
                      TempVATAmountLineRemainder."VAT Amount" +
                      VATAmountLine."VAT Amount" * PrepmtAmt / VATAmountLine."Line Amount";
                    NewAmountIncludingVAT :=
                      TempVATAmountLineRemainder."Amount Including VAT" +
                      VATAmountLine."Amount Including VAT" * PrepmtAmt / VATAmountLine."Line Amount";
                  end;
                  NewAmount :=
                    ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision") -
                    ROUND(VATAmount,Currency."Amount Rounding Precision");
                  NewVATBaseAmount :=
                    ROUND(
                      NewAmount * (1 - SalesHeader."VAT Base Discount %" / 100),
                      Currency."Amount Rounding Precision");
                end else begin
                  if "VAT Calculation Type" = "VAT Calculation Type"::"Full VAT" then begin
                    VATAmount := PrepmtAmt;
                    NewAmount := 0;
                    NewVATBaseAmount := 0;
                  end else begin
                    NewAmount := PrepmtAmt;
                    NewVATBaseAmount :=
                      ROUND(
                        NewAmount * (1 - SalesHeader."VAT Base Discount %" / 100),
                        Currency."Amount Rounding Precision");
                    if VATAmountLine."VAT Base" = 0 then
                      VATAmount := 0
                    else
                      VATAmount :=
                        TempVATAmountLineRemainder."VAT Amount" +
                        VATAmountLine."VAT Amount" * NewAmount / VATAmountLine."VAT Base";
                  end;
                  NewAmountIncludingVAT := NewAmount + ROUND(VATAmount,Currency."Amount Rounding Precision");
                end;

                "Prepayment Amount" := NewAmount;
                "Prepmt. Amt. Incl. VAT" :=
                  ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision");
                "Prepmt. VAT Base Amt." := NewVATBaseAmount;

                if (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount") = 0 then
                  VATDifference := 0
                else
                  if PrepmtAmtToInvTotal = 0 then
                    VATDifference :=
                      VATAmountLine."VAT Difference" * ("Prepmt. Line Amount" - "Prepmt. Amt. Inv.") /
                      (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount")
                  else
                    VATDifference :=
                      VATAmountLine."VAT Difference" * ("Prepmt. Line Amount" - "Prepmt. Amt. Inv.") /
                      PrepmtAmtToInvTotal;

                "Prepayment VAT Difference" := ROUND(VATDifference,Currency."Amount Rounding Precision");

                MODIFY;

                TempVATAmountLineRemainder."Amount Including VAT" :=
                  NewAmountIncludingVAT - ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision");
                TempVATAmountLineRemainder."VAT Amount" := VATAmount - NewAmountIncludingVAT + NewAmount;
                TempVATAmountLineRemainder."VAT Difference" := VATDifference - "Prepayment VAT Difference";
                TempVATAmountLineRemainder.MODIFY;
              end;
            end;
          until NEXT = 0;
      end;
    END;

    [External]
    PROCEDURE CalcVATAmountLines@35(VAR SalesHeader@1001 : Record "Sales Header";VAR SalesLine@1009 : Record "Sales Line";VAR VATAmountLine@1003 : Record "VAT Amount Line";DocumentType@1008 : 'Invoice,"Credit Memo",Statistic');
    VAR
      Currency@1004 : Record Currency;
      NewAmount@1002 : Decimal;
      NewPrepmtVATDiffAmt@1010 : Decimal;
    BEGIN
      Currency.Initialize(SalesHeader."Currency Code");

      VATAmountLine.DELETEALL;

      with SalesLine do begin
        ApplyFilter(SalesHeader,DocumentType,SalesLine);
        if FIND('-') then
          repeat
            NewAmount := PrepmtAmount(SalesLine,DocumentType);
            if NewAmount <> 0 then begin
              if DocumentType = DocumentType::Invoice then
                NewAmount := "Prepmt. Line Amount";
              if "Prepmt. VAT Calc. Type" in
                 ["VAT Calculation Type"::"Reverse Charge VAT","VAT Calculation Type"::"Sales Tax"]
              then
                "VAT %" := 0;
              if not VATAmountLine.GET(
                   "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",false,NewAmount >= 0)
              then
                VATAmountLine.InsertNewLine(
                  "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",false,
                  "Prepayment VAT %",NewAmount >= 0,true);

              VATAmountLine."Line Amount" := VATAmountLine."Line Amount" + NewAmount;
              NewPrepmtVATDiffAmt := PrepmtVATDiffAmount(SalesLine,DocumentType);
              if DocumentType = DocumentType::Invoice then
                NewPrepmtVATDiffAmt := "Prepayment VAT Difference" + "Prepmt VAT Diff. to Deduct" +
                  "Prepmt VAT Diff. Deducted";
              VATAmountLine."VAT Difference" := VATAmountLine."VAT Difference" + NewPrepmtVATDiffAmt;
              VATAmountLine.MODIFY;
            end;
          until NEXT = 0;
      end;

      VATAmountLine.UpdateLines(
        NewAmount,Currency,SalesHeader."Currency Factor",SalesHeader."Prices Including VAT",
        SalesHeader."VAT Base Discount %",SalesHeader."Tax Area Code",SalesHeader."Tax Liable",SalesHeader."Posting Date");
    END;

    [External]
    PROCEDURE SumPrepmt@15(SalesHeader@1000 : Record "Sales Header";VAR SalesLine@1011 : Record "Sales Line";VAR VATAmountLine@1003 : Record "VAT Amount Line";VAR TotalAmount@1001 : Decimal;VAR TotalVATAmount@1002 : Decimal;VAR VATAmountText@1004 : Text[30]);
    VAR
      PrepmtInvBuf@1005 : TEMPORARY Record "Prepayment Inv. Line Buffer";
      TotalPrepmtBuf@1007 : Record "Prepayment Inv. Line Buffer";
      TotalPrepmtBufLCY@1008 : Record "Prepayment Inv. Line Buffer";
      DifVATPct@1009 : Boolean;
      PrevVATPct@1010 : Decimal;
    BEGIN
      CalcVATAmountLines(SalesHeader,SalesLine,VATAmountLine,2);
      UpdateVATOnLines(SalesHeader,SalesLine,VATAmountLine,2);
      BuildInvLineBuffer(SalesHeader,SalesLine,2,PrepmtInvBuf,false);
      if PrepmtInvBuf.FIND('-') then begin
        PrevVATPct := PrepmtInvBuf."VAT %";
        repeat
          RoundAmounts(SalesHeader,PrepmtInvBuf,TotalPrepmtBuf,TotalPrepmtBufLCY);
          if PrepmtInvBuf."VAT %" <> PrevVATPct then
            DifVATPct := true;
        until PrepmtInvBuf.NEXT = 0;
      end;

      TotalAmount := TotalPrepmtBuf.Amount;
      TotalVATAmount := TotalPrepmtBuf."VAT Amount";
      if DifVATPct or (PrepmtInvBuf."VAT %" = 0) then
        VATAmountText := Text014
      else
        VATAmountText := STRSUBSTNO(Text015,PrevVATPct);
    END;

    [External]
    PROCEDURE GetSalesLines@16(SalesHeader@1000 : Record "Sales Header";DocumentType@1003 : 'Invoice,"Credit Memo",Statistic';VAR ToSalesLine@1001 : Record "Sales Line");
    VAR
      SalesSetup@1004 : Record "Sales & Receivables Setup";
      FromSalesLine@1002 : Record "Sales Line";
      InvRoundingSalesLine@1007 : Record "Sales Line";
      TempVATAmountLine@1005 : TEMPORARY Record "VAT Amount Line";
      TotalAmt@1006 : Decimal;
      NextLineNo@1008 : Integer;
    BEGIN
      ApplyFilter(SalesHeader,DocumentType,FromSalesLine);
      if FromSalesLine.FIND('-') then begin
        repeat
          ToSalesLine := FromSalesLine;
          ToSalesLine.INSERT;
        until FromSalesLine.NEXT = 0;

        SalesSetup.GET;
        if SalesSetup."Invoice Rounding" then begin
          CalcVATAmountLines(SalesHeader,ToSalesLine,TempVATAmountLine,2);
          UpdateVATOnLines(SalesHeader,ToSalesLine,TempVATAmountLine,2);
          ToSalesLine.CALCSUMS("Prepmt. Amt. Incl. VAT");
          TotalAmt := ToSalesLine."Prepmt. Amt. Incl. VAT";
          ToSalesLine.FINDLAST;
          if InitInvoiceRoundingLine(SalesHeader,TotalAmt,InvRoundingSalesLine) then
            with ToSalesLine do begin
              NextLineNo := "Line No." + 1;
              ToSalesLine := InvRoundingSalesLine;
              "Line No." := NextLineNo;

              if DocumentType <> DocumentType::"Credit Memo" then
                "Prepmt. Line Amount" := "Line Amount"
              else
                "Prepmt. Amt. Inv." := "Line Amount";
              "Prepmt. VAT Calc. Type" := "VAT Calculation Type";
              "Prepayment VAT Identifier" := "VAT Identifier";
              "Prepayment Tax Group Code" := "Tax Group Code";
              "Prepayment VAT Identifier" := "VAT Identifier";
              "Prepayment Tax Group Code" := "Tax Group Code";
              "Prepayment VAT %" := "VAT %";
              INSERT;
            end;
        end;
      end;
    END;

    LOCAL PROCEDURE CheckDim@34(SalesHeader@1001 : Record "Sales Header");
    VAR
      SalesLine@1003 : Record "Sales Line";
    BEGIN
      SalesLine."Line No." := 0;
      CheckDimValuePosting(SalesHeader,SalesLine);
      CheckDimComb(SalesHeader,SalesLine);

      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine.SETFILTER(Type,'<>%1',SalesLine.Type::" ");
      if SalesLine.FIND('-') then
        repeat
          CheckDimComb(SalesHeader,SalesLine);
          CheckDimValuePosting(SalesHeader,SalesLine);
        until SalesLine.NEXT = 0;
    END;

    LOCAL PROCEDURE ApplyFilter@20(SalesHeader@1000 : Record "Sales Header";DocumentType@1001 : 'Invoice,"Credit Memo",Statistic';VAR SalesLine@1002 : Record "Sales Line");
    BEGIN
      with SalesLine do begin
        RESET;
        SETRANGE("Document Type",SalesHeader."Document Type");
        SETRANGE("Document No.",SalesHeader."No.");
        SETFILTER(Type,'<>%1',Type::" ");
        if DocumentType in [DocumentType::Invoice,DocumentType::Statistic] then
          SETFILTER("Prepmt. Line Amount",'<>0')
        else
          SETFILTER("Prepmt. Amt. Inv.",'<>0');
      end;
    END;

    [External]
    PROCEDURE PrepmtAmount@12(SalesLine@1000 : Record "Sales Line";DocumentType@1001 : 'Invoice,"Credit Memo",Statistic') : Decimal;
    BEGIN
      with SalesLine do
        case DocumentType of
          DocumentType::Statistic:
            exit("Prepmt. Line Amount");
          DocumentType::Invoice:
            exit("Prepmt. Line Amount" - "Prepmt. Amt. Inv.");
          else
            exit("Prepmt. Amt. Inv." - "Prepmt Amt Deducted");
        end;
    END;

    LOCAL PROCEDURE CheckDimComb@30(SalesHeader@1001 : Record "Sales Header";SalesLine@1000 : Record "Sales Line");
    VAR
      DimMgt@1002 : Codeunit DimensionManagement;
    BEGIN
      if SalesLine."Line No." = 0 then
        if not DimMgt.CheckDimIDComb(SalesHeader."Dimension Set ID") then
          ERROR(Text007,SalesHeader."Document Type",SalesHeader."No.",DimMgt.GetDimCombErr);

      if SalesLine."Line No." <> 0 then
        if not DimMgt.CheckDimIDComb(SalesLine."Dimension Set ID") then
          ERROR(Text008,SalesHeader."Document Type",SalesHeader."No.",SalesLine."Line No.",DimMgt.GetDimCombErr);
    END;

    LOCAL PROCEDURE CheckDimValuePosting@28(SalesHeader@1004 : Record "Sales Header";VAR SalesLine@1000 : Record "Sales Line");
    VAR
      DimMgt@1005 : Codeunit DimensionManagement;
      TableIDArr@1002 : ARRAY [10] OF Integer;
      NumberArr@1003 : ARRAY [10] OF Code[20];
    BEGIN
      if SalesLine."Line No." = 0 then begin
        TableIDArr[1] := DATABASE::Customer;
        NumberArr[1] := SalesHeader."Bill-to Customer No.";
        TableIDArr[2] := DATABASE::Job;
        // NumberArr[2] := SalesHeader."Job No.";
        TableIDArr[3] := DATABASE::"Salesperson/Purchaser";
        NumberArr[3] := SalesHeader."Salesperson Code";
        TableIDArr[4] := DATABASE::Campaign;
        NumberArr[4] := SalesHeader."Campaign No.";
        TableIDArr[5] := DATABASE::"Responsibility Center";
        NumberArr[5] := SalesHeader."Responsibility Center";
        if not DimMgt.CheckDimValuePosting(TableIDArr,NumberArr,SalesHeader."Dimension Set ID") then
          ERROR(
            Text009,
            SalesHeader."Document Type",SalesHeader."No.",DimMgt.GetDimValuePostingErr);
      end else begin
        TableIDArr[1] := DimMgt.TypeToTableID3(SalesLine.Type);
        NumberArr[1] := SalesLine."No.";
        TableIDArr[2] := DATABASE::Job;
        NumberArr[2] := SalesLine."Job No.";
        if not DimMgt.CheckDimValuePosting(TableIDArr,NumberArr,SalesLine."Dimension Set ID") then
          ERROR(
            Text010,
            SalesHeader."Document Type",SalesHeader."No.",SalesLine."Line No.",DimMgt.GetDimValuePostingErr);
      end;
    END;

    LOCAL PROCEDURE PostPrepmtInvLineBuffer@38(SalesHeader@1000 : Record "Sales Header";PrepmtInvLineBuffer@1003 : Record "Prepayment Inv. Line Buffer";DocumentType@1008 : 'Invoice,"Credit Memo"';PostingDescription@1007 : Text[50];DocType@1006 : Option;DocNo@1005 : Code[20];ExtDocNo@1004 : Text[35];SrcCode@1002 : Code[10];PostingNoSeriesCode@1009 : Code[20]);
    VAR
      GenJnlLine@1001 : Record "Gen. Journal Line";
    BEGIN
      with GenJnlLine do begin
        InitNewLine(
          SalesHeader."Posting Date",SalesHeader."Document Date",PostingDescription,
          PrepmtInvLineBuffer."Global Dimension 1 Code",PrepmtInvLineBuffer."Global Dimension 2 Code",
          PrepmtInvLineBuffer."Dimension Set ID",SalesHeader."Reason Code");

        CopyDocumentFields(DocType,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode);
        CopyFromSalesHeaderPrepmt(SalesHeader);
        CopyFromPrepmtInvoiceBuffer(PrepmtInvLineBuffer);

        if not PrepmtInvLineBuffer.Adjustment then
          "Gen. Posting Type" := "Gen. Posting Type"::Sale;
        Correction :=
          (DocumentType = DocumentType::"Credit Memo") and GLSetup."Mark Cr. Memos as Corrections";

        RunGenJnlPostLine(GenJnlLine);
      end;
    END;

    LOCAL PROCEDURE PostCustomerEntry@33(SalesHeader@1000 : Record "Sales Header";TotalPrepmtInvLineBuffer@1008 : Record "Prepayment Inv. Line Buffer";TotalPrepmtInvLineBufferLCY@1009 : Record "Prepayment Inv. Line Buffer";DocumentType@1010 : 'Invoice,"Credit Memo"';PostingDescription@1002 : Text[50];DocType@1003 : Option;DocNo@1004 : Code[20];ExtDocNo@1005 : Text[35];SrcCode@1006 : Code[10];PostingNoSeriesCode@1007 : Code[20];CalcPmtDisc@1011 : Boolean);
    VAR
      GenJnlLine@1001 : Record "Gen. Journal Line";
    BEGIN
      with GenJnlLine do begin
        InitNewLine(
          SalesHeader."Posting Date",SalesHeader."Document Date",PostingDescription,
          SalesHeader."Shortcut Dimension 1 Code",SalesHeader."Shortcut Dimension 2 Code",
          SalesHeader."Dimension Set ID",SalesHeader."Reason Code");

        CopyDocumentFields(DocType,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode);

        CopyFromSalesHeaderPrepmtPost(SalesHeader,(DocumentType = DocumentType::Invoice) or CalcPmtDisc);

        Amount := -TotalPrepmtInvLineBuffer."Amount Incl. VAT";
        "Source Currency Amount" := -TotalPrepmtInvLineBuffer."Amount Incl. VAT";
        "Amount (LCY)" := -TotalPrepmtInvLineBufferLCY."Amount Incl. VAT";
        "Sales/Purch. (LCY)" := -TotalPrepmtInvLineBufferLCY.Amount;
        "Profit (LCY)" := -TotalPrepmtInvLineBufferLCY.Amount;

        Correction := (DocumentType = DocumentType::"Credit Memo") and GLSetup."Mark Cr. Memos as Corrections";

        GenJnlPostLine.RunWithCheck(GenJnlLine);
      end;
    END;

    LOCAL PROCEDURE PostBalancingEntry@40(SalesHeader@1000 : Record "Sales Header";TotalPrepmtInvLineBuffer@1003 : Record "Prepayment Inv. Line Buffer";TotalPrepmtInvLineBufferLCY@1004 : Record "Prepayment Inv. Line Buffer";CustLedgEntry@1002 : Record "Cust. Ledger Entry";DocumentType@1011 : 'Invoice,"Credit Memo"';PostingDescription@1010 : Text[50];DocType@1009 : Option;DocNo@1008 : Code[20];ExtDocNo@1007 : Text[35];SrcCode@1006 : Code[10];PostingNoSeriesCode@1005 : Code[20]);
    VAR
      GenJnlLine@1001 : Record "Gen. Journal Line";
    BEGIN
      with GenJnlLine do begin
        InitNewLine(
          SalesHeader."Posting Date",SalesHeader."Document Date",PostingDescription,
          SalesHeader."Shortcut Dimension 1 Code",SalesHeader."Shortcut Dimension 2 Code",
          SalesHeader."Dimension Set ID",SalesHeader."Reason Code");

        if DocType = "Document Type"::"Credit Memo" then
          CopyDocumentFields("Document Type"::Refund,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode)
        else
          CopyDocumentFields("Document Type"::Payment,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode);

        CopyFromSalesHeaderPrepmtPost(SalesHeader,false);
        if SalesHeader."Bal. Account Type" = SalesHeader."Bal. Account Type"::"Bank Account" then
          "Bal. Account Type" := "Bal. Account Type"::"Bank Account";
        "Bal. Account No." := SalesHeader."Bal. Account No.";

        Amount := TotalPrepmtInvLineBuffer."Amount Incl. VAT" + CustLedgEntry."Remaining Pmt. Disc. Possible";
        "Source Currency Amount" := Amount;
        CustLedgEntry.CALCFIELDS(Amount);
        if CustLedgEntry.Amount = 0 then
          "Amount (LCY)" := TotalPrepmtInvLineBufferLCY."Amount Incl. VAT"
        else
          "Amount (LCY)" :=
            TotalPrepmtInvLineBufferLCY."Amount Incl. VAT" +
            ROUND(
              CustLedgEntry."Remaining Pmt. Disc. Possible" / CustLedgEntry."Adjusted Currency Factor");

        Correction := (DocumentType = DocumentType::"Credit Memo") and GLSetup."Mark Cr. Memos as Corrections";

        "Applies-to Doc. Type" := DocType;
        "Applies-to Doc. No." := DocNo;

        GenJnlPostLine.RunWithCheck(GenJnlLine);
      end;
    END;

    LOCAL PROCEDURE RunGenJnlPostLine@23(VAR GenJnlLine@1000 : Record "Gen. Journal Line");
    BEGIN
      GenJnlPostLine.RunWithCheck(GenJnlLine);
    END;

    [External]
    PROCEDURE UpdatePrepmtAmountOnSaleslines@13(SalesHeader@1001 : Record "Sales Header";NewTotalPrepmtAmount@1000 : Decimal);
    VAR
      Currency@1004 : Record Currency;
      SalesLine@1002 : Record "Sales Line";
      TotalLineAmount@1003 : Decimal;
      TotalPrepmtAmount@1008 : Decimal;
      TotalPrepmtAmtInv@1010 : Decimal;
      LastLineNo@1009 : Integer;
    BEGIN
      Currency.Initialize(SalesHeader."Currency Code");

      with SalesLine do begin
        SETRANGE("Document Type",SalesHeader."Document Type");
        SETRANGE("Document No.",SalesHeader."No.");
        SETFILTER(Type,'<>%1',Type::" ");
        SETFILTER("Line Amount",'<>0');
        SETFILTER("Prepayment %",'<>0');
        LOCKTABLE;
        if FIND('-') then
          repeat
            TotalLineAmount := TotalLineAmount + "Line Amount";
            TotalPrepmtAmtInv := TotalPrepmtAmtInv + "Prepmt. Amt. Inv.";
            LastLineNo := "Line No.";
          until NEXT = 0
        else
          ERROR(Text017,FIELDCAPTION("Prepayment %"));
        if TotalLineAmount = 0 then
          ERROR(Text013,NewTotalPrepmtAmount);
        if not (NewTotalPrepmtAmount in [TotalPrepmtAmtInv ..TotalLineAmount]) then
          ERROR(Text016,TotalPrepmtAmtInv,TotalLineAmount);
        if FIND('-') then
          repeat
            if "Line No." <> LastLineNo then
              VALIDATE(
                "Prepmt. Line Amount",
                ROUND(
                  NewTotalPrepmtAmount * "Line Amount" / TotalLineAmount,
                  Currency."Amount Rounding Precision"))
            else
              VALIDATE("Prepmt. Line Amount",NewTotalPrepmtAmount - TotalPrepmtAmount);
            TotalPrepmtAmount := TotalPrepmtAmount + "Prepmt. Line Amount";
            MODIFY;
          until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CreateDimensions@26(VAR SalesLine@1009 : Record "Sales Line");
    VAR
      SourceCodeSetup@1006 : Record "Source Code Setup";
      DimMgt@1010 : Codeunit DimensionManagement;
      TableID@1007 : ARRAY [10] OF Integer;
      No@1008 : ARRAY [10] OF Code[20];
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := DATABASE::"G/L Account";
      No[1] := SalesLine."No.";
      TableID[2] := DATABASE::Job;
      No[2] := SalesLine."Job No.";
      TableID[3] := DATABASE::"Responsibility Center";
      No[3] := SalesLine."Responsibility Center";
      SalesLine."Shortcut Dimension 1 Code" := '';
      SalesLine."Shortcut Dimension 2 Code" := '';
      SalesLine."Dimension Set ID" :=
        DimMgt.GetRecDefaultDimID(
          SalesLine,0,TableID,No,SourceCodeSetup.Sales,
          SalesLine."Shortcut Dimension 1 Code",SalesLine."Shortcut Dimension 2 Code",SalesLine."Dimension Set ID",DATABASE::Customer);
    END;

    LOCAL PROCEDURE PrepmtDocTypeToDocType@21(DocumentType@1000 : 'Invoice,"Credit Memo"') : Integer;
    BEGIN
      case DocumentType of
        DocumentType::Invoice:
          exit(2);
        DocumentType::"Credit Memo":
          exit(3);
      end;
      exit(2);
    END;

    [External]
    PROCEDURE GetSalesLinesToDeduct@24(SalesHeader@1000 : Record "Sales Header";VAR SalesLines@1002 : Record "Sales Line");
    VAR
      SalesLine@1001 : Record "Sales Line";
    BEGIN
      ApplyFilter(SalesHeader,1,SalesLine);
      if SalesLine.FINDSET then
        repeat
          if (PrepmtAmount(SalesLine,0) <> 0) and (PrepmtAmount(SalesLine,1) <> 0) then begin
            SalesLines := SalesLine;
            SalesLines.INSERT;
          end;
        until SalesLine.NEXT = 0;
    END;

    LOCAL PROCEDURE PrepmtVATDiffAmount@37(SalesLine@1000 : Record "Sales Line";DocumentType@1001 : 'Invoice,"Credit Memo",Statistic') : Decimal;
    BEGIN
      with SalesLine do
        case DocumentType of
          DocumentType::Statistic:
            exit("Prepayment VAT Difference");
          DocumentType::Invoice:
            exit("Prepayment VAT Difference");
          else
            exit("Prepmt VAT Diff. to Deduct");
        end;
    END;

    LOCAL PROCEDURE UpdateSalesDocument@8(VAR SalesHeader@1001 : Record "Sales Header";VAR SalesLine@1000 : Record "Sales Line";DocumentType@1002 : 'Invoice,"Credit Memo"';GenJnlLineDocNo@1003 : Code[20]);
    BEGIN
      with SalesHeader do begin
        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type","Document Type");
        SalesLine.SETRANGE("Document No.","No.");
        if DocumentType = DocumentType::Invoice then begin
          "Last Prepayment No." := GenJnlLineDocNo;
          "Prepayment No." := '';
          SalesLine.SETFILTER("Prepmt. Line Amount",'<>0');
          if SalesLine.FINDSET(true) then
            repeat
              if SalesLine."Prepmt. Line Amount" <> SalesLine."Prepmt. Amt. Inv." then begin
                SalesLine."Prepmt. Amt. Inv." := SalesLine."Prepmt. Line Amount";
                SalesLine."Prepmt. Amount Inv. Incl. VAT" := SalesLine."Prepmt. Amt. Incl. VAT";
                SalesLine.CalcPrepaymentToDeduct;
                SalesLine."Prepmt VAT Diff. to Deduct" :=
                  SalesLine."Prepmt VAT Diff. to Deduct" + SalesLine."Prepayment VAT Difference";
                SalesLine."Prepayment VAT Difference" := 0;
                SalesLine.MODIFY;
              end;
            until SalesLine.NEXT = 0;
        end else begin
          "Last Prepmt. Cr. Memo No." := GenJnlLineDocNo;
          "Prepmt. Cr. Memo No." := '';
          SalesLine.SETFILTER("Prepmt. Amt. Inv.",'<>0');
          if SalesLine.FINDSET(true) then
            repeat
              SalesLine."Prepmt. Amt. Inv." := SalesLine."Prepmt Amt Deducted";
              if "Prices Including VAT" then
                SalesLine."Prepmt. Amount Inv. Incl. VAT" := SalesLine."Prepmt. Amt. Inv."
              else
                SalesLine."Prepmt. Amount Inv. Incl. VAT" :=
                  ROUND(
                    SalesLine."Prepmt. Amt. Inv." * (100 + SalesLine."Prepayment VAT %") / 100,
                    GetCurrencyAmountRoundingPrecision(SalesLine."Currency Code"));
              SalesLine."Prepmt. Amt. Incl. VAT" := SalesLine."Prepmt. Amount Inv. Incl. VAT";
              SalesLine."Prepayment Amount" := SalesLine."Prepmt. Amt. Inv.";
              SalesLine."Prepmt Amt to Deduct" := 0;
              SalesLine."Prepmt VAT Diff. to Deduct" := 0;
              SalesLine."Prepayment VAT Difference" := 0;
              SalesLine.MODIFY;
            until SalesLine.NEXT = 0;
        end;
      end;
    END;

    LOCAL PROCEDURE UpdatePostedSalesDocument@102(DocumentType@1002 : 'Invoice,"Credit Memo"';DocumentNo@1004 : Code[20]);
    VAR
      CustLedgerEntry@1000 : Record "Cust. Ledger Entry";
      SalesInvoiceHeader@1001 : Record "Sales Invoice Header";
      SalesCrMemoHeader@1003 : Record "Sales Cr.Memo Header";
    BEGIN
      case DocumentType of
        DocumentType::Invoice:
          begin
            CustLedgerEntry.SETRANGE("Document Type",CustLedgerEntry."Document Type"::Invoice);
            CustLedgerEntry.SETRANGE("Document No.",DocumentNo);
            CustLedgerEntry.FINDFIRST;
            SalesInvoiceHeader.GET(DocumentNo);
            SalesInvoiceHeader."Cust. Ledger Entry No." := CustLedgerEntry."Entry No.";
            SalesInvoiceHeader.MODIFY;
          end;
        DocumentType::"Credit Memo":
          begin
            CustLedgerEntry.SETRANGE("Document Type",CustLedgerEntry."Document Type"::"Credit Memo");
            CustLedgerEntry.SETRANGE("Document No.",DocumentNo);
            CustLedgerEntry.FINDFIRST;
            SalesCrMemoHeader.GET(DocumentNo);
            SalesCrMemoHeader."Cust. Ledger Entry No." := CustLedgerEntry."Entry No.";
            SalesCrMemoHeader.MODIFY;
          end;
      end;
    END;

    LOCAL PROCEDURE InsertSalesInvHeader@11(VAR SalesInvHeader@1001 : Record "Sales Invoice Header";SalesHeader@1002 : Record "Sales Header";PostingDescription@1003 : Text[50];GenJnlLineDocNo@1004 : Code[20];SrcCode@1005 : Code[10];PostingNoSeriesCode@1000 : Code[20]);
    BEGIN
      with SalesHeader do begin
        SalesInvHeader.INIT;
        SalesInvHeader.TRANSFERFIELDS(SalesHeader);
        SalesInvHeader."Posting Description" := PostingDescription;
        SalesInvHeader."Payment Terms Code" := "Prepmt. Payment Terms Code";
        SalesInvHeader."Due Date" := "Prepayment Due Date";
        SalesInvHeader."Pmt. Discount Date" := "Prepmt. Pmt. Discount Date";
        SalesInvHeader."Payment Discount %" := "Prepmt. Payment Discount %";
        SalesInvHeader."No." := GenJnlLineDocNo;
        SalesInvHeader."Pre-Assigned No. Series" := '';
        SalesInvHeader."Source Code" := SrcCode;
        SalesInvHeader."User ID" := USERID;
        SalesInvHeader."No. Printed" := 0;
        SalesInvHeader."Prepayment Invoice" := true;
        SalesInvHeader."Prepayment Order No." := "No.";
        SalesInvHeader."No. Series" := PostingNoSeriesCode;
        SalesInvHeader.INSERT;
      end;
    END;

    LOCAL PROCEDURE InsertSalesInvLine@18(SalesInvHeader@1001 : Record "Sales Invoice Header";LineNo@1004 : Integer;PrepmtInvLineBuffer@1000 : Record "Prepayment Inv. Line Buffer");
    VAR
      SalesInvLine@1002 : Record "Sales Invoice Line";
    BEGIN
      with PrepmtInvLineBuffer do begin
        SalesInvLine.INIT;
        SalesInvLine."Document No." := SalesInvHeader."No.";
        SalesInvLine."Line No." := LineNo;
        SalesInvLine."Sell-to Customer No." := SalesInvHeader."Sell-to Customer No.";
        SalesInvLine."Bill-to Customer No." := SalesInvHeader."Bill-to Customer No.";
        SalesInvLine.Type := SalesInvLine.Type::"G/L Account";
        SalesInvLine."No." := "G/L Account No.";
        SalesInvLine."Posting Date" := SalesInvHeader."Posting Date";
        SalesInvLine."Shortcut Dimension 1 Code" := "Global Dimension 1 Code";
        SalesInvLine."Shortcut Dimension 2 Code" := "Global Dimension 2 Code";
        SalesInvLine."Dimension Set ID" := "Dimension Set ID";
        SalesInvLine.Description := Description;
        SalesInvLine.Quantity := 1;
        if SalesInvHeader."Prices Including VAT" then begin
          SalesInvLine."Unit Price" := "Amount Incl. VAT";
          SalesInvLine."Line Amount" := "Amount Incl. VAT";
        end else begin
          SalesInvLine."Unit Price" := Amount;
          SalesInvLine."Line Amount" := Amount;
        end;
        SalesInvLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        SalesInvLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        SalesInvLine."VAT Bus. Posting Group" := "VAT Bus. Posting Group";
        SalesInvLine."VAT Prod. Posting Group" := "VAT Prod. Posting Group";
        SalesInvLine."VAT %" := "VAT %";
        SalesInvLine.Amount := Amount;
        SalesInvLine."VAT Difference" := "VAT Difference";
        SalesInvLine."Amount Including VAT" := "Amount Incl. VAT";
        SalesInvLine."VAT Calculation Type" := "VAT Calculation Type";
        SalesInvLine."VAT Base Amount" := "VAT Base Amount";
        SalesInvLine."VAT Identifier" := "VAT Identifier";
        SalesInvLine.INSERT;
      end;
    END;

    LOCAL PROCEDURE InsertSalesCrMemoHeader@27(VAR SalesCrMemoHeader@1001 : Record "Sales Cr.Memo Header";SalesHeader@1002 : Record "Sales Header";PostingDescription@1003 : Text[50];GenJnlLineDocNo@1004 : Code[20];SrcCode@1005 : Code[10];PostingNoSeriesCode@1000 : Code[20];CalcPmtDiscOnCrMemos@1006 : Boolean);
    BEGIN
      with SalesHeader do begin
        SalesCrMemoHeader.INIT;
        SalesCrMemoHeader.TRANSFERFIELDS(SalesHeader);
        SalesCrMemoHeader."Payment Terms Code" := "Prepmt. Payment Terms Code";
        SalesCrMemoHeader."Pmt. Discount Date" := "Prepmt. Pmt. Discount Date";
        SalesCrMemoHeader."Payment Discount %" := "Prepmt. Payment Discount %";
        if ("Prepmt. Payment Terms Code" <> '') and not CalcPmtDiscOnCrMemos then begin
          SalesCrMemoHeader."Payment Discount %" := 0;
          SalesCrMemoHeader."Pmt. Discount Date" := 0D;
        end;
        SalesCrMemoHeader."Posting Description" := PostingDescription;
        SalesCrMemoHeader."Due Date" := "Prepayment Due Date";
        SalesCrMemoHeader."No." := GenJnlLineDocNo;
        SalesCrMemoHeader."Pre-Assigned No. Series" := '';
        SalesCrMemoHeader."Source Code" := SrcCode;
        SalesCrMemoHeader."User ID" := USERID;
        SalesCrMemoHeader."No. Printed" := 0;
        SalesCrMemoHeader."Prepayment Credit Memo" := true;
        SalesCrMemoHeader."Prepayment Order No." := "No.";
        SalesCrMemoHeader.Correction := GLSetup."Mark Cr. Memos as Corrections";
        SalesCrMemoHeader."No. Series" := PostingNoSeriesCode;
        SalesCrMemoHeader.INSERT;
      end;
    END;

    LOCAL PROCEDURE InsertSalesCrMemoLine@32(SalesCrMemoHeader@1001 : Record "Sales Cr.Memo Header";LineNo@1002 : Integer;PrepmtInvLineBuffer@1003 : Record "Prepayment Inv. Line Buffer");
    VAR
      SalesCrMemoLine@1000 : Record "Sales Cr.Memo Line";
    BEGIN
      with PrepmtInvLineBuffer do begin
        SalesCrMemoLine.INIT;
        SalesCrMemoLine."Document No." := SalesCrMemoHeader."No.";
        SalesCrMemoLine."Line No." := LineNo;
        SalesCrMemoLine."Sell-to Customer No." := SalesCrMemoHeader."Sell-to Customer No.";
        SalesCrMemoLine."Bill-to Customer No." := SalesCrMemoHeader."Bill-to Customer No.";
        SalesCrMemoLine.Type := SalesCrMemoLine.Type::"G/L Account";
        SalesCrMemoLine."No." := "G/L Account No.";
        SalesCrMemoLine."Posting Date" := SalesCrMemoHeader."Posting Date";
        SalesCrMemoLine."Shortcut Dimension 1 Code" := "Global Dimension 1 Code";
        SalesCrMemoLine."Shortcut Dimension 2 Code" := "Global Dimension 2 Code";
        SalesCrMemoLine."Dimension Set ID" := "Dimension Set ID";
        SalesCrMemoLine.Description := Description;
        SalesCrMemoLine.Quantity := 1;
        if SalesCrMemoHeader."Prices Including VAT" then begin
          SalesCrMemoLine."Unit Price" := "Amount Incl. VAT";
          SalesCrMemoLine."Line Amount" := "Amount Incl. VAT";
        end else begin
          SalesCrMemoLine."Unit Price" := Amount;
          SalesCrMemoLine."Line Amount" := Amount;
        end;
        SalesCrMemoLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        SalesCrMemoLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        SalesCrMemoLine."VAT Bus. Posting Group" := "VAT Bus. Posting Group";
        SalesCrMemoLine."VAT Prod. Posting Group" := "VAT Prod. Posting Group";
        SalesCrMemoLine."VAT %" := "VAT %";
        SalesCrMemoLine.Amount := Amount;
        SalesCrMemoLine."VAT Difference" := "VAT Difference";
        SalesCrMemoLine."Amount Including VAT" := "Amount Incl. VAT";
        SalesCrMemoLine."VAT Calculation Type" := "VAT Calculation Type";
        SalesCrMemoLine."VAT Base Amount" := "VAT Base Amount";
        SalesCrMemoLine."VAT Identifier" := "VAT Identifier";
        SalesCrMemoLine.INSERT;
      end;
    END;

    LOCAL PROCEDURE GetCalcPmtDiscOnCrMemos@14(PrepmtPmtTermsCode@1000 : Code[10]) : Boolean;
    VAR
      PaymentTerms@1001 : Record "Payment Terms";
    BEGIN
      if PrepmtPmtTermsCode = '' then
        exit(false);
      PaymentTerms.GET(PrepmtPmtTermsCode);
      exit(PaymentTerms."Calc. Pmt. Disc. on Cr. Memos");
    END;

    BEGIN
    END.
  }
}

