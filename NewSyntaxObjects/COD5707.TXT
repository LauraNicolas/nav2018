OBJECT Codeunit 5707 TransferOrder-Post + Print
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Transfer Header";
    OnRun=BEGIN
            TransHeader.COPY(Rec);
            Code;
            Rec := TransHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Ship,&Receive';
      TransHeader@1001 : Record "Transfer Header";

    LOCAL PROCEDURE Code@2();
    VAR
      TransLine@1000 : Record "Transfer Line";
      TransferPostShipment@1002 : Codeunit "TransferOrder-Post Shipment";
      TransferPostReceipt@1003 : Codeunit "TransferOrder-Post Receipt";
      DefaultNumber@1001 : Integer;
      Selection@1004 : '" ",Shipment,Receipt';
    BEGIN
      with TransHeader do begin
        TransLine.SETRANGE("Document No.","No.");
        if TransLine.FIND('-') then
          repeat
            if (TransLine."Quantity Shipped" < TransLine.Quantity) and
               (DefaultNumber = 0)
            then
              DefaultNumber := Selection::Shipment;
            if (TransLine."Quantity Received" < TransLine.Quantity) and
               (DefaultNumber = 0)
            then
              DefaultNumber := Selection::Receipt;
          until (TransLine.NEXT = 0) or (DefaultNumber > 0);
        if "Direct Transfer" then begin
          TransferPostShipment.RUN(TransHeader);
          TransferPostReceipt.RUN(TransHeader);
          PrintReport(TransHeader,Selection::Receipt);
        end else begin
          if DefaultNumber = 0 then
            DefaultNumber := Selection::Shipment;
          Selection := STRMENU(Text000,DefaultNumber);
          case Selection of
            0:
              exit;
            Selection::Shipment:
              TransferPostShipment.RUN(TransHeader);
            Selection::Receipt:
              TransferPostReceipt.RUN(TransHeader);
          end;
          PrintReport(TransHeader,Selection);
        end;
      end;
    END;

    [External]
    PROCEDURE PrintReport@1(TransHeaderSource@1001 : Record "Transfer Header";Selection@1000 : '" ",Shipment,Receipt');
    BEGIN
      with TransHeaderSource do
        case Selection of
          Selection::Shipment:
            PrintShipment("Last Shipment No.");
          Selection::Receipt:
            PrintReceipt("Last Receipt No.");
        end;
    END;

    LOCAL PROCEDURE PrintShipment@6(DocNo@1000 : Code[20]);
    VAR
      TransShptHeader@1001 : Record "Transfer Shipment Header";
    BEGIN
      if TransShptHeader.GET(DocNo) then begin
        TransShptHeader.SETRECFILTER;
        TransShptHeader.PrintRecords(false);
      end;
    END;

    LOCAL PROCEDURE PrintReceipt@7(DocNo@1000 : Code[20]);
    VAR
      TransRcptHeader@1001 : Record "Transfer Receipt Header";
    BEGIN
      if TransRcptHeader.GET(DocNo) then begin
        TransRcptHeader.SETRECFILTER;
        TransRcptHeader.PrintRecords(false);
      end;
    END;

    BEGIN
    END.
  }
}

