OBJECT Codeunit 112 Empl. Entry-SetAppl.ID
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Employee Ledger Entry"=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EmplEntryApplID@1000 : Code[50];

    [External]
    PROCEDURE SetApplId@1(VAR EmplLedgEntry@1000 : Record "Employee Ledger Entry";ApplyingEmplLedgEntry@1001 : Record "Employee Ledger Entry";AppliesToID@1005 : Code[50]);
    VAR
      TempEmplLedgEntry@1003 : TEMPORARY Record "Employee Ledger Entry";
      EmplLedgEntryToUpdate@1002 : Record "Employee Ledger Entry";
    BEGIN
      EmplLedgEntry.LOCKTABLE;
      if EmplLedgEntry.FINDSET then begin
        // Make Applies-to ID
        if EmplLedgEntry."Applies-to ID" <> '' then
          EmplEntryApplID := ''
        else begin
          EmplEntryApplID := AppliesToID;
          if EmplEntryApplID = '' then begin
            EmplEntryApplID := USERID;
            if EmplEntryApplID = '' then
              EmplEntryApplID := '***';
          end;
        end;
        repeat
          TempEmplLedgEntry := EmplLedgEntry;
          TempEmplLedgEntry.INSERT;
        until EmplLedgEntry.NEXT = 0;
      end;

      if TempEmplLedgEntry.FINDSET then
        repeat
          EmplLedgEntryToUpdate.COPY(TempEmplLedgEntry);
          EmplLedgEntryToUpdate.TESTFIELD(Open,true);
          EmplLedgEntryToUpdate."Applies-to ID" := EmplEntryApplID;

          if ((EmplLedgEntryToUpdate."Amount to Apply" <> 0) and (EmplEntryApplID = '')) or
             (EmplEntryApplID = '')
          then
            EmplLedgEntryToUpdate."Amount to Apply" := 0
          else
            if EmplLedgEntryToUpdate."Amount to Apply" = 0 then begin
              EmplLedgEntryToUpdate.CALCFIELDS("Remaining Amount");
              if EmplLedgEntryToUpdate."Remaining Amount" <> 0 then
                EmplLedgEntryToUpdate."Amount to Apply" := EmplLedgEntryToUpdate."Remaining Amount";
            end;

          if EmplLedgEntryToUpdate."Entry No." = ApplyingEmplLedgEntry."Entry No." then
            EmplLedgEntryToUpdate."Applying Entry" := ApplyingEmplLedgEntry."Applying Entry";
          EmplLedgEntryToUpdate.MODIFY;
        until TempEmplLedgEntry.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

