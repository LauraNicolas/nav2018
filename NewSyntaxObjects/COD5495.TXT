OBJECT Codeunit 5495 Graph Mgt - Sales Order
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Sales Invoice Header"=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ProcessComplexTypes@1(VAR SalesOrderEntityBuffer@1000 : Record "Sales Order Entity Buffer";BillToAddressJSON@1001 : Text);
    BEGIN
      ParseBillToCustomerAddressFromJSON(BillToAddressJSON,SalesOrderEntityBuffer);
    END;

    PROCEDURE ParseBillToCustomerAddressFromJSON@10(BillToAddressJSON@1000 : Text;VAR SalesOrderEntityBuffer@1002 : Record "Sales Order Entity Buffer");
    VAR
      GraphMgtComplexTypes@1003 : Codeunit "Graph Mgt - Complex Types";
      RecRef@1001 : RecordRef;
    BEGIN
      if BillToAddressJSON <> '' then
        with SalesOrderEntityBuffer do begin
          RecRef.GETTABLE(SalesOrderEntityBuffer);
          GraphMgtComplexTypes.ApplyPostalAddressFromJSON(BillToAddressJSON,RecRef,
            FIELDNO("Sell-to Address"),FIELDNO("Sell-to Address 2"),FIELDNO("Sell-to City"),FIELDNO("Sell-to County"),
            FIELDNO("Sell-to Country/Region Code"),FIELDNO("Sell-to Post Code"));
          RecRef.SETTABLE(SalesOrderEntityBuffer);
        end;
    END;

    PROCEDURE BillToCustomerAddressToJSON@5(SalesOrderEntityBuffer@1000 : Record "Sales Order Entity Buffer") JSON : Text;
    VAR
      GraphMgtComplexTypes@1001 : Codeunit "Graph Mgt - Complex Types";
    BEGIN
      with SalesOrderEntityBuffer do
        GraphMgtComplexTypes.GetPostalAddressJSON("Sell-to Address","Sell-to Address 2",
          "Sell-to City","Sell-to County","Sell-to Country/Region Code","Sell-to Post Code",JSON);
    END;

    PROCEDURE UpdateIntegrationRecordIds@34(OnlyRecordsWithoutID@1007 : Boolean);
    VAR
      DummySalesOrderEntityBuffer@1001 : Record "Sales Order Entity Buffer";
      DummyCustomer@1005 : Record Customer;
      GraphMgtGeneralTools@1008 : Codeunit "Graph Mgt - General Tools";
      SalesHeaderRecordRef@1000 : RecordRef;
      CustomerRecordRef@1004 : RecordRef;
    BEGIN
      CustomerRecordRef.OPEN(DATABASE::Customer);
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        CustomerRecordRef,DummyCustomer.FIELDNO(Id),true);

      SalesHeaderRecordRef.OPEN(DATABASE::"Sales Header");
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        SalesHeaderRecordRef,DummySalesOrderEntityBuffer.FIELDNO(Id),OnlyRecordsWithoutID);
    END;

    LOCAL PROCEDURE CheckSupportedTable@3(VAR RecRef@1000 : RecordRef) : Boolean;
    VAR
      SalesHeader@1002 : Record "Sales Header";
    BEGIN
      if RecRef.NUMBER = DATABASE::"Sales Header" then begin
        RecRef.SETTABLE(SalesHeader);
        exit(SalesHeader."Document Type" = SalesHeader."Document Type"::Order);
      end;
      exit(false);
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnSalesInvoiceHeader@19(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummySalesOrderEntityBuffer@1004 : Record "Sales Order Entity Buffer";
      GraphMgtGeneralTools@1003 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      if not CheckSupportedTable(RecRef) then
        exit;

      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(
        RecRef,NewId,Handled,RecRef.NUMBER,DummySalesOrderEntityBuffer.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@21(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummySalesOrderEntityBuffer@1004 : Record "Sales Order Entity Buffer";
      GraphMgtGeneralTools@1007 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      if not CheckSupportedTable(RecRef) then
        exit;

      GraphMgtGeneralTools.HandleGetPredefinedIdValue(
        Id,RecRef,Handled,RecRef.NUMBER,DummySalesOrderEntityBuffer.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    PROCEDURE HandleApiSetup@2();
    VAR
      GraphMgtSalesOrderBuffer@1001 : Codeunit "Graph Mgt - Sales Order Buffer";
    BEGIN
      UpdateIntegrationRecordIds(false);
      GraphMgtSalesOrderBuffer.UpdateBufferTableRecords;
    END;

    BEGIN
    END.
  }
}

