OBJECT Report 5181 Relocate Attachments
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Relocate Attachments;
    ProcessingOnly=true;
    OnInitReport=BEGIN
                   if not CONFIRM(Text000,true) then
                     CurrReport.QUIT;
                 END;

    OnPreReport=BEGIN
                  RMSetup.GET;
                  NoOfRecords := Attachment.COUNT;
                  Window.OPEN(Text001);
                END;

    UseRequestPage=false;
  }
  DATASET
  {
    { 5264;    ;DataItem;Attachment          ;
               DataItemTable=Attachment;
               DataItemTableView=SORTING("No.");
               OnAfterGetRecord=VAR
                                  FromDiskFileName@1000 : Text[250];
                                  ServerFileName@1001 : Text;
                                BEGIN
                                  LineCount := LineCount + 1;
                                  Window.UPDATE(1,ROUND(LineCount / NoOfRecords * 10000,1));

                                  // Copy DiskFile to DiskFile
                                  if ("Storage Type" = "Storage Type"::"Disk File") and
                                     (RMSetup."Attachment Storage Type" = RMSetup."Attachment Storage Type"::"Disk File")
                                  then begin
                                    RMSetup.TESTFIELD("Attachment Storage Location");
                                    if "Storage Pointer" <> RMSetup."Attachment Storage Location" then begin
                                      FromDiskFileName := ConstDiskFileName;
                                      "Storage Pointer" := RMSetup."Attachment Storage Location";
                                      MODIFY;
                                      FileManagement.CopyServerFile(FromDiskFileName,ConstDiskFileName,false); // Copy from UNC location to another UNC location
                                      COMMIT;
                                      FileManagement.DeleteServerFile(FromDiskFileName);
                                    end;
                                    CurrReport.SKIP;
                                  end;

                                  // Export Embedded Blob to Diskfile
                                  if ("Storage Type" = "Storage Type"::Embedded) and
                                     (RMSetup."Attachment Storage Type" = RMSetup."Attachment Storage Type"::"Disk File")
                                  then begin
                                    RMSetup.TESTFIELD("Attachment Storage Location");
                                    CALCFIELDS("Attachment File");
                                    if "Attachment File".HASVALUE then begin
                                      "Storage Pointer" := RMSetup."Attachment Storage Location";
                                      ServerFileName := ConstDiskFileName;
                                      ExportAttachmentToServerFile(ServerFileName); // Export blob to UNC location
                                      "Storage Type" := "Storage Type"::"Disk File";
                                      CLEAR("Attachment File");
                                      MODIFY;
                                      COMMIT;
                                      CurrReport.SKIP;
                                    end;
                                  end;

                                  // Import DiskFile to Embedded Blob
                                  if ("Storage Type" = "Storage Type"::"Disk File") and
                                     (RMSetup."Attachment Storage Type" = RMSetup."Attachment Storage Type"::Embedded)
                                  then begin
                                    FromDiskFileName := ConstDiskFileName;
                                    ImportAttachmentFromServerFile(GetServerFileName(ConstDiskFileName),false,true); // Import file from UNC location
                                    COMMIT;
                                    FileManagement.DeleteServerFile(FromDiskFileName);
                                    CurrReport.SKIP;
                                  end;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      RMSetup@1000 : Record "Marketing Setup";
      Text000@1001 : TextConst 'ENU=Do you want to relocate existing attachments?';
      FileManagement@1003 : Codeunit "File Management";
      Window@1002 : Dialog;
      Text001@1004 : TextConst 'ENU=Relocating attachments @1@@@@@@@@@@@@@';
      NoOfRecords@1005 : Integer;
      LineCount@1006 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

