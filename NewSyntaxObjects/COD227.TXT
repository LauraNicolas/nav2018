OBJECT Codeunit 227 VendEntry-Apply Posted Entries
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Vendor Ledger Entry";
    EventSubscriberInstance=Manual;
    Permissions=TableData "Vendor Ledger Entry"=rimd;
    OnRun=BEGIN
            if PreviewMode then
              case RunOptionPreviewContext of
                RunOptionPreview::Apply:
                  Apply(Rec,DocumentNoPreviewContext,ApplicationDatePreviewContext);
                RunOptionPreview::Unapply:
                  PostUnApplyVendor(DetailedVendorLedgEntryPreviewContext,DocumentNoPreviewContext,ApplicationDatePreviewContext);
              end
            else
              Apply(Rec,"Document No.",0D);
          END;

  }
  CODE
  {
    VAR
      PostingApplicationMsg@1000 : TextConst 'ENU=Posting application...';
      MustNotBeBeforeErr@1002 : TextConst 'ENU=The posting date entered must not be before the posting date on the vendor ledger entry.';
      NoEntriesAppliedErr@1003 : TextConst '@@@=%1 - Caption of "Applies to ID" field of Gen. Journal Line;ENU=Cannot post because you did not specify which entry to apply. You must specify an entry in the %1 field for one or more open entries.';
      UnapplyPostedAfterThisEntryErr@1018 : TextConst 'ENU=Before you can unapply this entry, you must first unapply all application entries that were posted after this entry.';
      NoApplicationEntryErr@1017 : TextConst 'ENU=Vendor Ledger Entry No. %1 does not have an application entry.';
      UnapplyingMsg@1015 : TextConst 'ENU=Unapplying and posting...';
      UnapplyAllPostedAfterThisEntryErr@1021 : TextConst 'ENU=Before you can unapply this entry, you must first unapply all application entries in Vendor Ledger Entry No. %1 that were posted after this entry.';
      NotAllowedPostingDatesErr@1019 : TextConst 'ENU=Posting date is not within the range of allowed posting dates.';
      LatestEntryMustBeApplicationErr@1023 : TextConst 'ENU=The latest Transaction No. must be an application in Vendor Ledger Entry No. %1.';
      CannotUnapplyExchRateErr@1025 : TextConst 'ENU=You cannot unapply the entry with the posting date %1, because the exchange rate for the additional reporting currency has been changed.';
      CannotUnapplyInReversalErr@1026 : TextConst 'ENU=You cannot unapply Vendor Ledger Entry No. %1 because the entry is part of a reversal.';
      CannotApplyClosedEntriesErr@1102601000 : TextConst 'ENU=One or more of the entries that you selected is closed. You cannot apply closed entries.';
      DetailedVendorLedgEntryPreviewContext@1010 : Record "Detailed Vendor Ledg. Entry";
      ApplicationDatePreviewContext@1009 : Date;
      DocumentNoPreviewContext@1008 : Code[20];
      RunOptionPreview@1007 : 'Apply,Unapply';
      RunOptionPreviewContext@1006 : 'Apply,Unapply';
      PreviewMode@1005 : Boolean;

    [Internal]
    PROCEDURE Apply@17(VendLedgEntry@1000 : Record "Vendor Ledger Entry";DocumentNo@1001 : Code[20];ApplicationDate@1002 : Date);
    VAR
      PaymentToleranceMgt@1011 : Codeunit "Payment Tolerance Management";
    BEGIN
      with VendLedgEntry do begin
        PaymentToleranceMgt.ApplyPostedEntriesMode;
        if not PreviewMode then
          if not PaymentToleranceMgt.PmtTolVend(VendLedgEntry) then
            exit;
        GET("Entry No.");

        if ApplicationDate = 0D then
          ApplicationDate := GetApplicationDate(VendLedgEntry)
        else
          if ApplicationDate < GetApplicationDate(VendLedgEntry) then
            ERROR(MustNotBeBeforeErr);

        if DocumentNo = '' then
          DocumentNo := "Document No.";

        VendPostApplyVendLedgEntry(VendLedgEntry,DocumentNo,ApplicationDate);
      end;
    END;

    [External]
    PROCEDURE GetApplicationDate@14(VendLedgEntry@1000 : Record "Vendor Ledger Entry") ApplicationDate : Date;
    VAR
      ApplyToVendLedgEntry@1001 : Record "Vendor Ledger Entry";
    BEGIN
      with VendLedgEntry do begin
        ApplicationDate := 0D;
        ApplyToVendLedgEntry.SETCURRENTKEY("Vendor No.","Applies-to ID");
        ApplyToVendLedgEntry.SETRANGE("Vendor No.","Vendor No.");
        ApplyToVendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
        ApplyToVendLedgEntry.FIND('-');
        repeat
          if ApplyToVendLedgEntry."Posting Date" > ApplicationDate then
            ApplicationDate := ApplyToVendLedgEntry."Posting Date";
        until ApplyToVendLedgEntry.NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE VendPostApplyVendLedgEntry@12(VendLedgEntry@1000 : Record "Vendor Ledger Entry";DocumentNo@1002 : Code[20];ApplicationDate@1003 : Date);
    VAR
      SourceCodeSetup@1004 : Record "Source Code Setup";
      GenJnlLine@1005 : Record "Gen. Journal Line";
      UpdateAnalysisView@1001 : Codeunit "Update Analysis View";
      GenJnlPostLine@1006 : Codeunit "Gen. Jnl.-Post Line";
      GenJnlPostPreview@1008 : Codeunit "Gen. Jnl.-Post Preview";
      Window@1007 : Dialog;
      EntryNoBeforeApplication@1010 : Integer;
      EntryNoAfterApplication@1009 : Integer;
    BEGIN
      with VendLedgEntry do begin
        Window.OPEN(PostingApplicationMsg);

        SourceCodeSetup.GET;

        GenJnlLine.INIT;
        GenJnlLine."Document No." := DocumentNo;
        GenJnlLine."Posting Date" := ApplicationDate;
        GenJnlLine."Document Date" := GenJnlLine."Posting Date";
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::Vendor;
        GenJnlLine."Account No." := "Vendor No.";
        CALCFIELDS("Debit Amount","Credit Amount","Debit Amount (LCY)","Credit Amount (LCY)");
        GenJnlLine.Correction :=
          ("Debit Amount" < 0) or ("Credit Amount" < 0) or
          ("Debit Amount (LCY)" < 0) or ("Credit Amount (LCY)" < 0);
        GenJnlLine."Document Type" := "Document Type";
        GenJnlLine.Description := Description;
        GenJnlLine."Shortcut Dimension 1 Code" := "Global Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := "Global Dimension 2 Code";
        GenJnlLine."Dimension Set ID" := "Dimension Set ID";
        GenJnlLine."Posting Group" := "Vendor Posting Group";
        GenJnlLine."Source Type" := GenJnlLine."Source Type"::Vendor;
        GenJnlLine."Source No." := "Vendor No.";
        GenJnlLine."Source Code" := SourceCodeSetup."Purchase Entry Application";
        GenJnlLine."System-Created Entry" := true;

        EntryNoBeforeApplication := FindLastApplDtldVendLedgEntry;

        OnBeforePostApplyVendLedgEntry(GenJnlLine,VendLedgEntry);
        GenJnlPostLine.VendPostApplyVendLedgEntry(GenJnlLine,VendLedgEntry);

        EntryNoAfterApplication := FindLastApplDtldVendLedgEntry;
        if EntryNoAfterApplication = EntryNoBeforeApplication then
          ERROR(STRSUBSTNO(NoEntriesAppliedErr,GenJnlLine.FIELDCAPTION("Applies-to ID")));

        if PreviewMode then
          GenJnlPostPreview.ThrowError;

        COMMIT;
        Window.CLOSE;
        UpdateAnalysisView.UpdateAll(0,true);
      end;
    END;

    LOCAL PROCEDURE FindLastApplDtldVendLedgEntry@1() : Integer;
    VAR
      DtldVendLedgEntry@1000 : Record "Detailed Vendor Ledg. Entry";
    BEGIN
      DtldVendLedgEntry.LOCKTABLE;
      if DtldVendLedgEntry.FINDLAST then
        exit(DtldVendLedgEntry."Entry No.");

      exit(0);
    END;

    LOCAL PROCEDURE FindLastApplEntry@2(VendLedgEntryNo@1002 : Integer) : Integer;
    VAR
      DtldVendLedgEntry@1001 : Record "Detailed Vendor Ledg. Entry";
      ApplicationEntryNo@1000 : Integer;
    BEGIN
      with DtldVendLedgEntry do begin
        SETCURRENTKEY("Vendor Ledger Entry No.","Entry Type");
        SETRANGE("Vendor Ledger Entry No.",VendLedgEntryNo);
        SETRANGE("Entry Type","Entry Type"::Application);
        SETRANGE(Unapplied,false);
        ApplicationEntryNo := 0;
        if FIND('-') then
          repeat
            if "Entry No." > ApplicationEntryNo then
              ApplicationEntryNo := "Entry No.";
          until NEXT = 0;
      end;
      exit(ApplicationEntryNo);
    END;

    LOCAL PROCEDURE FindLastTransactionNo@6(VendLedgEntryNo@1002 : Integer) : Integer;
    VAR
      DtldVendLedgEntry@1001 : Record "Detailed Vendor Ledg. Entry";
      LastTransactionNo@1000 : Integer;
    BEGIN
      with DtldVendLedgEntry do begin
        SETCURRENTKEY("Vendor Ledger Entry No.","Entry Type");
        SETRANGE("Vendor Ledger Entry No.",VendLedgEntryNo);
        SETRANGE(Unapplied,false);
        SETFILTER("Entry Type",'<>%1&<>%2',"Entry Type"::"Unrealized Loss","Entry Type"::"Unrealized Gain");
        LastTransactionNo := 0;
        if FINDSET then
          repeat
            if LastTransactionNo < "Transaction No." then
              LastTransactionNo := "Transaction No.";
          until NEXT = 0;
      end;
      exit(LastTransactionNo);
    END;

    [External]
    PROCEDURE UnApplyDtldVendLedgEntry@3(DtldVendLedgEntry@1000 : Record "Detailed Vendor Ledg. Entry");
    VAR
      ApplicationEntryNo@1001 : Integer;
    BEGIN
      DtldVendLedgEntry.TESTFIELD("Entry Type",DtldVendLedgEntry."Entry Type"::Application);
      DtldVendLedgEntry.TESTFIELD(Unapplied,false);
      ApplicationEntryNo := FindLastApplEntry(DtldVendLedgEntry."Vendor Ledger Entry No.");

      if DtldVendLedgEntry."Entry No." <> ApplicationEntryNo then
        ERROR(UnapplyPostedAfterThisEntryErr);
      CheckReversal(DtldVendLedgEntry."Vendor Ledger Entry No.");
      UnApplyVendor(DtldVendLedgEntry);
    END;

    [External]
    PROCEDURE UnApplyVendLedgEntry@7(VendLedgEntryNo@1000 : Integer);
    VAR
      DtldVendLedgEntry@1002 : Record "Detailed Vendor Ledg. Entry";
      ApplicationEntryNo@1001 : Integer;
    BEGIN
      CheckReversal(VendLedgEntryNo);
      ApplicationEntryNo := FindLastApplEntry(VendLedgEntryNo);
      if ApplicationEntryNo = 0 then
        ERROR(NoApplicationEntryErr,VendLedgEntryNo);
      DtldVendLedgEntry.GET(ApplicationEntryNo);
      UnApplyVendor(DtldVendLedgEntry);
    END;

    LOCAL PROCEDURE UnApplyVendor@19(DtldVendLedgEntry@1000000000 : Record "Detailed Vendor Ledg. Entry");
    VAR
      UnapplyVendEntries@1000 : Page "Unapply Vendor Entries";
    BEGIN
      with DtldVendLedgEntry do begin
        TESTFIELD("Entry Type","Entry Type"::Application);
        TESTFIELD(Unapplied,false);
        UnapplyVendEntries.SetDtldVendLedgEntry("Entry No.");
        UnapplyVendEntries.LOOKUPMODE(true);
        UnapplyVendEntries.RUNMODAL;
      end;
    END;

    [Internal]
    PROCEDURE PostUnApplyVendor@4(DtldVendLedgEntry2@1007 : Record "Detailed Vendor Ledg. Entry";DocNo@1000 : Code[20];PostingDate@1001 : Date);
    VAR
      GLEntry@1013 : Record "G/L Entry";
      VendLedgEntry@1004 : Record "Vendor Ledger Entry";
      DtldVendLedgEntry@1010 : Record "Detailed Vendor Ledg. Entry";
      SourceCodeSetup@1006 : Record "Source Code Setup";
      GenJnlLine@1005 : Record "Gen. Journal Line";
      DateComprReg@1014 : Record "Date Compr. Register";
      GenJnlPostLine@1003 : Codeunit "Gen. Jnl.-Post Line";
      GenJnlPostPreview@1011 : Codeunit "Gen. Jnl.-Post Preview";
      Window@1002 : Dialog;
      LastTransactionNo@1009 : Integer;
      AddCurrChecked@1012 : Boolean;
      MaxPostingDate@1008 : Date;
    BEGIN
      MaxPostingDate := 0D;
      GLEntry.LOCKTABLE;
      DtldVendLedgEntry.LOCKTABLE;
      VendLedgEntry.LOCKTABLE;
      VendLedgEntry.GET(DtldVendLedgEntry2."Vendor Ledger Entry No.");
      CheckPostingDate(PostingDate,MaxPostingDate);
      if PostingDate < DtldVendLedgEntry2."Posting Date" then
        ERROR(MustNotBeBeforeErr);
      if DtldVendLedgEntry2."Transaction No." = 0 then begin
        DtldVendLedgEntry.SETCURRENTKEY("Application No.","Vendor No.","Entry Type");
        DtldVendLedgEntry.SETRANGE("Application No.",DtldVendLedgEntry2."Application No.");
      end else begin
        DtldVendLedgEntry.SETCURRENTKEY("Transaction No.","Vendor No.","Entry Type");
        DtldVendLedgEntry.SETRANGE("Transaction No.",DtldVendLedgEntry2."Transaction No.");
      end;
      DtldVendLedgEntry.SETRANGE("Vendor No.",DtldVendLedgEntry2."Vendor No.");
      DtldVendLedgEntry.SETFILTER("Entry Type",'<>%1',DtldVendLedgEntry."Entry Type"::"Initial Entry");
      DtldVendLedgEntry.SETRANGE(Unapplied,false);
      if DtldVendLedgEntry.FIND('-') then
        repeat
          if not AddCurrChecked then begin
            CheckAdditionalCurrency(PostingDate,DtldVendLedgEntry."Posting Date");
            AddCurrChecked := true;
          end;
          CheckReversal(DtldVendLedgEntry."Vendor Ledger Entry No.");
          if DtldVendLedgEntry."Transaction No." <> 0 then begin
            if DtldVendLedgEntry."Entry Type" = DtldVendLedgEntry."Entry Type"::Application then begin
              LastTransactionNo :=
                FindLastApplTransactionEntry(DtldVendLedgEntry."Vendor Ledger Entry No.");
              if (LastTransactionNo <> 0) and (LastTransactionNo <> DtldVendLedgEntry."Transaction No.") then
                ERROR(UnapplyAllPostedAfterThisEntryErr,DtldVendLedgEntry."Vendor Ledger Entry No.");
            end;
            LastTransactionNo := FindLastTransactionNo(DtldVendLedgEntry."Vendor Ledger Entry No.");
            if (LastTransactionNo <> 0) and (LastTransactionNo <> DtldVendLedgEntry."Transaction No.") then
              ERROR(LatestEntryMustBeApplicationErr,DtldVendLedgEntry."Vendor Ledger Entry No.");
          end;
        until DtldVendLedgEntry.NEXT = 0;

      DateComprReg.CheckMaxDateCompressed(MaxPostingDate,0);

      with DtldVendLedgEntry2 do begin
        SourceCodeSetup.GET;
        VendLedgEntry.GET("Vendor Ledger Entry No.");
        GenJnlLine."Document No." := DocNo;
        GenJnlLine."Posting Date" := PostingDate;
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::Vendor;
        GenJnlLine."Account No." := "Vendor No.";
        GenJnlLine.Correction := true;
        GenJnlLine."Document Type" := "Document Type";
        GenJnlLine.Description := VendLedgEntry.Description;
        GenJnlLine."Shortcut Dimension 1 Code" := VendLedgEntry."Global Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := VendLedgEntry."Global Dimension 2 Code";
        GenJnlLine."Dimension Set ID" := VendLedgEntry."Dimension Set ID";
        GenJnlLine."Posting Group" := VendLedgEntry."Vendor Posting Group";
        GenJnlLine."Source Type" := GenJnlLine."Source Type"::Vendor;
        GenJnlLine."Source No." := "Vendor No.";
        GenJnlLine."Source Code" := SourceCodeSetup."Unapplied Purch. Entry Appln.";
        GenJnlLine."Source Currency Code" := "Currency Code";
        GenJnlLine."System-Created Entry" := true;
        Window.OPEN(UnapplyingMsg);
        OnBeforePostUnapplyVendLedgEntry(GenJnlLine,VendLedgEntry,DtldVendLedgEntry2);
        GenJnlPostLine.UnapplyVendLedgEntry(GenJnlLine,DtldVendLedgEntry2);

        if PreviewMode then
          GenJnlPostPreview.ThrowError;

        COMMIT;
        Window.CLOSE;
      end;
    END;

    LOCAL PROCEDURE CheckPostingDate@5(PostingDate@1001 : Date;VAR MaxPostingDate@1005 : Date);
    VAR
      GenJnlCheckLine@1000 : Codeunit "Gen. Jnl.-Check Line";
    BEGIN
      if GenJnlCheckLine.DateNotAllowed(PostingDate) then
        ERROR(NotAllowedPostingDatesErr);

      if PostingDate > MaxPostingDate then
        MaxPostingDate := PostingDate;
    END;

    LOCAL PROCEDURE CheckAdditionalCurrency@8(OldPostingDate@1000 : Date;NewPostingDate@1001 : Date);
    VAR
      GLSetup@1002 : Record "General Ledger Setup";
      CurrExchRate@1003 : Record "Currency Exchange Rate";
    BEGIN
      if OldPostingDate = NewPostingDate then
        exit;
      GLSetup.GET;
      if GLSetup."Additional Reporting Currency" <> '' then
        if CurrExchRate.ExchangeRate(OldPostingDate,GLSetup."Additional Reporting Currency") <>
           CurrExchRate.ExchangeRate(NewPostingDate,GLSetup."Additional Reporting Currency")
        then
          ERROR(CannotUnapplyExchRateErr,NewPostingDate);
    END;

    LOCAL PROCEDURE CheckReversal@9(VendLedgEntryNo@1000 : Integer);
    VAR
      VendLedgEntry@1001 : Record "Vendor Ledger Entry";
    BEGIN
      VendLedgEntry.GET(VendLedgEntryNo);
      if VendLedgEntry.Reversed then
        ERROR(CannotUnapplyInReversalErr,VendLedgEntryNo);
    END;

    [External]
    PROCEDURE ApplyVendEntryFormEntry@10(VAR ApplyingVendLedgEntry@1000 : Record "Vendor Ledger Entry");
    VAR
      VendLedgEntry@1002 : Record "Vendor Ledger Entry";
      ApplyVendEntries@1001 : Page "Apply Vendor Entries";
      VendEntryApplID@1004 : Code[50];
    BEGIN
      if not ApplyingVendLedgEntry.Open then
        ERROR(CannotApplyClosedEntriesErr);

      VendEntryApplID := USERID;
      if VendEntryApplID = '' then
        VendEntryApplID := '***';
      if ApplyingVendLedgEntry."Remaining Amount" = 0 then
        ApplyingVendLedgEntry.CALCFIELDS("Remaining Amount");

      ApplyingVendLedgEntry."Applying Entry" := true;
      if ApplyingVendLedgEntry."Applies-to ID" = '' then
        ApplyingVendLedgEntry."Applies-to ID" := VendEntryApplID;
      ApplyingVendLedgEntry."Amount to Apply" := ApplyingVendLedgEntry."Remaining Amount";
      CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",ApplyingVendLedgEntry);
      COMMIT;

      VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive);
      VendLedgEntry.SETRANGE("Vendor No.",ApplyingVendLedgEntry."Vendor No.");
      VendLedgEntry.SETRANGE(Open,true);
      if VendLedgEntry.FINDFIRST then begin
        ApplyVendEntries.SetVendLedgEntry(ApplyingVendLedgEntry);
        ApplyVendEntries.SETRECORD(VendLedgEntry);
        ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
        if ApplyingVendLedgEntry."Applies-to ID" <> VendEntryApplID then
          ApplyVendEntries.SetAppliesToID(ApplyingVendLedgEntry."Applies-to ID");
        ApplyVendEntries.RUNMODAL;
        CLEAR(ApplyVendEntries);
        ApplyingVendLedgEntry."Applying Entry" := false;
        ApplyingVendLedgEntry."Applies-to ID" := '';
        ApplyingVendLedgEntry."Amount to Apply" := 0;
      end;
    END;

    LOCAL PROCEDURE FindLastApplTransactionEntry@11(VendLedgEntryNo@1000 : Integer) : Integer;
    VAR
      DtldVendLedgEntry@1001 : Record "Detailed Vendor Ledg. Entry";
      LastTransactionNo@1002 : Integer;
    BEGIN
      DtldVendLedgEntry.SETCURRENTKEY("Vendor Ledger Entry No.","Entry Type");
      DtldVendLedgEntry.SETRANGE("Vendor Ledger Entry No.",VendLedgEntryNo);
      DtldVendLedgEntry.SETRANGE("Entry Type",DtldVendLedgEntry."Entry Type"::Application);
      LastTransactionNo := 0;
      if DtldVendLedgEntry.FIND('-') then
        repeat
          if (DtldVendLedgEntry."Transaction No." > LastTransactionNo) and not DtldVendLedgEntry.Unapplied then
            LastTransactionNo := DtldVendLedgEntry."Transaction No.";
        until DtldVendLedgEntry.NEXT = 0;
      exit(LastTransactionNo);
    END;

    [Internal]
    PROCEDURE PreviewApply@16(VendorLedgerEntry@1003 : Record "Vendor Ledger Entry";DocumentNo@1002 : Code[20];ApplicationDate@1001 : Date);
    VAR
      GenJnlPostPreview@1004 : Codeunit "Gen. Jnl.-Post Preview";
      VendEntryApplyPostedEntries@1000 : Codeunit "VendEntry-Apply Posted Entries";
    BEGIN
      BINDSUBSCRIPTION(VendEntryApplyPostedEntries);
      VendEntryApplyPostedEntries.SetApplyContext(ApplicationDate,DocumentNo);
      GenJnlPostPreview.Preview(VendEntryApplyPostedEntries,VendorLedgerEntry);
    END;

    [Internal]
    PROCEDURE PreviewUnapply@15(DetailedVendorLedgEntry@1002 : Record "Detailed Vendor Ledg. Entry";DocumentNo@1001 : Code[20];ApplicationDate@1000 : Date);
    VAR
      VendorLedgerEntry@1004 : Record "Vendor Ledger Entry";
      GenJnlPostPreview@1003 : Codeunit "Gen. Jnl.-Post Preview";
      VendEntryApplyPostedEntries@1005 : Codeunit "VendEntry-Apply Posted Entries";
    BEGIN
      BINDSUBSCRIPTION(VendEntryApplyPostedEntries);
      VendEntryApplyPostedEntries.SetUnapplyContext(DetailedVendorLedgEntry,ApplicationDate,DocumentNo);
      GenJnlPostPreview.Preview(VendEntryApplyPostedEntries,VendorLedgerEntry);
    END;

    [External]
    PROCEDURE SetApplyContext@13(ApplicationDate@1000 : Date;DocumentNo@1001 : Code[20]);
    BEGIN
      ApplicationDatePreviewContext := ApplicationDate;
      DocumentNoPreviewContext := DocumentNo;
      RunOptionPreviewContext := RunOptionPreview::Apply;
    END;

    [External]
    PROCEDURE SetUnapplyContext@28(VAR DetailedVendorLedgEntry@1002 : Record "Detailed Vendor Ledg. Entry";ApplicationDate@1000 : Date;DocumentNo@1001 : Code[20]);
    BEGIN
      ApplicationDatePreviewContext := ApplicationDate;
      DocumentNoPreviewContext := DocumentNo;
      DetailedVendorLedgEntryPreviewContext := DetailedVendorLedgEntry;
      RunOptionPreviewContext := RunOptionPreview::Unapply;
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnRunPreview@18(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1002 : Variant);
    VAR
      VendEntryApplyPostedEntries@1003 : Codeunit "VendEntry-Apply Posted Entries";
    BEGIN
      VendEntryApplyPostedEntries := Subscriber;
      PreviewMode := true;
      Result := VendEntryApplyPostedEntries.RUN(RecVar);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostApplyVendLedgEntry@20(VAR GenJournalLine@1000 : Record "Gen. Journal Line";VendorLedgerEntry@1001 : Record "Vendor Ledger Entry");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostUnapplyVendLedgEntry@22(VAR GenJournalLine@1000 : Record "Gen. Journal Line";VendorLedgerEntry@1001 : Record "Vendor Ledger Entry";DetailedVendorLedgEntry@1002 : Record "Detailed Vendor Ledg. Entry");
    BEGIN
    END;

    BEGIN
    END.
  }
}

