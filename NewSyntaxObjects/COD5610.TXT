OBJECT Codeunit 5610 Calculate Depreciation
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DeprBook@1000 : Record "Depreciation Book";
      FADeprBook@1001 : Record "FA Depreciation Book";
      CalculateNormalDepr@1002 : Codeunit "Calculate Normal Depreciation";
      CalculateCustom1Depr@1003 : Codeunit "Calculate Custom 1 Depr.";

    [External]
    PROCEDURE Calculate@1(VAR DeprAmount@1000 : Decimal;VAR Custom1Amount@1001 : Decimal;VAR NumberOfDays@1002 : Integer;VAR Custom1NumberOfDays@1003 : Integer;FANo@1004 : Code[20];DeprBookCode@1005 : Code[10];UntilDate@1006 : Date;EntryAmounts@1007 : ARRAY [4] OF Decimal;DateFromProjection@1008 : Date;DaysInPeriod@1009 : Integer);
    BEGIN
      DeprAmount := 0;
      Custom1Amount := 0;
      NumberOfDays := 0;
      Custom1NumberOfDays := 0;
      if not DeprBook.GET(DeprBookCode) then
        exit;
      if not FADeprBook.GET(FANo,DeprBookCode) then
        exit;

      CheckDeprDaysInFiscalYear(DateFromProjection = 0D,UntilDate);

      if DeprBook."Use Custom 1 Depreciation" and
         (FADeprBook."Depr. Ending Date (Custom 1)" > 0D)
      then
        CalculateCustom1Depr.Calculate(
          DeprAmount,Custom1Amount,NumberOfDays,
          Custom1NumberOfDays,FANo,DeprBookCode,UntilDate,
          EntryAmounts,DateFromProjection,DaysInPeriod)
      else
        CalculateNormalDepr.Calculate(
          DeprAmount,NumberOfDays,FANo,DeprBookCode,UntilDate,
          EntryAmounts,DateFromProjection,DaysInPeriod);
    END;

    LOCAL PROCEDURE CheckDeprDaysInFiscalYear@2(CheckDeprDays@1001 : Boolean;UntilDate@1000 : Date);
    VAR
      DepreciationCalc@1004 : Codeunit "Depreciation Calculation";
      FADateCalc@1002 : Codeunit "FA Date Calculation";
      FiscalYearBegin@1003 : Date;
      NoOfDeprDays@1005 : Integer;
    BEGIN
      if DeprBook."Allow more than 360/365 Days" or not CheckDeprDays then
        exit;
      if  (FADeprBook."Depreciation Method" = FADeprBook."Depreciation Method"::"Declining-Balance 1") or
         (FADeprBook."Depreciation Method" = FADeprBook."Depreciation Method"::"DB1/SL")
      then
        FiscalYearBegin := FADateCalc.GetFiscalYear(DeprBook.Code,UntilDate);
      if DeprBook."Fiscal Year 365 Days" then
        NoOfDeprDays := 365
      else
        NoOfDeprDays := 360;
      if DepreciationCalc.DeprDays(
           FiscalYearBegin,UntilDate,DeprBook."Fiscal Year 365 Days") > NoOfDeprDays
      then
        DeprBook.TESTFIELD("Allow more than 360/365 Days");
    END;

    BEGIN
    END.
  }
}

