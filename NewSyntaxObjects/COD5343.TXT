OBJECT Codeunit 5343 CRM Sales Order to Sales Order
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CannotCreateSalesOrderInNAVTxt@1003 : TextConst 'ENU=The sales order cannot be created.';
      CannotFindCRMAccountForOrderErr@1014 : TextConst '@@@="%1=Dynamics CRM Sales Order Name, %2 - Microsoft Dynamics CRM";ENU=The %2 account for %2 sales order %1 does not exist.';
      ItemDoesNotExistErr@1002 : TextConst '@@@="%1= the text: ""The sales order cannot be created."", %2=product name";ENU=%1 The item %2 does not exist.';
      NoCustomerErr@1005 : TextConst '@@@="%1= the text: ""The Dynamics CRM Sales Order cannot be created."", %2=sales order title, %3 - Microsoft Dynamics CRM";ENU=%1 There is no potential customer defined on the %3 sales order %2.';
      CRMSynchHelper@1017 : Codeunit "CRM Synch. Helper";
      CRMProductName@1001 : Codeunit "CRM Product Name";
      LastSalesLineNo@1004 : Integer;
      NotCoupledCustomerErr@1012 : TextConst '@@@="%1= the text: ""The Dynamics CRM Sales Order cannot be created."", %2=account name, %3 - Microsoft Dynamics CRM";ENU=%1 There is no customer coupled to %3 account %2.';
      NotCoupledCRMProductErr@1006 : TextConst '@@@="%1= the text: ""The Dynamics CRM Sales Order cannot be created."", %2=product name, %3 - Microsoft Dynamics CRM";ENU=%1 The %3 product %2 is not coupled to an item.';
      NotCoupledCRMResourceErr@1007 : TextConst '@@@="%1= the text: ""The Dynamics CRM Sales Order cannot be created."", %2=resource name, %3 - Microsoft Dynamics CRM";ENU=%1 The %3 resource %2 is not coupled to a resource.';
      NotCoupledCRMSalesOrderErr@1000 : TextConst '@@@="%1=sales order number, %2 - Microsoft Dynamics CRM";ENU=The %2 sales order %1 is not coupled.';
      NotCoupledSalesHeaderErr@1015 : TextConst '@@@="%1=sales order number, %2 - Microsoft Dynamics CRM";ENU=The sales order %1 is not coupled to %2.';
      OverwriteCRMDiscountQst@1016 : TextConst '@@@=%1 - product name, %2 - Microsoft Dynamics CRM;ENU=There is a discount on the %2 sales order, which will be overwritten by %1 settings. You will have the possibility to update the discounts directly on the sales order, after it is created. Do you want to continue?';
      ResourceDoesNotExistErr@1009 : TextConst '@@@="%1= the text: ""The Dynamics CRM Sales Order cannot be created."", %2=product name";ENU=%1 The resource %2 does not exist.';
      WriteInProductDoesNotExistErr@1008 : TextConst '@@@="%1= the text: ""The Dynamics CRM Sales Order cannot be created."", %2=product name";ENU=%1 The write-in product %2 does not exist.';
      UnexpectedProductTypeErr@1011 : TextConst '@@@="%1= the text: ""The Dynamics CRM Sales Order cannot be created."", %2=product name";ENU=%1 Unexpected value of product type code for product %2. The supported values are: sales inventory, services.';
      ZombieCouplingErr@1013 : TextConst '@@@=%1 - product name, %2 - Microsoft Dynamics CRM;ENU=Although the coupling from %2 exists, the sales order had been manually deleted. If needed, please use the menu to create it again in %1.';

    LOCAL PROCEDURE ApplySalesOrderDiscounts@22(CRMSalesorder@1001 : Record "CRM Salesorder";VAR SalesHeader@1000 : Record "Sales Header");
    VAR
      SalesCalcDiscountByType@1002 : Codeunit "Sales - Calc Discount By Type";
      CRMDiscountAmount@1003 : Decimal;
    BEGIN
      // No discounts to apply
      if (CRMSalesorder.DiscountAmount = 0 ) and (CRMSalesorder.DiscountPercentage = 0) then
        exit;

      // Attempt to set the discount, if NAV general and customer settings allow it
      // Using CRM discounts
      CRMDiscountAmount := CRMSalesorder.TotalLineItemAmount - CRMSalesorder.TotalAmountLessFreight;
      SalesCalcDiscountByType.ApplyInvDiscBasedOnAmt(CRMDiscountAmount,SalesHeader);

      // NAV settings (in G/L Setup as well as per-customer discounts) did not allow using the CRM discounts
      // Using NAV discounts
      // But the user will be able to manually update the discounts after the order is created in NAV
      if not CONFIRM(STRSUBSTNO(OverwriteCRMDiscountQst,PRODUCTNAME.SHORT,CRMProductName.SHORT),true) then
        ERROR('');
    END;

    LOCAL PROCEDURE CopyCRMOptionFields@3(CRMSalesorder@1001 : Record "CRM Salesorder";VAR SalesHeader@1000 : Record "Sales Header");
    VAR
      CRMAccount@1003 : Record "CRM Account";
      CRMOptionMapping@1002 : Record "CRM Option Mapping";
    BEGIN
      if CRMOptionMapping.FindRecordID(
           DATABASE::"CRM Account",CRMAccount.FIELDNO(Address1_ShippingMethodCode),CRMSalesorder.ShippingMethodCode)
      then
        SalesHeader.VALIDATE(
          "Shipping Agent Code",
          COPYSTR(CRMOptionMapping.GetRecordKeyValue,1,MAXSTRLEN(SalesHeader."Shipping Agent Code")));

      if CRMOptionMapping.FindRecordID(
           DATABASE::"CRM Account",CRMAccount.FIELDNO(PaymentTermsCode),CRMSalesorder.PaymentTermsCode)
      then
        SalesHeader.VALIDATE(
          "Payment Terms Code",
          COPYSTR(CRMOptionMapping.GetRecordKeyValue,1,MAXSTRLEN(SalesHeader."Payment Terms Code")));

      if CRMOptionMapping.FindRecordID(
           DATABASE::"CRM Account",CRMAccount.FIELDNO(Address1_FreightTermsCode),CRMSalesorder.FreightTermsCode)
      then
        SalesHeader.VALIDATE(
          "Shipment Method Code",
          COPYSTR(CRMOptionMapping.GetRecordKeyValue,1,MAXSTRLEN(SalesHeader."Shipment Method Code")));
    END;

    LOCAL PROCEDURE CopyBillToInformationIfNotEmpty@7(CRMSalesorder@1001 : Record "CRM Salesorder";VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      // If the Bill-To fields in CRM are all empty, then let NAV keep its standard behavior (takes Bill-To from the Customer information)
      if ((CRMSalesorder.BillTo_Line1 = '') and
          (CRMSalesorder.BillTo_Line2 = '') and
          (CRMSalesorder.BillTo_City = '') and
          (CRMSalesorder.BillTo_PostalCode = '') and
          (CRMSalesorder.BillTo_Country = '') and
          (CRMSalesorder.BillTo_StateOrProvince = ''))
      then
        exit;

      SalesHeader.VALIDATE("Bill-to Address",FORMAT(CRMSalesorder.BillTo_Line1,MAXSTRLEN(SalesHeader."Bill-to Address")));
      SalesHeader.VALIDATE("Bill-to Address 2",FORMAT(CRMSalesorder.BillTo_Line2,MAXSTRLEN(SalesHeader."Bill-to Address 2")));
      SalesHeader.VALIDATE("Bill-to City",FORMAT(CRMSalesorder.BillTo_City,MAXSTRLEN(SalesHeader."Bill-to City")));
      SalesHeader.VALIDATE("Bill-to Post Code",FORMAT(CRMSalesorder.BillTo_PostalCode,MAXSTRLEN(SalesHeader."Bill-to Post Code")));
      SalesHeader.VALIDATE(
        "Bill-to Country/Region Code",FORMAT(CRMSalesorder.BillTo_Country,MAXSTRLEN(SalesHeader."Bill-to Country/Region Code")));
      SalesHeader.VALIDATE("Bill-to County",FORMAT(CRMSalesorder.BillTo_StateOrProvince,MAXSTRLEN(SalesHeader."Bill-to County")));
    END;

    LOCAL PROCEDURE CopyShipToInformationIfNotEmpty@31(CRMSalesorder@1001 : Record "CRM Salesorder";VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      // If the Ship-To fields in CRM are all empty, then let NAV keep its standard behavior (takes Bill-To from the Customer information)
      if ((CRMSalesorder.ShipTo_Line1 = '') and
          (CRMSalesorder.ShipTo_Line2 = '') and
          (CRMSalesorder.ShipTo_City = '') and
          (CRMSalesorder.ShipTo_PostalCode = '') and
          (CRMSalesorder.ShipTo_Country = '') and
          (CRMSalesorder.ShipTo_StateOrProvince = ''))
      then
        exit;

      SalesHeader.VALIDATE("Ship-to Address",FORMAT(CRMSalesorder.ShipTo_Line1,MAXSTRLEN(SalesHeader."Ship-to Address")));
      SalesHeader.VALIDATE("Ship-to Address 2",FORMAT(CRMSalesorder.ShipTo_Line2,MAXSTRLEN(SalesHeader."Ship-to Address 2")));
      SalesHeader.VALIDATE("Ship-to City",FORMAT(CRMSalesorder.ShipTo_City,MAXSTRLEN(SalesHeader."Ship-to City")));
      SalesHeader.VALIDATE("Ship-to Post Code",FORMAT(CRMSalesorder.ShipTo_PostalCode,MAXSTRLEN(SalesHeader."Ship-to Post Code")));
      SalesHeader.VALIDATE(
        "Ship-to Country/Region Code",FORMAT(CRMSalesorder.ShipTo_Country,MAXSTRLEN(SalesHeader."Ship-to Country/Region Code")));
      SalesHeader.VALIDATE("Ship-to County",FORMAT(CRMSalesorder.ShipTo_StateOrProvince,MAXSTRLEN(SalesHeader."Ship-to County")));
    END;

    LOCAL PROCEDURE CoupledSalesHeaderExists@16(CRMSalesorder@1001 : Record "CRM Salesorder") : Boolean;
    VAR
      SalesHeader@1002 : Record "Sales Header";
      CRMIntegrationRecord@1003 : Record "CRM Integration Record";
      NAVSalesHeaderRecordId@1000 : RecordID;
    BEGIN
      if not ISNULLGUID(CRMSalesorder.SalesOrderId) then
        if CRMIntegrationRecord.FindRecordIDFromID(CRMSalesorder.SalesOrderId,DATABASE::"Sales Header",NAVSalesHeaderRecordId) then
          exit(SalesHeader.GET(NAVSalesHeaderRecordId));
    END;

    [External]
    PROCEDURE CreateInNAV@1(CRMSalesorder@1012 : Record "CRM Salesorder";VAR SalesHeader@1000 : Record "Sales Header") : Boolean;
    BEGIN
      CRMSalesorder.TESTFIELD(StateCode,CRMSalesorder.StateCode::Submitted);
      exit(CreateNAVSalesOrder(CRMSalesorder,SalesHeader));
    END;

    LOCAL PROCEDURE CreateNAVSalesOrder@15(CRMSalesorder@1001 : Record "CRM Salesorder";VAR SalesHeader@1000 : Record "Sales Header") : Boolean;
    VAR
      CRMIntegrationRecord@1003 : Record "CRM Integration Record";
    BEGIN
      if ISNULLGUID(CRMSalesorder.SalesOrderId) then
        exit;

      CreateSalesOrderHeader(CRMSalesorder,SalesHeader);
      CreateSalesOrderLines(CRMSalesorder,SalesHeader);
      ApplySalesOrderDiscounts(CRMSalesorder,SalesHeader);
      CRMIntegrationRecord.CoupleRecordIdToCRMID(SalesHeader.RECORDID,CRMSalesorder.SalesOrderId);
      // Flag sales order has been submitted to NAV.
      SetLastBackOfficeSubmit(CRMSalesorder,TODAY);
      exit(true);
    END;

    [EventSubscriber(Table,36,OnBeforeDeleteEvent)]
    [Internal]
    PROCEDURE ClearLastBackOfficeSubmitOnSalesHeaderDelete@14(VAR Rec@1000 : Record "Sales Header";RunTrigger@1004 : Boolean);
    VAR
      CRMSalesorder@1002 : Record "CRM Salesorder";
      CRMIntegrationRecord@1003 : Record "CRM Integration Record";
      CRMIntegrationManagement@1001 : Codeunit "CRM Integration Management";
    BEGIN
      if CRMIntegrationManagement.IsCRMIntegrationEnabled then
        if CRMIntegrationRecord.FindIDFromRecordID(Rec.RECORDID,CRMSalesorder.SalesOrderId) then begin
          CRMSalesorder.FIND;
          SetLastBackOfficeSubmit(CRMSalesorder,0D);
        end;
    END;

    LOCAL PROCEDURE CreateSalesOrderHeader@10(CRMSalesorder@1001 : Record "CRM Salesorder";VAR SalesHeader@1000 : Record "Sales Header");
    VAR
      Customer@1010 : Record Customer;
    BEGIN
      SalesHeader.INIT;
      SalesHeader.VALIDATE("Document Type",SalesHeader."Document Type"::Order);
      SalesHeader.VALIDATE(Status,SalesHeader.Status::Open);
      SalesHeader.InitInsert;
      GetCoupledCustomer(CRMSalesorder,Customer);
      SalesHeader.VALIDATE("Sell-to Customer No.",Customer."No.");
      SalesHeader.VALIDATE("Your Reference",COPYSTR(CRMSalesorder.OrderNumber,1,MAXSTRLEN(SalesHeader."Your Reference")));
      SalesHeader.VALIDATE("Currency Code",CRMSynchHelper.GetNavCurrencyCode(CRMSalesorder.TransactionCurrencyId));
      SalesHeader.VALIDATE("Requested Delivery Date",CRMSalesorder.RequestDeliveryBy);
      CopyBillToInformationIfNotEmpty(CRMSalesorder,SalesHeader);
      CopyShipToInformationIfNotEmpty(CRMSalesorder,SalesHeader);
      CopyCRMOptionFields(CRMSalesorder,SalesHeader);
      SalesHeader.VALIDATE("Payment Discount %",CRMSalesorder.DiscountPercentage);
      SalesHeader.INSERT;
    END;

    LOCAL PROCEDURE CreateSalesOrderLines@13(CRMSalesorder@1001 : Record "CRM Salesorder";SalesHeader@1000 : Record "Sales Header");
    VAR
      CRMSalesorderdetail@1007 : Record "CRM Salesorderdetail";
      SalesLine@1004 : Record "Sales Line";
    BEGIN
      // If any of the products on the lines are not found in NAV, err
      CRMSalesorderdetail.SETRANGE(SalesOrderId,CRMSalesorder.SalesOrderId); // Get all sales order lines

      if CRMSalesorderdetail.FINDSET then begin
        repeat
          InitializeSalesOrderLine(CRMSalesorderdetail,SalesHeader,SalesLine);
          SalesLine.INSERT;
        until CRMSalesorderdetail.NEXT = 0;
      end else begin
        SalesLine.VALIDATE("Document Type",SalesHeader."Document Type");
        SalesLine.VALIDATE("Document No.",SalesHeader."No.");
      end;

      SalesLine.InsertFreightLine(CRMSalesorder.FreightAmount);
    END;

    [Internal]
    PROCEDURE CRMIsCoupledToValidRecord@9(CRMSalesorder@1002 : Record "CRM Salesorder";NAVTableID@1001 : Integer) : Boolean;
    VAR
      CRMIntegrationManagement@1000 : Codeunit "CRM Integration Management";
      CRMCouplingManagement@1003 : Codeunit "CRM Coupling Management";
    BEGIN
      exit(CRMIntegrationManagement.IsCRMIntegrationEnabled and
        CRMCouplingManagement.IsRecordCoupledToNAV(CRMSalesorder.SalesOrderId,NAVTableID) and
        CoupledSalesHeaderExists(CRMSalesorder));
    END;

    [External]
    PROCEDURE GetCRMSalesOrder@19(VAR CRMSalesorder@1001 : Record "CRM Salesorder";YourReference@1000 : Text[35]) : Boolean;
    BEGIN
      CRMSalesorder.SETRANGE(OrderNumber,YourReference);
      exit(CRMSalesorder.FINDFIRST)
    END;

    [External]
    PROCEDURE GetCoupledCRMSalesorder@21(SalesHeader@1000 : Record "Sales Header";VAR CRMSalesorder@1001 : Record "CRM Salesorder");
    VAR
      CRMIntegrationRecord@1002 : Record "CRM Integration Record";
      CoupledCRMId@1004 : GUID;
    BEGIN
      if SalesHeader.ISEMPTY then
        ERROR(NotCoupledSalesHeaderErr,SalesHeader."No.",CRMProductName.SHORT);

      if not CRMIntegrationRecord.FindIDFromRecordID(SalesHeader.RECORDID,CoupledCRMId) then
        ERROR(NotCoupledSalesHeaderErr,SalesHeader."No.",CRMProductName.SHORT);

      if CRMSalesorder.GET(CoupledCRMId) then
        exit;

      // If we reached this point, a zombie coupling exists but the sales order most probably was deleted manually by the user.
      CRMIntegrationRecord.RemoveCouplingToCRMID(CoupledCRMId,DATABASE::"Sales Header");
      ERROR(ZombieCouplingErr,PRODUCTNAME.SHORT,CRMProductName.SHORT);
    END;

    [External]
    PROCEDURE GetCoupledCustomer@4(CRMSalesorder@1001 : Record "CRM Salesorder";VAR Customer@1000 : Record Customer) : Boolean;
    VAR
      CRMAccount@1004 : Record "CRM Account";
      CRMIntegrationRecord@1002 : Record "CRM Integration Record";
      NAVCustomerRecordId@1003 : RecordID;
      CRMAccountId@1005 : GUID;
    BEGIN
      if ISNULLGUID(CRMSalesorder.CustomerId) then
        ERROR(NoCustomerErr,CannotCreateSalesOrderInNAVTxt,CRMSalesorder.Description,CRMProductName.SHORT);

      // Get the ID of the CRM Account associated to the sales order. Works for both CustomerType(s): account, contact
      if not GetCRMAccountOfCRMSalesOrder(CRMSalesorder,CRMAccount) then
        ERROR(CannotFindCRMAccountForOrderErr,CRMSalesorder.Name,CRMProductName.SHORT);
      CRMAccountId := CRMAccount.AccountId;

      if not CRMIntegrationRecord.FindRecordIDFromID(CRMAccountId,DATABASE::Customer,NAVCustomerRecordId) then
        ERROR(NotCoupledCustomerErr,CannotCreateSalesOrderInNAVTxt,CRMAccount.Name,CRMProductName.SHORT);

      exit(Customer.GET(NAVCustomerRecordId));
    END;

    [External]
    PROCEDURE GetCoupledSalesHeader@2(CRMSalesorder@1001 : Record "CRM Salesorder";VAR SalesHeader@1000 : Record "Sales Header") : Boolean;
    VAR
      CRMIntegrationRecord@1002 : Record "CRM Integration Record";
      NAVSalesHeaderRecordId@1003 : RecordID;
    BEGIN
      if ISNULLGUID(CRMSalesorder.SalesOrderId) then
        ERROR(NotCoupledCRMSalesOrderErr,CRMSalesorder.OrderNumber,CRMProductName.SHORT);

      // Attempt to find the coupled sales header
      if not CRMIntegrationRecord.FindRecordIDFromID(CRMSalesorder.SalesOrderId,DATABASE::"Sales Header",NAVSalesHeaderRecordId) then
        ERROR(NotCoupledCRMSalesOrderErr,CRMSalesorder.OrderNumber,CRMProductName.SHORT);

      if SalesHeader.GET(NAVSalesHeaderRecordId) then
        exit(true);

      // If we reached this point, a zombie coupling exists but the sales order most probably was deleted manually by the user.
      CRMIntegrationRecord.RemoveCouplingToCRMID(CRMSalesorder.SalesOrderId,DATABASE::"Sales Header");
      ERROR(ZombieCouplingErr,PRODUCTNAME.SHORT,CRMProductName.SHORT);
    END;

    [External]
    PROCEDURE GetCRMAccountOfCRMSalesOrder@11(CRMSalesorder@1002 : Record "CRM Salesorder";VAR CRMAccount@1000 : Record "CRM Account") : Boolean;
    VAR
      CRMContact@1001 : Record "CRM Contact";
    BEGIN
      if CRMSalesorder.CustomerIdType = CRMSalesorder.CustomerIdType::account then
        exit(CRMAccount.GET(CRMSalesorder.CustomerId));

      if CRMSalesorder.CustomerIdType = CRMSalesorder.CustomerIdType::contact then
        if CRMContact.GET(CRMSalesorder.CustomerId) then
          exit(CRMAccount.GET(CRMContact.ParentCustomerId));
      exit(false);
    END;

    [External]
    PROCEDURE GetCRMContactOfCRMSalesOrder@17(CRMSalesorder@1002 : Record "CRM Salesorder";VAR CRMContact@1000 : Record "CRM Contact") : Boolean;
    BEGIN
      if CRMSalesorder.CustomerIdType = CRMSalesorder.CustomerIdType::contact then
        exit(CRMContact.GET(CRMSalesorder.CustomerId));
    END;

    LOCAL PROCEDURE InitializeSalesOrderLine@18(CRMSalesorderdetail@1003 : Record "CRM Salesorderdetail";SalesHeader@1000 : Record "Sales Header";VAR SalesLine@1002 : Record "Sales Line");
    VAR
      CRMProduct@1004 : Record "CRM Product";
    BEGIN
      if ISNULLGUID(CRMSalesorderdetail.ProductId) then // This is a CRM write-in product, not supported in NAV
        ERROR(WriteInProductDoesNotExistErr,CannotCreateSalesOrderInNAVTxt,CRMSalesorderdetail.ProductDescription);
      CRMProduct.GET(CRMSalesorderdetail.ProductId);
      CRMProduct.TESTFIELD(StateCode,CRMProduct.StateCode::Active);

      SalesLine.INIT;
      SalesLine.VALIDATE("Document Type",SalesHeader."Document Type");
      SalesLine.VALIDATE("Document No.",SalesHeader."No.");
      LastSalesLineNo := LastSalesLineNo + 10000;
      SalesLine.VALIDATE("Line No.",LastSalesLineNo);

      case CRMProduct.ProductTypeCode of
        CRMProduct.ProductTypeCode::SalesInventory:
          InitializeSalesOrderLineFromItem(CRMProduct,SalesLine);
        CRMProduct.ProductTypeCode::Services:
          InitializeSalesOrderLineFromResource(CRMProduct,SalesLine);
        else
          ERROR(UnexpectedProductTypeErr,CannotCreateSalesOrderInNAVTxt,CRMProduct.ProductNumber);
      end;

      SalesLine.VALIDATE(Quantity,CRMSalesorderdetail.Quantity);
      SalesLine.VALIDATE("Unit Price",CRMSalesorderdetail.PricePerUnit);
      SalesLine.VALIDATE(Amount,CRMSalesorderdetail.BaseAmount);
      SalesLine.VALIDATE(
        "Line Discount Amount",
        CRMSalesorderdetail.Quantity * CRMSalesorderdetail.VolumeDiscountAmount +
        CRMSalesorderdetail.ManualDiscountAmount);
    END;

    LOCAL PROCEDURE InitializeSalesOrderLineFromItem@8(CRMProduct@1001 : Record "CRM Product";VAR SalesLine@1002 : Record "Sales Line");
    VAR
      CRMIntegrationRecord@1003 : Record "CRM Integration Record";
      Item@1005 : Record Item;
      NAVItemRecordID@1000 : RecordID;
    BEGIN
      // Attempt to find the coupled item
      if not CRMIntegrationRecord.FindRecordIDFromID(CRMProduct.ProductId,DATABASE::Item,NAVItemRecordID) then
        ERROR(NotCoupledCRMProductErr,CannotCreateSalesOrderInNAVTxt,CRMProduct.Name,CRMProductName.SHORT);

      if not Item.GET(NAVItemRecordID) then
        ERROR(ItemDoesNotExistErr,CannotCreateSalesOrderInNAVTxt,CRMProduct.ProductNumber);
      SalesLine.VALIDATE(Type,SalesLine.Type::Item);
      SalesLine.VALIDATE("No.",Item."No.");
    END;

    LOCAL PROCEDURE InitializeSalesOrderLineFromResource@6(CRMProduct@1001 : Record "CRM Product";VAR SalesLine@1002 : Record "Sales Line");
    VAR
      CRMIntegrationRecord@1003 : Record "CRM Integration Record";
      Resource@1005 : Record Resource;
      NAVResourceRecordID@1000 : RecordID;
    BEGIN
      // Attempt to find the coupled resource
      if not CRMIntegrationRecord.FindRecordIDFromID(CRMProduct.ProductId,DATABASE::Resource,NAVResourceRecordID) then
        ERROR(NotCoupledCRMResourceErr,CannotCreateSalesOrderInNAVTxt,CRMProduct.Name,CRMProductName.SHORT);

      if not Resource.GET(CRMProduct.ProductNumber) then
        ERROR(ResourceDoesNotExistErr,CannotCreateSalesOrderInNAVTxt,CRMProduct.ProductNumber);
      SalesLine.VALIDATE(Type,SalesLine.Type::Resource);
      SalesLine.VALIDATE("No.",Resource."No.");
    END;

    LOCAL PROCEDURE SetLastBackOfficeSubmit@20(VAR CRMSalesorder@1001 : Record "CRM Salesorder";NewDate@1000 : Date);
    BEGIN
      if CRMSalesorder.LastBackofficeSubmit <> NewDate then begin
        CRMSalesorder.StateCode := CRMSalesorder.StateCode::Active;
        CRMSalesorder.MODIFY(true);
        CRMSalesorder.LastBackofficeSubmit := NewDate;
        CRMSalesorder.MODIFY(true);
        CRMSalesorder.StateCode := CRMSalesorder.StateCode::Submitted;
        CRMSalesorder.MODIFY(true);
      end;
    END;

    BEGIN
    END.
  }
}

