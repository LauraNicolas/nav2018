OBJECT Table 1007 Job WIP Warning
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Job WIP Warning;
    LookupPageID="Job WIP Warnings";
    DrillDownPageID="Job WIP Warnings";
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No.;
                                                   Editable=false }
    { 2   ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Job No. }
    { 3   ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No.";
                                                   CaptionML=ENU=Job Task No. }
    { 4   ;   ;Job WIP Total Entry No.;Integer    ;TableRelation="Job WIP Total";
                                                   CaptionML=ENU=Job WIP Total Entry No.;
                                                   Editable=false }
    { 5   ;   ;Warning Message     ;Text250       ;CaptionML=ENU=Warning Message;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Entry No."                             ;Clustered=true }
    {    ;"Job No.","Job Task No."                 }
    {    ;"Job WIP Total Entry No."                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=%1 is 0.';
      Text002@1001 : TextConst 'ENU=Cost completion is greater than 100%.';
      Text003@1002 : TextConst 'ENU=%1 is negative.';

    [External]
    PROCEDURE CreateEntries@1(JobWIPTotal@1000 : Record "Job WIP Total");
    VAR
      Job@1001 : Record Job;
    BEGIN
      Job.GET(JobWIPTotal."Job No.");
      if not Job.Complete then begin
        if JobWIPTotal."Contract (Total Price)" = 0 then
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text001,JobWIPTotal.FIELDCAPTION("Contract (Total Price)")));

        if JobWIPTotal."Schedule (Total Cost)" = 0 then
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text001,JobWIPTotal.FIELDCAPTION("Schedule (Total Cost)")));

        if JobWIPTotal."Schedule (Total Price)" = 0 then
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text001,JobWIPTotal.FIELDCAPTION("Schedule (Total Price)")));

        if JobWIPTotal."Usage (Total Cost)" > JobWIPTotal."Schedule (Total Cost)" then
          InsertWarning(JobWIPTotal,Text002);

        if JobWIPTotal."Calc. Recog. Sales Amount" < 0 then
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text003,JobWIPTotal.FIELDCAPTION("Calc. Recog. Sales Amount")));

        if JobWIPTotal."Calc. Recog. Costs Amount" < 0 then
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text003,JobWIPTotal.FIELDCAPTION("Calc. Recog. Costs Amount")));
      end;
    END;

    [External]
    PROCEDURE DeleteEntries@3(JobWIPTotal@1000 : Record "Job WIP Total");
    BEGIN
      SETRANGE("Job WIP Total Entry No.",JobWIPTotal."Entry No.");
      if not ISEMPTY then
        DELETEALL(true);
    END;

    LOCAL PROCEDURE InsertWarning@2(JobWIPTotal@1000 : Record "Job WIP Total";Message@1001 : Text[250]);
    BEGIN
      RESET;
      if FINDLAST then
        "Entry No." += 1
      else
        "Entry No." := 1;
      "Job WIP Total Entry No." := JobWIPTotal."Entry No.";
      "Job No." := JobWIPTotal."Job No.";
      "Job Task No." := JobWIPTotal."Job Task No.";
      "Warning Message" := Message;
      INSERT;
    END;

    BEGIN
    END.
  }
}

