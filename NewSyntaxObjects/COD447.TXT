OBJECT Codeunit 447 Record Link Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            if CONFIRM(Text001,false) then begin
              RemoveOrphanedLink;
              MESSAGE(Text004,NoOfRemoved);
            end;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Do you want to remove links with no record reference?';
      Text002@1001 : TextConst 'ENU=Removing Record Links without record reference.\';
      Text003@1002 : TextConst 'ENU=@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text004@1003 : TextConst 'ENU=%1 orphaned links were removed.';
      NoOfRemoved@1004 : Integer;

    LOCAL PROCEDURE RemoveOrphanedLink@5();
    VAR
      RecordLink@1000 : Record "Record Link";
      RecordRef@1001 : RecordRef;
      PrevRecID@1006 : RecordID;
      Window@1002 : Dialog;
      i@1003 : Integer;
      Total@1004 : Integer;
      TimeLocked@1005 : Time;
      InTransaction@1011 : Boolean;
      RecordExists@1010 : Boolean;
    BEGIN
      Window.OPEN(Text002 + Text003);
      TimeLocked := TIME;
      with RecordLink do begin
        SETFILTER(Company,'%1|%2','',COMPANYNAME);
        SETCURRENTKEY("Record ID");
        Total := COUNT;
        if Total = 0 then
          exit;
        if FIND('-') then
          repeat
            i := i + 1;
            if (i mod 1000) = 0 then
              Window.UPDATE(1,ROUND(i / Total * 10000,1));
            if FORMAT("Record ID") <> FORMAT(PrevRecID) then begin  // Direct comparison doesn't work.
              PrevRecID := "Record ID";
              RecordExists := RecordRef.GET("Record ID");
            end;
            if not RecordExists then begin
              DELETE;
              NoOfRemoved := NoOfRemoved + 1;
              if not InTransaction then
                TimeLocked := TIME;
              InTransaction := true;
            end;
            if InTransaction and (TIME > (TimeLocked + 1000)) then begin
              COMMIT;
              TimeLocked := TIME;
              InTransaction := false;
            end;
          until NEXT = 0;
      end;
      Window.CLOSE;
    END;

    LOCAL PROCEDURE ResetNotifyOnLinks@11(RecVar@1000 : Variant);
    VAR
      RecordLink@1002 : Record "Record Link";
      RecRef@1001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(RecVar);
      RecordLink.SETRANGE("Record ID",RecRef.RECORDID);
      RecordLink.SETRANGE(Notify,true);
      if not RecordLink.ISEMPTY then
        RecordLink.MODIFYALL(Notify,false);
    END;

    [External]
    PROCEDURE CopyLinks@20(FromRecord@1000 : Variant;ToRecord@1001 : Variant);
    VAR
      RecRefTo@1002 : RecordRef;
    BEGIN
      RecRefTo.GETTABLE(ToRecord);
      RecRefTo.COPYLINKS(FromRecord);
      ResetNotifyOnLinks(RecRefTo);
    END;

    BEGIN
    END.
  }
}

