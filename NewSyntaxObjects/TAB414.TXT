OBJECT Table 414 IC Outbox Transaction
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ICOutboxJnlLine@1000 : Record "IC Outbox Jnl. Line";
               ICOutboxPurchHdr@1001 : Record "IC Outbox Purchase Header";
               ICOutboxSalesHdr@1002 : Record "IC Outbox Sales Header";
               ICCommentLine@1003 : Record "IC Comment Line";
             BEGIN
               case "Source Type" of
                 "Source Type"::"Journal Line":
                   begin
                     ICOutboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
                     ICOutboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
                     ICOutboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
                     if ICOutboxJnlLine.FINDFIRST then
                       ICOutboxJnlLine.DELETEALL(true);
                   end;
                 "Source Type"::"Sales Document":
                   begin
                     ICOutboxSalesHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICOutboxSalesHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICOutboxSalesHdr.SETRANGE("Transaction Source","Transaction Source");
                     if ICOutboxSalesHdr.FINDFIRST then
                       ICOutboxSalesHdr.DELETE(true);
                   end;
                 "Source Type"::"Purchase Document":
                   begin
                     ICOutboxPurchHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICOutboxPurchHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICOutboxPurchHdr.SETRANGE("Transaction Source","Transaction Source");
                     if ICOutboxPurchHdr.FINDFIRST then
                       ICOutboxPurchHdr.DELETE(true);
                   end;
               end;

               ICCommentLine.SETRANGE("Table Name",ICCommentLine."Table Name"::"IC Outbox Transaction");
               ICCommentLine.SETRANGE("Transaction No.","Transaction No.");
               ICCommentLine.SETRANGE("IC Partner Code","IC Partner Code");
               ICCommentLine.SETRANGE("Transaction Source","Transaction Source");
               if ICCommentLine.FIND('-') then
                 repeat
                   ICCommentLine.DELETE(true);
                 until ICCommentLine.NEXT = 0;
             END;

    CaptionML=ENU=IC Outbox Transaction;
  }
  FIELDS
  {
    { 1   ;   ;Transaction No.     ;Integer       ;CaptionML=ENU=Transaction No.;
                                                   Editable=false }
    { 2   ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner".Code;
                                                   CaptionML=ENU=IC Partner Code;
                                                   Editable=false }
    { 3   ;   ;Source Type         ;Option        ;CaptionML=ENU=Source Type;
                                                   OptionCaptionML=ENU=Journal Line,Sales Document,Purchase Document;
                                                   OptionString="Journal Line","Sales Document","Purchase Document";
                                                   Editable=false }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order";
                                                   OptionString=" ",Payment,Invoice,"Credit Memo",Refund,Order,"Return Order";
                                                   Editable=false }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No.;
                                                   Editable=false }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date;
                                                   Editable=false }
    { 8   ;   ;Transaction Source  ;Option        ;CaptionML=ENU=Transaction Source;
                                                   OptionCaptionML=ENU=Rejected by Current Company,Created by Current Company;
                                                   OptionString="Rejected by Current Company","Created by Current Company";
                                                   Editable=false }
    { 9   ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date;
                                                   Editable=false }
    { 10  ;   ;Line Action         ;Option        ;OnValidate=BEGIN
                                                                case "Line Action" of
                                                                  "Line Action"::"Return to Inbox":
                                                                    TESTFIELD("Transaction Source","Transaction Source"::"Rejected by Current Company");
                                                                  "Line Action"::"Send to IC Partner":
                                                                    OutboxCheckSend;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Line Action;
                                                   OptionCaptionML=ENU=No Action,Send to IC Partner,Return to Inbox,Cancel;
                                                   OptionString="No Action","Send to IC Partner","Return to Inbox",Cancel }
    { 12  ;   ;IC Partner G/L Acc. No.;Code20     ;CaptionML=ENU=IC Partner G/L Acc. No. }
    { 13  ;   ;Source Line No.     ;Integer       ;CaptionML=ENU=Source Line No. }
  }
  KEYS
  {
    {    ;"Transaction No.","IC Partner Code","Transaction Source","Document Type";
                                                   Clustered=true }
    {    ;"IC Partner Code"                        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE ShowDetails@1();
    VAR
      ICOutboxJnlLine@1000 : Record "IC Outbox Jnl. Line";
      ICOutboxSalesHeader@1001 : Record "IC Outbox Sales Header";
      ICOutboxPurchHeader@1002 : Record "IC Outbox Purchase Header";
      ICOutboxJnlLines@1005 : Page "IC Outbox Jnl. Lines";
      ICOutboxSalesDoc@1004 : Page "IC Outbox Sales Doc.";
      ICOutboxPurchDoc@1003 : Page "IC Outbox Purchase Doc.";
    BEGIN
      case "Source Type" of
        "Source Type"::"Journal Line":
          begin
            ICOutboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
            ICOutboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
            ICOutboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(ICOutboxJnlLines);
            ICOutboxJnlLines.SETTABLEVIEW(ICOutboxJnlLine);
            ICOutboxJnlLines.RUNMODAL;
          end;
        "Source Type"::"Sales Document":
          begin
            ICOutboxSalesHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICOutboxSalesHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICOutboxSalesHeader.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(ICOutboxSalesDoc);
            ICOutboxSalesDoc.SETTABLEVIEW(ICOutboxSalesHeader);
            ICOutboxSalesDoc.RUNMODAL;
          end;
        "Source Type"::"Purchase Document":
          begin
            ICOutboxPurchHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICOutboxPurchHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICOutboxPurchHeader.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(ICOutboxPurchDoc);
            ICOutboxPurchDoc.SETTABLEVIEW(ICOutboxPurchHeader);
            ICOutboxPurchDoc.RUNMODAL;
          end;
      end;
    END;

    LOCAL PROCEDURE OutboxCheckSend@36();
    VAR
      HandledICOutboxTrans@1001 : Record "Handled IC Outbox Trans.";
      ICOutboxTransaction2@1002 : Record "IC Outbox Transaction";
      Text001@1003 : TextConst 'ENU=Transaction No. %2 is a copy of Transaction No. %1, which has already been set to Send to IC Partner.\Do you also want to send Transaction No. %2?';
      Text002@1000 : TextConst 'ENU=A copy of Transaction No. %1 has already been sent to IC Partner and is now in the Handled IC Outbox Transactions window.\Do you also want to send Transaction No. %1?';
    BEGIN
      HandledICOutboxTrans.SETRANGE("Source Type","Source Type");
      HandledICOutboxTrans.SETRANGE("Document Type","Document Type");
      HandledICOutboxTrans.SETRANGE("Document No.","Document No.");
      if HandledICOutboxTrans.FINDFIRST then
        if not CONFIRM(STRSUBSTNO(Text002,"Transaction No."),false) then
          ERROR('');

      ICOutboxTransaction2.SETRANGE("Source Type","Source Type");
      ICOutboxTransaction2.SETRANGE("Document Type","Document Type");
      ICOutboxTransaction2.SETRANGE("Document No.","Document No.");
      ICOutboxTransaction2.SETFILTER("Transaction No.",'<>%1',"Transaction No.");
      ICOutboxTransaction2.SETRANGE("IC Partner G/L Acc. No.","IC Partner G/L Acc. No.");
      ICOutboxTransaction2.SETRANGE("Source Line No.","Source Line No.");
      ICOutboxTransaction2.SETRANGE("Line Action","Line Action"::"Send to IC Partner");
      if ICOutboxTransaction2.FINDFIRST then
        if not CONFIRM(STRSUBSTNO(Text001,ICOutboxTransaction2."Transaction No.","Transaction No."),false) then
          ERROR('');
    END;

    BEGIN
    END.
  }
}

