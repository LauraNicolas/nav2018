OBJECT Codeunit 5613 Calculate Acq. Cost Depr.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "FA Ledger Entry"=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 field must not have a check mark because %2 is zero or negative for %3.';

    [External]
    PROCEDURE DeprCalc@1(VAR DeprAmount@1000 : Decimal;VAR Custom1Amount@1001 : Decimal;FANo@1002 : Code[20];DeprBookCode@1003 : Code[10];LocalDeprBasis@1004 : Decimal;Custom1LocalDeprBasis@1005 : Decimal);
    VAR
      DeprBook@1006 : Record "Depreciation Book";
      FADeprBook@1007 : Record "FA Depreciation Book";
      DepreciationCalc@1008 : Codeunit "Depreciation Calculation";
      DeprBasis@1009 : Decimal;
    BEGIN
      DeprAmount := 0;
      Custom1Amount := 0;
      DeprBook.GET(DeprBookCode);
      with FADeprBook do begin
        if not GET(FANo,DeprBookCode) then
          exit;
        CALCFIELDS(Depreciation,"Acquisition Cost","Depreciable Basis");
        DeprBasis := "Depreciable Basis" - LocalDeprBasis;
        if DeprBasis <= 0 then
          CreateError(FANo,DeprBookCode);
        if DeprBasis > 0 then
          DeprAmount :=
            DepreciationCalc.CalcRounding(
              DeprBookCode,(Depreciation * LocalDeprBasis) / DeprBasis);
        if DeprBook."Use Custom 1 Depreciation" and
           ("Depr. Ending Date (Custom 1)" > 0D)
        then begin
          DeprBasis := "Acquisition Cost" - Custom1LocalDeprBasis;
          CALCFIELDS("Custom 1");
          if DeprBasis <= 0 then
            CreateError(FANo,DeprBookCode);
          if DeprBasis > 0 then
            Custom1Amount :=
              DepreciationCalc.CalcRounding(
                DeprBookCode,("Custom 1" * Custom1LocalDeprBasis) / DeprBasis);
        end;
      end;
    END;

    LOCAL PROCEDURE CreateError@2(FANo@1000 : Code[20];DeprBookCode@1001 : Code[20]);
    VAR
      GenJnlLine@1003 : Record "Gen. Journal Line";
      FA@1002 : Record "Fixed Asset";
      FADeprBook@1004 : Record "FA Depreciation Book";
      DepreciationCalc@1005 : Codeunit "Depreciation Calculation";
    BEGIN
      FA."No." := FANo;
      ERROR(
        Text000,
        GenJnlLine.FIELDCAPTION("Depr. Acquisition Cost"),
        FADeprBook.FIELDCAPTION("Depreciable Basis"),DepreciationCalc.FAName(FA,DeprBookCode));
    END;

    BEGIN
    END.
  }
}

