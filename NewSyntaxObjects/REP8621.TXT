OBJECT Report 8621 Config. Package - Process
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TransactionType=UpdateNoLocks;
    CaptionML=ENU=Config. Package - Process;
    ProcessingOnly=true;
  }
  DATASET
  {
    { 1   ;    ;DataItem;Config. Package Table;
               DataItemTable="Config. Package Table";
               DataItemTableView=SORTING("Package Code","Table ID") ORDER(Ascending);
               OnAfterGetRecord=BEGIN
                                  MESSAGE(STRSUBSTNO(ImplementProcessingLogicMsg,"Table ID"));

                                  // Code sample of the text transformation on package data
                                  // ProcessCustomRulesExample("Config. Package Table");
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      SourceTable="Config. Package Table";
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ImplementProcessingLogicMsg@1001 : TextConst '@@@=%1 - a table Id.;ENU=Implement processing logic for Table %1 in Report 8621 - Config. Package - Process.';

    LOCAL PROCEDURE ApplyTextTransformation@25(ConfigPackageTable@1001 : Record "Config. Package Table";FieldNo@1002 : Integer;TransformationRule@1000 : Record "Transformation Rule");
    VAR
      ConfigPackageData@1003 : Record "Config. Package Data";
    BEGIN
      if GetConfigPackageData(ConfigPackageData,ConfigPackageTable,FieldNo) then
        repeat
          ConfigPackageData.Value := COPYSTR(TransformationRule.TransformText(ConfigPackageData.Value),1,250);
          ConfigPackageData.MODIFY;
        until ConfigPackageData.NEXT = 0;
    END;

    LOCAL PROCEDURE GetConfigPackageData@4(VAR ConfigPackageData@1004 : Record "Config. Package Data";ConfigPackageTable@1000 : Record "Config. Package Table";FieldId@1001 : Integer) : Boolean;
    BEGIN
      ConfigPackageData.SETRANGE("Package Code",ConfigPackageTable."Package Code");
      ConfigPackageData.SETRANGE("Table ID",ConfigPackageTable."Table ID");
      ConfigPackageData.SETRANGE("Field ID",FieldId);
      exit(ConfigPackageData.FINDSET(true,false));
    END;

    [Internal]
    PROCEDURE ProcessCustomRulesExample@5(ConfigPackageTable@1000 : Record "Config. Package Table");
    VAR
      Customer@1001 : Record Customer;
      BankAccount@1002 : Record "Bank Account";
      PaymentTerms@1003 : Record "Payment Terms";
      TransformationRule@1004 : Record "Transformation Rule";
    BEGIN
      case ConfigPackageTable."Table ID" of
        DATABASE::"Payment Terms":
          begin
            TransformationRule."Transformation Type" := TransformationRule."Transformation Type"::"Title Case";
            ApplyTextTransformation(ConfigPackageTable,PaymentTerms.FIELDNO(Description),TransformationRule);
          end;
        DATABASE::"Bank Account":
          begin
            TransformationRule."Transformation Type" :=
              TransformationRule."Transformation Type"::"Remove Non-Alphanumeric Characters";
            ApplyTextTransformation(ConfigPackageTable,BankAccount.FIELDNO("SWIFT Code"),TransformationRule);
            ApplyTextTransformation(ConfigPackageTable,BankAccount.FIELDNO(IBAN),TransformationRule);
          end;
        DATABASE::Customer:
          begin
            TransformationRule."Transformation Type" := TransformationRule."Transformation Type"::Replace;
            TransformationRule."Find Value" := 'Mister';
            TransformationRule."Replace Value" := 'Mr.';
            ApplyTextTransformation(ConfigPackageTable,Customer.FIELDNO(Name),TransformationRule);
          end;
      end;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

