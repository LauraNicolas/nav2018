OBJECT Report 5690 Index Fixed Assets
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Index Fixed Assets;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  if FAPostingDate = 0D then
                    ERROR(Text000,FAJnlLine.FIELDCAPTION("FA Posting Date"));
                  if FAPostingDate <> NORMALDATE(FAPostingDate) then
                    ERROR(Text001);

                  if PostingDate = 0D then
                    PostingDate := FAPostingDate;
                  DeprBook.GET(DeprBookCode);
                  DeprBook.TESTFIELD("Allow Indexation",true);
                  if DeprBook."Use Same FA+G/L Posting Dates" and (FAPostingDate <> PostingDate) then
                    ERROR(
                      Text002,
                      FAJnlLine.FIELDCAPTION("FA Posting Date"),
                      FAJnlLine.FIELDCAPTION("Posting Date"),
                      DeprBook.FIELDCAPTION("Use Same FA+G/L Posting Dates"),
                      false,
                      DeprBook.TABLECAPTION,
                      DeprBook.FIELDCAPTION(Code),
                      DeprBook.Code);

                  DeprBook.IndexGLIntegration(GLIntegration);
                  FirstGenJnl := true;
                  FirstFAJnl := true;

                  FALedgEntry.SETCURRENTKEY(
                    "FA No.","Depreciation Book Code","FA Posting Category","FA Posting Type","FA Posting Date");
                  FALedgEntry.SETRANGE("Depreciation Book Code",DeprBookCode);
                  FALedgEntry.SETRANGE("FA Posting Category",FALedgEntry."FA Posting Category"::" ");
                  FALedgEntry.SETRANGE("FA Posting Date",0D,FAPostingDate);

                  MaintenanceLedgEntry.SETCURRENTKEY(
                    "FA No.","Depreciation Book Code","FA Posting Date");
                  MaintenanceLedgEntry.SETRANGE("Depreciation Book Code",DeprBookCode);
                  MaintenanceLedgEntry.SETRANGE("FA Posting Date",0D,FAPostingDate);

                  Window.OPEN(Text003);
                END;

  }
  DATASET
  {
    { 3794;    ;DataItem;Fixed Asset         ;
               DataItemTable="Fixed Asset";
               OnAfterGetRecord=BEGIN
                                  if not FADeprBook.GET("No.",DeprBookCode) then
                                    CurrReport.SKIP;
                                  Window.UPDATE(1,"No.");

                                  if Inactive or
                                     Blocked or
                                     (FADeprBook."Disposal Date" > 0D) or
                                     (FADeprBook."Acquisition Date" = 0D) or
                                     (FADeprBook."Acquisition Date" > FAPostingDate)
                                  then
                                    CurrReport.SKIP;

                                  FALedgEntry.SETRANGE("FA No.","No.");
                                  MaintenanceLedgEntry.SETRANGE("FA No.","No.");

                                  i := 0;
                                  while i < 9 do begin
                                    i := i + 1;
                                    if i = 7 then
                                      i := 8;
                                    IndexAmount := 0;
                                    if IndexChoices[i] then begin
                                      if i = 1 then
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Acquisition Cost");
                                      if i = 2 then
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::Depreciation);
                                      if i = 3 then
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Write-Down");
                                      if i = 4 then
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::Appreciation);
                                      if i = 5 then
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Custom 1");
                                      if i = 6 then
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Custom 2");
                                      if i = 9 then
                                        FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Salvage Value");
                                      if i = 8 then begin
                                        MaintenanceLedgEntry.CALCSUMS(Amount);
                                        IndexAmount := MaintenanceLedgEntry.Amount;
                                      end else begin
                                        FALedgEntry.CALCSUMS(Amount);
                                        IndexAmount := FALedgEntry.Amount;
                                      end;
                                      IndexAmount :=
                                        DepreciationCalc.CalcRounding(DeprBookCode,IndexAmount * (IndexFigure / 100 - 1));
                                      if not GLIntegration[i] or "Budgeted Asset" then
                                        InsertFAJnlLine("No.",IndexAmount,i - 1)
                                      else
                                        InsertGenJnlLine("No.",IndexAmount,i - 1);
                                    end;
                                  end;
                                END;

               ReqFilterFields="No.","FA Class Code","FA Subclass Code","Budgeted Asset" }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   if DeprBookCode = '' then begin
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   end;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 15  ;2   ;Field     ;
                  Name=DepreciationBook;
                  CaptionML=ENU=Depreciation Book;
                  ToolTipML=ENU=Specifies the code for the depreciation book to be included in the report or batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 5   ;2   ;Field     ;
                  Name=IndexFigure;
                  CaptionML=ENU=Index Figure;
                  ToolTipML=[ENU="Specifies an index figure that is to calculate the index amounts entered in the journal. For example, if you want to index by 2%, enter 102 in this field; if you want to index by -3% percent, enter 97 in this field."];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=IndexFigure;
                  MinValue=0 }

      { 9   ;2   ;Field     ;
                  Name=FAPostingDate;
                  CaptionML=ENU=FA Posting Date;
                  ToolTipML=ENU=Specifies the fixed asset posting date to be used by the batch job. The batch job includes ledger entries up to this date. This date appears in the FA Posting Date field in the resulting journal lines. If the Use Same FA+G/L Posting Dates field has been activated in the depreciation book that is used in the batch job, then this date must be the same as the posting date entered in the Posting Date field.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=FAPostingDate }

      { 11  ;2   ;Field     ;
                  Name=Posting Date;
                  CaptionML=ENU=Posting Date;
                  ToolTipML=ENU=Specifies the posting date to be used by the batch job. This date appears in the Posting Date field in the resulting FA G/L journal lines for assets that are integrated with the general ledger. If the Use Same FA+G/L Posting Dates field has been activated in the depreciation book that is used in the batch job, then this date must be the same as the fixed asset posting date entered in the FA Posting Date field.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDate }

      { 23  ;2   ;Field     ;
                  Name=Document No.;
                  CaptionML=ENU=Document No.;
                  ToolTipML=ENU=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  Name=Posting Description;
                  CaptionML=ENU=Posting Description;
                  ToolTipML=ENU=Specifies a posting description to appear on the resulting journal lines.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDescription }

      { 20  ;2   ;Field     ;
                  Name=InsertBalAccount;
                  CaptionML=ENU=Insert Bal. Account;
                  ToolTipML=ENU=Specifies if you want the batch job to automatically insert balancing accounts in the resulting journal. The batch job uses the accounts that you defined in the FA Posting Group table.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=BalAccount }

      { 4   ;2   ;Group     ;
                  Name=Index;
                  CaptionML=ENU=Index }

      { 3   ;3   ;Field     ;
                  Name=Acquisition Cost;
                  CaptionML=ENU=Acquisition Cost;
                  ToolTipML=ENU=Specifies the total percentage of acquisition cost that can be allocated when acquisition cost is posted.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=IndexChoices[1] }

      { 1   ;3   ;Field     ;
                  Name=Depreciation;
                  CaptionML=ENU=Depreciation;
                  ToolTipML=ENU=Specifies if you want to index for fixed assets of transaction type Depreciation in the batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=IndexChoices[2] }

      { 2   ;3   ;Field     ;
                  Name=Write-Down;
                  CaptionML=ENU=Write-Down;
                  ToolTipML=ENU=Specifies if you want to index for fixed assets of transaction type Write-down in the batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=IndexChoices[3] }

      { 7   ;3   ;Field     ;
                  Name=Appreciation;
                  CaptionML=ENU=Appreciation;
                  ToolTipML=ENU=Specifies if appreciations are included by the batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=IndexChoices[4] }

      { 8   ;3   ;Field     ;
                  Name=Custom 1;
                  CaptionML=ENU=Custom 1;
                  ToolTipML=ENU=Specifies if you want to index for fixed assets of transaction a custom type in the batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=IndexChoices[5] }

      { 17  ;3   ;Field     ;
                  Name=Custom 2;
                  CaptionML=ENU=Custom 2;
                  ToolTipML=ENU=Specifies if you want to index for fixed assets of transaction a custom type in the batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=IndexChoices[6] }

      { 18  ;3   ;Field     ;
                  Name=Salvage Value;
                  CaptionML=ENU=Salvage Value;
                  ToolTipML=ENU=Specifies if you want to index for fixed assets of transaction type Salvage Value in the batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=IndexChoices[9] }

      { 19  ;3   ;Field     ;
                  Name=Maintenance;
                  CaptionML=ENU=Maintenance;
                  ToolTipML=ENU=Specifies if you want to index for fixed assets of transaction type Salvage Value in the batch job.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=IndexChoices[8] }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.';
      Text001@1001 : TextConst 'ENU=FA Posting Date must not be a closing date.';
      Text002@1002 : TextConst 'ENU="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7."';
      Text003@1003 : TextConst 'ENU=Indexing fixed asset   #1##########';
      GenJnlLine@1004 : Record "Gen. Journal Line";
      FASetup@1005 : Record "FA Setup";
      FAJnlLine@1006 : Record "FA Journal Line";
      DeprBook@1007 : Record "Depreciation Book";
      FADeprBook@1008 : Record "FA Depreciation Book";
      FALedgEntry@1009 : Record "FA Ledger Entry";
      MaintenanceLedgEntry@1010 : Record "Maintenance Ledger Entry";
      FAJnlSetup@1011 : Record "FA Journal Setup";
      DepreciationCalc@1012 : Codeunit "Depreciation Calculation";
      Window@1013 : Dialog;
      FAPostingDate@1014 : Date;
      IndexFigure@1015 : Decimal;
      IndexChoices@1016 : ARRAY [9] OF Boolean;
      IndexAmount@1017 : Decimal;
      GLIntegration@1018 : ARRAY [9] OF Boolean;
      FirstGenJnl@1019 : Boolean;
      FirstFAJnl@1020 : Boolean;
      PostingDate@1021 : Date;
      DocumentNo@1022 : Code[20];
      DocumentNo2@1023 : Code[20];
      DocumentNo3@1024 : Code[20];
      NoSeries2@1025 : Code[10];
      Noseries3@1026 : Code[10];
      PostingDescription@1027 : Text[50];
      DeprBookCode@1028 : Code[10];
      BalAccount@1029 : Boolean;
      FAJnlNextLineNo@1030 : Integer;
      GenJnlNextLineNo@1031 : Integer;
      i@1032 : Integer;

    LOCAL PROCEDURE InsertGenJnlLine@2(FANo@1000 : Code[20];IndexAmount@1001 : Decimal;PostingType@1002 : '"Acquisition Cost",Depreciation,"Write-Down",Appreciation,"Custom 1","Custom 2",Disposal,Maintenance,"Salvage Value"');
    VAR
      FAInsertGLAcc@1003 : Codeunit "FA Insert G/L Account";
    BEGIN
      if IndexAmount = 0 then
        exit;
      if FirstGenJnl then begin
        GenJnlLine.LOCKTABLE;
        FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
        NoSeries2 := FAJnlSetup.GetGenNoSeries(GenJnlLine);
        if DocumentNo = '' then
          DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,FAPostingDate,true)
        else
          DocumentNo2 := DocumentNo;
        FirstGenJnl := false;
      end;
      with GenJnlLine do begin
        INIT;
        "Line No." := 0;
        FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
        "FA Posting Date" := FAPostingDate;
        "Posting Date" := PostingDate;
        if "Posting Date" = "FA Posting Date" then
          "FA Posting Date" := 0D;
        "FA Posting Type" := PostingType + 1;
        "Account Type" := "Account Type"::"Fixed Asset";
        VALIDATE("Account No.",FANo);
        "Document No." := DocumentNo2;
        "Posting No. Series" := NoSeries2;
        Description := PostingDescription;
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,IndexAmount);
        "Index Entry" := true;
        GenJnlNextLineNo := GenJnlNextLineNo + 10000;
        "Line No." := GenJnlNextLineNo;
        INSERT(true);
        if BalAccount then begin
          FAInsertGLAcc.GetBalAcc(GenJnlLine);
          if FINDLAST then ;
          GenJnlNextLineNo := "Line No.";
        end;
      end;
    END;

    LOCAL PROCEDURE InsertFAJnlLine@3(FANo@1000 : Code[20];IndexAmount@1001 : Decimal;PostingType@1002 : '"Acquisition Cost",Depreciation,"Write-Down",Appreciation,"Custom 1","Custom 2",Disposal,Maintenance,"Salvage Value"');
    BEGIN
      if IndexAmount = 0 then
        exit;
      if FirstFAJnl then begin
        FAJnlLine.LOCKTABLE;
        FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
        Noseries3 := FAJnlSetup.GetFANoSeries(FAJnlLine);
        if DocumentNo = '' then
          DocumentNo3 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,FAPostingDate,true)
        else
          DocumentNo3 := DocumentNo;
        FirstFAJnl := false;
      end;
      with FAJnlLine do begin
        INIT;
        "Line No." := 0;
        FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
        "Posting Date" := PostingDate;
        "FA Posting Date" := FAPostingDate;
        if "Posting Date" = "FA Posting Date" then
          "Posting Date" := 0D;
        "FA Posting Type" := PostingType;
        VALIDATE("FA No.",FANo);
        "Document No." := DocumentNo3;
        "Posting No. Series" := Noseries3;
        Description := PostingDescription;
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,IndexAmount);
        "Index Entry" := true;
        FAJnlNextLineNo := FAJnlNextLineNo + 10000;
        "Line No." := FAJnlNextLineNo;
        INSERT(true);
      end;
    END;

    PROCEDURE InitializeRequest@1(DeprBookCodeFrom@1000 : Code[10];IndexFigureFrom@1001 : Decimal;FAPostingDateFrom@1002 : Date;PostingDateFrom@1003 : Date;DocumentNoFrom@1004 : Code[20];PostingDescriptionFrom@1005 : Text[50];BalAccountFrom@1006 : Boolean);
    BEGIN
      DeprBookCode := DeprBookCodeFrom;
      IndexFigure := IndexFigureFrom;
      FAPostingDate := FAPostingDateFrom;
      PostingDate := PostingDateFrom;
      DocumentNo := DocumentNoFrom;
      PostingDescription := PostingDescriptionFrom;
      BalAccount := BalAccountFrom;
    END;

    PROCEDURE SetIndexAcquisitionCost@4(IndexChoice@1000 : Boolean);
    BEGIN
      IndexChoices[1] := IndexChoice;
    END;

    PROCEDURE SetIndexDepreciation@5(IndexChoice@1000 : Boolean);
    BEGIN
      IndexChoices[2] := IndexChoice;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

