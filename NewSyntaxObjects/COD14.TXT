OBJECT Codeunit 14 Gen. Jnl.-Show Entries
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo="Gen. Journal Line";
    OnRun=BEGIN
            case "Account Type" of
              "Account Type"::"G/L Account":
                begin
                  GLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
                  GLEntry.SETRANGE("G/L Account No.","Account No.");
                  if GLEntry.FINDLAST then;
                  PAGE.RUN(PAGE::"General Ledger Entries",GLEntry);
                end;
              "Account Type"::Customer:
                begin
                  CustLedgEntry.SETCURRENTKEY("Customer No.","Posting Date");
                  CustLedgEntry.SETRANGE("Customer No.","Account No.");
                  if CustLedgEntry.FINDLAST then;
                  PAGE.RUN(PAGE::"Customer Ledger Entries",CustLedgEntry);
                end;
              "Account Type"::Vendor:
                begin
                  VendLedgEntry.SETCURRENTKEY("Vendor No.","Posting Date");
                  VendLedgEntry.SETRANGE("Vendor No.","Account No.");
                  if VendLedgEntry.FINDLAST then;
                  PAGE.RUN(PAGE::"Vendor Ledger Entries",VendLedgEntry);
                end;
              "Account Type"::"Bank Account":
                begin
                  BankAccLedgEntry.SETCURRENTKEY("Bank Account No.","Posting Date");
                  BankAccLedgEntry.SETRANGE("Bank Account No.","Account No.");
                  if BankAccLedgEntry.FINDLAST then;
                  PAGE.RUN(PAGE::"Bank Account Ledger Entries",BankAccLedgEntry);
                end;
              "Account Type"::"Fixed Asset":
                if "FA Posting Type" <> "FA Posting Type"::Maintenance then begin
                  FALedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
                  FALedgEntry.SETRANGE("FA No.","Account No.");
                  if "Depreciation Book Code" <> '' then
                    FALedgEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
                  if FALedgEntry.FINDLAST then;
                  PAGE.RUN(PAGE::"FA Ledger Entries",FALedgEntry);
                end else begin
                  MaintenanceLedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
                  MaintenanceLedgEntry.SETRANGE("FA No.","Account No.");
                  if "Depreciation Book Code" <> '' then
                    MaintenanceLedgEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
                  if MaintenanceLedgEntry.FINDLAST then;
                  PAGE.RUN(PAGE::"Maintenance Ledger Entries",MaintenanceLedgEntry);
                end;
              "Account Type"::"IC Partner":
                ERROR(Text001);
            end;
          END;

  }
  CODE
  {
    VAR
      GLEntry@1000 : Record "G/L Entry";
      CustLedgEntry@1001 : Record "Cust. Ledger Entry";
      VendLedgEntry@1002 : Record "Vendor Ledger Entry";
      BankAccLedgEntry@1003 : Record "Bank Account Ledger Entry";
      FALedgEntry@1004 : Record "FA Ledger Entry";
      MaintenanceLedgEntry@1005 : Record "Maintenance Ledger Entry";
      Text001@1006 : TextConst 'ENU=Intercompany partners do not have ledger entries.';

    BEGIN
    END.
  }
}

