OBJECT Report 5683 Create Sum of Digits Table
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Sum of Digits Table;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  if NoOfYears < 1 then
                    ERROR(Text000);
                  if NoOfYears >= 200 then
                    ERROR(Text001);

                  CreateDeprTableLines(DeprTableCode,NoOfYears);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  Name=NoOfYears;
                  CaptionML=ENU=No. of Years;
                  ToolTipML=ENU=Specifies the number of years over which the fixed asset will be depreciated.;
                  ApplicationArea=#FixedAssets;
                  BlankZero=true;
                  SourceExpr=NoOfYears;
                  MinValue=0 }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      NoOfYears@1000 : Integer;
      DeprTableCode@1001 : Code[20];
      Text000@1002 : TextConst 'ENU=You must specify No. of Years.';
      Text001@1003 : TextConst 'ENU=No. of Years must be less than 200.';

    PROCEDURE SetTableCode@1(DeprTableCode2@1000 : Code[20]);
    BEGIN
      DeprTableCode := DeprTableCode2;
    END;

    LOCAL PROCEDURE CreateDeprTableLines@2(DeprTableCode@1000 : Code[10];NoOfYears@1001 : Integer);
    VAR
      DeprTableHeader@1002 : Record "Depreciation Table Header";
      DeprTableLine@1003 : Record "Depreciation Table Line";
      TotalDigitSum@1004 : Integer;
      TotalPercentSum@1007 : Decimal;
      CurrentDigit@1005 : Integer;
      CurrentDigitSum@1006 : Integer;
    BEGIN
      DeprTableHeader.GET(DeprTableCode);
      DeprTableHeader."Period Length" := DeprTableHeader."Period Length"::Year;
      DeprTableHeader."Total No. of Units" := 0;
      DeprTableLine.SETRANGE("Depreciation Table Code",DeprTableCode);
      DeprTableLine.DELETEALL;
      DeprTableHeader.MODIFY;

      CLEAR(DeprTableLine);
      DeprTableLine."Depreciation Table Code" := DeprTableCode;
      DeprTableLine."Period No." := 0;
      TotalDigitSum := 0;
      CurrentDigitSum := 0;
      TotalPercentSum := 0;
      for CurrentDigit := 1 to NoOfYears do
        TotalDigitSum := TotalDigitSum + CurrentDigit;
      for CurrentDigit := NoOfYears downto 1 do begin
        DeprTableLine."Period Depreciation %" := ROUND((CurrentDigit + CurrentDigitSum) / TotalDigitSum * 100,0.00000001) -
          TotalPercentSum;
        CurrentDigitSum := CurrentDigitSum + CurrentDigit;
        TotalPercentSum := TotalPercentSum + DeprTableLine."Period Depreciation %";
        DeprTableLine."Period No." := DeprTableLine."Period No." + 1;
        DeprTableLine.INSERT;
      end;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

