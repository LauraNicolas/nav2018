OBJECT Codeunit 571 Categ. Generate Acc. Schedules
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            CreateBalanceSheet;
            CreateIncomeStatement;
            CreateCashFlowStatement;
            CreateRetainedEarningsStatement;
          END;

  }
  CODE
  {
    VAR
      TotalingTxt@1003 : TextConst '@@@="%1 = Account category, e.g. Assets";ENU=Total %1';
      Totaling2Txt@1015 : TextConst '@@@="%1 and %2 = Account category, e.g. Assets";ENU=Total %1 & %2';
      GrossProfitTxt@1002 : TextConst 'ENU=Gross Profit';
      GeneralLedgerSetup@1004 : Record "General Ledger Setup";
      NetIncomeTxt@1005 : TextConst 'ENU=Net Income';
      AdjustmentsTxt@1006 : TextConst 'ENU=Adjustments to reconcile Net Income to net cash provided by operations:';
      NetCashProviededTxt@1008 : TextConst '@@@="%1=Operating Activities or Investing Activities";ENU=Net Cash Provided by %1';
      NetCashIncreaseTxt@1009 : TextConst 'ENU=Net Cash Increase for the Period';
      CashAtPeriodStartTxt@1010 : TextConst 'ENU=Cash at Beginning of the Period';
      CashAtPeriodEndTxt@1011 : TextConst 'ENU=Cash at End of the Period';
      DistribToShareholdersTxt@1012 : TextConst 'ENU=Distributions to Shareholders';
      RetainedEarningsPrimoTxt@1014 : TextConst 'ENU=Retained Earnings, Period Start';
      RetainedEarningsUltimoTxt@1013 : TextConst 'ENU=Retained Earnings, Period End';

    LOCAL PROCEDURE CreateBalanceSheet@2();
    VAR
      AccScheduleName@1004 : Record "Acc. Schedule Name";
      AccScheduleLine@1003 : Record "Acc. Schedule Line";
      GLAccountCategory@1002 : Record "G/L Account Category";
      GLAccountCategoryMgt@1001 : Codeunit "G/L Account Category Mgt.";
      RowNo@1000 : Integer;
      LiabilitiesRowNo@1005 : Code[10];
      EquityRowNo@1006 : Code[10];
    BEGIN
      GLAccountCategoryMgt.GetGLSetup(GeneralLedgerSetup);
      GeneralLedgerSetup.TESTFIELD("Acc. Sched. for Balance Sheet");
      AccScheduleName.GET(GeneralLedgerSetup."Acc. Sched. for Balance Sheet");
      AccScheduleLine.SETRANGE("Schedule Name",AccScheduleName.Name);
      AccScheduleLine.DELETEALL;
      AccScheduleLine."Schedule Name" := AccScheduleName.Name;

      AddAccSchedLineGroup(AccScheduleLine,RowNo,GLAccountCategory."Account Category"::Assets);
      AddBlankLine(AccScheduleLine,RowNo);
      AddAccSchedLineGroup(AccScheduleLine,RowNo,GLAccountCategory."Account Category"::Liabilities);
      LiabilitiesRowNo := AccScheduleLine."Row No.";
      AddBlankLine(AccScheduleLine,RowNo);
      AddAccSchedLineGroup(AccScheduleLine,RowNo,GLAccountCategory."Account Category"::Equity);
      EquityRowNo := AccScheduleLine."Row No.";
      AddBlankLine(AccScheduleLine,RowNo);
      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::Formula,
        STRSUBSTNO(Totaling2Txt,GLAccountCategory."Account Category"::Liabilities,GLAccountCategory."Account Category"::Equity),
        STRSUBSTNO('%1+%2',LiabilitiesRowNo,EquityRowNo),
        true,true,true,0);
    END;

    LOCAL PROCEDURE CreateIncomeStatement@7();
    VAR
      AccScheduleName@1004 : Record "Acc. Schedule Name";
      AccScheduleLine@1003 : Record "Acc. Schedule Line";
      GLAccountCategory@1002 : Record "G/L Account Category";
      GLAccountCategoryMgt@1001 : Codeunit "G/L Account Category Mgt.";
      RowNo@1000 : Integer;
      TurnoverRownNo@1005 : Integer;
      COGSRowNo@1006 : Integer;
      GrossProfitRowNo@1008 : Integer;
      ExpensesRowNo@1007 : Integer;
    BEGIN
      GLAccountCategoryMgt.GetGLSetup(GeneralLedgerSetup);
      GeneralLedgerSetup.TESTFIELD("Acc. Sched. for Income Stmt.");
      AccScheduleName.GET(GeneralLedgerSetup."Acc. Sched. for Income Stmt.");
      AccScheduleLine.SETRANGE("Schedule Name",AccScheduleName.Name);
      AccScheduleLine.DELETEALL;
      AccScheduleLine."Schedule Name" := AccScheduleName.Name;

      AddAccSchedLineGroup(AccScheduleLine,RowNo,GLAccountCategory."Account Category"::Income);
      TurnoverRownNo := RowNo;
      AddBlankLine(AccScheduleLine,RowNo);
      AddAccSchedLineGroup(AccScheduleLine,RowNo,GLAccountCategory."Account Category"::"Cost of Goods Sold");
      COGSRowNo := RowNo;
      AddBlankLine(AccScheduleLine,RowNo);
      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::Formula,GrossProfitTxt,
        STRSUBSTNO('%1+%2',FormatRowNo(TurnoverRownNo,true),FormatRowNo(COGSRowNo,true)),
        true,false,true,0);
      GrossProfitRowNo := RowNo;
      AddBlankLine(AccScheduleLine,RowNo);
      AddAccSchedLineGroup(AccScheduleLine,RowNo,GLAccountCategory."Account Category"::Expense);
      ExpensesRowNo := RowNo;
      AddBlankLine(AccScheduleLine,RowNo);
      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::Formula,NetIncomeTxt,
        STRSUBSTNO('%1+%2',FormatRowNo(GrossProfitRowNo,true),FormatRowNo(ExpensesRowNo,true)),
        true,true,true,0);
    END;

    LOCAL PROCEDURE CreateCashFlowStatement@3();
    VAR
      AccScheduleName@1004 : Record "Acc. Schedule Name";
      AccScheduleLine@1003 : Record "Acc. Schedule Line";
      GLAccountCategory@1002 : Record "G/L Account Category";
      GLAccountCategoryMgt@1001 : Codeunit "G/L Account Category Mgt.";
      RowNo@1000 : Integer;
      OperatingActRowNo@1005 : Code[10];
      InvestingActRowNo@1006 : Code[10];
      FinancingActRowNo@1008 : Code[10];
      NetCashIncreaseRowNo@1009 : Code[10];
      CashBeginningRowNo@1007 : Code[10];
    BEGIN
      GLAccountCategoryMgt.GetGLSetup(GeneralLedgerSetup);
      GeneralLedgerSetup.TESTFIELD("Acc. Sched. for Cash Flow Stmt");
      AccScheduleName.GET(GeneralLedgerSetup."Acc. Sched. for Cash Flow Stmt");
      AccScheduleLine.SETRANGE("Schedule Name",AccScheduleName.Name);
      AccScheduleLine.DELETEALL;
      AccScheduleLine."Schedule Name" := AccScheduleName.Name;

      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
        FORMAT(GLAccountCategory."Additional Report Definition"::"Operating Activities"),'',true,false,true,0);
      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
        NetIncomeTxt,GetIncomeStmtAccFilter,false,false,true,0);
      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
        AdjustmentsTxt,'',false,false,false,0);

      CreateCashFlowActivityPart(AccScheduleLine,RowNo,GLAccountCategory."Additional Report Definition"::"Operating Activities",false);
      OperatingActRowNo := AccScheduleLine."Row No.";

      AddBlankLine(AccScheduleLine,RowNo);

      CreateCashFlowActivityPart(AccScheduleLine,RowNo,GLAccountCategory."Additional Report Definition"::"Investing Activities",true);
      InvestingActRowNo := AccScheduleLine."Row No.";

      AddBlankLine(AccScheduleLine,RowNo);

      CreateCashFlowActivityPart(AccScheduleLine,RowNo,GLAccountCategory."Additional Report Definition"::"Financing Activities",true);
      FinancingActRowNo := AccScheduleLine."Row No.";

      AddBlankLine(AccScheduleLine,RowNo);

      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::Formula,
        NetCashIncreaseTxt,
        STRSUBSTNO('%1+%2+%3',OperatingActRowNo,InvestingActRowNo,FinancingActRowNo),
        false,false,false,0);
      NetCashIncreaseRowNo := AccScheduleLine."Row No.";

      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
        CashAtPeriodStartTxt,
        GetAccFilterForReportingDefinition(GLAccountCategory."Additional Report Definition"::"Cash Accounts"),
        false,true,false,0);
      AccScheduleLine."Row Type" := AccScheduleLine."Row Type"::"Beginning Balance";
      AccScheduleLine.MODIFY;
      CashBeginningRowNo := AccScheduleLine."Row No.";

      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::Formula,
        CashAtPeriodEndTxt,
        STRSUBSTNO('%1+%2',NetCashIncreaseRowNo,CashBeginningRowNo),
        true,true,false,0);
    END;

    LOCAL PROCEDURE CreateCashFlowActivityPart@17(VAR AccScheduleLine@1000 : Record "Acc. Schedule Line";VAR RowNo@1001 : Integer;AddReportDef@1002 : Option;IncludeHeader@1004 : Boolean);
    VAR
      GLAccountCategory@1003 : Record "G/L Account Category";
      FirstRangeRowNo@1005 : Integer;
    BEGIN
      GLAccountCategory."Additional Report Definition" := AddReportDef;
      if IncludeHeader then
        AddAccShedLine(
          AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
          FORMAT(GLAccountCategory."Additional Report Definition"),'',true,false,false,0);

      FirstRangeRowNo := RowNo;
      if AddReportDef = GLAccountCategory."Additional Report Definition"::"Financing Activities" then
        GLAccountCategory.SETFILTER(
          "Additional Report Definition",'%1|%2',
          GLAccountCategory."Additional Report Definition"::"Financing Activities",
          GLAccountCategory."Additional Report Definition"::"Distribution to Shareholders")
      else
        GLAccountCategory.SETRANGE("Additional Report Definition",AddReportDef);
      if GLAccountCategory.FINDSET then begin
        repeat
          AddAccShedLine(
            AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
            GLAccountCategory.Description,GLAccountCategory.GetTotaling,false,false,false,1);
        until GLAccountCategory.NEXT = 0;
        // Last line in group should be underlined
        AccScheduleLine.Underline := true;
        AccScheduleLine.MODIFY;

        AddAccShedLine(
          AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::Formula,
          STRSUBSTNO(NetCashProviededTxt,GLAccountCategory."Additional Report Definition"),
          STRSUBSTNO('%1..%2',FormatRowNo(FirstRangeRowNo,false),FormatRowNo(RowNo,false)),
          true,false,false,0);
      end;
    END;

    LOCAL PROCEDURE CreateRetainedEarningsStatement@27();
    VAR
      AccScheduleName@1004 : Record "Acc. Schedule Name";
      AccScheduleLine@1003 : Record "Acc. Schedule Line";
      GLAccountCategory@1002 : Record "G/L Account Category";
      GLAccountCategoryMgt@1001 : Codeunit "G/L Account Category Mgt.";
      RowNo@1000 : Integer;
      RetainedEarningsPrimoRowNo@1007 : Code[10];
      GrossRetainedEarningsRowNo@1011 : Code[10];
      DistributionRowNo@1006 : Code[10];
    BEGIN
      GLAccountCategoryMgt.GetGLSetup(GeneralLedgerSetup);
      GeneralLedgerSetup.TESTFIELD("Acc. Sched. for Retained Earn.");
      AccScheduleName.GET(GeneralLedgerSetup."Acc. Sched. for Retained Earn.");
      AccScheduleLine.SETRANGE("Schedule Name",AccScheduleName.Name);
      AccScheduleLine.DELETEALL;
      AccScheduleLine."Schedule Name" := AccScheduleName.Name;

      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
        RetainedEarningsPrimoTxt,
        GetAccFilterForReportingDefinition(GLAccountCategory."Additional Report Definition"::"Retained Earnings"),
        false,false,true,0);
      AccScheduleLine."Row Type" := AccScheduleLine."Row Type"::"Beginning Balance";
      AccScheduleLine.MODIFY;
      RetainedEarningsPrimoRowNo := AccScheduleLine."Row No.";

      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
        NetIncomeTxt,GetIncomeStmtAccFilter,false,true,true,0);

      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::Formula,
        '',
        STRSUBSTNO('%1+%2',RetainedEarningsPrimoRowNo,AccScheduleLine."Row No."),
        false,false,true,0);
      GrossRetainedEarningsRowNo := AccScheduleLine."Row No.";

      AddBlankLine(AccScheduleLine,RowNo);

      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
        DistribToShareholdersTxt,
        GetAccFilterForReportingDefinition(GLAccountCategory."Additional Report Definition"::"Distribution to Shareholders"),
        false,false,false,0);
      DistributionRowNo := AccScheduleLine."Row No.";

      AddBlankLine(AccScheduleLine,RowNo);
      AccScheduleLine.Underline := true;
      AccScheduleLine.MODIFY;

      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::Formula,
        RetainedEarningsUltimoTxt,
        STRSUBSTNO('%1-%2',GrossRetainedEarningsRowNo,DistributionRowNo),
        true,true,true,0);
      AccScheduleLine."Row Type" := AccScheduleLine."Row Type"::"Balance at Date";
      AccScheduleLine.MODIFY;
    END;

    LOCAL PROCEDURE AddAccSchedLineGroup@6(VAR AccScheduleLine@1000 : Record "Acc. Schedule Line";VAR RowNo@1003 : Integer;Category@1001 : Option);
    VAR
      GLAccountCategory@1002 : Record "G/L Account Category";
    BEGIN
      GLAccountCategory.SETRANGE("Account Category",Category);
      GLAccountCategory.SETRANGE(Indentation,0);
      GLAccountCategory.SETAUTOCALCFIELDS("Has Children");
      GLAccountCategory.SETCURRENTKEY("Presentation Order");
      if GLAccountCategory.FINDSET then
        repeat
          AddAccSchedLinesDetail(AccScheduleLine,RowNo,GLAccountCategory,0);
        until GLAccountCategory.NEXT = 0;
    END;

    LOCAL PROCEDURE AddAccSchedLinesDetail@11(VAR AccScheduleLine@1000 : Record "Acc. Schedule Line";VAR RowNo@1004 : Integer;ParentGLAccountCategory@1001 : Record "G/L Account Category";Indentation@1003 : Integer);
    VAR
      GLAccountCategory@1002 : Record "G/L Account Category";
      FromRowNo@1005 : Integer;
      TotalingFilter@1008 : Text;
    BEGIN
      if ParentGLAccountCategory."Has Children" then begin
        AddAccShedLine(
          AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
          ParentGLAccountCategory.Description,ParentGLAccountCategory.GetTotaling,true,false,
          not ParentGLAccountCategory.PositiveNormalBalance,Indentation);
        FromRowNo := RowNo;
        GLAccountCategory.SETRANGE("Parent Entry No.",ParentGLAccountCategory."Entry No.");
        GLAccountCategory.SETCURRENTKEY("Presentation Order");
        GLAccountCategory.SETAUTOCALCFIELDS("Has Children");
        if GLAccountCategory.FINDSET then
          repeat
            AddAccSchedLinesDetail(AccScheduleLine,RowNo,GLAccountCategory,Indentation + 1);
          until GLAccountCategory.NEXT = 0;
        AddAccShedLine(
          AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::Formula,
          STRSUBSTNO(TotalingTxt,ParentGLAccountCategory.Description),
          STRSUBSTNO('%1..%2',FormatRowNo(FromRowNo,false),FormatRowNo(RowNo,false)),true,false,
          not ParentGLAccountCategory.PositiveNormalBalance,Indentation);
      end else begin
        // Retained Earnings element of Equity must include non-closed income statement.
        TotalingFilter := ParentGLAccountCategory.GetTotaling;
        if ParentGLAccountCategory."Additional Report Definition" =
           ParentGLAccountCategory."Additional Report Definition"::"Retained Earnings"
        then begin
          if TotalingFilter <> '' then
            TotalingFilter += '|';
          TotalingFilter += GetIncomeStmtAccFilter;
        end;

        AddAccShedLine(
          AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
          ParentGLAccountCategory.Description,COPYSTR(TotalingFilter,1,250),
          Indentation = 0,false,not ParentGLAccountCategory.PositiveNormalBalance,Indentation);
        AccScheduleLine.Show := AccScheduleLine.Show::"If Any Column Not Zero";
        AccScheduleLine.MODIFY;
      end;
    END;

    LOCAL PROCEDURE AddAccShedLine@10(VAR AccScheduleLine@1000 : Record "Acc. Schedule Line";VAR RowNo@1006 : Integer;TotalingType@1007 : Option;Description@1001 : Text[80];Totaling@1002 : Text[250];Bold@1003 : Boolean;Underline@1004 : Boolean;ShowOppositeSign@1008 : Boolean;Indentation@1005 : Integer);
    BEGIN
      if AccScheduleLine.FINDLAST then;
      AccScheduleLine.INIT;
      AccScheduleLine."Line No." += 10000;
      RowNo += 1;
      AccScheduleLine."Row No." := FormatRowNo(RowNo,TotalingType = AccScheduleLine."Totaling Type"::Formula);
      AccScheduleLine."Totaling Type" := TotalingType;
      AccScheduleLine.Description := Description;
      AccScheduleLine.Totaling := Totaling;
      AccScheduleLine."Show Opposite Sign" := ShowOppositeSign;
      AccScheduleLine.Bold := Bold;
      AccScheduleLine.Underline := Underline;
      AccScheduleLine.Indentation := Indentation;
      AccScheduleLine.INSERT;
    END;

    LOCAL PROCEDURE FormatRowNo@1(RowNo@1000 : Integer;AddPrefix@1001 : Boolean) : Text[5];
    VAR
      Prefix@1002 : Text[1];
    BEGIN
      if AddPrefix then
        Prefix := 'F'
      else
        Prefix := 'P';
      exit(Prefix + COPYSTR(FORMAT(10000 + RowNo),2,4));
    END;

    LOCAL PROCEDURE AddBlankLine@12(VAR AccScheduleLine@1000 : Record "Acc. Schedule Line";VAR RowNo@1001 : Integer);
    BEGIN
      AddAccShedLine(
        AccScheduleLine,RowNo,AccScheduleLine."Totaling Type"::"Posting Accounts",
        '','',false,false,false,0);
    END;

    LOCAL PROCEDURE GetAccFilterForReportingDefinition@4(AdditionalReportingDefinition@1000 : Option) : Text[250];
    VAR
      GLAccountCategory@1002 : Record "G/L Account Category";
      Totaling@1004 : Text;
      AccFilter@1003 : Text;
    BEGIN
      GLAccountCategory.SETRANGE("Additional Report Definition",AdditionalReportingDefinition);
      if GLAccountCategory.FINDSET then
        repeat
          Totaling := GLAccountCategory.GetTotaling;
          if (AccFilter <> '') and (Totaling <> '') then
            AccFilter += '|';
          AccFilter += Totaling;
        until GLAccountCategory.NEXT = 0;
      exit(COPYSTR(AccFilter,1,250));
    END;

    LOCAL PROCEDURE GetIncomeStmtAccFilter@5() : Text[250];
    VAR
      GLAccount@1000 : Record "G/L Account";
      SelectionFilterManagement@1001 : Codeunit SelectionFilterManagement;
    BEGIN
      GLAccount.RESET;
      GLAccount.SETRANGE("Income/Balance",GLAccount."Income/Balance"::"Income Statement");
      exit(COPYSTR(SelectionFilterManagement.GetSelectionFilterForGLAccount(GLAccount),1,250));
    END;

    BEGIN
    END.
  }
}

