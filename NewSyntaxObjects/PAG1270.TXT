OBJECT Page 1270 OCR Service Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=OCR Service Setup;
    InsertAllowed=false;
    DeleteAllowed=false;
    LinksAllowed=false;
    SourceTable="OCR Service Setup";
    PageType=Card;
    PromotedActionCategoriesML=ENU=New,Process,Page,Encryption;
    ShowFilter=false;
    OnOpenPage=BEGIN
                 RESET;
                 if not GET then begin
                   INIT;
                   INSERT(true);
                   SetURLsToDefault;
                 end;
                 UpdateBasedOnEnable;
               END;

    OnAfterGetRecord=BEGIN
                       UpdateBasedOnEnable;
                     END;

    OnQueryClosePage=BEGIN
                       if not Enabled then
                         if not CONFIRM(STRSUBSTNO(EnableServiceQst,CurrPage.CAPTION),true) then
                           exit(false);
                     END;

    OnAfterGetCurrRecord=BEGIN
                           UpdateEncryptedField("Password Key",Password);
                           UpdateEncryptedField("Authorization Key",AuthorizationKey);
                         END;

    ActionList=ACTIONS
    {
      { 12      ;    ;ActionContainer;
                      Name=ActionContainer12;
                      ActionContainerType=ActionItems }
      { 11      ;1   ;Action    ;
                      Name=SetURLsToDefault;
                      CaptionML=ENU=Set URLs to Default;
                      ToolTipML=ENU=Change the service and sign-up URLs to their default values. You cannot cancel this action to revert back to the current values.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Enabled=EditableByNotEnabled;
                      Image=Restore;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SetURLsToDefault;
                               END;
                                }
      { 15      ;1   ;Action    ;
                      Name=TestConnection;
                      CaptionML=ENU=Test Connection;
                      ToolTipML=ENU=Check that the settings that you added are correct and the connection to the Data Exchange Service is working.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=Link;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 OCRServiceMgt@1000 : Codeunit "OCR Service Mgt.";
                               BEGIN
                                 OCRServiceMgt.TestConnection(Rec);
                               END;
                                }
      { 21      ;1   ;Action    ;
                      Name=UpdateOCRDocTemplateList;
                      CaptionML=ENU=Update OCR Doc. Template List;
                      ToolTipML=ENU=Check for new document templates that the OCR service supports, and add them to the list.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=Template;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 OCRServiceMgt@1000 : Codeunit "OCR Service Mgt.";
                               BEGIN
                                 OCRServiceMgt.UpdateOcrDocumentTemplates;
                               END;
                                }
      { 31      ;1   ;Action    ;
                      Name=ResyncMasterData;
                      CaptionML=ENU=Resync Master Data;
                      ToolTipML=ENU=Synchronize master data for vendors and vendor bank accounts with the OCR service.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Enabled=EditableBySyncEnabled;
                      Image=CopyFromChartOfAccounts;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ReadSoftOCRMasterDataSync@1000 : Codeunit "ReadSoft OCR Master Data Sync";
                               BEGIN
                                 CLEAR("Master Data Last Sync");
                                 MODIFY;
                                 ReadSoftOCRMasterDataSync.SyncMasterData(false,false);
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=JobQueueEntry;
                      CaptionML=ENU=Job Queue Entry;
                      ToolTipML=ENU=View or edit the jobs that automatically process the incoming and outgoing electronic documents.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Enabled=Enabled;
                      Image=JobListSetup;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowJobQueueEntry;
                               END;
                                }
      { 10      ;0   ;ActionContainer;
                      Name=ActionContainer10;
                      ActionContainerType=RelatedInformation }
      { 8       ;1   ;Action    ;
                      Name=EncryptionManagement;
                      CaptionML=ENU=Encryption Management;
                      ToolTipML=ENU=Enable or disable data encryption. Data encryption helps make sure that unauthorized users cannot read business data.;
                      ApplicationArea=#Advanced;
                      RunObject=Page "Data Encryption Management";
                      Promoted=true;
                      Enabled=EditableByNotEnabled;
                      PromotedIsBig=true;
                      Image=EncryptionKeys;
                      PromotedCategory=Category4;
                      RunPageMode=View }
      { 26      ;1   ;Action    ;
                      Name=ActivityLog;
                      CaptionML=ENU=Activity Log;
                      ToolTipML=ENU=See the status and any errors for the electronic document or OCR file that you send through the document exchange service.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Log;
                      OnAction=VAR
                                 ActivityLog@1002 : Record "Activity Log";
                               BEGIN
                                 ActivityLog.ShowEntries(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 23  ;2   ;Group     ;
                Name=Group23;
                GroupType=Group }

    { 3   ;3   ;Field     ;
                Name=User Name;
                ToolTipML=ENU=Specifies the user name that represents your company's login to the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User Name";
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             CurrPage.SAVERECORD;
                           END;

                ImplicitType=Text50;
                ShowMandatory=TRUE }

    { 9   ;3   ;Field     ;
                Name=Password;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Password;
                ToolTipML=ENU=Specifies the password that is used for your company's login to the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Password;
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             SavePassword("Password Key",Password);
                             if Password <> '' then
                               CheckEncryption;
                           END;

                ImplicitType=Text50;
                ShowMandatory=TRUE }

    { 7   ;3   ;Field     ;
                Name=AuthorizationKey;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Authorization Key;
                ToolTipML=ENU=Specifies the authorization key that is used for your company's login to the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AuthorizationKey;
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             SavePassword("Authorization Key",AuthorizationKey);
                             if AuthorizationKey <> '' then
                               CheckEncryption;
                           END;

                ImplicitType=Text50;
                ShowMandatory=TRUE }

    { 16  ;3   ;Field     ;
                Name=Default OCR Doc. Template;
                ToolTipML=ENU=Specifies the OCR template that must be used by default for electronic documents that are received from the OCR service. You can change the OCR template on the individual incoming document card before sending the related file to the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Default OCR Doc. Template";
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             CurrPage.UPDATE
                           END;

                ImplicitType=Code20 }

    { 25  ;2   ;Group     ;
                Name=Group25;
                GroupType=Group }

    { 32  ;3   ;Field     ;
                Name=Master Data Sync Enabled;
                ToolTipML=ENU=Specifies whether or not the master data sync has been enabled.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Master Data Sync Enabled";
                Editable=EditableByNotEnabled;
                OnValidate=BEGIN
                             UpdateBasedOnSyncEnable;
                           END;

                ImplicitType=Boolean }

    { 28  ;3   ;Field     ;
                Name=Master Data Last Sync;
                ToolTipML=ENU=Specifies the last time when the master data was synched.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Master Data Last Sync";
                Editable=False;
                ImplicitType=DateTime }

    { 22  ;3   ;Field     ;
                Name=Enabled;
                ToolTipML=ENU=Specifies if the service is enabled.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Enabled;
                OnValidate=BEGIN
                             UpdateBasedOnEnable;
                             CurrPage.UPDATE
                           END;

                ImplicitType=Boolean }

    { 27  ;3   ;Field     ;
                Name=ShowEnableWarning;
                AssistEdit=false;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ShowEnableWarning;
                Enabled=NOT EditableByNotEnabled;
                Editable=False;
                OnDrillDown=BEGIN
                              DrilldownCode;
                            END;

                ImplicitType=Text }

    { 14  ;1   ;Group     ;
                Name=Service;
                CaptionML=ENU=Service;
                GroupType=Group }

    { 4   ;2   ;Field     ;
                Name=Sign-up URL;
                ToolTipML=ENU=Specifies the web page where you sign up for the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sign-up URL";
                Editable=EditableByNotEnabled;
                ImplicitType=Text250 }

    { 5   ;2   ;Field     ;
                Name=Service URL;
                ToolTipML=ENU=Specifies the address of the OCR service. The service specified in the Service URL field is called when you send and receive files for OCR.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Service URL";
                Editable=EditableByNotEnabled;
                ImplicitType=Text250;
                ShowMandatory=TRUE }

    { 6   ;2   ;Field     ;
                Name=Sign-in URL;
                ToolTipML=ENU=Specifies the sign-in page for the OCR service. This is the web page where you enter your company's user name, password, and authorization key to sign in to the service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sign-in URL";
                Editable=EditableByNotEnabled;
                ImplicitType=Text250 }

    { 17  ;1   ;Group     ;
                Name=CustomerStatus;
                CaptionML=ENU=Status;
                GroupType=Group }

    { 18  ;2   ;Field     ;
                Name=Customer Name;
                ToolTipML=ENU=Specifies your company's name at the provider of the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Customer Name";
                Importance=Promoted;
                Editable=False;
                ImplicitType=Text80 }

    { 19  ;2   ;Field     ;
                Name=Customer ID;
                ToolTipML=ENU=Specifies your company's customer ID at the provider of the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Customer ID";
                Editable=False;
                ImplicitType=Text50 }

    { 20  ;2   ;Field     ;
                Name=Customer Status;
                ToolTipML=ENU=Specifies your company's status at the provider of the OCR service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Customer Status";
                Importance=Promoted;
                Editable=False;
                ImplicitType=Text30 }

  }
  CODE
  {
    VAR
      Password@1000 : Text[50];
      AuthorizationKey@1001 : Text[50];
      ShowEnableWarning@1005 : Text;
      EditableByNotEnabled@1002 : Boolean;
      EnabledWarningTok@1003 : TextConst 'ENU=You must disable the service before you can make changes.';
      DisableEnableQst@1004 : TextConst 'ENU=Do you want to disable the OCR service?';
      EnableServiceQst@1006 : TextConst '@@@="%1 = pagecaption (OCR Service Setup)";ENU=The %1 is not enabled. Are you sure you want to exit?';
      EncryptionIsNotActivatedQst@1007 : TextConst 'ENU=Data encryption is not activated. It is recommended that you encrypt data. \Do you want to open the Data Encryption Management window?';
      EditableBySyncEnabled@1009 : Boolean;
      CheckedEncryption@1008 : Boolean;

    LOCAL PROCEDURE UpdateBasedOnEnable@2();
    BEGIN
      EditableByNotEnabled := (not Enabled) and CurrPage.EDITABLE;
      ShowEnableWarning := '';
      if CurrPage.EDITABLE and Enabled then
        ShowEnableWarning := EnabledWarningTok;
      UpdateBasedOnSyncEnable;
    END;

    LOCAL PROCEDURE UpdateBasedOnSyncEnable@8();
    BEGIN
      EditableBySyncEnabled := "Master Data Sync Enabled" and Enabled;
      if EditableBySyncEnabled then
        exit;
      if "Master Data Last Sync" = 0DT then
        exit;
      CLEAR("Master Data Last Sync");
      MODIFY;
    END;

    LOCAL PROCEDURE DrilldownCode@1();
    BEGIN
      if CONFIRM(DisableEnableQst,true) then begin
        Enabled := false;
        UpdateBasedOnEnable;
        CurrPage.UPDATE;
      end;
    END;

    LOCAL PROCEDURE UpdateEncryptedField@3(InputGUID@1000 : GUID;VAR Text@1001 : Text[50]);
    BEGIN
      if ISNULLGUID(InputGUID) then
        Text := ''
      else
        Text := '*************';
    END;

    LOCAL PROCEDURE CheckEncryption@6();
    BEGIN
      if not CheckedEncryption and not ENCRYPTIONENABLED then begin
        CheckedEncryption := true;
        if not ENCRYPTIONENABLED then
          if CONFIRM(EncryptionIsNotActivatedQst) then
            PAGE.RUN(PAGE::"Data Encryption Management");
      end;
    END;

    BEGIN
    END.
  }
}

