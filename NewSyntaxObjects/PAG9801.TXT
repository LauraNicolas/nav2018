OBJECT Page 9801 User Subform
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=User Permission Sets;
    SourceTable="Access Control";
    DelayedInsert=true;
    PageType=ListPart;
    OnAfterGetRecord=BEGIN
                       if User."User Name" <> '' then
                         CurrPage.CAPTION := User."User Name";
                       PermissionEditable := ISNULLGUID("App ID");
                     END;

    OnNewRecord=BEGIN
                  if User.GET("User Security ID") then;
                  PermissionEditable := ISNULLGUID("App ID");
                  CALCFIELDS("App Name","Role Name");
                END;

    OnInsertRecord=BEGIN
                     User.TESTFIELD("User Name");
                     PermissionEditable := ISNULLGUID("App ID");
                     CALCFIELDS("App Name","Role Name");
                   END;

    OnModifyRecord=BEGIN
                     PermissionEditable := ISNULLGUID("App ID");
                     CALCFIELDS("App Name","Role Name");
                   END;

    OnDeleteRecord=VAR
                     UserGroupAccessControl@1000 : Record "User Group Access Control";
                   BEGIN
                     UserGroupAccessControl.SETFILTER("User Group Code",'<>%1','');
                     UserGroupAccessControl.SETRANGE("User Security ID","User Security ID");
                     UserGroupAccessControl.SETRANGE("Role ID","Role ID");
                     UserGroupAccessControl.SETRANGE("Company Name","Company Name");
                     if UserGroupAccessControl.FINDFIRST then
                       ERROR(InUserGroupErr,UserGroupAccessControl."User Group Code");
                     PermissionEditable := ISNULLGUID("App ID");
                   END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      Name=PermissionActions;
                      ActionContainerType=ActionItems }
      { 9       ;1   ;Action    ;
                      Name=Permissions;
                      ShortCutKey=Shift+Ctrl+p;
                      CaptionML=ENU=Permissions;
                      ToolTipML=ENU=View or edit which feature objects that users need to access and set up the related permissions in permission sets that you can assign to the users of the database.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page Permissions;
                      RunPageLink="Role ID"=FIELD("Role ID");
                      Enabled=PermissionEditable;
                      Image=Permission }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                CaptionML=ENU=User Permissions;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=PermissionSet;
                CaptionML=ENU=Permission Set;
                ToolTipML=ENU=Specifies the ID of a security role that has been assigned to this Windows login in the current database.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Role ID";
                OnValidate=VAR
                             AggregatePermissionSet@1000 : Record "Aggregate Permission Set";
                           BEGIN
                             // If the user used the lookup, skip validation
                             if SkipValidation then begin
                               SkipValidation := false;
                               exit;
                             end;

                             // Get the Scope and App ID for a matching Role ID
                             AggregatePermissionSet.SETRANGE("Role ID","Role ID");
                             AggregatePermissionSet.FINDFIRST;

                             if AggregatePermissionSet.COUNT > 1 then
                               ERROR(MultipleRoleIDErr,"Role ID");

                             Scope := AggregatePermissionSet.Scope;
                             "App ID" := AggregatePermissionSet."App ID";

                             CALCFIELDS("App Name","Role Name");

                             SkipValidation := false; // re-enable validation
                           END;

                OnLookup=VAR
                           PermissionSetLookupRecord@1002 : Record "Aggregate Permission Set";
                           PermissionSetLookup@1001 : Page "Permission Set Lookup";
                         BEGIN
                           PermissionSetLookup.LOOKUPMODE := true;
                           if PermissionSetLookup.RUNMODAL = ACTION::LookupOK then begin
                             PermissionSetLookup.GETRECORD(PermissionSetLookupRecord);
                             Scope := PermissionSetLookupRecord.Scope;
                             "App ID" := PermissionSetLookupRecord."App ID";
                             "Role ID" := PermissionSetLookupRecord."Role ID";
                             CALCFIELDS("App Name","Role Name");
                             SkipValidation := true;
                           end;
                         END;

                ImplicitType=Code20 }

    { 4   ;2   ;Field     ;
                Name=Description;
                DrillDown=false;
                CaptionML=ENU=Description;
                ToolTipML=ENU=Specifies the name of the security role that has been given to this Windows login in the current database.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Role Name";
                Editable=FALSE;
                ImplicitType=Text30 }

    { 5   ;2   ;Field     ;
                Name=Company;
                CaptionML=ENU=Company;
                ToolTipML=ENU=Specifies the name of the company that this role is limited to for this Windows login.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Company Name";
                ImplicitType=Text30 }

    { 6   ;2   ;Field     ;
                Name=ExtensionName;
                DrillDown=false;
                CaptionML=ENU=Extension Name;
                ToolTipML=ENU=Specifies the name of the extension.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="App Name";
                Editable=FALSE;
                ImplicitType=Text250 }

  }
  CODE
  {
    VAR
      User@1000 : Record User;
      InUserGroupErr@1001 : TextConst '@@@="%1=a user group code, e.g. ADMIN or SALESDEPT";ENU=You cannot remove this permission set because it is included in user group %1.';
      PermissionEditable@1002 : Boolean;
      MultipleRoleIDErr@1003 : TextConst '@@@=%1 will be replaced with a Role ID code value from the Permission Set table;ENU=The permission set %1 is defined multiple times in this context. Use the lookup button to select the relevant permission set.';
      SkipValidation@1004 : Boolean;

    BEGIN
    END.
  }
}

