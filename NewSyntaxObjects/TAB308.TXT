OBJECT Table 308 No. Series
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnDelete=BEGIN
               NoSeriesLine.SETRANGE("Series Code",Code);
               NoSeriesLine.DELETEALL;

               NoSeriesRelationship.SETRANGE(Code,Code);
               NoSeriesRelationship.DELETEALL;
               NoSeriesRelationship.SETRANGE(Code);

               NoSeriesRelationship.SETRANGE("Series Code",Code);
               NoSeriesRelationship.DELETEALL;
               NoSeriesRelationship.SETRANGE("Series Code");
             END;

    CaptionML=ENU=No. Series;
    LookupPageID="No. Series List";
    DrillDownPageID="No. Series List";
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 2   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 3   ;   ;Default Nos.        ;Boolean       ;OnValidate=BEGIN
                                                                if ("Default Nos." = false) and (xRec."Default Nos." <> "Default Nos.") and ("Manual Nos." = false) then
                                                                  VALIDATE("Manual Nos.",true);
                                                              END;

                                                   CaptionML=ENU=Default Nos. }
    { 4   ;   ;Manual Nos.         ;Boolean       ;OnValidate=BEGIN
                                                                if ("Manual Nos." = false) and (xRec."Manual Nos." <> "Manual Nos.") and ("Default Nos." = false) then
                                                                  VALIDATE("Default Nos.",true);
                                                              END;

                                                   CaptionML=ENU=Manual Nos. }
    { 5   ;   ;Date Order          ;Boolean       ;CaptionML=ENU=Date Order }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoSeriesLine@1000 : Record "No. Series Line";
      NoSeriesRelationship@1001 : Record "No. Series Relationship";

    [External]
    PROCEDURE DrillDown@6();
    VAR
      NoSeriesLine@1000 : Record "No. Series Line";
    BEGIN
      FindNoSeriesLineToShow(NoSeriesLine);
      if NoSeriesLine.FIND('-') then;
      NoSeriesLine.SETRANGE("Starting Date");
      NoSeriesLine.SETRANGE(Open);
      PAGE.RUNMODAL(0,NoSeriesLine);
    END;

    [External]
    PROCEDURE UpdateLine@3(VAR StartDate@1007 : Date;VAR StartNo@1006 : Code[20];VAR EndNo@1005 : Code[20];VAR LastNoUsed@1004 : Code[20];VAR WarningNo@1003 : Code[20];VAR IncrementByNo@1002 : Integer;VAR LastDateUsed@1001 : Date);
    VAR
      NoSeriesLine@1000 : Record "No. Series Line";
    BEGIN
      FindNoSeriesLineToShow(NoSeriesLine);
      if not NoSeriesLine.FIND('-') then
        NoSeriesLine.INIT;
      StartDate := NoSeriesLine."Starting Date";
      StartNo := NoSeriesLine."Starting No.";
      EndNo := NoSeriesLine."Ending No.";
      LastNoUsed := NoSeriesLine."Last No. Used";
      WarningNo := NoSeriesLine."Warning No.";
      IncrementByNo := NoSeriesLine."Increment-by No.";
      LastDateUsed := NoSeriesLine."Last Date Used"
    END;

    LOCAL PROCEDURE FindNoSeriesLineToShow@1(VAR NoSeriesLine@1001 : Record "No. Series Line");
    VAR
      NoSeriesMgt@1000 : Codeunit NoSeriesManagement;
    BEGIN
      NoSeriesMgt.SetNoSeriesLineFilter(NoSeriesLine,Code,0D);

      if NoSeriesLine.FINDLAST then
        exit;

      NoSeriesLine.RESET;
      NoSeriesLine.SETRANGE("Series Code",Code);
    END;

    BEGIN
    END.
  }
}

