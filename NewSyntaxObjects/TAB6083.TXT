OBJECT Table 6083 Serv. Price Adjustment Detail
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ValidateRecord;
             END;

    OnModify=BEGIN
               ValidateRecord;
             END;

    OnRename=BEGIN
               ValidateRecord;
             END;

    CaptionML=ENU=Serv. Price Adjustment Detail;
  }
  FIELDS
  {
    { 1   ;   ;Serv. Price Adjmt. Gr. Code;Code10 ;TableRelation="Service Price Adjustment Group";
                                                   CaptionML=ENU=Serv. Price Adjmt. Gr. Code;
                                                   NotBlank=true }
    { 2   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Item,Resource,Resource Group,Service Cost,G/L Account;
                                                   OptionString=Item,Resource,"Resource Group","Service Cost","G/L Account" }
    { 3   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST("Resource Group")) "Resource Group"
                                                                 ELSE IF (Type=CONST("Service Cost")) "Service Cost"
                                                                 ELSE IF (Type=CONST("G/L Account")) "G/L Account";
                                                   OnValidate=VAR
                                                                Item@1000 : Record Item;
                                                                Resource@1001 : Record Resource;
                                                                ResourceGrp@1002 : Record "Resource Group";
                                                                ServiceCost@1003 : Record "Service Cost";
                                                              BEGIN
                                                                if "No." <> '' then
                                                                  case Type of
                                                                    Type::Item:
                                                                      begin
                                                                        Item.GET("No.");
                                                                        Description := Item.Description;
                                                                      end;
                                                                    Type::Resource:
                                                                      begin
                                                                        Resource.GET("No.");
                                                                        Description := Resource.Name;
                                                                      end;
                                                                    Type::"Resource Group":
                                                                      begin
                                                                        ResourceGrp.GET("No.");
                                                                        Description := ResourceGrp.Name;
                                                                      end;
                                                                    Type::"Service Cost":
                                                                      begin
                                                                        ServiceCost.GET("No.");
                                                                        Description := ServiceCost.Description;
                                                                      end;
                                                                  end;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 4   ;   ;Work Type           ;Code10        ;TableRelation="Work Type";
                                                   OnValidate=VAR
                                                                ServPriceAdjmtDetail@1000 : Record "Serv. Price Adjustment Detail";
                                                                ServPriceAdjmtDetail2@1001 : Record "Serv. Price Adjustment Detail";
                                                              BEGIN
                                                                if not (Type in [Type::Resource,Type::"Resource Group"]) then begin
                                                                  ServPriceAdjmtDetail.Type := Type::Resource;
                                                                  ServPriceAdjmtDetail2.Type := Type::"Resource Group";
                                                                  ERROR(Text003,
                                                                    FIELDCAPTION("Work Type"),
                                                                    FIELDCAPTION(Type),
                                                                    ServPriceAdjmtDetail.Type,
                                                                    ServPriceAdjmtDetail2.Type);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Work Type }
    { 5   ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=ENU=Gen. Prod. Posting Group }
    { 6   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
  }
  KEYS
  {
    {    ;"Serv. Price Adjmt. Gr. Code",Type,"No.","Work Type","Gen. Prod. Posting Group";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=%1 already exists with adjustments for specific %2 numbers. Delete these lines if you need an adjustment for all %2 records.';
      Text002@1000 : TextConst 'ENU=%1 already exists for this %2 with %3 set to blank. Delete this line if you need adjustments for specific %4 numbers.';
      Text003@1002 : TextConst 'ENU=%1 can only be entered when %2 is %3 or %4.';

    LOCAL PROCEDURE ValidateRecord@1();
    VAR
      ServPriceAdjmtDetail@1000 : Record "Serv. Price Adjustment Detail";
    BEGIN
      if "No." = '' then begin
        ServPriceAdjmtDetail.RESET;
        ServPriceAdjmtDetail.SETRANGE("Serv. Price Adjmt. Gr. Code","Serv. Price Adjmt. Gr. Code");
        ServPriceAdjmtDetail.SETRANGE(Type,Type);
        ServPriceAdjmtDetail.SETRANGE("Work Type","Work Type");
        ServPriceAdjmtDetail.SETRANGE("Gen. Prod. Posting Group","Gen. Prod. Posting Group");
        if ServPriceAdjmtDetail.FINDFIRST then begin
          if
             (ServPriceAdjmtDetail."Serv. Price Adjmt. Gr. Code" <> "Serv. Price Adjmt. Gr. Code") or
             (ServPriceAdjmtDetail.Type <> Type) or
             (ServPriceAdjmtDetail."No." <> "No.") or
             (ServPriceAdjmtDetail."Work Type" <> "Work Type") or
             (ServPriceAdjmtDetail."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group")
          then
            ERROR(Text001,ServPriceAdjmtDetail.TABLECAPTION,FORMAT(Type))
        end;
      end else
        if ServPriceAdjmtDetail.GET(
             "Serv. Price Adjmt. Gr. Code",
             Type,
             '',
             "Work Type",
             "Gen. Prod. Posting Group")
        then
          ERROR(Text002,
            ServPriceAdjmtDetail.TABLECAPTION,
            FIELDCAPTION(Type),
            FIELDCAPTION("No."),
            FORMAT(Type));
    END;

    BEGIN
    END.
  }
}

