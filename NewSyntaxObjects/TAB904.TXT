OBJECT Table 904 Assemble-to-Order Link
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Assemble-to-Order Link;
  }
  FIELDS
  {
    { 1   ;   ;Assembly Document Type;Option      ;CaptionML=ENU=Assembly Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   OptionString=Quote,Order,Invoice,"Credit Memo","Blanket Order","Return Order" }
    { 2   ;   ;Assembly Document No.;Code20       ;TableRelation="Assembly Header" WHERE ("Document Type"=FIELD("Assembly Document Type"),
                                                                                          "No."=FIELD("Assembly Document No."));
                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Assembly Document No. }
    { 11  ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,Sale";
                                                   OptionString=" ",Sale }
    { 12  ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   OptionString=Quote,Order,Invoice,"Credit Memo","Blanket Order","Return Order" }
    { 13  ;   ;Document No.        ;Code20        ;TableRelation=IF (Type=CONST(Sale)) "Sales Line"."Document No." WHERE ("Document Type"=FIELD("Document Type"),
                                                                                                                          "Document No."=FIELD("Document No."),
                                                                                                                          "Line No."=FIELD("Document Line No."));
                                                   CaptionML=ENU=Document No. }
    { 14  ;   ;Document Line No.   ;Integer       ;CaptionML=ENU=Document Line No. }
    { 20  ;   ;Assembled Quantity  ;Decimal       ;CaptionML=ENU=Assembled Quantity;
                                                   DecimalPlaces=0:5 }
  }
  KEYS
  {
    {    ;"Assembly Document Type","Assembly Document No.";
                                                   Clustered=true }
    {    ;Type,"Document Type","Document No.","Document Line No." }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AsmHeader@1000 : Record "Assembly Header";
      Text000@1001 : TextConst '@@@="%1 = Table caption of SalesLine or WhseShptLine or InvtPickLine, %2 = Key text of SalesLine or WhseShptLine or InvtPickLine, %3 = Table caption of Assembly header, %4 = Key text of assembly header";ENU=Synchronizing...\  from: %1 with %2\  to: %3 with %4.';
      Text001@1002 : TextConst 'ENU=Do you want to roll up the price from the assembly components?';
      Text002@1003 : TextConst 'ENU=Do you want to roll up the cost from the assembly components?';
      Text003@1004 : TextConst '@@@="%1 = Document Type, %2 = No.";ENU=The item tracking defined on Assembly Header with Document Type %1, No. %2 exceeds %3 on Sales Line with Document Type %4, Document No. %5, Line No. %6.\\ You must adjust the existing item tracking before you can reenter the new quantity.';
      Text004@1005 : TextConst 'ENU=%1 cannot be lower than %2 or higher than %3.\These limits may be defined by constraints calculated from the %4 field on the related %5. Refer to the field help for more information.';
      Text005@1006 : TextConst 'ENU=One or more %1 lines exist for the %2.';
      Text006@1007 : TextConst 'ENU=The status of the linked assembly order will be changed to %1. Do you want to continue?';
      Text007@1008 : TextConst 'ENU=A %1 exists for the %2. \\If you want to record and post a different %3, then you must do this in the %4 field on the related %1.';
      Text008@1009 : TextConst '@@@="Key Value, say: %1=Line No. %2=10000";ENU=%1 %2';

    [External]
    PROCEDURE UpdateAsmFromSalesLine@1(VAR NewSalesLine@1000 : Record "Sales Line");
    BEGIN
      UpdateAsm(NewSalesLine,AsmExistsForSalesLine(NewSalesLine));
    END;

    [External]
    PROCEDURE UpdateAsmFromSalesLineATOExist@79(VAR NewSalesLine@1000 : Record "Sales Line");
    BEGIN
      UpdateAsm(NewSalesLine,true);
    END;

    LOCAL PROCEDURE UpdateAsm@73(VAR NewSalesLine@1001 : Record "Sales Line";AsmExists@1002 : Boolean);
    VAR
      SalesLine2@1000 : Record "Sales Line";
      InvtAdjmtEntryOrder@1003 : Record "Inventory Adjmt. Entry (Order)";
    BEGIN
      if AsmExists then begin
        if not NewSalesLine.IsAsmToOrderAllowed then begin
          DeleteAsmFromSalesLine(NewSalesLine);
          exit;
        end;
        if NewSalesLine."Qty. to Assemble to Order" = 0 then begin
          DeleteAsmFromSalesLine(NewSalesLine);
          InvtAdjmtEntryOrder.SETRANGE("Order Type",InvtAdjmtEntryOrder."Order Type"::Assembly);
          InvtAdjmtEntryOrder.SETRANGE("Order No.","Assembly Document No.");
          if ("Assembly Document Type" <> "Assembly Document Type"::Order) or not InvtAdjmtEntryOrder.ISEMPTY then
            INSERT;
          exit;
        end;
        if not GetAsmHeader then begin
          DELETE;
          InsertAsmHeader(AsmHeader,"Assembly Document Type","Assembly Document No.");
        end else begin
          if not NeedsSynchronization(NewSalesLine) then
            exit;
          AsmReopenIfReleased;
          DELETE;
        end;
      end else begin
        if NewSalesLine."Qty. to Assemble to Order" = 0 then
          exit;
        if not SalesLine2.GET(NewSalesLine."Document Type",NewSalesLine."Document No.",NewSalesLine."Line No.") then
          exit;

        InsertAsmHeader(AsmHeader,NewSalesLine."Document Type",'');

        "Assembly Document Type" := AsmHeader."Document Type";
        "Assembly Document No." := AsmHeader."No.";
        Type := Type::Sale;
        "Document Type" := NewSalesLine."Document Type";
        "Document No." := NewSalesLine."Document No.";
        "Document Line No." := NewSalesLine."Line No.";
      end;

      SynchronizeAsmFromSalesLine(NewSalesLine);
      INSERT;
      AsmHeader."Shortcut Dimension 1 Code" := NewSalesLine."Shortcut Dimension 1 Code";
      AsmHeader."Shortcut Dimension 2 Code" := NewSalesLine."Shortcut Dimension 2 Code";
      AsmHeader.MODIFY(true);
    END;

    [External]
    PROCEDURE UpdateAsmDimFromSalesLine@25(SalesLine@1001 : Record "Sales Line");
    VAR
      Window@1000 : Dialog;
    BEGIN
      if AsmExistsForSalesLine(SalesLine) then
        if GetAsmHeader then begin
          Window.OPEN(GetWindowOpenTextSale(SalesLine));
          if ChangeDim(SalesLine."Dimension Set ID") then begin
            AsmHeader."Shortcut Dimension 1 Code" := SalesLine."Shortcut Dimension 1 Code";
            AsmHeader."Shortcut Dimension 2 Code" := SalesLine."Shortcut Dimension 2 Code";
            AsmHeader.MODIFY(true);
          end;
          Window.CLOSE;
        end;
    END;

    [External]
    PROCEDURE UpdateQtyToAsmFromSalesLine@36(SalesLine@1000 : Record "Sales Line");
    VAR
      Window@1001 : Dialog;
    BEGIN
      if AsmExistsForSalesLine(SalesLine) then
        if GetAsmHeader then begin
          Window.OPEN(GetWindowOpenTextSale(SalesLine));
          UpdateQtyToAsm(MaxQtyToAsm(SalesLine,AsmHeader));
          Window.CLOSE;
        end;
    END;

    [External]
    PROCEDURE UpdateQtyToAsmFromWhseShptLine@51(WhseShptLine@1000 : Record "Warehouse Shipment Line");
    VAR
      Window@1001 : Dialog;
    BEGIN
      if AsmExistsForWhseShptLine(WhseShptLine) then
        if GetAsmHeader then begin
          Window.OPEN(GetWindowOpenTextWhseShpt(WhseShptLine));
          UpdateQtyToAsm(WhseShptLine."Qty. to Ship");
          Window.CLOSE;
        end;
    END;

    [External]
    PROCEDURE UpdateQtyToAsmFromInvtPickLine@57(InvtPickWhseActivityLine@1000 : Record "Warehouse Activity Line");
    VAR
      WhseActivityLine@1003 : Record "Warehouse Activity Line";
      Window@1001 : Dialog;
      TotalQtyToAsm@1004 : Decimal;
    BEGIN
      WhseActivityLine.SETRANGE("Activity Type",InvtPickWhseActivityLine."Activity Type");
      WhseActivityLine.SETRANGE("Source Type",InvtPickWhseActivityLine."Source Type");
      WhseActivityLine.SETRANGE("Source Subtype",InvtPickWhseActivityLine."Source Subtype");
      WhseActivityLine.SETRANGE("Source No.",InvtPickWhseActivityLine."Source No.");
      WhseActivityLine.SETRANGE("Source Line No.",InvtPickWhseActivityLine."Source Line No.");
      WhseActivityLine.SETRANGE("Assemble to Order",true);
      if WhseActivityLine.FINDSET then
        repeat
          TotalQtyToAsm += WhseActivityLine."Qty. to Handle";
        until WhseActivityLine.NEXT = 0;
      if AsmExistsForInvtPickLine(InvtPickWhseActivityLine) then
        if GetAsmHeader then begin
          Window.OPEN(GetWindowOpenTextInvtPick(InvtPickWhseActivityLine));
          UpdateQtyToAsm(TotalQtyToAsm);
          Window.CLOSE;
        end;
    END;

    LOCAL PROCEDURE UpdateQtyToAsm@53(NewQtyToAsm@1000 : Decimal);
    BEGIN
      DELETE;
      if ChangeQtyToAsm(NewQtyToAsm) then
        AsmHeader.MODIFY(true);
      INSERT;
    END;

    [External]
    PROCEDURE UpdateAsmBinCodeFromSalesLine@41(SalesLine@1001 : Record "Sales Line");
    VAR
      Window@1000 : Dialog;
    BEGIN
      if AsmExistsForSalesLine(SalesLine) then
        if GetAsmHeader then begin
          Window.OPEN(GetWindowOpenTextSale(SalesLine));
          UpdateAsmBinCode(SalesLine."Bin Code");
          Window.CLOSE;
        end;
    END;

    [External]
    PROCEDURE UpdateAsmBinCodeFromWhseShptLine@55(WhseShptLine@1000 : Record "Warehouse Shipment Line");
    VAR
      Window@1001 : Dialog;
    BEGIN
      if AsmExistsForWhseShptLine(WhseShptLine) then
        if GetAsmHeader then begin
          Window.OPEN(GetWindowOpenTextWhseShpt(WhseShptLine));
          UpdateAsmBinCode(WhseShptLine."Bin Code");
          Window.CLOSE;
        end;
    END;

    [External]
    PROCEDURE UpdateAsmBinCodeFromInvtPickLine@60(InvtPickWhseActivityLine@1000 : Record "Warehouse Activity Line");
    VAR
      Window@1001 : Dialog;
    BEGIN
      if AsmExistsForInvtPickLine(InvtPickWhseActivityLine) then
        if GetAsmHeader then begin
          Window.OPEN(GetWindowOpenTextInvtPick(InvtPickWhseActivityLine));
          UpdateAsmBinCode(InvtPickWhseActivityLine."Bin Code");
          Window.CLOSE;
        end;
    END;

    LOCAL PROCEDURE UpdateAsmBinCode@56(NewBinCode@1000 : Code[20]);
    BEGIN
      AsmHeader.SuspendStatusCheck(true);
      if ChangeBinCode(NewBinCode) then
        AsmHeader.MODIFY(true);
      AsmHeader.SuspendStatusCheck(false);
    END;

    [External]
    PROCEDURE DeleteAsmFromSalesLine@2(SalesLine@1000 : Record "Sales Line");
    BEGIN
      if AsmExistsForSalesLine(SalesLine) then begin
        DELETE;
        if "Document Type" = "Document Type"::Order then
          UnreserveAsm;

        if GetAsmHeader then begin
          AsmHeader.DELETE(true);
          AsmHeader.INIT;
        end;
      end;
    END;

    [External]
    PROCEDURE InsertAsmHeader@9(VAR AsmHeader@1006 : Record "Assembly Header";NewDocType@1000 : Option;NewDocNo@1001 : Code[20]);
    BEGIN
      AsmHeader.INIT;
      AsmHeader.VALIDATE("Document Type",NewDocType);
      AsmHeader.VALIDATE("No.",NewDocNo);
      AsmHeader.INSERT(true);
    END;

    LOCAL PROCEDURE SynchronizeAsmFromSalesLine@15(VAR NewSalesLine@1000 : Record "Sales Line");
    VAR
      TempTrackingSpecification@1002 : TEMPORARY Record "Tracking Specification";
      SalesHeader@1005 : Record "Sales Header";
      Window@1001 : Dialog;
      QtyTracked@1004 : Decimal;
      QtyTrackedBase@1003 : Decimal;
    BEGIN
      GetAsmHeader;

      Window.OPEN(GetWindowOpenTextSale(NewSalesLine));

      CaptureItemTracking(TempTrackingSpecification,QtyTracked,QtyTrackedBase);

      if NewSalesLine."Qty. to Asm. to Order (Base)" < QtyTrackedBase then
        ERROR(Text003,
          AsmHeader."Document Type",
          AsmHeader."No.",
          NewSalesLine.FIELDCAPTION("Qty. to Assemble to Order"),
          NewSalesLine."Document Type",
          NewSalesLine."Document No.",
          NewSalesLine."Line No.");

      UnreserveAsm;

      SalesHeader.GET(NewSalesLine."Document Type",NewSalesLine."Document No.");
      AsmHeader.SetWarningsOff;
      ChangeItem(NewSalesLine."No.");
      ChangeLocation(NewSalesLine."Location Code");
      ChangeVariant(NewSalesLine."Variant Code");
      ChangeBinCode(NewSalesLine."Bin Code");
      ChangeUOM(NewSalesLine."Unit of Measure Code");
      ChangeDate(NewSalesLine."Shipment Date");
      ChangePostingDate(SalesHeader."Posting Date");
      ChangeDim(NewSalesLine."Dimension Set ID");
      ChangePlanningFlexibility;
      ChangeQty(NewSalesLine."Qty. to Assemble to Order");
      if NewSalesLine."Document Type" <> NewSalesLine."Document Type"::Quote then
        ChangeQtyToAsm(MaxQtyToAsm(NewSalesLine,AsmHeader));

      AsmHeader.MODIFY(true);

      ReserveAsmToSale(NewSalesLine,
        AsmHeader."Remaining Quantity" - QtyTracked,
        AsmHeader."Remaining Quantity (Base)" - QtyTrackedBase);
      RestoreItemTracking(TempTrackingSpecification,NewSalesLine);

      NewSalesLine.CheckAsmToOrder(AsmHeader);
      Window.CLOSE;

      AsmHeader.ShowDueDateBeforeWorkDateMsg;
    END;

    [External]
    PROCEDURE MakeAsmOrderLinkedToSalesOrderLine@28(FromSalesLine@1000 : Record "Sales Line";ToSalesOrderLine@1001 : Record "Sales Line");
    VAR
      ToAsmOrderHeader@1002 : Record "Assembly Header";
    BEGIN
      if AsmExistsForSalesLine(FromSalesLine) then begin
        ToSalesOrderLine.TESTFIELD(Type,ToSalesOrderLine.Type::Item);
        ToSalesOrderLine.TESTFIELD("No.",FromSalesLine."No.");

        if GetAsmHeader then begin
          ToAsmOrderHeader.INIT;
          CopyAsmToNewAsmOrder(AsmHeader,ToAsmOrderHeader,true);

          INIT;
          "Assembly Document Type" := ToAsmOrderHeader."Document Type";
          "Assembly Document No." := ToAsmOrderHeader."No.";
          Type := Type::Sale;
          "Document Type" := ToSalesOrderLine."Document Type";
          "Document No." := ToSalesOrderLine."Document No.";
          "Document Line No." := ToSalesOrderLine."Line No.";

          SynchronizeAsmFromSalesLine(ToSalesOrderLine);
          RecalcAutoReserve(ToAsmOrderHeader);
          INSERT;
        end;
        if FromSalesLine."Document Type" = FromSalesLine."Document Type"::Quote then
          DeleteAsmFromSalesLine(FromSalesLine);
      end;
    END;

    LOCAL PROCEDURE NeedsSynchronization@18(SalesLine@1000 : Record "Sales Line") : Boolean;
    BEGIN
      GetAsmHeader;
      AsmHeader.CALCFIELDS("Reserved Qty. (Base)");
      exit(
        (SalesLine."No." <> AsmHeader."Item No.") or
        (SalesLine."Location Code" <> AsmHeader."Location Code") or
        (SalesLine."Shipment Date" <> AsmHeader."Due Date") or
        (SalesLine."Variant Code" <> AsmHeader."Variant Code") or
        (SalesLine."Bin Code" <> AsmHeader."Bin Code") or
        (SalesLine."Dimension Set ID" <> AsmHeader."Dimension Set ID") or
        (SalesLine."Qty. to Asm. to Order (Base)" <> AsmHeader."Quantity (Base)") or
        (SalesLine."Unit of Measure Code" <> AsmHeader."Unit of Measure Code") or
        (AsmHeader."Planning Flexibility" <> AsmHeader."Planning Flexibility"::None) or
        ((SalesLine."Document Type" = SalesLine."Document Type"::Order) and
         (AsmHeader."Remaining Quantity (Base)" <> AsmHeader."Reserved Qty. (Base)")));
    END;

    LOCAL PROCEDURE ChangeItem@3(NewItemNo@1001 : Code[20]);
    BEGIN
      if AsmHeader."Item No." = NewItemNo then
        exit;

      AsmHeader.VALIDATE("Item No.",NewItemNo);
    END;

    LOCAL PROCEDURE ChangeQty@4(NewQty@1001 : Decimal);
    BEGIN
      if AsmHeader.Quantity = NewQty then
        exit;

      AsmHeader.VALIDATE(Quantity,NewQty);
    END;

    LOCAL PROCEDURE ChangeQtyToAsm@32(NewQtyToAsm@1000 : Decimal) : Boolean;
    BEGIN
      if AsmHeader."Quantity to Assemble" = NewQtyToAsm then
        exit(false);

      AsmHeader.VALIDATE("Quantity to Assemble",NewQtyToAsm);
      exit(true)
    END;

    LOCAL PROCEDURE ChangeLocation@5(NewLocation@1001 : Code[10]);
    BEGIN
      if AsmHeader."Location Code" = NewLocation then
        exit;

      AsmHeader.VALIDATE("Location Code",NewLocation);
    END;

    LOCAL PROCEDURE ChangeVariant@6(NewVariant@1001 : Code[10]);
    BEGIN
      if AsmHeader."Variant Code" = NewVariant then
        exit;

      AsmHeader.VALIDATE("Variant Code",NewVariant);
    END;

    LOCAL PROCEDURE ChangeUOM@7(NewUOMCode@1001 : Code[10]);
    BEGIN
      if AsmHeader."Unit of Measure Code" = NewUOMCode then
        exit;

      AsmHeader.VALIDATE("Unit of Measure Code",NewUOMCode);
    END;

    LOCAL PROCEDURE ChangeDate@8(NewDate@1001 : Date);
    BEGIN
      if AsmHeader."Due Date" = NewDate then
        exit;

      AsmHeader.VALIDATE("Due Date",NewDate);
    END;

    LOCAL PROCEDURE ChangePostingDate@66(NewDate@1000 : Date);
    BEGIN
      if AsmHeader."Posting Date" = NewDate then
        exit;

      AsmHeader.VALIDATE("Posting Date",NewDate);
    END;

    LOCAL PROCEDURE ChangeDim@16(NewDimSetID@1000 : Integer) : Boolean;
    BEGIN
      if AsmHeader."Dimension Set ID" = NewDimSetID then
        exit(false);

      AsmHeader.VALIDATE("Dimension Set ID",NewDimSetID);
      exit(true)
    END;

    LOCAL PROCEDURE ChangeBinCode@40(NewBinCode@1001 : Code[20]) : Boolean;
    BEGIN
      if AsmHeader."Bin Code" = NewBinCode then
        exit(false);

      AsmHeader.ValidateBinCode(NewBinCode);
      exit(true);
    END;

    LOCAL PROCEDURE ChangePlanningFlexibility@46();
    BEGIN
      if AsmHeader."Planning Flexibility" = AsmHeader."Planning Flexibility"::None then
        exit;

      AsmHeader.VALIDATE("Planning Flexibility",AsmHeader."Planning Flexibility"::None);
    END;

    [External]
    PROCEDURE ReserveAsmToSale@10(VAR SalesLine@1003 : Record "Sales Line";QtyToReserve@1001 : Decimal;QtyToReserveBase@1004 : Decimal);
    VAR
      ReservEntry@1000 : Record "Reservation Entry";
      TrackingSpecification@1006 : Record "Tracking Specification";
      AsmHeaderReserve@1002 : Codeunit "Assembly Header-Reserve";
    BEGIN
      if SalesLine."Document Type" <> SalesLine."Document Type"::Order then
        exit;

      if Type = Type::Sale then begin
        GetAsmHeader;

        AsmHeaderReserve.SetBinding(ReservEntry.Binding::"Order-to-Order");
        AsmHeaderReserve.SetDisallowCancellation(true);
        TrackingSpecification.InitTrackingSpecification2(
          DATABASE::"Sales Line",SalesLine."Document Type",SalesLine."Document No.",'',0,SalesLine."Line No.",
          AsmHeader."Variant Code",AsmHeader."Location Code",AsmHeader."Qty. per Unit of Measure");
        AsmHeaderReserve.CreateReservationSetFrom(TrackingSpecification);
        AsmHeaderReserve.CreateReservation2(AsmHeader,AsmHeader.Description,AsmHeader."Due Date",QtyToReserve,QtyToReserveBase);

        if SalesLine.Reserve = SalesLine.Reserve::Never then
          SalesLine.Reserve := SalesLine.Reserve::Optional;
      end;
    END;

    LOCAL PROCEDURE UnreserveAsm@12();
    VAR
      ReservEntry@1002 : Record "Reservation Entry";
      AsmHeaderReserve@1001 : Codeunit "Assembly Header-Reserve";
    BEGIN
      GetAsmHeader;

      AsmHeaderReserve.FilterReservFor(ReservEntry,AsmHeader);
      AsmHeaderReserve.DeleteLine(AsmHeader);
    END;

    LOCAL PROCEDURE CaptureItemTracking@38(VAR TrackingSpecification@1008 : Record "Tracking Specification";VAR QtyTracked@1003 : Decimal;VAR QtyTrackedBase@1002 : Decimal);
    VAR
      ReservEntry@1001 : Record "Reservation Entry";
      Item@1006 : Record Item;
      AsmHeaderReserve@1000 : Codeunit "Assembly Header-Reserve";
    BEGIN
      GetAsmHeader;

      TrackingSpecification.RESET;
      TrackingSpecification.DELETEALL;

      AsmHeaderReserve.FilterReservFor(ReservEntry,AsmHeader);
      if ReservEntry.FIND('-') then begin
        Item.GET(AsmHeader."Item No.");
        repeat
          if ReservEntry.TrackingExists then begin
            TrackingSpecification.TRANSFERFIELDS(ReservEntry);
            TrackingSpecification.INSERT;

            QtyTracked += ReservEntry.Quantity;
            QtyTrackedBase += ReservEntry."Quantity (Base)";

            RemoveTrackingFromReservation(ReservEntry,Item."Item Tracking Code");
          end;
        until ReservEntry.NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE RemoveTrackingFromReservation@39(ReservEntry@1000 : Record "Reservation Entry";ItemTrackingCode@1001 : Code[10]);
    VAR
      ItemTrackingCodeRec@1002 : Record "Item Tracking Code";
      TrackingSpecification@1005 : Record "Tracking Specification";
      ReservEngineMgt@1003 : Codeunit "Reservation Engine Mgt.";
      QtyToAdd@1006 : Decimal;
    BEGIN
      ReservEntry.SetPointerFilter;
      ReservEntry.SetTrackingFilterFromReservEntry(ReservEntry);

      TrackingSpecification.TRANSFERFIELDS(ReservEntry);
      TrackingSpecification.SetTracking('','',0D,0D);

      ItemTrackingCodeRec.GET(ItemTrackingCode);
      ReservEngineMgt.AddItemTrackingToTempRecSet(
        ReservEntry,TrackingSpecification,
        TrackingSpecification."Quantity (Base)",QtyToAdd,
        ItemTrackingCodeRec."SN Specific Tracking",
        ItemTrackingCodeRec."Lot Specific Tracking");
    END;

    LOCAL PROCEDURE RestoreItemTracking@37(VAR TrackingSpecification@1003 : Record "Tracking Specification";SalesLine@1001 : Record "Sales Line");
    VAR
      ReservEntry@1002 : Record "Reservation Entry";
      CreateReservEntry@1000 : Codeunit "Create Reserv. Entry";
    BEGIN
      GetAsmHeader;

      if TrackingSpecification.FIND('-') then
        repeat
          CreateReservEntry.SetDates(TrackingSpecification."Warranty Date",TrackingSpecification."Expiration Date");
          CreateReservEntry.SetApplyFromEntryNo(TrackingSpecification."Appl.-from Item Entry");
          CreateReservEntry.SetDisallowCancellation(true);
          CreateReservEntry.SetBinding(ReservEntry.Binding::"Order-to-Order");
          CreateReservEntry.SetQtyToHandleAndInvoice(
            TrackingSpecification."Qty. to Handle (Base)",TrackingSpecification."Qty. to Invoice (Base)");

          CreateReservEntry.CreateReservEntryFor(
            DATABASE::"Assembly Header",AsmHeader."Document Type",AsmHeader."No.",'',0,0,
            AsmHeader."Qty. per Unit of Measure",
            0,
            TrackingSpecification."Quantity (Base)",
            TrackingSpecification."Serial No.",TrackingSpecification."Lot No.");
          CreateReservEntry.CreateReservEntryFrom(
            DATABASE::"Sales Line",SalesLine."Document Type",SalesLine."Document No.",'',0,SalesLine."Line No.",
            AsmHeader."Qty. per Unit of Measure",
            TrackingSpecification."Serial No.",TrackingSpecification."Lot No.");
          CreateReservEntry.CreateEntry(
            AsmHeader."Item No.",
            AsmHeader."Variant Code",
            AsmHeader."Location Code",
            AsmHeader.Description,
            AsmHeader."Due Date",
            AsmHeader."Due Date",0,0);
        until TrackingSpecification.NEXT = 0;
      TrackingSpecification.DELETEALL;
    END;

    LOCAL PROCEDURE CopyAsmToNewAsmOrder@21(FromAsmHeader@1001 : Record "Assembly Header";VAR ToAsmOrderHeader@1000 : Record "Assembly Header";CopyComments@1006 : Boolean);
    VAR
      FromAsmLine@1002 : Record "Assembly Line";
      ToAsmOrderLine@1003 : Record "Assembly Line";
      FromAsmCommentLine@1004 : Record "Assembly Comment Line";
      ToAsmCommentLine@1005 : Record "Assembly Comment Line";
      AssemblyLineReserve@1007 : Codeunit "Assembly Line-Reserve";
      RecordLinkManagement@1008 : Codeunit "Record Link Management";
    BEGIN
      ToAsmOrderHeader := FromAsmHeader;
      ToAsmOrderHeader."Document Type" := ToAsmOrderHeader."Document Type"::Order;
      ToAsmOrderHeader."No." := '';
      ToAsmOrderHeader.Status := ToAsmOrderHeader.Status::Open;
      ToAsmOrderHeader."Assembled Quantity" := 0;
      ToAsmOrderHeader."Assembled Quantity (Base)" := 0;
      ToAsmOrderHeader.VALIDATE(Quantity,FromAsmHeader."Quantity to Assemble");
      ToAsmOrderHeader.INSERT(true);
      RecordLinkManagement.CopyLinks(FromAsmHeader,ToAsmOrderHeader);

      FromAsmLine.SETRANGE("Document Type",FromAsmHeader."Document Type");
      FromAsmLine.SETRANGE("Document No.",FromAsmHeader."No.");
      if FromAsmLine.FIND('-') then
        repeat
          ToAsmOrderLine := FromAsmLine;
          ToAsmOrderLine."Document Type" := ToAsmOrderLine."Document Type"::Order;
          ToAsmOrderLine."Document No." := ToAsmOrderHeader."No.";
          ToAsmOrderLine."Consumed Quantity" := 0;
          ToAsmOrderLine."Consumed Quantity (Base)" := 0;
          ToAsmOrderLine.VALIDATE(Quantity,FromAsmLine."Quantity to Consume");
          ToAsmOrderLine.INSERT(true);
          AssemblyLineReserve.TransferAsmLineToAsmLine(FromAsmLine,ToAsmOrderLine,ToAsmOrderLine."Quantity (Base)");
          AssemblyLineReserve.SetDeleteItemTracking(true);
          AssemblyLineReserve.DeleteLine(FromAsmLine);
        until FromAsmLine.NEXT = 0;

      if CopyComments then begin
        FromAsmCommentLine.SETRANGE("Document Type",FromAsmHeader."Document Type");
        FromAsmCommentLine.SETRANGE("Document No.",FromAsmHeader."No.");
        if FromAsmCommentLine.FIND('-') then
          repeat
            ToAsmCommentLine := FromAsmCommentLine;
            ToAsmCommentLine."Document Type" := ToAsmCommentLine."Document Type"::"Assembly Order";
            ToAsmCommentLine."Document No." := ToAsmOrderHeader."No.";
            ToAsmCommentLine.INSERT(true);
          until FromAsmCommentLine.NEXT = 0;
      end;
    END;

    [Internal]
    PROCEDURE RollUpPrice@22(SalesHeader@1005 : Record "Sales Header";VAR SalesLine@1000 : Record "Sales Line");
    VAR
      AsmLine@1001 : Record "Assembly Line";
      CompSalesLine@1002 : Record "Sales Line";
      CompItem@1004 : Record Item;
      Res@1008 : Record Resource;
      Currency@1007 : Record Currency;
      SalesPriceCalcMgt@1003 : Codeunit "Sales Price Calc. Mgt.";
      UnitPrice@1006 : Decimal;
    BEGIN
      SalesLine.TESTFIELD(Quantity);
      SalesLine.TESTFIELD("Qty. to Asm. to Order (Base)");
      if not CONFIRM(Text001) then
        exit;
      if not AsmExistsForSalesLine(SalesLine) then
        exit;
      if not GetAsmHeader then
        exit;

      if SalesHeader."Currency Code" = '' then
        Currency.InitRoundingPrecision
      else begin
        SalesHeader.TESTFIELD("Currency Factor");
        Currency.GET(SalesHeader."Currency Code");
        Currency.TESTFIELD("Unit-Amount Rounding Precision");
      end;

      AsmLine.SETRANGE("Document Type",AsmHeader."Document Type");
      AsmLine.SETRANGE("Document No.",AsmHeader."No.");
      if AsmLine.FIND('-') then
        repeat
          if AsmLine.Type in [AsmLine.Type::Item,AsmLine.Type::Resource] then begin
            CompSalesLine := SalesLine;
            CompSalesLine."Line No." := 0;
            CompSalesLine.Quantity := 0;
            CompSalesLine."Quantity (Base)" := 0;

            CompSalesLine."No." := AsmLine."No.";
            CompSalesLine."Variant Code" := AsmLine."Variant Code";
            CompSalesLine."Qty. per Unit of Measure" := AsmLine."Qty. per Unit of Measure";

            case AsmLine.Type of
              AsmLine.Type::Item:
                begin
                  CompItem.GET(CompSalesLine."No.");
                  CompSalesLine.Type := CompSalesLine.Type::Item;
                  CompSalesLine."Gen. Prod. Posting Group" := CompItem."Gen. Prod. Posting Group";
                  CompSalesLine."Tax Group Code" := CompItem."Tax Group Code";
                  CompSalesLine.VALIDATE("VAT Prod. Posting Group",CompItem."VAT Prod. Posting Group");
                end;
              AsmLine.Type::Resource:
                begin
                  Res.GET(CompSalesLine."No.");
                  CompSalesLine.Type := CompSalesLine.Type::Resource;
                  CompSalesLine."Gen. Prod. Posting Group" := Res."Gen. Prod. Posting Group";
                  CompSalesLine."Tax Group Code" := Res."Tax Group Code";
                  CompSalesLine.VALIDATE("VAT Prod. Posting Group",Res."VAT Prod. Posting Group");
                end;
            end;

            CompSalesLine.Quantity := AsmLine.Quantity;
            CompSalesLine."Quantity (Base)" := AsmLine."Quantity (Base)";
            CompSalesLine."Unit of Measure Code" := AsmLine."Unit of Measure Code";
            CompSalesLine."Unit Price" := 0;
            CompSalesLine."Allow Line Disc." := false;

            SalesPriceCalcMgt.FindSalesLinePrice(SalesHeader,CompSalesLine,SalesLine.FIELDNO("No."));

            UnitPrice += CompSalesLine."Unit Price" * AsmLine.Quantity;
          end;
        until AsmLine.NEXT = 0;

      UnitPrice := ROUND(UnitPrice / AsmHeader.Quantity,Currency."Unit-Amount Rounding Precision");
      SalesLine.VALIDATE("Unit Price",UnitPrice);
      SalesLine.MODIFY(true);
    END;

    [External]
    PROCEDURE RollUpCost@23(VAR SalesLine@1000 : Record "Sales Line");
    VAR
      AsmLine@1006 : Record "Assembly Line";
      UnitCost@1002 : Decimal;
    BEGIN
      SalesLine.TESTFIELD(Quantity);
      SalesLine.TESTFIELD("Qty. to Asm. to Order (Base)");
      if not CONFIRM(Text002) then
        exit;
      if not AsmExistsForSalesLine(SalesLine) then
        exit;
      if not GetAsmHeader then
        exit;

      AsmLine.SETRANGE("Document Type",AsmHeader."Document Type");
      AsmLine.SETRANGE("Document No.",AsmHeader."No.");
      if AsmLine.FIND('-') then
        repeat
          UnitCost += AsmLine."Cost Amount";
        until AsmLine.NEXT = 0;

      SalesLine.VALIDATE("Unit Cost (LCY)",ROUND(UnitCost / AsmHeader.Quantity,0.00001));
      SalesLine.MODIFY(true);
    END;

    [External]
    PROCEDURE ShowAsm@11(SalesLine@1000 : Record "Sales Line");
    BEGIN
      SalesLine.TESTFIELD("Qty. to Asm. to Order (Base)");
      if AsmExistsForSalesLine(SalesLine) then begin
        GetAsmHeader;
        case "Document Type" of
          "Document Type"::Quote:
            PAGE.RUNMODAL(PAGE::"Assembly Quote",AsmHeader);
          "Document Type"::"Blanket Order":
            PAGE.RUNMODAL(PAGE::"Blanket Assembly Order",AsmHeader);
          "Document Type"::Order:
            PAGE.RUNMODAL(PAGE::"Assembly Order",AsmHeader);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowAsmToOrderLines@17(SalesLine@1000 : Record "Sales Line");
    VAR
      AsmLine@1001 : Record "Assembly Line";
    BEGIN
      SalesLine.TESTFIELD("Qty. to Asm. to Order (Base)");
      if AsmExistsForSalesLine(SalesLine) then begin
        AsmLine.FILTERGROUP := 2;
        AsmLine.SETRANGE("Document Type","Assembly Document Type");
        AsmLine.SETRANGE("Document No.","Assembly Document No.");
        AsmLine.FILTERGROUP := 0;
        PAGE.RUNMODAL(PAGE::"Assemble-to-Order Lines",AsmLine);
      end;
    END;

    [External]
    PROCEDURE ShowSales@13(AssemblyHeader@1000 : Record "Assembly Header");
    VAR
      SalesHeader@1001 : Record "Sales Header";
    BEGIN
      if GetATOLink(AssemblyHeader) then begin
        SalesHeader.GET("Document Type","Document No.");
        case "Document Type" of
          "Document Type"::Quote:
            PAGE.RUNMODAL(PAGE::"Sales Quote",SalesHeader);
          "Document Type"::"Blanket Order":
            PAGE.RUNMODAL(PAGE::"Blanket Sales Order",SalesHeader);
          "Document Type"::Order:
            PAGE.RUNMODAL(PAGE::"Sales Order",SalesHeader);
        end;
      end;
    END;

    [Internal]
    PROCEDURE SalesLineCheckAvailShowWarning@24(SalesLine@1000 : Record "Sales Line";VAR TempAsmHeader@1004 : TEMPORARY Record "Assembly Header";VAR TempAsmLine@1001 : TEMPORARY Record "Assembly Line") : Boolean;
    VAR
      AsmLine@1003 : Record "Assembly Line";
      SalesSetup@1005 : Record "Sales & Receivables Setup";
    BEGIN
      if SalesLine."Qty. to Assemble to Order" = 0 then
        exit(false);

      SalesSetup.GET;
      if not SalesSetup."Stockout Warning" then
        exit(false);

      TempAsmHeader.INIT;
      TempAsmHeader."Document Type" := SalesLine."Document Type";
      if SalesLine.AsmToOrderExists(AsmHeader) then
        TempAsmHeader := AsmHeader;
      TransAvailSalesLineToAsmHeader(TempAsmHeader,SalesLine);
      TempAsmHeader.INSERT;

      AsmLine.SETRANGE("Document Type",AsmHeader."Document Type");
      AsmLine.SETRANGE("Document No.",AsmHeader."No.");
      if SalesLine.AsmToOrderExists(AsmHeader) and not AsmLine.ISEMPTY then
        exit(TransAvailAsmLinesToAsmLines(AsmHeader,TempAsmHeader,TempAsmLine,false));
      exit(TransAvailBOMCompToAsmLines(TempAsmHeader,TempAsmLine));
    END;

    [Internal]
    PROCEDURE ATOCopyCheckAvailShowWarning@29(FromAsmHeader@1001 : Record "Assembly Header";SalesLine@1005 : Record "Sales Line";VAR TempAsmHeader@1000 : TEMPORARY Record "Assembly Header";VAR TempAsmLine@1003 : TEMPORARY Record "Assembly Line";Recalculate@1004 : Boolean) : Boolean;
    VAR
      SalesSetup@1009 : Record "Sales & Receivables Setup";
    BEGIN
      if SalesLine."Qty. to Assemble to Order" = 0 then
        exit(false);

      SalesSetup.GET;
      if not SalesSetup."Stockout Warning" then
        exit(false);

      TempAsmHeader.INIT;
      TempAsmHeader."Document Type" := SalesLine."Document Type";
      TransAvailSalesLineToAsmHeader(TempAsmHeader,SalesLine);
      TempAsmHeader.INSERT;

      if not Recalculate then
        exit(TransAvailAsmLinesToAsmLines(FromAsmHeader,TempAsmHeader,TempAsmLine,true));
      exit(TransAvailBOMCompToAsmLines(TempAsmHeader,TempAsmLine));
    END;

    [Internal]
    PROCEDURE PstdATOCopyCheckAvailShowWarn@42(FromPostedAsmHeader@1001 : Record "Posted Assembly Header";SalesLine@1005 : Record "Sales Line";VAR TempAsmHeader@1000 : TEMPORARY Record "Assembly Header";VAR TempAsmLine@1003 : TEMPORARY Record "Assembly Line";Recalculate@1004 : Boolean) : Boolean;
    VAR
      SalesSetup@1009 : Record "Sales & Receivables Setup";
    BEGIN
      if SalesLine."Qty. to Assemble to Order" = 0 then
        exit(false);

      SalesSetup.GET;
      if not SalesSetup."Stockout Warning" then
        exit(false);

      TempAsmHeader.INIT;
      TempAsmHeader."Document Type" := SalesLine."Document Type";
      TransAvailSalesLineToAsmHeader(TempAsmHeader,SalesLine);
      TempAsmHeader.INSERT;

      if not Recalculate then
        exit(TransAvailPstdAsmLnsToAsmLns(FromPostedAsmHeader,TempAsmHeader,TempAsmLine));
      exit(TransAvailBOMCompToAsmLines(TempAsmHeader,TempAsmLine));
    END;

    LOCAL PROCEDURE TransAvailSalesLineToAsmHeader@27(VAR NewAsmHeader@1006 : Record "Assembly Header";SalesLine@1010 : Record "Sales Line");
    BEGIN
      NewAsmHeader."Item No." := SalesLine."No.";
      NewAsmHeader."Variant Code" := SalesLine."Variant Code";
      NewAsmHeader."Location Code" := SalesLine."Location Code";
      NewAsmHeader."Bin Code" := SalesLine."Bin Code";
      NewAsmHeader."Due Date" := SalesLine."Shipment Date";
      NewAsmHeader.ValidateDates(NewAsmHeader.FIELDNO("Due Date"),true);
      NewAsmHeader."Unit of Measure Code" := SalesLine."Unit of Measure Code";
      NewAsmHeader."Qty. per Unit of Measure" := SalesLine."Qty. per Unit of Measure";
      NewAsmHeader.Quantity := SalesLine."Qty. to Assemble to Order" - AsmHeader."Assembled Quantity";
      NewAsmHeader."Quantity (Base)" := SalesLine."Qty. to Asm. to Order (Base)" - AsmHeader."Assembled Quantity (Base)";

      NewAsmHeader.InitRemainingQty;
    END;

    LOCAL PROCEDURE TransAvailAsmLinesToAsmLines@33(FromAsmHeader@1002 : Record "Assembly Header";VAR ToAsmHeader@1001 : Record "Assembly Header";VAR ToAsmLine@1000 : Record "Assembly Line";InitQtyConsumed@1006 : Boolean) : Boolean;
    VAR
      FromAsmLine@1003 : Record "Assembly Line";
      ShowAsmWarning@1005 : Boolean;
    BEGIN
      FromAsmLine.SETRANGE("Document Type",FromAsmHeader."Document Type");
      FromAsmLine.SETRANGE("Document No.",FromAsmHeader."No.");
      FromAsmLine.SETRANGE(Type,FromAsmLine.Type::Item);
      if FromAsmLine.FIND('-') then
        repeat
          ToAsmLine := FromAsmLine;
          if InitQtyConsumed then begin
            ToAsmLine."Consumed Quantity" := 0;
            ToAsmLine."Consumed Quantity (Base)" := 0;
          end;
          TransAvailAsmHeaderToAsmLine(ToAsmLine,ToAsmHeader);
          UpdateAsmLineQty(ToAsmLine,ToAsmHeader."Qty. per Unit of Measure" * ToAsmHeader."Remaining Quantity");

          if ToAsmLine.UpdateAvailWarning then
            ShowAsmWarning := true;
          ToAsmLine.INSERT;
        until FromAsmLine.NEXT = 0;

      exit(ShowAsmWarning);
    END;

    LOCAL PROCEDURE TransAvailBOMCompToAsmLines@34(VAR ToAsmHeader@1001 : Record "Assembly Header";VAR ToAsmLine@1000 : Record "Assembly Line") : Boolean;
    VAR
      BOMComponent@1004 : Record "BOM Component";
      ShowAsmWarning@1002 : Boolean;
    BEGIN
      BOMComponent.SETRANGE("Parent Item No.",ToAsmHeader."Item No.");
      BOMComponent.SETRANGE(Type,BOMComponent.Type::Item);
      if BOMComponent.FIND('-') then
        repeat
          ToAsmLine.INIT;
          ToAsmLine."Line No." += 10000;
          TransAvailAsmHeaderToAsmLine(ToAsmLine,ToAsmHeader);
          TransAvailBOMCompToAsmLine(ToAsmLine,BOMComponent);
          UpdateAsmLineQty(ToAsmLine,ToAsmHeader."Qty. per Unit of Measure" * ToAsmHeader."Remaining Quantity");

          if ToAsmLine.UpdateAvailWarning then
            ShowAsmWarning := true;
          ToAsmLine.INSERT;
        until BOMComponent.NEXT = 0;

      exit(ShowAsmWarning);
    END;

    LOCAL PROCEDURE TransAvailPstdAsmLnsToAsmLns@35(FromPostedAsmHeader@1003 : Record "Posted Assembly Header";VAR ToAsmHeader@1002 : Record "Assembly Header";VAR ToAsmLine@1001 : Record "Assembly Line") : Boolean;
    VAR
      FromPostedAsmLine@1004 : Record "Posted Assembly Line";
      ShowAsmWarning@1000 : Boolean;
    BEGIN
      FromPostedAsmLine.SETRANGE("Document No.",FromPostedAsmHeader."No.");
      FromPostedAsmLine.SETRANGE(Type,FromPostedAsmLine.Type::Item);
      if FromPostedAsmLine.FIND('-') then
        repeat
          ToAsmLine.TRANSFERFIELDS(FromPostedAsmLine);
          TransAvailAsmHeaderToAsmLine(ToAsmLine,ToAsmHeader);
          UpdateAsmLineQty(ToAsmLine,ToAsmHeader."Qty. per Unit of Measure" * ToAsmHeader."Remaining Quantity");

          if ToAsmLine.UpdateAvailWarning then
            ShowAsmWarning := true;
          ToAsmLine.INSERT;
        until FromPostedAsmLine.NEXT = 0;

      exit(ShowAsmWarning);
    END;

    LOCAL PROCEDURE TransAvailAsmHeaderToAsmLine@26(VAR AsmLine@1000 : Record "Assembly Line";VAR NewAsmHeader@1001 : Record "Assembly Header");
    BEGIN
      AsmLine."Document Type" := NewAsmHeader."Document Type";
      AsmLine."Document No." := NewAsmHeader."No.";

      if NewAsmHeader."Location Code" <> AsmHeader."Location Code" then begin
        AsmLine."Location Code" := NewAsmHeader."Location Code";
        if AsmLine.Type = AsmLine.Type::Item then
          AsmLine."Bin Code" := AsmLine.FindBin;
      end;
      if NewAsmHeader."Due Date" <> AsmHeader."Due Date" then
        AsmLine."Due Date" := NewAsmHeader."Starting Date";
    END;

    LOCAL PROCEDURE TransAvailBOMCompToAsmLine@30(VAR AsmLine@1002 : Record "Assembly Line";BOMComponent@1003 : Record "BOM Component");
    VAR
      ItemUOM@1000 : Record "Item Unit of Measure";
    BEGIN
      AsmLine.Type := AsmLine.Type::Item;
      AsmLine."No." := BOMComponent."No.";
      AsmLine."Variant Code" := BOMComponent."Variant Code";
      AsmLine."Quantity per" := BOMComponent."Quantity per";
      AsmLine."Unit of Measure Code" := BOMComponent."Unit of Measure Code";
      if not ItemUOM.GET(BOMComponent."No.",BOMComponent."Unit of Measure Code") then
        ItemUOM.INIT;
      AsmLine."Qty. per Unit of Measure" := ItemUOM."Qty. per Unit of Measure";
    END;

    LOCAL PROCEDURE UpdateAsmLineQty@31(VAR AsmLine@1000 : Record "Assembly Line";QtyFactor@1001 : Decimal);
    BEGIN
      AsmLine.Quantity := AsmLine."Quantity per" * QtyFactor;
      AsmHeader.RoundQty(AsmLine.Quantity);
      AsmLine."Quantity (Base)" := ROUND(AsmLine.Quantity * AsmLine."Qty. per Unit of Measure",0.00001);
      AsmLine.InitRemainingQty;
    END;

    [External]
    PROCEDURE AsmExistsForSalesLine@14(SalesLine@1000 : Record "Sales Line") : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY(Type,"Document Type","Document No.","Document Line No.");
      SETRANGE(Type,Type::Sale);
      SETRANGE("Document Type",SalesLine."Document Type");
      SETRANGE("Document No.",SalesLine."Document No.");
      SETRANGE("Document Line No.",SalesLine."Line No.");
      exit(FINDFIRST);
    END;

    [External]
    PROCEDURE AsmExistsForWhseShptLine@58(WhseShptLine@1000 : Record "Warehouse Shipment Line") : Boolean;
    VAR
      SalesLine@1001 : Record "Sales Line";
    BEGIN
      WhseShptLine.TESTFIELD("Assemble to Order",true);
      WhseShptLine.TESTFIELD("Source Type",DATABASE::"Sales Line");
      SalesLine.GET(WhseShptLine."Source Subtype",WhseShptLine."Source No.",WhseShptLine."Source Line No.");
      exit(AsmExistsForSalesLine(SalesLine));
    END;

    LOCAL PROCEDURE AsmExistsForInvtPickLine@70(InvtPickWhseActivityLine@1000 : Record "Warehouse Activity Line") : Boolean;
    VAR
      SalesLine@1001 : Record "Sales Line";
    BEGIN
      InvtPickWhseActivityLine.TESTFIELD("Assemble to Order",true);
      InvtPickWhseActivityLine.TESTFIELD("Source Type",DATABASE::"Sales Line");
      InvtPickWhseActivityLine.TESTFIELD("Activity Type",InvtPickWhseActivityLine."Activity Type"::"Invt. Pick");
      SalesLine.GET(
        InvtPickWhseActivityLine."Source Subtype",InvtPickWhseActivityLine."Source No.",InvtPickWhseActivityLine."Source Line No.");
      exit(AsmExistsForSalesLine(SalesLine));
    END;

    LOCAL PROCEDURE GetAsmHeader@19() : Boolean;
    BEGIN
      if (AsmHeader."Document Type" = "Assembly Document Type") and
         (AsmHeader."No." = "Assembly Document No.")
      then
        exit(true);
      exit(AsmHeader.GET("Assembly Document Type","Assembly Document No."));
    END;

    LOCAL PROCEDURE GetATOLink@43(AssemblyHeader@1000 : Record "Assembly Header") LinkFound : Boolean;
    BEGIN
      LinkFound := GET(AssemblyHeader."Document Type",AssemblyHeader."No.");
      if LinkFound then
        TESTFIELD(Type,Type::Sale);
    END;

    LOCAL PROCEDURE GetMin@52(a@1000 : Decimal;b@1001 : Decimal) : Decimal;
    BEGIN
      if a < b then
        exit(a);
      exit(b);
    END;

    LOCAL PROCEDURE GetMax@49(a@1001 : Decimal;b@1000 : Decimal) : Decimal;
    BEGIN
      if a > b then
        exit(a);
      exit(b);
    END;

    LOCAL PROCEDURE MaxQtyToAsm@44(SalesLine@1000 : Record "Sales Line";AssemblyHeader@1001 : Record "Assembly Header") : Decimal;
    BEGIN
      exit(GetMin(SalesLine."Qty. to Ship",AssemblyHeader."Remaining Quantity"));
    END;

    LOCAL PROCEDURE MaxQtyToAsmBase@50(SalesLine@1000 : Record "Sales Line";AssemblyHeader@1001 : Record "Assembly Header") : Decimal;
    BEGIN
      exit(GetMin(SalesLine."Qty. to Ship (Base)",AssemblyHeader."Remaining Quantity (Base)"));
    END;

    LOCAL PROCEDURE MinQtyToAsm@45(SalesLine@1002 : Record "Sales Line";AssemblyHeader@1001 : Record "Assembly Header") : Decimal;
    VAR
      UnshippedNonATOQty@1003 : Decimal;
    BEGIN
      UnshippedNonATOQty := SalesLine."Outstanding Quantity" - AssemblyHeader."Remaining Quantity";
      exit(GetMax(SalesLine."Qty. to Ship" - UnshippedNonATOQty,0));
    END;

    [External]
    PROCEDURE CheckQtyToAsm@47(AssemblyHeader@1000 : Record "Assembly Header");
    VAR
      SalesLine@1001 : Record "Sales Line";
      Location@1007 : Record Location;
      WhseActivityLine@1009 : Record "Warehouse Activity Line";
      WhseShptLine@1004 : Record "Warehouse Shipment Line";
      WMSMgt@1006 : Codeunit "WMS Management";
      MaxQty@1002 : Decimal;
      MinQty@1003 : Decimal;
    BEGIN
      if GetATOLink(AssemblyHeader) then begin
        SalesLine.GET("Document Type","Document No.","Document Line No.");

        if Location.GET(AssemblyHeader."Location Code") then
          if Location."Require Shipment" then begin
            AssemblyHeader.CALCFIELDS("Assemble to Order");
            if WMSMgt.ATOWhseShptExists(SalesLine) then
              ERROR(
                Text007,WhseShptLine.TABLECAPTION,AssemblyHeader."Document Type",AssemblyHeader.FIELDCAPTION("Quantity to Assemble"),
                WhseShptLine.FIELDCAPTION("Qty. to Ship"));
          end else begin
            if Location."Require Pick" then
              if WMSMgt.ATOInvtPickExists(SalesLine) then
                ERROR(Text005,WhseActivityLine."Activity Type"::"Invt. Pick",AssemblyHeader."Document Type");
          end;

        MinQty := MinQtyToAsm(SalesLine,AssemblyHeader);
        MaxQty := MaxQtyToAsm(SalesLine,AssemblyHeader);
        if (AssemblyHeader."Quantity to Assemble" < MinQty) or (AssemblyHeader."Quantity to Assemble" > MaxQty) then
          ERROR(
            Text004,AssemblyHeader.FIELDCAPTION("Quantity to Assemble"),MinQty,MaxQty,SalesLine.FIELDCAPTION("Qty. to Ship"),
            SalesLine.TABLECAPTION);
      end;
    END;

    [External]
    PROCEDURE InitQtyToAsm@48(AssemblyHeader@1000 : Record "Assembly Header";VAR QtyToAsm@1002 : Decimal;VAR QtyToAsmBase@1003 : Decimal);
    VAR
      SalesLine@1001 : Record "Sales Line";
    BEGIN
      if GetATOLink(AssemblyHeader) then begin
        SalesLine.GET("Document Type","Document No.","Document Line No.");
        QtyToAsm := MaxQtyToAsm(SalesLine,AssemblyHeader);
        QtyToAsmBase := MaxQtyToAsmBase(SalesLine,AssemblyHeader);
      end;
    END;

    LOCAL PROCEDURE AsmReopenIfReleased@54();
    VAR
      ItemCheckAvail@1000 : Codeunit "Item-Check Avail.";
      ReleaseAssemblyDoc@1001 : Codeunit "Release Assembly Document";
    BEGIN
      if AsmHeader.Status <> AsmHeader.Status::Released then
        exit;
      if not CONFIRM(Text006,false,AsmHeader.Status::Open) then
        ItemCheckAvail.RaiseUpdateInterruptedError;
      ReleaseAssemblyDoc.Reopen(AsmHeader);
    END;

    LOCAL PROCEDURE GetWindowOpenTextSale@59(SalesLine@1000 : Record "Sales Line") : Text;
    BEGIN
      exit(STRSUBSTNO(Text000,
          SalesLine.TABLECAPTION,
          ConstructKeyTextSalesLine(SalesLine),
          AsmHeader.TABLECAPTION,
          ConstructKeyTextAsmHeader));
    END;

    LOCAL PROCEDURE GetWindowOpenTextWhseShpt@62(WhseShptLine@1000 : Record "Warehouse Shipment Line") : Text;
    BEGIN
      exit(STRSUBSTNO(Text000,
          WhseShptLine.TABLECAPTION,
          ConstructKeyTextWhseShptLine(WhseShptLine),
          AsmHeader.TABLECAPTION,
          ConstructKeyTextAsmHeader));
    END;

    LOCAL PROCEDURE GetWindowOpenTextInvtPick@68(InvtPickWhseActivityLine@1000 : Record "Warehouse Activity Line") : Text;
    BEGIN
      exit(STRSUBSTNO(Text000,
          InvtPickWhseActivityLine.TABLECAPTION,
          ConstructKeyTextInvtPickLine(InvtPickWhseActivityLine),
          AsmHeader.TABLECAPTION,
          ConstructKeyTextAsmHeader));
    END;

    LOCAL PROCEDURE ConstructKeyTextAsmHeader@63() : Text;
    VAR
      DocTypeText@1000 : Text;
      DocNoText@1001 : Text;
    BEGIN
      DocTypeText := STRSUBSTNO(Text008,AsmHeader.FIELDCAPTION("Document Type"),AsmHeader."Document Type");
      DocNoText := STRSUBSTNO(Text008,AsmHeader.FIELDCAPTION("No."),AsmHeader."No.");
      exit(STRSUBSTNO(Text008,DocTypeText,DocNoText));
    END;

    LOCAL PROCEDURE ConstructKeyTextSalesLine@64(SalesLine@1000 : Record "Sales Line") : Text;
    VAR
      DocTypeText@1002 : Text;
      DocNoText@1001 : Text;
      LineNoText@1003 : Text;
    BEGIN
      DocTypeText := STRSUBSTNO(Text008,SalesLine.FIELDCAPTION("Document Type"),SalesLine."Document Type");
      DocNoText := STRSUBSTNO(Text008,SalesLine.FIELDCAPTION("Document No."),SalesLine."Document No.");
      LineNoText := STRSUBSTNO(Text008,SalesLine.FIELDCAPTION("Line No."),SalesLine."Line No.");
      exit(STRSUBSTNO(Text008,STRSUBSTNO(Text008,DocTypeText,DocNoText),LineNoText));
    END;

    LOCAL PROCEDURE ConstructKeyTextWhseShptLine@65(WhseShptLine@1000 : Record "Warehouse Shipment Line") : Text;
    VAR
      NoText@1001 : Text;
      LineNoText@1002 : Text;
    BEGIN
      NoText := STRSUBSTNO(Text008,WhseShptLine.FIELDCAPTION("No."),WhseShptLine."No.");
      LineNoText := STRSUBSTNO(Text008,WhseShptLine.FIELDCAPTION("Line No."),WhseShptLine."Line No.");
      exit(STRSUBSTNO(Text008,NoText,LineNoText));
    END;

    LOCAL PROCEDURE ConstructKeyTextInvtPickLine@69(InvtPickWhseActivityLine@1000 : Record "Warehouse Activity Line") : Text;
    VAR
      ActTypeText@1001 : Text;
      NoText@1002 : Text;
      LineNoText@1003 : Text;
    BEGIN
      ActTypeText :=
        STRSUBSTNO(Text008,InvtPickWhseActivityLine.FIELDCAPTION("Activity Type"),InvtPickWhseActivityLine."Activity Type");
      NoText := STRSUBSTNO(Text008,InvtPickWhseActivityLine.FIELDCAPTION("No."),InvtPickWhseActivityLine."No.");
      LineNoText := STRSUBSTNO(Text008,InvtPickWhseActivityLine.FIELDCAPTION("Line No."),InvtPickWhseActivityLine."Line No.");
      exit(STRSUBSTNO(Text008,STRSUBSTNO(Text008,ActTypeText,NoText),LineNoText));
    END;

    [External]
    PROCEDURE ShowAsmOrders@61(SalesHeader@1000 : Record "Sales Header");
    VAR
      AssembleToOrderLink@1002 : Record "Assemble-to-Order Link";
      AssemblyHeader@1003 : Record "Assembly Header";
      TempAssemblyHeader@1001 : TEMPORARY Record "Assembly Header";
    BEGIN
      TempAssemblyHeader.DELETEALL;
      with AssembleToOrderLink do begin
        SETCURRENTKEY(Type,"Document Type","Document No.","Document Line No.");
        SETRANGE(Type,Type::Sale);
        SETRANGE("Document Type",SalesHeader."Document Type");
        SETRANGE("Document No.",SalesHeader."No.");
        if FINDSET then
          repeat
            if not TempAssemblyHeader.GET("Assembly Document Type","Assembly Document No.") then
              if AssemblyHeader.GET("Assembly Document Type","Assembly Document No.") then begin
                TempAssemblyHeader := AssemblyHeader;
                TempAssemblyHeader.INSERT;
              end;
          until NEXT = 0;
      end;
      PAGE.RUN(PAGE::"Assembly Orders",TempAssemblyHeader);
    END;

    LOCAL PROCEDURE RecalcAutoReserve@67(AsmHeader@1000 : Record "Assembly Header");
    VAR
      AssemblyLine@1001 : Record "Assembly Line";
    BEGIN
      AssemblyLine.SETRANGE("Document Type",AsmHeader."Document Type");
      AssemblyLine.SETRANGE("Document No.",AsmHeader."No.");
      if AssemblyLine.FINDSET then
        repeat
          AsmHeader.AutoReserveAsmLine(AssemblyLine);
        until AssemblyLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

