OBJECT Codeunit 1541 Workflow Webhook Events
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      WorkflowWebhookResponseReceivedEventTxt@1001 : TextConst 'ENU=A response is received from a subscribed webhook.';

    [External]
    PROCEDURE WorkflowWebhookResponseReceivedEventCode@1() : Code[128];
    BEGIN
      exit(UPPERCASE('WorkflowWebhookResponseReceivedEvent'));
    END;

    [EventSubscriber(Codeunit,1520,OnAddWorkflowEventPredecessorsToLibrary)]
    LOCAL PROCEDURE AddWorkflowWebhookEventHierarchiesToLibrary@6(EventFunctionName@1000 : Code[128]);
    VAR
      WorkflowEventHandling@1001 : Codeunit "Workflow Event Handling";
    BEGIN
      case EventFunctionName of
        WorkflowWebhookResponseReceivedEventCode:
          begin
            WorkflowEventHandling.AddEventPredecessor(WorkflowWebhookResponseReceivedEventCode,
              WorkflowEventHandling.RunWorkflowOnSendCustomerForApprovalCode);
            WorkflowEventHandling.AddEventPredecessor(WorkflowWebhookResponseReceivedEventCode,
              WorkflowEventHandling.RunWorkflowOnSendGeneralJournalBatchForApprovalCode);
            WorkflowEventHandling.AddEventPredecessor(WorkflowWebhookResponseReceivedEventCode,
              WorkflowEventHandling.RunWorkflowOnSendGeneralJournalLineForApprovalCode);
            WorkflowEventHandling.AddEventPredecessor(WorkflowWebhookResponseReceivedEventCode,
              WorkflowEventHandling.RunWorkflowOnSendItemForApprovalCode);
            WorkflowEventHandling.AddEventPredecessor(WorkflowWebhookResponseReceivedEventCode,
              WorkflowEventHandling.RunWorkflowOnSendPurchaseDocForApprovalCode);
            WorkflowEventHandling.AddEventPredecessor(WorkflowWebhookResponseReceivedEventCode,
              WorkflowEventHandling.RunWorkflowOnSendSalesDocForApprovalCode);
            WorkflowEventHandling.AddEventPredecessor(WorkflowWebhookResponseReceivedEventCode,
              WorkflowEventHandling.RunWorkflowOnSendVendorForApprovalCode);
          end;
      end;
    END;

    [EventSubscriber(Codeunit,1520,OnAddWorkflowEventsToLibrary)]
    LOCAL PROCEDURE AddWorkflowWebhookEventsToLibrary@2();
    VAR
      WorkflowEventHandling@1000 : Codeunit "Workflow Event Handling";
    BEGIN
      WorkflowEventHandling.AddEventToLibrary(WorkflowWebhookResponseReceivedEventCode,DATABASE::"Workflow Webhook Entry",
        WorkflowWebhookResponseReceivedEventTxt,0,false);
    END;

    [EventSubscriber(Codeunit,1520,OnAddWorkflowTableRelationsToLibrary)]
    LOCAL PROCEDURE AddWorkflowWebhookTableRelationsToLibrary@7();
    VAR
      DummyCustomer@1006 : Record Customer;
      DummyItem@1007 : Record Item;
      DummyGenJournalBatch@1005 : Record "Gen. Journal Batch";
      DummyGenJournalLine@1004 : Record "Gen. Journal Line";
      DummyPurchaseHeader@1003 : Record "Purchase Header";
      DummySalesHeader@1001 : Record "Sales Header";
      DummyVendor@1008 : Record Vendor;
      DummyWorkflowWebhookEntry@1002 : Record "Workflow Webhook Entry";
      WorkflowSetup@1000 : Codeunit "Workflow Setup";
    BEGIN
      WorkflowSetup.InsertTableRelation(DATABASE::Customer,DummyCustomer.FIELDNO(Id),
        DATABASE::"Workflow Webhook Entry",DummyWorkflowWebhookEntry.FIELDNO("Data ID"));
      WorkflowSetup.InsertTableRelation(DATABASE::"Gen. Journal Batch",DummyGenJournalBatch.FIELDNO(Id),
        DATABASE::"Workflow Webhook Entry",DummyWorkflowWebhookEntry.FIELDNO("Data ID"));
      WorkflowSetup.InsertTableRelation(DATABASE::"Gen. Journal Line",DummyGenJournalLine.FIELDNO(Id),
        DATABASE::"Workflow Webhook Entry",DummyWorkflowWebhookEntry.FIELDNO("Data ID"));
      WorkflowSetup.InsertTableRelation(DATABASE::Item,DummyItem.FIELDNO(Id),
        DATABASE::"Workflow Webhook Entry",DummyWorkflowWebhookEntry.FIELDNO("Data ID"));
      WorkflowSetup.InsertTableRelation(DATABASE::"Purchase Header",DummyPurchaseHeader.FIELDNO(Id),
        DATABASE::"Workflow Webhook Entry",DummyWorkflowWebhookEntry.FIELDNO("Data ID"));
      WorkflowSetup.InsertTableRelation(DATABASE::"Sales Header",DummySalesHeader.FIELDNO(Id),
        DATABASE::"Workflow Webhook Entry",DummyWorkflowWebhookEntry.FIELDNO("Data ID"));
      WorkflowSetup.InsertTableRelation(DATABASE::Vendor,DummyVendor.FIELDNO(Id),
        DATABASE::"Workflow Webhook Entry",DummyWorkflowWebhookEntry.FIELDNO("Data ID"));
    END;

    [EventSubscriber(Codeunit,1543,OnCancelWorkflow)]
    LOCAL PROCEDURE HandleOnCancelWorkflow@3(WorkflowWebhookEntry@1000 : Record "Workflow Webhook Entry");
    VAR
      WorkflowManagement@1001 : Codeunit "Workflow Management";
    BEGIN
      WorkflowManagement.HandleEvent(WorkflowWebhookResponseReceivedEventCode,WorkflowWebhookEntry);
    END;

    [EventSubscriber(Codeunit,1543,OnContinueWorkflow)]
    LOCAL PROCEDURE HandleOnContinueWorkflow@4(WorkflowWebhookEntry@1000 : Record "Workflow Webhook Entry");
    VAR
      WorkflowManagement@1001 : Codeunit "Workflow Management";
    BEGIN
      WorkflowManagement.HandleEvent(WorkflowWebhookResponseReceivedEventCode,WorkflowWebhookEntry);
    END;

    [EventSubscriber(Codeunit,1543,OnRejectWorkflow)]
    LOCAL PROCEDURE HandleOnRejectWorkflow@5(WorkflowWebhookEntry@1000 : Record "Workflow Webhook Entry");
    VAR
      WorkflowManagement@1001 : Codeunit "Workflow Management";
    BEGIN
      WorkflowManagement.HandleEvent(WorkflowWebhookResponseReceivedEventCode,WorkflowWebhookEntry);
    END;

    [EventSubscriber(Codeunit,1535,OnCancelCustomerApprovalRequest)]
    LOCAL PROCEDURE HandleOnCancelCustomerApprovalRequest@12(VAR Customer@1000 : Record Customer);
    VAR
      WorkflowWebhookManagement@1001 : Codeunit "Workflow Webhook Management";
    BEGIN
      // Handles the scenario when a Customer is deleted after it's been sent approval
      WorkflowWebhookManagement.FindAndCancel(Customer.RECORDID);
    END;

    [EventSubscriber(Codeunit,1535,OnCancelItemApprovalRequest)]
    LOCAL PROCEDURE HandleOnCancelItemApprovalRequest@10(VAR Item@1000 : Record Item);
    VAR
      WorkflowWebhookManagement@1001 : Codeunit "Workflow Webhook Management";
    BEGIN
      // Handles the scenario when an Item is deleted after it's been sent approval
      WorkflowWebhookManagement.FindAndCancel(Item.RECORDID);
    END;

    [EventSubscriber(Codeunit,1535,OnRenameRecordInApprovalRequest)]
    LOCAL PROCEDURE HandleOnRenameRecordInApprovalRequest@8(OldRecordId@1000 : RecordID;NewRecordId@1001 : RecordID);
    VAR
      WorkflowWebhookManagement@1002 : Codeunit "Workflow Webhook Management";
    BEGIN
      WorkflowWebhookManagement.RenameRecord(OldRecordId,NewRecordId);
    END;

    [EventSubscriber(Codeunit,1535,OnCancelVendorApprovalRequest)]
    LOCAL PROCEDURE HandleOnCancelVendorApprovalRequest@13(VAR Vendor@1000 : Record Vendor);
    VAR
      WorkflowWebhookManagement@1001 : Codeunit "Workflow Webhook Management";
    BEGIN
      // Handles the scenario when a Vendor is deleted after it's been sent approval
      WorkflowWebhookManagement.FindAndCancel(Vendor.RECORDID);
    END;

    BEGIN
    END.
  }
}

