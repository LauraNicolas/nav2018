OBJECT Table 5088 Profile Questionnaire Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields="Profile Questionnaire Code",Description;
    OnDelete=VAR
               Rating@1000 : Record Rating;
               ProfileQuestionnaireLine@1001 : Record "Profile Questionnaire Line";
             BEGIN
               CALCFIELDS("No. of Contacts");
               TESTFIELD("No. of Contacts",0);

               Rating.SETRANGE("Rating Profile Quest. Code","Profile Questionnaire Code");
               Rating.SETRANGE("Rating Profile Quest. Line No.","Line No.");
               if not Rating.ISEMPTY then
                 ERROR(Text002);

               Rating.RESET;
               Rating.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
               Rating.SETRANGE("Profile Questionnaire Line No.","Line No.");
               if not Rating.ISEMPTY then
                 ERROR(Text003);

               if Type = Type::Question then begin
                 ProfileQuestionnaireLine.GET("Profile Questionnaire Code","Line No.");
                 if (ProfileQuestionnaireLine.NEXT <> 0) and
                    (ProfileQuestionnaireLine.Type = ProfileQuestnLine.Type::Answer)
                 then
                   ERROR(Text004);
               end;
             END;

    CaptionML=ENU=Profile Questionnaire Line;
    LookupPageID="Profile Questn. Line List";
  }
  FIELDS
  {
    { 1   ;   ;Profile Questionnaire Code;Code20  ;TableRelation="Profile Questionnaire Header";
                                                   CaptionML=ENU=Profile Questionnaire Code }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 3   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                case Type of
                                                                  Type::Question:
                                                                    begin
                                                                      CALCFIELDS("No. of Contacts");
                                                                      TESTFIELD("No. of Contacts",0);
                                                                      TESTFIELD("From Value",0);
                                                                      TESTFIELD("To Value",0);
                                                                    end;
                                                                  Type::Answer:
                                                                    begin
                                                                      TESTFIELD("Multiple Answers",false);
                                                                      TESTFIELD("Auto Contact Classification",false);
                                                                      TESTFIELD("Customer Class. Field",0);
                                                                      TESTFIELD("Vendor Class. Field",0);
                                                                      TESTFIELD("Contact Class. Field",0);
                                                                      TESTFIELD("Starting Date Formula",ZeroDateFormula);
                                                                      TESTFIELD("Ending Date Formula",ZeroDateFormula);
                                                                      TESTFIELD("Classification Method",0);
                                                                      TESTFIELD("Sorting Method",0);
                                                                      TESTFIELD("No. of Decimals",0);
                                                                    end;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Question,Answer;
                                                   OptionString=Question,Answer }
    { 4   ;   ;Description         ;Text50        ;CaptionML=ENU=Description;
                                                   NotBlank=true }
    { 5   ;   ;Multiple Answers    ;Boolean       ;OnValidate=BEGIN
                                                                if "Multiple Answers" then
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=ENU=Multiple Answers }
    { 6   ;   ;Auto Contact Classification;Boolean;OnValidate=BEGIN
                                                                if "Auto Contact Classification" then
                                                                  TESTFIELD(Type,Type::Question)
                                                                else begin
                                                                  TESTFIELD("Customer Class. Field","Customer Class. Field"::" ");
                                                                  TESTFIELD("Vendor Class. Field","Vendor Class. Field"::" ");
                                                                  TESTFIELD("Contact Class. Field","Contact Class. Field"::" ");
                                                                  TESTFIELD("Starting Date Formula",ZeroDateFormula);
                                                                  TESTFIELD("Ending Date Formula",ZeroDateFormula);
                                                                  TESTFIELD("Classification Method","Classification Method"::" ");
                                                                  TESTFIELD("Sorting Method","Sorting Method"::" ");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Auto Contact Classification }
    { 7   ;   ;Customer Class. Field;Option       ;OnValidate=BEGIN
                                                                if "Customer Class. Field" <> "Customer Class. Field"::" " then begin
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Vendor Class. Field");
                                                                  CLEAR("Contact Class. Field");
                                                                  if "Classification Method" = "Classification Method"::" " then
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                end else
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=ENU=Customer Class. Field;
                                                   OptionCaptionML=ENU=" ,Sales (LCY),Profit (LCY),Sales Frequency (Invoices/Year),Avg. Invoice Amount (LCY),Discount (%),Avg. Overdue (Day)";
                                                   OptionString=" ","Sales (LCY)","Profit (LCY)","Sales Frequency (Invoices/Year)","Avg. Invoice Amount (LCY)","Discount (%)","Avg. Overdue (Day)" }
    { 8   ;   ;Vendor Class. Field ;Option        ;OnValidate=BEGIN
                                                                if "Vendor Class. Field" <> "Vendor Class. Field"::" " then begin
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Customer Class. Field");
                                                                  CLEAR("Contact Class. Field");
                                                                  if "Classification Method" = "Classification Method"::" " then
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                end else
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=ENU=Vendor Class. Field;
                                                   OptionCaptionML=ENU=" ,Purchase (LCY),Purchase Frequency (Invoices/Year),Avg. Ticket Size (LCY),Discount (%),Avg. Overdue (Day)";
                                                   OptionString=" ","Purchase (LCY)","Purchase Frequency (Invoices/Year)","Avg. Ticket Size (LCY)","Discount (%)","Avg. Overdue (Day)" }
    { 9   ;   ;Contact Class. Field;Option        ;OnValidate=VAR
                                                                Rating@1000 : Record Rating;
                                                              BEGIN
                                                                if xRec."Contact Class. Field" = "Contact Class. Field"::Rating then begin
                                                                  Rating.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                                                                  Rating.SETRANGE("Profile Questionnaire Line No.","Line No.");
                                                                  if Rating.FINDFIRST then
                                                                    if CONFIRM(Text000,false) then
                                                                      Rating.DELETEALL
                                                                    else
                                                                      ERROR(Text001,FIELDCAPTION("Contact Class. Field"));
                                                                end;

                                                                if "Contact Class. Field" <> "Contact Class. Field"::" " then begin
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Customer Class. Field");
                                                                  CLEAR("Vendor Class. Field");
                                                                  if ("Classification Method" = "Classification Method"::" ") or
                                                                     ("Contact Class. Field" = "Contact Class. Field"::Rating)
                                                                  then begin
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                    "Sorting Method" := "Sorting Method"::" ";
                                                                  end;
                                                                  if "Contact Class. Field" = "Contact Class. Field"::Rating then begin
                                                                    CLEAR("Starting Date Formula");
                                                                    CLEAR("Ending Date Formula");
                                                                  end;
                                                                end else
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=ENU=Contact Class. Field;
                                                   OptionCaptionML=ENU=" ,Interaction Quantity,Interaction Frequency (No./Year),Avg. Interaction Cost (LCY),Avg. Interaction Duration (Min.),Opportunity Won (%),Rating";
                                                   OptionString=" ","Interaction Quantity","Interaction Frequency (No./Year)","Avg. Interaction Cost (LCY)","Avg. Interaction Duration (Min.)","Opportunity Won (%)",Rating }
    { 10  ;   ;Starting Date Formula;DateFormula  ;OnValidate=BEGIN
                                                                if FORMAT("Starting Date Formula") <> '' then
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=ENU=Starting Date Formula }
    { 11  ;   ;Ending Date Formula ;DateFormula   ;OnValidate=BEGIN
                                                                if FORMAT("Ending Date Formula") <> '' then
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=ENU=Ending Date Formula }
    { 12  ;   ;Classification Method;Option       ;OnValidate=BEGIN
                                                                if "Classification Method" <> "Classification Method"::" " then begin
                                                                  TESTFIELD(Type,Type::Question);
                                                                  if "Classification Method" <> "Classification Method"::"Defined Value" then
                                                                    "Sorting Method" := ProfileQuestnLine."Sorting Method"::Descending
                                                                  else
                                                                    "Sorting Method" := ProfileQuestnLine."Sorting Method"::" ";
                                                                end else
                                                                  "Sorting Method" := ProfileQuestnLine."Sorting Method"::" ";
                                                              END;

                                                   CaptionML=ENU=Classification Method;
                                                   OptionCaptionML=ENU=" ,Defined Value,Percentage of Value,Percentage of Contacts";
                                                   OptionString=" ","Defined Value","Percentage of Value","Percentage of Contacts" }
    { 13  ;   ;Sorting Method      ;Option        ;OnValidate=BEGIN
                                                                if "Sorting Method" <> "Sorting Method"::" " then
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=ENU=Sorting Method;
                                                   OptionCaptionML=ENU=" ,Descending,Ascending";
                                                   OptionString=" ",Descending,Ascending }
    { 14  ;   ;From Value          ;Decimal       ;OnValidate=BEGIN
                                                                if "From Value" <> 0 then
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=ENU=From Value;
                                                   DecimalPlaces=0:25;
                                                   BlankZero=true }
    { 15  ;   ;To Value            ;Decimal       ;OnValidate=BEGIN
                                                                if "To Value" <> 0 then
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=ENU=To Value;
                                                   DecimalPlaces=0:25;
                                                   BlankZero=true }
    { 16  ;   ;No. of Contacts     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Contact Profile Answer" WHERE ("Profile Questionnaire Code"=FIELD("Profile Questionnaire Code"),
                                                                                                     "Line No."=FIELD("Line No.")));
                                                   CaptionML=ENU=No. of Contacts;
                                                   BlankZero=true;
                                                   Editable=false }
    { 17  ;   ;Priority            ;Option        ;InitValue=Normal;
                                                   OnValidate=VAR
                                                                ContProfileAnswer@1000 : Record "Contact Profile Answer";
                                                              BEGIN
                                                                TESTFIELD(Type,Type::Answer);
                                                                ContProfileAnswer.SETCURRENTKEY("Profile Questionnaire Code","Line No.");
                                                                ContProfileAnswer.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                                                                ContProfileAnswer.SETRANGE("Line No.","Line No.");
                                                                ContProfileAnswer.MODIFYALL("Answer Priority",Priority);
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Priority;
                                                   OptionCaptionML=ENU=Very Low (Hidden),Low,Normal,High,Very High;
                                                   OptionString="Very Low (Hidden)",Low,Normal,High,"Very High" }
    { 18  ;   ;No. of Decimals     ;Integer       ;OnValidate=BEGIN
                                                                if "No. of Decimals" <> 0 then
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=ENU=No. of Decimals;
                                                   MinValue=-25;
                                                   MaxValue=25 }
    { 19  ;   ;Min. % Questions Answered;Decimal  ;OnValidate=BEGIN
                                                                if "Min. % Questions Answered" <> 0 then begin
                                                                  TESTFIELD(Type,Type::Question);
                                                                  TESTFIELD("Contact Class. Field","Contact Class. Field"::Rating);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Min. % Questions Answered;
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 9501;   ;Wizard Step         ;Option        ;CaptionML=ENU=Wizard Step;
                                                   OptionCaptionML=ENU=" ,1,2,3,4,5,6";
                                                   OptionString=" ","1","2","3","4","5","6";
                                                   Editable=false }
    { 9502;   ;Interval Option     ;Option        ;CaptionML=ENU=Interval Option;
                                                   OptionCaptionML=ENU=Minimum,Maximum,Interval;
                                                   OptionString=Minimum,Maximum,Interval }
    { 9503;   ;Answer Option       ;Option        ;CaptionML=ENU=Answer Option;
                                                   OptionCaptionML=ENU=HighLow,ABC,Custom;
                                                   OptionString=HighLow,ABC,Custom }
    { 9504;   ;Answer Description  ;Text50        ;CaptionML=ENU=Answer Description }
    { 9505;   ;Wizard From Value   ;Decimal       ;OnValidate=BEGIN
                                                                if "From Value" <> 0 then
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=ENU=Wizard From Value;
                                                   DecimalPlaces=0:25;
                                                   BlankZero=true }
    { 9506;   ;Wizard To Value     ;Decimal       ;OnValidate=BEGIN
                                                                if "To Value" <> 0 then
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=ENU=Wizard To Value;
                                                   DecimalPlaces=0:25;
                                                   BlankZero=true }
    { 9707;   ;Wizard From Line No.;Integer       ;OnValidate=BEGIN
                                                                if "To Value" <> 0 then
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=ENU=Wizard From Line No.;
                                                   BlankZero=true }
  }
  KEYS
  {
    {    ;"Profile Questionnaire Code","Line No." ;Clustered=true }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Type,Description,Priority,"Multiple Answers","Auto Contact Classification","No. of Contacts" }
  }
  CODE
  {
    VAR
      ProfileQuestnLine@1000 : Record "Profile Questionnaire Line";
      TempProfileQuestionnaireLine@1007 : TEMPORARY Record "Profile Questionnaire Line";
      ZeroDateFormula@1001 : DateFormula;
      Text000@1002 : TextConst 'ENU=Do you want to delete the rating values?';
      Text001@1003 : TextConst 'ENU=%1 cannot be changed until the rating value is deleted.';
      Text002@1004 : TextConst 'ENU=You cannot delete this line because one or more questions are depending on it.';
      Text003@1005 : TextConst 'ENU=You cannot delete this line because one or more rating values exists.';
      Text004@1006 : TextConst 'ENU=You cannot delete this question while answers exists.';
      Text005@1011 : TextConst 'ENU=Please select for which questionnaire this rating should be created.';
      Text006@1010 : TextConst 'ENU=Please describe the rating.';
      Text007@1009 : TextConst 'ENU=Please create one or more different answers.';
      Text008@1008 : TextConst 'ENU=Please enter which range of points this answer should require.';
      Text009@1012 : TextConst 'ENU=High';
      Text010@1013 : TextConst 'ENU=Low';
      Text011@1014 : TextConst '@@@=Selecting answer A;ENU=A';
      Text012@1015 : TextConst '@@@=Selecting answer B;ENU=B';
      Text013@1016 : TextConst '@@@=Selecting answer C;ENU=C';

    [External]
    PROCEDURE MoveUp@1();
    VAR
      UpperProfileQuestnLine@1000 : Record "Profile Questionnaire Line";
      LineNo@1001 : Integer;
      UpperRecLineNo@1002 : Integer;
    BEGIN
      TESTFIELD(Type,Type::Answer);
      UpperProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      LineNo := "Line No.";
      UpperProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");

      if UpperProfileQuestnLine.FIND('<') and
         (UpperProfileQuestnLine.Type = UpperProfileQuestnLine.Type::Answer)
      then begin
        UpperRecLineNo := UpperProfileQuestnLine."Line No.";
        RENAME("Profile Questionnaire Code",-1);
        UpperProfileQuestnLine.RENAME("Profile Questionnaire Code",LineNo);
        RENAME("Profile Questionnaire Code",UpperRecLineNo);
      end;
    END;

    [External]
    PROCEDURE MoveDown@2();
    VAR
      LowerProfileQuestnLine@1000 : Record "Profile Questionnaire Line";
      LineNo@1001 : Integer;
      LowerRecLineNo@1002 : Integer;
    BEGIN
      TESTFIELD(Type,Type::Answer);
      LowerProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      LineNo := "Line No.";
      LowerProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");

      if LowerProfileQuestnLine.FIND('>') and
         (LowerProfileQuestnLine.Type = LowerProfileQuestnLine.Type::Answer)
      then begin
        LowerRecLineNo := LowerProfileQuestnLine."Line No.";
        RENAME("Profile Questionnaire Code",-1);
        LowerProfileQuestnLine.RENAME("Profile Questionnaire Code",LineNo);
        RENAME("Profile Questionnaire Code",LowerRecLineNo);
      end;
    END;

    [External]
    PROCEDURE Question@4() : Text[50];
    BEGIN
      ProfileQuestnLine.RESET;
      ProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      ProfileQuestnLine.SETFILTER("Line No.",'<%1',"Line No.");
      ProfileQuestnLine.SETRANGE(Type,Type::Question);
      if ProfileQuestnLine.FINDLAST then
        exit(ProfileQuestnLine.Description);
    END;

    [External]
    PROCEDURE FindQuestionLine@3() : Integer;
    BEGIN
      ProfileQuestnLine.RESET;
      ProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      ProfileQuestnLine.SETFILTER("Line No.",'<%1',"Line No.");
      ProfileQuestnLine.SETRANGE(Type,Type::Question);
      if ProfileQuestnLine.FINDLAST then
        exit(ProfileQuestnLine."Line No.");
    END;

    LOCAL PROCEDURE ResetFields@5();
    BEGIN
      CLEAR("Starting Date Formula");
      CLEAR("Ending Date Formula");
      "Classification Method" := "Classification Method"::" ";
      "Sorting Method" := "Sorting Method"::" ";
      "No. of Decimals" := 0;
      "Min. % Questions Answered" := 0;
    END;

    [External]
    PROCEDURE CreateRatingFromProfQuestnLine@6(VAR ProfileQuestnLine@1001 : Record "Profile Questionnaire Line");
    BEGIN
      INIT;
      "Profile Questionnaire Code" := ProfileQuestnLine."Profile Questionnaire Code";
      StartWizard;
    END;

    LOCAL PROCEDURE StartWizard@16();
    BEGIN
      "Wizard Step" := "Wizard Step"::"1";
      VALIDATE("Auto Contact Classification",true);
      VALIDATE("Contact Class. Field","Contact Class. Field"::Rating);
      INSERT;

      ValidateAnswerOption;
      ValidateIntervalOption;

      PAGE.RUNMODAL(PAGE::"Create Rating",Rec);
    END;

    [External]
    PROCEDURE CheckStatus@9();
    BEGIN
      case "Wizard Step" of
        "Wizard Step"::"1":
          begin
            if "Profile Questionnaire Code" = '' then
              ERROR(Text005);
            if Description = '' then
              ERROR(Text006);
          end;
        "Wizard Step"::"2":
          begin
            if TempProfileQuestionnaireLine.COUNT = 0 then
              ERROR(Text007);
          end;
        "Wizard Step"::"3":
          if ("Wizard From Value" = 0) and ("Wizard To Value" = 0) then
            ERROR(Text008);
      end;
    END;

    [External]
    PROCEDURE PerformNextWizardStatus@8();
    BEGIN
      case "Wizard Step" of
        "Wizard Step"::"1":
          "Wizard Step" := "Wizard Step" + 1;
        "Wizard Step"::"2":
          begin
            "Wizard From Line No." := 0;
            "Wizard Step" := "Wizard Step" + 1;
            TempProfileQuestionnaireLine.SETRANGE("Line No.");
            TempProfileQuestionnaireLine.FIND('-');
            SetIntervalOption;
          end;
        "Wizard Step"::"3":
          begin
            TempProfileQuestionnaireLine.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
            TempProfileQuestionnaireLine.FIND('-');
            TempProfileQuestionnaireLine."From Value" := "Wizard From Value";
            TempProfileQuestionnaireLine."To Value" := "Wizard To Value";
            TempProfileQuestionnaireLine.MODIFY;
            if TempProfileQuestionnaireLine.NEXT <> 0 then begin
              TempProfileQuestionnaireLine.SETRANGE("Line No.",TempProfileQuestionnaireLine."Line No.");
              "Wizard From Line No." := TempProfileQuestionnaireLine."Line No.";
              "Wizard From Value" := TempProfileQuestionnaireLine."From Value";
              "Wizard To Value" := TempProfileQuestionnaireLine."To Value";
              SetIntervalOption;
            end else begin
              TempProfileQuestionnaireLine.SETRANGE("Line No.");
              TempProfileQuestionnaireLine.FIND('-');
              "Wizard Step" := "Wizard Step" + 1;
            end;
          end;
      end;
    END;

    [External]
    PROCEDURE PerformPrevWizardStatus@17();
    BEGIN
      case "Wizard Step" of
        "Wizard Step"::"3":
          begin
            TempProfileQuestionnaireLine.SETFILTER("Line No.",'..%1',"Wizard From Line No.");
            if TempProfileQuestionnaireLine.FIND('+') then begin
              TempProfileQuestionnaireLine."From Value" := "Wizard From Value";
              TempProfileQuestionnaireLine."To Value" := "Wizard To Value";
              TempProfileQuestionnaireLine.MODIFY;
            end;
            if TempProfileQuestionnaireLine.NEXT(-1) <> 0 then begin
              "Wizard From Line No." := TempProfileQuestionnaireLine."Line No.";
              "Wizard From Value" := TempProfileQuestionnaireLine."From Value";
              "Wizard To Value" := TempProfileQuestionnaireLine."To Value";
              SetIntervalOption
            end else begin
              TempProfileQuestionnaireLine.SETRANGE("Line No.");
              TempProfileQuestionnaireLine.FIND('-');
              "Wizard Step" := "Wizard Step" - 1;
            end;
          end;
        else
          "Wizard Step" := "Wizard Step" - 1;
      end;
    END;

    [External]
    PROCEDURE FinishWizard@7();
    VAR
      ProfileQuestionnaireLine@1000 : Record "Profile Questionnaire Line";
      ProfileMgt@1004 : Codeunit ProfileManagement;
      NextLineNo@1002 : Integer;
      QuestionLineNo@1001 : Integer;
    BEGIN
      ProfileQuestionnaireLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      if ProfileQuestionnaireLine.FINDLAST then
        QuestionLineNo := ProfileQuestionnaireLine."Line No." + 10000
      else
        QuestionLineNo := 10000;

      ProfileQuestionnaireLine := Rec;
      ProfileQuestionnaireLine."Line No." := QuestionLineNo;
      ProfileQuestionnaireLine.INSERT(true);

      NextLineNo := QuestionLineNo;
      TempProfileQuestionnaireLine.RESET;
      if TempProfileQuestionnaireLine.FINDSET then
        repeat
          NextLineNo := NextLineNo + 10000;
          ProfileQuestionnaireLine := TempProfileQuestionnaireLine;
          ProfileQuestionnaireLine."Profile Questionnaire Code" := "Profile Questionnaire Code";
          ProfileQuestionnaireLine."Line No." := NextLineNo;
          ProfileQuestionnaireLine.INSERT(true);
        until TempProfileQuestionnaireLine.NEXT = 0;

      COMMIT;

      ProfileQuestionnaireLine.GET("Profile Questionnaire Code",QuestionLineNo);
      ProfileMgt.ShowAnswerPoints(ProfileQuestionnaireLine);
    END;

    [External]
    PROCEDURE SetIntervalOption@10();
    BEGIN
      case true of
        (TempProfileQuestionnaireLine."From Value" = 0) and (TempProfileQuestionnaireLine."To Value" <> 0):
          "Interval Option" := "Interval Option"::Maximum;
        (TempProfileQuestionnaireLine."From Value" <> 0) and (TempProfileQuestionnaireLine."To Value" = 0):
          "Interval Option" := "Interval Option"::Minimum
        else
          "Interval Option" := "Interval Option"::Interval
      end;

      ValidateIntervalOption;
    END;

    [External]
    PROCEDURE ValidateIntervalOption@14();
    BEGIN
      TempProfileQuestionnaireLine.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
      TempProfileQuestionnaireLine.FIND('-');
      if "Interval Option" = "Interval Option"::Minimum then
        TempProfileQuestionnaireLine."To Value" := 0;
      if "Interval Option" = "Interval Option"::Maximum then
        TempProfileQuestionnaireLine."From Value" := 0;
      TempProfileQuestionnaireLine.MODIFY;
    END;

    [External]
    PROCEDURE ValidateAnswerOption@13();
    BEGIN
      if "Answer Option" = "Answer Option"::Custom then
        exit;

      TempProfileQuestionnaireLine.DELETEALL;

      case "Answer Option" of
        "Answer Option"::HighLow:
          begin
            CreateAnswer(Text009);
            CreateAnswer(Text010);
          end;
        "Answer Option"::ABC:
          begin
            CreateAnswer(Text011);
            CreateAnswer(Text012);
            CreateAnswer(Text013);
          end;
      end;
    END;

    LOCAL PROCEDURE CreateAnswer@15(AnswerDescription@1000 : Text[50]);
    BEGIN
      TempProfileQuestionnaireLine.INIT;
      TempProfileQuestionnaireLine."Line No." := (TempProfileQuestionnaireLine.COUNT + 1) * 10000;
      TempProfileQuestionnaireLine.Type := TempProfileQuestionnaireLine.Type::Answer;
      TempProfileQuestionnaireLine.Description := AnswerDescription;
      TempProfileQuestionnaireLine.INSERT;
    END;

    [External]
    PROCEDURE NoOfProfileAnswers@11() : Decimal;
    BEGIN
      exit(TempProfileQuestionnaireLine.COUNT);
    END;

    [External]
    PROCEDURE ShowAnswers@12();
    VAR
      TempProfileQuestionnaireLine2@1000 : TEMPORARY Record "Profile Questionnaire Line";
    BEGIN
      if "Answer Option" <> "Answer Option"::Custom then
        if TempProfileQuestionnaireLine.FIND('-') then
          repeat
            TempProfileQuestionnaireLine2 := TempProfileQuestionnaireLine;
            TempProfileQuestionnaireLine2.INSERT;
          until TempProfileQuestionnaireLine.NEXT = 0;

      PAGE.RUNMODAL(PAGE::"Rating Answers",TempProfileQuestionnaireLine);

      if "Answer Option" <> "Answer Option"::Custom then
        if TempProfileQuestionnaireLine.COUNT <> TempProfileQuestionnaireLine2.COUNT then
          "Answer Option" := "Answer Option"::Custom
        else begin
          if TempProfileQuestionnaireLine.FIND('-') then
            repeat
              if not TempProfileQuestionnaireLine2.GET(
                   TempProfileQuestionnaireLine."Profile Questionnaire Code",TempProfileQuestionnaireLine."Line No.")
              then
                "Answer Option" := "Answer Option"::Custom
              else
                if TempProfileQuestionnaireLine.Description <> TempProfileQuestionnaireLine2.Description then
                  "Answer Option" := "Answer Option"::Custom
            until (TempProfileQuestionnaireLine.NEXT = 0) or ("Answer Option" = "Answer Option"::Custom);
        end;
    END;

    [External]
    PROCEDURE GetProfileLineAnswerDesc@18() : Text[100];
    BEGIN
      TempProfileQuestionnaireLine.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
      TempProfileQuestionnaireLine.FIND('-');
      exit(TempProfileQuestionnaireLine.Description);
    END;

    [External]
    PROCEDURE GetAnswers@19(VAR ProfileQuestionnaireLine@1000 : Record "Profile Questionnaire Line");
    BEGIN
      TempProfileQuestionnaireLine.RESET;
      ProfileQuestionnaireLine.RESET;
      ProfileQuestionnaireLine.DELETEALL;
      if TempProfileQuestionnaireLine.FIND('-') then
        repeat
          ProfileQuestionnaireLine.INIT;
          ProfileQuestionnaireLine := TempProfileQuestionnaireLine;
          ProfileQuestionnaireLine.INSERT;
        until TempProfileQuestionnaireLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

