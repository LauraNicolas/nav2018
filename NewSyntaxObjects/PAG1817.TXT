OBJECT Page 1817 CRM Connection Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Service Password"=rimd;
    CaptionML=ENU=Dynamics 365 for Sales Connection Setup;
    SourceTable="CRM Connection Setup";
    PageType=NavigatePage;
    SourceTableTemporary=true;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=VAR
                 CRMConnectionSetup@1000 : Record "CRM Connection Setup";
               BEGIN
                 INIT;
                 if CRMConnectionSetup.GET then begin
                   "Server Address" := CRMConnectionSetup."Server Address";
                   "User Name" := CRMConnectionSetup."User Name";
                 end;
                 INSERT;
                 Step := Step::Start;
                 EnableControls;
               END;

    OnQueryClosePage=VAR
                       AssistedSetup@1000 : Record "Assisted Setup";
                     BEGIN
                       if CloseAction = ACTION::OK then
                         if AssistedSetup.GetStatus(PAGE::"CRM Connection Setup Wizard") = AssistedSetup.Status::"Not Completed" then
                           if not CONFIRM(ConnectionNotSetUpQst,false,CRMProductName.SHORT) then
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      Name=ActionContainer2;
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=ENU=Back;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackActionEnabled;
                      InFooterBar=true;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(true);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=ENU=Next;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextActionEnabled;
                      InFooterBar=true;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 if (Step = Step::Start) and ("Server Address" = '') then
                                   ERROR(CRMURLShouldNotBeEmptyErr,CRMProductName.SHORT);
                                 NextStep(false);
                               END;
                                }
      { 25      ;1   ;Action    ;
                      Name=ActionAdvanced;
                      CaptionML=ENU=Advanced;
                      ApplicationArea=#Basic,#Suite;
                      Visible=AdvancedActionEnabled;
                      InFooterBar=true;
                      Image=Setup;
                      OnAction=BEGIN
                                 ShowAdvancedSettings := true;
                                 AdvancedActionEnabled := false;
                                 SimpleActionEnabled := true;
                               END;
                                }
      { 27      ;1   ;Action    ;
                      Name=ActionSimple;
                      CaptionML=ENU=Simple;
                      ApplicationArea=#Basic,#Suite;
                      Visible=SimpleActionEnabled;
                      InFooterBar=true;
                      Image=Setup;
                      OnAction=BEGIN
                                 ShowAdvancedSettings := false;
                                 AdvancedActionEnabled := true;
                                 SimpleActionEnabled := false;
                               END;
                                }
      { 5       ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=ENU=Finish;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishActionEnabled;
                      InFooterBar=true;
                      Image=Approve;
                      OnAction=VAR
                                 AssistedSetup@1000 : Record "Assisted Setup";
                                 CRMConnectionSetup@1001 : Record "CRM Connection Setup";
                                 CRMSystemuser@1005 : Record "CRM Systemuser";
                                 LookupCRMTables@1004 : Codeunit "Lookup CRM Tables";
                                 CRMSystemuserList@1002 : Page "CRM Systemuser List";
                                 IntTableFilter@1003 : Text;
                               BEGIN
                                 if ("User Name" = '') or (Password = '') then
                                   ERROR(CRMSynchUserCredentialsNeededErr,CRMProductName.SHORT);
                                 if not FinalizeSetup then
                                   exit;
                                 AssistedSetup.SetStatus(PAGE::"CRM Connection Setup Wizard",AssistedSetup.Status::Completed);
                                 COMMIT;
                                 CRMConnectionSetup.GET;
                                 if CRMConnectionSetup."Is Enabled" then
                                   if CONFIRM(DoYouWantToMakeSalesPeopleMappingQst,true,CRMProductName.SHORT) then begin
                                     IntTableFilter :=
                                       LookupCRMTables.GetIntegrationTableFilter(DATABASE::"CRM Systemuser",DATABASE::"Salesperson/Purchaser");
                                     CRMSystemuser.SETVIEW(IntTableFilter);
                                     CRMSystemuserList.SETTABLEVIEW(CRMSystemuser);
                                     CRMSystemuserList.Initialize(true);
                                     CRMSystemuserList.RUNMODAL;
                                   end;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 18  ;1   ;Group     ;
                Name=BannerStandard;
                CaptionML=ENU="";
                Visible=TopBannerVisible AND NOT CredentialsStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 17  ;2   ;Field     ;
                Name=MediaResourcesStandard;
                ApplicationArea=#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=false }

    { 16  ;1   ;Group     ;
                Name=BannerDone;
                CaptionML=ENU="";
                Visible=TopBannerVisible AND CredentialsStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 15  ;2   ;Field     ;
                Name=MediaResourcesDone;
                ApplicationArea=#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=false }

    { 6   ;1   ;Group     ;
                Name=Step1;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 24  ;2   ;Group     ;
                Name=Welcome to Dynamics 365 for Sales Connection Setup;
                CaptionML=ENU=Welcome to Dynamics 365 for Sales Connection Setup;
                GroupType=Group }

    { 23  ;3   ;Group     ;
                Name=Group23;
                GroupType=Group;
                InstructionalTextML=ENU=You can set up a Dynamics 365 for Sales connection to enable seamless coupling of data. }

    { 21  ;3   ;Group     ;
                Name=Group21;
                GroupType=Group;
                InstructionalTextML=ENU=Start by specifying the URL to your Dynamics 365 for Sales solution, such as https://mycrm.crm4.dynamics.com }

    { 34  ;3   ;Field     ;
                Name=ServerAddress;
                ApplicationArea=#Suite;
                SourceExpr="Server Address";
                OnValidate=VAR
                             CRMIntegrationManagement@1000 : Codeunit "CRM Integration Management";
                           BEGIN
                             CRMIntegrationManagement.CheckModifyCRMConnectionURL("Server Address");
                           END;

                ImplicitType=Text250 }

    { 9   ;3   ;Group     ;
                Name=Group9;
                GroupType=Group;
                InstructionalTextML=ENU=Once coupled, you can work with and synchronize data types that are common to both services, such as customers, contacts, and sales information, and keep the data up-to-date in both locations. }

    { 8   ;1   ;Group     ;
                Name=Step2;
                CaptionML=ENU="";
                Visible=CredentialsStepVisible;
                GroupType=Group }

    { 19  ;2   ;Group     ;
                Name=Step2.1;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=Specify the user that will be used for synchronization between the two services. }

    { 11  ;3   ;Field     ;
                Name=Email;
                ExtendedDatatype=E-Mail;
                CaptionML=ENU=Email;
                ApplicationArea=#Suite;
                SourceExpr="User Name";
                ImplicitType=Text250 }

    { 12  ;3   ;Field     ;
                Name=Password;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Password;
                ApplicationArea=#Suite;
                SourceExpr=Password;
                ImplicitType=Text }

    { 22  ;2   ;Group     ;
                Name=Group22;
                GroupType=Group;
                InstructionalTextML=ENU=This account must already be a valid user in Dynamics 365 for Sales. }

    { 7   ;2   ;Group     ;
                Name=Advanced Settings;
                CaptionML=ENU=Advanced Settings;
                Visible=ShowAdvancedSettings;
                GroupType=Group }

    { 32  ;3   ;Field     ;
                Name=ImportCRMSolution;
                CaptionML=ENU=Import Dynamics 365 for Sales Solution;
                ApplicationArea=#Suite;
                SourceExpr=ImportSolution;
                Enabled=ImportCRMSolutionEnabled;
                OnValidate=BEGIN
                             OnImportSolutionChange;
                           END;

                ImplicitType=Boolean }

    { 31  ;3   ;Field     ;
                Name=PublishItemAvailabilityService;
                CaptionML=ENU=Publish Item Availability Web Service;
                ApplicationArea=#Suite;
                SourceExpr=PublishItemAvailabilityService;
                Enabled=PublishItemAvailabilityServiceEnabled;
                OnValidate=BEGIN
                             if not PublishItemAvailabilityService then begin
                               CLEAR("Dynamics NAV OData Username");
                               CLEAR("Dynamics NAV OData Accesskey");
                             end;
                           END;

                ImplicitType=Boolean }

    { 26  ;3   ;Field     ;
                Name=You must assign the security role Dynamics NAV Product Availability User to your sales people in Dynamics 365 for Sales.;
                CaptionML=ENU=You must assign the security role Dynamics NAV Product Availability User to your sales people in Dynamics 365 for Sales.;
                ApplicationArea=#Suite }

    { 30  ;3   ;Field     ;
                Name=NAVODataUsername;
                Lookup=true;
                CaptionML=ENU=Dynamics NAV OData Web Service User Name;
                ApplicationArea=#Suite;
                SourceExpr="Dynamics NAV OData Username";
                Enabled=PublishItemAvailabilityServiceEnabled;
                Editable=PublishItemAvailabilityService;
                LookupPageID=Users;
                OnLookup=VAR
                           User@1000 : Record User;
                         BEGIN
                           if PAGE.RUNMODAL(PAGE::Users,User) = ACTION::LookupOK then begin
                             "Dynamics NAV OData Username" := User."User Name";
                             UpdateUserWebKey(User);
                           end;
                         END;

                ImplicitType=Text250 }

    { 14  ;3   ;Field     ;
                Name=NAVODataAccesskey;
                CaptionML=ENU=Dynamics NAV OData Web Service Access Key;
                ApplicationArea=#Suite;
                SourceExpr="Dynamics NAV OData Accesskey";
                Enabled=PublishItemAvailabilityServiceEnabled;
                Editable=FALSE;
                ImplicitType=Text250 }

    { 13  ;3   ;Field     ;
                Name=EnableSalesOrderIntegration;
                CaptionML=ENU=Enable Sales Order Integration;
                ApplicationArea=#Suite;
                SourceExpr=EnableSalesOrderIntegration;
                Enabled=EnableSalesOrderIntegrationEnabled;
                ImplicitType=Boolean }

    { 10  ;3   ;Field     ;
                Name=EnableCRMConnection;
                CaptionML=ENU=Enable Dynamics 365 for Sales Connection;
                ApplicationArea=#Suite;
                SourceExpr=EnableCRMConnection;
                Enabled=EnableCRMConnectionEnabled;
                ImplicitType=Boolean }

    { 20  ;2   ;Group     ;
                Name=Group20;
                GroupType=Group;
                InstructionalTextML=ENU=To enable the connection, choose Finish. You will be asked to specify an administrative user account in Dynamics 365 for Sales. }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1015 : Record "Media Repository";
      MediaRepositoryDone@1014 : Record "Media Repository";
      MediaResourcesStandard@1025 : Record "Media Resources";
      MediaResourcesDone@1018 : Record "Media Resources";
      CRMProductName@1029 : Codeunit "CRM Product Name";
      Step@1000 : 'Start,Credentials,Finish';
      TopBannerVisible@1013 : Boolean;
      BackActionEnabled@1001 : Boolean;
      NextActionEnabled@1002 : Boolean;
      FinishActionEnabled@1003 : Boolean;
      FirstStepVisible@1004 : Boolean;
      CredentialsStepVisible@1006 : Boolean;
      EnableCRMConnection@1009 : Boolean;
      ImportSolution@1010 : Boolean;
      PublishItemAvailabilityService@1019 : Boolean;
      EnableCRMConnectionEnabled@1012 : Boolean;
      ImportCRMSolutionEnabled@1011 : Boolean;
      PublishItemAvailabilityServiceEnabled@1020 : Boolean;
      EnableSalesOrderIntegration@1021 : Boolean;
      EnableSalesOrderIntegrationEnabled@1022 : Boolean;
      ShowAdvancedSettings@1027 : Boolean;
      AdvancedActionEnabled@1017 : Boolean;
      SimpleActionEnabled@1007 : Boolean;
      Password@1008 : Text;
      ConnectionNotSetUpQst@1016 : TextConst '@@@="%1 = CRM product name";ENU=The %1 connection has not been set up.\\Are you sure you want to exit?';
      MustUpdateClientsQst@1023 : TextConst 'ENU=If you change the web service access key, the current access key will no longer be valid. You must update all clients that use it. Do you want to continue?';
      ConfirmCredentialsDomainQst@1024 : TextConst 'ENU=The administrator user account and the integration user account appear to be from different domains. Are you sure that the credentials are correct?';
      CRMURLShouldNotBeEmptyErr@1026 : TextConst '@@@="%1 = CRM product name";ENU=You must specify the URL of your %1 solution.';
      CRMSynchUserCredentialsNeededErr@1005 : TextConst '@@@="%1 = CRM product name";ENU=You must specify the credentials for the user account for synchronization with %1.';
      DoYouWantToMakeSalesPeopleMappingQst@1028 : TextConst '@@@="%1 = CRM product name";ENU=Do you want to map salespeople to users in %1?';

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      if MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE)) and
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE))
      then
        if MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") and
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        then
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    LOCAL PROCEDURE NextStep@4(Backward@1000 : Boolean);
    BEGIN
      if Backward then
        Step := Step - 1
      else
        Step := Step + 1;

      EnableControls;
    END;

    LOCAL PROCEDURE ResetControls@11();
    BEGIN
      BackActionEnabled := false;
      NextActionEnabled := false;
      FinishActionEnabled := false;
      AdvancedActionEnabled := false;

      FirstStepVisible := false;
      CredentialsStepVisible := false;

      ImportCRMSolutionEnabled := true;
      PublishItemAvailabilityServiceEnabled := true;
      EnableSalesOrderIntegrationEnabled := true;
      EnableSalesOrderIntegration := true;
    END;

    LOCAL PROCEDURE EnableControls@18();
    BEGIN
      ResetControls;

      case Step of
        Step::Start:
          ShowStartStep;
        Step::Credentials:
          ShowFinishStep;
      end;
    END;

    LOCAL PROCEDURE ShowStartStep@46();
    BEGIN
      BackActionEnabled := false;
      NextActionEnabled := true;
      FinishActionEnabled := false;
      FirstStepVisible := true;
      AdvancedActionEnabled := false;
      SimpleActionEnabled := false;
    END;

    LOCAL PROCEDURE ShowFinishStep@54();
    VAR
      CRMConnectionSetup@1000 : Record "CRM Connection Setup";
      User@1001 : Record User;
      IdentityManagement@1002 : Codeunit "Identity Management";
    BEGIN
      BackActionEnabled := true;
      NextActionEnabled := false;
      AdvancedActionEnabled := not ShowAdvancedSettings;
      SimpleActionEnabled := not AdvancedActionEnabled;
      CredentialsStepVisible := true;
      FinishActionEnabled := true;

      EnableSalesOrderIntegrationEnabled := ImportCRMSolutionEnabled;
      EnableCRMConnectionEnabled := "Server Address" <> '';
      if User.GET(USERSECURITYID) then begin
        "Dynamics NAV OData Accesskey" := IdentityManagement.GetWebServicesKey(User."User Security ID");
        if "Dynamics NAV OData Accesskey" <> '' then
          "Dynamics NAV OData Username" := User."User Name";
      end;
      "Authentication Type" := "Authentication Type"::Office365;
      if CRMConnectionSetup.GET then begin
        EnableCRMConnection := true;
        EnableCRMConnectionEnabled := not CRMConnectionSetup."Is Enabled";
        EnableSalesOrderIntegration := true;
        EnableSalesOrderIntegrationEnabled := not CRMConnectionSetup."Is S.Order Integration Enabled";
        ImportSolution := true;
        if CRMConnectionSetup."Is CRM Solution Installed" then begin
          ImportCRMSolutionEnabled := false;
          "Dynamics NAV OData Username" := CRMConnectionSetup."Dynamics NAV OData Username";
          "Dynamics NAV OData Accesskey" := CRMConnectionSetup."Dynamics NAV OData Accesskey";
        end;
      end else begin
        if ImportCRMSolutionEnabled then
          ImportSolution := true;
        if EnableCRMConnectionEnabled then
          EnableCRMConnection := true;
      end;
    END;

    LOCAL PROCEDURE FinalizeSetup@1() : Boolean;
    VAR
      CRMConnectionSetup@1002 : Record "CRM Connection Setup";
      CRMIntegrationManagement@1000 : Codeunit "CRM Integration Management";
      AdminEmail@1005 : Text[250];
      AdminPassKey@1003 : GUID;
    BEGIN
      if ImportSolution and ImportCRMSolutionEnabled then begin
        if not PromptForCredentials(AdminEmail,AdminPassKey) then
          exit(false);
        CRMIntegrationManagement.ImportCRMSolution("Server Address","User Name",AdminEmail,AdminPassKey);
      end;
      if PublishItemAvailabilityService then
        CRMIntegrationManagement.SetupItemAvailabilityService;

      CRMConnectionSetup.UpdateFromWizard(Rec,Password);
      if EnableCRMConnection then
        CRMConnectionSetup.EnableCRMConnectionFromWizard;
      if EnableSalesOrderIntegration and EnableSalesOrderIntegrationEnabled then
        CRMConnectionSetup.SetCRMSOPEnabledWithCredentials(AdminEmail,AdminPassKey,true);
      if PublishItemAvailabilityService and PublishItemAvailabilityServiceEnabled then begin
        CRMIntegrationManagement.SetCRMNAVConnectionUrl(GETURL(CLIENTTYPE::Web));
        CRMIntegrationManagement.SetCRMNAVODataUrlCredentials(
          GETURL(CLIENTTYPE::OData,COMPANYNAME),"Dynamics NAV OData Username","Dynamics NAV OData Accesskey");
      end;
      exit(true);
    END;

    LOCAL PROCEDURE OnImportSolutionChange@3();
    BEGIN
      PublishItemAvailabilityServiceEnabled := ImportSolution;
      PublishItemAvailabilityService := ImportSolution;
    END;

    LOCAL PROCEDURE UpdateUserWebKey@2(User@1002 : Record User);
    VAR
      PermissionManager@1001 : Codeunit "Permission Manager";
      IdentityManagement@1003 : Codeunit "Identity Management";
      SetWebServiceAccessKey@1000 : Page "Set Web Service Access Key";
    BEGIN
      "Dynamics NAV OData Accesskey" := IdentityManagement.GetWebServicesKey(User."User Security ID");
      if not (PermissionManager.SoftwareAsAService and (USERSECURITYID <> User."User Security ID")) and
         ("Dynamics NAV OData Accesskey" = '')
      then
        if CONFIRM(MustUpdateClientsQst) then begin
          User.SETCURRENTKEY("User Security ID");
          User.SETRANGE("User Name",User."User Name");
          SetWebServiceAccessKey.SETRECORD(User);
          SetWebServiceAccessKey.SETTABLEVIEW(User);
          if SetWebServiceAccessKey.RUNMODAL = ACTION::OK then
            "Dynamics NAV OData Accesskey" := IdentityManagement.GetWebServicesKey(User."User Security ID");
        end;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE PromptForCredentials@6(VAR AdminEmail@1002 : Text;VAR PasswordKey@1003 : GUID) : Boolean;
    VAR
      TempOfficeAdminCredentials@1000 : TEMPORARY Record "Office Admin. Credentials";
    BEGIN
      if TempOfficeAdminCredentials.ISEMPTY then begin
        TempOfficeAdminCredentials.INIT;
        TempOfficeAdminCredentials.INSERT(true);
        COMMIT;
        if PAGE.RUNMODAL(PAGE::"Dynamics CRM Admin Credentials",TempOfficeAdminCredentials) <> ACTION::LookupOK then
          exit(false);
      end;
      if (not TempOfficeAdminCredentials.FINDFIRST) or
         (TempOfficeAdminCredentials.Email = '') or (TempOfficeAdminCredentials.GetPassword = '')
      then begin
        TempOfficeAdminCredentials.DELETEALL(true);
        exit(false);
      end;

      AdminEmail := TempOfficeAdminCredentials.Email;
      if not CheckCredentialsSameDomain(AdminEmail) then
        exit(false);
      PasswordKey := TempOfficeAdminCredentials.Password;
      exit(true);
    END;

    LOCAL PROCEDURE CheckCredentialsSameDomain@8(AdminEmail@1000 : Text) : Boolean;
    BEGIN
      if (GetDomainFromEmail(AdminEmail) <> GetDomainFromEmail("User Name")) and (AdminEmail <> '') then
        if not CONFIRM(ConfirmCredentialsDomainQst) then
          exit(false);
      exit(true);
    END;

    LOCAL PROCEDURE GetDomainFromEmail@5(Email@1000 : Text) : Text;
    BEGIN
      exit(DELSTR(Email,1,STRPOS(Email,'@')));
    END;

    BEGIN
    END.
  }
}

