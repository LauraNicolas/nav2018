OBJECT Table 96 G/L Budget Entry
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Analysis View Budget Entry"=rd;
    OnInsert=VAR
               TempDimSetEntry@1000 : TEMPORARY Record "Dimension Set Entry";
             BEGIN
               CheckIfBlocked;
               TESTFIELD(Date);
               TESTFIELD("G/L Account No.");
               TESTFIELD("Budget Name");
               LOCKTABLE;
               "User ID" := USERID;
               "Last Date Modified" := TODAY;
               if "Entry No." = 0 then
                 "Entry No." := GetNextEntryNo;

               GetGLSetup;
               DimMgt.GetDimensionSet(TempDimSetEntry,"Dimension Set ID");
               UpdateDimSet(TempDimSetEntry,GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
               UpdateDimSet(TempDimSetEntry,GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
               UpdateDimSet(TempDimSetEntry,GLBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
               UpdateDimSet(TempDimSetEntry,GLBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
               UpdateDimSet(TempDimSetEntry,GLBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
               UpdateDimSet(TempDimSetEntry,GLBudgetName."Budget Dimension 4 Code","Budget Dimension 4 Code");
               VALIDATE("Dimension Set ID",DimMgt.GetDimensionSetID(TempDimSetEntry));
             END;

    OnModify=BEGIN
               CheckIfBlocked;
               "User ID" := USERID;
               "Last Date Modified" := TODAY;
             END;

    OnDelete=BEGIN
               CheckIfBlocked;
               DeleteAnalysisViewBudgetEntries;
             END;

    CaptionML=ENU=G/L Budget Entry;
    LookupPageID="G/L Budget Entries";
    DrillDownPageID="G/L Budget Entries";
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No. }
    { 2   ;   ;Budget Name         ;Code10        ;TableRelation="G/L Budget Name";
                                                   CaptionML=ENU=Budget Name }
    { 3   ;   ;G/L Account No.     ;Code20        ;TableRelation="G/L Account";
                                                   CaptionML=ENU=G/L Account No. }
    { 4   ;   ;Date                ;Date          ;CaptionML=ENU=Date;
                                                   ClosingDates=true }
    { 5   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1));
                                                   OnValidate=BEGIN
                                                                if "Global Dimension 1 Code" = xRec."Global Dimension 1 Code" then
                                                                  exit;
                                                                GetGLSetup;
                                                                ValidateDimValue(GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
                                                                UpdateDimensionSetId(GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 1 Code;
                                                   CaptionClass='1,1,1' }
    { 6   ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2));
                                                   OnValidate=BEGIN
                                                                if "Global Dimension 2 Code" = xRec."Global Dimension 2 Code" then
                                                                  exit;
                                                                GetGLSetup;
                                                                ValidateDimValue(GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
                                                                UpdateDimensionSetId(GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 2 Code;
                                                   CaptionClass='1,1,2' }
    { 7   ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   AutoFormatType=1 }
    { 9   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 10  ;   ;Business Unit Code  ;Code20        ;TableRelation="Business Unit";
                                                   CaptionML=ENU=Business Unit Code }
    { 11  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit "User Management";
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=false;
                                                   CaptionML=ENU=User ID;
                                                   Editable=false }
    { 12  ;   ;Budget Dimension 1 Code;Code20     ;OnValidate=BEGIN
                                                                if "Budget Dimension 1 Code" = xRec."Budget Dimension 1 Code" then
                                                                  exit;
                                                                if GLBudgetName.Name <> "Budget Name" then
                                                                  GLBudgetName.GET("Budget Name");
                                                                ValidateDimValue(GLBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
                                                                UpdateDimensionSetId(GLBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 1 Code" := OnLookupDimCode(2,"Budget Dimension 1 Code");
                                                            END;

                                                   AccessByPermission=TableData Dimension=R;
                                                   CaptionML=ENU=Budget Dimension 1 Code;
                                                   CaptionClass=GetCaptionClass(1) }
    { 13  ;   ;Budget Dimension 2 Code;Code20     ;OnValidate=BEGIN
                                                                if "Budget Dimension 2 Code" = xRec."Budget Dimension 2 Code" then
                                                                  exit;
                                                                if GLBudgetName.Name <> "Budget Name" then
                                                                  GLBudgetName.GET("Budget Name");
                                                                ValidateDimValue(GLBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
                                                                UpdateDimensionSetId(GLBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 2 Code" := OnLookupDimCode(3,"Budget Dimension 2 Code");
                                                            END;

                                                   AccessByPermission=TableData Dimension=R;
                                                   CaptionML=ENU=Budget Dimension 2 Code;
                                                   CaptionClass=GetCaptionClass(2) }
    { 14  ;   ;Budget Dimension 3 Code;Code20     ;OnValidate=BEGIN
                                                                if "Budget Dimension 3 Code" = xRec."Budget Dimension 3 Code" then
                                                                  exit;
                                                                if GLBudgetName.Name <> "Budget Name" then
                                                                  GLBudgetName.GET("Budget Name");
                                                                ValidateDimValue(GLBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
                                                                UpdateDimensionSetId(GLBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 3 Code" := OnLookupDimCode(4,"Budget Dimension 3 Code");
                                                            END;

                                                   AccessByPermission=TableData "Dimension Combination"=R;
                                                   CaptionML=ENU=Budget Dimension 3 Code;
                                                   CaptionClass=GetCaptionClass(3) }
    { 15  ;   ;Budget Dimension 4 Code;Code20     ;OnValidate=BEGIN
                                                                if "Budget Dimension 4 Code" = xRec."Budget Dimension 4 Code" then
                                                                  exit;
                                                                if GLBudgetName.Name <> "Budget Name" then
                                                                  GLBudgetName.GET("Budget Name");
                                                                ValidateDimValue(GLBudgetName."Budget Dimension 4 Code","Budget Dimension 4 Code");
                                                                UpdateDimensionSetId(GLBudgetName."Budget Dimension 4 Code","Budget Dimension 4 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 4 Code" := OnLookupDimCode(5,"Budget Dimension 4 Code");
                                                            END;

                                                   AccessByPermission=TableData "Dimension Combination"=R;
                                                   CaptionML=ENU=Budget Dimension 4 Code;
                                                   CaptionClass=GetCaptionClass(4) }
    { 16  ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=false }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                if not DimMgt.CheckDimIDComb("Dimension Set ID") then
                                                                  ERROR(DimMgt.GetDimCombErr);
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Entry No."                             ;Clustered=true }
    {    ;"Budget Name","G/L Account No.",Date    ;SumIndexFields=Amount }
    {    ;"Budget Name","G/L Account No.","Business Unit Code","Global Dimension 1 Code","Global Dimension 2 Code","Budget Dimension 1 Code","Budget Dimension 2 Code","Budget Dimension 3 Code","Budget Dimension 4 Code",Date;
                                                   SumIndexFields=Amount }
    {    ;"Budget Name","G/L Account No.",Description,Date }
    {    ;"G/L Account No.",Date,"Budget Name","Dimension Set ID";
                                                   SumIndexFields=Amount }
    {    ;"Last Date Modified","Budget Name"       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The dimension value %1 has not been set up for dimension %2.';
      Text001@1001 : TextConst 'ENU=1,5,,Budget Dimension 1 Code';
      Text002@1002 : TextConst 'ENU=1,5,,Budget Dimension 2 Code';
      Text003@1003 : TextConst 'ENU=1,5,,Budget Dimension 3 Code';
      Text004@1004 : TextConst 'ENU=1,5,,Budget Dimension 4 Code';
      GLBudgetName@1005 : Record "G/L Budget Name";
      GLSetup@1006 : Record "General Ledger Setup";
      DimVal@1009 : Record "Dimension Value";
      DimMgt@1008 : Codeunit DimensionManagement;
      GLSetupRetrieved@1007 : Boolean;

    LOCAL PROCEDURE CheckIfBlocked@1();
    BEGIN
      if "Budget Name" = GLBudgetName.Name then
        exit;
      if GLBudgetName.Name <> "Budget Name" then
        GLBudgetName.GET("Budget Name");
      GLBudgetName.TESTFIELD(Blocked,false);
    END;

    LOCAL PROCEDURE ValidateDimValue@5(DimCode@1000 : Code[20];VAR DimValueCode@1001 : Code[20]);
    VAR
      DimValue@1002 : Record "Dimension Value";
    BEGIN
      if DimValueCode = '' then
        exit;

      DimValue."Dimension Code" := DimCode;
      DimValue.Code := DimValueCode;
      DimValue.FIND('=><');
      if DimValueCode <> COPYSTR(DimValue.Code,1,STRLEN(DimValueCode)) then
        ERROR(Text000,DimValueCode,DimCode);
      DimValueCode := DimValue.Code;
    END;

    LOCAL PROCEDURE GetGLSetup@2();
    BEGIN
      if not GLSetupRetrieved then begin
        GLSetup.GET;
        GLSetupRetrieved := true;
      end;
    END;

    LOCAL PROCEDURE OnLookupDimCode@3(DimOption@1000 : '"Global Dimension 1","Global Dimension 2","Budget Dimension 1","Budget Dimension 2","Budget Dimension 3","Budget Dimension 4"';DefaultValue@1001 : Code[20]) : Code[20];
    VAR
      DimValue@1002 : Record "Dimension Value";
      DimValueList@1003 : Page "Dimension Value List";
    BEGIN
      if DimOption in [DimOption::"Global Dimension 1",DimOption::"Global Dimension 2"] then
        GetGLSetup
      else
        if GLBudgetName.Name <> "Budget Name" then
          GLBudgetName.GET("Budget Name");
      case DimOption of
        DimOption::"Global Dimension 1":
          DimValue."Dimension Code" := GLSetup."Global Dimension 1 Code";
        DimOption::"Global Dimension 2":
          DimValue."Dimension Code" := GLSetup."Global Dimension 2 Code";
        DimOption::"Budget Dimension 1":
          DimValue."Dimension Code" := GLBudgetName."Budget Dimension 1 Code";
        DimOption::"Budget Dimension 2":
          DimValue."Dimension Code" := GLBudgetName."Budget Dimension 2 Code";
        DimOption::"Budget Dimension 3":
          DimValue."Dimension Code" := GLBudgetName."Budget Dimension 3 Code";
        DimOption::"Budget Dimension 4":
          DimValue."Dimension Code" := GLBudgetName."Budget Dimension 4 Code";
      end;
      DimValue.SETRANGE("Dimension Code",DimValue."Dimension Code");
      if DimValue.GET(DimValue."Dimension Code",DefaultValue) then;
      DimValueList.SETTABLEVIEW(DimValue);
      DimValueList.SETRECORD(DimValue);
      DimValueList.LOOKUPMODE := true;
      if DimValueList.RUNMODAL = ACTION::LookupOK then begin
        DimValueList.GETRECORD(DimValue);
        exit(DimValue.Code);
      end;
      exit(DefaultValue);
    END;

    LOCAL PROCEDURE GetNextEntryNo@4() : Integer;
    VAR
      GLBudgetEntry@1000 : Record "G/L Budget Entry";
    BEGIN
      GLBudgetEntry.SETCURRENTKEY("Entry No.");
      if GLBudgetEntry.FINDLAST then
        exit(GLBudgetEntry."Entry No." + 1);

      exit(1);
    END;

    LOCAL PROCEDURE GetCaptionClass@7(BudgetDimType@1000 : Integer) : Text[250];
    BEGIN
      if GETFILTER("Budget Name") <> '' then begin
        GLBudgetName.SETFILTER(Name,GETFILTER("Budget Name"));
        if not GLBudgetName.FINDFIRST then
          CLEAR(GLBudgetName);
      end;
      case BudgetDimType of
        1:
          begin
            if GLBudgetName."Budget Dimension 1 Code" <> '' then
              exit('1,5,' + GLBudgetName."Budget Dimension 1 Code");

            exit(Text001);
          end;
        2:
          begin
            if GLBudgetName."Budget Dimension 2 Code" <> '' then
              exit('1,5,' + GLBudgetName."Budget Dimension 2 Code");

            exit(Text002);
          end;
        3:
          begin
            if GLBudgetName."Budget Dimension 3 Code" <> '' then
              exit('1,5,' + GLBudgetName."Budget Dimension 3 Code");

            exit(Text003);
          end;
        4:
          begin
            if GLBudgetName."Budget Dimension 4 Code" <> '' then
              exit('1,5,' + GLBudgetName."Budget Dimension 4 Code");

            exit(Text004);
          end;
      end;
    END;

    [External]
    PROCEDURE ShowDimensions@8();
    VAR
      DimSetEntry@1000 : Record "Dimension Set Entry";
      OldDimSetID@1001 : Integer;
    BEGIN
      OldDimSetID := "Dimension Set ID";
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet(
          "Dimension Set ID",STRSUBSTNO('%1 %2 %3',"Budget Name","G/L Account No.","Entry No."));

      if OldDimSetID = "Dimension Set ID" then
        exit;

      GetGLSetup;
      GLBudgetName.GET("Budget Name");

      "Global Dimension 1 Code" := '';
      "Global Dimension 2 Code" := '';
      "Budget Dimension 1 Code" := '';
      "Budget Dimension 2 Code" := '';
      "Budget Dimension 3 Code" := '';
      "Budget Dimension 4 Code" := '';

      if DimSetEntry.GET("Dimension Set ID",GLSetup."Global Dimension 1 Code") then
        "Global Dimension 1 Code" := DimSetEntry."Dimension Value Code";
      if DimSetEntry.GET("Dimension Set ID",GLSetup."Global Dimension 2 Code") then
        "Global Dimension 2 Code" := DimSetEntry."Dimension Value Code";
      if DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 1 Code") then
        "Budget Dimension 1 Code" := DimSetEntry."Dimension Value Code";
      if DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 2 Code") then
        "Budget Dimension 2 Code" := DimSetEntry."Dimension Value Code";
      if DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 3 Code") then
        "Budget Dimension 3 Code" := DimSetEntry."Dimension Value Code";
      if DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 4 Code") then
        "Budget Dimension 4 Code" := DimSetEntry."Dimension Value Code";
    END;

    [External]
    PROCEDURE UpdateDimSet@6(VAR TempDimSetEntry@1003 : TEMPORARY Record "Dimension Set Entry";DimCode@1000 : Code[20];DimValueCode@1001 : Code[20]);
    BEGIN
      if DimCode = '' then
        exit;
      if TempDimSetEntry.GET("Dimension Set ID",DimCode) then
        TempDimSetEntry.DELETE;
      if DimValueCode = '' then
        DimVal.INIT
      else
        DimVal.GET(DimCode,DimValueCode);
      TempDimSetEntry.INIT;
      TempDimSetEntry."Dimension Set ID" := "Dimension Set ID";
      TempDimSetEntry."Dimension Code" := DimCode;
      TempDimSetEntry."Dimension Value Code" := DimValueCode;
      TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
      TempDimSetEntry.INSERT;
    END;

    LOCAL PROCEDURE UpdateDimensionSetId@9(DimCode@1002 : Code[20];DimValueCode@1001 : Code[20]);
    VAR
      TempDimSetEntry@1000 : TEMPORARY Record "Dimension Set Entry";
    BEGIN
      DimMgt.GetDimensionSet(TempDimSetEntry,"Dimension Set ID");
      UpdateDimSet(TempDimSetEntry,DimCode,DimValueCode);
      "Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
    END;

    LOCAL PROCEDURE DeleteAnalysisViewBudgetEntries@10();
    VAR
      AnalysisViewBudgetEntry@1000 : Record "Analysis View Budget Entry";
    BEGIN
      AnalysisViewBudgetEntry.SETRANGE("Budget Name","Budget Name");
      AnalysisViewBudgetEntry.SETRANGE("Entry No.","Entry No.");
      AnalysisViewBudgetEntry.DELETEALL;
    END;

    BEGIN
    END.
  }
}

