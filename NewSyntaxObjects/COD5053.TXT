OBJECT Codeunit 5053 TAPIManagement
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=No Contact found.';
      Text002@1000 : TextConst 'ENU=No registered phone numbers have been found for this %1.';

    [External]
    PROCEDURE Dial@1(PhoneNumber@1001 : Text[80]) : Boolean;
    BEGIN
      HYPERLINK(STRSUBSTNO('tel:%1',PhoneNumber));
      exit(true);
    END;

    [External]
    PROCEDURE DialContCustVendBank@5(TableNo@1000 : Integer;No@1001 : Code[20];PhoneNo@1002 : Text[30];ContAltAddrCode@1003 : Code[10]);
    VAR
      ContBusRel@1005 : Record "Contact Business Relation";
      Contact@1006 : Record Contact;
      Task@1008 : Record "To-do";
      TempSegmentLine@1007 : TEMPORARY Record "Segment Line";
    BEGIN
      case TableNo of
        DATABASE::Contact:
          Contact.GET(No);
        DATABASE::"To-do":
          begin
            Task.GET(No);
            Task.TESTFIELD("Contact No.");
            Contact.GET(Task."Contact No.");
          end;
        else begin
          ContBusRel.RESET;
          ContBusRel.SETCURRENTKEY("Link to Table","No.");
          case TableNo of
            DATABASE::Customer:
              ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
            DATABASE::Vendor:
              ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Vendor);
            DATABASE::"Bank Account":
              ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::"Bank Account");
          end;
          ContBusRel.SETRANGE("No.",No);
          if ContBusRel.FINDFIRST then
            Contact.GET(ContBusRel."Contact No.")
          else
            ERROR(Text001);
        end;
      end;

      // Call Make Phone Call Wizard
      TempSegmentLine.MakePhoneCallFromContact(Contact,Task,TableNo,PhoneNo,ContAltAddrCode);
    END;

    [External]
    PROCEDURE ShowNumbers@4(ContactNo@1000 : Code[20];ContAltAddrCode@1001 : Code[10]) : Text[260];
    VAR
      TempCommunicationMethod@1002 : TEMPORARY Record "Communication Method";
      Contact@1003 : Record Contact;
      Contact2@1004 : Record Contact;
      ContAltAddrCode2@1006 : Code[10];
    BEGIN
      if not Contact.GET(ContactNo) then
        exit;

      if ContAltAddrCode = '' then
        ContAltAddrCode2 := Contact.ActiveAltAddress(TODAY)
      else
        ContAltAddrCode2 := ContAltAddrCode;

      CreateCommMethod(Contact,TempCommunicationMethod,ContactNo,ContAltAddrCode);

      // Get linked company phonenumbers
      if (Contact.Type = Contact.Type::Person) and (Contact."Company No." <> '') then begin
        Contact2.GET(Contact."Company No.");

        if ContAltAddrCode = '' then
          ContAltAddrCode2 := Contact2.ActiveAltAddress(TODAY)
        else
          ContAltAddrCode2 := ContAltAddrCode;

        CreateCommMethod(Contact2,TempCommunicationMethod,ContactNo,ContAltAddrCode2);
      end;
      if TempCommunicationMethod.FINDFIRST then begin
        if PAGE.RUNMODAL(PAGE::"Contact Through",TempCommunicationMethod) = ACTION::LookupOK then
          exit(TempCommunicationMethod.Number);
      end else
        ERROR(Text002,Contact.TABLECAPTION);
    END;

    LOCAL PROCEDURE TrimCode@3(Code@1001 : Code[20]) TrimString@1000 : Text[20];
    BEGIN
      TrimString := COPYSTR(Code,1,1) + LOWERCASE(COPYSTR(Code,2,STRLEN(Code) - 1))
    END;

    LOCAL PROCEDURE CreateCommMethod@2(Contact@1000 : Record Contact;VAR TempCommunicationMethod@1001 : TEMPORARY Record "Communication Method";ContactNo@1002 : Code[20];ContAltAddrCode@1004 : Code[10]);
    VAR
      ContAltAddr@1005 : Record "Contact Alt. Address";
    BEGIN
      with TempCommunicationMethod do begin
        INIT;
        "Contact No." := ContactNo;
        Name := Contact.Name;
        if Contact."Phone No." <> '' then begin
          Key += 1;
          Description := COPYSTR(Contact.FIELDCAPTION("Phone No."),1,MAXSTRLEN(Description));
          Number := Contact."Phone No.";
          Type := Contact.Type;
          INSERT;
        end;
        if Contact."Mobile Phone No." <> '' then begin
          Key += 1;
          Description := COPYSTR(Contact.FIELDCAPTION("Mobile Phone No."),1,MAXSTRLEN(Description));
          Number := Contact."Mobile Phone No.";
          Type := Contact.Type;
          INSERT;
        end;

        // Alternative address
        if ContAltAddr.GET(Contact."No.",ContAltAddrCode) then begin
          if ContAltAddr."Phone No." <> '' then begin
            Key += 1;
            Description :=
              COPYSTR(TrimCode(ContAltAddr.Code) + ' - ' + ContAltAddr.FIELDCAPTION("Phone No."),1,MAXSTRLEN(Description));
            Number := ContAltAddr."Phone No.";
            Type := Contact.Type;
            INSERT;
          end;
          if ContAltAddr."Mobile Phone No." <> '' then begin
            Key += 1;
            Description :=
              COPYSTR(TrimCode(ContAltAddr.Code) + ' - ' + ContAltAddr.FIELDCAPTION("Mobile Phone No."),1,MAXSTRLEN(Description));
            Number := ContAltAddr."Mobile Phone No.";
            Type := Contact.Type;
            INSERT;
          end;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

