OBJECT Codeunit 1022 Job Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="Job Journal Line";
    OnRun=BEGIN
            JobJnlLine.COPY(Rec);
            Code;
            COPY(JobJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals.';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU="The journal lines were successfully posted. "';
      Text005@1005 : TextConst 'ENU=You are now in the %1 journal.';
      JobJnlTemplate@1006 : Record "Job Journal Template";
      JobJnlLine@1007 : Record "Job Journal Line";
      JobReg@1008 : Record "Job Register";
      TempJnlBatchName@1010 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      with JobJnlLine do begin
        JobJnlTemplate.GET("Journal Template Name");
        JobJnlTemplate.TESTFIELD("Posting Report ID");
        if JobJnlTemplate.Recurring and (GETFILTER("Posting Date") <> '') then
          FIELDERROR("Posting Date",Text000);

        if not CONFIRM(Text001) then
          exit;

        TempJnlBatchName := "Journal Batch Name";

        CODEUNIT.RUN(CODEUNIT::"Job Jnl.-Post Batch",JobJnlLine);

        if JobReg.GET("Line No.") then begin
          JobReg.SETRECFILTER;
          REPORT.RUN(JobJnlTemplate."Posting Report ID",false,false,JobReg);
        end;

        if "Line No." = 0 then
          MESSAGE(Text002)
        else
          if TempJnlBatchName = "Journal Batch Name" then
            MESSAGE(Text003)
          else
            MESSAGE(
              Text004 +
              Text005,
              "Journal Batch Name");

        if not FIND('=><') or (TempJnlBatchName <> "Journal Batch Name") then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

