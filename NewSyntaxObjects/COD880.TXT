OBJECT Codeunit 880 OCR - Send to Service
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Job Queue Entry";
    OnRun=BEGIN
            SendAllReadyToOcr;
          END;

  }
  CODE
  {
    VAR
      SendMsg@1000 : TextConst 'ENU=Sending to the OCR Service @1@@@@@@@@@@@@@@@@@@@.';
      SendDoneMsg@1001 : TextConst '@@@=%1 is a number, e.g. 1;ENU=%1 documents have been sent to the OCR service.';

    [Internal]
    PROCEDURE SendAllReadyToOcr@1();
    VAR
      IncomingDocument@1001 : Record "Incoming Document";
      IncomingDocumentAttachment@1002 : Record "Incoming Document Attachment";
      TempIncomingDocumentAttachment@1006 : TEMPORARY Record "Incoming Document Attachment";
      OCRServiceMgt@1003 : Codeunit "OCR Service Mgt.";
      IncDocAttsReadyforOCR@1000 : Query "Inc. Doc. Atts. Ready for OCR";
      Window@1008 : Dialog;
      NoOfDocuments@1005 : Integer;
      i@1007 : Integer;
    BEGIN
      if not IncDocAttsReadyforOCR.OPEN then
        exit;  // empty

      Window.OPEN(SendMsg);

      // Find Document Count and lock records
      IncomingDocument.LOCKTABLE;
      IncomingDocumentAttachment.LOCKTABLE;
      while IncDocAttsReadyforOCR.READ do begin
        NoOfDocuments += 1;
        IncomingDocumentAttachment.GET(IncDocAttsReadyforOCR.Incoming_Document_Entry_No,IncDocAttsReadyforOCR.Line_No);
        IncomingDocument.GET(IncomingDocumentAttachment."Incoming Document Entry No.");  // lock
        TempIncomingDocumentAttachment := IncomingDocumentAttachment;
        TempIncomingDocumentAttachment.INSERT;
      end;
      IncDocAttsReadyforOCR.CLOSE;
      // Release locks
      COMMIT;

      if NoOfDocuments = 0 then
        exit;

      OCRServiceMgt.StartUpload(NoOfDocuments);

      TempIncomingDocumentAttachment.FINDSET;
      repeat
        i += 1;
        Window.UPDATE(1,10000 * i div NoOfDocuments);
        IncomingDocument.GET(TempIncomingDocumentAttachment."Incoming Document Entry No.");
        IncomingDocument.SendToOCR(false);
      until TempIncomingDocumentAttachment.NEXT = 0;

      COMMIT;
      Window.CLOSE;
      MESSAGE(SendDoneMsg,NoOfDocuments);
    END;

    BEGIN
    END.
  }
}

