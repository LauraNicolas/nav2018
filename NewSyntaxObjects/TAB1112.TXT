OBJECT Table 1112 Cost Center
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               SETRECFILTER;
               ConfirmDeleteIfEntriesExist(Rec,true);
               SETRANGE(Code);
               SETCURRENTKEY("Sorting Order");
             END;

    CaptionML=ENU=Cost Center;
    LookupPageID="Chart of Cost Centers";
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnLookup=VAR
                                                              CostAccMgt@1000 : Codeunit "Cost Account Mgt";
                                                            BEGIN
                                                              CostAccMgt.LookupCostCenterFromDimValue(Code);
                                                            END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 2   ;   ;Name                ;Text50        ;CaptionML=ENU=Name }
    { 3   ;   ;Cost Subtype        ;Option        ;CaptionML=ENU=Cost Subtype;
                                                   OptionCaptionML=ENU=" ,Service Cost Center,Aux. Cost Center,Main Cost Center";
                                                   OptionString=" ","Service Cost Center","Aux. Cost Center","Main Cost Center" }
    { 4   ;   ;Cost Type Filter    ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Type";
                                                   CaptionML=ENU=Cost Type Filter }
    { 5   ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
    { 6   ;   ;Net Change          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE ("Cost Center Code"=FIELD(Code),
                                                                                              "Cost Center Code"=FIELD(FILTER(Totaling)),
                                                                                              "Cost Type No."=FIELD("Cost Type Filter"),
                                                                                              "Posting Date"=FIELD("Date Filter")));
                                                   CaptionML=ENU=Net Change;
                                                   BlankZero=true;
                                                   Editable=false }
    { 7   ;   ;Balance at Date     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE ("Cost Center Code"=FIELD(Code),
                                                                                              "Cost Center Code"=FIELD(FILTER(Totaling)),
                                                                                              "Cost Type No."=FIELD("Cost Type Filter"),
                                                                                              "Posting Date"=FIELD(UPPERLIMIT("Date Filter"))));
                                                   CaptionML=ENU=Balance at Date;
                                                   BlankZero=true;
                                                   Editable=false }
    { 8   ;   ;Balance to Allocate ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cost Entry".Amount WHERE ("Cost Center Code"=FIELD(Code),
                                                                                              "Cost Center Code"=FIELD(FILTER(Totaling)),
                                                                                              "Cost Type No."=FIELD("Cost Type Filter"),
                                                                                              "Posting Date"=FIELD("Date Filter"),
                                                                                              Allocated=CONST(false)));
                                                   CaptionML=ENU=Balance to Allocate;
                                                   BlankZero=true;
                                                   Editable=false }
    { 9   ;   ;Responsible Person  ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=VAR
                                                                UserMgt@1000 : Codeunit "User Management";
                                                              BEGIN
                                                                UserMgt.ValidateUserID("Responsible Person");
                                                              END;

                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit "User Management";
                                                            BEGIN
                                                              UserMgt.LookupUserID("Responsible Person");
                                                            END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Responsible Person }
    { 10  ;   ;Sorting Order       ;Code10        ;CaptionML=ENU=Sorting Order }
    { 11  ;   ;Comment             ;Text50        ;CaptionML=ENU=Comment }
    { 12  ;   ;Line Type           ;Option        ;OnValidate=BEGIN
                                                                // Change to other type than cost type. Entries exist?
                                                                if (("Line Type" <> "Line Type"::"Cost Center") and
                                                                    (xRec."Line Type" = xRec."Line Type"::"Cost Center")) or
                                                                   (("Line Type" <> "Line Type"::"Begin-Total") and
                                                                    (xRec."Line Type" = xRec."Line Type"::"Begin-Total"))
                                                                then
                                                                  ConfirmModifyIfEntriesExist(Rec);

                                                                if "Line Type" <> "Line Type"::"Cost Center" then begin
                                                                  Blocked := true;
                                                                  "Cost Subtype" := 0;
                                                                end else
                                                                  Totaling := '';
                                                              END;

                                                   CaptionML=ENU=Line Type;
                                                   OptionCaptionML=ENU=Cost Center,Heading,Total,Begin-Total,End-Total;
                                                   OptionString="Cost Center",Heading,Total,"Begin-Total","End-Total" }
    { 13  ;   ;Blocked             ;Boolean       ;CaptionML=ENU=Blocked }
    { 14  ;   ;New Page            ;Boolean       ;CaptionML=ENU=New Page }
    { 15  ;   ;Blank Line          ;Boolean       ;CaptionML=ENU=Blank Line;
                                                   MinValue=false }
    { 16  ;   ;Indentation         ;Integer       ;CaptionML=ENU=Indentation;
                                                   MinValue=0;
                                                   Editable=false }
    { 17  ;   ;Totaling            ;Text250       ;OnValidate=BEGIN
                                                                if not ("Line Type" in ["Line Type"::Total,"Line Type"::"End-Total"]) then
                                                                  FIELDERROR("Line Type");

                                                                CALCFIELDS("Net Change");
                                                              END;

                                                   OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              if LookupCostCenterFilter(SelectionFilter) then
                                                                VALIDATE(Totaling,COPYSTR(SelectionFilter,1,MAXSTRLEN(Totaling)));
                                                            END;

                                                   CaptionML=ENU=Totaling }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=true }
    {    ;"Cost Subtype"                           }
    {    ;"Sorting Order"                          }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Name                                }
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=There are general ledger entries, cost entries, or cost budget entries that are posted to the selected cost center. Are you sure that you want to delete the cost center?';
      Text002@1002 : TextConst 'ENU=There are general ledger entries, cost entries, or cost budget entries that are posted to the selected cost center. Are you sure that you want to modify the cost center?';

    LOCAL PROCEDURE EntriesExist@1(VAR CostCenter@1007 : Record "Cost Center") EntriesFound : Boolean;
    VAR
      CostAccSetup@1000 : Record "Cost Accounting Setup";
      GLEntry@1001 : Record "G/L Entry";
      CostEntry@1002 : Record "Cost Entry";
      CostBudgetEntry@1003 : Record "Cost Budget Entry";
      DimensionMgt@1004 : Codeunit DimensionManagement;
      DimFilter@1005 : Text;
    BEGIN
      CostAccSetup.GET;
      if CostCenter.FINDSET then
        repeat
          DimensionMgt.GetDimSetIDsForFilter(CostAccSetup."Cost Center Dimension",CostCenter.Code);
          DimFilter := DimensionMgt.GetDimSetFilter;
          if DimFilter <> '' then begin
            GLEntry.SETFILTER("Dimension Set ID",DimFilter);
            if GLEntry.FINDFIRST then
              EntriesFound := true;
          end;

          if not EntriesFound then begin
            CostBudgetEntry.SETCURRENTKEY("Budget Name","Cost Center Code");
            CostBudgetEntry.SETRANGE("Cost Center Code",CostCenter.Code);
            EntriesFound := not CostBudgetEntry.ISEMPTY;
          end;

          if not EntriesFound then begin
            CostEntry.SETCURRENTKEY("Cost Center Code");
            CostEntry.SETRANGE("Cost Center Code",CostCenter.Code);
            EntriesFound := not CostEntry.ISEMPTY;
          end;
        until (CostCenter.NEXT = 0) or EntriesFound;
    END;

    [External]
    PROCEDURE ConfirmDeleteIfEntriesExist@3(VAR CostCenter@1000 : Record "Cost Center";CalledFromOnInsert@1001 : Boolean);
    BEGIN
      if EntriesExist(CostCenter) then
        if not CONFIRM(Text001,true) then
          ERROR('');
      if not CalledFromOnInsert then
        CostCenter.DELETEALL;
    END;

    LOCAL PROCEDURE ConfirmModifyIfEntriesExist@4(VAR CostCenter@1000 : Record "Cost Center");
    VAR
      CostCenter2@1001 : Record "Cost Center";
    BEGIN
      CostCenter2 := CostCenter;
      CostCenter2.SETRECFILTER;
      if EntriesExist(CostCenter2) then
        if not CONFIRM(Text002,true) then
          ERROR('');
    END;

    [External]
    PROCEDURE LookupCostCenterFilter@2(VAR Text@1000 : Text) : Boolean;
    VAR
      ChartOfCostCenters@1002 : Page "Chart of Cost Centers";
    BEGIN
      ChartOfCostCenters.LOOKUPMODE(true);
      if ChartOfCostCenters.RUNMODAL = ACTION::LookupOK then begin
        Text := ChartOfCostCenters.GetSelectionFilter;
        exit(true);
      end;
      exit(false)
    END;

    BEGIN
    END.
  }
}

