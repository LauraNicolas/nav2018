OBJECT Page 61 Applied Customer Entries
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Editable=false;
    CaptionML=ENU=Applied Customer Entries;
    SourceTable="Cust. Ledger Entry";
    DataCaptionExpr=Heading;
    PageType=List;
    OnInit=BEGIN
             AmountVisible := true;
           END;

    OnOpenPage=BEGIN
                 RESET;

                 if "Entry No." <> 0 then begin
                   CreateCustLedgEntry := Rec;
                   if CreateCustLedgEntry."Document Type" = 0 then
                     Heading := Text000
                   else
                     Heading := FORMAT(CreateCustLedgEntry."Document Type");
                   Heading := Heading + ' ' + CreateCustLedgEntry."Document No.";

                   FindApplnEntriesDtldtLedgEntry;
                   SETCURRENTKEY("Entry No.");
                   SETRANGE("Entry No.");

                   if CreateCustLedgEntry."Closed by Entry No." <> 0 then begin
                     "Entry No." := CreateCustLedgEntry."Closed by Entry No.";
                     MARK(true);
                   end;

                   SETCURRENTKEY("Closed by Entry No.");
                   SETRANGE("Closed by Entry No.",CreateCustLedgEntry."Entry No.");
                   if FIND('-') then
                     repeat
                       MARK(true);
                     until NEXT = 0;

                   SETCURRENTKEY("Entry No.");
                   SETRANGE("Closed by Entry No.");
                 end;

                 MARKEDONLY(true);

                 ShowAmounts;
               END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      Name=ActionContainer1900000003;
                      ActionContainerType=RelatedInformation }
      { 20      ;1   ;ActionGroup;
                      Name=Ent&ry;
                      CaptionML=ENU=Ent&ry;
                      Image=Entry }
      { 27      ;2   ;Action    ;
                      Name=Reminder/Fin. Charge Entries;
                      CaptionML=ENU=Reminder/Fin. Charge Entries;
                      ToolTipML=ENU=View entries that were created when reminders and finance charge memos were issued.;
                      ApplicationArea=#Advanced;
                      RunObject=Page "Reminder/Fin. Charge Entries";
                      RunPageView=SORTING("Customer Entry No.");
                      RunPageLink="Customer Entry No."=FIELD("Entry No.");
                      Image=Reminder }
      { 32      ;2   ;Action    ;
                      Name=Dimensions;
                      AccessByPermission=TableData Dimension=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=ENU=Dimensions;
                      ToolTipML=ENU=View or edit dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                      ApplicationArea=#Suite;
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                               END;
                                }
      { 35      ;2   ;Action    ;
                      Name=Detailed &Ledger Entries;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=ENU=Detailed &Ledger Entries;
                      ToolTipML=ENU=View a summary of the all posted entries and adjustments related to a specific customer ledger entry.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page "Detailed Cust. Ledg. Entries";
                      RunPageView=SORTING("Cust. Ledger Entry No.","Posting Date");
                      RunPageLink="Cust. Ledger Entry No."=FIELD("Entry No."),
                                  "Customer No."=FIELD("Customer No.");
                      Image=View }
      { 1900000004;0 ;ActionContainer;
                      Name=ActionContainer1900000004;
                      ActionContainerType=ActionItems }
      { 19      ;1   ;Action    ;
                      Name=&Navigate;
                      CaptionML=ENU=&Navigate;
                      ToolTipML=ENU=Find all entries and documents that exist for the document number and posting date on the selected entry or document.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=Navigate;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Navigate.SetDoc("Posting Date","Document No.");
                                 Navigate.RUN;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Name=Posting Date;
                ToolTipML=ENU=Specifies the customer entry's posting date.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Posting Date";
                ImplicitType=Date }

    { 4   ;2   ;Field     ;
                Name=Document Type;
                ToolTipML=ENU=Specifies the document type that the customer entry belongs to.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Document Type";
                ImplicitType=Option }

    { 6   ;2   ;Field     ;
                Name=Document No.;
                ToolTipML=ENU=Specifies the entry's document number.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Document No.";
                ImplicitType=Code20 }

    { 8   ;2   ;Field     ;
                Name=Description;
                ToolTipML=ENU=Specifies a description of the customer entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                ImplicitType=Text50 }

    { 77  ;2   ;Field     ;
                Name=Global Dimension 1 Code;
                ToolTipML=ENU=Specifies the code for the global dimension that is linked to the record or entry for analysis purposes. Two global dimensions, typically for the company's most important activities, are available on all cards, documents, reports, and lists.;
                ApplicationArea=#Suite;
                SourceExpr="Global Dimension 1 Code";
                Visible=FALSE;
                ImplicitType=Code20 }

    { 75  ;2   ;Field     ;
                Name=Global Dimension 2 Code;
                ToolTipML=ENU=Specifies the code for the global dimension that is linked to the record or entry for analysis purposes. Two global dimensions, typically for the company's most important activities, are available on all cards, documents, reports, and lists.;
                ApplicationArea=#Suite;
                SourceExpr="Global Dimension 2 Code";
                Visible=FALSE;
                ImplicitType=Code20 }

    { 73  ;2   ;Field     ;
                Name=Salesperson Code;
                ToolTipML=ENU=Specifies the code for the salesperson whom the entry is linked to.;
                ApplicationArea=#Advanced;
                SourceExpr="Salesperson Code";
                Visible=FALSE;
                ImplicitType=Code20 }

    { 71  ;2   ;Field     ;
                Name=Currency Code;
                ToolTipML=ENU=Specifies the currency code for the amount on the line.;
                ApplicationArea=#Suite;
                SourceExpr="Currency Code";
                ImplicitType=Code10 }

    { 33  ;2   ;Field     ;
                Name=Original Amount;
                ToolTipML=ENU=Specifies the amount of the original entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Original Amount";
                ImplicitType=Decimal }

    { 10  ;2   ;Field     ;
                Name=Amount;
                ToolTipML=ENU=Specifies the amount of the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Amount;
                Visible=AmountVisible;
                ImplicitType=Decimal }

    { 3   ;2   ;Field     ;
                Name=Debit Amount;
                ToolTipML=ENU=Specifies the total of the ledger entries that represent debits.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Debit Amount";
                Visible=DebitCreditVisible;
                ImplicitType=Decimal }

    { 5   ;2   ;Field     ;
                Name=Credit Amount;
                ToolTipML=ENU=Specifies the total of the ledger entries that represent credits.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Credit Amount";
                Visible=DebitCreditVisible;
                ImplicitType=Decimal }

    { 12  ;2   ;Field     ;
                Name=Closed by Amount;
                ToolTipML=ENU=Specifies the amount that the entry was finally applied to (closed) with.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Closed by Amount";
                ImplicitType=Decimal }

    { 28  ;2   ;Field     ;
                Name=Closed by Currency Code;
                ToolTipML=ENU=Specifies the code of the currency of the entry that was applied to (and closed) this customer ledger entry.;
                ApplicationArea=#Advanced;
                SourceExpr="Closed by Currency Code";
                Visible=FALSE;
                ImplicitType=Code10 }

    { 30  ;2   ;Field     ;
                Name=Closed by Currency Amount;
                ToolTipML=ENU=Specifies the amount that was finally applied to (and closed) this customer ledger entry.;
                ApplicationArea=#Advanced;
                SourceExpr="Closed by Currency Amount";
                AutoFormatType=1;
                AutoFormatExpr="Closed by Currency Code";
                Visible=FALSE;
                ImplicitType=Decimal }

    { 25  ;2   ;Field     ;
                Name=User ID;
                ToolTipML=ENU=Specifies the ID of the user who posted the entry, to be used, for example, in the change log.;
                ApplicationArea=#Advanced;
                SourceExpr="User ID";
                Visible=FALSE;
                ImplicitType=Code50 }

    { 23  ;2   ;Field     ;
                Name=Source Code;
                ToolTipML=ENU=Specifies the source code that is linked to the entry.;
                ApplicationArea=#Advanced;
                SourceExpr="Source Code";
                Visible=FALSE;
                ImplicitType=Code10 }

    { 21  ;2   ;Field     ;
                Name=Reason Code;
                ToolTipML=ENU=Specifies the reason code on the entry.;
                ApplicationArea=#Advanced;
                SourceExpr="Reason Code";
                Visible=FALSE;
                ImplicitType=Code10 }

    { 14  ;2   ;Field     ;
                Name=Entry No.;
                ToolTipML=ENU=Specifies the entry number that is assigned to the entry.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Entry No.";
                ImplicitType=Integer }

    { 1900000007;0;Container;
                Name=Container1900000007;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Name=Part1900383207;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Name=Part1905767507;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Document';
      CreateCustLedgEntry@1001 : Record "Cust. Ledger Entry";
      Navigate@1002 : Page Navigate;
      Heading@1003 : Text[50];
      AmountVisible@1005 : Boolean;
      DebitCreditVisible@1004 : Boolean;

    LOCAL PROCEDURE FindApplnEntriesDtldtLedgEntry@1();
    VAR
      DtldCustLedgEntry1@1001 : Record "Detailed Cust. Ledg. Entry";
      DtldCustLedgEntry2@1000 : Record "Detailed Cust. Ledg. Entry";
    BEGIN
      DtldCustLedgEntry1.SETCURRENTKEY("Cust. Ledger Entry No.");
      DtldCustLedgEntry1.SETRANGE("Cust. Ledger Entry No.",CreateCustLedgEntry."Entry No.");
      DtldCustLedgEntry1.SETRANGE(Unapplied,false);
      if DtldCustLedgEntry1.FIND('-') then
        repeat
          if DtldCustLedgEntry1."Cust. Ledger Entry No." =
             DtldCustLedgEntry1."Applied Cust. Ledger Entry No."
          then begin
            DtldCustLedgEntry2.INIT;
            DtldCustLedgEntry2.SETCURRENTKEY("Applied Cust. Ledger Entry No.","Entry Type");
            DtldCustLedgEntry2.SETRANGE(
              "Applied Cust. Ledger Entry No.",DtldCustLedgEntry1."Applied Cust. Ledger Entry No.");
            DtldCustLedgEntry2.SETRANGE("Entry Type",DtldCustLedgEntry2."Entry Type"::Application);
            DtldCustLedgEntry2.SETRANGE(Unapplied,false);
            if DtldCustLedgEntry2.FIND('-') then
              repeat
                if DtldCustLedgEntry2."Cust. Ledger Entry No." <>
                   DtldCustLedgEntry2."Applied Cust. Ledger Entry No."
                then begin
                  SETCURRENTKEY("Entry No.");
                  SETRANGE("Entry No.",DtldCustLedgEntry2."Cust. Ledger Entry No.");
                  if FIND('-') then
                    MARK(true);
                end;
              until DtldCustLedgEntry2.NEXT = 0;
          end else begin
            SETCURRENTKEY("Entry No.");
            SETRANGE("Entry No.",DtldCustLedgEntry1."Applied Cust. Ledger Entry No.");
            if FIND('-') then
              MARK(true);
          end;
        until DtldCustLedgEntry1.NEXT = 0;
    END;

    [External]
    PROCEDURE SetTempCustLedgEntry@2(NewTempCustLedgEntryNo@1102601000 : Integer);
    BEGIN
      if NewTempCustLedgEntryNo <> 0 then begin
        SETRANGE("Entry No.",NewTempCustLedgEntryNo);
        FIND('-');
      end;
    END;

    LOCAL PROCEDURE ShowAmounts@8();
    VAR
      GLSetup@1000 : Record "General Ledger Setup";
    BEGIN
      GLSetup.GET;
      AmountVisible := not (GLSetup."Show Amounts" = GLSetup."Show Amounts"::"Debit/Credit Only");
      DebitCreditVisible := not (GLSetup."Show Amounts" = GLSetup."Show Amounts"::"Amount Only");
    END;

    BEGIN
    END.
  }
}

