OBJECT Codeunit 1373 Batch Posting Print Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Codeunit,1380,OnAfterBatchProcessing)]
    LOCAL PROCEDURE PrintDocumentOnAfterBatchPosting@3(VAR RecRef@1000 : RecordRef;PostingResult@1001 : Boolean);
    VAR
      BatchPostParameterTypes@1002 : Codeunit "Batch Post Parameter Types";
      BatchProcessingMgt@1004 : Codeunit "Batch Processing Mgt.";
      Print@1003 : Boolean;
    BEGIN
      if not PostingResult then
        exit;

      if not BatchProcessingMgt.GetParameterBoolean(RecRef.RECORDID,BatchPostParameterTypes.Print,Print) or not Print then
        exit;

      PrintSalesDocument(RecRef);
      PrintPurchaseDocument(RecRef);
    END;

    LOCAL PROCEDURE PrintSalesDocument@5(RecRef@1000 : RecordRef);
    VAR
      SalesHeader@1001 : Record "Sales Header";
      ReportSelections@1002 : Record "Report Selections";
      SalesInvoiceHeader@1003 : Record "Sales Invoice Header";
      SalesShipmentHeader@1004 : Record "Sales Shipment Header";
      SalesCrMemoHeader@1005 : Record "Sales Cr.Memo Header";
    BEGIN
      if RecRef.NUMBER <> DATABASE::"Sales Header" then
        exit;

      RecRef.SETTABLE(SalesHeader);

      with SalesHeader do
        case "Document Type" of
          "Document Type"::Order:
            begin
              if Ship then begin
                SalesShipmentHeader."No." := "Last Shipping No.";
                SalesShipmentHeader.SETRECFILTER;
                PrintDocument(ReportSelections.Usage::"S.Shipment",SalesShipmentHeader);
              end;
              if Invoice then begin
                SalesInvoiceHeader."No." := "Last Posting No.";
                SalesInvoiceHeader.SETRECFILTER;
                PrintDocument(ReportSelections.Usage::"S.Invoice",SalesInvoiceHeader);
              end;
            end;
          "Document Type"::Invoice:
            begin
              if "Last Posting No." = '' then
                SalesInvoiceHeader."No." := "No."
              else
                SalesInvoiceHeader."No." := "Last Posting No.";
              SalesInvoiceHeader.SETRECFILTER;
              PrintDocument(ReportSelections.Usage::"S.Invoice",SalesInvoiceHeader);
            end;
          "Document Type"::"Credit Memo":
            begin
              if "Last Posting No." = '' then
                SalesCrMemoHeader."No." := "No."
              else
                SalesCrMemoHeader."No." := "Last Posting No.";
              SalesCrMemoHeader.SETRECFILTER;
              PrintDocument(ReportSelections.Usage::"S.Cr.Memo",SalesCrMemoHeader);
            end;
        end;
    END;

    LOCAL PROCEDURE PrintPurchaseDocument@6(RecRef@1000 : RecordRef);
    VAR
      PurchaseHeader@1001 : Record "Purchase Header";
      ReportSelections@1002 : Record "Report Selections";
      PurchRcptHeader@1003 : Record "Purch. Rcpt. Header";
      PurchInvHeader@1004 : Record "Purch. Inv. Header";
      PurchCrMemoHdr@1005 : Record "Purch. Cr. Memo Hdr.";
    BEGIN
      if RecRef.NUMBER <> DATABASE::"Purchase Header" then
        exit;

      RecRef.SETTABLE(PurchaseHeader);

      with PurchaseHeader do
        case "Document Type" of
          "Document Type"::Order:
            begin
              if Receive then begin
                PurchRcptHeader."No." := "Last Receiving No.";
                PurchRcptHeader.SETRECFILTER;
                PrintDocument(ReportSelections.Usage::"P.Receipt",PurchRcptHeader);
              end;
              if Invoice then begin
                PurchInvHeader."No." := "Last Posting No.";
                PurchInvHeader.SETRECFILTER;
                PrintDocument(ReportSelections.Usage::"P.Invoice",PurchInvHeader);
              end;
            end;
          "Document Type"::Invoice:
            begin
              if "Last Posting No." = '' then
                PurchInvHeader."No." := "No."
              else
                PurchInvHeader."No." := "Last Posting No.";
              PurchInvHeader.SETRECFILTER;
              PrintDocument(ReportSelections.Usage::"P.Invoice",PurchInvHeader);
            end;
          "Document Type"::"Credit Memo":
            begin
              if "Last Posting No." = '' then
                PurchCrMemoHdr."No." := "No."
              else
                PurchCrMemoHdr."No." := "Last Posting No.";
              PurchCrMemoHdr.SETRECFILTER;
              PrintDocument(ReportSelections.Usage::"P.Cr.Memo",PurchCrMemoHdr);
            end;
        end;
    END;

    LOCAL PROCEDURE PrintDocument@7(ReportUsage@1000 : Option;RecVar@1001 : Variant);
    VAR
      ReportSelections@1002 : Record "Report Selections";
    BEGIN
      ReportSelections.RESET;
      ReportSelections.SETRANGE(Usage,ReportUsage);
      ReportSelections.FINDSET;
      repeat
        ReportSelections.TESTFIELD("Report ID");
        REPORT.RUN(ReportSelections."Report ID",false,false,RecVar);
      until ReportSelections.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

