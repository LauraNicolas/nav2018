OBJECT Page 2197 O365 Email Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Email Setup;
    SourceTable="SMTP Mail Setup";
    PageType=NavigatePage;
    SourceTableTemporary=true;
    OnOpenPage=VAR
                 SMTPMailSetup@1000 : Record "SMTP Mail Setup";
                 User@1003 : Record User;
                 SMTPMail@1002 : Codeunit "SMTP Mail";
               BEGIN
                 INIT;
                 if SMTPMailSetup.GET then begin
                   TRANSFERFIELDS(SMTPMailSetup);
                   GetEmailProvider(SMTPMailSetup)
                 end else begin
                   SMTPMail.ApplyOffice365Smtp(Rec);
                   EmailProvider := EmailProvider::"Office 365";
                   if User.GET(USERSECURITYID) then
                     "User ID" := User."Authentication Email";
                 end;
                 INSERT;
                 if SMTPMailSetup.HasPassword then
                   Password := DummyPasswordTxt;

                 Step := Step::Start;
                 EnableControls;
               END;

    OnQueryClosePage=BEGIN
                       if CloseAction = ACTION::OK then
                         if not SetupCompleted then
                           if not CONFIRM(NotSetUpQst,false) then
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      Name=ActionContainer10;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=ENU=Back;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Enabled=BackActionEnabled;
                      InFooterBar=true;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(true);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=ENU=Next;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Enabled=NextActionEnabled;
                      InFooterBar=true;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 case Step of
                                   Step::Settings:
                                     if (Authentication = Authentication::Basic) and (("User ID" = '') or (Password = '')) then
                                       ERROR(EmailPasswordMissingErr);
                                 end;

                                 NextStep(false);
                               END;
                                }
      { 11      ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=ENU=Finish;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Enabled=FinishActionEnabled;
                      InFooterBar=true;
                      Image=Approve;
                      OnAction=BEGIN
                                 FinishAction;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                Name=Group20;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 13  ;2   ;Group     ;
                Name=Welcome to Email Setup;
                CaptionML=ENU=Welcome to Email Setup;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 18  ;3   ;Group     ;
                Name=Group18;
                GroupType=Group;
                InstructionalTextML=ENU=Let's set up the account from which invoice emails will be sent. }

    { 2   ;1   ;Group     ;
                Name=Group2;
                Visible=FirstStepVisible OR SettingsStepVisible;
                Editable=FirstStepVisible;
                GroupType=Group }

    { 19  ;2   ;Group     ;
                Name=Group19;
                Visible=NOT SettingsStepVisible;
                Editable=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Choose your email account type }

    { 3   ;2   ;Field     ;
                Name=Email Provider;
                CaptionML=ENU=Email Account type;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=EmailProvider;
                OnValidate=BEGIN
                             case EmailProvider of
                               EmailProvider::"Office 365":
                                 SMTPMail.ApplyOffice365Smtp(Rec);
                               EmailProvider::Outlook:
                                 SMTPMail.ApplyOutlookSmtp(Rec);
                               EmailProvider::Gmail:
                                 SMTPMail.ApplyGmailSmtp(Rec);
                               EmailProvider::Yahoo:
                                 SMTPMail.ApplyYahooSmtp(Rec);
                               EmailProvider::"Other Email Provider":
                                 "SMTP Server" := '';
                             end;
                             AdvancedSettingsVisible := EmailProvider = EmailProvider::"Other Email Provider";
                             EnableControls;
                           END;

                ImplicitType=Option;
                ShowCaption=false }

    { 12  ;1   ;Group     ;
                Name=Group12;
                Visible=SettingsStepVisible;
                GroupType=Group }

    { 27  ;2   ;Group     ;
                Name=Group27;
                Visible=AdvancedSettingsVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Enter the SMTP Server Details. }

    { 9   ;3   ;Field     ;
                Name=Authentication;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Authentication;
                OnValidate=BEGIN
                             EnableControls;
                           END;

                ImplicitType=Option }

    { 8   ;3   ;Field     ;
                Name=SMTP Server;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="SMTP Server";
                OnValidate=BEGIN
                             EnableControls;
                           END;

                ImplicitType=Text250;
                ShowMandatory=TRUE }

    { 7   ;3   ;Field     ;
                Name=SMTP Server Port;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="SMTP Server Port";
                ImplicitType=Integer }

    { 6   ;3   ;Field     ;
                Name=Secure Connection;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Secure Connection";
                ImplicitType=Boolean }

    { 26  ;2   ;Group     ;
                Name=Group26;
                Visible=MailSettingsVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Enter the credentials for the account, which will be used for sending the invoice emails. }

    { 5   ;3   ;Field     ;
                Name=User ID;
                ExtendedDatatype=E-Mail;
                CaptionML=ENU=Email;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="User ID";
                OnValidate=BEGIN
                             EnableControls;
                           END;

                ImplicitType=Text250 }

    { 4   ;3   ;Field     ;
                Name=Password;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Password;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Password;
                OnValidate=BEGIN
                             EnableControls;
                           END;

                ImplicitType=Text250 }

    { 17  ;1   ;Group     ;
                Name=Group17;
                Visible=FinalStepVisible;
                GroupType=Group }

    { 23  ;2   ;Group     ;
                Name=That's it!;
                CaptionML=ENU=That's it!;
                GroupType=Group;
                InstructionalTextML=ENU=If you want to verify that the specified email setup works, click on Send test email. }

    { 16  ;3   ;Field     ;
                Name=TestEmailLbl;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=TestEmailLbl;
                Editable=FALSE;
                Style=StandardAccent;
                StyleExpr=TRUE;
                OnDrillDown=BEGIN
                              SendTestEmailAction;
                            END;

                ImplicitType=TextConst;
                ShowCaption=false }

  }
  CODE
  {
    VAR
      SMTPMail@1008 : Codeunit "SMTP Mail";
      FirstStepVisible@1000 : Boolean;
      SettingsStepVisible@1011 : Boolean;
      AdvancedSettingsVisible@1003 : Boolean;
      MailSettingsVisible@1002 : Boolean;
      FinalStepVisible@1015 : Boolean;
      FinishActionEnabled@1009 : Boolean;
      BackActionEnabled@1013 : Boolean;
      NextActionEnabled@1014 : Boolean;
      NotSetUpQst@1004 : TextConst 'ENU=Email has not been set up.\\Are you sure you want to exit?';
      EmailPasswordMissingErr@1006 : TextConst 'ENU=Please enter a valid email address and password.';
      SetupCompleted@1012 : Boolean;
      Password@1007 : Text[250];
      DummyPasswordTxt@1017 : TextConst '@@@={Locked};ENU=***';
      Step@1010 : 'Start,Settings,Finish';
      EmailProvider@1001 : '"Office 365",Outlook,Gmail,Yahoo,"Other Email Provider"';
      TestEmailLbl@1005 : TextConst 'ENU=Send test email';

    LOCAL PROCEDURE EnableControls@2();
    BEGIN
      ResetControls;

      case Step of
        Step::Start:
          ShowStartStep;
        Step::Settings:
          ShowSettingsStep;
        Step::Finish:
          ShowFinishStep;
      end;
    END;

    LOCAL PROCEDURE StoreSMTPSetup@31();
    VAR
      SMTPMailSetup@1000 : Record "SMTP Mail Setup";
    BEGIN
      if not SMTPMailSetup.GET then begin
        SMTPMailSetup.INIT;
        SMTPMailSetup.INSERT;
      end;

      SMTPMailSetup.TRANSFERFIELDS(Rec,false);
      if Password <> DummyPasswordTxt then
        SMTPMailSetup.SetPassword(Password);
      SMTPMailSetup.MODIFY(true);
      COMMIT;
    END;

    LOCAL PROCEDURE SendTestEmailAction@10();
    BEGIN
      StoreSMTPSetup;
      CODEUNIT.RUN(CODEUNIT::"SMTP Test Mail");
    END;

    LOCAL PROCEDURE FinishAction@12();
    BEGIN
      StoreSMTPSetup;
      SetupCompleted := true;
      CurrPage.CLOSE;
    END;

    LOCAL PROCEDURE NextStep@41(Backwards@1000 : Boolean);
    BEGIN
      if Backwards then
        Step := Step - 1
      else
        Step := Step + 1;

      EnableControls;
    END;

    LOCAL PROCEDURE ShowStartStep@24();
    BEGIN
      FirstStepVisible := true;
      FinishActionEnabled := false;
      BackActionEnabled := false;
    END;

    LOCAL PROCEDURE ShowSettingsStep@34();
    BEGIN
      SettingsStepVisible := true;
      AdvancedSettingsVisible := EmailProvider = EmailProvider::"Other Email Provider";
      MailSettingsVisible := Authentication = Authentication::Basic;
      NextActionEnabled := "SMTP Server" <> '';
    END;

    LOCAL PROCEDURE ShowFinishStep@35();
    BEGIN
      FinalStepVisible := true;
      NextActionEnabled := false;
      FinishActionEnabled := true;
    END;

    LOCAL PROCEDURE ResetControls@1();
    BEGIN
      FinishActionEnabled := false;
      BackActionEnabled := true;
      NextActionEnabled := true;

      FirstStepVisible := false;
      SettingsStepVisible := false;
      FinalStepVisible := false;
    END;

    LOCAL PROCEDURE GetEmailProvider@11(VAR SMTPMailSetup@1000 : Record "SMTP Mail Setup");
    BEGIN
      if SMTPMail.IsOffice365Setup(SMTPMailSetup) then
        EmailProvider := EmailProvider::"Office 365"
      else
        if SMTPMail.IsOutlookSetup(SMTPMailSetup) then
          EmailProvider := EmailProvider::Outlook
        else
          if SMTPMail.IsGmailSetup(SMTPMailSetup) then
            EmailProvider := EmailProvider::Gmail
          else
            if SMTPMail.IsYahooSetup(SMTPMailSetup) then
              EmailProvider := EmailProvider::Yahoo
            else
              EmailProvider := EmailProvider::"Other Email Provider";
    END;

    BEGIN
    END.
  }
}

