OBJECT Codeunit 5652 Insurance Jnl.-Post Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Insurance Journal Line";
    Permissions=TableData "Ins. Coverage Ledger Entry"=rimd,
                TableData "Insurance Register"=rimd;
    OnRun=BEGIN
            GLSetup.GET;
            RunWithCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record "General Ledger Setup";
      FA@1001 : Record "Fixed Asset";
      Insurance@1002 : Record Insurance;
      InsuranceJnlLine@1003 : Record "Insurance Journal Line";
      InsCoverageLedgEntry@1004 : Record "Ins. Coverage Ledger Entry";
      InsCoverageLedgEntry2@1005 : Record "Ins. Coverage Ledger Entry";
      InsuranceReg@1006 : Record "Insurance Register";
      InsuranceJnlCheckLine@1008 : Codeunit "Insurance Jnl.-Check Line";
      MakeInsCoverageLedgEntry@1009 : Codeunit "Make Ins. Coverage Ledg. Entry";
      NextEntryNo@1011 : Integer;

    [External]
    PROCEDURE RunWithCheck@3(VAR InsuranceJnlLine2@1000 : Record "Insurance Journal Line");
    BEGIN
      InsuranceJnlLine.COPY(InsuranceJnlLine2);
      Code(true);
      InsuranceJnlLine2 := InsuranceJnlLine;
    END;

    [External]
    PROCEDURE RunWithOutCheck@1(VAR InsuranceJnlLine2@1000 : Record "Insurance Journal Line");
    BEGIN
      InsuranceJnlLine.COPY(InsuranceJnlLine2);
      Code(false);
      InsuranceJnlLine2 := InsuranceJnlLine;
    END;

    LOCAL PROCEDURE Code@2(CheckLine@1000 : Boolean);
    BEGIN
      with InsuranceJnlLine do begin
        if "Insurance No." = '' then
          exit;
        if CheckLine then
          InsuranceJnlCheckLine.RunCheck(InsuranceJnlLine);
        Insurance.GET("Insurance No.");
        Insurance.TESTFIELD(Blocked,false);
        FA.GET("FA No.");
        FA.TESTFIELD("Budgeted Asset",false);
        FA.TESTFIELD(Blocked,false);
        FA.TESTFIELD(Inactive,false);
        MakeInsCoverageLedgEntry.CopyFromJnlLine(InsCoverageLedgEntry,InsuranceJnlLine);
        MakeInsCoverageLedgEntry.CopyFromInsuranceCard(InsCoverageLedgEntry,Insurance);
      end;
      if NextEntryNo = 0 then begin
        InsCoverageLedgEntry.LOCKTABLE;
        if InsCoverageLedgEntry2.FINDLAST then
          NextEntryNo := InsCoverageLedgEntry2."Entry No.";
        InsuranceReg.LOCKTABLE;
        if InsuranceReg.FINDLAST then
          InsuranceReg."No." := InsuranceReg."No." + 1
        else
          InsuranceReg."No." := 1;
        InsuranceReg.INIT;
        InsuranceReg."From Entry No." := NextEntryNo + 1;
        InsuranceReg."Creation Date" := TODAY;
        InsuranceReg."Source Code" := InsuranceJnlLine."Source Code";
        InsuranceReg."Journal Batch Name" := InsuranceJnlLine."Journal Batch Name";
        InsuranceReg."User ID" := USERID;
      end;
      NextEntryNo := NextEntryNo + 1;
      InsCoverageLedgEntry."Entry No." := NextEntryNo;
      InsCoverageLedgEntry."Dimension Set ID" := InsuranceJnlLine."Dimension Set ID";
      InsCoverageLedgEntry.INSERT;
      if InsuranceReg."To Entry No." = 0 then begin
        InsuranceReg."To Entry No." := NextEntryNo;
        InsuranceReg.INSERT;
      end else begin
        InsuranceReg."To Entry No." := NextEntryNo;
        InsuranceReg.MODIFY;
      end;
    END;

    BEGIN
    END.
  }
}

