OBJECT Table 1523 Workflow Step Argument
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Workflow Step Argument"=rim;
    DataCaptionFields="General Journal Template Name","General Journal Batch Name","Notification User ID";
    OnInsert=BEGIN
               ID := CREATEGUID;
             END;

    OnModify=BEGIN
               CheckEditingIsAllowed;
             END;

    OnDelete=BEGIN
               CheckEditingIsAllowed;
             END;

    OnRename=BEGIN
               CheckEditingIsAllowed;
             END;

    CaptionML=ENU=Workflow Step Argument;
    LookupPageID="Workflow Response Options";
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;GUID          ;CaptionML=ENU=ID }
    { 2   ;   ;Type                ;Option        ;TableRelation="Workflow Step".Type WHERE (Argument=FIELD(ID));
                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Event,Response;
                                                   OptionString=Event,Response }
    { 3   ;   ;General Journal Template Name;Code10;
                                                   TableRelation="Gen. Journal Template".Name;
                                                   CaptionML=ENU=General Journal Template Name }
    { 4   ;   ;General Journal Batch Name;Code10  ;TableRelation="Gen. Journal Batch".Name WHERE ("Journal Template Name"=FIELD("General Journal Template Name"));
                                                   CaptionML=ENU=General Journal Batch Name }
    { 5   ;   ;Notification User ID;Code50        ;TableRelation="User Setup"."User ID";
                                                   CaptionML=ENU=Notification User ID }
    { 6   ;   ;Notification User License Type;Option;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."License Type" WHERE ("User Name"=FIELD("Notification User ID")));
                                                   CaptionML=ENU=Notification User License Type;
                                                   OptionCaptionML=ENU=Full User,Limited User,Device Only User,Windows Group,External User;
                                                   OptionString="Full User","Limited User","Device Only User","Windows Group","External User" }
    { 7   ;   ;Response Function Name;Code128     ;TableRelation="Workflow Response"."Function Name";
                                                   CaptionML=ENU=Response Function Name }
    { 9   ;   ;Link Target Page    ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Page));
                                                   CaptionML=ENU=Link Target Page }
    { 10  ;   ;Custom Link         ;Text250       ;OnValidate=VAR
                                                                WebRequestHelper@1000 : Codeunit "Web Request Helper";
                                                              BEGIN
                                                                if "Custom Link" <> '' then
                                                                  WebRequestHelper.IsValidUri("Custom Link");
                                                              END;

                                                   ExtendedDatatype=URL;
                                                   CaptionML=ENU=Custom Link }
    { 11  ;   ;Event Conditions    ;BLOB          ;CaptionML=ENU=Event Conditions }
    { 12  ;   ;Approver Type       ;Option        ;CaptionML=ENU=Approver Type;
                                                   OptionCaptionML=ENU=Salesperson/Purchaser,Approver,Workflow User Group;
                                                   OptionString="Salesperson/Purchaser",Approver,"Workflow User Group" }
    { 13  ;   ;Approver Limit Type ;Option        ;CaptionML=ENU=Approver Limit Type;
                                                   OptionCaptionML=ENU=Approver Chain,Direct Approver,First Qualified Approver,Specific Approver;
                                                   OptionString="Approver Chain","Direct Approver","First Qualified Approver","Specific Approver" }
    { 14  ;   ;Workflow User Group Code;Code20    ;TableRelation="Workflow User Group".Code;
                                                   CaptionML=ENU=Workflow User Group Code }
    { 15  ;   ;Due Date Formula    ;DateFormula   ;OnValidate=BEGIN
                                                                if COPYSTR(FORMAT("Due Date Formula"),1,1) = '-' then
                                                                  ERROR(STRSUBSTNO(NoNegValuesErr,FIELDCAPTION("Due Date Formula")));
                                                              END;

                                                   CaptionML=ENU=Due Date Formula }
    { 16  ;   ;Message             ;Text250       ;CaptionML=ENU=Message }
    { 17  ;   ;Delegate After      ;Option        ;CaptionML=ENU=Delegate After;
                                                   OptionCaptionML=ENU=Never,1 day,2 days,5 days;
                                                   OptionString=Never,"1 day","2 days","5 days" }
    { 18  ;   ;Show Confirmation Message;Boolean  ;CaptionML=ENU=Show Confirmation Message }
    { 19  ;   ;Table No.           ;Integer       ;CaptionML=ENU=Table No. }
    { 20  ;   ;Field No.           ;Integer       ;CaptionML=ENU=Field No. }
    { 21  ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD("Table No."),
                                                                                                   "No."=FIELD("Field No.")));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=false }
    { 22  ;   ;Approver User ID    ;Code50        ;TableRelation="User Setup"."User ID";
                                                   OnLookup=VAR
                                                              UserSetup@1000 : Record "User Setup";
                                                            BEGIN
                                                              if PAGE.RUNMODAL(PAGE::"Approval User Setup",UserSetup) = ACTION::LookupOK then
                                                                VALIDATE("Approver User ID",UserSetup."User ID");
                                                            END;

                                                   CaptionML=ENU=Approver User ID }
    { 23  ;   ;Response Type       ;Option        ;CaptionML=ENU=Response Type;
                                                   OptionCaptionML=ENU=Not Expected,User ID;
                                                   OptionString="Not Expected","User ID" }
    { 24  ;   ;Response User ID    ;Code50        ;TableRelation="User Setup"."User ID";
                                                   OnLookup=VAR
                                                              UserSetup@1000 : Record "User Setup";
                                                            BEGIN
                                                              if PAGE.RUNMODAL(PAGE::"User Setup",UserSetup) = ACTION::LookupOK then
                                                                VALIDATE("Response User ID",UserSetup."User ID");
                                                            END;

                                                   CaptionML=ENU=Response User ID }
    { 100 ;   ;Response Option Group;Code20       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Workflow Response"."Response Option Group" WHERE ("Function Name"=FIELD("Response Function Name")));
                                                   CaptionML=ENU=Response Option Group;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoNegValuesErr@1000 : TextConst 'ENU=%1 must be a positive value.';

    [External]
    PROCEDURE Clone@1() : GUID;
    VAR
      WorkflowStepArgument@1001 : Record "Workflow Step Argument";
    BEGIN
      CALCFIELDS("Event Conditions");
      WorkflowStepArgument.COPY(Rec);
      WorkflowStepArgument.INSERT(true);
      exit(WorkflowStepArgument.ID);
    END;

    [Internal]
    PROCEDURE Equals@9(WorkflowStepArgument@1000 : Record "Workflow Step Argument";SkipBlob@1004 : Boolean) : Boolean;
    VAR
      TypeHelper@1003 : Codeunit "Type Helper";
      OtherRecRef@1002 : RecordRef;
      ThisRecRef@1001 : RecordRef;
    BEGIN
      ThisRecRef.GETTABLE(Rec);
      OtherRecRef.GETTABLE(WorkflowStepArgument);

      if not TypeHelper.Equals(ThisRecRef,OtherRecRef,SkipBlob) then
        exit(false);

      exit(true);
    END;

    [External]
    PROCEDURE GetEventFilters@8() Filters : Text;
    VAR
      FiltersInStream@1002 : InStream;
    BEGIN
      if "Event Conditions".HASVALUE then begin
        CALCFIELDS("Event Conditions");
        "Event Conditions".CREATEINSTREAM(FiltersInStream);
        FiltersInStream.READ(Filters);
      end;
    END;

    [External]
    PROCEDURE SetEventFilters@2(Filters@1000 : Text);
    VAR
      FiltersOutStream@1001 : OutStream;
    BEGIN
      "Event Conditions".CREATEOUTSTREAM(FiltersOutStream);
      FiltersOutStream.WRITE(Filters);
      MODIFY(true);
    END;

    LOCAL PROCEDURE CheckEditingIsAllowed@12();
    VAR
      Workflow@1000 : Record Workflow;
      WorkflowStep@1001 : Record "Workflow Step";
    BEGIN
      if ISNULLGUID(ID) then
        exit;

      WorkflowStep.SETRANGE(Argument,ID);
      if WorkflowStep.FINDFIRST then begin
        Workflow.GET(WorkflowStep."Workflow Code");
        Workflow.CheckEditingIsAllowed;
      end;
    END;

    [External]
    PROCEDURE HideExternalUsers@5();
    VAR
      PermissionManager@1001 : Codeunit "Permission Manager";
      OriginalFilterGroup@1000 : Integer;
    BEGIN
      if not PermissionManager.SoftwareAsAService then
        exit;

      OriginalFilterGroup := FILTERGROUP;
      FILTERGROUP := 2;
      CALCFIELDS("Notification User License Type");
      SETFILTER("Notification User License Type",'<>%1',"Notification User License Type"::"External User");
      FILTERGROUP := OriginalFilterGroup;
    END;

    BEGIN
    END.
  }
}

