OBJECT Codeunit 272 Res. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="Res. Journal Line";
    OnRun=BEGIN
            ResJnlLine.COPY(Rec);
            Code;
            COPY(ResJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU="The journal lines were successfully posted. "';
      Text005@1005 : TextConst 'ENU=You are now in the %1 journal.';
      ResJnlTemplate@1006 : Record "Res. Journal Template";
      ResJnlLine@1007 : Record "Res. Journal Line";
      ResReg@1008 : Record "Resource Register";
      TempJnlBatchName@1010 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      with ResJnlLine do begin
        ResJnlTemplate.GET("Journal Template Name");
        ResJnlTemplate.TESTFIELD("Posting Report ID");
        if ResJnlTemplate.Recurring and (GETFILTER("Posting Date") <> '') then
          FIELDERROR("Posting Date",Text000);

        if not CONFIRM(Text001) then
          exit;

        TempJnlBatchName := "Journal Batch Name";

        CODEUNIT.RUN(CODEUNIT::"Res. Jnl.-Post Batch",ResJnlLine);

        if ResReg.GET("Line No.") then begin
          ResReg.SETRECFILTER;
          REPORT.RUN(ResJnlTemplate."Posting Report ID",false,false,ResReg);
        end;

        if "Line No." = 0 then
          MESSAGE(Text002)
        else
          if TempJnlBatchName = "Journal Batch Name" then
            MESSAGE(Text003)
          else
            MESSAGE(
              Text004 +
              Text005,
              "Journal Batch Name");

        if not FIND('=><') or (TempJnlBatchName <> "Journal Batch Name") then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

