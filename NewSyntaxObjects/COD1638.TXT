OBJECT Codeunit 1638 Office Jobs Handler
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Office Add-in Context";
    OnRun=BEGIN
            RedirectOfficeJobJournal(Rec);
          END;

  }
  CODE
  {
    VAR
      JobsRegExTxt@1002 : TextConst '@@@={Locked};ENU=([^:]+):([^:]+):([0-9]+)';
      UnableToFindJobErr@1000 : TextConst '@@@="%1 = Job No; %2 = Job Task No; %3 = Job Planning Line";ENU=Cannot find job number %1, job task number %2, line number %3.';

    [External]
    PROCEDURE IsJobsHostType@1(OfficeAddinContext@1000 : Record "Office Add-in Context") IsJob : Boolean;
    VAR
      RegEx@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Regex";
      Match@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Match";
    BEGIN
      if OfficeAddinContext.IsAppointment and (OfficeAddinContext.Subject <> '') then begin
        Match := RegEx.Match(OfficeAddinContext.Subject,JobsRegExTxt);
        IsJob := Match.Success;
      end;
    END;

    [Internal]
    PROCEDURE GetJobProperties@8(OfficeAddinContext@1005 : Record "Office Add-in Context";VAR JobNo@1000 : Text;VAR JobTaskNo@1001 : Text;VAR JobPlanningLineNo@1002 : Integer);
    VAR
      RegEx@1004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Regex";
      Match@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Match";
    BEGIN
      Match := RegEx.Match(OfficeAddinContext.Subject,JobsRegExTxt);

      if Match.Success then begin
        JobNo := Match.Groups.Item(1).Value;
        JobTaskNo := Match.Groups.Item(2).Value;
        EVALUATE(JobPlanningLineNo,Match.Groups.Item(3).Value);
      end;
    END;

    [External]
    PROCEDURE SubmitJobPlanningLine@2(JobPlanningLine@1000 : Record "Job Planning Line";JobJournalTemplateName@1004 : Code[10];JobJournalBatchName@1003 : Code[10]);
    VAR
      JobJournalLine@1005 : Record "Job Journal Line";
      TempOfficeJobJournal@1001 : TEMPORARY Record "Office Job Journal";
      JobTransferLine@1002 : Codeunit "Job Transfer Line";
    BEGIN
      SetJobJournalRange(JobJournalLine,JobPlanningLine);

      JobTransferLine.FromPlanningLineToJnlLine(
        JobPlanningLine,JobPlanningLine."Planning Date",JobJournalTemplateName,JobJournalBatchName,JobJournalLine);

      JobPlanningLine.FIND;
      TempOfficeJobJournal.Initialize(JobPlanningLine);
      PAGE.RUN(PAGE::"Office Job Journal",TempOfficeJobJournal);
    END;

    [External]
    PROCEDURE SetJobJournalRange@5(VAR JobJournalLine@1002 : Record "Job Journal Line";JobPlanningLine@1000 : Record "Job Planning Line");
    BEGIN
      JobJournalLine.SETRANGE("Job No.",JobPlanningLine."Job No.");
      JobJournalLine.SETRANGE("Job Task No.",JobPlanningLine."Job Task No.");
      JobJournalLine.SETRANGE("Job Planning Line No.",JobPlanningLine."Line No.");
    END;

    LOCAL PROCEDURE RedirectOfficeJobJournal@6(OfficeAddinContext@1003 : Record "Office Add-in Context");
    VAR
      TempOfficeJobJournal@1010 : TEMPORARY Record "Office Job Journal";
      JobPlanningLine@1004 : Record "Job Planning Line";
      OfficeErrorEngine@1013 : Codeunit "Office Error Engine";
      JobNo@1002 : Text;
      JobTaskNo@1001 : Text;
      JobPlanningLineNo@1000 : Integer;
    BEGIN
      GetJobProperties(OfficeAddinContext,JobNo,JobTaskNo,JobPlanningLineNo);

      if JobPlanningLine.GET(JobNo,JobTaskNo,JobPlanningLineNo) then begin
        TempOfficeJobJournal.Initialize(JobPlanningLine);
        PAGE.RUN(PAGE::"Office Job Journal",TempOfficeJobJournal);
      end else
        OfficeErrorEngine.ShowError(STRSUBSTNO(UnableToFindJobErr,JobNo,JobTaskNo,JobPlanningLineNo));
    END;

    BEGIN
    END.
  }
}

