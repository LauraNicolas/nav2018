OBJECT Table 9002 User Group Access Control
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataPerCompany=false;
    Permissions=TableData "Access Control"=rimd;
    CaptionML=ENU=User Group Access Control;
  }
  FIELDS
  {
    { 1   ;   ;User Group Code     ;Code20        ;TableRelation="User Group";
                                                   CaptionML=ENU=User Group Code }
    { 2   ;   ;User Security ID    ;GUID          ;TableRelation=User;
                                                   CaptionML=ENU=User Security ID }
    { 3   ;   ;Role ID             ;Code20        ;TableRelation="Permission Set";
                                                   CaptionML=ENU=Role ID;
                                                   Editable=false }
    { 4   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company Name }
    { 5   ;   ;User Name           ;Code50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."User Name" WHERE ("User Security ID"=FIELD("User Security ID")));
                                                   CaptionML=ENU=User Name;
                                                   Editable=false }
    { 6   ;   ;Scope               ;Option        ;CaptionML=ENU=Scope;
                                                   OptionCaptionML=ENU=System,Tenant;
                                                   OptionString=System,Tenant }
    { 7   ;   ;App ID              ;GUID          ;CaptionML=ENU=App ID }
  }
  KEYS
  {
    {    ;"User Group Code","User Security ID","Role ID","Company Name",Scope,"App ID";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE AddUserGroupMember@1(UserGroupCode@1000 : Code[20];UserSecurityID@1001 : GUID;SelectedCompany@1002 : Text[30]);
    VAR
      UserGroupPermissionSet@1003 : Record "User Group Permission Set";
    BEGIN
      UserGroupPermissionSet.SETRANGE("User Group Code",UserGroupCode);
      if UserGroupPermissionSet.FINDSET then
        repeat
          AddPermissionSetToUser(
            UserGroupCode,UserSecurityID,SelectedCompany,UserGroupPermissionSet."Role ID",UserGroupPermissionSet."App ID",
            UserGroupPermissionSet.Scope);
        until UserGroupPermissionSet.NEXT = 0;
    END;

    [External]
    PROCEDURE RemoveUserGroupMember@2(UserGroupCode@1000 : Code[20];UserSecurityID@1001 : GUID;SelectedCompany@1002 : Text[30]);
    VAR
      UserGroupPermissionSet@1003 : Record "User Group Permission Set";
    BEGIN
      UserGroupPermissionSet.SETRANGE("User Group Code",UserGroupCode);
      if UserGroupPermissionSet.FINDSET then
        repeat
          RemovePermissionSetFromUser(
            UserGroupCode,UserSecurityID,SelectedCompany,UserGroupPermissionSet."Role ID",UserGroupPermissionSet."App ID",
            UserGroupPermissionSet.Scope);
        until UserGroupPermissionSet.NEXT = 0;
    END;

    [External]
    PROCEDURE AddUserGroupPermissionSet@3(UserGroupCode@1000 : Code[20];RoleID@1001 : Code[20];AppID@1002 : GUID;ItemScope@1004 : Integer);
    VAR
      UserGroupMember@1003 : Record "User Group Member";
    BEGIN
      UserGroupMember.SETRANGE("User Group Code",UserGroupCode);
      if UserGroupMember.FINDSET then
        repeat
          AddPermissionSetToUser(
            UserGroupCode,UserGroupMember."User Security ID",UserGroupMember."Company Name",RoleID,AppID,ItemScope);
        until UserGroupMember.NEXT = 0;
    END;

    [External]
    PROCEDURE RemoveUserGroupPermissionSet@5(UserGroupCode@1000 : Code[20];RoleID@1001 : Code[20];AppID@1003 : GUID;ItemScope@1004 : Integer);
    VAR
      UserGroupMember@1002 : Record "User Group Member";
    BEGIN
      UserGroupMember.SETRANGE("User Group Code",UserGroupCode);
      if UserGroupMember.FINDSET then
        repeat
          RemovePermissionSetFromUser(
            UserGroupCode,UserGroupMember."User Security ID",UserGroupMember."Company Name",RoleID,AppID,ItemScope);
        until UserGroupMember.NEXT = 0;
    END;

    [External]
    PROCEDURE AddPermissionSetToUser@4(UserGroupCode@1000 : Code[20];UserSecurityID@1001 : GUID;SelectedCompany@1002 : Text[30];RoleID@1004 : Code[20];AppID@1006 : GUID;ItemScope@1009 : Integer);
    VAR
      AccessControl@1003 : Record "Access Control";
      AccessControlExists@1005 : Boolean;
    BEGIN
      // If this is the first assignment via a user group and the user already had a manually defined access control,
      // we add a 'null' record for it.
      if GET(UserGroupCode,UserSecurityID,RoleID,SelectedCompany,ItemScope,AppID) then
        exit;
      AccessControlExists := AccessControl.GET(UserSecurityID,RoleID,SelectedCompany,ItemScope,AppID);
      RESET;
      INIT;
      "User Group Code" := '';
      "User Security ID" := UserSecurityID;
      "Role ID" := RoleID;
      "Company Name" := SelectedCompany;
      "App ID" := AppID;
      Scope := ItemScope;
      if AccessControlExists then begin
        SETRANGE("User Security ID",UserSecurityID);
        SETRANGE("Role ID",RoleID);
        SETRANGE("Company Name",SelectedCompany);
        SETRANGE(Scope,ItemScope);
        SETRANGE("App ID",AppID);
        if ISEMPTY then
          INSERT;
      end;
      "User Group Code" := UserGroupCode;
      INSERT;
      if not AccessControlExists then begin
        AccessControl.INIT;
        AccessControl."User Security ID" := UserSecurityID;
        AccessControl."Role ID" := RoleID;
        AccessControl."Company Name" := SelectedCompany;
        AccessControl.Scope := ItemScope;
        AccessControl."App ID" := AppID;
        AccessControl.INSERT;
      end;
    END;

    [External]
    PROCEDURE RemovePermissionSetFromUser@8(UserGroupCode@1000 : Code[20];UserSecurityID@1001 : GUID;SelectedCompany@1002 : Text[30];RoleID@1004 : Code[20];AppID@1007 : GUID;ItemScope@1009 : Integer);
    VAR
      AccessControl@1003 : Record "Access Control";
      AccessControlExists@1005 : Boolean;
      ReferenceExists@1006 : Boolean;
    BEGIN
      // If this is the last assignment via a user group and the user does not have a manually defined access control,
      // we remove the 'null' record for it if it exists.
      if not GET(UserGroupCode,UserSecurityID,RoleID,SelectedCompany,ItemScope,AppID) then
        exit;
      DELETE;
      AccessControlExists := AccessControl.GET(UserSecurityID,RoleID,SelectedCompany,ItemScope,AppID);
      if AccessControlExists then begin
        RESET;
        SETRANGE("User Security ID",UserSecurityID);
        SETRANGE("Role ID",RoleID);
        SETRANGE("Company Name",SelectedCompany);
        SETRANGE(Scope,ItemScope);
        SETRANGE("App ID",AppID);
        ReferenceExists := FINDLAST;
        if not ReferenceExists then
          AccessControl.DELETE;
        if ReferenceExists and ("User Group Code" = '') then
          DELETE;
      end;
    END;

    BEGIN
    END.
  }
}

