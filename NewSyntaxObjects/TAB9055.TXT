OBJECT Table 9055 Purchase Cue
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Purchase Cue;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;To Send or Confirm  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE ("Document Type"=FILTER(Order),
                                                                                              Status=FILTER(Open),
                                                                                              "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   CaptionML=ENU=To Send or Confirm;
                                                   Editable=false }
    { 3   ;   ;Upcoming Orders     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE ("Document Type"=FILTER(Order),
                                                                                              Status=FILTER(Released),
                                                                                              "Expected Receipt Date"=FIELD("Date Filter"),
                                                                                              "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   AccessByPermission=TableData "Purch. Rcpt. Header"=R;
                                                   CaptionML=ENU=Upcoming Orders;
                                                   Editable=false }
    { 4   ;   ;Outstanding Purchase Orders;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE ("Document Type"=FILTER(Order),
                                                                                              Status=FILTER(Released),
                                                                                              "Completely Received"=FILTER(false),
                                                                                              "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   AccessByPermission=TableData "Purch. Rcpt. Header"=R;
                                                   CaptionML=ENU=Outstanding Purchase Orders;
                                                   Editable=false }
    { 5   ;   ;Purchase Return Orders - All;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE ("Document Type"=FILTER("Return Order"),
                                                                                              "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   AccessByPermission=TableData "Return Shipment Header"=R;
                                                   CaptionML=ENU=Purchase Return Orders - All;
                                                   Editable=false }
    { 6   ;   ;Not Invoiced        ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE ("Document Type"=FILTER(Order),
                                                                                              "Completely Received"=FILTER(true),
                                                                                              Invoice=FILTER(false),
                                                                                              "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   AccessByPermission=TableData "Purch. Rcpt. Header"=R;
                                                   CaptionML=ENU=Not Invoiced;
                                                   Editable=false }
    { 7   ;   ;Partially Invoiced  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE ("Document Type"=FILTER(Order),
                                                                                              "Completely Received"=FILTER(true),
                                                                                              Invoice=FILTER(true),
                                                                                              "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   AccessByPermission=TableData "Purch. Rcpt. Header"=R;
                                                   CaptionML=ENU=Partially Invoiced;
                                                   Editable=false }
    { 20  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter;
                                                   Editable=false }
    { 21  ;   ;Responsibility Center Filter;Code10;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Responsibility Center Filter;
                                                   Editable=false }
    { 22  ;   ;User ID Filter      ;Code50        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=User ID Filter }
    { 23  ;   ;Pending Tasks       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("User Task" WHERE ("Assigned To User Name"=FIELD("User ID Filter"),
                                                                                        "Percent Complete"=FILTER(<>100)));
                                                   CaptionML=ENU=Pending Tasks }
  }
  KEYS
  {
    {    ;"Primary Key"                           ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE SetRespCenterFilter@1();
    VAR
      UserSetupMgt@1000 : Codeunit "User Setup Management";
      RespCenterCode@1001 : Code[10];
    BEGIN
      RespCenterCode := UserSetupMgt.GetPurchasesFilter;
      if RespCenterCode <> '' then begin
        FILTERGROUP(2);
        SETRANGE("Responsibility Center Filter",RespCenterCode);
        FILTERGROUP(0);
      end;
    END;

    [External]
    PROCEDURE CountOrders@5(FieldNumber@1000 : Integer) : Integer;
    VAR
      CountPurchOrders@1001 : Query "Count Purchase Orders";
    BEGIN
      case FieldNumber of
        FIELDNO("Outstanding Purchase Orders"):
          begin
            CountPurchOrders.SETRANGE(Status,CountPurchOrders.Status::Released);
            CountPurchOrders.SETRANGE(Completely_Received,false);
            CountPurchOrders.SETRANGE(Invoice);
          end;
        FIELDNO("Not Invoiced"):
          begin
            CountPurchOrders.SETRANGE(Status);
            CountPurchOrders.SETRANGE(Completely_Received,true);
            CountPurchOrders.SETRANGE(Invoice,false);
          end;
        FIELDNO("Partially Invoiced"):
          begin
            CountPurchOrders.SETRANGE(Status);
            CountPurchOrders.SETRANGE(Completely_Received,true);
            CountPurchOrders.SETRANGE(Invoice,true);
          end;
      end;
      FILTERGROUP(2);
      CountPurchOrders.SETFILTER(Responsibility_Center,GETFILTER("Responsibility Center Filter"));
      FILTERGROUP(0);
      CountPurchOrders.OPEN;
      CountPurchOrders.READ;
      exit(CountPurchOrders.Count_Orders);
    END;

    [External]
    PROCEDURE ShowOrders@2(FieldNumber@1001 : Integer);
    VAR
      PurchHeader@1000 : Record "Purchase Header";
    BEGIN
      PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Order);
      case FieldNumber of
        FIELDNO("Outstanding Purchase Orders"):
          begin
            PurchHeader.SETRANGE(Status,PurchHeader.Status::Released);
            PurchHeader.SETRANGE("Completely Received",false);
            PurchHeader.SETRANGE(Invoice);
          end;
        FIELDNO("Not Invoiced"):
          begin
            PurchHeader.SETRANGE(Status);
            PurchHeader.SETRANGE("Completely Received",false);
            PurchHeader.SETRANGE(Receive,true);
          end;
        FIELDNO("Partially Invoiced"):
          begin
            PurchHeader.SETRANGE(Status);
            PurchHeader.SETRANGE("Completely Received",true);
            PurchHeader.SETRANGE(Invoice,true);
          end;
      end;
      FILTERGROUP(2);
      PurchHeader.SETFILTER("Responsibility Center",GETFILTER("Responsibility Center Filter"));
      FILTERGROUP(0);
      PAGE.RUN(PAGE::"Purchase Order List",PurchHeader);
    END;

    BEGIN
    END.
  }
}

