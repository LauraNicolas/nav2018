OBJECT Table 5718 Nonstock Item
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               NonStockItem.LOCKTABLE;
               if "Entry No." = '' then begin
                 GetInvtSetup;
                 InvtSetup.TESTFIELD("Nonstock Item Nos.");
                 NoSeriesMgt.InitSeries(InvtSetup."Nonstock Item Nos.",xRec."No. Series",0D,"Entry No.","No. Series");
               end;
             END;

    OnModify=VAR
               Item@1000 : Record Item;
             BEGIN
               "Last Date Modified" := TODAY;
               MfrLength := STRLEN("Manufacturer Code");
               VenLength := STRLEN("Vendor Item No.");

               NonStockItemSetup.GET;
               case NonStockItemSetup."No. Format" of
                 NonStockItemSetup."No. Format"::"Entry No.":
                   ItemNo := "Entry No.";
                 NonStockItemSetup."No. Format"::"Vendor Item No.":
                   ItemNo := "Vendor Item No.";
                 NonStockItemSetup."No. Format"::"Mfr. + Vendor Item No.":
                   if NonStockItemSetup."No. Format Separator" = '' then begin
                     if MfrLength + VenLength <= 20 then
                       ItemNo := INSSTR("Manufacturer Code","Vendor Item No.",6)
                     else
                       ItemNo := INSSTR("Manufacturer Code","Entry No.",6);
                   end else begin
                     TempItemNo :=
                       INSSTR("Manufacturer Code",NonStockItemSetup."No. Format Separator",6);
                     if MfrLength + VenLength < 20 then
                       ItemNo := INSSTR(TempItemNo,"Vendor Item No.",7)
                     else
                       ItemNo := INSSTR(TempItemNo,"Entry No.",7);
                   end;
                 NonStockItemSetup."No. Format"::"Vendor Item No. + Mfr.":
                   if NonStockItemSetup."No. Format Separator" = '' then begin
                     if VenLength + MfrLength <= 20 then
                       ItemNo := INSSTR("Vendor Item No.","Manufacturer Code",11)
                     else
                       ItemNo := INSSTR("Entry No.","Manufacturer Code",11);
                   end else begin
                     TempItemNo :=
                       INSSTR("Vendor Item No.",NonStockItemSetup."No. Format Separator",10);
                     if VenLength + MfrLength < 20 then
                       ItemNo := INSSTR(TempItemNo,"Manufacturer Code",11);
                   end;
               end;
               Item.SETRANGE("No.",ItemNo);
               if not Item.ISEMPTY then
                 ERROR(Text001);
             END;

    CaptionML=ENU=Nonstock Item;
    LookupPageID="Nonstock Item List";
    DrillDownPageID="Nonstock Item List";
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Code20        ;OnValidate=BEGIN
                                                                if "Entry No." <> xRec."Entry No." then begin
                                                                  GetInvtSetup;
                                                                  NoSeriesMgt.TestManual(InvtSetup."Nonstock Item Nos.");
                                                                  "No. Series" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Entry No.;
                                                   Editable=true }
    { 2   ;   ;Manufacturer Code   ;Code10        ;TableRelation=Manufacturer.Code;
                                                   OnValidate=BEGIN
                                                                if ("Manufacturer Code" <> xRec."Manufacturer Code") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Manufacturer Code }
    { 3   ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor."No.";
                                                   OnValidate=BEGIN
                                                                if ("Vendor No." <> xRec."Vendor No.") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);

                                                                if "Vendor No." <> xRec."Vendor No." then
                                                                  if CheckVendorItemNo("Vendor No.","Vendor Item No.") then
                                                                    ERROR(Text002,"Vendor No.","Vendor Item No.");
                                                              END;

                                                   CaptionML=ENU=Vendor No. }
    { 4   ;   ;Vendor Item No.     ;Code20        ;OnValidate=BEGIN
                                                                if ("Vendor Item No." <> xRec."Vendor Item No.") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);

                                                                if "Vendor Item No." <> xRec."Vendor Item No." then
                                                                  if CheckVendorItemNo("Vendor No.","Vendor Item No.") then
                                                                    ERROR(Text002,"Vendor No.","Vendor Item No.");
                                                              END;

                                                   CaptionML=ENU=Vendor Item No. }
    { 5   ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                if (Description <> xRec.Description) and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Description;
                                                   Editable=true }
    { 6   ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=BEGIN
                                                                if ("Unit of Measure" <> xRec."Unit of Measure") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Unit of Measure }
    { 7   ;   ;Published Cost      ;Decimal       ;OnValidate=BEGIN
                                                                if ("Published Cost" <> xRec."Published Cost") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Published Cost;
                                                   AutoFormatType=1 }
    { 8   ;   ;Negotiated Cost     ;Decimal       ;OnValidate=BEGIN
                                                                if ("Negotiated Cost" <> xRec."Negotiated Cost") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Negotiated Cost;
                                                   AutoFormatType=1 }
    { 9   ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                if ("Unit Price" <> xRec."Unit Price") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Unit Price;
                                                   AutoFormatType=1 }
    { 10  ;   ;Gross Weight        ;Decimal       ;OnValidate=BEGIN
                                                                if ("Gross Weight" <> xRec."Gross Weight") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Gross Weight;
                                                   DecimalPlaces=0:5 }
    { 11  ;   ;Net Weight          ;Decimal       ;OnValidate=BEGIN
                                                                if ("Net Weight" <> xRec."Net Weight") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Net Weight;
                                                   DecimalPlaces=0:5 }
    { 12  ;   ;Item Template Code  ;Code10        ;TableRelation="Config. Template Header".Code WHERE ("Table ID"=CONST(27));
                                                   OnValidate=BEGIN
                                                                if ("Item Template Code" <> xRec."Item Template Code") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);

                                                                "Product Group Code" := '';
                                                              END;

                                                   CaptionML=ENU=Item Template Code }
    { 13  ;   ;Product Group Code  ;Code10        ;OnValidate=BEGIN
                                                                if ("Product Group Code" <> xRec."Product Group Code") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Product Group Code }
    { 14  ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=false }
    { 15  ;   ;Bar Code            ;Code20        ;OnValidate=BEGIN
                                                                if ("Bar Code" <> xRec."Bar Code") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Bar Code }
    { 16  ;   ;Item No.            ;Code20        ;TableRelation=Item."No.";
                                                   OnValidate=BEGIN
                                                                if ("Item No." <> xRec."Item No.") and
                                                                   ("Item No." <> '')
                                                                then
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Item No.;
                                                   Editable=false }
    { 53  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE ("Table Name"=CONST("Nonstock Item"),
                                                                                           "No."=FIELD("Entry No.")));
                                                   CaptionML=ENU=Comment;
                                                   Editable=false }
    { 97  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Entry No."                             ;Clustered=true }
    {    ;"Vendor Item No.","Manufacturer Code"    }
    {    ;"Item No."                               }
    {    ;"Vendor No.","Vendor Item No."           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Modification not allowed, item record already exists.';
      NonStockItem@1002 : Record "Nonstock Item";
      NonStockItemSetup@1003 : Record "Nonstock Item Setup";
      InvtSetup@1013 : Record "Inventory Setup";
      NoSeriesMgt@1011 : Codeunit NoSeriesManagement;
      ItemNo@1005 : Code[20];
      TempItemNo@1006 : Code[20];
      MfrLength@1008 : Integer;
      VenLength@1009 : Integer;
      Text002@1010 : TextConst 'ENU="No.=<%1> and Vendor Item No.=<%2> already exists."';
      HasInvtSetup@1012 : Boolean;

    [External]
    PROCEDURE AssistEdit@2() : Boolean;
    BEGIN
      GetInvtSetup;
      InvtSetup.TESTFIELD("Nonstock Item Nos.");
      if NoSeriesMgt.SelectSeries(InvtSetup."Nonstock Item Nos.",xRec."No. Series","No. Series") then begin
        NoSeriesMgt.SetSeries("Entry No.");
        exit(true);
      end;
    END;

    LOCAL PROCEDURE CheckVendorItemNo@1(VendorNo@1000 : Code[20];VendorItemNo@1001 : Code[20]) : Boolean;
    BEGIN
      NonStockItem.RESET;
      NonStockItem.SETCURRENTKEY("Vendor No.","Vendor Item No.");
      NonStockItem.SETRANGE("Vendor No.",VendorNo);
      NonStockItem.SETRANGE("Vendor Item No.",VendorItemNo);
      exit(NonStockItem.FINDFIRST);
    END;

    LOCAL PROCEDURE GetInvtSetup@14();
    BEGIN
      if not HasInvtSetup then begin
        InvtSetup.GET;
        HasInvtSetup := true;
      end;
    END;

    BEGIN
    END.
  }
}

