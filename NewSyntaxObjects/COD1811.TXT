OBJECT Codeunit 1811 Excel Post Processor
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="Config. Package Record";
    OnRun=BEGIN
            case "Table ID" of
              DATABASE::Item:
                PostProcessItem(Rec);
              else
                exit;
            end;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE PostProcessItem@1(ConfigPackageRecord@1004 : Record "Config. Package Record");
    VAR
      Item@1002 : Record Item;
      AdjustItemInventory@1005 : Codeunit "Adjust Item Inventory";
      QuantityOnInventory@1000 : Decimal;
      ErrorText@1006 : Text;
    BEGIN
      if not FindItemAndGetInventory(ConfigPackageRecord,Item,QuantityOnInventory) then
        exit;

      if (Item."Base Unit of Measure" = '') or
         (Item."Gen. Prod. Posting Group" = '') or
         (Item."Inventory Posting Group" = '')
      then
        exit;

      ErrorText := AdjustItemInventory.PostAdjustmentToItemLedger(Item,QuantityOnInventory);
      if ErrorText <> '' then
        ERROR(ErrorText);
    END;

    LOCAL PROCEDURE FindItemAndGetInventory@2(ConfigPackageRecord@1000 : Record "Config. Package Record";VAR Item@1001 : Record Item;VAR QuantityOnInventory@1002 : Decimal) : Boolean;
    VAR
      ConfigPackageData@1003 : Record "Config. Package Data";
    BEGIN
      with ConfigPackageRecord do begin
        ConfigPackageData.GET("Package Code","Table ID","No.",Item.FIELDNO("No."));
        if not Item.GET(ConfigPackageData.Value) then
          exit(false);
        if not ConfigPackageData.GET("Package Code","Table ID","No.",Item.FIELDNO(Inventory)) then
          exit(false);
        if not EVALUATE(QuantityOnInventory,ConfigPackageData.Value) then
          exit(false);
      end;

      exit(true);
    END;

    BEGIN
    END.
  }
}

