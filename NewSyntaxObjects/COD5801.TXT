OBJECT Codeunit 5801 Show Applied Entries
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="Item Ledger Entry";
    Permissions=TableData "Item Ledger Entry"=rim,
                TableData "Item Application Entry"=r;
    OnRun=BEGIN
            TempItemEntry.DELETEALL;
            FindAppliedEntry(Rec);
            PAGE.RUNMODAL(PAGE::"Applied Item Entries",TempItemEntry);
          END;

  }
  CODE
  {
    VAR
      TempItemEntry@1000 : TEMPORARY Record "Item Ledger Entry";

    LOCAL PROCEDURE FindAppliedEntry@2(ItemLedgEntry@1000 : Record "Item Ledger Entry");
    VAR
      ItemApplnEntry@1001 : Record "Item Application Entry";
    BEGIN
      with ItemLedgEntry do
        if Positive then begin
          ItemApplnEntry.RESET;
          ItemApplnEntry.SETCURRENTKEY("Inbound Item Entry No.","Outbound Item Entry No.","Cost Application");
          ItemApplnEntry.SETRANGE("Inbound Item Entry No.","Entry No.");
          ItemApplnEntry.SETFILTER("Outbound Item Entry No.",'<>%1',0);
          ItemApplnEntry.SETRANGE("Cost Application",true);
          if ItemApplnEntry.FIND('-') then
            repeat
              InsertTempEntry(ItemApplnEntry."Outbound Item Entry No.",ItemApplnEntry.Quantity);
            until ItemApplnEntry.NEXT = 0;
        end else begin
          ItemApplnEntry.RESET;
          ItemApplnEntry.SETCURRENTKEY("Outbound Item Entry No.","Item Ledger Entry No.","Cost Application");
          ItemApplnEntry.SETRANGE("Outbound Item Entry No.","Entry No.");
          ItemApplnEntry.SETRANGE("Item Ledger Entry No.","Entry No.");
          ItemApplnEntry.SETRANGE("Cost Application",true);
          if ItemApplnEntry.FIND('-') then
            repeat
              InsertTempEntry(ItemApplnEntry."Inbound Item Entry No.",-ItemApplnEntry.Quantity);
            until ItemApplnEntry.NEXT = 0;
        end;
    END;

    LOCAL PROCEDURE InsertTempEntry@1(EntryNo@1000 : Integer;AppliedQty@1001 : Decimal);
    VAR
      ItemLedgEntry@1002 : Record "Item Ledger Entry";
    BEGIN
      ItemLedgEntry.GET(EntryNo);
      if AppliedQty * ItemLedgEntry.Quantity < 0 then
        exit;

      if not TempItemEntry.GET(EntryNo) then begin
        TempItemEntry.INIT;
        TempItemEntry := ItemLedgEntry;
        TempItemEntry.Quantity := AppliedQty;
        TempItemEntry.INSERT;
      end else begin
        TempItemEntry.Quantity := TempItemEntry.Quantity + AppliedQty;
        TempItemEntry.MODIFY;
      end;
    END;

    BEGIN
    END.
  }
}

