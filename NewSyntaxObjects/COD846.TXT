OBJECT Codeunit 846 Cash Flow Wksh. -Register Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Cash Flow Worksheet Line";
    Permissions=TableData "Cash Flow Forecast Entry"=imd;
    OnRun=BEGIN
            GLSetup.GET;
            RunWithCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      CFWkshLine@1000 : Record "Cash Flow Worksheet Line";
      CFForecastEntry@1001 : Record "Cash Flow Forecast Entry";
      CashFlowForecast@1003 : Record "Cash Flow Forecast";
      GLSetup@1007 : Record "General Ledger Setup";
      CFAccount@1011 : Record "Cash Flow Account";
      CFWkshCheckLine@1008 : Codeunit "Cash Flow Wksh.- Check Line";
      NextEntryNo@1010 : Integer;

    [External]
    PROCEDURE RunWithCheck@1001(VAR CFWkshLine2@1000 : Record "Cash Flow Worksheet Line");
    BEGIN
      CFWkshLine.COPY(CFWkshLine2);
      Code;
      CFWkshLine2 := CFWkshLine;
    END;

    LOCAL PROCEDURE Code@1002();
    BEGIN
      with CFWkshLine do begin
        if EmptyLine then
          exit;

        CFWkshCheckLine.RunCheck(CFWkshLine);

        if NextEntryNo = 0 then begin
          CFForecastEntry.LOCKTABLE;
          if CFForecastEntry.FINDLAST then
            NextEntryNo := CFForecastEntry."Entry No.";
          NextEntryNo := NextEntryNo + 1;
        end;

        CashFlowForecast.GET("Cash Flow Forecast No.");
        if "Cash Flow Account No." <> '' then begin
          CFAccount.GET("Cash Flow Account No.");
          CFAccount.TESTFIELD(Blocked,false);
        end;

        CFForecastEntry.INIT;
        CFForecastEntry."Cash Flow Forecast No." := "Cash Flow Forecast No.";
        CFForecastEntry."Cash Flow Date" := "Cash Flow Date";
        CFForecastEntry."Document No." := "Document No.";
        CFForecastEntry."Document Date" := "Document Date";
        CFForecastEntry."Cash Flow Account No." := "Cash Flow Account No.";
        CFForecastEntry."Source Type" := "Source Type";
        CFForecastEntry."Source No." := "Source No.";
        CFForecastEntry."G/L Budget Name" := "G/L Budget Name";
        CFForecastEntry.Description := Description;
        CFForecastEntry."Payment Discount" := ROUND("Payment Discount",0.00001);
        CFForecastEntry."Associated Entry No." := "Associated Entry No.";
        CFForecastEntry.Overdue := Overdue;
        CFForecastEntry."Global Dimension 2 Code" := "Shortcut Dimension 2 Code";
        CFForecastEntry."Global Dimension 1 Code" := "Shortcut Dimension 1 Code";
        CFForecastEntry."Dimension Set ID" := "Dimension Set ID";
        CFForecastEntry."Amount (LCY)" := ROUND("Amount (LCY)",0.00001);
        CFForecastEntry.Positive := CFForecastEntry."Amount (LCY)" > 0;

        if CFForecastEntry.Description = CashFlowForecast.Description then
          CFForecastEntry.Description := '';
        CFForecastEntry."User ID" := USERID;
        CFForecastEntry."Entry No." := NextEntryNo;

        CFForecastEntry.INSERT;

        NextEntryNo := NextEntryNo + 1;
      end;
    END;

    BEGIN
    END.
  }
}

