OBJECT Page 7004 Sales Line Discounts
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Line Discounts;
    SaveValues=true;
    SourceTable="Sales Line Discount";
    DataCaptionExpr=GetCaption;
    DelayedInsert=true;
    PageType=List;
    ShowFilter=false;
    OnInit=BEGIN
             CodeFilterCtrlEnable := true;
             SalesCodeFilterCtrlEnable := true;
             SalesCodeEditable := true;
           END;

    OnOpenPage=BEGIN
                 IsOnMobile := CURRENTCLIENTTYPE = CLIENTTYPE::Phone;
                 GetRecFilters;
                 SetRecFilters;
               END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditableFields;
                         END;

    ActionList=ACTIONS
    {
      { 11      ;    ;ActionContainer;
                      Name=Filtering;
                      ActionContainerType=ActionItems }
      { 7       ;1   ;Action    ;
                      Name=Filter;
                      CaptionML=ENU=Filter;
                      ToolTipML=ENU=Apply the filter.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Visible=IsOnMobile;
                      Image=Filter;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FilterLines;
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=ClearFilter;
                      CaptionML=ENU=Clear Filter;
                      ToolTipML=ENU=Clear filter.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Visible=IsOnMobile;
                      Image=ClearFilter;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 RESET;
                                 UpdateBasicRecFilters;
                                 SetEditableFields;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                Visible=NOT IsOnMobile }

    { 28  ;2   ;Field     ;
                Name=SalesTypeFilter;
                CaptionML=ENU=Sales Type Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                OptionCaptionML=ENU=Customer,Customer Discount Group,All Customers,Campaign,None;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesTypeFilter;
                OnValidate=BEGIN
                             SalesTypeFilterOnAfterValidate;
                           END;

                ImplicitType=Option }

    { 26  ;2   ;Field     ;
                Name=SalesCodeFilterCtrl;
                CaptionML=ENU=Sales Code Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesCodeFilter;
                Enabled=SalesCodeFilterCtrlEnable;
                OnValidate=BEGIN
                             SalesCodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           CustList@1005 : Page "Customer List";
                           CustDiscGrList@1004 : Page "Customer Disc. Groups";
                           CampaignList@1003 : Page "Campaign List";
                         BEGIN
                           if SalesTypeFilter = SalesTypeFilter::"All Customers" then
                             exit;

                           case SalesTypeFilter of
                             SalesTypeFilter::Customer:
                               begin
                                 CustList.LOOKUPMODE := true;
                                 if CustList.RUNMODAL = ACTION::LookupOK then
                                   Text := CustList.GetSelectionFilter
                                 else
                                   exit(false);
                               end;
                             SalesTypeFilter::"Customer Discount Group":
                               begin
                                 CustDiscGrList.LOOKUPMODE := true;
                                 if CustDiscGrList.RUNMODAL = ACTION::LookupOK then
                                   Text := CustDiscGrList.GetSelectionFilter
                                 else
                                   exit(false);
                               end;
                             SalesTypeFilter::Campaign:
                               begin
                                 CampaignList.LOOKUPMODE := true;
                                 if CampaignList.RUNMODAL = ACTION::LookupOK then
                                   Text := CampaignList.GetSelectionFilter
                                 else
                                   exit(false);
                               end;
                           end;

                           exit(true);
                         END;

                ImplicitType=Text }

    { 34  ;2   ;Field     ;
                Name=StartingDateFilter;
                CaptionML=ENU=Starting Date Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=StartingDateFilter;
                OnValidate=VAR
                             ApplicationMgt@1000 : Codeunit ApplicationManagement;
                           BEGIN
                             if ApplicationMgt.MakeDateFilter(StartingDateFilter) = 0 then;
                             StartingDateFilterOnAfterValid;
                           END;

                ImplicitType=Text30 }

    { 10  ;2   ;Field     ;
                Name=ItemTypeFilter;
                CaptionML=ENU=Type Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                OptionCaptionML=ENU=Item,Item Discount Group,None;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ItemTypeFilter;
                OnValidate=BEGIN
                             ItemTypeFilterOnAfterValidate;
                           END;

                ImplicitType=Option }

    { 40  ;2   ;Field     ;
                Name=CodeFilterCtrl;
                CaptionML=ENU=Code Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CodeFilter;
                Enabled=CodeFilterCtrlEnable;
                OnValidate=BEGIN
                             CodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           ItemList@1002 : Page "Item List";
                           ItemDiscGrList@1003 : Page "Item Disc. Groups";
                         BEGIN
                           case Type of
                             Type::Item:
                               begin
                                 ItemList.LOOKUPMODE := true;
                                 if ItemList.RUNMODAL = ACTION::LookupOK then
                                   Text := ItemList.GetSelectionFilter
                                 else
                                   exit(false);
                               end;
                             Type::"Item Disc. Group":
                               begin
                                 ItemDiscGrList.LOOKUPMODE := true;
                                 if ItemDiscGrList.RUNMODAL = ACTION::LookupOK then
                                   Text := ItemDiscGrList.GetSelectionFilter
                                 else
                                   exit(false);
                               end;
                           end;

                           exit(true);
                         END;

                ImplicitType=Text }

    { 16  ;2   ;Field     ;
                Name=SalesCodeFilterCtrl2;
                CaptionML=ENU=Currency Code Filter;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Suite;
                SourceExpr=CurrencyCodeFilter;
                OnValidate=BEGIN
                             CurrencyCodeFilterOnAfterValid;
                           END;

                OnLookup=VAR
                           CurrencyList@1000 : Page Currencies;
                         BEGIN
                           CurrencyList.LOOKUPMODE := true;
                           if CurrencyList.RUNMODAL = ACTION::LookupOK then
                             Text := CurrencyList.GetSelectionFilter
                           else
                             exit(false);

                           exit(true);
                         END;

                ImplicitType=Text }

    { 3   ;1   ;Group     ;
                Name=Filters;
                CaptionML=ENU=Filters;
                Visible=IsOnMobile;
                GroupType=Group }

    { 5   ;2   ;Field     ;
                Name=GetFilterDescription;
                ToolTipML=ENU=Specifies a filter for which sales line discounts to display.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=GetFilterDescription;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               FilterLines;
                               CurrPage.UPDATE(false);
                             END;
                              }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 32  ;2   ;Field     ;
                Name=SalesType;
                ToolTipML=ENU=Specifies the sales type of the sales line discount. The sales type defines whether the sales price is for an individual customer, customer discount group, all customers, or for a campaign.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Type";
                OnValidate=BEGIN
                             SetEditableFields;
                           END;

                ImplicitType=Option }

    { 18  ;2   ;Field     ;
                Name=SalesCode;
                ToolTipML=ENU=Specifies one of the following values, depending on the value in the Sales Type field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Code";
                Editable=SalesCodeEditable;
                ImplicitType=Code20 }

    { 42  ;2   ;Field     ;
                Name=Type;
                ToolTipML=ENU=Specifies the type of item that the sales discount line is valid for. That is, either an item or an item discount group.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Type;
                ImplicitType=Option }

    { 4   ;2   ;Field     ;
                Name=Code;
                ToolTipML=ENU=Specifies one of two values, depending on the value in the Type field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Code;
                ImplicitType=Code20 }

    { 30  ;2   ;Field     ;
                Name=Variant Code;
                ToolTipML=ENU=Specifies the variant of the item on the line.;
                ApplicationArea=#Advanced;
                SourceExpr="Variant Code";
                Visible=FALSE;
                ImplicitType=Code10 }

    { 14  ;2   ;Field     ;
                Name=Currency Code;
                ToolTipML=ENU=Specifies the currency code of the sales line discount price.;
                ApplicationArea=#Advanced;
                SourceExpr="Currency Code";
                Visible=FALSE;
                ImplicitType=Code10 }

    { 8   ;2   ;Field     ;
                Name=Unit of Measure Code;
                ToolTipML=ENU=Specifies how each unit of the item or resource is measured, such as in pieces or hours. By default, the value in the Base Unit of Measure field on the item or resource card is inserted.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Unit of Measure Code";
                ImplicitType=Code10 }

    { 12  ;2   ;Field     ;
                Name=Minimum Quantity;
                ToolTipML=ENU=Specifies the minimum quantity that the customer must purchase in order to gain the agreed discount.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Minimum Quantity";
                ImplicitType=Decimal }

    { 6   ;2   ;Field     ;
                Name=Line Discount %;
                ToolTipML=ENU=Specifies the discount percentage that is granted for the item on the line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Line Discount %";
                ImplicitType=Decimal }

    { 36  ;2   ;Field     ;
                Name=Starting Date;
                ToolTipML=ENU=Specifies the date from which the sales line discount is valid.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Starting Date";
                ImplicitType=Date }

    { 2   ;2   ;Field     ;
                Name=Ending Date;
                ToolTipML=ENU=Specifies the date to which the sales line discount is valid.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Ending Date";
                ImplicitType=Date }

    { 1900000007;0;Container;
                Name=Container1900000007;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Name=Part1900383207;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Name=Part1905767507;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Cust@1001 : Record Customer;
      CustDiscGr@1005 : Record "Customer Discount Group";
      Campaign@1008 : Record Campaign;
      Item@1004 : Record Item;
      ItemDiscGr@1013 : Record "Item Discount Group";
      SalesTypeFilter@1000 : 'Customer,"Customer Discount Group","All Customers",Campaign,None';
      SalesCodeFilter@1003 : Text;
      ItemTypeFilter@1012 : 'Item,"Item Discount Group",None';
      CodeFilter@1011 : Text;
      StartingDateFilter@1006 : Text[30];
      Text000@1007 : TextConst 'ENU=All Customers';
      CurrencyCodeFilter@1002 : Text;
      SalesCodeEditable@19031640 : Boolean INDATASET;
      SalesCodeFilterCtrlEnable@19013164 : Boolean INDATASET;
      CodeFilterCtrlEnable@19026169 : Boolean INDATASET;
      IsOnMobile@1009 : Boolean;

    LOCAL PROCEDURE GetRecFilters@2();
    BEGIN
      if GETFILTERS <> '' then
        UpdateBasicRecFilters;
    END;

    [External]
    PROCEDURE SetRecFilters@1();
    BEGIN
      SalesCodeFilterCtrlEnable := true;
      CodeFilterCtrlEnable := true;

      if SalesTypeFilter <> SalesTypeFilter::None then
        SETRANGE("Sales Type",SalesTypeFilter)
      else
        SETRANGE("Sales Type");

      if SalesTypeFilter in [SalesTypeFilter::"All Customers",SalesTypeFilter::None] then begin
        SalesCodeFilterCtrlEnable := false;
        SalesCodeFilter := '';
      end;

      if SalesCodeFilter <> '' then
        SETFILTER("Sales Code",SalesCodeFilter)
      else
        SETRANGE("Sales Code");

      if ItemTypeFilter <> ItemTypeFilter::None then
        SETRANGE(Type,ItemTypeFilter)
      else
        SETRANGE(Type);

      if ItemTypeFilter = ItemTypeFilter::None then begin
        CodeFilterCtrlEnable := false;
        CodeFilter := '';
      end;

      if CodeFilter <> '' then begin
        SETFILTER(Code,CodeFilter);
      end else
        SETRANGE(Code);

      if CurrencyCodeFilter <> '' then begin
        SETFILTER("Currency Code",CurrencyCodeFilter);
      end else
        SETRANGE("Currency Code");

      if StartingDateFilter <> '' then
        SETFILTER("Starting Date",StartingDateFilter)
      else
        SETRANGE("Starting Date");

      CurrPage.UPDATE(false);
    END;

    LOCAL PROCEDURE GetCaption@3() : Text[250];
    BEGIN
      if IsOnMobile then
        exit('');

      exit(GetFilterDescription);
    END;

    LOCAL PROCEDURE GetFilterDescription@4() : Text;
    VAR
      ObjTranslation@1000 : Record "Object Translation";
      SourceTableName@1002 : Text;
      SalesSrcTableName@1003 : Text;
      Description@1001 : Text;
    BEGIN
      GetRecFilters;

      SourceTableName := '';
      case ItemTypeFilter of
        ItemTypeFilter::Item:
          begin
            SourceTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,27);
            Item.SETFILTER("No.",CodeFilter);
            if not Item.FINDFIRST then
              CLEAR(Item);
          end;
        ItemTypeFilter::"Item Discount Group":
          begin
            SourceTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,341);
            ItemDiscGr.SETFILTER(Code,CodeFilter);
            if not ItemDiscGr.FINDFIRST then
              CLEAR(ItemDiscGr);
          end;
      end;

      SalesSrcTableName := '';
      case SalesTypeFilter of
        SalesTypeFilter::Customer:
          begin
            SalesSrcTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,18);
            Cust.SETFILTER("No.",SalesCodeFilter);
            if Cust.FINDFIRST then
              Description := Cust.Name;
          end;
        SalesTypeFilter::"Customer Discount Group":
          begin
            SalesSrcTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,340);
            CustDiscGr.SETFILTER(Code,SalesCodeFilter);
            if CustDiscGr.FINDFIRST then
              Description := CustDiscGr.Description;
          end;
        SalesTypeFilter::Campaign:
          begin
            SalesSrcTableName := ObjTranslation.TranslateObject(ObjTranslation."Object Type"::Table,5071);
            Campaign.SETFILTER("No.",SalesCodeFilter);
            if Campaign.FINDFIRST then
              Description := Campaign.Description;
          end;
        SalesTypeFilter::"All Customers":
          begin
            SalesSrcTableName := Text000;
            Description := '';
          end;
      end;

      if SalesSrcTableName = Text000 then
        exit(STRSUBSTNO('%1 %2 %3 %4 %5',SalesSrcTableName,SalesCodeFilter,Description,SourceTableName,CodeFilter));
      exit(STRSUBSTNO('%1 %2 %3 %4 %5',SalesSrcTableName,SalesCodeFilter,Description,SourceTableName,CodeFilter));
    END;

    LOCAL PROCEDURE SalesCodeFilterOnAfterValidate@19067727();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE SalesTypeFilterOnAfterValidate@19024044();
    BEGIN
      CurrPage.SAVERECORD;
      SalesCodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE StartingDateFilterOnAfterValid@19055286();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE ItemTypeFilterOnAfterValidate@19002579();
    BEGIN
      CurrPage.SAVERECORD;
      CodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE CodeFilterOnAfterValidate@19025728();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE CurrencyCodeFilterOnAfterValid@19015290();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE GetSalesTypeFilter@1000() : Integer;
    BEGIN
      case GETFILTER("Sales Type") of
        FORMAT("Sales Type"::Customer):
          exit(0);
        FORMAT("Sales Type"::"Customer Disc. Group"):
          exit(1);
        FORMAT("Sales Type"::"All Customers"):
          exit(2);
        FORMAT("Sales Type"::Campaign):
          exit(3);
      end;
    END;

    LOCAL PROCEDURE GetTypeFilter@1001() : Integer;
    BEGIN
      case GETFILTER(Type) of
        FORMAT(Type::Item):
          exit(0);
        FORMAT(Type::"Item Disc. Group"):
          exit(1);
      end;
    END;

    LOCAL PROCEDURE FilterLines@8();
    VAR
      FilterPageBuilder@1000 : FilterPageBuilder;
    BEGIN
      FilterPageBuilder.ADDTABLE(TABLECAPTION,DATABASE::"Sales Line Discount");
      FilterPageBuilder.SETVIEW(TABLECAPTION,GETVIEW);

      if GETFILTER("Sales Type") = '' then
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO("Sales Type"));
      if GETFILTER("Sales Code") = '' then
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO("Sales Code"));
      if GETFILTER(Type) = '' then
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO(Type));
      if GETFILTER(Code) = '' then
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO(Code));
      if GETFILTER("Starting Date") = '' then
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO("Starting Date"));
      if GETFILTER("Currency Code") = '' then
        FilterPageBuilder.ADDFIELDNO(TABLECAPTION,FIELDNO("Currency Code"));

      if FilterPageBuilder.RUNMODAL then
        SETVIEW(FilterPageBuilder.GETVIEW(TABLECAPTION));

      UpdateBasicRecFilters;
      SetEditableFields;
    END;

    LOCAL PROCEDURE UpdateBasicRecFilters@14();
    BEGIN
      if GETFILTER("Sales Type") <> '' then
        SalesTypeFilter := GetSalesTypeFilter
      else
        SalesTypeFilter := SalesTypeFilter::None;

      if GETFILTER(Type) <> '' then
        ItemTypeFilter := GetTypeFilter
      else
        ItemTypeFilter := ItemTypeFilter::None;

      SalesCodeFilter := GETFILTER("Sales Code");
      CodeFilter := GETFILTER(Code);
      CurrencyCodeFilter := GETFILTER("Currency Code");
      EVALUATE(StartingDateFilter,GETFILTER("Starting Date"));
    END;

    LOCAL PROCEDURE SetEditableFields@18();
    BEGIN
      SalesCodeEditable := "Sales Type" <> "Sales Type"::"All Customers";
    END;

    BEGIN
    END.
  }
}

