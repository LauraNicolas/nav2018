OBJECT Report 5194 Create Conts. from Vendors
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Conts. from Vendors;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  RMSetup.GET;
                  RMSetup.TESTFIELD("Bus. Rel. Code for Vendors");
                END;

  }
  DATASET
  {
    { 3182;    ;DataItem;Vendor              ;
               DataItemTable=Vendor;
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000 +
                                 Text001,"No.");
                             END;

               OnAfterGetRecord=VAR
                                  VendContUpdate@1000 : Codeunit "VendCont-Update";
                                BEGIN
                                  Window.UPDATE(1);

                                  with ContBusRel do begin
                                    SETRANGE("Link to Table","Link to Table"::Vendor);
                                    SETRANGE("No.",Vendor."No.");
                                    if FINDFIRST then
                                      CurrReport.SKIP;
                                  end;

                                  Cont.INIT;
                                  Cont.TRANSFERFIELDS(Vendor);
                                  Cont."No." := '';
                                  Cont.SetSkipDefault;
                                  Cont.INSERT(true);
                                  DuplMgt.MakeContIndex(Cont);

                                  if not DuplicateContactExist then
                                    DuplicateContactExist := DuplMgt.DuplicateExist(Cont);

                                  with ContBusRel do begin
                                    INIT;
                                    "Contact No." := Cont."No.";
                                    "Business Relation Code" := RMSetup."Bus. Rel. Code for Vendors";
                                    "Link to Table" := "Link to Table"::Vendor;
                                    "No." := Vendor."No.";
                                    INSERT;
                                  end;

                                  VendContUpdate.InsertNewContactPerson(Vendor,false);
                                  MODIFY(true);
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                if DuplicateContactExist then begin
                                  COMMIT;
                                  PAGE.RUNMODAL(PAGE::"Contact Duplicates");
                                end;
                              END;

               ReqFilterFields="No.","Search Name","Vendor Posting Group","Currency Code" }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing vendors...\\';
      Text001@1001 : TextConst 'ENU=Vendor No.      #1##########';
      RMSetup@1002 : Record "Marketing Setup";
      Cont@1003 : Record Contact;
      ContBusRel@1004 : Record "Contact Business Relation";
      DuplMgt@1006 : Codeunit DuplicateManagement;
      Window@1005 : Dialog;
      DuplicateContactExist@1007 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

