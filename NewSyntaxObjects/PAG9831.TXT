OBJECT Page 9831 User Group Members
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=User Group Members;
    InsertAllowed=false;
    SourceTable="User Group Member";
    DelayedInsert=true;
    PopulateAllFields=true;
    DataCaptionFields="User Group Code","User Group Name";
    PageType=List;
    OnOpenPage=BEGIN
                 SelectedCompany := COMPANYNAME;
                 UpdateCompany;
               END;

    OnAfterGetRecord=BEGIN
                       CALCFIELDS("User Name");
                       UserName := "User Name";
                     END;

    OnNewRecord=BEGIN
                  UserName := '';
                END;

    OnInsertRecord=BEGIN
                     exit(not ISNULLGUID("User Security ID"));
                   END;

    OnModifyRecord=BEGIN
                     TESTFIELD("User Name");
                   END;

    ActionList=ACTIONS
    {
      { 9       ;    ;ActionContainer;
                      Name=New;
                      CaptionML=ENU=New;
                      ActionContainerType=NewDocumentItems }
      { 10      ;1   ;Action    ;
                      Name=AddUsers;
                      CaptionML=ENU=Add Users;
                      ToolTipML=ENU=See a list of existing users and add users to the user group.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      PromotedIsBig=true;
                      Image=Users;
                      PromotedOnly=true;
                      OnAction=BEGIN
                                 AddUsers(Company.Name);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 5   ;2   ;Field     ;
                Name=SelectedCompany;
                CaptionML=ENU=Company Name;
                ToolTipML=ENU=Specifies the company that you want to see users for.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SelectedCompany;
                TableRelation=Company;
                OnValidate=BEGIN
                             UpdateCompany;
                           END;

                ImplicitType=Text30 }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=UserName;
                Lookup=true;
                CaptionML=ENU=User Name;
                ToolTipML=ENU=Specifies the name of the user.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=UserName;
                TableRelation=User."User Name";
                LookupPageID=Users;
                OnValidate=VAR
                             User@1000 : Record User;
                           BEGIN
                             if UserName = '' then
                               exit;
                             User.SETRANGE("User Name",UserName);
                             User.FINDFIRST;
                             INIT;
                             VALIDATE("User Security ID",User."User Security ID");
                             VALIDATE("Company Name",SelectedCompany);
                             CALCFIELDS("User Name");
                             INSERT(true);
                             CurrPage.UPDATE(false);
                           END;

                OnLookup=VAR
                           User@1000 : Record User;
                           UserLookup@1001 : Page "User Lookup";
                         BEGIN
                           if Text <> '' then begin
                             User.SETRANGE("User Name",Text);
                             if User.FINDFIRST then;
                             UserLookup.SETRECORD(User);
                           end;
                           UserLookup.LOOKUPMODE := true;
                           if UserLookup.RUNMODAL = ACTION::LookupOK then begin
                             UserLookup.GETRECORD(User);
                             if User."User Security ID" = "User Security ID" then
                               exit;
                             if GET("User Group Code","User Security ID",SelectedCompany) then
                               DELETE(true);
                             INIT;
                             VALIDATE("User Security ID",User."User Security ID");
                             VALIDATE("Company Name",SelectedCompany);
                             CALCFIELDS("User Name");
                             INSERT(true);
                             CurrPage.UPDATE(false);
                           end;
                         END;

                ImplicitType=Code50;
                ShowMandatory=TRUE }

    { 4   ;2   ;Field     ;
                Name=User Full Name;
                CaptionML=ENU=Full Name;
                ToolTipML=ENU=Specifies the full name of the user.;
                ApplicationArea=#All;
                SourceExpr="User Full Name";
                ImplicitType=Text80 }

    { 7   ;2   ;Field     ;
                Name=User Group Code;
                ToolTipML=ENU=Specifies a user group.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User Group Code";
                TableRelation="User Group".Code;
                ImplicitType=Code20;
                ShowMandatory=TRUE }

    { 8   ;2   ;Field     ;
                Name=Company Name;
                ToolTipML=ENU=Specifies the company.;
                ApplicationArea=#All;
                SourceExpr="Company Name";
                ImplicitType=Text30 }

  }
  CODE
  {
    VAR
      Company@1001 : Record Company;
      SelectedCompany@1000 : Text[30];
      UserName@1002 : Code[50];

    LOCAL PROCEDURE UpdateCompany@5();
    BEGIN
      Company.Name := SelectedCompany;
      if SelectedCompany <> '' then begin
        Company.FIND('=<>');
        SelectedCompany := Company.Name;
      end;
      SETRANGE("Company Name",Company.Name);
      CurrPage.UPDATE(false);
    END;

    BEGIN
    END.
  }
}

