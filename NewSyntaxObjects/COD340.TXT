OBJECT Codeunit 340 VATStmtManagement
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "VAT Statement Template"=imd,
                TableData "VAT Statement Name"=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=VAT';
      Text001@1001 : TextConst 'ENU=VAT Statement';
      Text002@1002 : TextConst 'ENU=DEFAULT';
      Text003@1003 : TextConst 'ENU=Default Statement';
      OpenFromBatch@1004 : Boolean;

    [External]
    PROCEDURE TemplateSelection@1(PageID@1003 : Integer;VAR VATStmtLine@1002 : Record "VAT Statement Line";VAR StmtSelected@1001 : Boolean);
    VAR
      VATStmtTmpl@1000 : Record "VAT Statement Template";
    BEGIN
      StmtSelected := true;

      VATStmtTmpl.RESET;
      VATStmtTmpl.SETRANGE("Page ID",PageID);

      case VATStmtTmpl.COUNT of
        0:
          begin
            VATStmtTmpl.INIT;
            VATStmtTmpl.Name := Text000;
            VATStmtTmpl.Description := Text001;
            VATStmtTmpl.VALIDATE("Page ID");
            VATStmtTmpl.INSERT;
            COMMIT;
          end;
        1:
          VATStmtTmpl.FINDFIRST;
        else
          StmtSelected := PAGE.RUNMODAL(0,VATStmtTmpl) = ACTION::LookupOK;
      end;
      if StmtSelected then begin
        VATStmtLine.FILTERGROUP(2);
        VATStmtLine.SETRANGE("Statement Template Name",VATStmtTmpl.Name);
        VATStmtLine.FILTERGROUP(0);
        if OpenFromBatch then begin
          VATStmtLine."Statement Template Name" := '';
          PAGE.RUN(VATStmtTmpl."Page ID",VATStmtLine);
        end;
      end;
    END;

    [External]
    PROCEDURE TemplateSelectionFromBatch@9(VAR VATStmtName@1003 : Record "VAT Statement Name");
    VAR
      VATStmtLine@1000 : Record "VAT Statement Line";
      VATStmtTmpl@1002 : Record "VAT Statement Template";
    BEGIN
      OpenFromBatch := true;
      VATStmtTmpl.GET(VATStmtName."Statement Template Name");
      VATStmtTmpl.TESTFIELD("Page ID");
      VATStmtName.TESTFIELD(Name);

      VATStmtLine.FILTERGROUP := 2;
      VATStmtLine.SETRANGE("Statement Template Name",VATStmtTmpl.Name);
      VATStmtLine.FILTERGROUP := 0;

      VATStmtLine."Statement Template Name" := '';
      VATStmtLine."Statement Name" := VATStmtName.Name;
      PAGE.RUN(VATStmtTmpl."Page ID",VATStmtLine);
    END;

    [External]
    PROCEDURE OpenStmt@2(VAR CurrentStmtName@1000 : Code[10];VAR VATStmtLine@1001 : Record "VAT Statement Line");
    BEGIN
      CheckTemplateName(VATStmtLine.GETRANGEMAX("Statement Template Name"),CurrentStmtName);
      VATStmtLine.FILTERGROUP(2);
      VATStmtLine.SETRANGE("Statement Name",CurrentStmtName);
      VATStmtLine.FILTERGROUP(0);
    END;

    [External]
    PROCEDURE OpenStmtBatch@10(VAR VATStmtName@1005 : Record "VAT Statement Name");
    VAR
      VATStmtTmpl@1004 : Record "VAT Statement Template";
      VATStmtLine@1001 : Record "VAT Statement Line";
      JnlSelected@1003 : Boolean;
    BEGIN
      if VATStmtName.GETFILTER("Statement Template Name") <> '' then
        exit;
      VATStmtName.FILTERGROUP(2);
      if VATStmtName.GETFILTER("Statement Template Name") <> '' then begin
        VATStmtName.FILTERGROUP(0);
        exit;
      end;
      VATStmtName.FILTERGROUP(0);

      if not VATStmtName.FIND('-') then begin
        if not VATStmtTmpl.FINDFIRST then
          TemplateSelection(0,VATStmtLine,JnlSelected);
        if VATStmtTmpl.FINDFIRST then
          CheckTemplateName(VATStmtTmpl.Name,VATStmtName.Name);
      end;
      VATStmtName.FIND('-');
      JnlSelected := true;
      if VATStmtName.GETFILTER("Statement Template Name") <> '' then
        VATStmtTmpl.SETRANGE(Name,VATStmtName.GETFILTER("Statement Template Name"));
      case VATStmtTmpl.COUNT of
        1:
          VATStmtTmpl.FINDFIRST;
        else
          JnlSelected := PAGE.RUNMODAL(0,VATStmtTmpl) = ACTION::LookupOK;
      end;
      if not JnlSelected then
        ERROR('');

      VATStmtName.FILTERGROUP(0);
      VATStmtName.SETRANGE("Statement Template Name",VATStmtTmpl.Name);
      VATStmtName.FILTERGROUP(2);
    END;

    LOCAL PROCEDURE CheckTemplateName@3(CurrentStmtTemplateName@1000 : Code[10];VAR CurrentStmtName@1001 : Code[10]);
    VAR
      VATStmtTmpl@1002 : Record "VAT Statement Template";
      VATStmtName@1003 : Record "VAT Statement Name";
    BEGIN
      VATStmtName.SETRANGE("Statement Template Name",CurrentStmtTemplateName);
      if not VATStmtName.GET(CurrentStmtTemplateName,CurrentStmtName) then begin
        if not VATStmtName.FINDFIRST then begin
          VATStmtTmpl.GET(CurrentStmtTemplateName);
          VATStmtName.INIT;
          VATStmtName."Statement Template Name" := VATStmtTmpl.Name;
          VATStmtName.Name := Text002;
          VATStmtName.Description := Text003;
          VATStmtName.INSERT;
          COMMIT;
        end;
        CurrentStmtName := VATStmtName.Name;
      end;
    END;

    [External]
    PROCEDURE CheckName@4(CurrentStmtName@1000 : Code[10];VAR VATStmtLine@1001 : Record "VAT Statement Line");
    VAR
      VATStmtName@1002 : Record "VAT Statement Name";
    BEGIN
      VATStmtName.GET(VATStmtLine.GETRANGEMAX("Statement Template Name"),CurrentStmtName);
    END;

    [External]
    PROCEDURE SetName@5(CurrentStmtName@1000 : Code[10];VAR VATStmtLine@1001 : Record "VAT Statement Line");
    BEGIN
      VATStmtLine.FILTERGROUP(2);
      VATStmtLine.SETRANGE("Statement Name",CurrentStmtName);
      VATStmtLine.FILTERGROUP(0);
      if VATStmtLine.FIND('-') then;
    END;

    [External]
    PROCEDURE LookupName@6(CurrentStmtTemplateName@1000 : Code[10];CurrentStmtName@1001 : Code[10];VAR EntrdStmtName@1002 : Text[10]) : Boolean;
    VAR
      VATStmtName@1003 : Record "VAT Statement Name";
    BEGIN
      VATStmtName."Statement Template Name" := CurrentStmtTemplateName;
      VATStmtName.Name := CurrentStmtName;
      VATStmtName.FILTERGROUP(2);
      VATStmtName.SETRANGE("Statement Template Name",CurrentStmtTemplateName);
      VATStmtName.FILTERGROUP(0);
      if PAGE.RUNMODAL(0,VATStmtName) <> ACTION::LookupOK then
        exit(false);

      EntrdStmtName := VATStmtName.Name;
      exit(true);
    END;

    BEGIN
    END.
  }
}

