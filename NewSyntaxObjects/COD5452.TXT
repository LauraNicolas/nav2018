OBJECT Codeunit 5452 Graph Sync. Runner
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            RunFullSync;
          END;

  }
  CODE
  {
    VAR
      GraphDataSetup@1000 : Codeunit "Graph Data Setup";

    [External]
    PROCEDURE IsGraphSyncEnabled@4() : Boolean;
    VAR
      MarketingSetup@1000 : Record "Marketing Setup";
      AuxSyncEnabled@1001 : Boolean;
    BEGIN
      if CURRENTEXECUTIONMODE = EXECUTIONMODE::Debug then
        exit(false);

      if not MarketingSetup.GET then
        exit(false);

      if MarketingSetup."Sync with Microsoft Graph" then
        exit(true);

      OnCheckAuxiliarySyncEnabled(AuxSyncEnabled);
      exit(AuxSyncEnabled);
    END;

    [External]
    PROCEDURE RunDeltaSync@10();
    BEGIN
      OnRunGraphDeltaSync;
    END;

    [External]
    PROCEDURE RunDeltaSyncForEntity@3(TableID@1002 : Integer);
    VAR
      IntegrationTableMapping@1000 : Record "Integration Table Mapping";
      IntegrationMappingCode@1001 : Code[20];
    BEGIN
      IntegrationMappingCode := GraphDataSetup.GetMappingCodeForTable(TableID);
      GraphDataSetup.GetIntegrationTableMapping(IntegrationTableMapping,IntegrationMappingCode);
      if not IntegrationTableMapping.IsFullSyncAllowed then
        exit;

      IntegrationTableMapping.SetFullSyncStartAndCommit;
      RunIntegrationTableSynch(IntegrationTableMapping);

      IntegrationTableMapping.GET(IntegrationTableMapping.Name);
      IntegrationTableMapping.SetFullSyncEndAndCommit;
    END;

    [External]
    PROCEDURE RunFullSync@11();
    BEGIN
      OnRunGraphFullSync;
    END;

    [External]
    PROCEDURE RunFullSyncForEntity@2(TableID@1002 : Integer);
    VAR
      IntegrationTableMapping@1000 : Record "Integration Table Mapping";
      IntegrationMappingCode@1001 : Code[20];
    BEGIN
      IntegrationMappingCode := GraphDataSetup.GetMappingCodeForTable(TableID);
      GraphDataSetup.GetIntegrationTableMapping(IntegrationTableMapping,IntegrationMappingCode);
      if not IntegrationTableMapping.IsFullSyncAllowed then
        exit;
      IntegrationTableMapping.SetFullSyncStartAndCommit;

      IntegrationTableMapping."Graph Delta Token" := '';
      IntegrationTableMapping.MODIFY(true);

      CreateIntegrationRecordsForUncoupledRecords(IntegrationTableMapping."Table ID");
      RunIntegrationTableSynch(IntegrationTableMapping);

      IntegrationTableMapping.GET(IntegrationTableMapping.Name);
      IntegrationTableMapping.SetFullSyncEndAndCommit;
    END;

    [External]
    PROCEDURE RunIntegrationTableSynch@1(IntegrationTableMapping@1000 : Record "Integration Table Mapping");
    VAR
      IntegrationManagement@1001 : Codeunit "Integration Management";
      InsertEnabled@1005 : Boolean;
      ModifyEnabled@1004 : Boolean;
      DeleteEnabled@1003 : Boolean;
      RenameEnabled@1002 : Boolean;
    BEGIN
      IntegrationManagement.GetDatabaseTableTriggerSetup(
        IntegrationTableMapping."Table ID",InsertEnabled,ModifyEnabled,DeleteEnabled,RenameEnabled);
      CODEUNIT.RUN(IntegrationTableMapping."Synch. Codeunit ID",IntegrationTableMapping);
    END;

    LOCAL PROCEDURE CreateIntegrationRecordsForUncoupledRecords@7(TableId@1000 : Integer);
    VAR
      IntegrationRecord@1002 : Record "Integration Record";
      NavRecordRef@1001 : RecordRef;
    BEGIN
      NavRecordRef.OPEN(TableId);

      if NavRecordRef.FINDSET then
        repeat
          CLEAR(IntegrationRecord);
          IntegrationRecord.SETRANGE("Record ID",NavRecordRef.RECORDID);
          if IntegrationRecord.ISEMPTY then begin
            CLEAR(IntegrationRecord);
            IntegrationRecord."Record ID" := NavRecordRef.RECORDID;
            IntegrationRecord."Table ID" := NavRecordRef.NUMBER;
            IntegrationRecord.INSERT(true);
          end;
        until NavRecordRef.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnCheckAuxiliarySyncEnabled@8(VAR AuxSyncEnabled@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnRunGraphDeltaSync@6();
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnRunGraphFullSync@5();
    BEGIN
    END;

    BEGIN
    END.
  }
}

