OBJECT Page 1804 Approval Workflow Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Approval Workflow Setup;
    InsertAllowed=false;
    DeleteAllowed=false;
    LinksAllowed=false;
    SourceTable="Approval Workflow Wizard";
    PageType=NavigatePage;
    SourceTableTemporary=true;
    ShowFilter=false;
    OnInit=BEGIN
             if not GET then begin
               INIT;
               "Use Exist. Approval User Setup" := true;
               SetDefaultValues;
               INSERT;
             end;
             LoadTopBanners;
           END;

    OnOpenPage=VAR
                 ApprovalUserSetup@1000 : Page "Approval User Setup";
               BEGIN
                 ShowIntroStep;
                 ApprovalUserSetupLabel := STRSUBSTNO(OpenPageTxt,ApprovalUserSetup.CAPTION);
               END;

    OnQueryClosePage=VAR
                       AssistedSetup@1001 : Record "Assisted Setup";
                     BEGIN
                       if CloseAction = ACTION::OK then
                         if AssistedSetup.GetStatus(PAGE::"Approval Workflow Setup Wizard") = AssistedSetup.Status::"Not Completed" then
                           if not CONFIRM(NAVNotSetUpQst,false) then
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 8       ;    ;ActionContainer;
                      Name=ActionContainer8;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=Back;
                      CaptionML=ENU=Back;
                      ApplicationArea=#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=true;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(true);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=Next;
                      CaptionML=ENU=Next;
                      ApplicationArea=#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=true;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 if Step = Step::"Approval User Setup" then
                                   ValidateApproverUserSetup("Approver ID");
                                 NextStep(false);
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=ENU=Finish;
                      ApplicationArea=#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=true;
                      Image=Approve;
                      OnAction=VAR
                                 AssistedSetup@1004 : Record "Assisted Setup";
                                 ApprovalWorkflowSetupMgt@1001 : Codeunit "Approval Workflow Setup Mgt.";
                               BEGIN
                                 ApprovalWorkflowSetupMgt.ApplyInitialWizardUserInput(Rec);
                                 AssistedSetup.SetStatus(PAGE::"Approval Workflow Setup Wizard",AssistedSetup.Status::Completed);

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Name=Group96;
                Visible=TopBannerVisible AND NOT DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                Name=MediaResourcesStandard;
                ApplicationArea=#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=false }

    { 98  ;1   ;Group     ;
                Name=Group98;
                Visible=TopBannerVisible AND DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                Name=MediaResourcesDone;
                ApplicationArea=#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=false }

    { 11  ;1   ;Group     ;
                Name=Step1;
                CaptionML=ENU="";
                Visible=IntroVisible;
                GroupType=Group }

    { 18  ;2   ;Group     ;
                Name=Para1.1;
                CaptionML=ENU=Welcome to Approval Workflow Setup;
                GroupType=Group }

    { 19  ;3   ;Group     ;
                Name=Para1.1.1;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=You can create approval workflows that automatically notify an approver when a user tries to create or change certain values on documents, journal lines, or cards, such as an amount above a specified limit. }

    { 20  ;3   ;Group     ;
                Name=Para1.1.2;
                CaptionML=ENU="";
                GroupType=Group;
                InstructionalTextML=ENU=To enable typical approval workflows, such as for amounts on purchase invoices, you must specify basic settings, such as the approver, the amount limit and when the approval is due. More advanced settings are preset on the related approval workflow, which you can modify later. }

    { 21  ;2   ;Group     ;
                Name=Para1.2;
                CaptionML=ENU=Let's go!;
                GroupType=Group;
                InstructionalTextML=ENU=Choose Next to select from a list of typical approval workflows and then start specifying basic settings for each workflow. }

    { 2   ;1   ;Group     ;
                Name=Step2;
                CaptionML=ENU="";
                Visible=ApprovalDocumentTypesVisible;
                GroupType=Group }

    { 22  ;2   ;Group     ;
                Name=Para2.1;
                CaptionML=ENU=Which approval workflow do you want to set up?;
                GroupType=Group }

    { 3   ;3   ;Field     ;
                Name=Purchase Invoice Approval;
                CaptionML=ENU=Purchase Invoice Approval;
                ApplicationArea=#Suite;
                SourceExpr="Purch Invoice App. Workflow";
                OnValidate=BEGIN
                             NextEnabled := "Purch Invoice App. Workflow" or "Sales Invoice App. Workflow"
                           END;

                ImplicitType=Boolean }

    { 16  ;3   ;Field     ;
                Name=Sales Invoice Approval;
                CaptionML=ENU=Sales Invoice Approval;
                ApplicationArea=#Suite;
                SourceExpr="Sales Invoice App. Workflow";
                OnValidate=BEGIN
                             NextEnabled := "Purch Invoice App. Workflow" or "Sales Invoice App. Workflow"
                           END;

                ImplicitType=Boolean }

    { 32  ;1   ;Group     ;
                Name=Step3;
                CaptionML=ENU="";
                Visible=UseExistingApprovalSetupVisible;
                GroupType=Group }

    { 33  ;2   ;Group     ;
                Name=Para3.1;
                CaptionML=ENU=An approval user setup already exists.;
                GroupType=Group }

    { 36  ;3   ;Field     ;
                Name=Use Exist. Approval User Setup;
                CaptionML=ENU=Use the existing setup;
                ApplicationArea=#Suite;
                SourceExpr="Use Exist. Approval User Setup";
                ImplicitType=Boolean }

    { 34  ;3   ;Field     ;
                Name=ApprovalUserSetupLabel;
                ApplicationArea=#Suite;
                SourceExpr=ApprovalUserSetupLabel;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              PAGE.RUNMODAL(PAGE::"Approval User Setup");
                            END;

                ImplicitType=Text;
                ShowCaption=false }

    { 4   ;1   ;Group     ;
                Name=Step4;
                CaptionML=ENU="";
                Visible=ApprovalUserSetupVisible;
                GroupType=Group }

    { 12  ;2   ;Group     ;
                Name=Para4.1;
                CaptionML=ENU=To set up the approval users, answer the following questions.;
                Visible=PurchInvoiceApprovalDetailsVisible OR SalesInvoiceApprovalDetailsVisible;
                GroupType=Group }

    { 37  ;3   ;Field     ;
                Name=Who is the approver?;
                CaptionML=ENU=Who is the approver?;
                ApplicationArea=#Suite;
                SourceExpr="Approver ID";
                TableRelation=User."User Name";
                Enabled="Purch Invoice App. Workflow" OR "Sales Invoice App. Workflow";
                ImplicitType=Code50 }

    { 23  ;2   ;Group     ;
                Name=Para4.2;
                CaptionML=ENU="";
                Visible=PurchInvoiceApprovalDetailsVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Which amount can the user enter on purchase invoices before the invoice requires approval? }

    { 5   ;3   ;Field     ;
                Name=Purch Amount Approval Limit;
                CaptionML=ENU=Amount Limit;
                ApplicationArea=#Suite;
                SourceExpr="Purch Amount Approval Limit";
                ImplicitType=Integer }

    { 24  ;2   ;Group     ;
                Name=Para4.3;
                CaptionML=ENU="";
                Visible=SalesInvoiceApprovalDetailsVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Which amount can the user enter on sales invoices before the invoice requires approval? }

    { 13  ;3   ;Field     ;
                Name=Sales Amount Approval Limit;
                CaptionML=ENU=Amount Limit;
                ApplicationArea=#Suite;
                SourceExpr="Sales Amount Approval Limit";
                ImplicitType=Integer }

    { 10  ;1   ;Group     ;
                Name=Step5;
                CaptionML=ENU="";
                Visible=DoneVisible;
                GroupType=Group }

    { 25  ;2   ;Group     ;
                Name=Para5.1;
                CaptionML=ENU=That's it!;
                GroupType=Group;
                InstructionalTextML=ENU=Choose Finish to enable the selected approval workflows with the specified settings. }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1040 : Record "Media Repository";
      MediaRepositoryDone@1041 : Record "Media Repository";
      MediaResourcesStandard@1002 : Record "Media Resources";
      MediaResourcesDone@1000 : Record "Media Resources";
      Step@1015 : 'Intro,"Approval Document Types","Use Existing Approval User Setup","Approval User Setup",Done';
      BackEnabled@1014 : Boolean;
      NextEnabled@1013 : Boolean;
      FinishEnabled@1010 : Boolean;
      TopBannerVisible@1042 : Boolean;
      IntroVisible@1009 : Boolean;
      ApprovalDocumentTypesVisible@1008 : Boolean;
      UseExistingApprovalSetupVisible@1017 : Boolean;
      ApprovalUserSetupVisible@1011 : Boolean;
      SalesInvoiceApprovalDetailsVisible@1003 : Boolean;
      PurchInvoiceApprovalDetailsVisible@1006 : Boolean;
      DoneVisible@1004 : Boolean;
      NAVNotSetUpQst@1005 : TextConst 'ENU=Document Approval has not been set up.\\Are you sure that you want to exit?';
      NoUnlimitedApproverErr@1001 : TextConst 'ENU=Select a user that has unlimited approval rights.';
      OpenPageTxt@1012 : TextConst '@@@=%1 is the page that will be opened when clicking the control;ENU=Open %1';
      ApprovalUserSetupLabel@1016 : Text;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      if Backwards then
        Step := Step - 1
      else
        Step := Step + 1;

      case Step of
        Step::Intro:
          ShowIntroStep;
        Step::"Approval Document Types":
          ShowApprovalDocumentTypesStep;
        Step::"Use Existing Approval User Setup":
          begin
            ShowUseExistingApprovalSetupQstStep;
            if not UseExistingApprovalSetupVisible then
              NextStep(Backwards)
          end;
        Step::"Approval User Setup":
          if not "Use Exist. Approval User Setup" then
            ShowApprovalUserSetupDetailsStep
          else
            NextStep(Backwards);
        Step::Done:
          ShowDoneStep;
      end;
      CurrPage.UPDATE(true);
    END;

    LOCAL PROCEDURE ShowIntroStep@1();
    BEGIN
      ResetWizardControls;
      IntroVisible := true;
      BackEnabled := false;
    END;

    LOCAL PROCEDURE ShowApprovalDocumentTypesStep@2();
    BEGIN
      ResetWizardControls;
      ApprovalDocumentTypesVisible := true;
      NextEnabled := "Purch Invoice App. Workflow" or "Sales Invoice App. Workflow";
    END;

    LOCAL PROCEDURE ShowApprovalUserSetupDetailsStep@9();
    BEGIN
      ResetWizardControls;
      ApprovalUserSetupVisible := not "Use Exist. Approval User Setup";
      SalesInvoiceApprovalDetailsVisible := not "Use Exist. Approval User Setup" and "Sales Invoice App. Workflow";
      PurchInvoiceApprovalDetailsVisible := not "Use Exist. Approval User Setup" and "Purch Invoice App. Workflow";
    END;

    LOCAL PROCEDURE ShowUseExistingApprovalSetupQstStep@33();
    VAR
      ApprovalUserSetup@1000 : Record "User Setup";
    BEGIN
      ResetWizardControls;
      UseExistingApprovalSetupVisible := not ApprovalUserSetup.ISEMPTY;
      if not UseExistingApprovalSetupVisible then
        "Use Exist. Approval User Setup" := false;
    END;

    LOCAL PROCEDURE ShowDoneStep@6();
    BEGIN
      ResetWizardControls;
      DoneVisible := true;
      NextEnabled := false;
      FinishEnabled := true;
    END;

    LOCAL PROCEDURE ResetWizardControls@10();
    BEGIN
      // Buttons
      BackEnabled := true;
      NextEnabled := true;
      FinishEnabled := false;

      // Tabs
      IntroVisible := false;
      ApprovalDocumentTypesVisible := false;
      UseExistingApprovalSetupVisible := false;
      ApprovalUserSetupVisible := false;
      PurchInvoiceApprovalDetailsVisible := false;
      SalesInvoiceApprovalDetailsVisible := false;
      DoneVisible := false;
    END;

    LOCAL PROCEDURE ValidateApproverUserSetup@5(UserName@1001 : Code[50]);
    BEGIN
      if UserName = '' then
        ERROR(NoUnlimitedApproverErr);
    END;

    LOCAL PROCEDURE SetDefaultValues@8();
    VAR
      ApprovalUserSetup@1003 : Record "User Setup";
      Workflow@1000 : Record Workflow;
      WorkflowSetup@1002 : Codeunit "Workflow Setup";
      WorkflowCode@1001 : Code[20];
    BEGIN
      // Specific Purchase Invoice Approval Workflow: WZ-PIAPW
      WorkflowCode := WorkflowSetup.GetWorkflowWizardCode(WorkflowSetup.PurchaseInvoiceApprovalWorkflowCode);
      if Workflow.GET(WorkflowCode) then
        "Purch Invoice App. Workflow" := true;

      // Specific Sales Invoice Approval Workflow: WZ-SIAPW
      WorkflowCode := WorkflowSetup.GetWorkflowWizardCode(WorkflowSetup.SalesInvoiceApprovalWorkflowCode);
      if Workflow.GET(WorkflowCode) then
        "Sales Invoice App. Workflow" := true;

      ApprovalUserSetup.SETRANGE("Unlimited Sales Approval",true);
      ApprovalUserSetup.SETRANGE("Unlimited Purchase Approval",true);
      if ApprovalUserSetup.FINDFIRST then
        "Approver ID" := ApprovalUserSetup."User ID";

      "Purch Amount Approval Limit" := ApprovalUserSetup.GetDefaultPurchaseAmountApprovalLimit;
      "Sales Amount Approval Limit" := ApprovalUserSetup.GetDefaultSalesAmountApprovalLimit;
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      if MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE)) and
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE))
      then
        if MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") and
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        then
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    BEGIN
    END.
  }
}

