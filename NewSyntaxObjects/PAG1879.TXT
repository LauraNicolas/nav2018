OBJECT Page 1879 VAT Product Posting Grp Part
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=VAT Product Posting Grp Part;
    SourceTable="VAT Setup Posting Groups";
    PageType=ListPart;
    OnOpenPage=BEGIN
                 VATNotification.ID := FORMAT(CREATEGUID);
                 PopulateVATProdGroups;
                 ShowVATRates;
               END;

    OnNewRecord=BEGIN
                  VALIDATE(Selected,true);
                  VALIDATE("Application Type","Application Type"::Items);
                END;

    OnInsertRecord=BEGIN
                     if VATAccountsGroup or VATClausesGroup then
                       ERROR(VATAddIsNotallowedErr);
                   END;

    OnDeleteRecord=BEGIN
                     if CheckExistingItemAndServiceWithVAT("VAT Prod. Posting Group","Application Type" = "Application Type"::Services ) then begin
                       TrigerNotification(VATDeleteIsNotallowedErr);
                       exit(false);
                     end;
                     if VATAccountsGroup or VATClausesGroup then begin
                       SETRANGE(Selected,true);
                       if COUNT = 1 then begin
                         TrigerNotification(VATEmptyErrorMsg);
                         exit(false);
                       end;
                     end;
                   END;

    ActionList=ACTIONS
    {
      { 3       ;    ;ActionContainer;
                      Name=ActionContainer3;
                      ActionContainerType=ActionItems }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 9   ;2   ;Field     ;
                Name=Selected;
                ToolTipML=ENU=Specifies whether to include the VAT product posting group on the line.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Selected;
                Visible=VATRatesGroup;
                OnValidate=BEGIN
                             if Selected then
                               exit;

                             if CheckExistingItemAndServiceWithVAT(xRec."VAT Prod. Posting Group",xRec."Application Type" = "Application Type"::Services) then begin
                               TrigerNotification(VATDeleteIsNotallowedErr);
                               ERROR('');
                             end;
                           END;

                ImplicitType=Boolean }

    { 4   ;2   ;Field     ;
                Name=VAT Prod. Posting Group;
                ToolTipML=ENU=Specifies the VAT product posting group. Links business transactions made for the item, resource, or G/L account with the general ledger, to account for VAT amounts resulting from trade with that record.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Prod. Posting Group";
                OnValidate=BEGIN
                             if CheckExistingItemAndServiceWithVAT(xRec."VAT Prod. Posting Group",xRec."Application Type" = "Application Type"::Services) then begin
                               TrigerNotification(VATDeleteIsNotallowedErr);
                               ERROR('');
                             end;
                           END;

                ImplicitType=Code20 }

    { 12  ;2   ;Field     ;
                Name=Application Type;
                ToolTipML=ENU=Specifies how a cost recipient is linked to its cost source to provide cost forwarding according to the costing method.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Application Type";
                Visible=VATRatesGroup;
                OnValidate=BEGIN
                             if CheckExistingItemAndServiceWithVAT(xRec."VAT Prod. Posting Group",xRec."Application Type" = "Application Type"::Services) then begin
                               TrigerNotification(VATDeleteIsNotallowedErr);
                               ERROR('');
                             end;
                           END;

                ImplicitType=Option }

    { 10  ;2   ;Field     ;
                Name=VAT Prod. Posting Grp Desc.;
                ToolTipML=ENU=Specifies a description of the VAT product posting group.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Prod. Posting Grp Desc.";
                Visible=VATRatesGroup;
                ImplicitType=Text50 }

    { 5   ;2   ;Field     ;
                Name=VAT %;
                Width=3;
                ToolTipML=ENU=Specifies the VAT percentage used.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT %";
                Visible=VATRatesGroup;
                ImplicitType=Decimal }

    { 6   ;2   ;Field     ;
                Name=Sales VAT Account;
                ToolTipML=ENU=Specifies the general ledger account number to which to post sales VAT, for the particular combination of VAT business posting group and VAT product posting group.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales VAT Account";
                Visible=VATAccountsGroup;
                ImplicitType=Code20 }

    { 7   ;2   ;Field     ;
                Name=Purchase VAT Account;
                ToolTipML=ENU=Specifies the general ledger account number to which to post purchase VAT.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Purchase VAT Account";
                Visible=VATAccountsGroup;
                ImplicitType=Code20 }

    { 8   ;2   ;Field     ;
                Name=Reverse Chrg. VAT Acc.;
                ToolTipML=ENU=Specifies the general ledger account number to which you want to post reverse charge VAT (purchase VAT) for this combination of VAT business posting group and VAT product posting group, if you have selected the Reverse Charge VAT option in the VAT Calculation Type field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Reverse Chrg. VAT Acc.";
                Visible=VATAccountsGroup;
                ImplicitType=Code20 }

    { 11  ;2   ;Field     ;
                Name=VAT Clause Desc;
                ToolTipML=ENU=Specifies a description of the VAT clause.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Clause Desc";
                Visible=VATClausesGroup;
                ImplicitType=Text250 }

  }
  CODE
  {
    VAR
      VATNotification@1007 : Notification;
      VATRatesGroup@1000 : Boolean;
      VATAccountsGroup@1001 : Boolean;
      VATClausesGroup@1002 : Boolean;
      VATAddIsNotallowedErr@1003 : TextConst 'ENU=You can''t add accounts now because they won''t have settings like VAT rates. Go back to the VAT Rates for Items and Services page, add a line, and continue.';
      VATDeleteIsNotallowedErr@1004 : TextConst 'ENU=You can''t delete or modify this VAT record because it is connected to existing item.';
      VATEmptyErrorMsg@1009 : TextConst 'ENU=You can''t delete the record because the VAT setup would be empty.';

    PROCEDURE ShowVATRates@1();
    BEGIN
      ResetView;
      VATRatesGroup := true;
      RESET;
      CurrPage.UPDATE;
    END;

    PROCEDURE ShowVATAccounts@2();
    BEGIN
      ResetView;
      VATAccountsGroup := true;
      ShowOnlySelectedSrvItem;
    END;

    PROCEDURE ShowVATClauses@3();
    BEGIN
      ResetView;
      VATClausesGroup := true;
      ShowOnlySelectedSrvItem;
    END;

    LOCAL PROCEDURE ResetView@4();
    BEGIN
      VATNotification.RECALL;
      VATRatesGroup := false;
      VATAccountsGroup := false;
      VATClausesGroup := false;
    END;

    LOCAL PROCEDURE ShowOnlySelectedSrvItem@13();
    BEGIN
      SETRANGE(Selected,true);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE TrigerNotification@15(NotificationMsg@1000 : Text);
    BEGIN
      VATNotification.RECALL;
      VATNotification.MESSAGE(NotificationMsg);
      VATNotification.SEND;
    END;

    PROCEDURE HideNotification@5();
    VAR
      DummyGuid@1000 : GUID;
    BEGIN
      if VATNotification.ID = DummyGuid then
        exit;
      VATNotification.MESSAGE := '';
      VATNotification.RECALL;
    END;

    BEGIN
    END.
  }
}

