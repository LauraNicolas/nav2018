OBJECT Codeunit 99000845 Reservation Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Item Ledger Entry"=rm,
                TableData "Reservation Entry"=rimd,
                TableData "Action Message Entry"=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Firm Planned %1';
      Text001@1001 : TextConst 'ENU=Released %1';
      Text003@1002 : TextConst 'ENU=CU99000845: CalculateRemainingQty - Source type missing';
      Text004@1003 : TextConst 'ENU=Codeunit 99000845: Illegal FieldFilter parameter';
      Text006@1005 : TextConst 'ENU=Outbound,Inbound';
      Text007@1006 : TextConst 'ENU=CU99000845 DeleteReservEntries2: Surplus order tracking double record detected.';
      CalcReservEntry@1007 : Record "Reservation Entry";
      CalcReservEntry2@1008 : Record "Reservation Entry";
      ForItemLedgEntry@1009 : Record "Item Ledger Entry";
      CalcItemLedgEntry@1010 : Record "Item Ledger Entry";
      ForSalesLine@1011 : Record "Sales Line";
      CalcSalesLine@1012 : Record "Sales Line";
      ForPurchLine@1013 : Record "Purchase Line";
      CalcPurchLine@1014 : Record "Purchase Line";
      ForItemJnlLine@1015 : Record "Item Journal Line";
      ForReqLine@1016 : Record "Requisition Line";
      CalcReqLine@1017 : Record "Requisition Line";
      ForProdOrderLine@1018 : Record "Prod. Order Line";
      CalcProdOrderLine@1019 : Record "Prod. Order Line";
      ForProdOrderComp@1020 : Record "Prod. Order Component";
      CalcProdOrderComp@1021 : Record "Prod. Order Component";
      ForPlanningComponent@1022 : Record "Planning Component";
      CalcPlanningComponent@1023 : Record "Planning Component";
      ForAssemblyHeader@1070 : Record "Assembly Header";
      CalcAssemblyHeader@1073 : Record "Assembly Header";
      ForAssemblyLine@1071 : Record "Assembly Line";
      CalcAssemblyLine@1072 : Record "Assembly Line";
      ForTransLine@1024 : Record "Transfer Line";
      CalcTransLine@1025 : Record "Transfer Line";
      ForServiceLine@1026 : Record "Service Line";
      CalcServiceLine@1027 : Record "Service Line";
      ForJobPlanningLine@1067 : Record "Job Planning Line";
      CalcJobPlanningLine@1066 : Record "Job Planning Line";
      ActionMessageEntry@1028 : Record "Action Message Entry";
      Item@1029 : Record Item;
      Location@1061 : Record Location;
      MfgSetup@1030 : Record "Manufacturing Setup";
      SKU@1031 : Record "Stockkeeping Unit";
      ItemTrackingCode@1004 : Record "Item Tracking Code";
      TempTrackingSpecification@1055 : TEMPORARY Record "Tracking Specification";
      CallTrackingSpecification@1056 : Record "Tracking Specification";
      ForJobJnlLine@1065 : Record "Job Journal Line";
      CreateReservEntry@1032 : Codeunit "Create Reserv. Entry";
      ReservEngineMgt@1033 : Codeunit "Reservation Engine Mgt.";
      ReserveSalesLine@1034 : Codeunit "Sales Line-Reserve";
      ReserveReqLine@1035 : Codeunit "Req. Line-Reserve";
      ReservePurchLine@1036 : Codeunit "Purch. Line-Reserve";
      ReserveItemJnlLine@1037 : Codeunit "Item Jnl. Line-Reserve";
      ReserveProdOrderLine@1038 : Codeunit "Prod. Order Line-Reserve";
      ReserveProdOrderComp@1039 : Codeunit "Prod. Order Comp.-Reserve";
      AssemblyHeaderReserve@1075 : Codeunit "Assembly Header-Reserve";
      AssemblyLineReserve@1074 : Codeunit "Assembly Line-Reserve";
      ReservePlanningComponent@1040 : Codeunit "Plng. Component-Reserve";
      ReserveServiceInvLine@1041 : Codeunit "Service Line-Reserve";
      ReserveTransLine@1042 : Codeunit "Transfer Line-Reserve";
      JobPlanningLineReserve@1068 : Codeunit "Job Planning Line-Reserve";
      GetPlanningParameters@1043 : Codeunit "Planning-Get Parameters";
      CreatePick@1058 : Codeunit "Create Pick";
      Positive@1044 : Boolean;
      CurrentBindingIsSet@1045 : Boolean;
      HandleItemTracking@1060 : Boolean;
      InvSearch@1046 : Text[1];
      FieldFilter@1047 : Text[80];
      InvNextStep@1048 : Integer;
      ValueArray@1049 : ARRAY [18] OF Integer;
      CurrentBinding@1050 : ',"Order-to-Order"';
      ItemTrackingHandling@1051 : 'None,"Allow deletion",Match';
      Text008@1053 : TextConst 'ENU=Item tracking defined for item %1 in the %2 accounts for more than the quantity you have entered.\You must adjust the existing item tracking and then reenter the new quantity.';
      Text009@1052 : TextConst 'ENU=Item Tracking cannot be fully matched.\Serial No.: %1, Lot No.: %2, outstanding quantity: %3.';
      Text010@1054 : TextConst 'ENU=Item tracking is defined for item %1 in the %2.\You must delete the existing item tracking before modifying or deleting the %2.';
      TotalAvailQty@1062 : Decimal;
      QtyAllocInWhse@1057 : Decimal;
      QtyOnOutBound@1059 : Decimal;
      Text011@1063 : TextConst 'ENU=Item tracking is defined for item %1 in the %2.\Do you want to delete the %2 and the item tracking lines?';
      QtyReservedOnPickShip@1069 : Decimal;
      AssemblyTxt@1076 : TextConst 'ENU=Assembly';
      DeleteItemReservationQst@1064 : TextConst '@@@="%1 = Document Type, %2 = Document No.";ENU=%1 %2 has item reservation. Do you want to delete it anyway?';

    [External]
    PROCEDURE IsPositive@18() : Boolean;
    BEGIN
      exit(Positive);
    END;

    [External]
    PROCEDURE FormatQty@22(Quantity@1000 : Decimal) : Decimal;
    BEGIN
      if Positive then
        exit(Quantity);

      exit(-Quantity);
    END;

    [External]
    PROCEDURE SetCalcReservEntry@70(TrackingSpecification@1000 : Record "Tracking Specification";VAR ReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      // Late Binding
      CalcReservEntry.TRANSFERFIELDS(TrackingSpecification);
      SourceQuantity(CalcReservEntry,true);
      CalcReservEntry.CopyTrackingFromSpec(TrackingSpecification);
      ReservEntry := CalcReservEntry;
      HandleItemTracking := true;
    END;

    [External]
    PROCEDURE SetSalesLine@1(NewSalesLine@1000 : Record "Sales Line");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForSalesLine := NewSalesLine;

      CalcReservEntry.SetSource(
        DATABASE::"Sales Line",ForSalesLine."Document Type",NewSalesLine."Document No.",NewSalesLine."Line No.",'',0);
      CalcReservEntry.SetItemData(
        NewSalesLine."No.",NewSalesLine.Description,NewSalesLine."Location Code",NewSalesLine."Variant Code",
        NewSalesLine."Qty. per Unit of Measure");
      if NewSalesLine.Type <> NewSalesLine.Type::Item then
        CalcReservEntry."Item No." := '';
      CalcReservEntry."Expected Receipt Date" := NewSalesLine."Shipment Date";
      CalcReservEntry."Shipment Date" := NewSalesLine."Shipment Date";

      UpdateReservation((CreateReservEntry.SignFactor(CalcReservEntry) * ForSalesLine."Outstanding Qty. (Base)") <= 0);
    END;

    [External]
    PROCEDURE SetReqLine@13(NewReqLine@1000 : Record "Requisition Line");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForReqLine := NewReqLine;

      CalcReservEntry.SetSource(
        DATABASE::"Requisition Line",0,NewReqLine."Worksheet Template Name",NewReqLine."Line No.",NewReqLine."Journal Batch Name",0);
      CalcReservEntry.SetItemData(
        NewReqLine."No.",NewReqLine.Description,NewReqLine."Location Code",NewReqLine."Variant Code",
        NewReqLine."Qty. per Unit of Measure");
      if NewReqLine.Type <> NewReqLine.Type::Item then
        CalcReservEntry."Item No." := '';
      CalcReservEntry."Expected Receipt Date" := NewReqLine."Due Date";
      CalcReservEntry."Shipment Date" := NewReqLine."Due Date";
      CalcReservEntry."Planning Flexibility" := NewReqLine."Planning Flexibility";

      UpdateReservation(ForReqLine."Net Quantity (Base)" < 0);
    END;

    [External]
    PROCEDURE SetPurchLine@2(NewPurchLine@1000 : Record "Purchase Line");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForPurchLine := NewPurchLine;

      CalcReservEntry.SetSource(
        DATABASE::"Purchase Line",ForPurchLine."Document Type",NewPurchLine."Document No.",NewPurchLine."Line No.",'',0);
      CalcReservEntry.SetItemData(
        NewPurchLine."No.",NewPurchLine.Description,NewPurchLine."Location Code",NewPurchLine."Variant Code",
        NewPurchLine."Qty. per Unit of Measure");
      if NewPurchLine.Type <> NewPurchLine.Type::Item then
        CalcReservEntry."Item No." := '';
      CalcReservEntry."Expected Receipt Date" := NewPurchLine."Expected Receipt Date";
      CalcReservEntry."Shipment Date" := NewPurchLine."Expected Receipt Date";
      CalcReservEntry."Planning Flexibility" := NewPurchLine."Planning Flexibility";

      UpdateReservation((CreateReservEntry.SignFactor(CalcReservEntry) * ForPurchLine."Outstanding Qty. (Base)") < 0);
    END;

    [External]
    PROCEDURE SetItemJnlLine@3(NewItemJnlLine@1000 : Record "Item Journal Line");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForItemJnlLine := NewItemJnlLine;

      CalcReservEntry.SetSource(
        DATABASE::"Item Journal Line",ForItemJnlLine."Entry Type",NewItemJnlLine."Journal Template Name",
        NewItemJnlLine."Line No.",NewItemJnlLine."Journal Batch Name",0);
      CalcReservEntry.SetItemData(
        NewItemJnlLine."Item No.",NewItemJnlLine.Description,NewItemJnlLine."Location Code",NewItemJnlLine."Variant Code",
        NewItemJnlLine."Qty. per Unit of Measure");
      CalcReservEntry."Expected Receipt Date" := NewItemJnlLine."Posting Date";
      CalcReservEntry."Shipment Date" := NewItemJnlLine."Posting Date";

      UpdateReservation((CreateReservEntry.SignFactor(CalcReservEntry) * ForItemJnlLine."Quantity (Base)") < 0);
    END;

    [External]
    PROCEDURE SetProdOrderLine@9(NewProdOrderLine@1000 : Record "Prod. Order Line");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForProdOrderLine := NewProdOrderLine;

      CalcReservEntry.SetSource(
        DATABASE::"Prod. Order Line",ForProdOrderLine.Status,ForProdOrderLine."Prod. Order No.",0,'',NewProdOrderLine."Line No.");
      CalcReservEntry.SetItemData(
        NewProdOrderLine."Item No.",NewProdOrderLine.Description,NewProdOrderLine."Location Code",NewProdOrderLine."Variant Code",
        NewProdOrderLine."Qty. per Unit of Measure");
      CalcReservEntry."Expected Receipt Date" := NewProdOrderLine."Due Date";
      CalcReservEntry."Shipment Date" := NewProdOrderLine."Due Date";
      CalcReservEntry."Planning Flexibility" := NewProdOrderLine."Planning Flexibility";

      UpdateReservation(ForProdOrderLine."Remaining Qty. (Base)" < 0);
    END;

    [External]
    PROCEDURE SetProdOrderComponent@11(NewProdOrderComp@1000 : Record "Prod. Order Component");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForProdOrderComp := NewProdOrderComp;

      CalcReservEntry.SetSource(
        DATABASE::"Prod. Order Component",NewProdOrderComp.Status,NewProdOrderComp."Prod. Order No.",
        NewProdOrderComp."Line No.",'',NewProdOrderComp."Prod. Order Line No.");
      CalcReservEntry.SetItemData(
        NewProdOrderComp."Item No.",NewProdOrderComp.Description,NewProdOrderComp."Location Code",NewProdOrderComp."Variant Code",
        NewProdOrderComp."Qty. per Unit of Measure");
      CalcReservEntry."Expected Receipt Date" := NewProdOrderComp."Due Date";
      CalcReservEntry."Shipment Date" := NewProdOrderComp."Due Date";

      UpdateReservation(ForProdOrderComp."Remaining Qty. (Base)" > 0);
    END;

    [External]
    PROCEDURE SetAssemblyHeader@105(NewAssemblyHeader@1000 : Record "Assembly Header");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForAssemblyHeader := NewAssemblyHeader;

      CalcReservEntry.SetSource(
        DATABASE::"Assembly Header",ForAssemblyHeader."Document Type",NewAssemblyHeader."No.",0,'',0);
      CalcReservEntry.SetItemData(
        NewAssemblyHeader."Item No.",NewAssemblyHeader.Description,NewAssemblyHeader."Location Code",NewAssemblyHeader."Variant Code",
        NewAssemblyHeader."Qty. per Unit of Measure");
      CalcReservEntry."Expected Receipt Date" := NewAssemblyHeader."Due Date";
      CalcReservEntry."Shipment Date" := NewAssemblyHeader."Due Date";

      UpdateReservation((CreateReservEntry.SignFactor(CalcReservEntry) * ForAssemblyHeader."Remaining Quantity (Base)") < 0);
    END;

    [External]
    PROCEDURE SetAssemblyLine@106(NewAssemblyLine@1000 : Record "Assembly Line");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForAssemblyLine := NewAssemblyLine;

      CalcReservEntry.SetSource(
        DATABASE::"Assembly Line",ForAssemblyLine."Document Type",NewAssemblyLine."Document No.",NewAssemblyLine."Line No.",'',0);
      CalcReservEntry.SetItemData(
        NewAssemblyLine."No.",NewAssemblyLine.Description,NewAssemblyLine."Location Code",NewAssemblyLine."Variant Code",
        NewAssemblyLine."Qty. per Unit of Measure");
      if NewAssemblyLine.Type <> NewAssemblyLine.Type::Item then
        CalcReservEntry."Item No." := '';
      CalcReservEntry."Expected Receipt Date" := NewAssemblyLine."Due Date";
      CalcReservEntry."Shipment Date" := NewAssemblyLine."Due Date";

      UpdateReservation((CreateReservEntry.SignFactor(CalcReservEntry) * ForAssemblyLine."Remaining Quantity (Base)") < 0);
    END;

    [External]
    PROCEDURE SetPlanningComponent@12(NewPlanningComponent@1000 : Record "Planning Component");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForPlanningComponent := NewPlanningComponent;

      CalcReservEntry.SetSource(
        DATABASE::"Planning Component",0,NewPlanningComponent."Worksheet Template Name",
        NewPlanningComponent."Line No.",NewPlanningComponent."Worksheet Batch Name",NewPlanningComponent."Worksheet Line No.");
      CalcReservEntry.SetItemData(
        NewPlanningComponent."Item No.",NewPlanningComponent.Description,NewPlanningComponent."Location Code",
        NewPlanningComponent."Variant Code",NewPlanningComponent."Qty. per Unit of Measure");
      CalcReservEntry."Expected Receipt Date" := NewPlanningComponent."Due Date";
      CalcReservEntry."Shipment Date" := NewPlanningComponent."Due Date";

      UpdateReservation(ForPlanningComponent."Net Quantity (Base)" > 0);
    END;

    [External]
    PROCEDURE SetItemLedgEntry@34(NewItemLedgEntry@1000 : Record "Item Ledger Entry");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForItemLedgEntry := NewItemLedgEntry;

      CalcReservEntry.SetSource(DATABASE::"Item Ledger Entry",0,'',NewItemLedgEntry."Entry No.",'',0);

      CalcReservEntry.SetItemData(
        NewItemLedgEntry."Item No.",NewItemLedgEntry.Description,NewItemLedgEntry."Location Code",NewItemLedgEntry."Variant Code",
        NewItemLedgEntry."Qty. per Unit of Measure");
      CalcReservEntry.CopyTrackingFromItemLedgEntry(NewItemLedgEntry);

      Positive := ForItemLedgEntry."Remaining Quantity" <= 0;
      if Positive then begin
        CalcReservEntry."Expected Receipt Date" := DMY2DATE(31,12,9999);
        CalcReservEntry."Shipment Date" := DMY2DATE(31,12,9999);
      end else begin
        CalcReservEntry."Expected Receipt Date" := 0D;
        CalcReservEntry."Shipment Date" := 0D;
      end;

      UpdateReservation(Positive);
    END;

    [External]
    PROCEDURE SetTransferLine@47(NewTransLine@1000 : Record "Transfer Line";Direction@1001 : 'Outbound,Inbound');
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForTransLine := NewTransLine;

      CalcReservEntry.SetSource(
        DATABASE::"Transfer Line",Direction,NewTransLine."Document No.",NewTransLine."Line No.",'',
        NewTransLine."Derived From Line No.");

      case Direction of
        Direction::Outbound:
          begin
            CalcReservEntry.SetItemData(
              NewTransLine."Item No.",NewTransLine.Description,NewTransLine."Transfer-from Code",NewTransLine."Variant Code",
              NewTransLine."Qty. per Unit of Measure");
            CalcReservEntry."Shipment Date" := NewTransLine."Shipment Date";
          end;
        Direction::Inbound:
          begin
            CalcReservEntry.SetItemData(
              NewTransLine."Item No.",NewTransLine.Description,NewTransLine."Transfer-to Code",NewTransLine."Variant Code",
              NewTransLine."Qty. per Unit of Measure");
            CalcReservEntry."Expected Receipt Date" := NewTransLine."Receipt Date";
          end;
      end;

      UpdateReservation((CreateReservEntry.SignFactor(CalcReservEntry) * ForTransLine."Outstanding Qty. (Base)") <= 0);
    END;

    [External]
    PROCEDURE SetServLine@48(NewServiceLine@1000 : Record "Service Line");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForServiceLine := NewServiceLine;

      CalcReservEntry.SetSource(
        DATABASE::"Service Line",ForServiceLine."Document Type",ForServiceLine."Document No.",NewServiceLine."Line No.",'',0);
      CalcReservEntry.SetItemData(
        NewServiceLine."No.",NewServiceLine.Description,NewServiceLine."Location Code",NewServiceLine."Variant Code",
        NewServiceLine."Qty. per Unit of Measure");
      if NewServiceLine.Type <> NewServiceLine.Type::Item then
        CalcReservEntry."Item No." := '';
      CalcReservEntry."Expected Receipt Date" := NewServiceLine."Needed by Date";
      CalcReservEntry."Shipment Date" := NewServiceLine."Needed by Date";

      UpdateReservation((CreateReservEntry.SignFactor(CalcReservEntry) * ForServiceLine."Outstanding Qty. (Base)") <= 0);
    END;

    [External]
    PROCEDURE SetJobJnlLine@20(NewJobJnlLine@1000 : Record "Job Journal Line");
    BEGIN
      CLEARALL;
      ForJobJnlLine := NewJobJnlLine;

      CalcReservEntry.SetSource(
        DATABASE::"Job Journal Line",ForJobJnlLine."Entry Type",NewJobJnlLine."Journal Template Name",NewJobJnlLine."Line No.",
        NewJobJnlLine."Journal Batch Name",0);
      CalcReservEntry.SetItemData(
        NewJobJnlLine."No.",NewJobJnlLine.Description,NewJobJnlLine."Location Code",NewJobJnlLine."Variant Code",
        NewJobJnlLine."Qty. per Unit of Measure");
      CalcReservEntry."Expected Receipt Date" := NewJobJnlLine."Posting Date";
      CalcReservEntry."Shipment Date" := NewJobJnlLine."Posting Date";

      UpdateReservation((CreateReservEntry.SignFactor(CalcReservEntry) * ForJobJnlLine."Quantity (Base)") < 0);
    END;

    [External]
    PROCEDURE SetJobPlanningLine@71(NewJobPlanningLine@1000 : Record "Job Planning Line");
    BEGIN
      CLEARALL;
      TempTrackingSpecification.DELETEALL;

      ForJobPlanningLine := NewJobPlanningLine;

      CalcReservEntry.SetSource(
        DATABASE::"Job Planning Line",ForJobPlanningLine.Status,NewJobPlanningLine."Job No.",
        NewJobPlanningLine."Job Contract Entry No.",'',0);
      CalcReservEntry.SetItemData(
        NewJobPlanningLine."No.",NewJobPlanningLine.Description,NewJobPlanningLine."Location Code",NewJobPlanningLine."Variant Code",
        NewJobPlanningLine."Qty. per Unit of Measure");
      if NewJobPlanningLine.Type <> NewJobPlanningLine.Type::Item then
        CalcReservEntry."Item No." := '';
      CalcReservEntry."Expected Receipt Date" := NewJobPlanningLine."Planning Date";
      CalcReservEntry."Shipment Date" := NewJobPlanningLine."Planning Date";

      UpdateReservation((CreateReservEntry.SignFactor(CalcReservEntry) * ForJobPlanningLine."Remaining Qty. (Base)") <= 0);
    END;

    [External]
    PROCEDURE SalesLineUpdateValues@54(VAR CurrentSalesLine@1000 : Record "Sales Line";VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1001 : Decimal;VAR QtyReservedThisLine@1004 : Decimal;VAR QtyReservedThisLineBase@1002 : Decimal);
    BEGIN
      with CurrentSalesLine do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        if "Document Type" = "Document Type"::"Return Order" then begin
          "Reserved Quantity" := -"Reserved Quantity";
          "Reserved Qty. (Base)" := -"Reserved Qty. (Base)";
        end;
        QtyReservedThisLine := "Reserved Quantity";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := "Outstanding Quantity" - "Reserved Quantity";
        QtyToReserveBase := "Outstanding Qty. (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    [External]
    PROCEDURE ReqLineUpdateValues@56(VAR CurrentReqLine@1000 : Record "Requisition Line";VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1001 : Decimal;VAR QtyReservedThisLine@1004 : Decimal;VAR QtyReservedThisLineBase@1002 : Decimal);
    BEGIN
      with CurrentReqLine do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        QtyReservedThisLine := "Reserved Quantity";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := Quantity - "Reserved Quantity";
        QtyToReserveBase := "Quantity (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    [External]
    PROCEDURE PurchLineUpdateValues@44(VAR CurrentPurchLine@1000 : Record "Purchase Line";VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1001 : Decimal;VAR QtyReservedThisLine@1004 : Decimal;VAR QtyReservedThisLineBase@1002 : Decimal);
    BEGIN
      with CurrentPurchLine do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        if "Document Type" = "Document Type"::"Return Order" then begin
          "Reserved Quantity" := -"Reserved Quantity";
          "Reserved Qty. (Base)" := -"Reserved Qty. (Base)";
        end;
        QtyReservedThisLine := "Reserved Quantity";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := "Outstanding Quantity" - "Reserved Quantity";
        QtyToReserveBase := "Outstanding Qty. (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    [External]
    PROCEDURE ProdOrderLineUpdateValues@15(VAR CurrentProdOrderLine@1000 : Record "Prod. Order Line";VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1001 : Decimal;VAR QtyReservedThisLine@1004 : Decimal;VAR QtyReservedThisLineBase@1002 : Decimal);
    BEGIN
      with CurrentProdOrderLine do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        QtyReservedThisLine := "Reserved Quantity";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := "Remaining Quantity" - "Reserved Quantity";
        QtyToReserveBase := "Remaining Qty. (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    [External]
    PROCEDURE ProdOrderCompUpdateValues@16(VAR CurrentProdOrderComp@1000 : Record "Prod. Order Component";VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1001 : Decimal;VAR QtyReservedThisLine@1004 : Decimal;VAR QtyReservedThisLineBase@1002 : Decimal);
    BEGIN
      with CurrentProdOrderComp do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        QtyReservedThisLine := "Reserved Qty. (Base)";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := "Remaining Quantity" - "Reserved Quantity";
        QtyToReserveBase := "Remaining Qty. (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    [External]
    PROCEDURE AssemblyHeaderUpdateValues@108(VAR CurrentAssemblyHeader@1000 : Record "Assembly Header";VAR QtyToReserve@1001 : Decimal;VAR QtyToReserveBase@1003 : Decimal;VAR QtyReservedThisLine@1002 : Decimal;VAR QtyReservedThisLineBase@1004 : Decimal);
    BEGIN
      with CurrentAssemblyHeader do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        QtyReservedThisLine := "Reserved Quantity";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := "Remaining Quantity" - "Reserved Quantity";
        QtyToReserveBase := "Remaining Quantity (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    [External]
    PROCEDURE AssemblyLineUpdateValues@107(VAR CurrentAssemblyLine@1000 : Record "Assembly Line";VAR QtyToReserve@1001 : Decimal;VAR QtyToReserveBase@1003 : Decimal;VAR QtyReservedThisLine@1002 : Decimal;VAR QtyReservedThisLineBase@1004 : Decimal);
    BEGIN
      with CurrentAssemblyLine do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        QtyReservedThisLine := "Reserved Quantity";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := "Remaining Quantity" - "Reserved Quantity";
        QtyToReserveBase := "Remaining Quantity (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    [External]
    PROCEDURE PlanningComponentUpdateValues@19(VAR CurrentPlanningComponent@1000 : Record "Planning Component";VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1001 : Decimal;VAR QtyReservedThisLine@1005 : Decimal;VAR QtyReservedThisLineBase@1002 : Decimal);
    BEGIN
      with CurrentPlanningComponent do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        QtyReservedThisLine := "Reserved Quantity";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := 0;
        QtyToReserveBase := "Net Quantity (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    [External]
    PROCEDURE ItemLedgEntryUpdateValues@55(VAR CurrentItemLedgEntry@1000 : Record "Item Ledger Entry";VAR QtyToReserve@1001 : Decimal;VAR QtyReservedThisLine@1002 : Decimal);
    BEGIN
      with CurrentItemLedgEntry do begin
        CALCFIELDS("Reserved Quantity");
        QtyReservedThisLine := "Reserved Quantity";
        QtyToReserve := "Remaining Quantity" - "Reserved Quantity";
      end;
    END;

    [External]
    PROCEDURE ServiceInvLineUpdateValues@50(VAR CurrentServiceInvLine@1000 : Record "Service Line";VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1001 : Decimal;VAR QtyReservedThisLine@1004 : Decimal;VAR QtyReservedThisLineBase@1002 : Decimal);
    BEGIN
      with CurrentServiceInvLine do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        QtyReservedThisLine := "Reserved Quantity";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := "Outstanding Quantity" - "Reserved Quantity";
        QtyToReserveBase := "Outstanding Qty. (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    [External]
    PROCEDURE TransferLineUpdateValues@49(VAR CurrentTransLine@1000 : Record "Transfer Line";VAR QtyToReserve@1004 : Decimal;VAR QtyToReserveBase@1001 : Decimal;VAR QtyReservedThisLine@1005 : Decimal;VAR QtyReservedThisLineBase@1002 : Decimal;Direction@1003 : 'Outbound,Inbound');
    BEGIN
      with CurrentTransLine do
        case Direction of
          Direction::Outbound:
            begin
              CALCFIELDS("Reserved Quantity Outbnd.","Reserved Qty. Outbnd. (Base)");
              QtyReservedThisLine := "Reserved Quantity Outbnd.";
              QtyReservedThisLineBase := "Reserved Qty. Outbnd. (Base)";
              QtyToReserve := -"Outstanding Quantity" + "Reserved Quantity Outbnd.";
              QtyToReserveBase := -"Outstanding Qty. (Base)" + "Reserved Qty. Outbnd. (Base)";
            end;
          Direction::Inbound:
            begin
              CALCFIELDS("Reserved Quantity Inbnd.","Reserved Qty. Inbnd. (Base)");
              QtyReservedThisLine := "Reserved Quantity Inbnd.";
              QtyReservedThisLineBase := "Reserved Qty. Inbnd. (Base)";
              QtyToReserve := "Outstanding Quantity" - "Reserved Quantity Inbnd.";
              QtyToReserveBase := "Outstanding Qty. (Base)" - "Reserved Qty. Inbnd. (Base)";
            end;
        end;
    END;

    [External]
    PROCEDURE JobPlanningLineUpdateValues@72(VAR CurrentJobPlanningLine@1000 : Record "Job Planning Line";VAR QtyToReserve@1003 : Decimal;VAR QtyToReserveBase@1001 : Decimal;VAR QtyReservedThisLine@1004 : Decimal;VAR QtyReservedThisLineBase@1002 : Decimal);
    BEGIN
      with CurrentJobPlanningLine do begin
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        QtyReservedThisLine := "Reserved Quantity";
        QtyReservedThisLineBase := "Reserved Qty. (Base)";
        QtyToReserve := "Remaining Qty." - "Reserved Quantity";
        QtyToReserveBase := "Remaining Qty. (Base)" - "Reserved Qty. (Base)";
      end;
    END;

    LOCAL PROCEDURE UpdateReservation@21(EntryIsPositive@1000 : Boolean);
    BEGIN
      CalcReservEntry2 := CalcReservEntry;
      GetItemSetup(CalcReservEntry);
      Positive := EntryIsPositive;
      CalcReservEntry2.SetPointerFilter;
      CallCalcReservedQtyOnPick;
    END;

    [External]
    PROCEDURE UpdateStatistics@7(VAR ReservSummEntry@1000 : Record "Entry Summary";AvailabilityDate@1001 : Date;HandleItemTracking2@1006 : Boolean);
    VAR
      i@1002 : Integer;
      CurrentEntryNo@1003 : Integer;
      ValueArrayNo@1007 : Integer;
      CalcSumValue@1004 : Decimal;
    BEGIN
      CurrentEntryNo := ReservSummEntry."Entry No.";
      CalcReservEntry.TESTFIELD("Source Type");
      ReservSummEntry.DELETEALL;
      HandleItemTracking := HandleItemTracking2;
      if HandleItemTracking2 then
        ValueArrayNo := 3;
      for i := 1 to SetValueArray(ValueArrayNo) do begin
        CalcSumValue := 0;
        ReservSummEntry.INIT;
        ReservSummEntry."Entry No." := ValueArray[i];

        case ValueArray[i] of
          1: // Item Ledger Entry
            UpdateItemLedgEntryStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue,HandleItemTracking2);
          12,16: // Purchase Order, Purchase Return Order
            UpdatePurchLineStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          32,36: // Sales Order, Sales Return Order
            UpdateSalesLineStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          63: // Firm Planned Prod. Order Line
            UpdateProdOrderLineStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          64: // Released Prod. Order Line
            UpdateProdOrderLineStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          73: // Firm Planned Component Line
            UpdateProdOrderCompStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          74: // Released Component Line
            UpdateProdOrderCompStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          101,102: // Transfer Line
            UpdateTransLineStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          110: // Service Line Order
            UpdateServLineStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          133: // Job Planning Line Order
            UpdateJobPlanningLineStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          141,142: // Assembly Header
            UpdateAssemblyHeaderStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          151,152: // AssemblyLine
            UpdateAssemblyLineStats(ReservSummEntry,AvailabilityDate,i,CalcSumValue);
          6500: // Item Tracking
            UpdateItemTrackingLineStats(ReservSummEntry,AvailabilityDate);
        end;
      end;
      if not ReservSummEntry.GET(CurrentEntryNo) then;
    END;

    LOCAL PROCEDURE UpdateItemLedgEntryStats@69(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1003 : Decimal;HandleItemTracking2@1004 : Boolean);
    VAR
      LateBindingMgt@1005 : Codeunit "Late Binding Management";
      ReservForm@1006 : Page Reservation;
      CurrReservedQtyBase@1007 : Decimal;
    BEGIN
      if CalcItemLedgEntry.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if CalcItemLedgEntry.FINDSET then
          repeat
            CalcItemLedgEntry.CALCFIELDS("Reserved Quantity");
            ReservEntrySummary."Total Reserved Quantity" += CalcItemLedgEntry."Reserved Quantity";
            CalcSumValue += CalcItemLedgEntry."Remaining Quantity";
          until CalcItemLedgEntry.NEXT = 0;
        if HandleItemTracking2 then
          if ReservEntrySummary."Total Reserved Quantity" > 0 then
            ReservEntrySummary."Non-specific Reserved Qty." := LateBindingMgt.NonspecificReservedQty(CalcItemLedgEntry);

        if CalcSumValue <> 0 then
          if (CalcSumValue > 0) = Positive then begin
            if Location.GET(CalcItemLedgEntry."Location Code") and
               (Location."Bin Mandatory" or Location."Require Pick")
            then begin
              CalcReservedQtyOnPick(TotalAvailQty,QtyAllocInWhse);
              QtyOnOutBound :=
                CreatePick.CheckOutBound(
                  CalcReservEntry."Source Type",CalcReservEntry."Source Subtype",
                  CalcReservEntry."Source ID",CalcReservEntry."Source Ref. No.",
                  CalcReservEntry."Source Prod. Order Line");
            end else begin
              QtyAllocInWhse := 0;
              QtyOnOutBound := 0;
            end;
            if QtyAllocInWhse < 0 then
              QtyAllocInWhse := 0;
            ReservEntrySummary."Table ID" := DATABASE::"Item Ledger Entry";
            ReservEntrySummary."Summary Type" :=
              COPYSTR(CalcItemLedgEntry.TABLECAPTION,1,MAXSTRLEN(ReservEntrySummary."Summary Type"));
            ReservEntrySummary."Total Quantity" := CalcSumValue;
            ReservEntrySummary."Total Available Quantity" :=
              ReservEntrySummary."Total Quantity" - ReservEntrySummary."Total Reserved Quantity";

            CLEAR(ReservForm);
            ReservForm.SetReservEntry(CalcReservEntry);
            CurrReservedQtyBase := ReservForm.ReservedThisLine(ReservEntrySummary);
            if (CurrReservedQtyBase <> 0) and (QtyOnOutBound <> 0) then
              if QtyOnOutBound > CurrReservedQtyBase then
                QtyOnOutBound := QtyOnOutBound - CurrReservedQtyBase
              else
                QtyOnOutBound := 0;

            if Location."Bin Mandatory" or Location."Require Pick" then begin
              if TotalAvailQty + QtyOnOutBound < ReservEntrySummary."Total Available Quantity" then
                ReservEntrySummary."Total Available Quantity" := TotalAvailQty + QtyOnOutBound;
              ReservEntrySummary."Qty. Alloc. in Warehouse" := QtyAllocInWhse;
              ReservEntrySummary."Res. Qty. on Picks & Shipmts." := QtyReservedOnPickShip
            end else begin
              ReservEntrySummary."Qty. Alloc. in Warehouse" := 0;
              ReservEntrySummary."Res. Qty. on Picks & Shipmts." := 0
            end;
            if not ReservEntrySummary.INSERT then
              ReservEntrySummary.MODIFY;
          end;
      end;
    END;

    LOCAL PROCEDURE UpdatePurchLineStats@73(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1003 : Decimal);
    BEGIN
      if CalcPurchLine.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if CalcPurchLine.FINDSET then
          repeat
            CalcPurchLine.CALCFIELDS("Reserved Qty. (Base)");
            if not CalcPurchLine."Special Order" then begin
              ReservEntrySummary."Total Reserved Quantity" += CalcPurchLine."Reserved Qty. (Base)";
              CalcSumValue += CalcPurchLine."Outstanding Qty. (Base)";
            end;
          until CalcPurchLine.NEXT = 0;

        if CalcSumValue <> 0 then
          with ReservEntrySummary do
            if (Positive = (CalcSumValue > 0)) and (ValueArray[i] <> 16) or
               (Positive = (CalcSumValue < 0)) and (ValueArray[i] = 16)
            then begin
              "Table ID" := DATABASE::"Purchase Line";
              "Summary Type" :=
                COPYSTR(
                  STRSUBSTNO('%1, %2',CalcPurchLine.TABLECAPTION,CalcPurchLine."Document Type"),
                  1,MAXSTRLEN("Summary Type"));
              if ValueArray[i] = 16 then
                "Total Quantity" := -CalcSumValue
              else
                "Total Quantity" := CalcSumValue;
              "Total Available Quantity" := "Total Quantity" - "Total Reserved Quantity";
              if not INSERT then
                MODIFY;
            end;
      end;
    END;

    LOCAL PROCEDURE UpdateSalesLineStats@74(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1004 : Decimal);
    BEGIN
      if CalcSalesLine.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if CalcSalesLine.FINDSET then
          repeat
            CalcSalesLine.CALCFIELDS("Reserved Qty. (Base)");
            ReservEntrySummary."Total Reserved Quantity" -= CalcSalesLine."Reserved Qty. (Base)";
            CalcSumValue += CalcSalesLine."Outstanding Qty. (Base)";
          until CalcSalesLine.NEXT = 0;

        if CalcSumValue <> 0 then
          with ReservEntrySummary do
            if (Positive = (CalcSumValue < 0)) and (ValueArray[i] <> 36) or
               (Positive = (CalcSumValue > 0)) and (ValueArray[i] = 36)
            then begin
              "Table ID" := DATABASE::"Sales Line";
              "Summary Type" :=
                COPYSTR(
                  STRSUBSTNO('%1, %2',CalcSalesLine.TABLECAPTION,CalcSalesLine."Document Type"),
                  1,MAXSTRLEN("Summary Type"));
              if ValueArray[i] = 36 then
                "Total Quantity" := CalcSumValue
              else
                "Total Quantity" := -CalcSumValue;
              "Total Available Quantity" := "Total Quantity" - "Total Reserved Quantity";
              if not INSERT then
                MODIFY;
            end;
      end;
    END;

    LOCAL PROCEDURE UpdateProdOrderLineStats@75(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1004 : Decimal);
    BEGIN
      if CalcProdOrderLine.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if CalcProdOrderLine.FINDSET then
          repeat
            CalcProdOrderLine.CALCFIELDS("Reserved Qty. (Base)");
            ReservEntrySummary."Total Reserved Quantity" += CalcProdOrderLine."Reserved Qty. (Base)";
            CalcSumValue += CalcProdOrderLine."Remaining Qty. (Base)";
          until CalcProdOrderLine.NEXT = 0;

        if CalcSumValue <> 0 then
          with ReservEntrySummary do
            if (CalcSumValue > 0) = Positive then begin
              "Table ID" := DATABASE::"Prod. Order Line";
              if "Entry No." = 63 then
                "Summary Type" := COPYSTR(STRSUBSTNO(Text000,CalcProdOrderLine.TABLECAPTION),1,MAXSTRLEN("Summary Type"))
              else
                "Summary Type" := COPYSTR(STRSUBSTNO(Text001,CalcProdOrderLine.TABLECAPTION),1,MAXSTRLEN("Summary Type"));
              "Total Quantity" := CalcSumValue;
              "Total Available Quantity" := "Total Quantity" - "Total Reserved Quantity";
              if not INSERT then
                MODIFY;
            end;
      end;
    END;

    LOCAL PROCEDURE UpdateAssemblyHeaderStats@111(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1004 : Decimal);
    BEGIN
      if CalcAssemblyHeader.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if CalcAssemblyHeader.FINDSET then
          repeat
            CalcAssemblyHeader.CALCFIELDS("Reserved Qty. (Base)");
            ReservEntrySummary."Total Reserved Quantity" += CalcAssemblyHeader."Reserved Qty. (Base)";
            CalcSumValue += CalcAssemblyHeader."Remaining Quantity (Base)";
          until CalcAssemblyHeader.NEXT = 0;

        if CalcSumValue <> 0 then
          with ReservEntrySummary do
            if (CalcSumValue > 0) = Positive then begin
              "Table ID" := DATABASE::"Assembly Header";
              "Summary Type" :=
                COPYSTR(
                  STRSUBSTNO('%1 %2',AssemblyTxt,CalcAssemblyHeader."Document Type"),
                  1,MAXSTRLEN("Summary Type"));
              "Total Quantity" := CalcSumValue;
              "Total Available Quantity" := "Total Quantity" - "Total Reserved Quantity";
              if not INSERT then
                MODIFY;
            end;
      end;
    END;

    LOCAL PROCEDURE UpdateAssemblyLineStats@112(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1004 : Decimal);
    BEGIN
      if  CalcAssemblyLine.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if  CalcAssemblyLine.FINDSET then
          repeat
            CalcAssemblyLine.CALCFIELDS("Reserved Qty. (Base)");
            ReservEntrySummary."Total Reserved Quantity" -= CalcAssemblyLine."Reserved Qty. (Base)";
            CalcSumValue += CalcAssemblyLine."Remaining Quantity (Base)";
          until  CalcAssemblyLine.NEXT = 0;

        if CalcSumValue <> 0 then
          with ReservEntrySummary do
            if CalcSumValue < 0 = Positive then begin
              "Table ID" := DATABASE::"Assembly Line";
              "Summary Type" :=
                COPYSTR(
                  STRSUBSTNO('%1, %2',CalcAssemblyLine.TABLECAPTION,CalcAssemblyLine."Document Type"),
                  1,MAXSTRLEN("Summary Type"));
              "Total Quantity" := -CalcSumValue;
              "Total Available Quantity" := "Total Quantity" - "Total Reserved Quantity";
              if not INSERT then
                MODIFY;
            end;
      end;
    END;

    LOCAL PROCEDURE UpdateProdOrderCompStats@76(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1003 : Decimal);
    BEGIN
      if CalcProdOrderComp.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if CalcProdOrderComp.FINDSET then
          repeat
            CalcProdOrderComp.CALCFIELDS("Reserved Qty. (Base)");
            ReservEntrySummary."Total Reserved Quantity" -= CalcProdOrderComp."Reserved Qty. (Base)";
            CalcSumValue += CalcProdOrderComp."Remaining Qty. (Base)";
          until CalcProdOrderComp.NEXT = 0;

        if CalcSumValue <> 0 then
          with ReservEntrySummary do
            if (CalcSumValue < 0) = Positive then begin
              "Table ID" := DATABASE::"Prod. Order Component";
              if "Entry No." = 73 then
                "Summary Type" :=
                  COPYSTR(STRSUBSTNO(Text000,CalcProdOrderComp.TABLECAPTION),1,MAXSTRLEN("Summary Type"))
              else
                "Summary Type" :=
                  COPYSTR(STRSUBSTNO(Text001,CalcProdOrderComp.TABLECAPTION),1,MAXSTRLEN("Summary Type"));
              "Total Quantity" := -CalcSumValue;
              "Total Available Quantity" := "Total Quantity" - "Total Reserved Quantity";
              if not INSERT then
                MODIFY;
            end;
      end;
    END;

    LOCAL PROCEDURE UpdateTransLineStats@77(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1004 : Decimal);
    BEGIN
      if CalcTransLine.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if CalcTransLine.FINDSET then
          repeat
            case ValueArray[i] of
              101:
                begin
                  CalcTransLine.CALCFIELDS("Reserved Qty. Outbnd. (Base)");
                  ReservEntrySummary."Total Reserved Quantity" -= CalcTransLine."Reserved Qty. Outbnd. (Base)";
                  CalcSumValue -= CalcTransLine."Outstanding Qty. (Base)";
                end;
              102:
                begin
                  CalcTransLine.CALCFIELDS("Reserved Qty. Inbnd. (Base)");
                  ReservEntrySummary."Total Reserved Quantity" += CalcTransLine."Reserved Qty. Inbnd. (Base)";
                  CalcSumValue += CalcTransLine."Outstanding Qty. (Base)";
                end;
            end;
          until CalcTransLine.NEXT = 0;

        if CalcSumValue <> 0 then
          with ReservEntrySummary do
            if (CalcSumValue > 0) = Positive then begin
              "Table ID" := DATABASE::"Transfer Line";
              "Summary Type" :=
                COPYSTR(
                  STRSUBSTNO('%1, %2',CalcTransLine.TABLECAPTION,SELECTSTR(ValueArray[i] - 100,Text006)),
                  1,MAXSTRLEN("Summary Type"));
              "Total Quantity" := CalcSumValue;
              "Total Available Quantity" := "Total Quantity" - "Total Reserved Quantity";
              if not INSERT then
                MODIFY;
            end;
      end;
    END;

    LOCAL PROCEDURE UpdateServLineStats@78(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1003 : Decimal);
    BEGIN
      if CalcServiceLine.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if CalcServiceLine.FINDSET then
          repeat
            CalcServiceLine.CALCFIELDS("Reserved Qty. (Base)");
            ReservEntrySummary."Total Reserved Quantity" -= CalcServiceLine."Reserved Qty. (Base)";
            CalcSumValue += CalcServiceLine."Outstanding Qty. (Base)";
          until CalcServiceLine.NEXT = 0;

        if CalcSumValue <> 0 then
          with ReservEntrySummary do
            if (CalcSumValue < 0) = Positive then begin
              "Table ID" := DATABASE::"Service Line";
              "Summary Type" :=
                COPYSTR(STRSUBSTNO('%1',CalcServiceLine.TABLECAPTION),1,MAXSTRLEN("Summary Type"));
              "Total Quantity" := -CalcSumValue;
              "Total Available Quantity" := "Total Quantity" - "Total Reserved Quantity";
              if not INSERT then
                MODIFY;
            end;
      end;
    END;

    LOCAL PROCEDURE UpdateJobPlanningLineStats@79(VAR ReservEntrySummary@1001 : Record "Entry Summary";AvailabilityDate@1000 : Date;i@1002 : Integer;VAR CalcSumValue@1004 : Decimal);
    BEGIN
      if CalcJobPlanningLine.READPERMISSION then begin
        InitFilter(ValueArray[i],AvailabilityDate);
        if CalcJobPlanningLine.FINDSET then
          repeat
            CalcJobPlanningLine.CALCFIELDS("Reserved Qty. (Base)");
            ReservEntrySummary."Total Reserved Quantity" -= CalcJobPlanningLine."Reserved Qty. (Base)";
            CalcSumValue += CalcJobPlanningLine."Remaining Qty. (Base)";
          until CalcJobPlanningLine.NEXT = 0;

        if CalcSumValue <> 0 then
          with ReservEntrySummary do
            if (CalcSumValue < 0) = Positive then begin
              "Table ID" := DATABASE::"Job Planning Line";
              "Summary Type" :=
                COPYSTR(
                  STRSUBSTNO('%1, %2',CalcJobPlanningLine.TABLECAPTION,CalcJobPlanningLine.Status),
                  1,MAXSTRLEN("Summary Type"));
              "Total Quantity" := -CalcSumValue;
              "Total Available Quantity" := "Total Quantity" - "Total Reserved Quantity";
              if not INSERT then
                MODIFY;
            end;
      end;
    END;

    LOCAL PROCEDURE UpdateItemTrackingLineStats@80(VAR ReservEntrySummary@1002 : Record "Entry Summary";AvailabilityDate@1001 : Date);
    VAR
      ReservEntry@1000 : Record "Reservation Entry";
    BEGIN
      ReservEntry.RESET;
      ReservEntry.SETCURRENTKEY(
        "Item No.","Source Type","Source Subtype","Reservation Status","Location Code",
        "Variant Code","Shipment Date","Expected Receipt Date","Serial No.","Lot No.");
      ReservEntry.SETRANGE("Item No.",CalcReservEntry."Item No.");
      ReservEntry.SETFILTER("Source Type",'<> %1',DATABASE::"Item Ledger Entry");
      ReservEntry.SETRANGE("Reservation Status",
        ReservEntry."Reservation Status"::Reservation,ReservEntry."Reservation Status"::Surplus);
      ReservEntry.SETRANGE("Location Code",CalcReservEntry."Location Code");
      ReservEntry.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
      if Positive then
        ReservEntry.SETFILTER("Expected Receipt Date",'..%1',AvailabilityDate)
      else
        ReservEntry.SETFILTER("Shipment Date",'>=%1',AvailabilityDate);
      ReservEntry.SetTrackingFilterFromReservEntry(CalcReservEntry);
      ReservEntry.SETRANGE(Positive,Positive);
      if ReservEntry.FINDSET then
        repeat
          ReservEntry.SETRANGE("Source Type",ReservEntry."Source Type");
          ReservEntry.SETRANGE("Source Subtype",ReservEntry."Source Subtype");
          ReservEntrySummary.INIT;
          ReservEntrySummary."Entry No." := ReservEntry.SummEntryNo;
          ReservEntrySummary."Table ID" := ReservEntry."Source Type";
          ReservEntrySummary."Summary Type" :=
            COPYSTR(ReservEntry.TextCaption,1,MAXSTRLEN(ReservEntrySummary."Summary Type"));
          ReservEntrySummary."Source Subtype" := ReservEntry."Source Subtype";
          ReservEntrySummary."Serial No." := ReservEntry."Serial No.";
          ReservEntrySummary."Lot No." := ReservEntry."Lot No.";
          if ReservEntry.FINDSET then
            repeat
              ReservEntrySummary."Total Quantity" += ReservEntry."Quantity (Base)";
              if ReservEntry."Reservation Status" = ReservEntry."Reservation Status"::Reservation then
                ReservEntrySummary."Total Reserved Quantity" += ReservEntry."Quantity (Base)";
              if CalcReservEntry.HasSamePointer(ReservEntry) then
                ReservEntrySummary."Current Reserved Quantity" += ReservEntry."Quantity (Base)";
            until ReservEntry.NEXT = 0;

          ReservEntrySummary."Total Available Quantity" :=
            ReservEntrySummary."Total Quantity" - ReservEntrySummary."Total Reserved Quantity";
          ReservEntrySummary.INSERT;
          ReservEntry.SETRANGE("Source Type");
          ReservEntry.SETRANGE("Source Subtype");
        until ReservEntry.NEXT = 0;
    END;

    [External]
    PROCEDURE AutoReserve@5(VAR FullAutoReservation@1000 : Boolean;Description@1001 : Text[50];AvailabilityDate@1002 : Date;MaxQtyToReserve@1007 : Decimal;MaxQtyToReserveBase@1006 : Decimal);
    VAR
      RemainingQtyToReserve@1008 : Decimal;
      RemainingQtyToReserveBase@1003 : Decimal;
      i@1004 : Integer;
      StopReservation@1005 : Boolean;
    BEGIN
      CalcReservEntry.TESTFIELD("Source Type");

      if CalcReservEntry."Source Type" in [DATABASE::"Sales Line",DATABASE::"Purchase Line",DATABASE::"Service Line"] then
        StopReservation := not (CalcReservEntry."Source Subtype" in [1,5]); // Only order and return order

      if CalcReservEntry."Source Type" in [DATABASE::"Assembly Line",DATABASE::"Assembly Header"] then
        StopReservation := not (CalcReservEntry."Source Subtype" = 1); // Only Assembly Order

      if CalcReservEntry."Source Type" in [DATABASE::"Prod. Order Line",DATABASE::"Prod. Order Component"]
      then
        StopReservation := CalcReservEntry."Source Subtype" < 2; // Not simulated or planned

      if CalcReservEntry."Source Type" = DATABASE::"Sales Line" then
        if (CalcReservEntry."Source Subtype" = 1) and (ForSalesLine.Quantity < 0) then
          StopReservation := true;

      if CalcReservEntry."Source Type" = DATABASE::"Sales Line" then
        if (CalcReservEntry."Source Subtype" = 5) and (ForSalesLine.Quantity >= 0) then
          StopReservation := true;

      if StopReservation then begin
        FullAutoReservation := true;
        exit;
      end;

      CalculateRemainingQty(RemainingQtyToReserve,RemainingQtyToReserveBase);
      if (MaxQtyToReserveBase <> 0) and (ABS(MaxQtyToReserveBase) < ABS(RemainingQtyToReserveBase)) then begin
        RemainingQtyToReserve := MaxQtyToReserve;
        RemainingQtyToReserveBase := MaxQtyToReserveBase;
      end;

      if (RemainingQtyToReserveBase <> 0) and
         HandleItemTracking and
         ItemTrackingCode."SN Specific Tracking"
      then
        RemainingQtyToReserveBase := 1;
      FullAutoReservation := false;

      if RemainingQtyToReserveBase = 0 then begin
        FullAutoReservation := true;
        exit;
      end;

      for i := 1 to SetValueArray(0) do
        AutoReserveOneLine(ValueArray[i],RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate);

      FullAutoReservation := (RemainingQtyToReserveBase = 0);
    END;

    [External]
    PROCEDURE AutoReserveOneLine@4(ReservSummEntryNo@1018 : Integer;VAR RemainingQtyToReserve@1000 : Decimal;VAR RemainingQtyToReserveBase@1017 : Decimal;Description@1016 : Text[50];AvailabilityDate@1015 : Date);
    VAR
      Item@1004 : Record Item;
      Search@1007 : Text[1];
      NextStep@1008 : Integer;
    BEGIN
      CalcReservEntry.TESTFIELD("Source Type");

      if RemainingQtyToReserveBase = 0 then
        exit;

      if not Item.GET(CalcReservEntry."Item No.") then
        CLEAR(Item);

      CalcReservEntry.Lock;

      if Positive then begin
        Search := '+';
        NextStep := -1;
        if Item."Costing Method" = Item."Costing Method"::LIFO then begin
          InvSearch := '+';
          InvNextStep := -1;
        end else begin
          InvSearch := '-';
          InvNextStep := 1;
        end;
      end else begin
        Search := '-';
        NextStep := 1;
        InvSearch := '-';
        InvNextStep := 1;
      end;

      case ReservSummEntryNo of
        1: // Item Ledger Entry
          AutoReserveItemLedgEntry(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate);
        12,
        16: // Purchase Line, Purchase Return Line
          AutoReservePurchLine(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate,
            Search,NextStep);
        21: // Requisition Line
          AutoReserveReqLine(ReservSummEntryNo,AvailabilityDate);
        31,
        32,
        36: // Sales Line, Sales Return Line
          AutoReserveSalesLine(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate,
            Search,NextStep);
        61,
        62,
        63,
        64: // Prod. Order
          AutoReserveProdOrderLine(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate,
            Search,NextStep);
        71,
        72,
        73,
        74: // Prod. Order Component
          AutoReserveProdOrderComp(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate,
            Search,NextStep);
        91: // Planning Component
          AutoReservePlanningComp(ReservSummEntryNo,AvailabilityDate);
        101,
        102: // Transfer
          AutoReserveTransLine(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate,
            Search,NextStep);
        110: // Service Line Order
          AutoReserveServLine(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate,
            Search,NextStep);
        133: // Job Planning Line Order
          AutoReserveJobPlanningLine(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate,
            Search,NextStep);
        142: // Assembly Header
          AutoReserveAssemblyHeader(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate,
            Search,NextStep);
        152: // Assembly Line
          AutoReserveAssemblyLine(ReservSummEntryNo,RemainingQtyToReserve,RemainingQtyToReserveBase,Description,AvailabilityDate,
            Search,NextStep);
      end;
    END;

    LOCAL PROCEDURE AutoReserveItemLedgEntry@114(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1000 : Decimal;VAR RemainingQtyToReserveBase@1004 : Decimal;Description@1003 : Text[50];AvailabilityDate@1002 : Date);
    VAR
      Location@1010 : Record Location;
      LateBindingMgt@1007 : Codeunit "Late Binding Management";
      AllocationsChanged@1006 : Boolean;
      QtyThisLine@1009 : Decimal;
      QtyThisLineBase@1001 : Decimal;
    BEGIN
      if not Location.GET(CalcReservEntry."Location Code") then
        CLEAR(Location);

      InitFilter(ReservSummEntryNo,AvailabilityDate);
      // Late Binding
      if HandleItemTracking then
        AllocationsChanged :=
          LateBindingMgt.ReleaseForReservation3(CalcItemLedgEntry,CalcReservEntry,RemainingQtyToReserveBase);

      if CalcItemLedgEntry.FIND(InvSearch) then begin
        if Location."Bin Mandatory" or Location."Require Pick" then begin
          QtyOnOutBound :=
            CreatePick.CheckOutBound(
              CalcReservEntry."Source Type",CalcReservEntry."Source Subtype",
              CalcReservEntry."Source ID",CalcReservEntry."Source Ref. No.",
              CalcReservEntry."Source Prod. Order Line") -
            CalcCurrLineReservQtyOnPicksShips(
              CalcReservEntry."Source Type",CalcReservEntry."Source Subtype",
              CalcReservEntry."Source ID",CalcReservEntry."Source Ref. No.",
              CalcReservEntry."Source Prod. Order Line");
          if AllocationsChanged then
            CalcReservedQtyOnPick(TotalAvailQty,QtyAllocInWhse); // If allocations have changed we must recalculate
        end;
        repeat
          CalcItemLedgEntry.CALCFIELDS("Reserved Quantity");
          if (CalcItemLedgEntry."Remaining Quantity" -
              CalcItemLedgEntry."Reserved Quantity") <> 0
          then begin
            if ABS(CalcItemLedgEntry."Remaining Quantity" -
                 CalcItemLedgEntry."Reserved Quantity") > ABS(RemainingQtyToReserveBase)
            then begin
              QtyThisLine := ABS(RemainingQtyToReserve);
              QtyThisLineBase := ABS(RemainingQtyToReserveBase);
            end else begin
              QtyThisLineBase :=
                CalcItemLedgEntry."Remaining Quantity" - CalcItemLedgEntry."Reserved Quantity";
              QtyThisLine := 0;
            end;
            if IsSpecialOrder(CalcItemLedgEntry."Purchasing Code") or (Positive = (QtyThisLineBase < 0)) then begin
              QtyThisLineBase := 0;
              QtyThisLine := 0;
            end;

            if (Location."Bin Mandatory" or Location."Require Pick") and
               (TotalAvailQty + QtyOnOutBound < QtyThisLineBase)
            then
              if (TotalAvailQty + QtyOnOutBound) < 0 then begin
                QtyThisLineBase := 0;
                QtyThisLine := 0
              end else begin
                QtyThisLineBase := TotalAvailQty + QtyOnOutBound;
                QtyThisLine := ROUND(QtyThisLineBase,0.00001);
              end;

            CallTrackingSpecification.InitTrackingSpecification(
              DATABASE::"Item Ledger Entry",0,'','',0,CalcItemLedgEntry."Entry No.",
              CalcItemLedgEntry."Variant Code",CalcItemLedgEntry."Location Code",
              CalcItemLedgEntry."Serial No.",CalcItemLedgEntry."Lot No.",
              CalcItemLedgEntry."Qty. per Unit of Measure");

            if CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,0,
                 Description,0D,QtyThisLine,QtyThisLineBase,CallTrackingSpecification)
            then
              if Location."Bin Mandatory" or Location."Require Pick" then
                TotalAvailQty := TotalAvailQty - QtyThisLineBase;
          end;
        until (CalcItemLedgEntry.NEXT(InvNextStep) = 0) or (RemainingQtyToReserveBase = 0);
      end;
    END;

    LOCAL PROCEDURE AutoReservePurchLine@81(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1008 : Decimal;VAR RemainingQtyToReserveBase@1004 : Decimal;Description@1003 : Text[50];AvailabilityDate@1002 : Date;Search@1001 : Text[1];NextStep@1000 : Integer);
    VAR
      QtyThisLine@1009 : Decimal;
      QtyThisLineBase@1007 : Decimal;
      ReservQty@1006 : Decimal;
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
      if CalcPurchLine.FIND(Search) then
        repeat
          CalcPurchLine.CALCFIELDS("Reserved Qty. (Base)");
          if not CalcPurchLine."Special Order" then begin
            QtyThisLine := CalcPurchLine."Outstanding Quantity";
            QtyThisLineBase := CalcPurchLine."Outstanding Qty. (Base)";
          end;
          if ReservSummEntryNo = 16 then // Return Order
            ReservQty := -CalcPurchLine."Reserved Qty. (Base)"
          else
            ReservQty := CalcPurchLine."Reserved Qty. (Base)";
          if (Positive = (QtyThisLineBase < 0)) and (ReservSummEntryNo <> 16) or
             (Positive = (QtyThisLineBase > 0)) and (ReservSummEntryNo = 16)
          then begin
            QtyThisLine := 0;
            QtyThisLineBase := 0;
          end;

          CallTrackingSpecification.InitTrackingSpecification(
            DATABASE::"Purchase Line",CalcPurchLine."Document Type",CalcPurchLine."Document No.",'',
            0,CalcPurchLine."Line No.",
            CalcPurchLine."Variant Code",CalcPurchLine."Location Code",
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
            CalcPurchLine."Qty. per Unit of Measure");

          CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,ReservQty,
            Description,CalcPurchLine."Expected Receipt Date",QtyThisLine,QtyThisLineBase,CallTrackingSpecification);
        until (CalcPurchLine.NEXT(NextStep) = 0) or (RemainingQtyToReserveBase = 0);
    END;

    LOCAL PROCEDURE AutoReserveReqLine@82(ReservSummEntryNo@1005 : Integer;AvailabilityDate@1002 : Date);
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
    END;

    LOCAL PROCEDURE AutoReserveSalesLine@83(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1008 : Decimal;VAR RemainingQtyToReserveBase@1004 : Decimal;Description@1003 : Text[50];AvailabilityDate@1002 : Date;Search@1001 : Text[1];NextStep@1000 : Integer);
    VAR
      QtyThisLine@1009 : Decimal;
      QtyThisLineBase@1007 : Decimal;
      ReservQty@1006 : Decimal;
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
      if CalcSalesLine.FIND(Search) then
        repeat
          CalcSalesLine.CALCFIELDS("Reserved Qty. (Base)");
          QtyThisLine := CalcSalesLine."Outstanding Quantity";
          QtyThisLineBase := CalcSalesLine."Outstanding Qty. (Base)";
          if ReservSummEntryNo = 36 then // Return Order
            ReservQty := -CalcSalesLine."Reserved Qty. (Base)"
          else
            ReservQty := CalcSalesLine."Reserved Qty. (Base)";
          if (Positive = (QtyThisLineBase > 0)) and (ReservSummEntryNo <> 36) or
             (Positive = (QtyThisLineBase < 0)) and (ReservSummEntryNo = 36)
          then begin
            QtyThisLine := 0;
            QtyThisLineBase := 0;
          end;

          CallTrackingSpecification.InitTrackingSpecification(
            DATABASE::"Sales Line",CalcSalesLine."Document Type",CalcSalesLine."Document No.",'',
            0,CalcSalesLine."Line No.",CalcSalesLine."Variant Code",CalcSalesLine."Location Code",
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
            CalcSalesLine."Qty. per Unit of Measure");

          CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,ReservQty,
            Description,CalcSalesLine."Shipment Date",QtyThisLine,QtyThisLineBase,CallTrackingSpecification);
        until (CalcSalesLine.NEXT(NextStep) = 0) or (RemainingQtyToReserveBase = 0);
    END;

    LOCAL PROCEDURE AutoReserveProdOrderLine@84(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1006 : Decimal;VAR RemainingQtyToReserveBase@1004 : Decimal;Description@1003 : Text[50];AvailabilityDate@1002 : Date;Search@1001 : Text[1];NextStep@1000 : Integer);
    VAR
      QtyThisLine@1009 : Decimal;
      QtyThisLineBase@1007 : Decimal;
      ReservQty@1008 : Decimal;
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
      if CalcProdOrderLine.FIND(Search) then
        repeat
          CalcProdOrderLine.CALCFIELDS("Reserved Qty. (Base)");
          QtyThisLine := CalcProdOrderLine."Remaining Quantity";
          QtyThisLineBase := CalcProdOrderLine."Remaining Qty. (Base)";
          ReservQty := CalcProdOrderLine."Reserved Qty. (Base)";
          if Positive = (QtyThisLineBase < 0) then begin
            QtyThisLine := 0;
            QtyThisLineBase := 0;
          end;

          CallTrackingSpecification.InitTrackingSpecification(
            DATABASE::"Prod. Order Line",CalcProdOrderLine.Status,CalcProdOrderLine."Prod. Order No.",'',
            CalcProdOrderLine."Line No.",0,CalcProdOrderLine."Variant Code",CalcProdOrderLine."Location Code",
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
            CalcProdOrderLine."Qty. per Unit of Measure");

          CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,ReservQty,
            Description,CalcProdOrderLine."Due Date",QtyThisLine,QtyThisLineBase,CallTrackingSpecification);
        until (CalcProdOrderLine.NEXT(NextStep) = 0) or (RemainingQtyToReserveBase = 0);
    END;

    LOCAL PROCEDURE AutoReserveProdOrderComp@85(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1008 : Decimal;VAR RemainingQtyToReserveBase@1004 : Decimal;Description@1003 : Text[50];AvailabilityDate@1002 : Date;Search@1001 : Text[1];NextStep@1000 : Integer);
    VAR
      QtyThisLine@1009 : Decimal;
      QtyThisLineBase@1007 : Decimal;
      ReservQty@1006 : Decimal;
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
      if CalcProdOrderComp.FIND(Search) then
        repeat
          CalcProdOrderComp.CALCFIELDS("Reserved Qty. (Base)");
          QtyThisLine := CalcProdOrderComp."Remaining Quantity";
          QtyThisLineBase := CalcProdOrderComp."Remaining Qty. (Base)";
          ReservQty := CalcProdOrderComp."Reserved Qty. (Base)";
          if Positive = (QtyThisLineBase > 0) then begin
            QtyThisLine := 0;
            QtyThisLineBase := 0;
          end;

          CallTrackingSpecification.InitTrackingSpecification(
            DATABASE::"Prod. Order Component",CalcProdOrderComp.Status,CalcProdOrderComp."Prod. Order No.",'',
            CalcProdOrderComp."Prod. Order Line No.",CalcProdOrderComp."Line No.",
            CalcProdOrderComp."Variant Code",CalcProdOrderComp."Location Code",
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
            CalcProdOrderComp."Qty. per Unit of Measure");

          CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,ReservQty,
            Description,CalcProdOrderComp."Due Date",QtyThisLine,QtyThisLineBase,CallTrackingSpecification);
        until (CalcProdOrderComp.NEXT(NextStep) = 0) or (RemainingQtyToReserveBase = 0);
    END;

    LOCAL PROCEDURE AutoReserveAssemblyHeader@110(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1006 : Decimal;VAR RemainingQtyToReserveBase@1004 : Decimal;Description@1003 : Text[50];AvailabilityDate@1002 : Date;Search@1001 : Text[1];NextStep@1000 : Integer);
    VAR
      QtyThisLine@1009 : Decimal;
      QtyThisLineBase@1007 : Decimal;
      ReservQty@1008 : Decimal;
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
      if CalcAssemblyHeader.FIND(Search) then
        repeat
          CalcAssemblyHeader.CALCFIELDS("Reserved Qty. (Base)");
          QtyThisLine := CalcAssemblyHeader."Remaining Quantity";
          QtyThisLineBase := CalcAssemblyHeader."Remaining Quantity (Base)";
          ReservQty := CalcAssemblyHeader."Reserved Qty. (Base)";
          if Positive = (QtyThisLineBase < 0) then begin
            QtyThisLine := 0;
            QtyThisLineBase := 0;
          end;

          CallTrackingSpecification.InitTrackingSpecification(
            DATABASE::"Assembly Header",CalcAssemblyHeader."Document Type",CalcAssemblyHeader."No.",'',
            0,0,CalcAssemblyHeader."Variant Code",CalcAssemblyHeader."Location Code",
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
            CalcAssemblyHeader."Qty. per Unit of Measure");

          CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,ReservQty,
            Description,CalcAssemblyHeader."Due Date",QtyThisLine,QtyThisLineBase,CallTrackingSpecification);
        until (CalcAssemblyHeader.NEXT(NextStep) = 0) or (RemainingQtyToReserveBase = 0);
    END;

    LOCAL PROCEDURE AutoReserveAssemblyLine@109(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1008 : Decimal;VAR RemainingQtyToReserveBase@1004 : Decimal;Description@1003 : Text[50];AvailabilityDate@1002 : Date;Search@1001 : Text[1];NextStep@1000 : Integer);
    VAR
      QtyThisLine@1009 : Decimal;
      QtyThisLineBase@1007 : Decimal;
      ReservQty@1006 : Decimal;
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
      if CalcAssemblyLine.FIND(Search) then
        repeat
          CalcAssemblyLine.CALCFIELDS("Reserved Qty. (Base)");
          QtyThisLine := CalcAssemblyLine."Remaining Quantity";
          QtyThisLineBase := CalcAssemblyLine."Remaining Quantity (Base)";
          ReservQty := CalcAssemblyLine."Reserved Qty. (Base)";
          if Positive = (QtyThisLineBase > 0) then begin
            QtyThisLine := 0;
            QtyThisLineBase := 0;
          end;

          CallTrackingSpecification.InitTrackingSpecification(
            DATABASE::"Assembly Line",CalcAssemblyLine."Document Type",CalcAssemblyLine."Document No.",'',
            0,CalcAssemblyLine."Line No.",CalcAssemblyLine."Variant Code",CalcAssemblyLine."Location Code",
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
            CalcAssemblyLine."Qty. per Unit of Measure");

          CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,ReservQty,
            Description,CalcAssemblyHeader."Due Date",QtyThisLine,QtyThisLineBase,CallTrackingSpecification);
        until (CalcAssemblyLine.NEXT(NextStep) = 0) or (RemainingQtyToReserveBase = 0);
    END;

    LOCAL PROCEDURE AutoReservePlanningComp@86(ReservSummEntryNo@1005 : Integer;AvailabilityDate@1002 : Date);
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
    END;

    LOCAL PROCEDURE AutoReserveTransLine@87(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1010 : Decimal;VAR RemainingQtyToReserveBase@1004 : Decimal;Description@1003 : Text[50];AvailabilityDate@1002 : Date;Search@1001 : Text[1];NextStep@1000 : Integer);
    VAR
      QtyThisLine@1011 : Decimal;
      QtyThisLineBase@1007 : Decimal;
      ReservQty@1006 : Decimal;
      LocationCode@1009 : Code[10];
      EntryDate@1008 : Date;
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
      if CalcTransLine.FIND(Search) then
        repeat
          case ReservSummEntryNo of
            101: // Outbound
              begin
                CalcTransLine.CALCFIELDS("Reserved Qty. Outbnd. (Base)");
                QtyThisLine := -CalcTransLine."Outstanding Quantity";
                QtyThisLineBase := -CalcTransLine."Outstanding Qty. (Base)";
                ReservQty := -CalcTransLine."Reserved Qty. Outbnd. (Base)";
                EntryDate := CalcTransLine."Shipment Date";
                LocationCode := CalcTransLine."Transfer-from Code";
                if Positive = (QtyThisLineBase < 0) then begin
                  QtyThisLine := 0;
                  QtyThisLineBase := 0;
                end;
              end;
            102: // Inbound
              begin
                CalcTransLine.CALCFIELDS("Reserved Qty. Inbnd. (Base)");
                QtyThisLine := CalcTransLine."Outstanding Quantity";
                QtyThisLineBase := CalcTransLine."Outstanding Qty. (Base)";
                ReservQty := CalcTransLine."Reserved Qty. Inbnd. (Base)";
                EntryDate := CalcTransLine."Receipt Date";
                LocationCode := CalcTransLine."Transfer-to Code";
                if Positive = (QtyThisLineBase < 0) then begin
                  QtyThisLine := 0;
                  QtyThisLineBase := 0;
                end;
              end;
          end;

          CallTrackingSpecification.InitTrackingSpecification(
            DATABASE::"Transfer Line",ReservSummEntryNo - 101,CalcTransLine."Document No.",'',
            CalcTransLine."Derived From Line No.",CalcTransLine."Line No.",
            CalcTransLine."Variant Code",LocationCode,
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
            CalcTransLine."Qty. per Unit of Measure");

          CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,ReservQty,
            Description,EntryDate,QtyThisLine,QtyThisLineBase,CallTrackingSpecification);
        until (CalcTransLine.NEXT(NextStep) = 0) or (RemainingQtyToReserveBase = 0);
    END;

    LOCAL PROCEDURE AutoReserveServLine@88(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1008 : Decimal;VAR RemainingQtyToReserveBase@1004 : Decimal;Description@1003 : Text[50];AvailabilityDate@1002 : Date;Search@1001 : Text[1];NextStep@1000 : Integer);
    VAR
      QtyThisLine@1009 : Decimal;
      QtyThisLineBase@1007 : Decimal;
      ReservQty@1006 : Decimal;
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
      if CalcServiceLine.FIND(Search) then
        repeat
          CalcServiceLine.CALCFIELDS("Reserved Qty. (Base)");
          QtyThisLine := CalcServiceLine."Outstanding Quantity";
          QtyThisLineBase := CalcServiceLine."Outstanding Qty. (Base)";
          ReservQty := CalcServiceLine."Reserved Qty. (Base)";
          if Positive = (QtyThisLineBase > 0) then begin
            QtyThisLine := 0;
            QtyThisLineBase := 0;
          end;

          CallTrackingSpecification.InitTrackingSpecification(
            DATABASE::"Service Line",CalcServiceLine."Document Type",CalcServiceLine."Document No.",'',
            0,CalcServiceLine."Line No.",CalcServiceLine."Variant Code",CalcServiceLine."Location Code",
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
            CalcServiceLine."Qty. per Unit of Measure");

          CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,ReservQty,
            Description,CalcServiceLine."Needed by Date",QtyThisLine,QtyThisLineBase,CallTrackingSpecification);
        until (CalcServiceLine.NEXT(NextStep) = 0) or (RemainingQtyToReserveBase = 0);
    END;

    LOCAL PROCEDURE AutoReserveJobPlanningLine@89(ReservSummEntryNo@1005 : Integer;VAR RemainingQtyToReserve@1008 : Decimal;VAR RemainingQtyToReserveBase@1002 : Decimal;Description@1001 : Text[50];AvailabilityDate@1000 : Date;Search@1006 : Text[1];NextStep@1007 : Integer);
    VAR
      QtyThisLine@1009 : Decimal;
      QtyThisLineBase@1003 : Decimal;
      ReservQty@1004 : Decimal;
    BEGIN
      InitFilter(ReservSummEntryNo,AvailabilityDate);
      if CalcJobPlanningLine.FIND(Search) then
        repeat
          CalcJobPlanningLine.CALCFIELDS("Reserved Qty. (Base)");
          QtyThisLine := CalcJobPlanningLine."Remaining Qty.";
          QtyThisLineBase := CalcJobPlanningLine."Remaining Qty. (Base)";
          ReservQty := CalcJobPlanningLine."Reserved Qty. (Base)";
          if Positive = (QtyThisLineBase > 0) then begin
            QtyThisLine := 0;
            QtyThisLineBase := 0;
          end;

          CallTrackingSpecification.InitTrackingSpecification(
            DATABASE::"Job Planning Line",CalcJobPlanningLine.Status,CalcJobPlanningLine."Job No.",'',
            0,CalcJobPlanningLine."Job Contract Entry No.",
            CalcJobPlanningLine."Variant Code",CalcJobPlanningLine."Location Code",
            CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
            CalcJobPlanningLine."Qty. per Unit of Measure");

          CallCreateReservation(RemainingQtyToReserve,RemainingQtyToReserveBase,ReservQty,
            Description,CalcJobPlanningLine."Planning Date",QtyThisLine,QtyThisLineBase,CallTrackingSpecification);
        until (CalcJobPlanningLine.NEXT(NextStep) = 0) or (RemainingQtyToReserveBase = 0);
    END;

    LOCAL PROCEDURE CallCreateReservation@158(VAR RemainingQtyToReserve@1016 : Decimal;VAR RemainingQtyToReserveBase@1015 : Decimal;ReservQty@1014 : Decimal;Description@1013 : Text[50];ExpectedDate@1012 : Date;QtyThisLine@1017 : Decimal;QtyThisLineBase@1011 : Decimal;TrackingSpecification@1170000000 : Record "Tracking Specification") ReservationCreated : Boolean;
    BEGIN
      if QtyThisLineBase = 0 then
        exit;
      if ABS(QtyThisLineBase - ReservQty) > 0 then begin
        if ABS(QtyThisLineBase - ReservQty) > ABS(RemainingQtyToReserveBase) then begin
          QtyThisLine := RemainingQtyToReserve;
          QtyThisLineBase := RemainingQtyToReserveBase;
        end else begin
          QtyThisLineBase := QtyThisLineBase - ReservQty;
          QtyThisLine := ROUND(RemainingQtyToReserve / RemainingQtyToReserveBase * QtyThisLineBase,0.00001);
        end;
        CopySign(RemainingQtyToReserveBase,QtyThisLineBase);
        CopySign(RemainingQtyToReserve,QtyThisLine);
        CreateReservation(Description,ExpectedDate,QtyThisLine,QtyThisLineBase,TrackingSpecification);
        RemainingQtyToReserve := RemainingQtyToReserve - QtyThisLine;
        RemainingQtyToReserveBase := RemainingQtyToReserveBase - QtyThisLineBase;
        ReservationCreated := true;
      end;
    END;

    [External]
    PROCEDURE CreateReservation@39(Description@1000 : Text[50];ExpectedDate@1001 : Date;Quantity@1011 : Decimal;QuantityBase@1002 : Decimal;TrackingSpecification@1003 : Record "Tracking Specification");
    BEGIN
      CalcReservEntry.TESTFIELD("Source Type");

      case CalcReservEntry."Source Type" of
        DATABASE::"Sales Line":
          begin
            ReserveSalesLine.CreateReservationSetFrom(TrackingSpecification);
            ReserveSalesLine.CreateReservation(
              ForSalesLine,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForSalesLine.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Requisition Line":
          begin
            ReserveReqLine.CreateReservationSetFrom(TrackingSpecification);
            ReserveReqLine.CreateReservation(
              ForReqLine,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForReqLine.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Purchase Line":
          begin
            ReservePurchLine.CreateReservationSetFrom(TrackingSpecification);
            ReservePurchLine.CreateReservation(
              ForPurchLine,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForPurchLine.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Item Journal Line":
          begin
            ReserveItemJnlLine.CreateReservationSetFrom(TrackingSpecification);
            ReserveItemJnlLine.CreateReservation(
              ForItemJnlLine,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForItemJnlLine.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Prod. Order Line":
          begin
            ReserveProdOrderLine.CreateReservationSetFrom(TrackingSpecification);
            ReserveProdOrderLine.CreateReservation(
              ForProdOrderLine,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForProdOrderLine.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Prod. Order Component":
          begin
            ReserveProdOrderComp.CreateReservationSetFrom(TrackingSpecification);
            ReserveProdOrderComp.CreateReservation(
              ForProdOrderComp,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForProdOrderComp.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Assembly Header":
          begin
            AssemblyHeaderReserve.CreateReservationSetFrom(TrackingSpecification);
            AssemblyHeaderReserve.CreateReservation(
              ForAssemblyHeader,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForAssemblyHeader.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Assembly Line":
          begin
            AssemblyLineReserve.CreateReservationSetFrom(TrackingSpecification);
            AssemblyLineReserve.CreateReservation(
              ForAssemblyLine,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForAssemblyLine.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Planning Component":
          begin
            ReservePlanningComponent.CreateReservationSetFrom(TrackingSpecification);
            ReservePlanningComponent.CreateReservation(
              ForPlanningComponent,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForPlanningComponent.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Transfer Line":
          begin
            ReserveTransLine.CreateReservationSetFrom(TrackingSpecification);
            ReserveTransLine.CreateReservation(
              ForTransLine,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.",
              CalcReservEntry."Source Subtype");
            ForTransLine.CALCFIELDS("Reserved Qty. Outbnd. (Base)");
            ForTransLine.CALCFIELDS("Reserved Qty. Inbnd. (Base)");
          end;
        DATABASE::"Service Line":
          begin
            ReserveServiceInvLine.CreateReservationSetFrom(TrackingSpecification);
            ReserveServiceInvLine.CreateReservation(
              ForServiceLine,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForServiceLine.CALCFIELDS("Reserved Qty. (Base)");
          end;
        DATABASE::"Job Planning Line":
          begin
            JobPlanningLineReserve.CreateReservationSetFrom(TrackingSpecification);
            JobPlanningLineReserve.CreateReservation(
              ForJobPlanningLine,Description,ExpectedDate,Quantity,QuantityBase,
              CalcReservEntry."Serial No.",CalcReservEntry."Lot No.");
            ForJobPlanningLine.CALCFIELDS("Reserved Qty. (Base)");
          end;
      end;
    END;

    [External]
    PROCEDURE DeleteReservEntries@30(DeleteAll@1000 : Boolean;DownToQuantity@1001 : Decimal);
    VAR
      CalcReservEntry4@1004 : Record "Reservation Entry";
      TrackingMgt@1002 : Codeunit OrderTrackingManagement;
      ReservMgt@1003 : Codeunit "Reservation Management";
      QtyToReTrack@1005 : Decimal;
      QtyTracked@1006 : Decimal;
    BEGIN
      DeleteReservEntries2(DeleteAll,DownToQuantity,CalcReservEntry2);

      // Handle both sides of a req. line related to a transfer line:
      if ((CalcReservEntry."Source Type" = DATABASE::"Requisition Line") and
          (ForReqLine."Ref. Order Type" = ForReqLine."Ref. Order Type"::Transfer))
      then begin
        CalcReservEntry4 := CalcReservEntry;
        CalcReservEntry4."Source Subtype" := 1;
        CalcReservEntry4.SetPointerFilter;
        DeleteReservEntries2(DeleteAll,DownToQuantity,CalcReservEntry4);
      end;

      if DeleteAll then
        if ((CalcReservEntry."Source Type" = DATABASE::"Requisition Line") and
            (ForReqLine."Planning Line Origin" <> ForReqLine."Planning Line Origin"::" ")) or
           (CalcReservEntry."Source Type" = DATABASE::"Planning Component")
        then begin
          CalcReservEntry4.RESET;
          if TrackingMgt.DerivePlanningFilter(CalcReservEntry2,CalcReservEntry4) then
            if CalcReservEntry4.FINDFIRST then begin
              QtyToReTrack := ReservMgt.SourceQuantity(CalcReservEntry4,true);
              CalcReservEntry4.SETRANGE("Reservation Status",CalcReservEntry4."Reservation Status"::Reservation);
              if not CalcReservEntry4.ISEMPTY then begin
                CalcReservEntry4.CALCSUMS("Quantity (Base)");
                QtyTracked += CalcReservEntry4."Quantity (Base)";
              end;
              CalcReservEntry4.SETFILTER("Reservation Status",'<>%1',CalcReservEntry4."Reservation Status"::Reservation);
              CalcReservEntry4.SETFILTER("Item Tracking",'<>%1',CalcReservEntry4."Item Tracking"::None);
              if not CalcReservEntry4.ISEMPTY then begin
                CalcReservEntry4.CALCSUMS("Quantity (Base)");
                QtyTracked += CalcReservEntry4."Quantity (Base)";
              end;
              if CalcReservEntry."Source Type" = DATABASE::"Planning Component" then
                QtyTracked := -QtyTracked;
              ReservMgt.DeleteReservEntries(QtyTracked = 0,QtyTracked);
              ReservMgt.AutoTrack(QtyToReTrack);
            end;
        end;
    END;

    [External]
    PROCEDURE DeleteReservEntries2@31(DeleteAll@1000 : Boolean;DownToQuantity@1001 : Decimal;VAR ReservEntry@1002 : Record "Reservation Entry");
    VAR
      CalcReservEntry4@1003 : Record "Reservation Entry";
      SurplusEntry@1004 : Record "Reservation Entry";
      DummyEntry@1005 : Record "Reservation Entry";
      ReqLine@1018 : Record "Requisition Line";
      Status@1006 : 'Reservation,Tracking,Surplus,Prospect';
      QtyToRelease@1007 : Decimal;
      QtyTracked@1008 : Decimal;
      QtyToReleaseForLotSN@1017 : Decimal;
      CurrentQty@1016 : Decimal;
      CurrentSerialNo@1014 : Code[20];
      CurrentLotNo@1015 : Code[20];
      AvailabilityDate@1009 : Date;
      Release@1010 : '"Non-Inventory",Inventory';
      HandleItemTracking2@1013 : Boolean;
      SignFactor@1012 : Integer;
      QuantityIsValidated@1066 : Boolean;
    BEGIN
      ReservEntry.SETRANGE("Reservation Status");
      if ReservEntry.ISEMPTY then
        exit;
      CurrentSerialNo := ReservEntry."Serial No.";
      CurrentLotNo := ReservEntry."Lot No.";
      CurrentQty := ReservEntry."Quantity (Base)";

      GetItemSetup(ReservEntry);
      ReservEntry.TESTFIELD("Source Type");
      ReservEntry.Lock;
      SignFactor := CreateReservEntry.SignFactor(ReservEntry);
      QtyTracked := QuantityTracked(ReservEntry);
      CurrentBinding := ReservEntry.Binding;
      CurrentBindingIsSet := true;

      if (ReservEntry."Source Type" = DATABASE::"Requisition Line") and (DownToQuantity <> 0) then
        if ReqLine.GET(ReservEntry."Source ID",ReservEntry."Source Batch Name",ReservEntry."Source Ref. No.") then
          DownToQuantity := DownToQuantity - ReqLine."Original Quantity";

      // Item Tracking:
      if ItemTrackingCode."SN Specific Tracking" or ItemTrackingCode."Lot Specific Tracking" or
         (CurrentSerialNo <> '') or (CurrentLotNo <> '')
      then begin
        HandleItemTracking2 := true;
        case ItemTrackingHandling of
          ItemTrackingHandling::None:
            ReservEntry.SetTrackingFilter('','');
          ItemTrackingHandling::Match:
            begin
              if not ((CurrentSerialNo = '') and (CurrentLotNo = '')) then begin
                QtyToReleaseForLotSN := QuantityTracked2(ReservEntry);
                if ABS(QtyToReleaseForLotSN) > ABS(CurrentQty) then
                  QtyToReleaseForLotSN := CurrentQty;
                DownToQuantity := (QtyTracked - QtyToReleaseForLotSN) * SignFactor;
                ReservEntry.SetTrackingFilter(CurrentSerialNo,CurrentLotNo);
              end else
                DownToQuantity += CalcDownToQtySyncingToAssembly(ReservEntry);
            end;
        end;
      end;

      if SignFactor * QtyTracked * DownToQuantity < 0 then
        DeleteAll := true
      else
        if ABS(QtyTracked) < ABS(DownToQuantity) then
          exit;

      QtyToRelease := QtyTracked - (DownToQuantity * SignFactor);

      for Status := Status::Prospect downto Status::Reservation do begin
        ReservEntry.SETRANGE("Reservation Status",Status);
        if ReservEntry.FINDSET and (QtyToRelease <> 0) then
          case Status of
            Status::Prospect:
              repeat
                if (ABS(ReservEntry."Quantity (Base)") <= ABS(QtyToRelease)) or DeleteAll then begin
                  ReservEntry.DELETE;
                  SaveTrackingSpecification(ReservEntry,ReservEntry."Quantity (Base)");
                  QtyToRelease := QtyToRelease - ReservEntry."Quantity (Base)";
                end else begin
                  ReservEntry.VALIDATE("Quantity (Base)",ReservEntry."Quantity (Base)" - QtyToRelease);
                  ReservEntry.MODIFY;
                  SaveTrackingSpecification(ReservEntry,QtyToRelease);
                  QtyToRelease := 0;
                end;
              until (ReservEntry.NEXT = 0) or ((not DeleteAll) and (QtyToRelease = 0));
            Status::Surplus:
              repeat
                if CalcReservEntry4.GET(ReservEntry."Entry No.",not ReservEntry.Positive) then // Find related entry
                  ERROR(Text007);
                if (ABS(ReservEntry."Quantity (Base)") <= ABS(QtyToRelease)) or DeleteAll then begin
                  ReservEngineMgt.CloseReservEntry(ReservEntry,false,DeleteAll);
                  SaveTrackingSpecification(ReservEntry,ReservEntry."Quantity (Base)");
                  QtyToRelease := QtyToRelease - ReservEntry."Quantity (Base)";
                  if not DeleteAll and CalcReservEntry4.TrackingExists then begin
                    CalcReservEntry4."Reservation Status" := CalcReservEntry4."Reservation Status"::Surplus;
                    CalcReservEntry4.INSERT;
                  end;
                  ModifyActionMessage(ReservEntry."Entry No.",0,true); // Delete action messages
                end else begin
                  ReservEntry.VALIDATE("Quantity (Base)",ReservEntry."Quantity (Base)" - QtyToRelease);
                  ReservEntry.MODIFY;
                  SaveTrackingSpecification(ReservEntry,QtyToRelease);
                  ModifyActionMessage(ReservEntry."Entry No.",QtyToRelease,false); // Modify action messages
                  QtyToRelease := 0;
                end;
              until (ReservEntry.NEXT = 0) or ((not DeleteAll) and (QtyToRelease = 0));
            Status::Tracking,Status::Reservation:
              for Release := Release::"Non-Inventory" to Release::Inventory do begin
                // Release non-inventory reservations in first cycle
                repeat
                  CalcReservEntry4.GET(ReservEntry."Entry No.",not ReservEntry.Positive); // Find related entry
                  if (Release = Release::Inventory) = (CalcReservEntry4."Source Type" = DATABASE::"Item Ledger Entry") then begin
                    if (ABS(ReservEntry."Quantity (Base)") <= ABS(QtyToRelease)) or DeleteAll then begin
                      ReservEngineMgt.CloseReservEntry(ReservEntry,false,DeleteAll);
                      SaveTrackingSpecification(ReservEntry,ReservEntry."Quantity (Base)");
                      QtyToRelease := QtyToRelease - ReservEntry."Quantity (Base)";
                    end else begin
                      ReservEntry.VALIDATE("Quantity (Base)",ReservEntry."Quantity (Base)" - QtyToRelease);
                      ReservEntry.MODIFY;
                      SaveTrackingSpecification(ReservEntry,QtyToRelease);

                      if Item."Order Tracking Policy" <> Item."Order Tracking Policy"::None then begin
                        if CalcReservEntry4."Quantity (Base)" > 0 then
                          AvailabilityDate := CalcReservEntry4."Shipment Date"
                        else
                          AvailabilityDate := CalcReservEntry4."Expected Receipt Date";

                        QtyToRelease := -MatchSurplus(CalcReservEntry4,SurplusEntry,-QtyToRelease,
                            CalcReservEntry4."Quantity (Base)" < 0,AvailabilityDate,Item."Order Tracking Policy");

                        // Make residual surplus record:
                        if QtyToRelease <> 0 then begin
                          MakeConnection(CalcReservEntry4,CalcReservEntry4,-QtyToRelease,2,
                            AvailabilityDate,CalcReservEntry4.Binding);
                          if Item."Order Tracking Policy" = Item."Order Tracking Policy"::"Tracking & Action Msg." then begin
                            CreateReservEntry.GetLastEntry(SurplusEntry); // Get the surplus-entry just inserted
                            IssueActionMessage(SurplusEntry,false,DummyEntry);
                          end;
                        end;
                      end else
                        if ItemTrackingHandling = ItemTrackingHandling::None then
                          QuantityIsValidated := SaveItemTrackingAsSurplus(CalcReservEntry4,
                              -ReservEntry.Quantity,-ReservEntry."Quantity (Base)");

                      if not QuantityIsValidated then
                        CalcReservEntry4.VALIDATE("Quantity (Base)",-ReservEntry."Quantity (Base)");

                      CalcReservEntry4.MODIFY;
                      QtyToRelease := 0;
                      QuantityIsValidated := false;
                    end;
                  end;
                until (ReservEntry.NEXT = 0) or ((not DeleteAll) and (QtyToRelease = 0));
                if not ReservEntry.FINDFIRST then // Rewind for second cycle
                  Release := Release::Inventory;
              end;
          end;
      end;

      if HandleItemTracking2 then
        CheckQuantityIsCompletelyReleased(QtyToRelease,DeleteAll,CurrentSerialNo,CurrentLotNo,ReservEntry);
    END;

    [External]
    PROCEDURE CalculateRemainingQty@52(VAR RemainingQty@1000 : Decimal;VAR RemainingQtyBase@1001 : Decimal);
    BEGIN
      CalcReservEntry.TESTFIELD("Source Type");

      case CalcReservEntry."Source Type" of
        DATABASE::"Sales Line":
          begin
            ForSalesLine.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := ForSalesLine."Outstanding Quantity" - ABS(ForSalesLine."Reserved Quantity");
            RemainingQtyBase := ForSalesLine."Outstanding Qty. (Base)" - ABS(ForSalesLine."Reserved Qty. (Base)");
          end;
        DATABASE::"Requisition Line":
          begin
            ForReqLine.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := 0;
            RemainingQtyBase := ForReqLine."Net Quantity (Base)" - ABS(ForReqLine."Reserved Qty. (Base)");
          end;
        DATABASE::"Purchase Line":
          begin
            ForPurchLine.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := ForPurchLine."Outstanding Quantity" - ABS(ForPurchLine."Reserved Quantity");
            RemainingQtyBase := ForPurchLine."Outstanding Qty. (Base)" - ABS(ForPurchLine."Reserved Qty. (Base)");
          end;
        DATABASE::"Prod. Order Line":
          begin
            ForProdOrderLine.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := ForProdOrderLine."Remaining Quantity" - ABS(ForProdOrderLine."Reserved Quantity");
            RemainingQtyBase := ForProdOrderLine."Remaining Qty. (Base)" - ABS(ForProdOrderLine."Reserved Qty. (Base)");
          end;
        DATABASE::"Prod. Order Component":
          begin
            ForProdOrderComp.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := ForProdOrderComp."Remaining Quantity" - ABS(ForProdOrderComp."Reserved Quantity");
            RemainingQtyBase := ForProdOrderComp."Remaining Qty. (Base)" - ABS(ForProdOrderComp."Reserved Qty. (Base)");
          end;
        DATABASE::"Assembly Header":
          begin
            ForAssemblyHeader.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := ForAssemblyHeader."Remaining Quantity" - ABS(ForAssemblyHeader."Reserved Quantity");
            RemainingQtyBase := ForAssemblyHeader."Remaining Quantity (Base)" - ABS(ForAssemblyHeader."Reserved Qty. (Base)");
          end;
        DATABASE::"Assembly Line":
          begin
            ForAssemblyLine.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := ForAssemblyLine."Remaining Quantity" - ABS(ForAssemblyLine."Reserved Quantity");
            RemainingQtyBase := ForAssemblyLine."Remaining Quantity (Base)" - ABS(ForAssemblyLine."Reserved Qty. (Base)");
          end;
        DATABASE::"Planning Component":
          begin
            ForPlanningComponent.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := 0;
            RemainingQtyBase := ForPlanningComponent."Net Quantity (Base)" - ABS(ForPlanningComponent."Reserved Qty. (Base)");
          end;
        DATABASE::"Transfer Line":
          case CalcReservEntry."Source Subtype" of
            0: // Outbound
              begin
                ForTransLine.CALCFIELDS("Reserved Quantity Outbnd.","Reserved Qty. Outbnd. (Base)");
                RemainingQty := ForTransLine."Outstanding Quantity" - ABS(ForTransLine."Reserved Quantity Outbnd.");
                RemainingQtyBase := ForTransLine."Outstanding Qty. (Base)" - ABS(ForTransLine."Reserved Qty. Outbnd. (Base)");
              end;
            1: // Inbound
              begin
                ForTransLine.CALCFIELDS("Reserved Quantity Inbnd.","Reserved Qty. Inbnd. (Base)");
                RemainingQty := ForTransLine."Outstanding Quantity" - ABS(ForTransLine."Reserved Quantity Inbnd.");
                RemainingQtyBase := ForTransLine."Outstanding Qty. (Base)" - ABS(ForTransLine."Reserved Qty. Inbnd. (Base)");
              end;
          end;
        DATABASE::"Service Line":
          begin
            ForServiceLine.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := ForServiceLine."Outstanding Quantity" - ABS(ForServiceLine."Reserved Quantity");
            RemainingQtyBase := ForServiceLine."Outstanding Qty. (Base)" - ABS(ForServiceLine."Reserved Qty. (Base)");
          end;
        DATABASE::"Job Planning Line":
          begin
            ForJobPlanningLine.CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
            RemainingQty := ForJobPlanningLine."Remaining Qty." - ABS(ForJobPlanningLine."Reserved Quantity");
            RemainingQtyBase := ForJobPlanningLine."Remaining Qty. (Base)" - ABS(ForJobPlanningLine."Reserved Qty. (Base)");
          end;
        else
          ERROR(Text003);
      end;
    END;

    LOCAL PROCEDURE FieldFilterNeeded@6(VAR ReservEntry@1000 : Record "Reservation Entry";Field@1001 : '"Lot No.","Serial No."') : Boolean;
    BEGIN
      exit(FieldFilterNeeded2(ReservEntry,Positive,Field));
    END;

    [External]
    PROCEDURE FieldFilterNeeded2@42(VAR ReservEntry@1000 : Record "Reservation Entry";SearchForSupply@1001 : Boolean;Field@1002 : '"Lot No.","Serial No."') : Boolean;
    VAR
      ReservEntry2@1003 : Record "Reservation Entry";
      FieldValue@1004 : Code[20];
    BEGIN
      FieldFilter := '';

      FieldValue := '';
      GetItemSetup(ReservEntry);
      case Field of
        0:
          begin
            if not ItemTrackingCode."Lot Specific Tracking" then
              exit(false);
            FieldValue := ReservEntry."Lot No.";
          end;
        1:
          begin
            if not ItemTrackingCode."SN Specific Tracking" then
              exit(false);
            FieldValue := ReservEntry."Serial No.";
          end;
        else
          ERROR(Text004);
      end;

      // The field "Lot No." is used a foundation for building up the filter:

      if FieldValue <> '' then begin
        if SearchForSupply then // Demand
          ReservEntry2.SETRANGE("Lot No.",FieldValue)
        else // Supply
          ReservEntry2.SETFILTER("Lot No.",'%1|%2',FieldValue,'');
        FieldFilter := ReservEntry2.GETFILTER("Lot No.");
      end else // FieldValue = ''
        if SearchForSupply then // Demand
          exit(false)
        else
          FieldFilter := STRSUBSTNO('''%1''','');

      exit(true);
    END;

    LOCAL PROCEDURE FilterPlanningComponent@119(AvailabilityDate@1000 : Date);
    BEGIN
      with CalcPlanningComponent do begin
        RESET;
        SETCURRENTKEY("Item No.","Variant Code","Location Code","Due Date");
        SETRANGE("Item No.",CalcReservEntry."Item No.");
        SETRANGE("Variant Code",CalcReservEntry."Variant Code");
        SETRANGE("Location Code",CalcReservEntry."Location Code");
        SETFILTER("Due Date",GetAvailabilityFilter(AvailabilityDate));
        if Positive then
          SETFILTER("Net Quantity (Base)",'<0')
        else
          SETFILTER("Net Quantity (Base)",'>0');
      end;
    END;

    [External]
    PROCEDURE GetFieldFilter@8() : Text[80];
    BEGIN
      exit(FieldFilter);
    END;

    [External]
    PROCEDURE GetAvailabilityFilter@24(AvailabilityDate@1000 : Date) : Text[80];
    BEGIN
      exit(GetAvailabilityFilter2(AvailabilityDate,Positive));
    END;

    LOCAL PROCEDURE GetAvailabilityFilter2@41(AvailabilityDate@1000 : Date;SearchForSupply@1001 : Boolean) : Text[80];
    VAR
      ReservEntry2@1002 : Record "Reservation Entry";
    BEGIN
      if SearchForSupply then
        ReservEntry2.SETFILTER("Expected Receipt Date",'..%1',AvailabilityDate)
      else
        ReservEntry2.SETFILTER("Expected Receipt Date",'>=%1',AvailabilityDate);

      exit(ReservEntry2.GETFILTER("Expected Receipt Date"));
    END;

    [External]
    PROCEDURE CopySign@14(FromValue@1000 : Decimal;VAR ToValue@1001 : Decimal);
    BEGIN
      if FromValue * ToValue < 0 then
        ToValue := -ToValue;
    END;

    LOCAL PROCEDURE InitFilter@23(EntryID@1000 : Integer;AvailabilityDate@1001 : Date);
    BEGIN
      case EntryID of
        1:
          begin // Item Ledger Entry
            CalcItemLedgEntry.RESET;
            CalcItemLedgEntry.SETCURRENTKEY("Item No.",Open,"Variant Code",Positive,"Location Code");
            CalcItemLedgEntry.SETRANGE("Item No.",CalcReservEntry."Item No.");
            CalcItemLedgEntry.SETRANGE(Open,true);
            CalcItemLedgEntry.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcItemLedgEntry.SETRANGE(Positive,Positive);
            CalcItemLedgEntry.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcItemLedgEntry.SETRANGE("Drop Shipment",false);
            if FieldFilterNeeded2(CalcReservEntry,Positive,0) then
              CalcItemLedgEntry.SETFILTER("Lot No.",GetFieldFilter);
            if FieldFilterNeeded2(CalcReservEntry,Positive,1) then
              CalcItemLedgEntry.SETFILTER("Serial No.",GetFieldFilter);
          end;
        12,16:
          begin // Purchase Line
            CalcPurchLine.RESET;
            CalcPurchLine.SETCURRENTKEY(
              "Document Type",Type,"No.","Variant Code","Drop Shipment","Location Code","Expected Receipt Date");
            CalcPurchLine.SETRANGE("Document Type",EntryID - 11);
            CalcPurchLine.SETRANGE(Type,CalcPurchLine.Type::Item);
            CalcPurchLine.SETRANGE("No.",CalcReservEntry."Item No.");
            CalcPurchLine.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcPurchLine.SETRANGE("Drop Shipment",false);
            CalcPurchLine.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcPurchLine.SETFILTER("Expected Receipt Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive and (EntryID <> 16) then
              CalcPurchLine.SETFILTER("Quantity (Base)",'>0')
            else
              CalcPurchLine.SETFILTER("Quantity (Base)",'<0');
            CalcPurchLine.SETRANGE("Job No.",' ');
          end;
        21:
          begin // Requisition Line
            CalcReqLine.RESET;
            CalcReqLine.SETCURRENTKEY(
              Type,"No.","Variant Code","Location Code","Sales Order No.","Planning Line Origin","Due Date");
            CalcReqLine.SETRANGE(Type,CalcReqLine.Type::Item);
            CalcReqLine.SETRANGE("No.",CalcReservEntry."Item No.");
            CalcReqLine.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcReqLine.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcReqLine.SETRANGE("Sales Order No.",'');
            CalcReqLine.SETFILTER("Due Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive then
              CalcReqLine.SETFILTER("Quantity (Base)",'>0')
            else
              CalcReqLine.SETFILTER("Quantity (Base)",'<0');
          end;
        31,32,36:
          begin // Sales Line
            CalcSalesLine.RESET;
            CalcSalesLine.SETCURRENTKEY(
              "Document Type",Type,"No.","Variant Code","Drop Shipment","Location Code","Shipment Date");
            CalcSalesLine.SETRANGE("Document Type",EntryID - 31);
            CalcSalesLine.SETRANGE(Type,CalcSalesLine.Type::Item);
            CalcSalesLine.SETRANGE("No.",CalcReservEntry."Item No.");
            CalcSalesLine.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcSalesLine.SETRANGE("Drop Shipment",false);
            CalcSalesLine.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcSalesLine.SETFILTER("Shipment Date",GetAvailabilityFilter(AvailabilityDate));
            if EntryID = 36 then
              if Positive then
                CalcSalesLine.SETFILTER("Quantity (Base)",'>0')
              else
                CalcSalesLine.SETFILTER("Quantity (Base)",'<0')
            else
              if Positive then
                CalcSalesLine.SETFILTER("Quantity (Base)",'<0')
              else
                CalcSalesLine.SETFILTER("Quantity (Base)",'>0');
            CalcSalesLine.SETRANGE("Job No.",' ');
          end;
        61,62,63,64:
          begin // Prod. Order
            CalcProdOrderLine.RESET;
            CalcProdOrderLine.SETCURRENTKEY(Status,"Item No.","Variant Code","Location Code","Due Date");
            CalcProdOrderLine.SETRANGE(Status,EntryID - 61);
            CalcProdOrderLine.SETRANGE("Item No.",CalcReservEntry."Item No.");
            CalcProdOrderLine.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcProdOrderLine.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcProdOrderLine.SETFILTER("Due Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive then
              CalcProdOrderLine.SETFILTER("Remaining Qty. (Base)",'>0')
            else
              CalcProdOrderLine.SETFILTER("Remaining Qty. (Base)",'<0');
          end;
        71,72,73,74:
          begin // Prod. Order Component
            CalcProdOrderComp.RESET;
            CalcProdOrderComp.SETCURRENTKEY(Status,"Item No.","Variant Code","Location Code","Due Date");
            CalcProdOrderComp.SETRANGE(Status,EntryID - 71);
            CalcProdOrderComp.SETRANGE("Item No.",CalcReservEntry."Item No.");
            CalcProdOrderComp.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcProdOrderComp.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcProdOrderComp.SETFILTER("Due Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive then
              CalcProdOrderComp.SETFILTER("Remaining Qty. (Base)",'<0')
            else
              CalcProdOrderComp.SETFILTER("Remaining Qty. (Base)",'>0');
          end;
        91:
          FilterPlanningComponent(AvailabilityDate);
        101:
          begin // Transfer, Outbound
            CalcTransLine.RESET;
            CalcTransLine.SETCURRENTKEY("Transfer-from Code");
            CalcTransLine.SETRANGE("Item No.",CalcReservEntry."Item No.");
            CalcTransLine.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcTransLine.SETRANGE("Transfer-from Code",CalcReservEntry."Location Code");
            CalcTransLine.SETFILTER("Shipment Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive then
              CalcTransLine.SETFILTER("Outstanding Qty. (Base)",'<0')
            else
              CalcTransLine.SETFILTER("Outstanding Qty. (Base)",'>0');
          end;
        102:
          begin // Transfer, Inbound
            CalcTransLine.RESET;
            CalcTransLine.SETCURRENTKEY("Transfer-to Code");
            CalcTransLine.SETRANGE("Item No.",CalcReservEntry."Item No.");
            CalcTransLine.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcTransLine.SETRANGE("Transfer-to Code",CalcReservEntry."Location Code");
            CalcTransLine.SETFILTER("Receipt Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive then
              CalcTransLine.SETFILTER("Outstanding Qty. (Base)",'>0')
            else
              CalcTransLine.SETFILTER("Outstanding Qty. (Base)",'<0');
          end;
        110:
          begin // Service Line
            CalcServiceLine.RESET;
            CalcServiceLine.SETCURRENTKEY(Type,"No.","Variant Code","Location Code","Needed by Date","Document Type");
            CalcServiceLine.SETRANGE(Type,CalcServiceLine.Type::Item);
            CalcServiceLine.SETRANGE("No.",CalcReservEntry."Item No.");
            CalcServiceLine.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcServiceLine.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcServiceLine.SETFILTER("Needed by Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive then
              CalcServiceLine.SETFILTER("Quantity (Base)",'<0')
            else
              CalcServiceLine.SETFILTER("Quantity (Base)",'>0');
            CalcServiceLine.SETRANGE("Job No.",' ');
          end;
        133:
          begin // Job Planning Line
            CalcJobPlanningLine.RESET;
            CalcJobPlanningLine.SETCURRENTKEY(Status,Type,"No.","Variant Code","Location Code","Planning Date");
            CalcJobPlanningLine.SETRANGE(Status,EntryID - 131);
            CalcJobPlanningLine.SETRANGE(Type,CalcJobPlanningLine.Type::Item);
            CalcJobPlanningLine.SETRANGE("No.",CalcReservEntry."Item No.");
            CalcJobPlanningLine.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcJobPlanningLine.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcJobPlanningLine.SETFILTER("Planning Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive then
              CalcJobPlanningLine.SETFILTER("Quantity (Base)",'<0')
            else
              CalcJobPlanningLine.SETFILTER("Quantity (Base)",'>0');
          end;
        141,142:
          begin // Assembly Header
            CalcAssemblyHeader.RESET;
            CalcAssemblyHeader.SETCURRENTKEY(
              "Document Type","Item No.","Variant Code","Location Code","Due Date");
            CalcAssemblyHeader.SETRANGE("Document Type",EntryID - 141);
            CalcAssemblyHeader.SETRANGE("Item No.",CalcReservEntry."Item No.");
            CalcAssemblyHeader.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcAssemblyHeader.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcAssemblyHeader.SETFILTER("Due Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive then
              CalcAssemblyHeader.SETFILTER("Remaining Quantity (Base)",'>0')
            else
              CalcAssemblyHeader.SETFILTER("Remaining Quantity (Base)",'<0');
          end;
        151,152:
          begin // Assembly Line
            CalcAssemblyLine.RESET;
            CalcAssemblyLine.SETCURRENTKEY(
              "Document Type",Type,"No.","Variant Code","Location Code","Due Date");
            CalcAssemblyLine.SETRANGE("Document Type",EntryID - 151);
            CalcAssemblyLine.SETRANGE(Type,CalcAssemblyLine.Type::Item);
            CalcAssemblyLine.SETRANGE("No.",CalcReservEntry."Item No.");
            CalcAssemblyLine.SETRANGE("Variant Code",CalcReservEntry."Variant Code");
            CalcAssemblyLine.SETRANGE("Location Code",CalcReservEntry."Location Code");
            CalcAssemblyLine.SETFILTER("Due Date",GetAvailabilityFilter(AvailabilityDate));
            if Positive then
              CalcAssemblyLine.SETFILTER("Remaining Quantity (Base)",'<0')
            else
              CalcAssemblyLine.SETFILTER("Remaining Quantity (Base)",'>0');
          end;
      end;
    END;

    LOCAL PROCEDURE SetValueArray@25(EntryStatus@1000 : 'Reservation,Tracking,Simulation') : Integer;
    BEGIN
      CLEAR(ValueArray);
      case EntryStatus of
        0:
          begin // Reservation
            ValueArray[1] := 1;
            ValueArray[2] := 12;
            ValueArray[3] := 16;
            ValueArray[4] := 32;
            ValueArray[5] := 36;
            ValueArray[6] := 63;
            ValueArray[7] := 64;
            ValueArray[8] := 73;
            ValueArray[9] := 74;
            ValueArray[10] := 101;
            ValueArray[11] := 102;
            ValueArray[12] := 110;
            ValueArray[13] := 133;
            ValueArray[14] := 142;
            ValueArray[15] := 152;
            exit(15);
          end;
        1:
          begin // Order Tracking
            ValueArray[1] := 1;
            ValueArray[2] := 12;
            ValueArray[3] := 16;
            ValueArray[4] := 21;
            ValueArray[5] := 32;
            ValueArray[6] := 36;
            ValueArray[7] := 62;
            ValueArray[8] := 63;
            ValueArray[9] := 64;
            ValueArray[10] := 72;
            ValueArray[11] := 73;
            ValueArray[12] := 74;
            ValueArray[13] := 101;
            ValueArray[14] := 102;
            ValueArray[15] := 110;
            ValueArray[16] := 133;
            ValueArray[17] := 142;
            ValueArray[18] := 152;
            exit(18);
          end;
        2:
          begin // Simulation order tracking
            ValueArray[1] := 31;
            ValueArray[2] := 61;
            ValueArray[3] := 71;
            exit(3);
          end;
        3:
          begin // Item Tracking
            ValueArray[1] := 1;
            ValueArray[2] := 6500;
            exit(2);
          end;
      end;
    END;

    [External]
    PROCEDURE ClearSurplus@36();
    VAR
      ReservEntry2@1000 : Record "Reservation Entry";
    BEGIN
      CalcReservEntry.TESTFIELD("Source Type");
      ReservEntry2 := CalcReservEntry;
      ReservEntry2.SetPointerFilter;
      ReservEntry2.SETRANGE("Reservation Status",ReservEntry2."Reservation Status"::Surplus);
      // Item Tracking
      if ItemTrackingHandling = ItemTrackingHandling::None then
        ReservEntry2.SetTrackingFilter('','');

      if Item."Order Tracking Policy" = Item."Order Tracking Policy"::"Tracking & Action Msg." then begin
        ReservEntry2.Lock;
        if not ReservEntry2.FINDSET then
          exit;
        ActionMessageEntry.RESET;
        ActionMessageEntry.SETCURRENTKEY("Reservation Entry");
        repeat
          ActionMessageEntry.SETRANGE("Reservation Entry",ReservEntry2."Entry No.");
          ActionMessageEntry.DELETEALL;
        until ReservEntry2.NEXT = 0;
      end;

      ReservEntry2.SETRANGE(
        "Reservation Status",ReservEntry2."Reservation Status"::Surplus,ReservEntry2."Reservation Status"::Prospect);
      ReservEntry2.DELETEALL;
    END;

    LOCAL PROCEDURE QuantityTracked@28(VAR ReservEntry@1000 : Record "Reservation Entry") : Decimal;
    VAR
      ReservEntry2@1001 : Record "Reservation Entry";
      QtyTracked@1004 : Decimal;
    BEGIN
      ReservEntry2 := ReservEntry;
      ReservEntry2.SetPointerFilter;
      ReservEntry.COPYFILTER("Serial No.",ReservEntry2."Serial No.");
      ReservEntry.COPYFILTER("Lot No.",ReservEntry2."Lot No.");
      if ReservEntry2.FINDFIRST then begin
        ReservEntry.Binding := ReservEntry2.Binding;
        ReservEntry2.CALCSUMS("Quantity (Base)");
        QtyTracked := ReservEntry2."Quantity (Base)";
      end;
      exit(QtyTracked);
    END;

    LOCAL PROCEDURE QuantityTracked2@57(VAR ReservEntry@1000 : Record "Reservation Entry") : Decimal;
    VAR
      ReservEntry2@1001 : Record "Reservation Entry";
      QtyTracked@1004 : Decimal;
    BEGIN
      ReservEntry2 := ReservEntry;
      ReservEntry2.SetPointerFilter;
      ReservEntry2.SetTrackingFilterFromReservEntry(ReservEntry);
      ReservEntry2.SETRANGE("Reservation Status",
        ReservEntry2."Reservation Status"::Tracking,ReservEntry2."Reservation Status"::Prospect);
      if not ReservEntry2.ISEMPTY then begin
        ReservEntry2.CALCSUMS("Quantity (Base)");
        QtyTracked := ReservEntry2."Quantity (Base)";
      end;
      exit(QtyTracked);
    END;

    [External]
    PROCEDURE AutoTrack@27(TotalQty@1000 : Decimal);
    VAR
      SurplusEntry@1001 : Record "Reservation Entry";
      DummyEntry@1002 : Record "Reservation Entry";
      AvailabilityDate@1003 : Date;
      QtyToTrack@1004 : Decimal;
    BEGIN
      CalcReservEntry.TESTFIELD("Source Type");
      if CalcReservEntry."Item No." = '' then
        exit;

      GetItemSetup(CalcReservEntry);
      if Item."Order Tracking Policy" = Item."Order Tracking Policy"::None then
        exit;

      if CalcReservEntry."Source Type" in [DATABASE::"Sales Line",DATABASE::"Purchase Line",DATABASE::"Service Line"] then
        if not (CalcReservEntry."Source Subtype" in [1,5]) then
          exit; // Only order, return order

      if CalcReservEntry."Source Type" in [DATABASE::"Prod. Order Line",DATABASE::"Prod. Order Component"]
      then
        if CalcReservEntry."Source Subtype" = 0 then
          exit; // Not simulation

      CalcReservEntry.Lock;

      QtyToTrack := CreateReservEntry.SignFactor(CalcReservEntry) * TotalQty - QuantityTracked(CalcReservEntry);

      if QtyToTrack = 0 then begin
        UpdateDating;
        exit;
      end;

      QtyToTrack := MatchSurplus(CalcReservEntry,SurplusEntry,QtyToTrack,Positive,AvailabilityDate,Item."Order Tracking Policy");

      // Make residual surplus record:
      if QtyToTrack <> 0 then begin
        if CurrentBindingIsSet then
          MakeConnection(CalcReservEntry,SurplusEntry,QtyToTrack,2,AvailabilityDate,CurrentBinding)
        else
          MakeConnection(CalcReservEntry,SurplusEntry,QtyToTrack,2,AvailabilityDate,CalcReservEntry.Binding);
        if Item."Order Tracking Policy" = Item."Order Tracking Policy"::"Tracking & Action Msg." then begin // Issue Action Message
          CreateReservEntry.GetLastEntry(SurplusEntry); // Get the surplus-entry just inserted
          IssueActionMessage(SurplusEntry,true,DummyEntry);
        end;
      end else
        UpdateDating;
    END;

    [External]
    PROCEDURE MatchSurplus@40(VAR ReservEntry@1000 : Record "Reservation Entry";VAR SurplusEntry@1001 : Record "Reservation Entry";QtyToTrack@1002 : Decimal;SearchForSupply@1003 : Boolean;VAR AvailabilityDate@1004 : Date;TrackingPolicy@1005 : 'None,"Tracking Only","Tracking & Action Msg."') : Decimal;
    VAR
      ReservEntry2@1009 : Record "Reservation Entry";
      Search@1006 : Text[1];
      NextStep@1007 : Integer;
      ReservationStatus@1008 : 'Reservation,Tracking';
    BEGIN
      if QtyToTrack = 0 then
        exit;

      ReservEntry.Lock;
      SurplusEntry.SETCURRENTKEY(
        "Item No.","Variant Code","Location Code","Reservation Status",
        "Shipment Date","Expected Receipt Date","Serial No.","Lot No.");
      SurplusEntry.SETRANGE("Item No.",ReservEntry."Item No.");
      SurplusEntry.SETRANGE("Variant Code",ReservEntry."Variant Code");
      SurplusEntry.SETRANGE("Location Code",ReservEntry."Location Code");
      SurplusEntry.SETRANGE("Reservation Status",SurplusEntry."Reservation Status"::Surplus);
      if SearchForSupply then begin
        AvailabilityDate := ReservEntry."Shipment Date";
        Search := '+';
        NextStep := -1;
        SurplusEntry.SETFILTER("Expected Receipt Date",GetAvailabilityFilter2(AvailabilityDate,SearchForSupply));
        SurplusEntry.SETFILTER("Quantity (Base)",'>0');
      end else begin
        AvailabilityDate := ReservEntry."Expected Receipt Date";
        Search := '-';
        NextStep := 1;
        SurplusEntry.SETFILTER("Shipment Date",GetAvailabilityFilter2(AvailabilityDate,SearchForSupply));
        SurplusEntry.SETFILTER("Quantity (Base)",'<0')
      end;
      if FieldFilterNeeded2(ReservEntry,SearchForSupply,0) then
        SurplusEntry.SETFILTER("Lot No.",GetFieldFilter);
      if FieldFilterNeeded2(ReservEntry,SearchForSupply,1) then
        SurplusEntry.SETFILTER("Serial No.",GetFieldFilter);
      if SurplusEntry.FIND(Search) then
        repeat
          ReservationStatus := ReservationStatus::Tracking;
          if ABS(SurplusEntry."Quantity (Base)") <= ABS(QtyToTrack) then begin
            ReservEntry2 := SurplusEntry;
            MakeConnection(ReservEntry,SurplusEntry,-SurplusEntry."Quantity (Base)",ReservationStatus,
              AvailabilityDate,SurplusEntry.Binding);
            QtyToTrack := QtyToTrack + SurplusEntry."Quantity (Base)";
            SurplusEntry := ReservEntry2;
            SurplusEntry.DELETE;
            if TrackingPolicy = TrackingPolicy::"Tracking & Action Msg." then
              ModifyActionMessage(SurplusEntry."Entry No.",0,true); // Delete related Action Message
          end else begin
            SurplusEntry.VALIDATE("Quantity (Base)",SurplusEntry."Quantity (Base)" + QtyToTrack);
            SurplusEntry.MODIFY;
            MakeConnection(ReservEntry,SurplusEntry,QtyToTrack,ReservationStatus,AvailabilityDate,SurplusEntry.Binding);
            if TrackingPolicy = TrackingPolicy::"Tracking & Action Msg." then
              ModifyActionMessage(SurplusEntry."Entry No.",QtyToTrack,false); // Modify related Action Message
            QtyToTrack := 0;
          end;
        until (SurplusEntry.NEXT(NextStep) = 0) or (QtyToTrack = 0);

      exit(QtyToTrack);
    END;

    LOCAL PROCEDURE MakeConnection@26(VAR FromReservEntry@1000 : Record "Reservation Entry";VAR ToReservEntry@1001 : Record "Reservation Entry";Quantity@1002 : Decimal;ReservationStatus@1003 : 'Reservation,Tracking,Surplus';AvailabilityDate@1004 : Date;Binding@1005 : ',"Order-to-Order"');
    VAR
      sign@1007 : Integer;
    BEGIN
      if Quantity < 0 then
        ToReservEntry."Shipment Date" := AvailabilityDate
      else
        ToReservEntry."Expected Receipt Date" := AvailabilityDate;

      CreateReservEntry.SetBinding(Binding);

      if FromReservEntry."Planning Flexibility" <> FromReservEntry."Planning Flexibility"::Unlimited then
        CreateReservEntry.SetPlanningFlexibility(FromReservEntry."Planning Flexibility");

      sign := CreateReservEntry.SignFactor(FromReservEntry);
      CreateReservEntry.CreateReservEntryFor(
        FromReservEntry."Source Type",FromReservEntry."Source Subtype",FromReservEntry."Source ID",
        FromReservEntry."Source Batch Name",FromReservEntry."Source Prod. Order Line",FromReservEntry."Source Ref. No.",
        FromReservEntry."Qty. per Unit of Measure",
        0,sign * Quantity,
        FromReservEntry."Serial No.",FromReservEntry."Lot No.");
      CreateReservEntry.CreateReservEntryFrom(
        ToReservEntry."Source Type",ToReservEntry."Source Subtype",ToReservEntry."Source ID",ToReservEntry."Source Batch Name",
        ToReservEntry."Source Prod. Order Line",ToReservEntry."Source Ref. No.",ToReservEntry."Qty. per Unit of Measure",
        ToReservEntry."Serial No.",ToReservEntry."Lot No.");
      CreateReservEntry.SetApplyFromEntryNo(FromReservEntry."Appl.-from Item Entry");
      CreateReservEntry.SetApplyToEntryNo(FromReservEntry."Appl.-to Item Entry");
      CreateReservEntry.CreateEntry(
        FromReservEntry."Item No.",FromReservEntry."Variant Code",FromReservEntry."Location Code",
        FromReservEntry.Description,ToReservEntry."Expected Receipt Date",ToReservEntry."Shipment Date",0,ReservationStatus);
    END;

    [External]
    PROCEDURE ModifyUnitOfMeasure@29();
    BEGIN
      ReservEngineMgt.ModifyUnitOfMeasure(CalcReservEntry,CalcReservEntry."Qty. per Unit of Measure");
    END;

    [External]
    PROCEDURE MakeRoomForReservation@66(VAR ReservEntry@1000 : Record "Reservation Entry");
    VAR
      ReservEntry2@1001 : Record "Reservation Entry";
      TotalQuantity@1002 : Decimal;
    BEGIN
      TotalQuantity := SourceQuantity(ReservEntry,false);
      ReservEntry2 := ReservEntry;
      ReservEntry2.SetPointerFilter;
      ItemTrackingHandling := ItemTrackingHandling::Match;
      DeleteReservEntries2(false,TotalQuantity - (ReservEntry."Quantity (Base)" * CreateReservEntry.SignFactor(ReservEntry)),
        ReservEntry2);
    END;

    LOCAL PROCEDURE SaveTrackingSpecification@60(VAR ReservEntry@1000 : Record "Reservation Entry";QtyReleased@1001 : Decimal);
    BEGIN
      // Used when creating reservations.
      if ItemTrackingHandling = ItemTrackingHandling::None then
        exit;
      if not ReservEntry.TrackingExists then
        exit;
      TempTrackingSpecification.SetTrackingFilterFromReservEntry(ReservEntry);
      if TempTrackingSpecification.FINDSET then begin
        TempTrackingSpecification.VALIDATE("Quantity (Base)",
          TempTrackingSpecification."Quantity (Base)" + QtyReleased);
        TempTrackingSpecification.MODIFY;
      end else begin
        TempTrackingSpecification.TRANSFERFIELDS(ReservEntry);
        TempTrackingSpecification.VALIDATE("Quantity (Base)",QtyReleased);
        TempTrackingSpecification.INSERT;
      end;
      TempTrackingSpecification.RESET;
    END;

    [External]
    PROCEDURE CollectTrackingSpecification@64(VAR TargetTrackingSpecification@1001 : TEMPORARY Record "Tracking Specification") : Boolean;
    BEGIN
      // Used when creating reservations.
      TempTrackingSpecification.RESET;
      TargetTrackingSpecification.RESET;

      if not TempTrackingSpecification.FINDSET then
        exit(false);

      repeat
        TargetTrackingSpecification := TempTrackingSpecification;
        TargetTrackingSpecification.INSERT;
      until TempTrackingSpecification.NEXT = 0;

      TempTrackingSpecification.DELETEALL;

      exit(true);
    END;

    [External]
    PROCEDURE SourceQuantity@32(VAR ReservEntry@1000 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean) : Decimal;
    BEGIN
      exit(GetSourceRecordValue(ReservEntry,SetAsCurrent,0));
    END;

    [External]
    PROCEDURE GetSourceRecordValue@62(VAR ReservEntry@1000 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1012 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    BEGIN
      with ReservEntry do
        case "Source Type" of
          DATABASE::"Item Ledger Entry":
            exit(GetSourceItemLedgEntryValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Sales Line":
            exit(GetSourceSalesLineValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Requisition Line":
            exit(GetSourceReqLineValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Purchase Line":
            exit(GetSourcePurchLineValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Item Journal Line":
            exit(GetSourceItemJnlLineValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Job Journal Line":
            exit(GetSourceJobJnlLineValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Prod. Order Line":
            exit(GetSourceProdOrderLineValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Prod. Order Component":
            exit(GetSourceProdOrderCompValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Assembly Header":
            exit(GetSourceAsmHeaderValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Assembly Line":
            exit(GetSourceAsmLineValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Planning Component":
            exit(GetSourcePlanningCompValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Transfer Line":
            exit(GetSourceTransLineValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Service Line":
            exit(GetSourceServLineValue(ReservEntry,SetAsCurrent,ReturnOption));
          DATABASE::"Job Planning Line":
            exit(GetSourceJobPlanningLineValue(ReservEntry,SetAsCurrent,ReturnOption));
        end;
    END;

    LOCAL PROCEDURE GetSourceItemLedgEntryValue@91(VAR ReservEntry@1003 : Record "Reservation Entry";SetAsCurrent@1002 : Boolean;ReturnOption@1001 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      ItemLedgEntry@1000 : Record "Item Ledger Entry";
    BEGIN
      ItemLedgEntry.GET(ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetItemLedgEntry(ItemLedgEntry);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(ItemLedgEntry."Remaining Quantity");
        ReturnOption::"Gross Qty. (Base)":
          exit(ItemLedgEntry.Quantity);
      end;
    END;

    LOCAL PROCEDURE GetSourceSalesLineValue@92(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      SalesLine@1003 : Record "Sales Line";
    BEGIN
      SalesLine.GET(ReservEntry."Source Subtype",ReservEntry."Source ID",ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetSalesLine(SalesLine);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(SalesLine."Outstanding Qty. (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(SalesLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceReqLineValue@93(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      ReqLine@1003 : Record "Requisition Line";
    BEGIN
      ReqLine.GET(ReservEntry."Source ID",ReservEntry."Source Batch Name",ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetReqLine(ReqLine);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(ReqLine."Net Quantity (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(ReqLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourcePurchLineValue@94(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      PurchLine@1003 : Record "Purchase Line";
    BEGIN
      PurchLine.GET(ReservEntry."Source Subtype",ReservEntry."Source ID",ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetPurchLine(PurchLine);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(PurchLine."Outstanding Qty. (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(PurchLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceItemJnlLineValue@95(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      ItemJnlLine@1003 : Record "Item Journal Line";
    BEGIN
      ItemJnlLine.GET(ReservEntry."Source ID",ReservEntry."Source Batch Name",ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetItemJnlLine(ItemJnlLine);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(ItemJnlLine."Quantity (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(ItemJnlLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceJobJnlLineValue@96(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      JobJnlLine@1003 : Record "Job Journal Line";
    BEGIN
      JobJnlLine.GET(ReservEntry."Source ID",ReservEntry."Source Batch Name",ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetJobJnlLine(JobJnlLine);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(JobJnlLine."Quantity (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(JobJnlLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceProdOrderLineValue@98(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      ProdOrderLine@1003 : Record "Prod. Order Line";
    BEGIN
      ProdOrderLine.GET(ReservEntry."Source Subtype",ReservEntry."Source ID",ReservEntry."Source Prod. Order Line");
      if SetAsCurrent then
        SetProdOrderLine(ProdOrderLine);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(ProdOrderLine."Remaining Qty. (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(ProdOrderLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceProdOrderCompValue@99(VAR ReservEntry@1003 : Record "Reservation Entry";SetAsCurrent@1002 : Boolean;ReturnOption@1001 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      ProdOrderComp@1000 : Record "Prod. Order Component";
    BEGIN
      ProdOrderComp.GET(
        ReservEntry."Source Subtype",
        ReservEntry."Source ID",
        ReservEntry."Source Prod. Order Line",
        ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetProdOrderComponent(ProdOrderComp);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(ProdOrderComp."Remaining Qty. (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(ProdOrderComp."Expected Qty. (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceAsmHeaderValue@116(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      AssemblyHeader@1003 : Record "Assembly Header";
    BEGIN
      AssemblyHeader.GET(ReservEntry."Source Subtype",ReservEntry."Source ID");
      if SetAsCurrent then
        SetAssemblyHeader(AssemblyHeader);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(AssemblyHeader."Remaining Quantity (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(AssemblyHeader."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceAsmLineValue@115(VAR ReservEntry@1003 : Record "Reservation Entry";SetAsCurrent@1002 : Boolean;ReturnOption@1001 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      AssemblyLine@1000 : Record "Assembly Line";
    BEGIN
      AssemblyLine.GET(ReservEntry."Source Subtype",ReservEntry."Source ID",ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetAssemblyLine(AssemblyLine);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(AssemblyLine."Remaining Quantity (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(AssemblyLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourcePlanningCompValue@100(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      PlanningComponent@1003 : Record "Planning Component";
    BEGIN
      PlanningComponent.GET(
        ReservEntry."Source ID",ReservEntry."Source Batch Name",
        ReservEntry."Source Prod. Order Line",ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetPlanningComponent(PlanningComponent);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(PlanningComponent."Net Quantity (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(PlanningComponent."Expected Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceTransLineValue@101(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      TransLine@1003 : Record "Transfer Line";
    BEGIN
      TransLine.GET(ReservEntry."Source ID",ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetTransferLine(TransLine,ReservEntry."Source Subtype");
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(TransLine."Outstanding Qty. (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(TransLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceServLineValue@102(VAR ReservEntry@1002 : Record "Reservation Entry";SetAsCurrent@1001 : Boolean;ReturnOption@1000 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      ServLine@1003 : Record "Service Line";
    BEGIN
      ServLine.GET(ReservEntry."Source Subtype",ReservEntry."Source ID",ReservEntry."Source Ref. No.");
      if SetAsCurrent then
        SetServLine(ServLine);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(ServLine."Outstanding Qty. (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(ServLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetSourceJobPlanningLineValue@103(VAR ReservEntry@1001 : Record "Reservation Entry";SetAsCurrent@1002 : Boolean;ReturnOption@1003 : '"Net Qty. (Base)","Gross Qty. (Base)"') : Decimal;
    VAR
      JobPlanningLine@1000 : Record "Job Planning Line";
    BEGIN
      JobPlanningLine.SETCURRENTKEY("Job Contract Entry No.");
      JobPlanningLine.SETRANGE("Job Contract Entry No.",ReservEntry."Source Ref. No.");
      JobPlanningLine.FINDFIRST;
      if SetAsCurrent then
        SetJobPlanningLine(JobPlanningLine);
      case ReturnOption of
        ReturnOption::"Net Qty. (Base)":
          exit(JobPlanningLine."Remaining Qty. (Base)");
        ReturnOption::"Gross Qty. (Base)":
          exit(JobPlanningLine."Quantity (Base)");
      end;
    END;

    LOCAL PROCEDURE GetItemSetup@33(VAR ReservEntry@1000 : Record "Reservation Entry");
    BEGIN
      if ReservEntry."Item No." <> Item."No." then begin
        Item.GET(ReservEntry."Item No.");
        if Item."Item Tracking Code" <> '' then
          ItemTrackingCode.GET(Item."Item Tracking Code")
        else
          ItemTrackingCode.INIT;
        GetPlanningParameters.AtSKU(SKU,ReservEntry."Item No.",
          ReservEntry."Variant Code",ReservEntry."Location Code");
        MfgSetup.GET;
      end;
    END;

    [External]
    PROCEDURE MarkReservConnection@35(VAR ReservEntry@1001 : Record "Reservation Entry";TargetReservEntry@1002 : Record "Reservation Entry") ReservedQuantity@1000 : Decimal;
    VAR
      ReservEntry2@1003 : Record "Reservation Entry";
      SignFactor@1004 : Integer;
    BEGIN
      if not ReservEntry.FINDSET then
        exit;
      SignFactor := CreateReservEntry.SignFactor(ReservEntry);

      repeat
        if ReservEntry2.GET(ReservEntry."Entry No.",not ReservEntry.Positive) then
          if ((ReservEntry2."Source Type" = TargetReservEntry."Source Type") and
              (ReservEntry2."Source Subtype" = TargetReservEntry."Source Subtype") and
              (ReservEntry2."Source ID" = TargetReservEntry."Source ID") and
              (ReservEntry2."Source Batch Name" = TargetReservEntry."Source Batch Name") and
              (ReservEntry2."Source Prod. Order Line" = TargetReservEntry."Source Prod. Order Line") and
              (ReservEntry2."Source Ref. No." = TargetReservEntry."Source Ref. No."))
          then begin
            ReservEntry.MARK(true);
            ReservedQuantity += ReservEntry."Quantity (Base)" * SignFactor;
          end;
      until ReservEntry.NEXT = 0;
      ReservEntry.MARKEDONLY(true);
    END;

    LOCAL PROCEDURE IsSpecialOrder@121(PurchasingCode@1000 : Code[10]) : Boolean;
    VAR
      Purchasing@1001 : Record Purchasing;
    BEGIN
      if PurchasingCode <> '' then
        if Purchasing.GET(PurchasingCode) then
          exit(Purchasing."Special Order");

      exit(false);
    END;

    [External]
    PROCEDURE IssueActionMessage@37(VAR SurplusEntry@1000 : Record "Reservation Entry";UseGlobalSettings@1001 : Boolean;AllDeletedEntry@1002 : Record "Reservation Entry");
    VAR
      ReservEntry@1003 : Record "Reservation Entry";
      ReservEntry2@1004 : Record "Reservation Entry";
      ReservEntry3@1005 : Record "Reservation Entry";
      ActionMessageEntry2@1006 : Record "Action Message Entry";
      NextEntryNo@1007 : Integer;
      FirstDate@1008 : Date;
      Found@1009 : Boolean;
      FreeBinding@1010 : Boolean;
      NoMoreData@1011 : Boolean;
      DateFormula@1012 : DateFormula;
    BEGIN
      SurplusEntry.TESTFIELD("Quantity (Base)");
      if SurplusEntry."Reservation Status" < SurplusEntry."Reservation Status"::Surplus then
        SurplusEntry.FIELDERROR("Reservation Status");
      SurplusEntry.CALCFIELDS("Action Message Adjustment");
      if SurplusEntry."Quantity (Base)" + SurplusEntry."Action Message Adjustment" = 0 then
        exit;

      ActionMessageEntry.RESET;

      if ActionMessageEntry.FINDLAST then
        NextEntryNo := ActionMessageEntry."Entry No." + 1
      else
        NextEntryNo := 1;

      ActionMessageEntry.INIT;
      ActionMessageEntry."Entry No." := NextEntryNo;

      if SurplusEntry."Quantity (Base)" > 0 then begin // Supply: Issue AM directly
        if SurplusEntry."Planning Flexibility" = SurplusEntry."Planning Flexibility"::None then
          exit;
        if not (SurplusEntry."Source Type" in [DATABASE::"Prod. Order Line",DATABASE::"Purchase Line"])
        then
          exit;

        ActionMessageEntry.TransferFromReservEntry(SurplusEntry);
        ActionMessageEntry.Quantity := -(SurplusEntry."Quantity (Base)" + SurplusEntry."Action Message Adjustment");
        ActionMessageEntry.Type := ActionMessageEntry.Type::New;
        ReservEntry2 := SurplusEntry;
      end else begin // Demand: Find supply and issue AM
        case SurplusEntry.Binding of
          SurplusEntry.Binding::" ":
            begin
              if UseGlobalSettings then begin
                ReservEntry.COPY(SurplusEntry); // Copy filter and sorting
                ReservEntry.SETRANGE("Reservation Status"); // Remove filter on Reservation Status
              end else begin
                GetItemSetup(SurplusEntry);
                Positive := true;
                ReservEntry.SETCURRENTKEY(
                  "Item No.","Variant Code","Location Code","Reservation Status",
                  "Shipment Date","Expected Receipt Date","Serial No.","Lot No.");
                ReservEntry.SETRANGE("Item No.",SurplusEntry."Item No.");
                ReservEntry.SETRANGE("Variant Code",SurplusEntry."Variant Code");
                ReservEntry.SETRANGE("Location Code",SurplusEntry."Location Code");
                ReservEntry.SETFILTER("Expected Receipt Date",GetAvailabilityFilter(SurplusEntry."Shipment Date"));
                if FieldFilterNeeded(SurplusEntry,0) then
                  ReservEntry.SETFILTER("Lot No.",GetFieldFilter);
                if FieldFilterNeeded(SurplusEntry,1) then
                  ReservEntry.SETFILTER("Serial No.",GetFieldFilter);
                ReservEntry.SETRANGE(Positive,true);
              end;
              ReservEntry.SETRANGE(Binding,ReservEntry.Binding::" ");
              ReservEntry.SETRANGE("Planning Flexibility",ReservEntry."Planning Flexibility"::Unlimited);
              ReservEntry.SETFILTER("Source Type",'=%1|=%2',DATABASE::"Purchase Line",DATABASE::"Prod. Order Line");
            end;
          SurplusEntry.Binding::"Order-to-Order":
            begin
              ReservEntry3 := SurplusEntry;
              ReservEntry3.SetPointerFilter;
              ReservEntry3.SETRANGE(
                "Reservation Status",ReservEntry3."Reservation Status"::Reservation,ReservEntry3."Reservation Status"::Tracking);
              ReservEntry3.SETRANGE(Binding,ReservEntry3.Binding::"Order-to-Order");
              if ReservEntry3.FINDFIRST then begin
                ReservEntry3.GET(ReservEntry3."Entry No.",not ReservEntry3.Positive);
                ReservEntry := ReservEntry3;
                ReservEntry.SETRECFILTER;
                Found := true;
              end else begin
                Found := false;
                FreeBinding := true;
              end;
            end;
        end;

        ActionMessageEntry.Quantity := -(SurplusEntry."Quantity (Base)" + SurplusEntry."Action Message Adjustment");

        if not FreeBinding then
          if ReservEntry.FIND('+') then begin
            if AllDeletedEntry."Entry No." > 0 then // The supply record has been deleted and cannot be reused.
              repeat
                Found := not (
                              (AllDeletedEntry."Source Type" = ReservEntry."Source Type") and
                              (AllDeletedEntry."Source Subtype" = ReservEntry."Source Subtype") and
                              (AllDeletedEntry."Source ID" = ReservEntry."Source ID") and
                              (AllDeletedEntry."Source Batch Name" = ReservEntry."Source Batch Name") and
                              (AllDeletedEntry."Source Prod. Order Line" = ReservEntry."Source Prod. Order Line") and
                              (AllDeletedEntry."Source Ref. No." = ReservEntry."Source Ref. No."));
                if not Found then
                  NoMoreData := ReservEntry.NEXT(-1) = 0;
              until Found or NoMoreData
            else
              Found := true;
          end;

        if Found then begin
          ActionMessageEntry.TransferFromReservEntry(ReservEntry);
          ActionMessageEntry.Type := ActionMessageEntry.Type::"Change Qty.";
          ReservEntry2 := ReservEntry;
        end else begin
          ActionMessageEntry."Location Code" := SurplusEntry."Location Code";
          ActionMessageEntry."Variant Code" := SurplusEntry."Variant Code";
          ActionMessageEntry."Item No." := SurplusEntry."Item No.";

          case SKU."Replenishment System" of
            SKU."Replenishment System"::Purchase:
              ActionMessageEntry."Source Type" := DATABASE::"Purchase Line";
            SKU."Replenishment System"::"Prod. Order":
              ActionMessageEntry."Source Type" := DATABASE::"Prod. Order Line";
            SKU."Replenishment System"::Transfer:
              ActionMessageEntry."Source Type" := DATABASE::"Transfer Line";
            SKU."Replenishment System"::Assembly:
              ActionMessageEntry."Source Type" := DATABASE::"Assembly Header";
          end;

          ActionMessageEntry.Type := ActionMessageEntry.Type::New;
        end;
        ActionMessageEntry."Reservation Entry" := SurplusEntry."Entry No.";
      end;

      ReservEntry2.SetPointerFilter;
      ReservEntry2.SETRANGE(
        "Reservation Status",ReservEntry2."Reservation Status"::Reservation,ReservEntry2."Reservation Status"::Tracking);

      if ReservEntry2."Source Type" <> DATABASE::"Item Ledger Entry" then
        if ReservEntry2.FINDFIRST then begin
          FirstDate := FindDate(ReservEntry2,0,true);
          if FirstDate <> 0D then begin
            if (FORMAT(MfgSetup."Default Dampener Period") = '') or
               ((ReservEntry2.Binding = ReservEntry2.Binding::"Order-to-Order") and
                (ReservEntry2."Reservation Status" = ReservEntry2."Reservation Status"::Reservation))
            then
              EVALUATE(MfgSetup."Default Dampener Period",'<0D>');

            EVALUATE(DateFormula,STRSUBSTNO('%1%2','-',FORMAT(MfgSetup."Default Dampener Period")));
            if CALCDATE(DateFormula,FirstDate) > ReservEntry2."Expected Receipt Date" then begin
              ActionMessageEntry2.SETCURRENTKEY(
                "Source Type","Source Subtype","Source ID","Source Batch Name","Source Prod. Order Line","Source Ref. No.");
              ActionMessageEntry2.SETRANGE("Source Type",ActionMessageEntry."Source Type");
              ActionMessageEntry2.SETRANGE("Source Subtype",ActionMessageEntry."Source Subtype");
              ActionMessageEntry2.SETRANGE("Source ID",ActionMessageEntry."Source ID");
              ActionMessageEntry2.SETRANGE("Source Batch Name",ActionMessageEntry."Source Batch Name");
              ActionMessageEntry2.SETRANGE("Source Prod. Order Line",ActionMessageEntry."Source Prod. Order Line");
              ActionMessageEntry2.SETRANGE("Source Ref. No.",ActionMessageEntry."Source Ref. No.");
              ActionMessageEntry2.SETRANGE(Quantity,0);
              ActionMessageEntry2.DELETEALL;
              ActionMessageEntry2.RESET;
              ActionMessageEntry2 := ActionMessageEntry;
              ActionMessageEntry2.Quantity := 0;
              ActionMessageEntry2."New Date" := FirstDate;
              ActionMessageEntry2.Type := ActionMessageEntry.Type::Reschedule;
              ActionMessageEntry2."Reservation Entry" := ReservEntry2."Entry No.";
              while not ActionMessageEntry2.INSERT do
                ActionMessageEntry2."Entry No." += 1;
              ActionMessageEntry."Entry No." := ActionMessageEntry2."Entry No." + 1;
            end;
          end;
        end;

      while not ActionMessageEntry.INSERT do
        ActionMessageEntry."Entry No." += 1;
    END;

    [External]
    PROCEDURE ModifyActionMessage@38(RelatedToEntryNo@1000 : Integer;Quantity@1001 : Decimal;Delete@1002 : Boolean);
    BEGIN
      ActionMessageEntry.RESET;
      ActionMessageEntry.SETCURRENTKEY("Reservation Entry");
      ActionMessageEntry.SETRANGE("Reservation Entry",RelatedToEntryNo);

      if Delete then begin
        ActionMessageEntry.DELETEALL;
        exit;
      end;
      ActionMessageEntry.SETRANGE("New Date",0D);

      if ActionMessageEntry.FINDFIRST then begin
        ActionMessageEntry.Quantity -= Quantity;
        if ActionMessageEntry.Quantity = 0 then
          ActionMessageEntry.DELETE
        else
          ActionMessageEntry.MODIFY;
      end;
    END;

    [External]
    PROCEDURE FindDate@43(VAR ReservEntry@1000 : Record "Reservation Entry";Which@1001 : '"Earliest Shipment","Latest Receipt"';ReturnRecord@1002 : Boolean) : Date;
    VAR
      ReservEntry2@1003 : Record "Reservation Entry";
      LastDate@1004 : Date;
    BEGIN
      ReservEntry2.COPY(ReservEntry); // Copy filter and sorting

      if not ReservEntry2.FINDSET then
        exit;

      case Which of
        0:
          begin
            LastDate := DMY2DATE(31,12,9999);
            repeat
              if ReservEntry2."Shipment Date" < LastDate then begin
                LastDate := ReservEntry2."Shipment Date";
                if ReturnRecord then
                  ReservEntry := ReservEntry2;
              end;
            until ReservEntry2.NEXT = 0;
          end;
        1:
          begin
            LastDate := 0D;
            repeat
              if ReservEntry2."Expected Receipt Date" > LastDate then begin
                LastDate := ReservEntry2."Expected Receipt Date";
                if ReturnRecord then
                  ReservEntry := ReservEntry2;
              end;
            until ReservEntry2.NEXT = 0;
          end;
      end;
      exit(LastDate);
    END;

    LOCAL PROCEDURE UpdateDating@45();
    VAR
      FilterReservEntry@1000 : Record "Reservation Entry";
      ReservEntry2@1001 : Record "Reservation Entry";
    BEGIN
      if CalcReservEntry2."Source Type" = DATABASE::"Planning Component" then
        exit;

      if Item."Order Tracking Policy" <> Item."Order Tracking Policy"::"Tracking & Action Msg." then
        exit;

      if CalcReservEntry2."Source Type" = DATABASE::"Requisition Line" then
        if ForReqLine."Planning Line Origin" <> ForReqLine."Planning Line Origin"::" " then
          exit;

      FilterReservEntry := CalcReservEntry2;
      FilterReservEntry.SetPointerFilter;

      if not FilterReservEntry.FINDFIRST then
        exit;

      if CalcReservEntry2."Source Type" in [DATABASE::"Prod. Order Line",DATABASE::"Purchase Line"]
      then
        ReservEngineMgt.ModifyActionMessageDating(FilterReservEntry)
      else begin
        if FilterReservEntry.Positive then
          exit;
        FilterReservEntry.SETRANGE("Reservation Status",FilterReservEntry."Reservation Status"::Reservation,
          FilterReservEntry."Reservation Status"::Tracking);
        if not FilterReservEntry.FINDSET then
          exit;
        repeat
          if ReservEntry2.GET(FilterReservEntry."Entry No.",not FilterReservEntry.Positive) then
            ReservEngineMgt.ModifyActionMessageDating(ReservEntry2);
        until FilterReservEntry.NEXT = 0;
      end;
    END;

    [External]
    PROCEDURE ClearActionMessageReferences@46();
    VAR
      ActionMessageEntry2@1000 : Record "Action Message Entry";
    BEGIN
      ActionMessageEntry.RESET;
      ActionMessageEntry.FilterFromReservEntry(CalcReservEntry);
      if ActionMessageEntry.FINDSET then
        repeat
          ActionMessageEntry2 := ActionMessageEntry;
          if ActionMessageEntry2.Quantity = 0 then
            ActionMessageEntry2.DELETE
          else begin
            ActionMessageEntry2."Source Subtype" := 0;
            ActionMessageEntry2."Source ID" := '';
            ActionMessageEntry2."Source Batch Name" := '';
            ActionMessageEntry2."Source Prod. Order Line" := 0;
            ActionMessageEntry2."Source Ref. No." := 0;
            ActionMessageEntry2."New Date" := 0D;
            ActionMessageEntry2.MODIFY;
          end;
        until ActionMessageEntry.NEXT = 0;
    END;

    [External]
    PROCEDURE SetItemTrackingHandling@10(Mode@1000 : 'None,"Allow deletion",Match');
    BEGIN
      ItemTrackingHandling := Mode;
    END;

    [External]
    PROCEDURE DeleteItemTrackingConfirm@65() : Boolean;
    BEGIN
      if not ItemTrackingExist(CalcReservEntry2) then
        exit(true);

      if CONFIRM(Text011,false,CalcReservEntry2."Item No.",CalcReservEntry2.TextCaption) then
        exit(true);

      exit(false);
    END;

    LOCAL PROCEDURE ItemTrackingExist@67(VAR ReservEntry@1000 : Record "Reservation Entry") : Boolean;
    VAR
      ReservEntry2@1001 : Record "Reservation Entry";
    BEGIN
      ReservEntry2.COPY(ReservEntry);
      ReservEntry2.SETFILTER("Item Tracking",'> %1',ReservEntry2."Item Tracking"::None);
      exit(not ReservEntry2.ISEMPTY);
    END;

    [External]
    PROCEDURE SetSerialLotNo@17(SerialNo@1001 : Code[20];LotNo@1000 : Code[20]);
    BEGIN
      CalcReservEntry."Serial No." := SerialNo;
      CalcReservEntry."Lot No." := LotNo;
    END;

    [External]
    PROCEDURE SetMatchFilter@51(VAR ReservEntry@1000 : Record "Reservation Entry";VAR FilterReservEntry@1001 : Record "Reservation Entry";SearchForSupply@1002 : Boolean;AvailabilityDate@1003 : Date);
    BEGIN
      FilterReservEntry.RESET;
      FilterReservEntry.SETCURRENTKEY(
        "Item No.","Variant Code","Location Code","Reservation Status",
        "Shipment Date","Expected Receipt Date","Serial No.","Lot No.");
      FilterReservEntry.SETRANGE("Item No.",ReservEntry."Item No.");
      FilterReservEntry.SETRANGE("Variant Code",ReservEntry."Variant Code");
      FilterReservEntry.SETRANGE("Location Code",ReservEntry."Location Code");
      FilterReservEntry.SETRANGE("Reservation Status",
        FilterReservEntry."Reservation Status"::Reservation,FilterReservEntry."Reservation Status"::Surplus);
      if SearchForSupply then
        FilterReservEntry.SETFILTER("Expected Receipt Date",'..%1',AvailabilityDate)
      else
        FilterReservEntry.SETFILTER("Shipment Date",'>=%1',AvailabilityDate);
      if FieldFilterNeeded2(ReservEntry,SearchForSupply,0) then
        FilterReservEntry.SETFILTER("Lot No.",GetFieldFilter);
      if FieldFilterNeeded2(ReservEntry,SearchForSupply,1) then
        FilterReservEntry.SETFILTER("Serial No.",GetFieldFilter);
      FilterReservEntry.SETRANGE(Positive,SearchForSupply);
    END;

    [External]
    PROCEDURE LookupLine@59(SourceType@1005 : Integer;SourceSubtype@1004 : Integer;SourceID@1003 : Code[20];SourceBatchName@1002 : Code[10];SourceProdOrderLine@1001 : Integer;SourceRefNo@1000 : Integer);
    VAR
      ItemLedgEntry@1020 : Record "Item Ledger Entry";
      SalesLine@1018 : Record "Sales Line";
      PurchLine@1016 : Record "Purchase Line";
      ItemJnlLine@1015 : Record "Item Journal Line";
      ReqLine@1013 : Record "Requisition Line";
      ProdOrderLine@1011 : Record "Prod. Order Line";
      ProdOrderComp@1010 : Record "Prod. Order Component";
      PlanningComponent@1009 : Record "Planning Component";
      ServLine@1006 : Record "Service Line";
      JobPlanningLine@1007 : Record "Job Planning Line";
      AsmHeader@1008 : Record "Assembly Header";
      AsmLine@1012 : Record "Assembly Line";
    BEGIN
      case SourceType of
        DATABASE::"Sales Line":
          begin
            SalesLine.RESET;
            SalesLine.SETRANGE("Document Type",SourceSubtype);
            SalesLine.SETRANGE("Document No.",SourceID);
            SalesLine.SETRANGE("Line No.",SourceRefNo);
            PAGE.RUN(PAGE::"Sales Lines",SalesLine);
          end;
        DATABASE::"Requisition Line":
          begin
            ReqLine.RESET;
            ReqLine.SETRANGE("Worksheet Template Name",SourceID);
            ReqLine.SETRANGE("Journal Batch Name",SourceBatchName);
            ReqLine.SETRANGE("Line No.",SourceRefNo);
            PAGE.RUN(PAGE::"Requisition Lines",ReqLine);
          end;
        DATABASE::"Purchase Line":
          begin
            PurchLine.RESET;
            PurchLine.SETRANGE("Document Type",SourceSubtype);
            PurchLine.SETRANGE("Document No.",SourceID);
            PurchLine.SETRANGE("Line No.",SourceRefNo);
            PAGE.RUN(PAGE::"Purchase Lines",PurchLine);
          end;
        DATABASE::"Item Journal Line":
          begin
            ItemJnlLine.RESET;
            ItemJnlLine.SETRANGE("Journal Template Name",SourceID);
            ItemJnlLine.SETRANGE("Journal Batch Name",SourceBatchName);
            ItemJnlLine.SETRANGE("Line No.",SourceRefNo);
            ItemJnlLine.SETRANGE("Entry Type",SourceSubtype);
            PAGE.RUN(PAGE::"Item Journal Lines",ItemJnlLine);
          end;
        DATABASE::"Item Ledger Entry":
          begin
            ItemLedgEntry.RESET;
            ItemLedgEntry.SETRANGE("Entry No.",SourceRefNo);
            PAGE.RUN(0,ItemLedgEntry);
          end;
        DATABASE::"Prod. Order Line":
          begin
            ProdOrderLine.RESET;
            ProdOrderLine.SETRANGE(Status,SourceSubtype);
            ProdOrderLine.SETRANGE("Prod. Order No.",SourceID);
            ProdOrderLine.SETRANGE("Line No.",SourceProdOrderLine);
            PAGE.RUN(0,ProdOrderLine);
          end;
        DATABASE::"Prod. Order Component":
          begin
            ProdOrderComp.RESET;
            ProdOrderComp.SETRANGE(Status,SourceSubtype);
            ProdOrderComp.SETRANGE("Prod. Order No.",SourceID);
            ProdOrderComp.SETRANGE("Prod. Order Line No.",SourceProdOrderLine);
            ProdOrderComp.SETRANGE("Line No.",SourceRefNo);
            PAGE.RUN(0,ProdOrderComp);
          end;
        DATABASE::"Planning Component":
          begin
            PlanningComponent.RESET;
            PlanningComponent.SETRANGE("Worksheet Template Name",SourceID);
            PlanningComponent.SETRANGE("Worksheet Batch Name",SourceBatchName);
            PlanningComponent.SETRANGE("Worksheet Line No.",SourceProdOrderLine);
            PlanningComponent.SETRANGE("Line No.",SourceRefNo);
            PAGE.RUN(0,PlanningComponent);
          end;
        DATABASE::"Service Line":
          begin
            ServLine.RESET;
            ServLine.SETRANGE("Document Type",SourceSubtype);
            ServLine.SETRANGE("Document No.",SourceID);
            ServLine.SETRANGE("Line No.",SourceRefNo);
            PAGE.RUN(0,ServLine);
          end;
        DATABASE::"Job Planning Line":
          begin
            JobPlanningLine.RESET;
            JobPlanningLine.SETCURRENTKEY("Job Contract Entry No.");
            JobPlanningLine.SETRANGE("Job Contract Entry No.",SourceRefNo);
            PAGE.RUN(0,JobPlanningLine);
          end;
        DATABASE::"Assembly Header":
          begin
            AsmHeader.RESET;
            AsmHeader.SETRANGE("Document Type",SourceSubtype);
            AsmHeader.SETRANGE("No.",SourceID);
            PAGE.RUN(PAGE::"Assembly Orders",AsmHeader);
          end;
        DATABASE::"Assembly Line":
          begin
            AsmLine.RESET;
            AsmLine.SETRANGE("Document Type",SourceSubtype);
            AsmLine.SETRANGE("Document No.",SourceID);
            AsmLine.SETRANGE("Line No.",SourceRefNo);
            PAGE.RUN(PAGE::"Assembly Lines",AsmLine);
          end;
      end;
    END;

    [External]
    PROCEDURE LookupDocument@58(SourceType@1005 : Integer;SourceSubtype@1004 : Integer;SourceID@1003 : Code[20];SourceBatchName@1002 : Code[10];SourceProdOrderLine@1001 : Integer;SourceRefNo@1000 : Integer);
    VAR
      SalesHeader@1019 : Record "Sales Header";
      PurchHeader@1017 : Record "Purchase Header";
      ReqLine@1006 : Record "Requisition Line";
      ItemJnlLine@1009 : Record "Item Journal Line";
      ItemLedgEntry@1010 : Record "Item Ledger Entry";
      ProdOrder@1012 : Record "Production Order";
      TransHeader@1008 : Record "Transfer Header";
      ServiceHeader@1007 : Record "Service Header";
      Job@1013 : Record Job;
      AsmHeader@1011 : Record "Assembly Header";
    BEGIN
      case SourceType of
        DATABASE::"Sales Line":
          begin
            SalesHeader.RESET;
            SalesHeader.SETRANGE("Document Type",SourceSubtype);
            SalesHeader.SETRANGE("No.",SourceID);
            case SourceSubtype of
              0:
                PAGE.RUNMODAL(PAGE::"Sales Quote",SalesHeader);
              1:
                PAGE.RUNMODAL(PAGE::"Sales Order",SalesHeader);
              2:
                PAGE.RUNMODAL(PAGE::"Sales Invoice",SalesHeader);
              3:
                PAGE.RUNMODAL(PAGE::"Sales Credit Memo",SalesHeader);
              5:
                PAGE.RUNMODAL(PAGE::"Sales Return Order",SalesHeader);
            end;
          end;
        DATABASE::"Requisition Line":
          begin
            ReqLine.RESET;
            ReqLine.SETRANGE("Worksheet Template Name",SourceID);
            ReqLine.SETRANGE("Journal Batch Name",SourceBatchName);
            ReqLine.SETRANGE("Line No.",SourceRefNo);
            PAGE.RUNMODAL(PAGE::"Requisition Lines",ReqLine);
          end;
        DATABASE::"Planning Component":
          begin
            ReqLine.RESET;
            ReqLine.SETRANGE("Worksheet Template Name",SourceID);
            ReqLine.SETRANGE("Journal Batch Name",SourceBatchName);
            ReqLine.SETRANGE("Line No.",SourceProdOrderLine);
            PAGE.RUNMODAL(PAGE::"Requisition Lines",ReqLine);
          end;
        DATABASE::"Purchase Line":
          begin
            PurchHeader.RESET;
            PurchHeader.SETRANGE("Document Type",SourceSubtype);
            PurchHeader.SETRANGE("No.",SourceID);
            case SourceSubtype of
              0:
                PAGE.RUNMODAL(PAGE::"Purchase Quote",PurchHeader);
              1:
                PAGE.RUNMODAL(PAGE::"Purchase Order",PurchHeader);
              2:
                PAGE.RUNMODAL(PAGE::"Purchase Invoice",PurchHeader);
              3:
                PAGE.RUNMODAL(PAGE::"Purchase Credit Memo",PurchHeader);
              5:
                PAGE.RUNMODAL(PAGE::"Purchase Return Order",PurchHeader);
            end;
          end;
        DATABASE::"Item Journal Line":
          begin
            ItemJnlLine.RESET;
            ItemJnlLine.SETRANGE("Journal Template Name",SourceID);
            ItemJnlLine.SETRANGE("Journal Batch Name",SourceBatchName);
            ItemJnlLine.SETRANGE("Line No.",SourceRefNo);
            ItemJnlLine.SETRANGE("Entry Type",SourceSubtype);
            PAGE.RUNMODAL(PAGE::"Item Journal Lines",ItemJnlLine);
          end;
        DATABASE::"Item Ledger Entry":
          begin
            ItemLedgEntry.RESET;
            ItemLedgEntry.SETRANGE("Entry No.",SourceRefNo);
            PAGE.RUNMODAL(0,ItemLedgEntry);
          end;
        DATABASE::"Prod. Order Line",
        DATABASE::"Prod. Order Component":
          begin
            ProdOrder.RESET;
            ProdOrder.SETRANGE(Status,SourceSubtype);
            ProdOrder.SETRANGE("No.",SourceID);
            case SourceSubtype of
              0:
                PAGE.RUNMODAL(PAGE::"Simulated Production Order",ProdOrder);
              1:
                PAGE.RUNMODAL(PAGE::"Planned Production Order",ProdOrder);
              2:
                PAGE.RUNMODAL(PAGE::"Firm Planned Prod. Order",ProdOrder);
              3:
                PAGE.RUNMODAL(PAGE::"Released Production Order",ProdOrder);
            end;
          end;
        DATABASE::"Transfer Line":
          begin
            TransHeader.RESET;
            TransHeader.SETRANGE("No.",SourceID);
            PAGE.RUNMODAL(PAGE::"Transfer Order",TransHeader);
          end;
        DATABASE::"Service Line":
          begin
            ServiceHeader.RESET;
            ServiceHeader.SETRANGE("Document Type",SourceSubtype);
            ServiceHeader.SETRANGE("No.",SourceID);
            if SourceSubtype = 0 then
              PAGE.RUNMODAL(PAGE::"Service Quote",ServiceHeader)
            else
              PAGE.RUNMODAL(PAGE::"Service Order",ServiceHeader);
          end;
        DATABASE::"Job Planning Line":
          begin
            Job.RESET;
            Job.SETRANGE("No.",SourceID);
            PAGE.RUNMODAL(PAGE::"Job Card",Job)
          end;
        DATABASE::"Assembly Header",
        DATABASE::"Assembly Line":
          begin
            AsmHeader.RESET;
            AsmHeader.SETRANGE("Document Type",SourceSubtype);
            AsmHeader.SETRANGE("No.",SourceID);
            case SourceSubtype of
              0:
                ;
              1:
                PAGE.RUNMODAL(PAGE::"Assembly Order",AsmHeader);
              5:
                ;
            end;
          end;
      end;
    END;

    LOCAL PROCEDURE CallCalcReservedQtyOnPick@104();
    BEGIN
      if Positive and (CalcReservEntry."Location Code" <> '') then
        if Location.GET(CalcReservEntry."Location Code") and
           (Location."Bin Mandatory" or Location."Require Pick")
        then
          CalcReservedQtyOnPick(TotalAvailQty,QtyAllocInWhse);
    END;

    LOCAL PROCEDURE CalcReservedQtyOnPick@63(VAR AvailQty@1004 : Decimal;VAR AllocQty@1001 : Decimal);
    VAR
      WhseActivLine@1003 : Record "Warehouse Activity Line";
      TempWhseActivLine2@1000 : TEMPORARY Record "Warehouse Activity Line";
      WhseAvailMgt@1009 : Codeunit "Warehouse Availability Mgt.";
      PickQty@1007 : Decimal;
      QtyOnOutboundBins@1002 : Decimal;
      QtyOnInvtMovement@1005 : Decimal;
      QtyOnAssemblyBin@1006 : Decimal;
    BEGIN
      with CalcReservEntry do begin
        GetItemSetup(CalcReservEntry);
        Item.SETRANGE("Location Filter","Location Code");
        Item.SETRANGE("Variant Filter","Variant Code");
        if "Lot No." <> '' then
          Item.SETRANGE("Lot No. Filter","Lot No.");
        if "Serial No." <> '' then
          Item.SETRANGE("Serial No. Filter","Serial No.");
        Item.CALCFIELDS(
          Inventory,"Reserved Qty. on Inventory");

        WhseActivLine.SETCURRENTKEY(
          "Item No.","Bin Code","Location Code","Action Type","Variant Code",
          "Unit of Measure Code","Breakbulk No.","Activity Type","Lot No.","Serial No.");

        WhseActivLine.SETRANGE("Item No.","Item No.");
        if Location."Bin Mandatory" then
          WhseActivLine.SETFILTER("Bin Code",'<>%1','');
        WhseActivLine.SETRANGE("Location Code","Location Code");
        WhseActivLine.SETFILTER(
          "Action Type",'%1|%2',WhseActivLine."Action Type"::" ",WhseActivLine."Action Type"::Take);
        WhseActivLine.SETRANGE("Variant Code","Variant Code");
        WhseActivLine.SETRANGE("Breakbulk No.",0);
        WhseActivLine.SETFILTER(
          "Activity Type",'%1|%2',WhseActivLine."Activity Type"::Pick,WhseActivLine."Activity Type"::"Invt. Pick");
        if "Lot No." <> '' then
          WhseActivLine.SETRANGE("Lot No.","Lot No.");
        if "Serial No." <> '' then
          WhseActivLine.SETRANGE("Serial No.","Serial No.");
        WhseActivLine.CALCSUMS("Qty. Outstanding (Base)");

        if Location."Require Pick" then begin
          QtyOnOutboundBins :=
            CreatePick.CalcQtyOnOutboundBins(
              "Location Code","Item No.","Variant Code","Lot No.","Serial No.",true);

          QtyReservedOnPickShip :=
            WhseAvailMgt.CalcReservQtyOnPicksShips(
              "Location Code","Item No.","Variant Code",TempWhseActivLine2);

          QtyOnInvtMovement := CalcQtyOnInvtMovement(WhseActivLine);

          QtyOnAssemblyBin :=
            WhseAvailMgt.CalcQtyOnAssemblyBin("Location Code","Item No.","Variant Code","Lot No.","Serial No.");
        end;

        AllocQty :=
          WhseActivLine."Qty. Outstanding (Base)" + QtyOnInvtMovement + QtyOnOutboundBins + QtyOnAssemblyBin;
        PickQty := WhseActivLine."Qty. Outstanding (Base)" + QtyOnInvtMovement;

        AvailQty :=
          Item.Inventory - PickQty - QtyOnOutboundBins - QtyOnAssemblyBin -
          Item."Reserved Qty. on Inventory" + QtyReservedOnPickShip;
      end;
    END;

    LOCAL PROCEDURE SaveItemTrackingAsSurplus@1000000000(VAR ReservEntry@1000000004 : Record "Reservation Entry";NewQty@1001 : Decimal;NewQtyBase@1000000000 : Decimal) QuantityIsValidated : Boolean;
    VAR
      SurplusEntry@1000000003 : Record "Reservation Entry";
      CreateReservEntry2@1000000006 : Codeunit "Create Reserv. Entry";
      QtyToSave@1000 : Decimal;
      QtyToSaveBase@1000000007 : Decimal;
      QtyToHandleThisLine@1000000002 : Decimal;
      QtyToInvoiceThisLine@1000000001 : Decimal;
      SignFactor@1000000005 : Integer;
    BEGIN
      QtyToSave := ReservEntry.Quantity - NewQty;
      QtyToSaveBase := ReservEntry."Quantity (Base)" - NewQtyBase;

      if QtyToSaveBase = 0 then
        exit;

      if ReservEntry."Item Tracking" = ReservEntry."Item Tracking"::None then
        exit;

      if ReservEntry."Source Type" = DATABASE::"Item Ledger Entry" then
        exit;

      if QtyToSaveBase * ReservEntry."Quantity (Base)" < 0 then
        ReservEntry.FIELDERROR("Quantity (Base)");

      SignFactor := ReservEntry."Quantity (Base)" / ABS(ReservEntry."Quantity (Base)");

      if SignFactor * QtyToSaveBase > SignFactor * ReservEntry."Quantity (Base)" then
        ReservEntry.FIELDERROR("Quantity (Base)");

      QtyToHandleThisLine := ReservEntry."Qty. to Handle (Base)" - NewQtyBase;
      QtyToInvoiceThisLine := ReservEntry."Qty. to Invoice (Base)" - NewQtyBase;

      ReservEntry.VALIDATE("Quantity (Base)",NewQtyBase);

      if SignFactor * QtyToHandleThisLine < 0 then begin
        ReservEntry.VALIDATE("Qty. to Handle (Base)",ReservEntry."Qty. to Handle (Base)" + QtyToHandleThisLine);
        QtyToHandleThisLine := 0;
      end;

      if SignFactor * QtyToInvoiceThisLine < 0 then begin
        ReservEntry.VALIDATE("Qty. to Invoice (Base)",ReservEntry."Qty. to Invoice (Base)" + QtyToInvoiceThisLine);
        QtyToInvoiceThisLine := 0;
      end;

      QuantityIsValidated := true;

      SurplusEntry := ReservEntry;
      SurplusEntry."Reservation Status" := SurplusEntry."Reservation Status"::Surplus;
      if SurplusEntry.Positive then
        SurplusEntry."Shipment Date" := 0D
      else
        SurplusEntry."Expected Receipt Date" := 0D;
      CreateReservEntry2.SetQtyToHandleAndInvoice(QtyToHandleThisLine,QtyToInvoiceThisLine);
      CreateReservEntry2.CreateRemainingReservEntry(SurplusEntry,QtyToSave,QtyToSaveBase);
    END;

    [External]
    PROCEDURE CalcIsAvailTrackedQtyInBin@61(ItemNo@1000 : Code[20];BinCode@1001 : Code[20];LocationCode@1002 : Code[10];VariantCode@1015 : Code[10];SourceType@1013 : Integer;SourceSubtype@1012 : Integer;SourceID@1011 : Code[20];SourceBatchName@1010 : Code[10];SourceProdOrderLine@1009 : Integer;SourceRefNo@1008 : Integer) : Boolean;
    VAR
      ReservationEntry@1007 : Record "Reservation Entry";
      WhseEntry@1014 : Record "Warehouse Entry";
      ItemTrackingMgt@1005 : Codeunit "Item Tracking Management";
      WhseSNRequired@1004 : Boolean;
      WhseLNRequired@1003 : Boolean;
    BEGIN
      ItemTrackingMgt.CheckWhseItemTrkgSetup(ItemNo,WhseSNRequired,WhseLNRequired,false);
      if not (WhseSNRequired or WhseLNRequired) or (BinCode = '') then
        exit(true);

      ReservationEntry.SetSourceFilter(SourceType,SourceSubtype,SourceID,SourceRefNo,false);
      ReservationEntry.SetSourceFilter2(SourceBatchName,SourceProdOrderLine);
      ReservationEntry.SETRANGE(Positive,false);
      if ReservationEntry.FINDSET then
        repeat
          if ReservEntryPositiveTypeIsItemLedgerEntry(ReservationEntry."Entry No.") then begin
            WhseEntry.SETCURRENTKEY("Item No.","Location Code","Variant Code","Bin Type Code");
            WhseEntry.SETRANGE("Item No.",ItemNo);
            WhseEntry.SETRANGE("Location Code",LocationCode);
            WhseEntry.SETRANGE("Bin Code",BinCode);
            WhseEntry.SETRANGE("Variant Code",VariantCode);
            if ReservationEntry."Lot No." <> '' then
              WhseEntry.SETRANGE("Lot No.",ReservationEntry."Lot No.");
            if ReservationEntry."Serial No." <> '' then
              WhseEntry.SETRANGE("Serial No.",ReservationEntry."Serial No.");
            WhseEntry.CALCSUMS("Qty. (Base)");
            if WhseEntry."Qty. (Base)" < ABS(ReservationEntry."Quantity (Base)") then
              exit(false);
          end;
        until ReservationEntry.NEXT = 0;

      exit(true);
    END;

    LOCAL PROCEDURE CalcQtyOnInvtMovement@90(VAR WarehouseActivityLine@1000 : Record "Warehouse Activity Line") : Decimal;
    VAR
      xWarehouseActivityLine@1001 : Record "Warehouse Activity Line";
      OutstandingQty@1002 : Decimal;
    BEGIN
      xWarehouseActivityLine.COPY(WarehouseActivityLine);

      WarehouseActivityLine.SETRANGE("Activity Type",WarehouseActivityLine."Activity Type"::"Invt. Movement");
      if WarehouseActivityLine.FIND('-') then
        repeat
          if WarehouseActivityLine."Source Type" <> 0 then
            OutstandingQty += WarehouseActivityLine."Qty. Outstanding (Base)"
        until WarehouseActivityLine.NEXT = 0;

      WarehouseActivityLine.COPY(xWarehouseActivityLine);
      exit(OutstandingQty);
    END;

    LOCAL PROCEDURE ProdJnlLineEntry@53(ReservationEntry@1000 : Record "Reservation Entry") : Boolean;
    BEGIN
      with ReservationEntry do
        exit(("Source Type" = DATABASE::"Item Journal Line") and ("Source Subtype" = 6));
    END;

    LOCAL PROCEDURE CalcDownToQtySyncingToAssembly@97(ReservEntry@1001 : Record "Reservation Entry") : Decimal;
    VAR
      SynchronizingSalesLine@1000 : Record "Sales Line";
    BEGIN
      if ReservEntry."Source Type" = DATABASE::"Sales Line" then begin
        SynchronizingSalesLine.GET(ReservEntry."Source Subtype",ReservEntry."Source ID",ReservEntry."Source Ref. No.");
        if (Item."Order Tracking Policy" <> Item."Order Tracking Policy"::None) and
           (Item."Assembly Policy" = Item."Assembly Policy"::"Assemble-to-Order") and
           (Item."Replenishment System" = Item."Replenishment System"::Assembly) and
           (SynchronizingSalesLine."Quantity (Base)" = 0)
        then
          exit(ReservEntry."Quantity (Base)" * CreateReservEntry.SignFactor(ReservEntry));
      end;
    END;

    LOCAL PROCEDURE CalcCurrLineReservQtyOnPicksShips@118(SourceType@1002 : Integer;SourceSubtype@1003 : Option;SourceID@1005 : Code[20];SourceRefNo@1006 : Integer;SourceProdOrderLine@1007 : Integer) : Decimal;
    VAR
      ReservEntry@1000 : Record "Reservation Entry";
      WhseActivLine@1001 : Record "Warehouse Activity Line";
      WhseAvailMgt@1004 : Codeunit "Warehouse Availability Mgt.";
    BEGIN
      with ReservEntry do begin
        SetSourceFilter(SourceType,SourceSubtype,SourceID,SourceRefNo,false);
        SETRANGE("Source Prod. Order Line",SourceProdOrderLine);
        SETRANGE("Reservation Status","Reservation Status"::Reservation);
        CALCSUMS("Quantity (Base)");
        exit(
          WhseAvailMgt.CalcLineReservQtyOnPicksShips(
            SourceType,SourceSubtype,
            SourceID,SourceRefNo,
            SourceProdOrderLine,"Quantity (Base)",
            WhseActivLine));
      end;
    END;

    LOCAL PROCEDURE CheckQuantityIsCompletelyReleased@113(QtyToRelease@1000 : Decimal;DeleteAll@1001 : Boolean;CurrentSerialNo@1002 : Code[20];CurrentLotNo@1003 : Code[20];ReservEntry@1004 : Record "Reservation Entry");
    BEGIN
      if QtyToRelease = 0 then
        exit;

      if ItemTrackingHandling = ItemTrackingHandling::None then begin
        if DeleteAll then
          ERROR(Text010,ReservEntry."Item No.",ReservEntry.TextCaption);
        if not ProdJnlLineEntry(ReservEntry) then
          ERROR(Text008,ReservEntry."Item No.",ReservEntry.TextCaption);
      end;

      if ItemTrackingHandling = ItemTrackingHandling::Match then
        ERROR(Text009,CurrentSerialNo,CurrentLotNo,ABS(QtyToRelease));
    END;

    LOCAL PROCEDURE ReservEntryPositiveTypeIsItemLedgerEntry@120(ReservationEntryNo@1000 : Integer) : Boolean;
    VAR
      ReservationEntryPositive@1001 : Record "Reservation Entry";
    BEGIN
      if ReservationEntryPositive.GET(ReservationEntryNo,true) then
        exit(ReservationEntryPositive."Source Type" = DATABASE::"Item Ledger Entry");

      exit(true);
    END;

    [External]
    PROCEDURE DeleteDocumentReservation@68(TableID@1002 : Integer;DocType@1003 : Option;DocNo@1004 : Code[20];HideValidationDialog@1005 : Boolean);
    VAR
      ReservEntry@1001 : Record "Reservation Entry";
      ReservEntry2@1000 : Record "Reservation Entry";
      Confirmed@1006 : Boolean;
    BEGIN
      with ReservEntry do begin
        RESET;
        SETCURRENTKEY(
          "Source ID","Source Ref. No.","Source Type","Source Subtype",
          "Source Batch Name","Source Prod. Order Line","Reservation Status");
        if TableID <> DATABASE::"Prod. Order Line" then begin
          SETRANGE("Source Type",TableID);
          SETRANGE("Source Prod. Order Line",0);
        end else
          SETFILTER("Source Type",'%1|%2',DATABASE::"Prod. Order Line",DATABASE::"Prod. Order Component");
        if TableID <> DATABASE::"Transfer Line" then
          SETRANGE("Source Subtype",DocType)
        else
          SETRANGE("Source Subtype");
        SETRANGE("Source ID",DocNo);
        SETRANGE("Source Batch Name",'');
        SETFILTER("Item Tracking",'> %1',"Item Tracking"::None);
        if ISEMPTY then
          exit;

        if HideValidationDialog or not GUIALLOWED then
          Confirmed := true
        else
          Confirmed := CONFIRM(DeleteItemReservationQst,false,LOWERCASE(FORMAT(DocType)),DocNo);

        if not Confirmed then
          ERROR('');

        if FINDSET then
          repeat
            ReservEntry2 := ReservEntry;
            ReservEntry2.ClearItemTrackingFields;
            ReservEntry2.MODIFY;
          until NEXT = 0;
      end;
    END;

    BEGIN
    END.
  }
}

