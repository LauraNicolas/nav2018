OBJECT Page 9107 Resource Statistics FactBox
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Resource Statistics - Year To Date;
    SourceTable=Resource;
    PageType=CardPart;
    OnFindRecord=BEGIN
                   ResCapacity := 0;
                   UnusedCapacity := 0;
                   UnitPrice := 0;
                   InvoicedPct := 0;
                   ResUsageCost := 0;
                   Profit := 0;
                   ResProfitPct := 0;

                   exit(FIND(Which));
                 END;

    OnAfterGetRecord=BEGIN
                       if CurrentDate <> WORKDATE then begin
                         CurrentDate := WORKDATE;
                         DateFilterCalc.CreateFiscalYearFilter(ResDateFilter,ResDateName,CurrentDate,0);
                       end;

                       CLEAR(TotalUsageUnits);

                       SETFILTER("Date Filter",ResDateFilter);
                       SETRANGE("Chargeable Filter");
                       CALCFIELDS(Capacity,"Usage (Cost)","Sales (Price)");

                       ResCapacity := Capacity;
                       ResUsageCost := "Usage (Cost)";
                       UnitPrice := "Sales (Price)";

                       for j := 1 to 2 do begin
                         if j = 1 then
                           Chargeable := false
                         else
                           Chargeable := true;
                         SETRANGE("Chargeable Filter",Chargeable);
                         CALCFIELDS("Usage (Qty.)","Usage (Price)");
                         ResUsagePrice := "Usage (Price)";
                         TotalUsageUnits := TotalUsageUnits + "Usage (Qty.)";
                       end;

                       UnusedCapacity := ResCapacity - TotalUsageUnits;
                       InvoicedPct := CalcPercentage(UnitPrice,ResUsagePrice);
                       Profit := UnitPrice - ResUsageCost;
                       ResProfitPct := CalcPercentage(Profit,UnitPrice);

                       SETRANGE("Date Filter");
                       SETRANGE("Chargeable Filter");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 15  ;1   ;Field     ;
                Name=No.;
                CaptionML=ENU=Resource No.;
                ToolTipML=ENU=Specifies a number for the resource.;
                ApplicationArea=#Jobs;
                SourceExpr="No.";
                OnDrillDown=BEGIN
                              ShowDetails;
                            END;

                ImplicitType=Code20 }

    { 2   ;1   ;Field     ;
                Name=ResCapacity;
                CaptionML=ENU=Capacity;
                ToolTipML=ENU=Specifies the scheduled capacity of the resource. The amount is the sum of values in the Quantity field on job planning lines for the resource.;
                ApplicationArea=#Jobs;
                DecimalPlaces=0:5;
                SourceExpr=ResCapacity;
                ImplicitType=Decimal }

    { 1   ;1   ;Field     ;
                Name=UnusedCapacity;
                CaptionML=ENU=Unused Capacity;
                ToolTipML=ENU="Specifies the difference between the capacity and the capacity used. It is calculated as follows: Unused Capacity = Capacity - Charg. Usage + Not Charg. Usage.";
                ApplicationArea=#Jobs;
                DecimalPlaces=0:5;
                SourceExpr=UnusedCapacity;
                ImplicitType=Decimal }

    { 7   ;1   ;Field     ;
                Name=UnitPrice;
                CaptionML=ENU=Invoiced;
                ToolTipML=ENU=Specifies the unit prices of postings of the type sale. Sales can be posted in the resource group journal and the sales lines.;
                ApplicationArea=#Jobs;
                SourceExpr=UnitPrice;
                AutoFormatType=1;
                ImplicitType=Decimal }

    { 8   ;1   ;Field     ;
                Name=InvoicedPct;
                CaptionML=ENU=Invoiced %;
                ToolTipML=ENU="Specifies the percentage of invoiced amounts in unit prices. It is calculated as follows: Invoiced % = (Invoiced (LCY) / Usage(Unit Price)) x 100.";
                ApplicationArea=#Jobs;
                DecimalPlaces=1:1;
                SourceExpr=InvoicedPct;
                ImplicitType=Decimal }

    { 11  ;1   ;Field     ;
                Name=ResUsageCost;
                CaptionML=ENU=Usage (Cost);
                ToolTipML=ENU=Specifies values for job usage based on cost. Specifies, based on cost, how much the resources has been used.;
                ApplicationArea=#Jobs;
                SourceExpr=ResUsageCost;
                AutoFormatType=1;
                ImplicitType=Decimal }

    { 10  ;1   ;Field     ;
                Name=Profit;
                CaptionML=ENU=Profit;
                ToolTipML=ENU=Specifies the profit amounts.;
                ApplicationArea=#Jobs;
                SourceExpr=Profit;
                AutoFormatType=1;
                ImplicitType=Decimal }

    { 9   ;1   ;Field     ;
                Name=ResProfitPct;
                CaptionML=ENU=Profit %;
                ToolTipML=ENU=Specifies the profit percentages.;
                ApplicationArea=#Jobs;
                DecimalPlaces=1:1;
                SourceExpr=ResProfitPct;
                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      DateFilterCalc@1008 : Codeunit "DateFilter-Calc";
      CurrentDate@1007 : Date;
      ResCapacity@1001 : Decimal;
      UnusedCapacity@1000 : Decimal;
      UnitPrice@1003 : Decimal;
      InvoicedPct@1002 : Decimal;
      ResUsageCost@1006 : Decimal;
      Profit@1005 : Decimal;
      ResProfitPct@1004 : Decimal;
      ResDateFilter@1013 : Text[30];
      ResDateName@1012 : Text[30];
      Chargeable@1009 : Boolean;
      TotalUsageUnits@1021 : Decimal;
      ResUsagePrice@1018 : Decimal;
      j@1010 : Integer;

    LOCAL PROCEDURE ShowDetails@1102601000();
    BEGIN
      PAGE.RUN(PAGE::"Resource Card",Rec);
    END;

    LOCAL PROCEDURE CalcPercentage@1(PartAmount@1000 : Decimal;Base@1001 : Decimal) : Decimal;
    BEGIN
      if Base <> 0 then
        exit(100 * PartAmount / Base);

      exit(0);
    END;

    BEGIN
    END.
  }
}

