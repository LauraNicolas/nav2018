OBJECT Codeunit 740 VAT Report Mediator
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      VATReportReleaseReopen@1000 : Codeunit "VAT Report Release/Reopen";
      Text001@1002 : TextConst 'ENU=This action will also mark the report as released. Are you sure you want to continue?';

    [External]
    PROCEDURE GetLines@5(VATReportHeader@1001 : Record "VAT Report Header");
    VAR
      VATReportsConfiguration@1000 : Record "VAT Reports Configuration";
    BEGIN
      VATReportHeader.TESTFIELD(Status,VATReportHeader.Status::Open);
      if (VATReportHeader."VAT Report Type" = VATReportHeader."VAT Report Type"::Corrective) or
         (VATReportHeader."VAT Report Type" = VATReportHeader."VAT Report Type"::Supplementary)
      then
        VATReportHeader.TESTFIELD("Original Report No.");

      VATReportsConfiguration.SETRANGE("VAT Report Type",VATReportHeader."VAT Report Config. Code");
      VATReportsConfiguration.FINDFIRST;

      VATReportHeader.FILTERGROUP(2);
      VATReportHeader.SETRANGE("VAT Report Config. Code",VATReportHeader."VAT Report Config. Code");
      VATReportHeader.SETRANGE("No.",VATReportHeader."No.");
      CODEUNIT.RUN(VATReportsConfiguration."Suggest Lines Codeunit ID",VATReportHeader);
    END;

    [Internal]
    PROCEDURE Export@6(VATReportHeader@1001 : Record "VAT Report Header");
    VAR
      VATReportsConfiguration@1002 : Record "VAT Reports Configuration";
    BEGIN
      case VATReportHeader."VAT Report Config. Code" of
        VATReportHeader."VAT Report Config. Code"::"VAT Return":
          VATReportsConfiguration.SETRANGE("VAT Report Type",VATReportsConfiguration."VAT Report Type"::"VAT Return");
        VATReportHeader."VAT Report Config. Code"::"EC Sales List":
          VATReportsConfiguration.SETRANGE("VAT Report Type",VATReportsConfiguration."VAT Report Type"::"EC Sales List");
      end;
      VATReportsConfiguration.FINDFIRST;

      if VATReportsConfiguration."Content Codeunit ID" <> 0 then
        CODEUNIT.RUN(VATReportsConfiguration."Content Codeunit ID",VATReportHeader);
      if VATReportsConfiguration."Submission Codeunit ID" <> 0 then
        CODEUNIT.RUN(VATReportsConfiguration."Submission Codeunit ID",VATReportHeader);
    END;

    [External]
    PROCEDURE Release@8(VATReportHeader@1000 : Record "VAT Report Header");
    BEGIN
      VATReportReleaseReopen.Release(VATReportHeader);
    END;

    [External]
    PROCEDURE Reopen@7(VATReportHeader@1000 : Record "VAT Report Header");
    BEGIN
      VATReportReleaseReopen.Reopen(VATReportHeader);
    END;

    [External]
    PROCEDURE Print@1(VATReportHeader@1000 : Record "VAT Report Header");
    BEGIN
      case VATReportHeader.Status of
        VATReportHeader.Status::Open:
          PrintOpen(VATReportHeader);
        VATReportHeader.Status::Released:
          PrintReleased(VATReportHeader);
        VATReportHeader.Status::Submitted:
          PrintReleased(VATReportHeader);
      end;
    END;

    LOCAL PROCEDURE PrintOpen@2(VAR VATReportHeader@1000 : Record "VAT Report Header");
    VAR
      VATReportReleaseReopen@1002 : Codeunit "VAT Report Release/Reopen";
    BEGIN
      VATReportHeader.TESTFIELD(Status,VATReportHeader.Status::Open);
      if CONFIRM(Text001,true) then begin
        VATReportReleaseReopen.Release(VATReportHeader);
        PrintReleased(VATReportHeader);
      end
    END;

    LOCAL PROCEDURE PrintReleased@3(VAR VATReportHeader@1000 : Record "VAT Report Header");
    BEGIN
      VATReportHeader.SETRANGE("No.",VATReportHeader."No.");
      REPORT.RUNMODAL(REPORT::"VAT Report Print",false,false,VATReportHeader);
    END;

    [External]
    PROCEDURE Submit@4(VATReportHeader@1000 : Record "VAT Report Header");
    BEGIN
      VATReportReleaseReopen.Submit(VATReportHeader);
    END;

    BEGIN
    END.
  }
}

