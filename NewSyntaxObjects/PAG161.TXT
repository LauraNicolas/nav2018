OBJECT Page 161 Purchase Statistics
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Purchase Statistics;
    InsertAllowed=false;
    DeleteAllowed=false;
    LinksAllowed=false;
    SourceTable="Purchase Header";
    PageType=ListPlus;
    OnOpenPage=BEGIN
                 PurchSetup.GET;
                 AllowInvDisc :=
                   not (PurchSetup."Calc. Inv. Discount" and VendInvDiscRecExists("Invoice Disc. Code"));
                 AllowVATDifference :=
                   PurchSetup."Allow VAT Difference" and
                   not ("Document Type" in ["Document Type"::Quote,"Document Type"::"Blanket Order"]);
                 CurrPage.EDITABLE := AllowVATDifference or AllowInvDisc;
                 SetVATSpecification;
               END;

    OnAfterGetRecord=VAR
                       PurchLine@1000 : Record "Purchase Line";
                       TempPurchLine@1001 : TEMPORARY Record "Purchase Line";
                     BEGIN
                       CurrPage.CAPTION(STRSUBSTNO(Text000,"Document Type"));
                       if PrevNo = "No." then begin
                         GetVATSpecification;
                         exit;
                       end;
                       PrevNo := "No.";
                       FILTERGROUP(2);
                       SETRANGE("No.",PrevNo);
                       FILTERGROUP(0);
                       CLEAR(PurchLine);
                       CLEAR(TotalPurchLine);
                       CLEAR(TotalPurchLineLCY);
                       CLEAR(PurchPost);

                       PurchPost.GetPurchLines(Rec,TempPurchLine,0);
                       CLEAR(PurchPost);
                       PurchPost.SumPurchLinesTemp(
                         Rec,TempPurchLine,0,TotalPurchLine,TotalPurchLineLCY,VATAmount,VATAmountText);

                       if "Prices Including VAT" then begin
                         TotalAmount2 := TotalPurchLine.Amount;
                         TotalAmount1 := TotalAmount2 + VATAmount;
                         TotalPurchLine."Line Amount" := TotalAmount1 + TotalPurchLine."Inv. Discount Amount";
                       end else begin
                         TotalAmount1 := TotalPurchLine.Amount;
                         TotalAmount2 := TotalPurchLine."Amount Including VAT";
                       end;

                       if Vend.GET("Pay-to Vendor No.") then
                         Vend.CALCFIELDS("Balance (LCY)")
                       else
                         CLEAR(Vend);

                       PurchLine.CalcVATAmountLines(0,Rec,TempPurchLine,TempVATAmountLine);
                       TempVATAmountLine.MODIFYALL(Modified,false);
                       SetVATSpecification;
                     END;

    OnQueryClosePage=BEGIN
                       GetVATSpecification;
                       if TempVATAmountLine.GetAnyLineModified then
                         UpdateVATOnPurchLines;
                       exit(true);
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General }

    { 97  ;2   ;Field     ;
                Name=Amount;
                CaptionML=ENU=Amount;
                ToolTipML=ENU=Specifies the net amount of all the lines in the purchase document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TotalPurchLine."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,false);
                Editable=FALSE }

    { 93  ;2   ;Field     ;
                Name=InvDiscountAmount;
                CaptionML=ENU=Inv. Discount Amount;
                ToolTipML=ENU=Specifies the invoice discount amount for the purchase document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TotalPurchLine."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                OnValidate=BEGIN
                             UpdateInvDiscAmount;
                           END;
                            }

    { 80  ;2   ;Field     ;
                Name=TotalAmount1;
                CaptionML=ENU=Total;
                ToolTipML=ENU=Specifies the total amount less any invoice discount amount and excluding VAT for the purchase document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TotalAmount1;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,false);
                OnValidate=BEGIN
                             UpdateTotalAmount;
                           END;

                ImplicitType=Decimal }

    { 75  ;2   ;Field     ;
                Name=VATAmount;
                CaptionML=ENU=VAT Amount;
                ToolTipML=ENU=Specifies the total VAT amount that has been calculated for all the lines in the purchase document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=VATAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT(VATAmountText);
                Editable=FALSE;
                ImplicitType=Decimal }

    { 76  ;2   ;Field     ;
                Name=TotalAmount2;
                CaptionML=ENU=Total Incl. VAT;
                ToolTipML=ENU=Specifies the total amount including VAT that will be posted to the vendor's account for all the lines in the purchase document. This is the amount that you owe the vendor based on this purchase document. If the document is a credit memo, it is the amount that the vendor owes you.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TotalAmount2;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,true);
                Editable=FALSE;
                ImplicitType=Decimal }

    { 3   ;2   ;Field     ;
                Name=Purchase (LCY);
                CaptionML=ENU=Purchase (LCY);
                ToolTipML=ENU=Specifies your total purchases. It is calculated from amounts excluding VAT on all completed and open purchase invoices and credit memos.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TotalPurchLineLCY.Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 95  ;2   ;Field     ;
                Name=Quantity;
                CaptionML=ENU=Quantity;
                ToolTipML=ENU=Specifies the total quantity of G/L account entries, items, and/or resources in the purchase document.;
                ApplicationArea=#Basic,#Suite;
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine.Quantity;
                Editable=FALSE }

    { 73  ;2   ;Field     ;
                Name=Parcels;
                CaptionML=ENU=Parcels;
                ToolTipML=ENU=Specifies the total number of parcels in the purchase document.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine."Units per Parcel";
                Editable=FALSE }

    { 91  ;2   ;Field     ;
                Name=Net Weight;
                CaptionML=ENU=Net Weight;
                ToolTipML=ENU=Specifies the total net weight of the items in the purchase document.;
                ApplicationArea=#Basic,#Suite;
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine."Net Weight";
                Editable=FALSE }

    { 82  ;2   ;Field     ;
                Name=Gross Weight;
                CaptionML=ENU=Gross Weight;
                ToolTipML=ENU=Specifies the total gross weight of the items in the purchase document.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine."Gross Weight";
                Editable=FALSE }

    { 71  ;2   ;Field     ;
                Name=Volume;
                CaptionML=ENU=Volume;
                ToolTipML=ENU=Specifies the total volume of the items in the purchase document.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine."Unit Volume";
                Editable=FALSE }

    { 5   ;1   ;Part      ;
                Name=SubForm;
                ApplicationArea=#Basic,#Suite;
                PagePartID="VAT Specification Subform" }

    { 1901061301;1;Group  ;
                Name=Vendor;
                CaptionML=ENU=Vendor }

    { 67  ;2   ;Field     ;
                Name=Balance (LCY);
                CaptionML=ENU=Balance (LCY);
                ToolTipML=ENU=Specifies the balance on the vendor's account.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Vend."Balance (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Purchase %1 Statistics';
      Text001@1001 : TextConst 'ENU=Amount';
      Text002@1002 : TextConst 'ENU=Total';
      Text003@1003 : TextConst 'ENU=%1 must not be 0.';
      Text004@1004 : TextConst 'ENU=%1 must not be greater than %2.';
      Text005@1005 : TextConst 'ENU=You cannot change the invoice discount because a vendor invoice discount with the code %1 exists.';
      TotalPurchLine@1006 : Record "Purchase Line";
      TotalPurchLineLCY@1007 : Record "Purchase Line";
      Vend@1008 : Record Vendor;
      TempVATAmountLine@1009 : TEMPORARY Record "VAT Amount Line";
      PurchSetup@1010 : Record "Purchases & Payables Setup";
      PurchPost@1011 : Codeunit "Purch.-Post";
      TotalAmount1@1012 : Decimal;
      TotalAmount2@1013 : Decimal;
      VATAmount@1014 : Decimal;
      VATAmountText@1015 : Text[30];
      PrevNo@1016 : Code[20];
      AllowInvDisc@1017 : Boolean;
      AllowVATDifference@1018 : Boolean;

    LOCAL PROCEDURE UpdateHeaderInfo@5();
    VAR
      CurrExchRate@1000 : Record "Currency Exchange Rate";
      UseDate@1001 : Date;
    BEGIN
      TotalPurchLine."Inv. Discount Amount" := TempVATAmountLine.GetTotalInvDiscAmount;
      TotalAmount1 :=
        TotalPurchLine."Line Amount" - TotalPurchLine."Inv. Discount Amount";
      VATAmount := TempVATAmountLine.GetTotalVATAmount;
      if "Prices Including VAT" then begin
        TotalAmount1 := TempVATAmountLine.GetTotalAmountInclVAT;
        TotalAmount2 := TotalAmount1 - VATAmount;
        TotalPurchLine."Line Amount" := TotalAmount1 + TotalPurchLine."Inv. Discount Amount";
      end else
        TotalAmount2 := TotalAmount1 + VATAmount;

      if "Prices Including VAT" then
        TotalPurchLineLCY.Amount := TotalAmount2
      else
        TotalPurchLineLCY.Amount := TotalAmount1;
      if "Currency Code" <> '' then begin
        if ("Document Type" in ["Document Type"::"Blanket Order","Document Type"::Quote]) and
           ("Posting Date" = 0D)
        then
          UseDate := WORKDATE
        else
          UseDate := "Posting Date";

        TotalPurchLineLCY.Amount :=
          CurrExchRate.ExchangeAmtFCYToLCY(
            UseDate,"Currency Code",TotalPurchLineLCY.Amount,"Currency Factor");
      end;
    END;

    LOCAL PROCEDURE GetVATSpecification@21();
    BEGIN
      CurrPage.SubForm.PAGE.GetTempVATAmountLine(TempVATAmountLine);
      if TempVATAmountLine.GetAnyLineModified then
        UpdateHeaderInfo;
    END;

    LOCAL PROCEDURE SetVATSpecification@11();
    BEGIN
      CurrPage.SubForm.PAGE.SetTempVATAmountLine(TempVATAmountLine);
      CurrPage.SubForm.PAGE.InitGlobals(
        "Currency Code",AllowVATDifference,AllowVATDifference,
        "Prices Including VAT",AllowInvDisc,"VAT Base Discount %");
    END;

    LOCAL PROCEDURE UpdateTotalAmount@16();
    VAR
      SaveTotalAmount@1000 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      if "Prices Including VAT" then begin
        SaveTotalAmount := TotalAmount1;
        UpdateInvDiscAmount;
        TotalAmount1 := SaveTotalAmount;
      end;

      with TotalPurchLine do
        "Inv. Discount Amount" := "Line Amount" - TotalAmount1;
      UpdateInvDiscAmount;
    END;

    LOCAL PROCEDURE UpdateInvDiscAmount@3();
    VAR
      InvDiscBaseAmount@1000 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      InvDiscBaseAmount := TempVATAmountLine.GetTotalInvDiscBaseAmount(false,"Currency Code");
      if InvDiscBaseAmount = 0 then
        ERROR(Text003,TempVATAmountLine.FIELDCAPTION("Inv. Disc. Base Amount"));

      if TotalPurchLine."Inv. Discount Amount" / InvDiscBaseAmount > 1 then
        ERROR(
          Text004,
          TotalPurchLine.FIELDCAPTION("Inv. Discount Amount"),
          TempVATAmountLine.FIELDCAPTION("Inv. Disc. Base Amount"));

      TempVATAmountLine.SetInvoiceDiscountAmount(
        TotalPurchLine."Inv. Discount Amount","Currency Code","Prices Including VAT","VAT Base Discount %");
      CurrPage.SubForm.PAGE.SetTempVATAmountLine(TempVATAmountLine);
      UpdateHeaderInfo;

      "Invoice Discount Calculation" := "Invoice Discount Calculation"::Amount;
      "Invoice Discount Value" := TotalPurchLine."Inv. Discount Amount";
      MODIFY;
      UpdateVATOnPurchLines;
    END;

    LOCAL PROCEDURE GetCaptionClass@2(FieldCaption@1000 : Text[100];ReverseCaption@1001 : Boolean) : Text[80];
    BEGIN
      if "Prices Including VAT" xor ReverseCaption then
        exit('2,1,' + FieldCaption);

      exit('2,0,' + FieldCaption);
    END;

    LOCAL PROCEDURE UpdateVATOnPurchLines@1();
    VAR
      PurchLine@1000 : Record "Purchase Line";
    BEGIN
      GetVATSpecification;
      if TempVATAmountLine.GetAnyLineModified then begin
        PurchLine.UpdateVATOnLines(0,Rec,PurchLine,TempVATAmountLine);
        PurchLine.UpdateVATOnLines(1,Rec,PurchLine,TempVATAmountLine);
      end;
      PrevNo := '';
    END;

    LOCAL PROCEDURE VendInvDiscRecExists@4(InvDiscCode@1000 : Code[20]) : Boolean;
    VAR
      VendInvDisc@1001 : Record "Vendor Invoice Disc.";
    BEGIN
      VendInvDisc.SETRANGE(Code,InvDiscCode);
      exit(VendInvDisc.FINDFIRST);
    END;

    LOCAL PROCEDURE CheckAllowInvDisc@8();
    BEGIN
      if not AllowInvDisc then
        ERROR(Text005,"Invoice Disc. Code");
    END;

    BEGIN
    END.
  }
}

