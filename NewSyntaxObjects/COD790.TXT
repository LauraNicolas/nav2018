OBJECT Codeunit 790 IC Inbox Outbox Subscribers
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="IC Inbox Transaction";
    OnRun=BEGIN
            SETRECFILTER;
            "Line Action" := "Line Action"::Accept;
            MODIFY;
            REPORT.RUN(REPORT::"Complete IC Inbox Action",false,false,Rec);
            RESET;
          END;

  }
  CODE
  {

    [EventSubscriber(Report,513,OnICInboxTransactionCreated)]
    PROCEDURE AcceptOnAfterInsertICInboxTransaction@3(VAR Sender@1000 : Report "Move IC Trans. to Partner Comp";VAR ICInboxTransaction@1001 : Record "IC Inbox Transaction";PartnerCompanyName@1006 : Text);
    VAR
      CompanyInformation@1002 : Record "Company Information";
      ICPartner@1005 : Record "IC Partner";
    BEGIN
      CompanyInformation.GET;
      ICPartner.CHANGECOMPANY(PartnerCompanyName);

      if not ICPartner.GET(CompanyInformation."IC Partner Code") then
        exit;

      if ICPartner."Auto. Accept Transactions" then
        TASKSCHEDULER.CREATETASK(CODEUNIT::"IC Inbox Outbox Subscribers",0,
          true,PartnerCompanyName,0DT,ICInboxTransaction.RECORDID);
    END;

    [EventSubscriber(Codeunit,427,ICOutboxTransactionCreated)]
    PROCEDURE AcceptOnAfterInsertICOutboxTransaction@6(ICOutboxTransaction@1003 : Record "IC Outbox Transaction");
    VAR
      CompanyInformation@1001 : Record "Company Information";
      ICOutboxExport@1002 : Codeunit "IC Outbox Export";
    BEGIN
      CompanyInformation.GET;
      if CompanyInformation."Auto. Send Transactions" then begin
        ICOutboxTransaction."Line Action" := ICOutboxTransaction."Line Action"::"Send to IC Partner";
        ICOutboxTransaction.MODIFY;
        ICOutboxExport.RunOutboxTransactions(ICOutboxTransaction);
      end;
    END;

    BEGIN
    END.
  }
}

