OBJECT Page 5471 Customer Entity
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=customers];
    SourceTable=Customer;
    DelayedInsert=true;
    PageType=API;
    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     Customer@1002 : Record Customer;
                     RecRef@1005 : RecordRef;
                   BEGIN
                     Customer.SETRANGE("No.","No.");
                     if not Customer.ISEMPTY then
                       INSERT;

                     INSERT(true);

                     GraphMgtCustomer.ProcessComplexTypes(Rec,PaymentTermsJSON,ShipmentMethodJSON,PaymentMethodJSON,PostalAddressJSON);

                     RecRef.GETTABLE(Rec);
                     GraphMgtGeneralTools.ProcessNewRecordFromAPI(RecRef,TempFieldSet,CURRENTDATETIME);
                     RecRef.SETTABLE(Rec);

                     MODIFY(true);
                     SetCalculatedFields;
                     exit(false);
                   END;

    OnModifyRecord=VAR
                     Customer@1001 : Record Customer;
                     GraphMgtGeneralTools@1000 : Codeunit "Graph Mgt - General Tools";
                     GraphMgtCustomer@1002 : Codeunit "Graph Mgt - Customer";
                   BEGIN
                     if xRec.Id <> Id then
                       GraphMgtGeneralTools.ErrorIdImmutable;

                     Customer.SETRANGE(Id,Id);
                     Customer.FINDFIRST;

                     GraphMgtCustomer.ProcessComplexTypes(Rec,PaymentTermsJSON,ShipmentMethodJSON,PaymentMethodJSON,PostalAddressJSON);

                     if "No." = Customer."No." then
                       MODIFY(true)
                     else begin
                       Customer.TRANSFERFIELDS(Rec,false);
                       Customer.RENAME("No.");
                       TRANSFERFIELDS(Customer);
                     end;

                     SetCalculatedFields;
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 24  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id];
                ApplicationArea=#All;
                SourceExpr=Id;
                ImplicitType=GUID }

    { 3   ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number];
                ApplicationArea=#All;
                SourceExpr="No.";
                ImplicitType=Code20 }

    { 4   ;2   ;Field     ;
                Name=displayName;
                CaptionML=[@@@={Locked};
                           ENU=DisplayName];
                ApplicationArea=#All;
                SourceExpr=Name;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Name));
                           END;

                ImplicitType=Text50 }

    { 22  ;2   ;Field     ;
                Name=type;
                CaptionML=[@@@={Locked};
                           ENU=type];
                ApplicationArea=#All;
                SourceExpr="Contact Type";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Contact Type"));
                           END;

                ImplicitType=Option }

    { 14  ;2   ;Field     ;
                Name=address;
                CaptionML=[@@@={Locked};
                           ENU=address];
                ToolTipML=ENU=Specifies the address for the customer.;
                ApplicationArea=#All;
                SourceExpr=PostalAddressJSON;
                ODataEDMType=POSTALADDRESS;
                ImplicitType=Text }

    { 11  ;2   ;Field     ;
                Name=phoneNumber;
                CaptionML=[@@@={Locked};
                           ENU=PhoneNumber];
                ApplicationArea=#All;
                SourceExpr="Phone No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Phone No."));
                           END;

                ImplicitType=Text30 }

    { 21  ;2   ;Field     ;
                Name=email;
                CaptionML=[@@@={Locked};
                           ENU=Email];
                ApplicationArea=#All;
                SourceExpr="E-Mail";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("E-Mail"));
                           END;

                ImplicitType=Text80 }

    { 20  ;2   ;Field     ;
                Name=website;
                CaptionML=[@@@={Locked};
                           ENU=Website];
                ApplicationArea=#All;
                SourceExpr="Home Page";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Home Page"));
                           END;

                ImplicitType=Text80 }

    { 26  ;2   ;Field     ;
                Name=taxLiable;
                CaptionML=[@@@={Locked};
                           ENU=TaxLiable];
                ApplicationArea=#All;
                SourceExpr="Tax Liable";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Tax Liable"));
                           END;

                ImplicitType=Boolean }

    { 9   ;2   ;Field     ;
                Name=taxAreaId;
                CaptionML=[@@@={Locked};
                           ENU=taxAreaId];
                ApplicationArea=#All;
                SourceExpr="Tax Area ID";
                OnValidate=VAR
                             GeneralLedgerSetup@1000 : Record "General Ledger Setup";
                           BEGIN
                             RegisterFieldSet(FIELDNO("Tax Area ID"));

                             if not GeneralLedgerSetup.UseVat then
                               RegisterFieldSet(FIELDNO("Tax Area Code"))
                             else
                               RegisterFieldSet(FIELDNO("VAT Bus. Posting Group"));
                           END;

                ImplicitType=GUID }

    { 10  ;2   ;Field     ;
                Name=taxAreaDisplayName;
                CaptionML=[@@@={Locked};
                           ENU=taxAreaDisplayName];
                ToolTipML=ENU=Specifies the display name of the tax area.;
                ApplicationArea=#All;
                SourceExpr=TaxAreaDisplayName;
                Editable=FALSE;
                ImplicitType=Text }

    { 19  ;2   ;Field     ;
                Name=taxRegistrationNumber;
                CaptionML=[@@@={Locked};
                           ENU=taxRegistrationNumber];
                ApplicationArea=#All;
                SourceExpr="VAT Registration No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("VAT Registration No."));
                           END;

                ImplicitType=Text20 }

    { 5   ;2   ;Field     ;
                Name=currencyId;
                CaptionML=[@@@={Locked};
                           ENU=CurrencyId];
                ApplicationArea=#All;
                SourceExpr="Currency Id";
                OnValidate=BEGIN
                             if "Currency Id" = BlankGUID then
                               "Currency Code" := ''
                             else begin
                               Currency.SETRANGE(Id,"Currency Id");
                               if not Currency.FINDFIRST then
                                 ERROR(CurrencyIdDoesNotMatchACurrencyErr);

                               "Currency Code" := Currency.Code;
                             end;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;

                ImplicitType=GUID }

    { 12  ;2   ;Field     ;
                Name=currencyCode;
                CaptionML=[@@@={Locked};
                           ENU=CurrencyCode];
                ApplicationArea=#All;
                SourceExpr=CurrencyCodeTxt;
                OnValidate=BEGIN
                             "Currency Code" :=
                               GraphMgtGeneralTools.TranslateCurrencyCodeToNAVCurrencyCode(
                                 LCYCurrencyCode,COPYSTR(CurrencyCodeTxt,1,MAXSTRLEN(LCYCurrencyCode)));

                             if Currency.Code <> '' then begin
                               if Currency.Code <> "Currency Code" then
                                 ERROR(CurrencyValuesDontMatchErr);
                               exit;
                             end;

                             if "Currency Code" = '' then
                               "Currency Id" := BlankGUID
                             else begin
                               if not Currency.GET("Currency Code") then
                                 ERROR(CurrencyCodeDoesNotMatchACurrencyErr);

                               "Currency Id" := Currency.Id;
                             end;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;

                ImplicitType=Text }

    { 23  ;2   ;Field     ;
                Name=paymentTermsId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentTermsId];
                ApplicationArea=#All;
                SourceExpr="Payment Terms Id";
                OnValidate=BEGIN
                             if "Payment Terms Id" = BlankGUID then
                               "Payment Terms Code" := ''
                             else begin
                               PaymentTerms.SETRANGE(Id,"Payment Terms Id");
                               if not PaymentTerms.FINDFIRST then
                                 ERROR(PaymentTermsIdDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Code" := PaymentTerms.Code;
                             end;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;

                ImplicitType=GUID }

    { 13  ;2   ;Field     ;
                Name=paymentTerms;
                CaptionML=[@@@={Locked};
                           ENU=PaymentTerms];
                ToolTipML=ENU=Specifies the Payment Terms for the Customer.;
                ApplicationArea=#All;
                SourceExpr=PaymentTermsJSON;
                OnValidate=BEGIN
                             GraphMgtCustomer.UpdatePaymentTerms(PaymentTermsJSON,Rec);

                             if PaymentTerms.Code <> '' then begin
                               if PaymentTerms.Code <> "Payment Terms Code" then
                                 ERROR(PaymentTermsValuesDontMatchErr);
                               exit;
                             end;

                             if "Payment Terms Code" = '' then
                               "Payment Terms Id" := BlankGUID
                             else begin
                               if not PaymentTerms.GET("Payment Terms Code") then
                                 ERROR(PaymentTermsCodeDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Id" := PaymentTerms.Id;
                             end;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;

                ODataEDMType=PAYMENTTERMSTYPE;
                ImplicitType=Text }

    { 25  ;2   ;Field     ;
                Name=shipmentMethodId;
                CaptionML=[@@@={Locked};
                           ENU=ShipmentMethodId];
                ApplicationArea=#All;
                SourceExpr="Shipment Method Id";
                OnValidate=BEGIN
                             if "Shipment Method Id" = BlankGUID then
                               "Shipment Method Code" := ''
                             else begin
                               ShipmentMethod.SETRANGE(Id,"Shipment Method Id");
                               if not ShipmentMethod.FINDFIRST then
                                 ERROR(ShipmentMethodIdDoesNotMatchAShipmentMethodErr);

                               "Shipment Method Code" := ShipmentMethod.Code;
                             end;

                             RegisterFieldSet(FIELDNO("Shipment Method Id"));
                             RegisterFieldSet(FIELDNO("Shipment Method Code"));
                           END;

                ImplicitType=GUID }

    { 15  ;2   ;Field     ;
                Name=shipmentMethod;
                CaptionML=[@@@={Locked};
                           ENU=ShipmentMethod];
                ToolTipML=ENU=Specifies the Shipment Method for the Customer.;
                ApplicationArea=#All;
                SourceExpr=ShipmentMethodJSON;
                OnValidate=BEGIN
                             GraphMgtCustomer.UpdateShipmentMethod(ShipmentMethodJSON,Rec);

                             if ShipmentMethod.Code <> '' then begin
                               if ShipmentMethod.Code <> "Shipment Method Code" then
                                 ERROR(ShipmentMethodValuesDontMatchErr);
                               exit;
                             end;

                             if "Shipment Method Code" = '' then
                               "Shipment Method Id" := BlankGUID
                             else begin
                               if not ShipmentMethod.GET("Shipment Method Code") then
                                 ERROR(ShipmentMethodCodeDoesNotMatchAShipmentMethodErr);

                               "Shipment Method Id" := ShipmentMethod.Id;
                             end;

                             RegisterFieldSet(FIELDNO("Shipment Method Id"));
                             RegisterFieldSet(FIELDNO("Shipment Method Code"));
                           END;

                ODataEDMType=SHIPMENTMETHOD;
                ImplicitType=Text }

    { 27  ;2   ;Field     ;
                Name=paymentMethodId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentMethodId];
                ApplicationArea=#All;
                SourceExpr="Payment Method Id";
                OnValidate=BEGIN
                             if "Payment Method Id" = BlankGUID then
                               "Payment Method Code" := ''
                             else begin
                               PaymentMethod.SETRANGE(Id,"Payment Method Id");
                               if not PaymentMethod.FINDFIRST then
                                 ERROR(PaymentMethodIdDoesNotMatchAPaymentMethodErr);

                               "Payment Method Code" := PaymentMethod.Code;
                             end;

                             RegisterFieldSet(FIELDNO("Payment Method Id"));
                             RegisterFieldSet(FIELDNO("Payment Method Code"));
                           END;

                ImplicitType=GUID }

    { 16  ;2   ;Field     ;
                Name=paymentMethod;
                CaptionML=[@@@={Locked};
                           ENU=PaymentMethod];
                ToolTipML=ENU=Specifies the Payment Method for the Vendor.;
                ApplicationArea=#All;
                SourceExpr=PaymentMethodJSON;
                OnValidate=BEGIN
                             GraphMgtCustomer.UpdatePaymentMethod(PaymentMethodJSON,Rec);

                             if PaymentMethod.Code <> '' then begin
                               if PaymentMethod.Code <> "Payment Method Code" then
                                 ERROR(PaymentMethodValuesDontMatchErr);
                               exit;
                             end;

                             if "Payment Method Code" = '' then
                               "Payment Method Id" := BlankGUID
                             else begin
                               if not PaymentMethod.GET("Payment Method Code") then
                                 ERROR(PaymentMethodCodeDoesNotMatchAPaymentMethodErr);

                               "Payment Method Id" := PaymentMethod.Id;
                             end;

                             RegisterFieldSet(FIELDNO("Payment Method Id"));
                             RegisterFieldSet(FIELDNO("Payment Method Code"));
                           END;

                ODataEDMType=PAYMENTMETHOD;
                ImplicitType=Text }

    { 17  ;2   ;Field     ;
                Name=blocked;
                CaptionML=[@@@={Locked};
                           ENU=Blocked];
                ApplicationArea=#All;
                SourceExpr=Blocked;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Blocked));
                           END;

                ImplicitType=Option }

    { 18  ;2   ;Field     ;
                Name=balance;
                CaptionML=[@@@={Locked};
                           ENU=Balance];
                ApplicationArea=#All;
                SourceExpr="Balance (LCY)";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 6   ;2   ;Field     ;
                Name=overdueAmount;
                CaptionML=[@@@={Locked};
                           ENU=overdueAmount];
                ApplicationArea=#All;
                SourceExpr=OverdueAmount;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 8   ;2   ;Field     ;
                Name=totalSalesExcludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalSalesExcludingTax];
                ApplicationArea=#All;
                SourceExpr="Sales (LCY)";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 7   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time";
                ImplicitType=DateTime }

  }
  CODE
  {
    VAR
      Currency@1009 : Record Currency;
      PaymentTerms@1016 : Record "Payment Terms";
      ShipmentMethod@1018 : Record "Shipment Method";
      PaymentMethod@1025 : Record "Payment Method";
      TempFieldSet@1003 : TEMPORARY Record Field;
      GraphMgtCustomer@1017 : Codeunit "Graph Mgt - Customer";
      GraphMgtGeneralTools@1008 : Codeunit "Graph Mgt - General Tools";
      LCYCurrencyCode@1007 : Code[10];
      PaymentMethodJSON@1002 : Text;
      PaymentTermsJSON@1000 : Text;
      CurrencyCodeTxt@1006 : Text;
      PostalAddressJSON@1004 : Text;
      ShipmentMethodJSON@1001 : Text;
      TaxAreaDisplayName@1027 : Text;
      OverdueAmount@1005 : Decimal;
      CurrencyValuesDontMatchErr@1012 : TextConst '@@@={Locked};ENU=The currency values do not match to a specific Currency.';
      CurrencyIdDoesNotMatchACurrencyErr@1011 : TextConst '@@@={Locked};ENU=The "currencyId" does not match to a Currency.';
      CurrencyCodeDoesNotMatchACurrencyErr@1010 : TextConst '@@@={Locked};ENU=The "currencyCode" does not match to a Currency.';
      PaymentTermsValuesDontMatchErr@1015 : TextConst '@@@={Locked};ENU=The payment terms values do not match to a specific Payment Terms.';
      PaymentTermsIdDoesNotMatchAPaymentTermsErr@1014 : TextConst '@@@={Locked};ENU=The "paymentTermsId" does not match to a Payment Terms.';
      PaymentTermsCodeDoesNotMatchAPaymentTermsErr@1013 : TextConst '@@@={Locked};ENU=The "paymentTermsCode" does not match to a Payment Terms.';
      ShipmentMethodValuesDontMatchErr@1021 : TextConst '@@@={Locked};ENU=The shipment method values do not match to a specific Shipment Method.';
      ShipmentMethodIdDoesNotMatchAShipmentMethodErr@1020 : TextConst '@@@={Locked};ENU=The "shipmentMethodId" does not match to a Shipment Method.';
      ShipmentMethodCodeDoesNotMatchAShipmentMethodErr@1019 : TextConst '@@@={Locked};ENU=The "shipmentMethodCode" does not match to a Shipment Method.';
      PaymentMethodValuesDontMatchErr@1024 : TextConst '@@@={Locked};ENU=The payment method values do not match to a specific Payment Method.';
      PaymentMethodIdDoesNotMatchAPaymentMethodErr@1023 : TextConst '@@@={Locked};ENU=The "paymentMethodId" does not match to a Payment Method.';
      PaymentMethodCodeDoesNotMatchAPaymentMethodErr@1022 : TextConst '@@@={Locked};ENU=The "paymentMethodCode" does not match to a Payment Method.';
      BlankGUID@1026 : GUID;

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      TaxAreaBuffer@1001 : Record "Tax Area Buffer";
      GraphMgtCustomer@1000 : Codeunit "Graph Mgt - Customer";
    BEGIN
      PaymentTermsJSON := GraphMgtCustomer.PaymentTermsToJSON("Payment Terms Code");
      ShipmentMethodJSON := GraphMgtCustomer.ShipmentMethodToJSON("Shipment Method Code");
      PaymentMethodJSON := GraphMgtCustomer.PaymentMethodToJSON("Payment Method Code");
      PostalAddressJSON := GraphMgtCustomer.PostalAddressToJSON(Rec);
      CurrencyCodeTxt := GraphMgtGeneralTools.TranslateNAVCurrencyCodeToCurrencyCode(LCYCurrencyCode,"Currency Code");

      SETRANGE("Date Filter",0D,WORKDATE - 1);
      CALCFIELDS("Balance Due (LCY)");
      OverdueAmount := "Balance Due (LCY)";
      SETRANGE("Date Filter",0D,WORKDATE);
      CALCFIELDS("Sales (LCY)","Balance (LCY)");

      TaxAreaDisplayName := TaxAreaBuffer.GetTaxAreaDisplayName("Tax Area ID");
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(Id);
      CLEAR(PaymentTermsJSON);
      CLEAR(TaxAreaDisplayName);
      CLEAR(ShipmentMethodJSON);
      CLEAR(PaymentMethodJSON);
      CLEAR(PostalAddressJSON);
      CLEAR(OverdueAmount);
      CLEAR("Balance (LCY)");
      CLEAR("Sales (LCY)");
      TempFieldSet.DELETEALL;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      if TempFieldSet.GET(DATABASE::Customer,FieldNo) then
        exit;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::Customer;
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(true);
    END;

    BEGIN
    END.
  }
}

