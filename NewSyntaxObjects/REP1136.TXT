OBJECT Report 1136 Copy Cost Acctg. Budget to G/L
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Cost Acctg. Budget to G/L;
    ProcessingOnly=true;
    OnInitReport=BEGIN
                   if NoOfCopies = 0 then
                     NoOfCopies := 1;
                   if Factor = 0 then
                     Factor := 1;
                 END;

  }
  DATASET
  {
    { 3233;    ;DataItem;Cost Budget Entry   ;
               DataItemTable="Cost Budget Entry";
               DataItemTableView=SORTING("Budget Name","Cost Type No.",Date);
               OnPreDataItem=BEGIN
                               if Factor <= 0 then
                                 ERROR(Text000);

                               if NoOfCopies < 1 then
                                 ERROR(Text001);

                               if (NoOfCopies > 1) and (DateChange = '') then
                                 ERROR(Text002);

                               if GETFILTER("Budget Name") = '' then
                                 ERROR(Text008);

                               if GLBudgetNameTarget.Name = '' then
                                 ERROR(Text009);

                               if not CONFIRM(Text004,false,GETFILTER("Budget Name"),GLBudgetNameTarget.Name,Factor,NoOfCopies,GETFILTER(Date),DateChange) then
                                 ERROR('');

                               LOCKTABLE;

                               if GLBudgetEntryTarget.FINDLAST then
                                 LastEntryNo := GLBudgetEntryTarget."Entry No.";

                               Window.OPEN(Text005);

                               Window.UPDATE(1,COUNT);
                               Window.UPDATE(2,NoOfCopies);
                             END;

               OnAfterGetRecord=VAR
                                  TempDimSetEntry@1000 : TEMPORARY Record "Dimension Set Entry";
                                BEGIN
                                  GLBudgetEntryTarget.INIT;
                                  GLBudgetEntryTarget."Budget Name" := GLBudgetNameTarget.Name;

                                  // Get corresponding G/L account
                                  if not CostType.GET("Cost Type No.") then begin
                                    NoSkipped := NoSkipped + 1;
                                    CurrReport.SKIP;
                                  end;

                                  GLAcc.SETFILTER("No.",'%1',CostType."G/L Account Range");
                                  if not GLAcc.FINDFIRST then begin
                                    NoSkipped := NoSkipped + 1;
                                    CurrReport.SKIP;
                                  end;
                                  GLBudgetEntryTarget."G/L Account No." := GLAcc."No.";
                                  GLBudgetEntryTarget.Date := Date;

                                  CostAccSetup.GET;
                                  if CostAccMgt.CostCenterExistsAsDimValue("Cost Center Code") then
                                    GLBudgetEntryTarget.UpdateDimSet(TempDimSetEntry,CostAccSetup."Cost Center Dimension","Cost Center Code");
                                  if CostAccMgt.CostObjectExistsAsDimValue("Cost Object Code") then
                                    GLBudgetEntryTarget.UpdateDimSet(TempDimSetEntry,CostAccSetup."Cost Object Dimension","Cost Object Code");
                                  GLBudgetEntryTarget."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
                                  UpdateBudgetDimensions(GLBudgetEntryTarget);
                                  GLBudgetEntryTarget.Description :=
                                    COPYSTR(STRSUBSTNO(Text006,GETFILTER("Budget Name")),1,MAXSTRLEN(GLBudgetEntryTarget.Description));
                                  GLBudgetEntryTarget."User ID" := USERID;

                                  // Amt * req. window factor
                                  GLBudgetEntryTarget.Amount := ROUND(Amount * Factor,0.01);

                                  // Create entries according to "copies". Increment date.
                                  for i := 1 to NoOfCopies do begin
                                    LastEntryNo := LastEntryNo + 1;
                                    GLBudgetEntryTarget."Entry No." := LastEntryNo;

                                    // Prepare date for next entry
                                    if DateChange <> '' then
                                      GLBudgetEntryTarget.Date := CALCDATE(DateFormula,GLBudgetEntryTarget.Date);

                                    GLBudgetEntryTarget.INSERT;

                                    NoInserted := NoInserted + 1;
                                    if (NoInserted mod 100) = 0 then
                                      Window.UPDATE(3,NoInserted);
                                  end;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                if NoInserted = 0 then begin
                                  MESSAGE(Text010,NoSkipped);
                                  ERROR('');
                                end;

                                if not CONFIRM(Text007,true,NoInserted,GLBudgetNameTarget.Name,NoSkipped) then
                                  ERROR('');
                              END;

               ReqFilterFields="Budget Name","Cost Type No.","Cost Center Code","Cost Object Code",Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnInit=BEGIN
               GLBudgetNameTarget.INIT;
             END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  Name=Allocation Target Budget Name;
                  Lookup=true;
                  CaptionML=ENU=Allocation Target Budget Name;
                  ToolTipML=ENU=Specifies a general ledger budget name.;
                  ApplicationArea=#Advanced;
                  SourceExpr=GLBudgetNameTarget.Name;
                  TableRelation="G/L Budget Name" }

      { 3   ;2   ;Field     ;
                  Name=Amount multiplication factor;
                  CaptionML=ENU=Amount multiplication factor;
                  ToolTipML=ENU=Specifies the amount multiplication factor. Enter a 1.00 if you want to copy the values 1:1. The value 1.05 increases the budget figures by 5 percent.;
                  ApplicationArea=#Advanced;
                  SourceExpr=Factor }

      { 8   ;2   ;Field     ;
                  Name=No. of Copies;
                  CaptionML=ENU=No. of Copies;
                  ToolTipML=ENU=Specifies how many times the cost accounting budget is copied.;
                  ApplicationArea=#Advanced;
                  SourceExpr=NoOfCopies }

      { 6   ;2   ;Field     ;
                  Name=Date Change Formula;
                  CaptionML=ENU=Date Change Formula;
                  ToolTipML=ENU=Specifies how the dates on the entries that are copied will be changed. For example, to copy last week's budget to this week, use the formula 1W, which is one week.;
                  ApplicationArea=#Advanced;
                  DateFormula=true;
                  SourceExpr=DateChange;
                  OnValidate=BEGIN
                               EVALUATE(DateFormula,DateChange);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLBudgetEntryTarget@1000 : Record "G/L Budget Entry";
      GLBudgetNameTarget@1001 : Record "G/L Budget Name";
      CostType@1002 : Record "Cost Type";
      GLAcc@1003 : Record "G/L Account";
      CostAccSetup@1004 : Record "Cost Accounting Setup";
      CostAccMgt@1005 : Codeunit "Cost Account Mgt";
      DimMgt@1006 : Codeunit DimensionManagement;
      Window@1007 : Dialog;
      DateChange@1008 : Code[10];
      DateFormula@1009 : DateFormula;
      LastEntryNo@1010 : Integer;
      NoOfCopies@1011 : Integer;
      Factor@1012 : Decimal;
      i@1013 : Integer;
      NoSkipped@1014 : Integer;
      NoInserted@1015 : Integer;
      Text000@1016 : TextConst 'ENU=The multiplication factor must not be 0 or less than 0.';
      Text001@1017 : TextConst 'ENU=Number of copies must be at least 1.';
      Text002@1018 : TextConst 'ENU=If more than one copy is created, a formula for date change must be defined.';
      Text004@1020 : TextConst '@@@="%3=multiplication factor (decimal);%4=No of copies (integer)";ENU=Cost budget "%1" will be copied to G/L budget "%2". The budget amounts will be multiplied by factor %3. \%4 copies will be created and the posting will be increased from the range of "%5" to "%6".\\Do you want do copy the budget?';
      Text005@1021 : TextConst 'ENU=Copying budget entries\No of entries #1#####\No of copies  #2#####\Copied        #3#####';
      Text006@1022 : TextConst '@@@=%1 - Budget Name.;ENU=Copy of cost budget %1';
      Text007@1023 : TextConst '@@@="%2=budget name;%3=integer value";ENU=%1 entries generated in budget %2.\\%3 entries were skipped because there were no corresponding G/L accounts defined.\\Copy entries?';
      Text008@1024 : TextConst 'ENU=Define name of source budget.';
      Text009@1025 : TextConst 'ENU=Define name of target budget.';
      Text010@1026 : TextConst 'ENU=No entries were copied. %1 entries were skipped because no corresponding general ledger accounts were defined.';

    LOCAL PROCEDURE UpdateBudgetDimensions@1(VAR GLBudgetEntry@1000 : Record "G/L Budget Entry");
    VAR
      GLSetup@1001 : Record "General Ledger Setup";
      GLBudgetName@1002 : Record "G/L Budget Name";
      DimSetEntry@1003 : Record "Dimension Set Entry";
    BEGIN
      GLSetup.GET;
      with GLBudgetEntry do begin
        GLBudgetName.GET("Budget Name");

        "Global Dimension 1 Code" := '';
        "Global Dimension 2 Code" := '';
        "Budget Dimension 1 Code" := '';
        "Budget Dimension 2 Code" := '';
        "Budget Dimension 3 Code" := '';
        "Budget Dimension 4 Code" := '';

        if DimSetEntry.GET("Dimension Set ID",GLSetup."Global Dimension 1 Code") then
          "Global Dimension 1 Code" := DimSetEntry."Dimension Value Code";
        if DimSetEntry.GET("Dimension Set ID",GLSetup."Global Dimension 2 Code") then
          "Global Dimension 2 Code" := DimSetEntry."Dimension Value Code";
        if DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 1 Code") then
          "Budget Dimension 1 Code" := DimSetEntry."Dimension Value Code";
        if DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 2 Code") then
          "Budget Dimension 2 Code" := DimSetEntry."Dimension Value Code";
        if DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 3 Code") then
          "Budget Dimension 3 Code" := DimSetEntry."Dimension Value Code";
        if DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 4 Code") then
          "Budget Dimension 4 Code" := DimSetEntry."Dimension Value Code";
      end;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

