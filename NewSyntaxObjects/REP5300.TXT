OBJECT Report 5300 Outlook Synch. Change Log Set.
{
  OBJECT-PROPERTIES
  {
    Date=20150915D;
    Time=120000T;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Outlook Synch. Change Log Set.;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  if not OSynchEntity.FINDFIRST then
                    ERROR(Text004);
                END;

    OnPostReport=VAR
                   ChangeLogSetup@1000 : Record "Change Log Setup";
                 BEGIN
                   if not ChangeLogSetup.GET then
                     ChangeLogSetup.INSERT;
                   if not ChangeLogSetup."Change Log Activated" then begin
                     ChangeLogSetup."Change Log Activated" := true;
                     ChangeLogSetup.MODIFY;
                     MESSAGE(Text002);
                   end else
                     MESSAGE(Text003);
                 END;

  }
  DATASET
  {
    { 6770;    ;DataItem;OSynchEntity        ;
               DataItemTable="Outlook Synch. Entity";
               DataItemTableView=SORTING(Code);
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogPrimaryKey("Table No.");
                                END;

               ReqFilterFields=Code }

    { 4710;1   ;DataItem;OSynchEntityElement ;
               DataItemTable="Outlook Synch. Entity Element";
               DataItemTableView=SORTING("Synch. Entity Code","Element No.") ORDER(Ascending) WHERE("Element No."=FILTER(<>0));
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogPrimaryKey("Table No.");
                                END;

               DataItemLink="Synch. Entity Code"=FIELD(Code) }

    { 6868;2   ;DataItem;OSynchFilterElement ;
               DataItemTable="Outlook Synch. Filter";
               DataItemTableView=SORTING("Record GUID","Filter Type","Line No.") ORDER(Ascending) WHERE("Filter Type"=CONST("Table Relation"));
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogFilter(OSynchFilterElement);
                                END;

               DataItemLink="Record GUID"=FIELD("Record GUID") }

    { 2890;1   ;DataItem;OSynchField         ;
               DataItemTable="Outlook Synch. Field";
               DataItemTableView=SORTING("Synch. Entity Code","Element No.","Line No.");
               OnAfterGetRecord=VAR
                                  OSynchFilter@1000 : Record "Outlook Synch. Filter";
                                  OSynchEntityElement@1001 : Record "Outlook Synch. Entity Element";
                                BEGIN
                                  if "Table No." <> 0 then begin
                                    OSynchFilter.RESET;
                                    OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                    OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
                                    OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
                                    if not OSynchFilter.FINDFIRST then begin
                                      CALCFIELDS("Table Caption");
                                      if "Element No." = 0 then
                                        ERROR(Text001,"Table Caption",OSynchEntity.TABLECAPTION,OSynchEntity.Code);

                                      OSynchEntityElement.GET("Synch. Entity Code","Element No.");
                                      OSynchEntityElement.CALCFIELDS("Table Caption");
                                      ERROR(
                                        Text005,
                                        "Table Caption",
                                        OSynchEntityElement."Table Caption",
                                        OSynchEntityElement."Outlook Collection",
                                        OSynchEntity.Code);
                                    end;

                                    Field.GET("Table No.","Field No.");
                                    Field.TESTFIELD(Enabled,true);
                                    RegisterChangeLogField("Table No.","Field No.");

                                    FieldID := OSynchFilter."Master Table Field No.";
                                  end else
                                    FieldID := "Field No.";

                                  Field.GET("Master Table No.",FieldID);
                                  Field.TESTFIELD(Enabled,true);

                                  RegisterChangeLogField("Master Table No.",FieldID);
                                END;

               DataItemLink="Synch. Entity Code"=FIELD(Code) }

    { 7744;    ;DataItem;OSynchFilterEntity  ;
               DataItemTable="Outlook Synch. Filter";
               DataItemTableView=SORTING("Record GUID","Filter Type","Line No.") ORDER(Ascending);
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogFilter(OSynchFilterEntity);
                                END;

               DataItemLinkReference=OSynchEntity;
               DataItemLink="Record GUID"=FIELD("Record GUID") }

    { 9398;    ;DataItem;OSynchUserSetup     ;
               DataItemTable="Outlook Synch. User Setup";
               DataItemTableView=SORTING("User ID","Synch. Entity Code") ORDER(Ascending);
               DataItemLinkReference=OSynchEntity;
               DataItemLink="Synch. Entity Code"=FIELD(Code) }

    { 3638;1   ;DataItem;OSynchFilterUserSetup;
               DataItemTable="Outlook Synch. Filter";
               DataItemTableView=SORTING("Record GUID","Filter Type","Line No.") ORDER(Ascending);
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogFilter(OSynchFilterUserSetup);
                                END;

               DataItemLink="Record GUID"=FIELD("Record GUID") }

    { 1658;    ;DataItem;OSynchDependency    ;
               DataItemTable="Outlook Synch. Dependency";
               DataItemTableView=SORTING("Synch. Entity Code","Element No.","Depend. Synch. Entity Code") ORDER(Ascending);
               DataItemLinkReference=OSynchEntity;
               DataItemLink="Synch. Entity Code"=FIELD(Code) }

    { 7267;1   ;DataItem;OSynchFilterDependency;
               DataItemTable="Outlook Synch. Filter";
               DataItemTableView=SORTING("Record GUID","Filter Type","Line No.") ORDER(Ascending);
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogFilter(OSynchFilterDependency);
                                END;

               DataItemLink="Record GUID"=FIELD("Record GUID") }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Field@1004 : Record Field;
      FieldID@1000 : Integer;
      Text001@1002 : TextConst 'ENU=The relation between the %1 table and %2 table in the %3 entity cannot be determined. Please verify your synchronization settings for this entity.';
      Text002@1001 : TextConst 'ENU=The change log settings have been registered successfully. You must close and reopen the company for the new change log settings to take effect.';
      Text003@1003 : TextConst 'ENU=The change log settings have been registered successfully.';
      Text004@1005 : TextConst 'ENU=The entity cannot be found. Make sure that you have typed its name correctly.';
      Text005@1006 : TextConst 'ENU=The relation between the %1 table and %2 table in the %3 collection from the %4 entity cannot be determined. Verify your synchronization settings for this entity.';

    LOCAL PROCEDURE RegisterChangeLogField@10(TableID@1000 : Integer;FieldID@1001 : Integer);
    VAR
      ChangeLogSetupTable@1002 : Record "Change Log Setup (Table)";
      ChangeLogSetupField@1003 : Record "Change Log Setup (Field)";
      NeedToBeUpdated@1004 : Boolean;
    BEGIN
      with ChangeLogSetupTable do begin
        RESET;
        if not GET(TableID) then begin
          INIT;
          "Table No." := TableID;
          VALIDATE("Log Insertion","Log Insertion"::"Some Fields");
          VALIDATE("Log Modification","Log Modification"::"Some Fields");
          VALIDATE("Log Deletion","Log Modification"::"Some Fields");
          INSERT;
          NeedToBeUpdated := true;
        end else begin
          NeedToBeUpdated :=
            ("Log Insertion" <> "Log Insertion"::"All Fields") or
            ("Log Modification" <> "Log Modification"::"All Fields") or
            ("Log Deletion" <> "Log Deletion"::"All Fields");

          if "Log Insertion" <> "Log Insertion"::"All Fields" then
            "Log Insertion" := "Log Insertion"::"Some Fields";

          if "Log Modification" <> "Log Modification"::"All Fields" then
            "Log Modification" := "Log Insertion"::"Some Fields";

          if "Log Deletion" <> "Log Deletion"::"All Fields" then
            "Log Deletion" := "Log Deletion"::"Some Fields";

          if NeedToBeUpdated then
            MODIFY;
        end;
      end;

      if not NeedToBeUpdated then
        exit;

      with ChangeLogSetupField do begin
        RESET;
        if not GET(TableID,FieldID) then begin
          INIT;
          "Table No." := TableID;
          "Field No." := FieldID;
          "Log Insertion" := true;
          "Log Modification" := true;
          "Log Deletion" := true;
          INSERT;
        end else begin
          "Log Insertion" := true;
          "Log Modification" := true;
          "Log Deletion" := true;
          MODIFY;
        end;
      end;
    END;

    LOCAL PROCEDURE RegisterChangeLogPrimaryKey@1(TableID@1000 : Integer);
    VAR
      RecRef@1001 : RecordRef;
      I@1002 : Integer;
    BEGIN
      RecRef.OPEN(TableID,true);
      for I := 1 to RecRef.KEYINDEX(1).FIELDCOUNT do
        RegisterChangeLogField(TableID,RecRef.KEYINDEX(1).FIELDINDEX(I).NUMBER);
      RecRef.CLOSE;
    END;

    LOCAL PROCEDURE RegisterChangeLogFilter@8(OSynchFilter1@1000 : Record "Outlook Synch. Filter");
    BEGIN
      if Field.GET(OSynchFilter1."Table No.",OSynchFilter1."Field No.") then begin
        Field.TESTFIELD(Enabled,true);
        RegisterChangeLogField(OSynchFilter1."Table No.",OSynchFilter1."Field No.");
      end;
      if Field.GET(OSynchFilter1."Master Table No.",OSynchFilter1."Master Table Field No.") then begin
        Field.TESTFIELD(Enabled,true);
        RegisterChangeLogField(OSynchFilter1."Master Table No.",OSynchFilter1."Master Table Field No.");
      end;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

