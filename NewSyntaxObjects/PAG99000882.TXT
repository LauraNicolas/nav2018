OBJECT Page 99000882 Change Status on Prod. Order
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Change Status on Prod. Order;
    InsertAllowed=false;
    DeleteAllowed=false;
    ModifyAllowed=false;
    DataCaptionExpr='';
    PageType=ConfirmationDialog;
    InstructionalTextML=ENU=Do you want to change the status of this production order?;
    OnInit=BEGIN
             FinishedStatusEditable := true;
             ReleasedStatusEditable := true;
             FirmPlannedStatusEditable := true;
           END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 8   ;1   ;Field     ;
                Name=FirmPlannedStatus;
                CaptionML=ENU=New Status;
                ApplicationArea=#Manufacturing;
                SourceExpr=ProdOrderStatus.Status;
                ValuesAllowed=["""Firm Planned""";Released;Finished];
                OnValidate=BEGIN
                             case ProdOrderStatus.Status of
                               ProdOrderStatus.Status::Finished:
                                 CheckStatus(FinishedStatusEditable);
                               ProdOrderStatus.Status::Released:
                                 CheckStatus(ReleasedStatusEditable);
                               ProdOrderStatus.Status::"Firm Planned":
                                 CheckStatus(FirmPlannedStatusEditable);
                             end;
                           END;
                            }

    { 6   ;1   ;Field     ;
                Name=PostingDate;
                CaptionML=ENU=Posting Date;
                ApplicationArea=#Manufacturing;
                SourceExpr=PostingDate }

    { 9   ;1   ;Field     ;
                Name=ReqUpdUnitCost;
                CaptionML=ENU=Update Unit Cost;
                ApplicationArea=#Manufacturing;
                SourceExpr=ReqUpdUnitCost }

  }
  CODE
  {
    VAR
      ProdOrderStatus@1000 : Record "Production Order";
      PostingDate@1001 : Date;
      ReqUpdUnitCost@1002 : Boolean;
      FirmPlannedStatusEditable@19025196 : Boolean INDATASET;
      ReleasedStatusEditable@19039198 : Boolean INDATASET;
      FinishedStatusEditable@19021340 : Boolean INDATASET;
      Text666@19003950 : TextConst 'ENU=%1 is not a valid selection.';

    [External]
    PROCEDURE Set@1(ProdOrder@1000 : Record "Production Order");
    BEGIN
      if ProdOrder.Status = ProdOrder.Status::Finished then
        ProdOrder.FIELDERROR(Status);
      FirmPlannedStatusEditable := ProdOrder.Status < ProdOrder.Status::"Firm Planned";
      ReleasedStatusEditable := ProdOrder.Status <> ProdOrder.Status::Released;
      FinishedStatusEditable := ProdOrder.Status = ProdOrder.Status::Released;

      if ProdOrder.Status > ProdOrder.Status::Simulated then
        ProdOrderStatus.Status := ProdOrder.Status + 1
      else
        ProdOrderStatus.Status := ProdOrderStatus.Status::"Firm Planned";

      PostingDate := WORKDATE;
    END;

    [External]
    PROCEDURE ReturnPostingInfo@4(VAR Status@1000 : 'Simulated,Planned,"Firm Planned",Released,Finished';VAR PostingDate2@1001 : Date;VAR UpdUnitCost@1002 : Boolean);
    BEGIN
      Status := ProdOrderStatus.Status;
      PostingDate2 := PostingDate;
      UpdUnitCost := ReqUpdUnitCost;
    END;

    LOCAL PROCEDURE CheckStatus@19071396(StatusEditable@19000001 : Boolean);
    BEGIN
      if not StatusEditable then
        ERROR(Text666,ProdOrderStatus.Status);
    END;

    BEGIN
    END.
  }
}

