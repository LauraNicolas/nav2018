OBJECT Codeunit 1278 Exp. User Feedback Gen. Jnl.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Data Exch.";
    Permissions=TableData "Payment Export Data"=rimd;
    OnRun=VAR
            CreditTransferRegister@1001 : Record "Credit Transfer Register";
            GenJnlLine@1000 : Record "Gen. Journal Line";
            PaymentExportData@1002 : Record "Payment Export Data";
          BEGIN
            GenJnlLine.SETRANGE("Data Exch. Entry No.","Entry No.");
            GenJnlLine.FINDFIRST;

            CreditTransferRegister.SETRANGE("From Bank Account No.",GenJnlLine."Bal. Account No.");
            CreditTransferRegister.FINDLAST;
            SetFileOnCreditTransferRegister(Rec,CreditTransferRegister);
            SetExportFlagOnGenJnlLine(GenJnlLine);

            PaymentExportData.SETRANGE("Data Exch Entry No.","Entry No.");
            PaymentExportData.DELETEALL(true);
          END;

  }
  CODE
  {

    LOCAL PROCEDURE SetFileOnCreditTransferRegister@5(DataExch@1000 : Record "Data Exch.";VAR CreditTransferRegister@1001 : Record "Credit Transfer Register");
    BEGIN
      CreditTransferRegister.SetStatus(CreditTransferRegister.Status::"File Created");
      CreditTransferRegister.SetFileContent(DataExch);
    END;

    [External]
    PROCEDURE SetExportFlagOnGenJnlLine@1(VAR GenJnlLine@1000 : Record "Gen. Journal Line");
    VAR
      GenJnlLine2@1001 : Record "Gen. Journal Line";
    BEGIN
      GenJnlLine2.COPYFILTERS(GenJnlLine);
      if GenJnlLine2.FINDSET then
        repeat
          case GenJnlLine2."Account Type" of
            GenJnlLine2."Account Type"::Vendor:
              SetExportFlagOnAppliedVendorLedgerEntry(GenJnlLine2);
            GenJnlLine2."Account Type"::Customer:
              SetExportFlagOnAppliedCustLedgerEntry(GenJnlLine2);
          end;
          GenJnlLine2.VALIDATE("Check Exported",true);
          GenJnlLine2.VALIDATE("Exported to Payment File",true);
          GenJnlLine2.MODIFY(true);
        until GenJnlLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE SetExportFlagOnAppliedVendorLedgerEntry@31(GenJnlLine@1000 : Record "Gen. Journal Line");
    VAR
      VendLedgerEntry@1001 : Record "Vendor Ledger Entry";
    BEGIN
      if GenJnlLine.IsApplied then begin
        VendLedgerEntry.SETRANGE("Vendor No.",GenJnlLine."Account No.");

        if GenJnlLine."Applies-to Doc. No." <> '' then begin
          VendLedgerEntry.SETRANGE("Document Type",GenJnlLine."Applies-to Doc. Type");
          VendLedgerEntry.SETRANGE("Document No.",GenJnlLine."Applies-to Doc. No.");
        end;

        if GenJnlLine."Applies-to ID" <> '' then
          VendLedgerEntry.SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID");

        if VendLedgerEntry.FINDSET then
          repeat
            VendLedgerEntry.VALIDATE("Exported to Payment File",true);
            CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",VendLedgerEntry);
          until VendLedgerEntry.NEXT = 0;
      end;

      VendLedgerEntry.RESET;
      VendLedgerEntry.SETRANGE("Vendor No.",GenJnlLine."Account No.");
      VendLedgerEntry.SETRANGE("Applies-to Doc. Type",GenJnlLine."Document Type");
      VendLedgerEntry.SETRANGE("Applies-to Doc. No.",GenJnlLine."Document No.");
      if VendLedgerEntry.FINDSET then
        repeat
          VendLedgerEntry.VALIDATE("Exported to Payment File",true);
          CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",VendLedgerEntry);
        until VendLedgerEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE SetExportFlagOnAppliedCustLedgerEntry@2(GenJnlLine@1000 : Record "Gen. Journal Line");
    VAR
      CustLedgerEntry@1001 : Record "Cust. Ledger Entry";
    BEGIN
      if GenJnlLine.IsApplied then begin
        CustLedgerEntry.SETRANGE("Customer No.",GenJnlLine."Account No.");

        if GenJnlLine."Applies-to Doc. No." <> '' then begin
          CustLedgerEntry.SETRANGE("Document Type",GenJnlLine."Applies-to Doc. Type");
          CustLedgerEntry.SETRANGE("Document No.",GenJnlLine."Applies-to Doc. No.");
        end;

        if GenJnlLine."Applies-to ID" <> '' then
          CustLedgerEntry.SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID");

        if CustLedgerEntry.FINDSET then
          repeat
            CustLedgerEntry.VALIDATE("Exported to Payment File",true);
            CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",CustLedgerEntry);
          until CustLedgerEntry.NEXT = 0;
      end;

      CustLedgerEntry.RESET;
      CustLedgerEntry.SETRANGE("Customer No.",GenJnlLine."Account No.");
      CustLedgerEntry.SETRANGE("Applies-to Doc. Type",GenJnlLine."Document Type");
      CustLedgerEntry.SETRANGE("Applies-to Doc. No.",GenJnlLine."Document No.");

      if CustLedgerEntry.FINDSET then
        repeat
          CustLedgerEntry.VALIDATE("Exported to Payment File",true);
          CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",CustLedgerEntry);
        until CustLedgerEntry.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

