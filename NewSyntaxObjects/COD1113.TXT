OBJECT Codeunit 1113 CA Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo="Cost Journal Line";
    OnRun=BEGIN
            CostJnlLine.COPY(Rec);
            Code;
            COPY(CostJnlLine);
          END;

  }
  CODE
  {
    VAR
      CostJnlLine@1000 : Record "Cost Journal Line";
      CostReg@1001 : Record "Cost Register";
      CostJnlTemplate@1002 : Record "Cost Journal Template";
      Text001@1003 : TextConst 'ENU=Do you want to post the journal lines?';
      Text002@1004 : TextConst 'ENU=There is nothing to post.';
      Text003@1005 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1006 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.';

    LOCAL PROCEDURE Code@1();
    VAR
      TempJnlBatchName@1000 : Code[10];
    BEGIN
      with CostJnlLine do begin
        CostJnlTemplate.GET("Journal Template Name");
        CostJnlTemplate.TESTFIELD("Posting Report ID");
        if not CONFIRM(Text001) then
          exit;

        TempJnlBatchName := "Journal Batch Name";
        CODEUNIT.RUN(CODEUNIT::"CA Jnl.-Post Batch",CostJnlLine);
        CostReg.GET("Line No.");
        CostReg.SETRECFILTER;
        REPORT.RUN(CostJnlTemplate."Posting Report ID",false,false,CostReg);

        if "Line No." = 0 then
          MESSAGE(Text002)
        else
          if TempJnlBatchName = "Journal Batch Name" then
            MESSAGE(Text003)
          else
            MESSAGE(Text004,"Journal Batch Name");

        if not FIND('=><') or (TempJnlBatchName <> "Journal Batch Name") then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

