OBJECT Codeunit 5812 Calculate Standard Cost
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Too many levels. Must be below %1.';
      MfgSetup@1001 : Record "Manufacturing Setup";
      GLSetup@1002 : Record "General Ledger Setup";
      TempItem@1003 : TEMPORARY Record Item;
      TempWorkCenter@1016 : TEMPORARY Record "Work Center";
      TempMachineCenter@1017 : TEMPORARY Record "Machine Center";
      TempResCost@1027 : TEMPORARY Record "Resource Cost";
      ProdBOMVersionErrBuf@1012 : TEMPORARY Record "Production BOM Version";
      RtngVersionErrBuf@1014 : TEMPORARY Record "Routing Version";
      CostCalcMgt@1004 : Codeunit "Cost Calculation Management";
      VersionMgt@1005 : Codeunit VersionManagement;
      UOMMgt@1006 : Codeunit "Unit of Measure Management";
      Window@1019 : Dialog;
      MaxLevel@1007 : Integer;
      CalculationDate@1008 : Date;
      CalcMultiLevel@1009 : Boolean;
      UseAssemblyList@1010 : Boolean;
      LogErrors@1011 : Boolean;
      Text001@1013 : TextConst 'ENU=&Top level,&All levels';
      ShowDialog@1020 : Boolean;
      StdCostWkshName@1015 : Text[50];
      Text002@1018 : TextConst 'ENU=@1@@@@@@@@@@@@@';
      CalcMfgPrompt@1021 : TextConst 'ENU=One or more subassemblies on the assembly list for item %1 use replenishment system Prod. Order. Do you want to calculate standard cost for those subassemblies?';
      TargetText@1026 : TextConst 'ENU=Standard Cost,Unit Price';
      RecursionInstruction@1025 : TextConst '@@@="%1 = Item No., %2 = Description";ENU=Calculate the %3 of item %1 %2 by rolling up the assembly list components. Select All levels to include and update the %3 of any subassemblies.';
      NonAssemblyItemError@1024 : TextConst '@@@="%1 = Item No., %2 = Description";ENU=Item %1 %2 does not use replenishment system Assembly. The %3 will not be calculated.';
      NoAssemblyListError@1023 : TextConst '@@@="%1 = Item No., %2 = Description";ENU=Item %1 %2 has no assembly list. The %3 will not be calculated.';
      NonAssemblyComponentWithList@1022 : TextConst 'ENU=One or more subassemblies on the assembly list for this item does not use replenishment system Assembly. The %1 for these subassemblies will not be calculated. Are you sure that you want to continue?';
      ColIdx@1028 : ',StdCost,ExpCost,ActCost,Dev,Var';
      RowIdx@1029 : ',MatCost,ResCost,ResOvhd,AsmOvhd,Total';

    [External]
    PROCEDURE SetProperties@1(NewCalculationDate@1000 : Date;NewCalcMultiLevel@1001 : Boolean;NewUseAssemblyList@1002 : Boolean;NewLogErrors@1003 : Boolean;NewStdCostWkshName@1004 : Text[50];NewShowDialog@1005 : Boolean);
    BEGIN
      TempItem.DELETEALL;
      ProdBOMVersionErrBuf.DELETEALL;
      RtngVersionErrBuf.DELETEALL;
      CLEARALL;

      CalculationDate := NewCalculationDate;
      CalcMultiLevel := NewCalcMultiLevel;
      UseAssemblyList := NewUseAssemblyList;
      LogErrors := NewLogErrors;
      StdCostWkshName := NewStdCostWkshName;
      ShowDialog := NewShowDialog;

      MaxLevel := 50;
      MfgSetup.GET;
      GLSetup.GET;
    END;

    [External]
    PROCEDURE TestPreconditions@18(VAR Item@1000 : Record Item;VAR NewProdBOMVersionErrBuf@1002 : Record "Production BOM Version";VAR NewRtngVersionErrBuf@1001 : Record "Routing Version");
    VAR
      TempItem2@1003 : TEMPORARY Record Item;
    BEGIN
      CalcItems(Item,TempItem2);

      ProdBOMVersionErrBuf.RESET;
      if ProdBOMVersionErrBuf.FIND('-') then
        repeat
          NewProdBOMVersionErrBuf := ProdBOMVersionErrBuf;
          NewProdBOMVersionErrBuf.INSERT;
        until ProdBOMVersionErrBuf.NEXT = 0;

      RtngVersionErrBuf.RESET;
      if RtngVersionErrBuf.FIND('-') then
        repeat
          NewRtngVersionErrBuf := RtngVersionErrBuf;
          NewRtngVersionErrBuf.INSERT;
        until RtngVersionErrBuf.NEXT = 0;
    END;

    LOCAL PROCEDURE AnalyzeAssemblyList@12(VAR Item@1000 : Record Item;VAR Depth@1002 : Integer;VAR NonAssemblyItemWithList@1001 : Boolean;VAR ContainsProdBOM@1007 : Boolean);
    VAR
      BOMComponent@1003 : Record "BOM Component";
      SubItem@1004 : Record Item;
      BaseDepth@1005 : Integer;
      MaxDepth@1006 : Integer;
    BEGIN
      if Item.IsMfgItem and ((Item."Production BOM No." <> '') or (Item."Routing No." <> '')) then begin
        ContainsProdBOM := true;
        if Item."Production BOM No." <> '' then
          AnalyzeProdBOM(Item."Production BOM No.",Depth,NonAssemblyItemWithList,ContainsProdBOM)
        else
          Depth += 1;
        exit
      end;
      BOMComponent.SETRANGE("Parent Item No.",Item."No.");
      if BOMComponent.FINDSET then begin
        if not Item.IsAssemblyItem then begin
          NonAssemblyItemWithList := true;
          exit
        end;
        Depth += 1;
        BaseDepth := Depth;
        repeat
          if BOMComponent.Type = BOMComponent.Type::Item then begin
            SubItem.GET(BOMComponent."No.");
            MaxDepth := BaseDepth;
            AnalyzeAssemblyList(SubItem,MaxDepth,NonAssemblyItemWithList,ContainsProdBOM);
            if MaxDepth > Depth then
              Depth := MaxDepth
          end
        until BOMComponent.NEXT = 0
      end;
    END;

    LOCAL PROCEDURE AnalyzeProdBOM@31(ProductionBOMNo@1001 : Code[20];VAR Depth@1000 : Integer;VAR NonAssemblyItemWithList@1008 : Boolean;VAR ContainsProdBOM@1007 : Boolean);
    VAR
      ProdBOMLine@1003 : Record "Production BOM Line";
      SubItem@1006 : Record Item;
      PBOMVersionCode@1002 : Code[20];
      BaseDepth@1005 : Integer;
      MaxDepth@1004 : Integer;
    BEGIN
      SetProdBOMFilters(ProdBOMLine,PBOMVersionCode,ProductionBOMNo);
      if ProdBOMLine.FINDSET then begin
        Depth += 1;
        BaseDepth := Depth;
        repeat
          case ProdBOMLine.Type of
            ProdBOMLine.Type::Item:
              begin
                SubItem.GET(ProdBOMLine."No.");
                MaxDepth := BaseDepth;
                AnalyzeAssemblyList(SubItem,MaxDepth,NonAssemblyItemWithList,ContainsProdBOM);
                if MaxDepth > Depth then
                  Depth := MaxDepth
              end;
            ProdBOMLine.Type::"Production BOM":
              begin
                MaxDepth := BaseDepth;
                AnalyzeProdBOM(ProdBOMLine."No.",MaxDepth,NonAssemblyItemWithList,ContainsProdBOM);
                MaxDepth -= 1;
                if MaxDepth > Depth then
                  Depth := MaxDepth
              end;
          end;
        until ProdBOMLine.NEXT = 0
      end
    END;

    LOCAL PROCEDURE PrepareAssemblyCalculation@17(VAR Item@1000 : Record Item;VAR Depth@1001 : Integer;Target@1002 : '"Standard Cost","Unit Price"';VAR ContainsProdBOM@1011 : Boolean) Instruction : Text[1024];
    VAR
      CalculationTarget@1008 : Text[80];
      SubNonAssemblyItemWithList@1009 : Boolean;
    BEGIN
      CalculationTarget := SELECTSTR(Target,TargetText);
      if not Item.IsAssemblyItem then
        ERROR(NonAssemblyItemError,Item."No.",Item.Description,CalculationTarget);
      AnalyzeAssemblyList(Item,Depth,SubNonAssemblyItemWithList,ContainsProdBOM);
      if Depth = 0 then
        ERROR(NoAssemblyListError,Item."No.",Item.Description,CalculationTarget);
      Instruction := STRSUBSTNO(RecursionInstruction,Item."No.",Item.Description,CalculationTarget);
      if SubNonAssemblyItemWithList then
        Instruction += STRSUBSTNO(NonAssemblyComponentWithList,CalculationTarget)
    END;

    [External]
    PROCEDURE CalcItem@4(ItemNo@1001 : Code[20];NewUseAssemblyList@1000 : Boolean);
    VAR
      Item@1002 : Record Item;
      ItemCostMgt@1004 : Codeunit ItemCostManagement;
      Instruction@1008 : Text[1024];
      NewCalcMultiLevel@1003 : Boolean;
      Depth@1009 : Integer;
      AssemblyContainsProdBOM@1005 : Boolean;
      CalcMfgItems@1006 : Boolean;
    BEGIN
      Item.GET(ItemNo);
      if NewUseAssemblyList then
        Instruction := PrepareAssemblyCalculation(Item,Depth,1,AssemblyContainsProdBOM) // 1=StandardCost
      else
        if not Item.IsMfgItem then
          exit;
      if not NewUseAssemblyList or (Depth > 1) then
        case STRMENU(Text001,1,Instruction) of
          0:
            exit;
          1:
            NewCalcMultiLevel := false;
          2:
            NewCalcMultiLevel := true;
        end;

      SetProperties(WORKDATE,NewCalcMultiLevel,NewUseAssemblyList,false,'',false);

      if NewUseAssemblyList then begin
        if NewCalcMultiLevel and AssemblyContainsProdBOM then
          CalcMfgItems := CONFIRM(CalcMfgPrompt,false,Item."No.");
        CalcAssemblyItem(ItemNo,Item,0,CalcMfgItems)
      end else
        CalcMfgItem(ItemNo,Item,0);

      if TempItem.FIND('-') then
        repeat
          ItemCostMgt.UpdateStdCostShares(TempItem);
        until TempItem.NEXT = 0;
    END;

    [External]
    PROCEDURE CalcItems@7(VAR Item@1000 : Record Item;VAR NewTempItem@1001 : Record Item);
    VAR
      Item2@1002 : Record Item;
      Item3@1003 : Record Item;
      NoOfRecords@1004 : Integer;
      LineCount@1005 : Integer;
    BEGIN
      NewTempItem.DELETEALL;

      Item2.COPY(Item);

      NoOfRecords := Item.COUNT;
      if ShowDialog then
        Window.OPEN(Text002);

      if Item2.FIND('-') then
        repeat
          LineCount := LineCount + 1;
          if ShowDialog then
            Window.UPDATE(1,ROUND(LineCount / NoOfRecords * 10000,1));
          if UseAssemblyList then
            CalcAssemblyItem(Item2."No.",Item3,0,true)
          else
            CalcMfgItem(Item2."No.",Item3,0);
        until Item2.NEXT = 0;

      TempItem.RESET;
      if TempItem.FIND('-') then
        repeat
          NewTempItem := TempItem;
          NewTempItem.INSERT;
        until TempItem.NEXT = 0;

      if ShowDialog then
        Window.CLOSE;
    END;

    LOCAL PROCEDURE CalcAssemblyItem@5(ItemNo@1000 : Code[20];VAR Item@1001 : Record Item;Level@1002 : Integer;CalcMfgItems@1007 : Boolean);
    VAR
      BOMComp@1004 : Record "BOM Component";
      CompItem@1005 : Record Item;
      Res@1003 : Record Resource;
      LotSize@1009 : Decimal;
      ComponentQuantity@1011 : Decimal;
    BEGIN
      if Level > MaxLevel then
        ERROR(Text000,MaxLevel);

      if GetItem(ItemNo,Item) then
        exit;

      if not Item.IsAssemblyItem then
        exit;

      if not CalcMultiLevel and (Level <> 0) then
        exit;

      BOMComp.SETRANGE("Parent Item No.",ItemNo);
      BOMComp.SETFILTER(Type,'<>%1',BOMComp.Type::" ");
      if BOMComp.FINDSET then begin
        Item."Rolled-up Material Cost" := 0;
        Item."Rolled-up Capacity Cost" := 0;
        Item."Rolled-up Cap. Overhead Cost" := 0;
        Item."Rolled-up Mfg. Ovhd Cost" := 0;
        Item."Rolled-up Subcontracted Cost" := 0;
        Item."Single-Level Material Cost" := 0;
        Item."Single-Level Capacity Cost" := 0;
        Item."Single-Level Cap. Ovhd Cost" := 0;
        Item."Single-Level Subcontrd. Cost" := 0;

        repeat
          case BOMComp.Type of
            BOMComp.Type::Item:
              begin
                GetItem(BOMComp."No.",CompItem);
                ComponentQuantity :=
                  BOMComp."Quantity per" *
                  UOMMgt.GetQtyPerUnitOfMeasure(CompItem,BOMComp."Unit of Measure Code");
                if CompItem.IsAssemblyItem or CompItem.IsMfgItem then begin
                  if CompItem.IsAssemblyItem then
                    CalcAssemblyItem(BOMComp."No.",CompItem,Level + 1,CalcMfgItems)
                  else
                    if CalcMfgItems then
                      CalcMfgItem(BOMComp."No.",CompItem,Level + 1);
                  Item."Rolled-up Material Cost" += ComponentQuantity * CompItem."Rolled-up Material Cost";
                  Item."Rolled-up Capacity Cost" += ComponentQuantity * CompItem."Rolled-up Capacity Cost";
                  Item."Rolled-up Cap. Overhead Cost" += ComponentQuantity * CompItem."Rolled-up Cap. Overhead Cost";
                  Item."Rolled-up Mfg. Ovhd Cost" += ComponentQuantity * CompItem."Rolled-up Mfg. Ovhd Cost";
                  Item."Rolled-up Subcontracted Cost" += ComponentQuantity * CompItem."Rolled-up Subcontracted Cost";
                  Item."Single-Level Material Cost" += ComponentQuantity * CompItem."Standard Cost"
                end else begin
                  Item."Rolled-up Material Cost" += ComponentQuantity * CompItem."Unit Cost";
                  Item."Single-Level Material Cost" += ComponentQuantity * CompItem."Unit Cost"
                end
              end;
            BOMComp.Type::Resource:
              begin
                LotSize := 1;
                if BOMComp."Resource Usage Type" = BOMComp."Resource Usage Type"::Fixed then
                  if Item."Lot Size" <> 0 then
                    LotSize := Item."Lot Size";

                GetResCost(BOMComp."No.",TempResCost);
                Res.GET(BOMComp."No.");
                ComponentQuantity :=
                  BOMComp."Quantity per" *
                  UOMMgt.GetResQtyPerUnitOfMeasure(Res,BOMComp."Unit of Measure Code") /
                  LotSize;
                Item."Single-Level Capacity Cost" += ComponentQuantity * TempResCost."Direct Unit Cost";
                Item."Single-Level Cap. Ovhd Cost" += ComponentQuantity * (TempResCost."Unit Cost" - TempResCost."Direct Unit Cost");
              end;
          end;
        until BOMComp.NEXT = 0;

        Item."Single-Level Mfg. Ovhd Cost" :=
          ROUND(
            (Item."Single-Level Material Cost" +
             Item."Single-Level Capacity Cost" +
             Item."Single-Level Cap. Ovhd Cost") * Item."Indirect Cost %" / 100 +
            Item."Overhead Rate",
            GLSetup."Unit-Amount Rounding Precision");
        Item."Rolled-up Material Cost" :=
          ROUND(
            Item."Rolled-up Material Cost",
            GLSetup."Unit-Amount Rounding Precision");
        Item."Rolled-up Capacity Cost" :=
          ROUND(
            Item."Rolled-up Capacity Cost" + Item."Single-Level Capacity Cost",
            GLSetup."Unit-Amount Rounding Precision");
        Item."Rolled-up Cap. Overhead Cost" :=
          ROUND(
            Item."Rolled-up Cap. Overhead Cost" + Item."Single-Level Cap. Ovhd Cost",
            GLSetup."Unit-Amount Rounding Precision");
        Item."Rolled-up Mfg. Ovhd Cost" :=
          ROUND(
            Item."Rolled-up Mfg. Ovhd Cost" + Item."Single-Level Mfg. Ovhd Cost",
            GLSetup."Unit-Amount Rounding Precision");
        Item."Rolled-up Subcontracted Cost" :=
          ROUND(
            Item."Rolled-up Subcontracted Cost",
            GLSetup."Unit-Amount Rounding Precision");
        Item."Standard Cost" :=
          ROUND(
            Item."Single-Level Material Cost" +
            Item."Single-Level Capacity Cost" +
            Item."Single-Level Cap. Ovhd Cost" +
            Item."Single-Level Mfg. Ovhd Cost" +
            Item."Single-Level Subcontrd. Cost",
            GLSetup."Unit-Amount Rounding Precision");
        Item."Single-Level Capacity Cost" :=
          ROUND(
            Item."Single-Level Capacity Cost",
            GLSetup."Unit-Amount Rounding Precision");
        Item."Single-Level Cap. Ovhd Cost" :=
          ROUND(
            Item."Single-Level Cap. Ovhd Cost",
            GLSetup."Unit-Amount Rounding Precision");

        Item."Last Unit Cost Calc. Date" := CalculationDate;

        TempItem := Item;
        TempItem.INSERT
      end
    END;

    [External]
    PROCEDURE CalcAssemblyItemPrice@15(ItemNo@1001 : Code[20]);
    VAR
      Item@1002 : Record Item;
      Instruction@1000 : Text[1024];
      Depth@1004 : Integer;
      NewCalcMultiLevel@1003 : Boolean;
      AssemblyContainsProdBOM@1005 : Boolean;
    BEGIN
      Item.GET(ItemNo);
      Instruction := PrepareAssemblyCalculation(Item,Depth,2,AssemblyContainsProdBOM); // 2=UnitPrice
      if Depth > 1 then
        case STRMENU(Text001,1,Instruction) of
          0:
            exit;
          1:
            NewCalcMultiLevel := false;
          2:
            NewCalcMultiLevel := true;
        end;

      SetProperties(WORKDATE,NewCalcMultiLevel,true,false,'',false);

      Item.GET(ItemNo);
      DoCalcAssemblyItemPrice(Item,0);
    END;

    LOCAL PROCEDURE DoCalcAssemblyItemPrice@16(VAR Item@1001 : Record Item;Level@1002 : Integer);
    VAR
      BOMComp@1004 : Record "BOM Component";
      CompItem@1005 : Record Item;
      CompResource@1000 : Record Resource;
      UnitPrice@1007 : Decimal;
    BEGIN
      if Level > MaxLevel then
        ERROR(Text000,MaxLevel);

      if not CalcMultiLevel and (Level <> 0) then
        exit;

      if not Item.IsAssemblyItem then
        exit;

      BOMComp.SETRANGE("Parent Item No.",Item."No.");
      if BOMComp.FIND('-') then begin
        repeat
          case BOMComp.Type of
            BOMComp.Type::Item:
              if CompItem.GET(BOMComp."No.")then begin
                DoCalcAssemblyItemPrice(CompItem,Level + 1);
                UnitPrice +=
                  BOMComp."Quantity per" *
                  UOMMgt.GetQtyPerUnitOfMeasure(CompItem,BOMComp."Unit of Measure Code") *
                  CompItem."Unit Price";
              end;
            BOMComp.Type::Resource:
              if CompResource.GET(BOMComp."No.") then
                UnitPrice +=
                  BOMComp."Quantity per" *
                  UOMMgt.GetResQtyPerUnitOfMeasure(CompResource,BOMComp."Unit of Measure Code") *
                  CompResource."Unit Price";
          end
        until BOMComp.NEXT = 0;
        UnitPrice := ROUND(UnitPrice,GLSetup."Unit-Amount Rounding Precision");
        Item.VALIDATE("Unit Price",UnitPrice);
        Item.MODIFY(true)
      end;
    END;

    LOCAL PROCEDURE CalcMfgItem@2(ItemNo@1000 : Code[20];VAR Item@1001 : Record Item;Level@1002 : Integer);
    VAR
      LotSize@1003 : Decimal;
      MfgItemQtyBase@1004 : Decimal;
      SLMat@1005 : Decimal;
      SLCap@1006 : Decimal;
      SLSub@1007 : Decimal;
      SLCapOvhd@1008 : Decimal;
      SLMfgOvhd@1009 : Decimal;
      RUMat@1010 : Decimal;
      RUCap@1011 : Decimal;
      RUSub@1012 : Decimal;
      RUCapOvhd@1013 : Decimal;
      RUMfgOvhd@1014 : Decimal;
    BEGIN
      if Level > MaxLevel then
        ERROR(Text000,MaxLevel);

      if GetItem(ItemNo,Item) then
        exit;

      if not CalcMultiLevel and (Level <> 0) then
        exit;

      with Item do begin
        LotSize := 1;

        if IsMfgItem then begin
          if "Lot Size" <> 0 then
            LotSize := "Lot Size";
          MfgItemQtyBase := CostCalcMgt.CalcQtyAdjdForBOMScrap(LotSize,"Scrap %");
          CalcRtngCost("Routing No.",MfgItemQtyBase,SLCap,SLSub,SLCapOvhd);
          CalcProdBOMCost(
            Item,"Production BOM No.","Routing No.",
            MfgItemQtyBase,true,Level,SLMat,RUMat,RUCap,RUSub,RUCapOvhd,RUMfgOvhd);
          SLMfgOvhd :=
            CostCalcMgt.CalcOvhdCost(
              SLMat + SLCap + SLSub + SLCapOvhd,
              "Indirect Cost %","Overhead Rate",LotSize);
          "Last Unit Cost Calc. Date" := CalculationDate;
        end else
          if IsAssemblyItem then begin
            CalcAssemblyItem(ItemNo,Item,Level,true);
            exit
          end else begin
            SLMat := "Unit Cost";
            RUMat := "Unit Cost";
          end;

        "Single-Level Material Cost" := CalcCostPerUnit(SLMat,LotSize);
        "Single-Level Capacity Cost" := CalcCostPerUnit(SLCap,LotSize);
        "Single-Level Subcontrd. Cost" := CalcCostPerUnit(SLSub,LotSize);
        "Single-Level Cap. Ovhd Cost" := CalcCostPerUnit(SLCapOvhd,LotSize);
        "Single-Level Mfg. Ovhd Cost" := CalcCostPerUnit(SLMfgOvhd,LotSize);
        "Rolled-up Material Cost" := CalcCostPerUnit(RUMat,LotSize);
        "Rolled-up Capacity Cost" := CalcCostPerUnit(RUCap + SLCap,LotSize);
        "Rolled-up Subcontracted Cost" := CalcCostPerUnit(RUSub + SLSub,LotSize);
        "Rolled-up Cap. Overhead Cost" := CalcCostPerUnit(RUCapOvhd + SLCapOvhd,LotSize);
        "Rolled-up Mfg. Ovhd Cost" := CalcCostPerUnit(RUMfgOvhd + SLMfgOvhd,LotSize);
        "Standard Cost" :=
          "Single-Level Material Cost" +
          "Single-Level Capacity Cost" +
          "Single-Level Subcontrd. Cost" +
          "Single-Level Cap. Ovhd Cost" +
          "Single-Level Mfg. Ovhd Cost";
      end;

      TempItem := Item;
      TempItem.INSERT;
    END;

    LOCAL PROCEDURE SetProdBOMFilters@20(VAR ProdBOMLine@1014 : Record "Production BOM Line";VAR PBOMVersionCode@1002 : Code[20];ProdBOMNo@1001 : Code[20]);
    VAR
      ProdBOMHeader@1013 : Record "Production BOM Header";
    BEGIN
      PBOMVersionCode :=
        VersionMgt.GetBOMVersion(ProdBOMNo,CalculationDate,true);
      if PBOMVersionCode = '' then begin
        ProdBOMHeader.GET(ProdBOMNo);
        TestBOMVersionIsCertified(PBOMVersionCode,ProdBOMHeader);
      end;

      with ProdBOMLine do begin
        SETRANGE("Production BOM No.",ProdBOMNo);
        SETRANGE("Version Code",PBOMVersionCode);
        SETFILTER("Starting Date",'%1|..%2',0D,CalculationDate);
        SETFILTER("Ending Date",'%1|%2..',0D,CalculationDate);
        SETFILTER("No.",'<>%1','')
      end
    END;

    LOCAL PROCEDURE CalcProdBOMCost@6(MfgItem@1000 : Record Item;ProdBOMNo@1001 : Code[20];RtngNo@1002 : Code[20];MfgItemQtyBase@1003 : Decimal;IsTypeItem@1004 : Boolean;Level@1005 : Integer;VAR SLMat@1006 : Decimal;VAR RUMat@1007 : Decimal;VAR RUCap@1008 : Decimal;VAR RUSub@1009 : Decimal;VAR RUCapOvhd@1010 : Decimal;VAR RUMfgOvhd@1011 : Decimal);
    VAR
      CompItem@1012 : Record Item;
      ProdBOMLine@1014 : Record "Production BOM Line";
      CompItemQtyBase@1015 : Decimal;
      UOMFactor@1018 : Decimal;
      PBOMVersionCode@1016 : Code[10];
    BEGIN
      if ProdBOMNo = '' then
        exit;

      SetProdBOMFilters(ProdBOMLine,PBOMVersionCode,ProdBOMNo);

      if IsTypeItem then
        UOMFactor := UOMMgt.GetQtyPerUnitOfMeasure(MfgItem,VersionMgt.GetBOMUnitOfMeasure(ProdBOMNo,PBOMVersionCode))
      else
        UOMFactor := 1;

      with ProdBOMLine do begin
        if FIND('-') then
          repeat
            CompItemQtyBase :=
              CostCalcMgt.CalcCompItemQtyBase(ProdBOMLine,CalculationDate,MfgItemQtyBase,RtngNo,IsTypeItem) /
              UOMFactor;
            case Type of
              Type::Item:
                begin
                  CalcMfgItem("No.",CompItem,Level + 1);
                  IncrCost(SLMat,CompItem."Standard Cost",CompItemQtyBase);
                  IncrCost(RUMat,CompItem."Rolled-up Material Cost",CompItemQtyBase);
                  IncrCost(RUCap,CompItem."Rolled-up Capacity Cost",CompItemQtyBase);
                  IncrCost(RUSub,CompItem."Rolled-up Subcontracted Cost",CompItemQtyBase);
                  IncrCost(RUCapOvhd,CompItem."Rolled-up Cap. Overhead Cost",CompItemQtyBase);
                  IncrCost(RUMfgOvhd,CompItem."Rolled-up Mfg. Ovhd Cost",CompItemQtyBase);
                end;
              Type::"Production BOM":
                CalcProdBOMCost(
                  MfgItem,"No.",RtngNo,CompItemQtyBase,false,Level,SLMat,RUMat,RUCap,RUSub,RUCapOvhd,RUMfgOvhd);
            end;
          until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CalcRtngCost@3(RtngHeaderNo@1000 : Code[20];MfgItemQtyBase@1001 : Decimal;VAR SLCap@1002 : Decimal;VAR SLSub@1003 : Decimal;VAR SLCapOvhd@1004 : Decimal);
    VAR
      RtngLine@1007 : Record "Routing Line";
      RtngHeader@1005 : Record "Routing Header";
    BEGIN
      if RtngLine.CertifiedRoutingVersionExists(RtngHeaderNo,CalculationDate) then begin
        if RtngLine."Version Code" = '' then begin
          RtngHeader.GET(RtngHeaderNo);
          TestRtngVersionIsCertified(RtngLine."Version Code",RtngHeader);
        end;

        repeat
          CalcRtngLineCost(RtngLine,MfgItemQtyBase,SLCap,SLSub,SLCapOvhd);
        until RtngLine.NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CalcRtngCostPerUnit@9(Type@1000 : '"Work Center","Machine Center"," "';No@1001 : Code[20];VAR DirUnitCost@1002 : Decimal;VAR IndirCostPct@1003 : Decimal;VAR OvhdRate@1004 : Decimal;VAR UnitCost@1005 : Decimal;VAR UnitCostCalculation@1006 : 'Time,Unit');
    VAR
      WorkCenter@1007 : Record "Work Center";
      MachineCenter@1008 : Record "Machine Center";
    BEGIN
      case Type of
        Type::"Work Center":
          GetWorkCenter(No,WorkCenter);
        Type::"Machine Center":
          GetMachineCenter(No,MachineCenter);
      end;
      CostCalcMgt.RoutingCostPerUnit2(Type,DirUnitCost,IndirCostPct,OvhdRate,UnitCost,UnitCostCalculation,WorkCenter,MachineCenter);
    END;

    LOCAL PROCEDURE CalcCostPerUnit@10(CostPerLot@1000 : Decimal;LotSize@1001 : Decimal) : Decimal;
    BEGIN
      exit(ROUND(CostPerLot / LotSize,GLSetup."Unit-Amount Rounding Precision"));
    END;

    LOCAL PROCEDURE TestBOMVersionIsCertified@14(BOMVersionCode@1002 : Code[20];ProdBOMHeader@1001 : Record "Production BOM Header");
    BEGIN
      if BOMVersionCode = '' then begin
        if ProdBOMHeader.Status <> ProdBOMHeader.Status::Certified then
          if LogErrors then
            InsertInErrBuf(ProdBOMHeader."No.",'',false)
          else
            ProdBOMHeader.TESTFIELD(Status,ProdBOMHeader.Status::Certified);
      end;
    END;

    LOCAL PROCEDURE InsertInErrBuf@23(No@1000 : Code[20];Version@1001 : Code[10];IsRtng@1002 : Boolean);
    BEGIN
      if not LogErrors then
        exit;

      if IsRtng then begin
        RtngVersionErrBuf."Routing No." := No;
        RtngVersionErrBuf."Version Code" := Version;
        if RtngVersionErrBuf.INSERT then;
      end else begin
        ProdBOMVersionErrBuf."Production BOM No." := No;
        ProdBOMVersionErrBuf."Version Code" := Version;
        if ProdBOMVersionErrBuf.INSERT then;
      end;
    END;

    LOCAL PROCEDURE GetItem@22(ItemNo@1001 : Code[20];VAR Item@1002 : Record Item) IsInBuffer@1000 : Boolean;
    VAR
      StdCostWksh@1003 : Record "Standard Cost Worksheet";
    BEGIN
      if TempItem.GET(ItemNo) then begin
        Item := TempItem;
        IsInBuffer := true;
      end else begin
        Item.GET(ItemNo);
        if (StdCostWkshName <> '') and
           not (Item.IsMfgItem or Item.IsAssemblyItem)
        then begin
          if StdCostWksh.GET(StdCostWkshName,StdCostWksh.Type::Item,ItemNo) then begin
            Item."Unit Cost" := StdCostWksh."New Standard Cost";
            Item."Standard Cost" := StdCostWksh."New Standard Cost";
            Item."Indirect Cost %" := StdCostWksh."New Indirect Cost %";
            Item."Overhead Rate" := StdCostWksh."New Overhead Rate";
          end;
        end;
        IsInBuffer := false;
      end;
    END;

    LOCAL PROCEDURE GetWorkCenter@11(No@1001 : Code[20];VAR WorkCenter@1002 : Record "Work Center");
    VAR
      StdCostWksh@1003 : Record "Standard Cost Worksheet";
    BEGIN
      if TempWorkCenter.GET(No) then
        WorkCenter := TempWorkCenter
      else begin
        WorkCenter.GET(No);
        if StdCostWkshName <> '' then begin
          if StdCostWksh.GET(StdCostWkshName,StdCostWksh.Type::"Work Center",No) then begin
            WorkCenter."Unit Cost" := StdCostWksh."New Standard Cost";
            WorkCenter."Indirect Cost %" := StdCostWksh."New Indirect Cost %";
            WorkCenter."Overhead Rate" := StdCostWksh."New Overhead Rate";
            WorkCenter."Direct Unit Cost" :=
              CostCalcMgt.CalcDirUnitCost(
                StdCostWksh."New Standard Cost",StdCostWksh."New Overhead Rate",StdCostWksh."New Indirect Cost %");
          end;
        end;
        TempWorkCenter := WorkCenter;
        TempWorkCenter.INSERT;
      end;
    END;

    LOCAL PROCEDURE GetMachineCenter@13(No@1001 : Code[20];VAR MachineCenter@1002 : Record "Machine Center");
    VAR
      StdCostWksh@1003 : Record "Standard Cost Worksheet";
    BEGIN
      if TempMachineCenter.GET(No) then
        MachineCenter := TempMachineCenter
      else begin
        MachineCenter.GET(No);
        if StdCostWkshName <> '' then begin
          if StdCostWksh.GET(StdCostWkshName,StdCostWksh.Type::"Machine Center",No) then begin
            MachineCenter."Unit Cost" := StdCostWksh."New Standard Cost";
            MachineCenter."Indirect Cost %" := StdCostWksh."New Indirect Cost %";
            MachineCenter."Overhead Rate" := StdCostWksh."New Overhead Rate";
            MachineCenter."Direct Unit Cost" :=
              CostCalcMgt.CalcDirUnitCost(
                StdCostWksh."New Standard Cost",StdCostWksh."New Overhead Rate",StdCostWksh."New Indirect Cost %");
          end;
        end;
        TempMachineCenter := MachineCenter;
        TempMachineCenter.INSERT;
      end;
    END;

    LOCAL PROCEDURE GetResCost@25(No@1001 : Code[20];VAR ResCost@1002 : Record "Resource Cost");
    VAR
      StdCostWksh@1003 : Record "Standard Cost Worksheet";
    BEGIN
      if TempResCost.GET(TempResCost.Type::Resource,No) then
        ResCost := TempResCost
      else begin
        ResCost.INIT;
        ResCost.Code := No;
        ResCost."Work Type Code" := '';
        CODEUNIT.RUN(CODEUNIT::"Resource-Find Cost",ResCost);

        if StdCostWkshName <> '' then begin
          if StdCostWksh.GET(StdCostWkshName,StdCostWksh.Type::Resource,No) then begin
            ResCost."Unit Cost" := StdCostWksh."New Standard Cost";
            ResCost."Direct Unit Cost" :=
              CostCalcMgt.CalcDirUnitCost(
                StdCostWksh."New Standard Cost",
                StdCostWksh."New Overhead Rate",
                StdCostWksh."New Indirect Cost %");
          end;
        end;
        TempResCost := ResCost;
        TempResCost.INSERT;
      end;
    END;

    LOCAL PROCEDURE IncrCost@8(VAR Cost@1000 : Decimal;UnitCost@1001 : Decimal;Qty@1002 : Decimal);
    BEGIN
      Cost := Cost + (Qty * UnitCost);
    END;

    [External]
    PROCEDURE CalculateAssemblyCostExp@19(AssemblyHeader@1000 : Record "Assembly Header";VAR ExpCost@1002 : ARRAY [5] OF Decimal);
    BEGIN
      GLSetup.GET;

      ExpCost[RowIdx::AsmOvhd] :=
        ROUND(
          CalcOverHeadAmt(
            AssemblyHeader.CalcTotalCost(ExpCost),
            AssemblyHeader."Indirect Cost %",
            AssemblyHeader."Overhead Rate" * AssemblyHeader.Quantity),
          GLSetup."Unit-Amount Rounding Precision");
    END;

    LOCAL PROCEDURE CalculateAssemblyCostStd@21(ItemNo@1000 : Code[20];QtyBase@1001 : Decimal;VAR StdCost@1007 : ARRAY [5] OF Decimal);
    VAR
      Item@1002 : Record Item;
      StdTotalCost@1006 : Decimal;
    BEGIN
      GLSetup.GET;

      Item.GET(ItemNo);
      StdCost[RowIdx::MatCost] :=
        ROUND(
          Item."Single-Level Material Cost" * QtyBase,
          GLSetup."Unit-Amount Rounding Precision");
      StdCost[RowIdx::ResCost] :=
        ROUND(
          Item."Single-Level Capacity Cost" * QtyBase,
          GLSetup."Unit-Amount Rounding Precision");
      StdCost[RowIdx::ResOvhd] :=
        ROUND(
          Item."Single-Level Cap. Ovhd Cost" * QtyBase,
          GLSetup."Unit-Amount Rounding Precision");
      StdTotalCost := StdCost[RowIdx::MatCost] + StdCost[RowIdx::ResCost] + StdCost[RowIdx::ResOvhd];
      StdCost[RowIdx::AsmOvhd] :=
        ROUND(
          CalcOverHeadAmt(
            StdTotalCost,
            Item."Indirect Cost %",
            Item."Overhead Rate" * QtyBase),
          GLSetup."Unit-Amount Rounding Precision");
    END;

    [External]
    PROCEDURE CalcOverHeadAmt@27(CostAmt@1000 : Decimal;IndirectCostPct@1001 : Decimal;OverheadRateAmt@1002 : Decimal) : Decimal;
    BEGIN
      exit(CostAmt * IndirectCostPct / 100 + OverheadRateAmt);
    END;

    LOCAL PROCEDURE CalculatePostedAssemblyCostExp@24(PostedAssemblyHeader@1000 : Record "Posted Assembly Header";VAR ExpCost@1002 : ARRAY [5] OF Decimal);
    BEGIN
      GLSetup.GET;

      ExpCost[RowIdx::AsmOvhd] :=
        ROUND(
          CalcOverHeadAmt(
            PostedAssemblyHeader.CalcTotalCost(ExpCost),
            PostedAssemblyHeader."Indirect Cost %",
            PostedAssemblyHeader."Overhead Rate" * PostedAssemblyHeader.Quantity),
          GLSetup."Unit-Amount Rounding Precision");
    END;

    LOCAL PROCEDURE CalcTotalAndVar@30(VAR Value@1001 : ARRAY [5,5] OF Decimal);
    BEGIN
      CalcTotal(Value);
      CalcVariance(Value);
    END;

    LOCAL PROCEDURE CalcTotal@29(VAR Value@1001 : ARRAY [5,5] OF Decimal);
    VAR
      RowId@1002 : Integer;
      ColId@1000 : Integer;
    BEGIN
      for ColId := 1 to 3 do begin
        Value[ColId,5] := 0;
        for RowId := 1 to 4 do
          Value[ColId,5] += Value[ColId,RowId];
      end;
    END;

    LOCAL PROCEDURE CalcVariance@28(VAR Value@1001 : ARRAY [5,5] OF Decimal);
    VAR
      i@1000 : Integer;
    BEGIN
      for i := 1 to 5 do begin
        Value[ColIdx::Dev,i] := CalcIndicatorPct(Value[ColIdx::StdCost,i],Value[ColIdx::ActCost,i]);
        Value[ColIdx::"Var",i] := Value[ColIdx::ActCost,i] - Value[ColIdx::StdCost,i];
      end;
    END;

    LOCAL PROCEDURE CalcIndicatorPct@26(Value@1000 : Decimal;Sum@1001 : Decimal) : Decimal;
    BEGIN
      if Value = 0 then
        exit(0);

      exit(ROUND((Sum - Value) / Value * 100,1));
    END;

    [External]
    PROCEDURE CalcAsmOrderStatistics@33(AssemblyHeader@1000 : Record "Assembly Header";VAR Value@1001 : ARRAY [5,5] OF Decimal);
    BEGIN
      CalculateAssemblyCostStd(
        AssemblyHeader."Item No.",
        AssemblyHeader."Quantity (Base)",
        Value[ColIdx::StdCost]);
      CalculateAssemblyCostExp(AssemblyHeader,Value[ColIdx::ExpCost]);
      AssemblyHeader.CalcActualCosts(Value[ColIdx::ActCost]);
      CalcTotalAndVar(Value);
    END;

    [External]
    PROCEDURE CalcPostedAsmOrderStatistics@34(PostedAssemblyHeader@1000 : Record "Posted Assembly Header";VAR Value@1001 : ARRAY [5,5] OF Decimal);
    BEGIN
      CalculateAssemblyCostStd(
        PostedAssemblyHeader."Item No.",
        PostedAssemblyHeader."Quantity (Base)",
        Value[ColIdx::StdCost]);
      CalculatePostedAssemblyCostExp(PostedAssemblyHeader,Value[ColIdx::ExpCost]);
      PostedAssemblyHeader.CalcActualCosts(Value[ColIdx::ActCost]);
      CalcTotalAndVar(Value);
    END;

    [External]
    PROCEDURE CalcRtngLineCost@35(RtngLine@1000 : Record "Routing Line";MfgItemQtyBase@1009 : Decimal;VAR SLCap@1012 : Decimal;VAR SLSub@1011 : Decimal;VAR SLCapOvhd@1010 : Decimal);
    VAR
      WorkCenter@1002 : Record "Work Center";
      CostCalculationMgt@1013 : Codeunit "Cost Calculation Management";
      UnitCost@1008 : Decimal;
      DirUnitCost@1007 : Decimal;
      IndirCostPct@1006 : Decimal;
      OvhdRate@1005 : Decimal;
      CostTime@1004 : Decimal;
      UnitCostCalculation@1003 : Option;
    BEGIN
      with RtngLine do begin
        if (Type = Type::"Work Center") and ("No." <> '') then
          WorkCenter.GET("No.");

        UnitCost := "Unit Cost per";
        CalcRtngCostPerUnit(Type,"No.",DirUnitCost,IndirCostPct,OvhdRate,UnitCost,UnitCostCalculation);
        CostTime :=
          CostCalculationMgt.CalcCostTime(
            MfgItemQtyBase,
            "Setup Time","Setup Time Unit of Meas. Code",
            "Run Time","Run Time Unit of Meas. Code","Lot Size",
            "Scrap Factor % (Accumulated)","Fixed Scrap Qty. (Accum.)",
            "Work Center No.",UnitCostCalculation,MfgSetup."Cost Incl. Setup",
            "Concurrent Capacities");

        if (Type = Type::"Work Center") and (WorkCenter."Subcontractor No." <> '') then
          IncrCost(SLSub,DirUnitCost,CostTime)
        else
          IncrCost(SLCap,DirUnitCost,CostTime);
        IncrCost(SLCapOvhd,CostCalcMgt.CalcOvhdCost(DirUnitCost,IndirCostPct,OvhdRate,1),CostTime);
      end;
    END;

    LOCAL PROCEDURE TestRtngVersionIsCertified@32(RtngVersionCode@1002 : Code[20];RtngHeader@1001 : Record "Routing Header");
    BEGIN
      if RtngVersionCode = '' then begin
        if RtngHeader.Status <> RtngHeader.Status::Certified then
          if LogErrors then
            InsertInErrBuf(RtngHeader."No.",'',true)
          else
            RtngHeader.TESTFIELD(Status,RtngHeader.Status::Certified);
      end;
    END;

    BEGIN
    END.
  }
}

