OBJECT Report 99 Delete Empty G/L Registers
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData "G/L Register"=rimd;
    CaptionML=ENU=Delete Empty G/L Registers;
    ProcessingOnly=true;
  }
  DATASET
  {
    { 9922;    ;DataItem;G/L Register        ;
               DataItemTable="G/L Register";
               DataItemTableView=SORTING("No.");
               OnPreDataItem=BEGIN
                               if not CONFIRM(Text000,false) then
                                 CurrReport.BREAK;

                               Window.OPEN(
                                 Text001 +
                                 Text002 +
                                 Text003 +
                                 Text004);
                             END;

               OnAfterGetRecord=BEGIN
                                  GLEntry.SETRANGE("Entry No.","From Entry No.","To Entry No.");
                                  if GLEntry.FINDFIRST then
                                    CurrReport.SKIP;
                                  CustLedgEntry.SETRANGE("Entry No.","From Entry No.","To Entry No.");
                                  if CustLedgEntry.FINDFIRST then
                                    CurrReport.SKIP;
                                  VendLedgEntry.SETRANGE("Entry No.","From Entry No.","To Entry No.");
                                  if VendLedgEntry.FINDFIRST then
                                    CurrReport.SKIP;
                                  VATEntry.SETRANGE("Entry No.","From VAT Entry No.","To VAT Entry No.");
                                  if VATEntry.FINDFIRST then
                                    CurrReport.SKIP;
                                  BankAccLedgEntry.SETRANGE("Entry No.","From Entry No.","To Entry No.");
                                  if BankAccLedgEntry.FINDFIRST then
                                    CurrReport.SKIP;
                                  FAReg.SETCURRENTKEY("Creation Date");
                                  FAReg.SETRANGE("Creation Date","Creation Date");
                                  FAReg.SETRANGE("G/L Register No.","No.");
                                  if FAReg.FINDFIRST then begin
                                    FALedgEntry.SETRANGE("Entry No.",FAReg."From Entry No.",FAReg."To Entry No.");
                                    if FALedgEntry.FINDFIRST then
                                      CurrReport.SKIP;
                                    MaintLedgEntry.SETRANGE("Entry No.",FAReg."From Maintenance Entry No.",FAReg."To Maintenance Entry No.");
                                    if MaintLedgEntry.FINDFIRST then
                                      CurrReport.SKIP;
                                  end;

                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,"Creation Date");
                                  DELETE;
                                  NoOfDeleted := NoOfDeleted + 1;
                                  Window.UPDATE(3,NoOfDeleted);
                                  if NoOfDeleted >= NoOfDeleted2 + 10 then begin
                                    NoOfDeleted2 := NoOfDeleted;
                                    COMMIT;
                                  end;
                                END;

               ReqFilterFields="Creation Date" }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to delete the registers?';
      Text001@1001 : TextConst 'ENU=Deleting empty G/L registers...\\';
      Text002@1002 : TextConst 'ENU=No.                      #1######\';
      Text003@1003 : TextConst 'ENU=Posted on                #2######\\';
      Text004@1004 : TextConst 'ENU=No. of registers deleted #3######';
      GLEntry@1005 : Record "G/L Entry";
      CustLedgEntry@1006 : Record "Cust. Ledger Entry";
      VendLedgEntry@1007 : Record "Vendor Ledger Entry";
      VATEntry@1008 : Record "VAT Entry";
      BankAccLedgEntry@1009 : Record "Bank Account Ledger Entry";
      FAReg@1013 : Record "FA Register";
      FALedgEntry@1014 : Record "FA Ledger Entry";
      MaintLedgEntry@1015 : Record "Maintenance Ledger Entry";
      Window@1010 : Dialog;
      NoOfDeleted@1011 : Integer;
      NoOfDeleted2@1012 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

