OBJECT Codeunit 211 Res. Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Res. Journal Line";
    OnRun=BEGIN
            GLSetup.GET;
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be a closing date';
      Text001@1001 : TextConst 'ENU=is not within your range of allowed posting dates';
      Text002@1002 : TextConst 'ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5';
      Text003@1003 : TextConst 'ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5';
      GLSetup@1004 : Record "General Ledger Setup";
      UserSetup@1005 : Record "User Setup";
      DimMgt@1007 : Codeunit DimensionManagement;
      TimeSheetMgt@1006 : Codeunit "Time Sheet Management";
      AllowPostingFrom@1008 : Date;
      AllowPostingTo@1009 : Date;

    [External]
    PROCEDURE RunCheck@1(VAR ResJnlLine@1000 : Record "Res. Journal Line");
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      with ResJnlLine do begin
        if EmptyLine then
          exit;

        TESTFIELD("Resource No.");
        TESTFIELD("Posting Date");
        TESTFIELD("Gen. Prod. Posting Group");

        if "Posting Date" <> NORMALDATE("Posting Date") then
          FIELDERROR("Posting Date",Text000);

        if (AllowPostingFrom = 0D) and (AllowPostingTo = 0D) then begin
          if USERID <> '' then
            if UserSetup.GET(USERID) then begin
              AllowPostingFrom := UserSetup."Allow Posting From";
              AllowPostingTo := UserSetup."Allow Posting To";
            end;
          if (AllowPostingFrom = 0D) and (AllowPostingTo = 0D) then begin
            GLSetup.GET;
            AllowPostingFrom := GLSetup."Allow Posting From";
            AllowPostingTo := GLSetup."Allow Posting To";
          end;
          if AllowPostingTo = 0D then
            AllowPostingTo := DMY2DATE(31,12,9999);
        end;
        if ("Posting Date" < AllowPostingFrom) or ("Posting Date" > AllowPostingTo) then
          FIELDERROR("Posting Date",Text001);

        if "Document Date" <> 0D then
          if "Document Date" <> NORMALDATE("Document Date") then
            FIELDERROR("Document Date",Text000);

        if ("Entry Type" = "Entry Type"::Usage) and ("Time Sheet No." <> '') then
          TimeSheetMgt.CheckResJnlLine(ResJnlLine);

        if not DimMgt.CheckDimIDComb("Dimension Set ID") then
          ERROR(
            Text002,
            TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
            DimMgt.GetDimCombErr);

        TableID[1] := DATABASE::Resource;
        No[1] := "Resource No.";
        TableID[2] := DATABASE::"Resource Group";
        No[2] := "Resource Group No.";
        TableID[3] := DATABASE::Job;
        No[3] := "Job No.";
        if not DimMgt.CheckDimValuePosting(TableID,No,"Dimension Set ID") then
          if "Line No." <> 0 then
            ERROR(
              Text003,
              TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
              DimMgt.GetDimValuePostingErr)
          else
            ERROR(DimMgt.GetDimValuePostingErr);
      end;
    END;

    BEGIN
    END.
  }
}

