OBJECT Codeunit 884 ReadSoft OCR Master Data Sync
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SyncVendorsUriTxt@1020 : TextConst '@@@={Locked};ENU=masterdata/rest/%1/suppliers';
      SyncVendorBankAccountsUriTxt@1019 : TextConst '@@@={Locked};ENU=masterdata/rest/%1/supplierbankaccounts';
      SyncModifiedVendorsMsg@1017 : TextConst 'ENU=Send updated vendors to the OCR service.';
      SyncBankAccountsMsg@1016 : TextConst 'ENU=Send vendor bank accounts to the OCR service.';
      SyncSuccessfulSimpleMsg@1003 : TextConst 'ENU=Synchronization succeeded.';
      SyncSuccessfulDetailedMsg@1015 : TextConst '@@@=%1 number of created entities, %2 number of updated entities, %3 number of deleted entities;ENU=Synchronization succeeded. Created: %1, Updated: %2, Deleted: %3';
      SyncFailedSimpleMsg@1014 : TextConst 'ENU=Synchronization failed.';
      SyncFailedDetailedMsg@1013 : TextConst '@@@=%1 error code, %2 error message;ENU=Synchronization failed. Code: %1, Message: %2';
      InvalidResponseMsg@1012 : TextConst 'ENU=Response is invalid.';
      MasterDataSyncMsg@1006 : TextConst '@@@="#1 place holder for SendingPackageMsg ";ENU=Master data synchronization.\#1########################################';
      SendingPackageMsg@1005 : TextConst '@@@=%1 package number, %2 package count;ENU=Sending package %1 of %2';
      MaxPortionSizeTxt@1004 : TextConst '@@@={Locked};ENU=3000';
      MethodPutTok@1002 : TextConst '@@@={Locked};ENU=PUT';
      MethodPostTok@1001 : TextConst '@@@={Locked};ENU=POST';
      OCRServiceSetup@1021 : Record "OCR Service Setup";
      OCRServiceMgt@1022 : Codeunit "OCR Service Mgt.";
      Window@1011 : Dialog;
      WindowUpdateDateTime@1010 : DateTime;
      OrganizationId@1023 : Text;
      PackageNo@1009 : Integer;
      PackageCount@1008 : Integer;
      MaxPortionSizeValue@1007 : Integer;

    [External]
    PROCEDURE SyncMasterData@3(Resync@1000 : Boolean;Silent@1001 : Boolean) : Boolean;
    VAR
      LastSyncTime@1004 : DateTime;
      SyncStartTime@1009 : DateTime;
    BEGIN
      OCRServiceMgt.GetOcrServiceSetupExtended(OCRServiceSetup,true);
      OCRServiceSetup.TESTFIELD("Master Data Sync Enabled");

      if Resync then begin
        CLEAR(OCRServiceSetup."Master Data Last Sync");
        OCRServiceSetup.MODIFY;
        COMMIT;
      end;

      LastSyncTime := OCRServiceSetup."Master Data Last Sync";
      SyncStartTime := CURRENTDATETIME;

      if not SyncVendors(LastSyncTime,SyncStartTime) then begin
        if not Silent then
          MESSAGE(SyncFailedSimpleMsg);
        exit(false);
      end;

      OCRServiceSetup."Master Data Last Sync" := SyncStartTime;
      OCRServiceSetup.MODIFY;
      if not Silent then
        MESSAGE(SyncSuccessfulSimpleMsg);
      exit(true);
    END;

    [External]
    PROCEDURE ResetLastSyncTime@2();
    BEGIN
      if not IsSyncEnabled then
        exit;
      OCRServiceSetup.GET;
      if OCRServiceSetup."Master Data Last Sync" = 0DT then
        exit;
      CLEAR(OCRServiceSetup."Master Data Last Sync");
      OCRServiceSetup.MODIFY;
      COMMIT;
    END;

    [External]
    PROCEDURE IsSyncEnabled@1() : Boolean;
    VAR
      OCRServiceSetup@1000 : Record "OCR Service Setup";
    BEGIN
      if not OCRServiceSetup.GET then
        exit(false);

      if not OCRServiceSetup."Master Data Sync Enabled" then
        exit(false);

      if not OCRServiceSetup.Enabled then
        exit(false);

      if OCRServiceSetup."Service URL" = '' then
        exit(false);

      exit(true);
    END;

    LOCAL PROCEDURE CheckSyncResponse@104(VAR ResponseStream@1000 : InStream;ActivityDescription@1010 : Text) : Boolean;
    VAR
      XMLDOMManagement@1004 : Codeunit "XML DOM Management";
      XMLRootNode@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLNode@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      NoOfCreated@1006 : Integer;
      NoOfUpdated@1005 : Integer;
      NoOfDeleted@1001 : Integer;
      ErrorCode@1009 : Text;
      ErrorMessage@1007 : Text;
    BEGIN
      XMLDOMManagement.LoadXMLNodeFromInStream(ResponseStream,XMLRootNode);
      case XMLRootNode.Name of
        'UpdateResult':
          begin
            if XMLDOMManagement.FindNode(XMLRootNode,'Created',XMLNode) then
              EVALUATE(NoOfCreated,XMLNode.InnerText,9);
            if XMLDOMManagement.FindNode(XMLRootNode,'Updated',XMLNode) then
              EVALUATE(NoOfUpdated,XMLNode.InnerText,9);
            if XMLDOMManagement.FindNode(XMLRootNode,'Deleted',XMLNode) then
              EVALUATE(NoOfDeleted,XMLNode.InnerText,9);
            OCRServiceMgt.LogActivitySucceeded(
              OCRServiceSetup.RECORDID,ActivityDescription,STRSUBSTNO(SyncSuccessfulDetailedMsg,NoOfCreated,NoOfUpdated,NoOfDeleted));
            exit(true);
          end;
        'ServiceError':
          begin
            if XMLDOMManagement.FindNode(XMLRootNode,'Code',XMLNode) then
              ErrorCode := XMLNode.InnerText;
            if XMLDOMManagement.FindNode(XMLRootNode,'Message',XMLNode) then
              ErrorMessage := XMLNode.InnerText;
            OCRServiceMgt.LogActivityFailed(
              OCRServiceSetup.RECORDID,ActivityDescription,STRSUBSTNO(SyncFailedDetailedMsg,ErrorCode,ErrorMessage));
            exit(false);
          end;
        else begin
          OCRServiceMgt.LogActivityFailed(OCRServiceSetup.RECORDID,ActivityDescription,InvalidResponseMsg);
          exit(false);
        end;
      end;
    END;

    LOCAL PROCEDURE SyncVendors@92(StartDateTime@1001 : DateTime;EndDateTime@1000 : DateTime) : Boolean;
    VAR
      TempBlobModifiedVendor@1003 : TEMPORARY Record TempBlob;
      TempBlobBankAccount@1004 : TEMPORARY Record TempBlob;
      ModifiedVendorCount@1011 : Integer;
      BankAccountCount@1010 : Integer;
      ModifyVendorPackageCount@1008 : Integer;
      BankAccountPackageCount@1009 : Integer;
      TotalPackageCount@1005 : Integer;
      Success@1006 : Boolean;
      ModifiedVendorFirstPortionAction@1015 : Code[6];
    BEGIN
      if StartDateTime > 0DT then
        ModifiedVendorFirstPortionAction := MethodPostTok
      else
        ModifiedVendorFirstPortionAction := MethodPutTok;

      GetModifiedVendors(TempBlobModifiedVendor,StartDateTime,EndDateTime);
      GetVendorBankAccounts(TempBlobBankAccount,StartDateTime,EndDateTime);

      ModifiedVendorCount := TempBlobModifiedVendor.COUNT;
      BankAccountCount := TempBlobBankAccount.COUNT;

      if (ModifiedVendorCount > 0) or (StartDateTime = 0DT) then
        ModifyVendorPackageCount := (ModifiedVendorCount div MaxPortionSize) + 1;
      if BankAccountCount > 0 then
        BankAccountPackageCount := (TempBlobBankAccount.COUNT div MaxPortionSize) + 1;
      TotalPackageCount := ModifyVendorPackageCount + BankAccountPackageCount;

      if TotalPackageCount = 0 then
        exit(true);

      CheckOrganizationId;

      OpenWindow(TotalPackageCount);

      Success := SyncMasterDataEntities(
          TempBlobModifiedVendor,VendorsUri,ModifiedVendorFirstPortionAction,MethodPostTok,
          'Suppliers',SyncModifiedVendorsMsg,MaxPortionSize);

      if Success then
        Success := SyncMasterDataEntities(
            TempBlobBankAccount,VendorBankAccountsUri,MethodPutTok,MethodPutTok,
            'SupplierBankAccounts',SyncBankAccountsMsg,MaxPortionSize);

      CloseWindow;

      exit(Success);
    END;

    LOCAL PROCEDURE SyncMasterDataEntities@62(VAR TempBlob@1000 : TEMPORARY Record TempBlob;RequestUri@1002 : Text;FirstPortionAction@1003 : Code[6];NextPortionAction@1012 : Code[6];RootNodeName@1004 : Text;ActivityDescription@1006 : Text;PortionSize@1011 : Integer) : Boolean;
    VAR
      ResponseStream@1005 : InStream;
      EntityCount@1009 : Integer;
      PortionCount@1008 : Integer;
      PortionNumber@1007 : Integer;
      LastPortion@1010 : Boolean;
      Data@1001 : Text;
      RequestAction@1013 : Code[6];
    BEGIN
      EntityCount := TempBlob.COUNT;

      if EntityCount = 0 then begin
        if FirstPortionAction <> MethodPutTok then
          exit(true);
        PortionCount := 1;
        PortionSize := 0;
      end else begin
        PortionCount := (EntityCount div PortionSize) + 1;
        TempBlob.FINDFIRST;
      end;

      RequestAction := FirstPortionAction;
      for PortionNumber := 1 to PortionCount do begin
        UpdateWindow;
        Data := GetMasterDataEntitiesXml(TempBlob,RootNodeName,PortionSize,LastPortion);
        if not OCRServiceMgt.RsoRequest(RequestUri,RequestAction,Data,ResponseStream) then begin
          OCRServiceMgt.LogActivityFailed(OCRServiceSetup.RECORDID,ActivityDescription,SyncFailedSimpleMsg);
          exit(false);
        end;
        if not CheckSyncResponse(ResponseStream,ActivityDescription) then
          exit(false);
        if LastPortion then
          break;
        RequestAction := NextPortionAction;
      end;
      exit(true);
    END;

    LOCAL PROCEDURE GetModifiedVendors@76(VAR TempBlob@1003 : Record TempBlob;StartDateTime@1001 : DateTime;EndDateTime@1000 : DateTime);
    VAR
      OCRVendors@1002 : Query "OCR Vendors";
      Index@1005 : Integer;
      Data@1004 : Text;
    BEGIN
      OCRVendors.SETRANGE(Modified_On,StartDateTime,EndDateTime);
      if OCRVendors.OPEN then
        while OCRVendors.READ do begin
          Index += 1;
          Data := GetModifiedVendorXml(OCRVendors);
          PutToBuffer(TempBlob,Index,Data);
        end;
    END;

    LOCAL PROCEDURE GetVendorBankAccounts@74(VAR TempBlob@1003 : Record TempBlob;StartDateTime@1001 : DateTime;EndDateTime@1002 : DateTime);
    VAR
      OCRVendorBankAccounts@1000 : Query "OCR Vendor Bank Accounts";
      VendorId@1006 : GUID;
      AccountIndex@1007 : Integer;
      VendorIndex@1005 : Integer;
      Data@1004 : Text;
    BEGIN
      OCRVendorBankAccounts.SETRANGE(Modified_On,StartDateTime,EndDateTime);
      if not OCRVendorBankAccounts.OPEN then
        exit;

      while OCRVendorBankAccounts.READ do begin
        if AccountIndex = 0 then
          VendorId := OCRVendorBankAccounts.Id;
        AccountIndex += 1;
        if VendorId <> OCRVendorBankAccounts.Id then begin
          VendorIndex += 1;
          PutToBuffer(TempBlob,VendorIndex,Data);
          VendorId := OCRVendorBankAccounts.Id;
          Data := '';
        end;
        Data += GetVendorBankAccountXml(OCRVendorBankAccounts);
      end;
      VendorIndex += 1;
      PutToBuffer(TempBlob,VendorIndex,Data);
    END;

    LOCAL PROCEDURE GetMasterDataEntitiesXml@109(VAR TempBlob@1000 : Record TempBlob;RootNodeName@1002 : Text;PortionSize@1004 : Integer;VAR LastPortion@1007 : Boolean) : Text;
    VAR
      Data@1001 : Text;
      Count@1006 : Integer;
    BEGIN
      Data := '';
      if PortionSize = 0 then
        LastPortion := true
      else
        for Count := 1 to PortionSize do begin
          Data += GetFromBuffer(TempBlob);
          if TempBlob.NEXT = 0 then begin
            LastPortion := true;
            break;
          end;
        end;
      Data := STRSUBSTNO('<%1 xmlns:i="http://www.w3.org/2001/XMLSchema-instance">%2</%3>',RootNodeName,Data,RootNodeName);
      exit(Data);
    END;

    LOCAL PROCEDURE GetModifiedVendorXml@69(VAR OCRVendors@1000 : Query "OCR Vendors") : Text;
    VAR
      XMLDOMManagement@1003 : Codeunit "XML DOM Management";
      DotNetXmlDocument@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XmlNode@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XmlNodeChild@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      Blocked@1007 : Boolean;
    BEGIN
      Blocked := OCRVendors.Blocked <> OCRVendors.Blocked::" ";
      DotNetXmlDocument := DotNetXmlDocument.XmlDocument;
      XMLDOMManagement.AddRootElement(DotNetXmlDocument,'Supplier',XmlNode);

      // when using XML as the input for API, the element order needs to match exactly
      XMLDOMManagement.AddElement(XmlNode,'SupplierNumber',OCRVendors.No,'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'Name',OCRVendors.Name,'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'TaxRegistrationNumber',OCRVendors.VAT_Registration_No,'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'Street',OCRVendors.Address,'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'PostalCode',OCRVendors.Post_Code,'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'City',OCRVendors.City,'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'Blocked',FORMAT(Blocked,0,9),'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'TelephoneNumber',OCRVendors.Phone_No,'',XmlNodeChild);

      exit(DotNetXmlDocument.OuterXml);
    END;

    LOCAL PROCEDURE GetVendorBankAccountXml@68(VAR OCRVendorBankAccounts@1000 : Query "OCR Vendor Bank Accounts") : Text;
    VAR
      XMLDOMManagement@1001 : Codeunit "XML DOM Management";
      DotNetXmlDocument@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XmlNode@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XmlNodeChild@1008 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      DotNetXmlDocument := DotNetXmlDocument.XmlDocument;
      XMLDOMManagement.AddRootElement(DotNetXmlDocument,'SupplierBankAccount',XmlNode);

      // when using XML as the input for API, the element order needs to match exactly
      XMLDOMManagement.AddElement(XmlNode,'BankName',OCRVendorBankAccounts.Name,'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'SupplierNumber',OCRVendorBankAccounts.No,'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'BankNumber',OCRVendorBankAccounts.Bank_Branch_No,'',XmlNodeChild);
      XMLDOMManagement.AddElement(XmlNode,'AccountNumber',OCRVendorBankAccounts.Bank_Account_No,'',XmlNodeChild);

      exit(DotNetXmlDocument.OuterXml);
    END;

    LOCAL PROCEDURE PutToBuffer@101(VAR TempBlob@1000 : Record TempBlob;Index@1003 : Integer;Data@1001 : Text);
    VAR
      OutStream@1002 : OutStream;
    BEGIN
      TempBlob.INIT;
      TempBlob."Primary Key" := Index;
      TempBlob.Blob.CREATEOUTSTREAM(OutStream);
      OutStream.WRITETEXT(Data);
      TempBlob.INSERT;
    END;

    LOCAL PROCEDURE GetFromBuffer@102(VAR TempBlob@1000 : Record TempBlob) : Text;
    VAR
      InStream@1002 : InStream;
      Data@1001 : Text;
    BEGIN
      if TempBlob.ISEMPTY then
        exit;
      TempBlob.CALCFIELDS(Blob);
      TempBlob.Blob.CREATEINSTREAM(InStream);
      InStream.READTEXT(Data);
      exit(Data);
    END;

    LOCAL PROCEDURE VendorsUri@60() : Text;
    BEGIN
      exit(STRSUBSTNO(SyncVendorsUriTxt,OrganizationId));
    END;

    LOCAL PROCEDURE VendorBankAccountsUri@59() : Text;
    BEGIN
      exit(STRSUBSTNO(SyncVendorBankAccountsUriTxt,OrganizationId));
    END;

    LOCAL PROCEDURE CheckOrganizationId@58();
    BEGIN
      OrganizationId := OCRServiceSetup."Organization ID";
      if OrganizationId = '' then begin
        OCRServiceMgt.UpdateOrganizationInfo(OCRServiceSetup);
        OrganizationId := OCRServiceSetup."Organization ID";
      end;
      OCRServiceSetup.TESTFIELD("Organization ID");
    END;

    LOCAL PROCEDURE MaxPortionSize@110() : Integer;
    BEGIN
      if MaxPortionSizeValue = 0 then
        EVALUATE(MaxPortionSizeValue,MaxPortionSizeTxt);
      exit(MaxPortionSizeValue);
    END;

    LOCAL PROCEDURE OpenWindow@100(Count@1000 : Integer);
    BEGIN
      PackageNo := 0;
      PackageCount := Count;
      WindowUpdateDateTime := CURRENTDATETIME;
      Window.OPEN(MasterDataSyncMsg);
      Window.UPDATE(1,'');
    END;

    LOCAL PROCEDURE UpdateWindow@99();
    BEGIN
      PackageNo += 1;
      if CURRENTDATETIME - WindowUpdateDateTime >= 300 then begin
        WindowUpdateDateTime := CURRENTDATETIME;
        Window.UPDATE(1,STRSUBSTNO(SendingPackageMsg,PackageNo,PackageCount));
      end;
    END;

    LOCAL PROCEDURE CloseWindow@112();
    BEGIN
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

