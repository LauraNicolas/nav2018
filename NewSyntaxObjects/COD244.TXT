OBJECT Codeunit 244 Item Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo="Item Journal Batch";
    OnRun=BEGIN
            ItemJnlBatch.COPY(Rec);
            Code;
            Rec := ItemJnlBatch;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.';
      ItemJnlTemplate@1004 : Record "Item Journal Template";
      ItemJnlBatch@1005 : Record "Item Journal Batch";
      ItemJnlLine@1006 : Record "Item Journal Line";
      ItemReg@1007 : Record "Item Register";
      WhseReg@1010 : Record "Warehouse Register";
      ItemJnlPostBatch@1008 : Codeunit "Item Jnl.-Post Batch";
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with ItemJnlBatch do begin
        ItemJnlTemplate.GET("Journal Template Name");
        ItemJnlTemplate.TESTFIELD("Posting Report ID");

        if not CONFIRM(Text000,false) then
          exit;

        FIND('-');
        repeat
          ItemJnlLine."Journal Template Name" := "Journal Template Name";
          ItemJnlLine."Journal Batch Name" := Name;
          ItemJnlLine."Line No." := 1;
          CLEAR(ItemJnlPostBatch);
          if ItemJnlPostBatch.RUN(ItemJnlLine) then begin
            MARK(false);
            if ItemReg.GET(ItemJnlPostBatch.GetItemRegNo) then begin
              ItemReg.SETRECFILTER;
              REPORT.RUN(ItemJnlTemplate."Posting Report ID",false,false,ItemReg);
            end;

            if WhseReg.GET(ItemJnlPostBatch.GetWhseRegNo) then begin
              WhseReg.SETRECFILTER;
              REPORT.RUN(ItemJnlTemplate."Whse. Register Report ID",false,false,WhseReg);
            end;
          end else begin
            MARK(true);
            JnlWithErrors := true;
          end;
        until NEXT = 0;

        if not JnlWithErrors then
          MESSAGE(Text001)
        else
          MESSAGE(
            Text002 +
            Text003);

        if not FIND('=><') then begin
          RESET;
          Name := '';
        end;
      end;
    END;

    BEGIN
    END.
  }
}

