OBJECT Codeunit 5921 ServComponent-Copy from BOM
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo="Service Item";
    OnRun=BEGIN
            ServItem.GET("No.");
            ServItem.TESTFIELD("Item No.");

            ServItemComponent.LOCKTABLE;
            ServItemComponent.RESET;
            ServItemComponent.SETRANGE(Active,true);
            ServItemComponent.SETRANGE("Parent Service Item No.",ServItem."No.");
            if ServItemComponent.FINDLAST then
              LineNo := ServItemComponent."Line No."
            else
              LineNo := 0;

            BOMComp.SETCURRENTKEY("Parent Item No.","Line No.");
            BOMComp.SETRANGE("Parent Item No.",ServItem."Item No.");
            BOMComp.SETRANGE(Type,BOMComp.Type::Item);
            if BOMComp.FIND('-') then begin
              repeat
                if BOMComp."Quantity per" <> ROUND(BOMComp."Quantity per",1) then
                  ERROR(Text001,ServItem.TABLECAPTION,BOMComp.FIELDCAPTION("Quantity per"));
                for Index := 1 to BOMComp."Quantity per" do begin
                  LineNo := LineNo + 10000;
                  Item.GET(BOMComp."No.");
                  CLEAR(ServItemComponent);
                  ServItemComponent.Active := true;
                  ServItemComponent.INIT;
                  ServItemComponent."Parent Service Item No." := ServItem."No.";
                  ServItemComponent."Line No." := LineNo;
                  ServItemComponent.Type := ServItemComponent.Type::Item;
                  ServItemComponent."No." := BOMComp."No.";
                  ServItemComponent."Variant Code" := BOMComp."Variant Code";
                  ServItemComponent."Date Installed" := ServItem."Installation Date";
                  ServItemComponent.Description := Item.Description;
                  ServItemComponent."Description 2" := Item."Description 2";
                  ServItemComponent."From Line No." := 0;
                  if not ServItemComponent.INSERT then
                    ServItemComponent.MODIFY;
                end;
              until BOMComp.NEXT = 0;
            end else
              ERROR(
                Text000,
                BOMComp.TABLECAPTION,ServItem.FIELDCAPTION("Item No."),ServItem."Item No.");
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 for %2 %3 cannot be found.';
      ServItem@1001 : Record "Service Item";
      BOMComp@1002 : Record "BOM Component";
      ServItemComponent@1003 : Record "Service Item Component";
      Item@1004 : Record Item;
      LineNo@1005 : Integer;
      Index@1006 : Integer;
      Text001@1007 : TextConst 'ENU=You cannot copy the component list for this %1 from BOM. The %2 of one or more BOM components is not a whole number.';

    BEGIN
    END.
  }
}

