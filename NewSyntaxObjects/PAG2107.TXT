OBJECT Page 2107 O365 Sales Customer Card
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Customer;
    SourceTable=Customer;
    DataCaptionExpr=Name;
    PageType=Card;
    PromotedActionCategoriesML=ENU=New,Process,Report,Details;
    OnInit=VAR
             O365SalesInitialSetup@1000 : Record "O365 Sales Initial Setup";
             PostcodeServiceManager@1001 : Codeunit "Postcode Service Manager";
           BEGIN
             if O365SalesInitialSetup.GET then
               IsUsingVAT := O365SalesInitialSetup.IsUsingVAT;
             IsAddressLookupAvailable := PostcodeServiceManager.IsConfigured;
           END;

    OnOpenPage=BEGIN
                 CurrPage.EDITABLE := Blocked = Blocked::" ";
                 SETRANGE("Date Filter",0D,WORKDATE);
                 DeviceContactProviderIsAvailable := DeviceContactProvider.IsAvailable;
               END;

    OnNewRecord=BEGIN
                  OnNewRec;
                END;

    OnInsertRecord=BEGIN
                     if Name = '' then
                       CustomerCardState := CustomerCardState::Prompt
                     else
                       CustomerCardState := CustomerCardState::Keep;

                     exit(true);
                   END;

    OnModifyRecord=BEGIN
                     if Name = '' then
                       CustomerCardState := CustomerCardState::Prompt
                     else
                       CustomerCardState := CustomerCardState::Keep;

                     exit(true);
                   END;

    OnDeleteRecord=BEGIN
                     O365SalesManagement.BlockCustomerAndDeleteContact(Rec);
                   END;

    OnQueryClosePage=BEGIN
                       exit(CanExitAfterProcessingCustomer);
                     END;

    OnAfterGetCurrRecord=VAR
                           TempStandardAddress@1000 : TEMPORARY Record "Standard Address";
                           TaxArea@1001 : Record "Tax Area";
                         BEGIN
                           CreateCustomerFromTemplate;
                           CurrPageEditable := CurrPage.EDITABLE;

                           OverdueAmount := CalcOverdueBalance;

                           TempStandardAddress.CopyFromCustomer(Rec);
                           FullAddress := TempStandardAddress.ToString;

                           if TaxArea.GET("Tax Area Code") then
                             TaxAreaDescription := TaxArea.Description;

                           UpdateInvoicesLbl;
                           UpdateEstimatesLbl;
                         END;

    ActionList=ACTIONS
    {
      { 11      ;0   ;ActionContainer;
                      Name=ActionContainer11;
                      ActionContainerType=ActionItems }
      { 9       ;1   ;Action    ;
                      Name=NewSalesInvoice;
                      CaptionML=ENU=New Invoice;
                      ToolTipML=ENU=Create a new invoice for the customer.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      PromotedIsBig=true;
                      Image=NewSalesInvoice;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 O365SalesManagement.OpenNewInvoiceForCustomer(Rec);
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=NewSalesQuote;
                      CaptionML=ENU=New Estimate;
                      ToolTipML=ENU=Create an estimate for the customer.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      PromotedIsBig=true;
                      Image=NewSalesQuote;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 O365SalesManagement.OpenNewQuoteForCustomer(Rec);
                               END;
                                }
      { 16      ;1   ;Action    ;
                      Name=Invoice Discounts;
                      CaptionML=ENU=Invoice Discounts;
                      ToolTipML=ENU=Set up different discounts that are applied to invoices for the customer. An invoice discount is automatically granted to the customer when the total on a sales invoice exceeds a certain amount.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      Visible=FALSE;
                      Image=Discount;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 O365CustInvoiceDiscount@1000 : Page "O365 Cust. Invoice Discount";
                               BEGIN
                                 O365CustInvoiceDiscount.FillO365CustInvDiscount("No.");
                                 O365CustInvoiceDiscount.RUN;
                               END;
                                }
      { 20      ;1   ;Action    ;
                      Name=ImportDeviceContact;
                      CaptionML=ENU=Import Contact;
                      ToolTipML=ENU="Import a contact directly from your iOS or Android device and have the new customer fields automatically populated. ";
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      Visible=CurrPageEditable AND DeviceContactProviderIsAvailable;
                      PromotedIsBig=true;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 if DeviceContactProviderIsAvailable then begin
                                   if ISNULL(DeviceContactProvider) then
                                     DeviceContactProvider := DeviceContactProvider.Create;
                                   DeviceContactProvider.RequestDeviceContactAsync;
                                 end
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                Name=Name;
                ToolTipML=ENU=Specifies the customer's name.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Name;
                ImplicitType=Text50;
                ShowCaption=false }

    { 25  ;2   ;Field     ;
                Name=Contact Type;
                ToolTipML=ENU=Specifies if the contact is a company or a person.;
                OptionCaptionML=ENU=Company contact,Person;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Contact Type";
                OnValidate=BEGIN
                             VALIDATE("Prices Including VAT","Contact Type" = "Contact Type"::Person);
                           END;

                ImplicitType=Option;
                ShowCaption=false }

    { 4   ;2   ;Field     ;
                Name=E-Mail;
                ExtendedDatatype=E-Mail;
                CaptionML=ENU=Email Address;
                ToolTipML=ENU=Specifies the customer's email address.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="E-Mail";
                Importance=Promoted;
                OnValidate=VAR
                             MailManagement@1000 : Codeunit "Mail Management";
                           BEGIN
                             if "E-Mail" <> '' then
                               MailManagement.CheckValidEmailAddress("E-Mail");
                           END;

                ImplicitType=Text80;
                ShowCaption=false }

    { 7   ;2   ;Field     ;
                Name=Phone No.;
                ToolTipML=ENU=Specifies the customer's telephone number.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Phone No.";
                ImplicitType=Text30;
                ShowCaption=false }

    { 17  ;2   ;Group     ;
                Name=Group17;
                Visible="Balance (LCY)" <> 0;
                GroupType=Group }

    { 5   ;3   ;Field     ;
                Name=Balance (LCY);
                Lookup=false;
                DrillDown=false;
                CaptionML=ENU=Outstanding;
                ToolTipML=ENU=Specifies the customer's balance.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Balance (LCY)";
                AutoFormatType=10;
                AutoFormatExpr='1';
                Importance=Additional;
                ImplicitType=Decimal }

    { 18  ;2   ;Group     ;
                Name=Group18;
                Visible=OverdueAmount <> 0;
                GroupType=Group }

    { 6   ;3   ;Field     ;
                Name=OverdueAmount;
                Lookup=false;
                DrillDown=false;
                CaptionML=ENU=Overdue;
                ToolTipML=ENU=Specifies payments from the customer that are overdue per today's date.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=OverdueAmount;
                AutoFormatType=10;
                AutoFormatExpr='1';
                Importance=Additional;
                Editable=FALSE;
                Style=Unfavorable;
                StyleExpr=OverdueAmount > 0;
                ImplicitType=Decimal }

    { 8   ;2   ;Field     ;
                Name=Sales (LCY);
                Lookup=false;
                DrillDown=false;
                CaptionML=ENU=Total Sales (Excl. VAT);
                ToolTipML=ENU=Specifies the total net amount of sales to the customer in LCY.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sales (LCY)";
                AutoFormatType=10;
                AutoFormatExpr='1';
                Importance=Additional;
                ImplicitType=Decimal }

    { 22  ;1   ;Group     ;
                Name=Details;
                CaptionML=ENU=Details;
                GroupType=Group }

    { 26  ;2   ;Group     ;
                Name=Group26;
                GroupType=Group }

    { 13  ;3   ;Group     ;
                Name=Group13;
                Visible=("Contact Type" = "Contact Type"::Company) AND IsUsingVAT;
                GroupType=Group }

    { 21  ;4   ;Field     ;
                Name=VAT Registration No.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="VAT Registration No.";
                ImplicitType=Text20 }

    { 23  ;3   ;Group     ;
                Name=Group23;
                Visible=("Contact Type" = "Contact Type"::Person) AND IsAddressLookupAvailable AND CurrPageEditable;
                GroupType=Group }

    { 24  ;4   ;Field     ;
                Name=AddressLookup;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=AddressLookupLbl;
                Editable=FALSE;
                ImplicitType=TextConst;
                ShowCaption=false }

    { 27  ;3   ;Field     ;
                Name=FullAddress;
                CaptionML=ENU=Address;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=FullAddress;
                Editable=CurrPageEditable;
                OnAssistEdit=VAR
                               TempStandardAddress@1000 : TEMPORARY Record "Standard Address";
                             BEGIN
                               CurrPage.SAVERECORD;
                               COMMIT;
                               TempStandardAddress.CopyFromCustomer(Rec);
                               if PAGE.RUNMODAL(PAGE::"O365 Address",TempStandardAddress) = ACTION::LookupOK then begin
                                 GET("No.");
                                 FullAddress := TempStandardAddress.ToString;
                               end;
                             END;

                ImplicitType=Text;
                QuickEntry=FALSE }

    { 12  ;1   ;Group     ;
                Name=Tax Information;
                CaptionML=ENU=Tax;
                Visible=NOT IsUsingVAT;
                GroupType=Group }

    { 14  ;2   ;Field     ;
                Name=Tax Liable;
                ToolTipML=ENU=Specifies if the sales invoice contains sales tax.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Tax Liable";
                ImplicitType=Boolean }

    { 19  ;2   ;Field     ;
                Name=TaxAreaDescription;
                CaptionML=ENU=Tax Rate;
                ToolTipML=ENU=Specifies the customer's tax area.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                NotBlank=true;
                SourceExpr=TaxAreaDescription;
                Editable=CurrPageEditable;
                OnLookup=VAR
                           TaxArea@1001 : Record "Tax Area";
                         BEGIN
                           if PAGE.RUNMODAL(PAGE::"O365 Tax Area List",TaxArea) = ACTION::LookupOK then begin
                             VALIDATE("Tax Area Code",TaxArea.Code);
                             TaxAreaDescription := TaxArea.Description;
                           end;
                         END;

                ImplicitType=Text50;
                QuickEntry=FALSE }

    { 15  ;1   ;Group     ;
                Name=Documents;
                Visible=NOT CurrPageEditable;
                GroupType=Group }

    { 30  ;2   ;Field     ;
                Name=InvoicesForCustomer;
                DrillDown=true;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=InvoicesLabelText;
                Editable=FALSE;
                OnDrillDown=VAR
                              O365SalesDocument@1000 : Record "O365 Sales Document";
                            BEGIN
                              O365SalesDocument.SETRANGE("Document Type",O365SalesDocument."Document Type"::Invoice);
                              O365SalesDocument.SETRANGE("Sell-to Customer No.","No.");
                              PAGE.RUN(PAGE::"O365 Customer Sales Documents",O365SalesDocument);
                            END;

                ImplicitType=Text;
                ShowCaption=false }

    { 31  ;2   ;Field     ;
                Name=EstimatesForCustomer;
                DrillDown=true;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=EstimatesLabelText;
                Editable=FALSE;
                OnDrillDown=VAR
                              O365SalesDocument@1000 : Record "O365 Sales Document";
                            BEGIN
                              O365SalesDocument.SETRANGE("Document Type",O365SalesDocument."Document Type"::Quote);
                              O365SalesDocument.SETRANGE("Sell-to Customer No.","No.");
                              O365SalesDocument.SETRANGE(Posted,false);
                              PAGE.RUN(PAGE::"O365 Customer Sales Documents",O365SalesDocument);
                            END;

                ImplicitType=Text;
                ShowCaption=false }

  }
  CODE
  {
    VAR
      CustContUpdate@1006 : Codeunit "CustCont-Update";
      O365SalesManagement@1013 : Codeunit "O365 Sales Management";
      ProcessNewCustomerOptionQst@1001 : TextConst 'ENU=Keep editing,Discard';
      ProcessNewCustomerInstructionTxt@1003 : TextConst 'ENU=Name is missing. Keep the customer?';
      AddressLookupLbl@1009 : TextConst 'ENU=Lookup customer address';
      InvoicesForCustomerLbl@1015 : TextConst '@@@="%1= positive or zero integer: the number of invoices for the customer";ENU=Invoices (%1)';
      EstimatesForCustomerLbl@1016 : TextConst '@@@="%1= positive or zero integer: the number of estimates for the customer";ENU=Estimates (%1)';
      DeviceContactProvider@1014 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.DeviceContactProvider" WITHEVENTS RUNONCLIENT;
      CustomerCardState@1017 : 'Keep,Delete,Prompt';
      DeviceContactProviderIsAvailable@1018 : Boolean;
      NewMode@1012 : Boolean;
      IsAddressLookupAvailable@1011 : Boolean;
      CurrPageEditable@1010 : Boolean;
      IsUsingVAT@1008 : Boolean;
      OverdueAmount@1007 : Decimal;
      FullAddress@1005 : Text;
      TaxAreaDescription@1004 : Text[50];
      InvoicesLabelText@1002 : Text;
      EstimatesLabelText@1000 : Text;

    LOCAL PROCEDURE CanExitAfterProcessingCustomer@2() : Boolean;
    VAR
      Response@1001 : ',KeepEditing,Discard';
    BEGIN
      if "No." = '' then
        exit(true);

      if CustomerCardState = CustomerCardState::Delete then
        exit(DeleteCustomerRelatedData);

      if GUIALLOWED and (CustomerCardState = CustomerCardState::Prompt) then
        case STRMENU(ProcessNewCustomerOptionQst,Response::KeepEditing,ProcessNewCustomerInstructionTxt) of
          Response::Discard:
            exit(DeleteCustomerRelatedData);
          else
            exit(false);
        end;

      exit(true);
    END;

    LOCAL PROCEDURE DeleteCustomerRelatedData@4() : Boolean;
    BEGIN
      CustContUpdate.DeleteCustomerContacts(Rec);

      // workaround for bug: delete for new empty record returns false
      if DELETE(true) then;
      exit(true);
    END;

    LOCAL PROCEDURE OnNewRec@1();
    VAR
      DocumentNoVisibility@1000 : Codeunit DocumentNoVisibility;
    BEGIN
      if GUIALLOWED and DocumentNoVisibility.CustomerNoSeriesIsDefault then
        NewMode := true;
    END;

    LOCAL PROCEDURE CreateCustomerFromTemplate@3();
    VAR
      MiniCustomerTemplate@1001 : Record "Mini Customer Template";
      Customer@1000 : Record Customer;
    BEGIN
      if NewMode then begin
        if MiniCustomerTemplate.NewCustomerFromTemplate(Customer) then begin
          COPY(Customer);
          CurrPage.UPDATE;
        end;
        CustomerCardState := CustomerCardState::Delete;
        NewMode := false;
      end;
    END;

    LOCAL PROCEDURE UpdateInvoicesLbl@6();
    VAR
      SalesInvoiceHeader@1000 : Record "Sales Invoice Header";
      SalesHeader@1001 : Record "Sales Header";
      NumberOfInvoices@1002 : Integer;
    BEGIN
      SalesHeader.SETRANGE("Sell-to Customer No.","No.");
      SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Invoice);
      NumberOfInvoices := SalesHeader.COUNT;

      SalesInvoiceHeader.SETRANGE("Sell-to Customer No.","No.");
      NumberOfInvoices := NumberOfInvoices + SalesInvoiceHeader.COUNT;

      InvoicesLabelText := STRSUBSTNO(InvoicesForCustomerLbl,NumberOfInvoices);
    END;

    LOCAL PROCEDURE UpdateEstimatesLbl@7();
    VAR
      SalesHeader@1001 : Record "Sales Header";
      NumberOfEstimates@1000 : Integer;
    BEGIN
      SalesHeader.SETRANGE("Sell-to Customer No.","No.");
      SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Quote);
      NumberOfEstimates := SalesHeader.COUNT;

      EstimatesLabelText := STRSUBSTNO(EstimatesForCustomerLbl,NumberOfEstimates);
    END;

    EVENT DeviceContactProvider@1014::DeviceContactRetrieved@9(deviceContact@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.DeviceContact");
    BEGIN
      if deviceContact.Status <> 0 then
        exit;

      CreateCustomerFromTemplate;

      Name := COPYSTR(deviceContact.PreferredName,1,MAXSTRLEN("E-Mail"));
      "E-Mail" := COPYSTR(deviceContact.PreferredEmail,1,MAXSTRLEN("E-Mail"));
      "Phone No." := COPYSTR(deviceContact.PreferredPhoneNumber,1,MAXSTRLEN("Phone No."));
      Address := COPYSTR(deviceContact.PreferredAddress.StreetAddress,1,MAXSTRLEN(Address));
      City := COPYSTR(deviceContact.PreferredAddress.Locality,1,MAXSTRLEN(City));
      County := COPYSTR(deviceContact.PreferredAddress.Region,1,MAXSTRLEN(County));

      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

