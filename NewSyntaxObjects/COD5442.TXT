OBJECT Codeunit 5442 Graph Int - Business Profile
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      BusinessTypeTxt@1001 : TextConst '@@@={Locked};ENU=Business';
      ShippingTypeTxt@1002 : TextConst '@@@={Locked};ENU=Shipping';

    PROCEDURE UpdateCompanyBusinessProfileId@30(BusinessProfileId@1001 : Text[250]);
    VAR
      Company@1002 : Record Company;
    BEGIN
      Company.GET(COMPANYNAME);
      if Company."Business Profile Id" <> BusinessProfileId then begin
        Company."Business Profile Id" := BusinessProfileId;
        Company.MODIFY;
      end;
    END;

    [EventSubscriber(Codeunit,5345,OnAfterTransferRecordFields)]
    [Internal]
    PROCEDURE OnAfterTransferRecordFields@35(SourceRecordRef@1002 : RecordRef;VAR DestinationRecordRef@1001 : RecordRef;VAR AdditionalFieldsWereModified@1000 : Boolean);
    BEGIN
      case GetSourceDestCode(SourceRecordRef,DestinationRecordRef) of
        'Company Information-Graph Business Profile':
          begin
            SetAddressesOnGraph(SourceRecordRef,DestinationRecordRef);
            AdditionalFieldsWereModified := true;
          end;
      end;
    END;

    [EventSubscriber(Codeunit,5345,OnAfterInsertRecord)]
    [Internal]
    PROCEDURE OnAfterInsertRecord@45(SourceRecordRef@1001 : RecordRef;DestinationRecordRef@1000 : RecordRef);
    BEGIN
      OnAfterModifyRecord(SourceRecordRef,DestinationRecordRef);
    END;

    [EventSubscriber(Codeunit,5345,OnAfterModifyRecord)]
    [Internal]
    PROCEDURE OnAfterModifyRecord@51(SourceRecordRef@1001 : RecordRef;VAR DestinationRecordRef@1002 : RecordRef);
    VAR
      GraphBusinessProfile@1000 : Record "Graph Business Profile";
    BEGIN
      case GetSourceDestCode(SourceRecordRef,DestinationRecordRef) of
        'Graph Business Profile-Company Information':
          begin
            HandleLogoChanges(DestinationRecordRef,SourceRecordRef);
            SetAddressesFromGraph(SourceRecordRef,DestinationRecordRef);
            DestinationRecordRef.MODIFY(true);
            SourceRecordRef.SETTABLE(GraphBusinessProfile);
            UpdateCompanyBusinessProfileId(GraphBusinessProfile.Id);
          end;
        'Company Information-Graph Business Profile':
          begin
            HandleLogoChanges(SourceRecordRef,DestinationRecordRef);
            DestinationRecordRef.SETTABLE(GraphBusinessProfile);
            UpdateCompanyBusinessProfileId(GraphBusinessProfile.Id);
          end;
      end;
    END;

    LOCAL PROCEDURE GetSourceDestCode@38(SourceRecordRef@1001 : RecordRef;DestinationRecordRef@1000 : RecordRef) : Text;
    BEGIN
      if (SourceRecordRef.NUMBER <> 0) and (DestinationRecordRef.NUMBER <> 0) then
        exit(STRSUBSTNO('%1-%2',SourceRecordRef.NAME,DestinationRecordRef.NAME));
      exit('');
    END;

    LOCAL PROCEDURE SetAddressesFromGraph@13(VAR SourceRecordRef@1000 : RecordRef;VAR DestinationRecordRef@1001 : RecordRef);
    VAR
      CompanyInformation@1002 : Record "Company Information";
      GraphBusinessProfile@1003 : Record "Graph Business Profile";
      GraphMgtCompanyInfo@1008 : Codeunit "Graph Mgt - Company Info.";
      AddressesString@1007 : Text;
      EmailAddressesString@1006 : Text;
      PhoneNumbersString@1005 : Text;
      SocialLinksString@1009 : Text;
      WebsiteString@1004 : Text;
    BEGIN
      SourceRecordRef.SETTABLE(GraphBusinessProfile);
      DestinationRecordRef.SETTABLE(CompanyInformation);

      AddressesString := GraphBusinessProfile.GetAddressesString;
      PhoneNumbersString := GraphBusinessProfile.GetPhoneNumbersString;
      WebsiteString := GraphBusinessProfile.GetWebsiteString;
      EmailAddressesString := GraphBusinessProfile.GetEmailAddressesString;
      SocialLinksString := GraphBusinessProfile.GetSocialLinksString;

      if GraphMgtCompanyInfo.HasPostalAddress(AddressesString,BusinessTypeTxt) then
        with CompanyInformation do
          GraphMgtCompanyInfo.GetPostalAddress(AddressesString,BusinessTypeTxt,Address,"Address 2",
            City,County,"Country/Region Code","Post Code");

      if GraphMgtCompanyInfo.HasPostalAddress(AddressesString,ShippingTypeTxt) then
        with CompanyInformation do
          GraphMgtCompanyInfo.GetPostalAddress(AddressesString,ShippingTypeTxt,"Ship-to Address","Ship-to Address 2",
            "Ship-to City","Ship-to County","Ship-to Country/Region Code","Ship-to Post Code");

      if GraphMgtCompanyInfo.HasEmailAddress(EmailAddressesString,BusinessTypeTxt) then
        GraphMgtCompanyInfo.GetEmailAddress(EmailAddressesString,BusinessTypeTxt,CompanyInformation."E-Mail");

      if GraphMgtCompanyInfo.HasPhoneNumber(PhoneNumbersString,BusinessTypeTxt) then
        GraphMgtCompanyInfo.GetPhone(PhoneNumbersString,BusinessTypeTxt,CompanyInformation."Phone No.");

      if WebsiteString <> '' then
        GraphMgtCompanyInfo.GetWebsite(WebsiteString,CompanyInformation."Home Page");

      if SocialLinksString <> '' then
        GraphMgtCompanyInfo.UpdateSocialNetworks(SocialLinksString);

      DestinationRecordRef.GETTABLE(CompanyInformation);
    END;

    LOCAL PROCEDURE SetAddressesOnGraph@2(VAR SourceRecordRef@1000 : RecordRef;VAR DestinationRecordRef@1001 : RecordRef);
    VAR
      CompanyInformation@1002 : Record "Company Information";
      GraphBusinessProfile@1003 : Record "Graph Business Profile";
      O365SocialNetwork@1010 : Record "O365 Social Network";
      GraphMgtCompanyInfo@1008 : Codeunit "Graph Mgt - Company Info.";
      AddressesString@1004 : Text;
      EmailAddressesString@1005 : Text;
      PhoneNumbersString@1006 : Text;
      SocialNetworksString@1009 : Text;
      WebsiteString@1007 : Text;
    BEGIN
      SourceRecordRef.SETTABLE(CompanyInformation);
      DestinationRecordRef.SETTABLE(GraphBusinessProfile);

      AddressesString := GraphBusinessProfile.GetAddressesString;
      with CompanyInformation do begin
        AddressesString := GraphMgtCompanyInfo.UpdatePostalAddressJson(AddressesString,BusinessTypeTxt,
            Address,"Address 2",City,County,"Country/Region Code","Post Code");
        AddressesString := GraphMgtCompanyInfo.UpdatePostalAddressJson(AddressesString,ShippingTypeTxt,
            "Ship-to Address","Ship-to Address 2","Ship-to City","Ship-to County","Ship-to Country/Region Code","Ship-to Post Code");
      end;
      GraphBusinessProfile.SetAddressesString(AddressesString);

      EmailAddressesString := GraphBusinessProfile.GetEmailAddressesString;
      EmailAddressesString :=
        GraphMgtCompanyInfo.UpdateEmailAddressJson(EmailAddressesString,BusinessTypeTxt,CompanyInformation."E-Mail");
      GraphBusinessProfile.SetEmailAddressesString(EmailAddressesString);

      PhoneNumbersString := GraphBusinessProfile.GetPhoneNumbersString;
      PhoneNumbersString := GraphMgtCompanyInfo.UpdatePhoneJson(PhoneNumbersString,BusinessTypeTxt,CompanyInformation."Phone No.");
      GraphBusinessProfile.SetPhoneNumbersString(PhoneNumbersString);

      WebsiteString := GraphBusinessProfile.GetWebsiteString;
      WebsiteString := GraphMgtCompanyInfo.UpdateWorkWebsiteJson(WebsiteString,BusinessTypeTxt,CompanyInformation."Home Page");
      GraphBusinessProfile.SetWebsitesString(WebsiteString);

      GraphMgtCompanyInfo.GetSocialNetworksJSON(O365SocialNetwork,SocialNetworksString);
      GraphBusinessProfile.SetSocialLinksString(SocialNetworksString);

      DestinationRecordRef.GETTABLE(GraphBusinessProfile);
    END;

    LOCAL PROCEDURE HandleLogoChanges@9(VAR LocalRecRef@1000 : RecordRef;VAR GraphRecRef@1001 : RecordRef);
    VAR
      CompanyInformation@1002 : Record "Company Information";
      GraphBusinessProfile@1005 : Record "Graph Business Profile";
      DateFilterCalc@1010 : Codeunit "DateFilter-Calc";
      JSONManagement@1003 : Codeunit "JSON Management";
      JObject@1004 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      DateVariant@1006 : Variant;
      LastModifiedDateTime@1007 : DateTime;
      SetFromGraph@1009 : Boolean;
    BEGIN
      LocalRecRef.SETTABLE(CompanyInformation);
      GraphRecRef.SETTABLE(GraphBusinessProfile);

      if GraphBusinessProfile.CALCFIELDS(Logo) and GraphBusinessProfile.Logo.HASVALUE then begin
        JSONManagement.InitializeObject(GraphBusinessProfile.GetLogoString);
        JSONManagement.GetJSONObject(JObject);
        JSONManagement.GetPropertyValueFromJObjectByName(JObject,'lastModifiedDate',DateVariant);
        if EVALUATE(LastModifiedDateTime,FORMAT(DateVariant)) then
          if LastModifiedDateTime > DateFilterCalc.ConvertToUtcDateTime(CompanyInformation."Picture - Last Mod. Date Time") then begin
            // Need to update picture from graph;
            GraphBusinessProfile.CALCFIELDS(LogoContent);
            CompanyInformation.VALIDATE(Picture,GraphBusinessProfile.LogoContent);
            CompanyInformation.MODIFY(true);
            SetFromGraph := true;
          end;
      end;
      if not SetFromGraph then
        if not GraphBusinessProfile.Logo.HASVALUE or
           (CompanyInformation."Last Modified Date Time" = CompanyInformation."Picture - Last Mod. Date Time")
        then
          if CompanyInformation.Picture.HASVALUE then begin
            CompanyInformation.CALCFIELDS(Picture);
            CLEAR(GraphBusinessProfile.Logo);
            GraphBusinessProfile.LogoContent := CompanyInformation.Picture;
            GraphBusinessProfile.MODIFY;
          end;

      LocalRecRef.GETTABLE(CompanyInformation);
      GraphRecRef.SETTABLE(GraphBusinessProfile);
    END;

    BEGIN
    END.
  }
}

