OBJECT Report 7113 Export Analysis Rep. to Excel
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Export Analysis Rep. to Excel;
    ProcessingOnly=true;
    UseRequestPage=false;
  }
  DATASET
  {
    { 1   ;    ;DataItem;Integer             ;
               DataItemTable=Integer;
               DataItemTableView=SORTING(Number) WHERE(Number=CONST(1));
               OnAfterGetRecord=VAR
                                  Window@1000 : Dialog;
                                  RecNo@1001 : Integer;
                                  TotalRecNo@1002 : Integer;
                                  RowNo@1003 : Integer;
                                  ColumnNo@1004 : Integer;
                                BEGIN
                                  if DoUpdateExistingWorksheet then begin
                                    if ServerFileName = '' then
                                      ServerFileName := FileMgt.UploadFile(Text002,ExcelFileExtensionTok);
                                    if ServerFileName = '' then
                                      exit;
                                    SheetName := TempExcelBuffer.SelectSheetsName(ServerFileName);
                                    if SheetName = '' then
                                      exit;
                                  end;

                                  Window.OPEN(
                                    Text000 +
                                    '@1@@@@@@@@@@@@@@@@@@@@@\');
                                  Window.UPDATE(1,0);
                                  TotalRecNo := AnalysisLine.COUNT;
                                  RecNo := 0;

                                  TempExcelBuffer.DELETEALL;
                                  CLEAR(TempExcelBuffer);

                                  AnalysisLineTemplate.GET(AnalysisLine."Analysis Area",AnalysisLine."Analysis Line Template Name");
                                  if AnalysisLineTemplate."Item Analysis View Code" <> '' then
                                    ItemAnalysisView.GET(AnalysisLineTemplate."Analysis Area",AnalysisLineTemplate."Item Analysis View Code");
                                  GLSetup.GET;

                                  RowNo := 1;
                                  EnterCell(RowNo,1,Text001,false,false,true,'',TempExcelBuffer."Cell Type"::Text);
                                  if AnalysisLine.GETFILTER("Date Filter") <> '' then begin
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(
                                      RowNo,
                                      AnalysisLine.GETFILTER("Date Filter"),
                                      AnalysisLine.FIELDCAPTION("Date Filter"),
                                      '',
                                      TempExcelBuffer."Cell Type"::Date);
                                  end;
                                  if AnalysisLine.GETFILTER("Item Budget Filter") <> '' then begin
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(
                                      RowNo,
                                      AnalysisLine.GETFILTER("Item Budget Filter"),
                                      AnalysisLine.FIELDCAPTION("Item Budget Filter"),
                                      '',
                                      TempExcelBuffer."Cell Type"::Text);
                                  end;
                                  if AnalysisLine.GETFILTER("Dimension 1 Filter") <> '' then begin
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(
                                      RowNo,
                                      AnalysisLine.GETFILTER("Dimension 1 Filter"),
                                      GetDimFilterCaption(1),
                                      '',
                                      TempExcelBuffer."Cell Type"::Text);
                                  end;
                                  if AnalysisLine.GETFILTER("Dimension 2 Filter") <> '' then begin
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(
                                      RowNo,
                                      AnalysisLine.GETFILTER("Dimension 2 Filter"),
                                      GetDimFilterCaption(2),
                                      '',
                                      TempExcelBuffer."Cell Type"::Text);
                                  end;
                                  if AnalysisLine.GETFILTER("Dimension 3 Filter") <> '' then begin
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(
                                      RowNo,
                                      AnalysisLine.GETFILTER("Dimension 3 Filter"),
                                      GetDimFilterCaption(3),
                                      '',
                                      TempExcelBuffer."Cell Type"::Text);
                                  end;

                                  RowNo := RowNo + 1;
                                  if GLSetup."LCY Code" <> '' then begin
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(
                                      RowNo,
                                      GLSetup."LCY Code",
                                      Currency.TABLECAPTION,
                                      '',
                                      TempExcelBuffer."Cell Type"::Text);
                                  end;

                                  RowNo := RowNo + 1;
                                  AnalysisLine.SETFILTER(Show,'<>%1',AnalysisLine.Show::No);
                                  if AnalysisLine.FIND('-') then begin
                                    if ColumnLayout.FIND('-') then begin
                                      RowNo := RowNo + 1;
                                      ColumnNo := 1;
                                      repeat
                                        ColumnNo := ColumnNo + 1;
                                        EnterCell(
                                          RowNo,
                                          ColumnNo,
                                          ColumnLayout."Column Header",
                                          false,
                                          false,
                                          false,
                                          '',
                                          TempExcelBuffer."Cell Type"::Text);
                                      until ColumnLayout.NEXT = 0;
                                    end;
                                    repeat
                                      RecNo := RecNo + 1;
                                      Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));
                                      RowNo := RowNo + 1;
                                      ColumnNo := 1;
                                      EnterCell(
                                        RowNo,
                                        ColumnNo,
                                        AnalysisLine.Description,
                                        AnalysisLine.Bold,
                                        AnalysisLine.Italic,
                                        AnalysisLine.Underline,
                                        '',
                                        TempExcelBuffer."Cell Type"::Text);
                                      if ColumnLayout.FIND('-') then begin
                                        repeat
                                          ColumnValue := AnalysisReportManagement.CalcCell(AnalysisLine,ColumnLayout,false);
                                          if AnalysisReportManagement.GetDivisionError then
                                            ColumnValue := 0;
                                          ColumnNo := ColumnNo + 1;
                                          EnterCell(
                                            RowNo,
                                            ColumnNo,
                                            MatrixMgt.FormatValue(ColumnValue,ColumnLayout."Rounding Factor",false),
                                            AnalysisLine.Bold,
                                            AnalysisLine.Italic,
                                            AnalysisLine.Underline,
                                            '',
                                            TempExcelBuffer."Cell Type"::Number)
                                        until ColumnLayout.NEXT = 0;
                                      end;
                                    until AnalysisLine.NEXT = 0;
                                  end;

                                  Window.CLOSE;

                                  if DoUpdateExistingWorksheet then begin
                                    TempExcelBuffer.UpdateBook(ServerFileName,SheetName);
                                    TempExcelBuffer.WriteSheet('',COMPANYNAME,USERID);
                                    TempExcelBuffer.CloseBook;
                                    if not TestMode then
                                      TempExcelBuffer.DownloadAndOpenExcel;
                                  end else begin
                                    TempExcelBuffer.CreateBook(ServerFileName,AnalysisTemplateName);
                                    TempExcelBuffer.WriteSheet(AnalysisLine.Description,COMPANYNAME,USERID);
                                    TempExcelBuffer.CloseBook;
                                    if not TestMode then
                                      TempExcelBuffer.OpenExcel;
                                  end;
                                  if not TestMode then
                                    TempExcelBuffer.GiveUserControl;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Analyzing Data...\\';
      Text001@1001 : TextConst 'ENU=Filters';
      Text002@1002 : TextConst 'ENU=Update Workbook';
      AnalysisLineTemplate@1003 : Record "Analysis Line Template";
      AnalysisLine@1004 : Record "Analysis Line";
      ColumnLayout@1005 : Record "Analysis Column";
      TempExcelBuffer@1006 : TEMPORARY Record "Excel Buffer";
      GLSetup@1013 : Record "General Ledger Setup";
      ItemAnalysisView@1014 : Record "Item Analysis View";
      Currency@1015 : Record Currency;
      AnalysisReportManagement@1007 : Codeunit "Analysis Report Management";
      MatrixMgt@1016 : Codeunit "Matrix Management";
      FileMgt@1017 : Codeunit "File Management";
      ColumnValue@1009 : Decimal;
      ServerFileName@1008 : Text;
      SheetName@1011 : Text[250];
      AnalysisTemplateName@1019 : Code[10];
      DoUpdateExistingWorksheet@1010 : Boolean;
      ExcelFileExtensionTok@1012 : TextConst '@@@={Locked};ENU=.xlsx';
      TestMode@1018 : Boolean;

    [External]
    PROCEDURE SetOptions@3(VAR AnalysisLine2@1000 : Record "Analysis Line";ColumnLayoutName2@1001 : Code[10];NewAnalysisTemplateName@1002 : Code[10]);
    BEGIN
      AnalysisTemplateName := NewAnalysisTemplateName;
      AnalysisLine.COPYFILTERS(AnalysisLine2);
      AnalysisLine := AnalysisLine2;
      AnalysisLine.SETFILTER("Analysis Line Template Name",NewAnalysisTemplateName);
      ColumnLayout.SETRANGE("Analysis Area",AnalysisLine2."Analysis Area");
      ColumnLayout.SETRANGE("Analysis Column Template",ColumnLayoutName2);
      ColumnLayout.SETFILTER(Show,'<>%1',ColumnLayout.Show::Never);
    END;

    LOCAL PROCEDURE EnterFilterInCell@5(RowNo@1000 : Integer;Filter@1001 : Text[250];FieldName@1002 : Text[100];Format@1003 : Text[30];CellType@1004 : Option);
    BEGIN
      if Filter <> '' then begin
        EnterCell(RowNo,1,FieldName,false,false,false,'',TempExcelBuffer."Cell Type"::Text);
        EnterCell(RowNo,2,Filter,false,false,false,Format,CellType);
      end;
    END;

    LOCAL PROCEDURE EnterCell@2(RowNo@1000 : Integer;ColumnNo@1001 : Integer;CellValue@1002 : Text[250];Bold@1003 : Boolean;Italic@1004 : Boolean;UnderLine@1005 : Boolean;Format@1006 : Text[30];CellType@1007 : Option);
    BEGIN
      TempExcelBuffer.INIT;
      TempExcelBuffer.VALIDATE("Row No.",RowNo);
      TempExcelBuffer.VALIDATE("Column No.",ColumnNo);
      TempExcelBuffer."Cell Value as Text" := CellValue;
      TempExcelBuffer.Formula := '';
      TempExcelBuffer.Bold := Bold;
      TempExcelBuffer.Italic := Italic;
      TempExcelBuffer.Underline := UnderLine;
      TempExcelBuffer.NumberFormat := Format;
      TempExcelBuffer."Cell Type" := CellType;
      TempExcelBuffer.INSERT;
    END;

    LOCAL PROCEDURE GetDimFilterCaption@4(DimFilterNo@1000 : Integer) : Text[80];
    VAR
      Dimension@1001 : Record Dimension;
    BEGIN
      if AnalysisLineTemplate."Item Analysis View Code" = '' then
        case DimFilterNo of
          1:
            Dimension.GET(GLSetup."Global Dimension 1 Code");
          2:
            Dimension.GET(GLSetup."Global Dimension 2 Code");
        end
      else
        case DimFilterNo of
          1:
            Dimension.GET(ItemAnalysisView."Dimension 1 Code");
          2:
            Dimension.GET(ItemAnalysisView."Dimension 2 Code");
          3:
            Dimension.GET(ItemAnalysisView."Dimension 3 Code");
        end;
      exit(COPYSTR(Dimension.GetMLFilterCaption(GLOBALLANGUAGE),1,80));
    END;

    [External]
    PROCEDURE SetUpdateExistingWorksheet@1(UpdateExistingWorksheet@1000 : Boolean);
    BEGIN
      DoUpdateExistingWorksheet := UpdateExistingWorksheet;
    END;

    [External]
    PROCEDURE SetFileNameSilent@6(NewFileName@1000 : Text);
    BEGIN
      ServerFileName := NewFileName;
    END;

    [External]
    PROCEDURE SetTestMode@7(NewTestMode@1000 : Boolean);
    BEGIN
      TestMode := NewTestMode;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

