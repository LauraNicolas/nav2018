OBJECT Codeunit 2130 O365 Excel Import Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ValueIsNotValidDecimalErr@1000 : TextConst '@@@=%1 - some value which should be converted to decimal.;ENU=%1 is not a valid decimal.';

    PROCEDURE ImportData@1(VAR ExcelBuffer@1002 : Record "Excel Buffer";VAR O365FieldExcelMapping@1001 : Record "O365 Field Excel Mapping";StartRowNo@1000 : Integer;ObjectType@1003 : 'Customer,Item') : Integer;
    BEGIN
      ExcelBuffer.RESET;
      case ObjectType of
        ObjectType::Customer:
          exit(ImportCustomers(ExcelBuffer,O365FieldExcelMapping,StartRowNo));
        ObjectType::Item:
          exit(ImportItems(ExcelBuffer,O365FieldExcelMapping,StartRowNo));
      end;
    END;

    LOCAL PROCEDURE ImportCustomers@2(VAR ExcelBuffer@1000 : Record "Excel Buffer";VAR O365FieldExcelMapping@1001 : Record "O365 Field Excel Mapping";StartRowNo@1003 : Integer) : Integer;
    VAR
      TempCustomer@1006 : TEMPORARY Record Customer;
      RecRef@1005 : RecordRef;
      i@1004 : Integer;
    BEGIN
      RecRef.GETTABLE(TempCustomer);

      for i := StartRowNo to GetLastRowNo(ExcelBuffer) do begin
        ExcelBuffer.SETRANGE("Row No.",i);
        InitNewCustomerRecordRef(RecRef,i);
        FillRecordRefFromExcelBuffer(ExcelBuffer,O365FieldExcelMapping,RecRef);
        RecRef.INSERT;
      end;

      CreateCustomersFromBuffer(TempCustomer);
      exit(TempCustomer.COUNT);
    END;

    LOCAL PROCEDURE ImportItems@3(VAR ExcelBuffer@1000 : Record "Excel Buffer";VAR O365FieldExcelMapping@1001 : Record "O365 Field Excel Mapping";StartRowNo@1003 : Integer) : Integer;
    VAR
      TempItem@1006 : TEMPORARY Record Item;
      RecRef@1005 : RecordRef;
      i@1002 : Integer;
    BEGIN
      RecRef.GETTABLE(TempItem);

      for i := StartRowNo to GetLastRowNo(ExcelBuffer) do begin
        ExcelBuffer.SETRANGE("Row No.",i);
        InitNewItemRecordRef(RecRef,i);
        FillRecordRefFromExcelBuffer(ExcelBuffer,O365FieldExcelMapping,RecRef);
        RecRef.INSERT;
      end;

      CreateItemsFromBuffer(TempItem);
      exit(TempItem.COUNT);
    END;

    LOCAL PROCEDURE GetLastRowNo@4(VAR ExcelBuffer@1000 : Record "Excel Buffer") : Integer;
    BEGIN
      ExcelBuffer.RESET;
      ExcelBuffer.FINDLAST;
      exit(ExcelBuffer."Row No.");
    END;

    LOCAL PROCEDURE AddFieldValueToRecordRef@9(ExcelBuffer@1000 : Record "Excel Buffer";VAR O365FieldExcelMapping@1001 : Record "O365 Field Excel Mapping";VAR RecRef@1002 : RecordRef);
    VAR
      FieldRef@1003 : FieldRef;
    BEGIN
      O365FieldExcelMapping.SETRANGE("Excel Column No.",ExcelBuffer."Column No.");
      if O365FieldExcelMapping.FINDFIRST then begin
        FieldRef := RecRef.FIELD(O365FieldExcelMapping."Field ID");
        if FORMAT(FieldRef.TYPE) = 'Decimal' then
          TryEvaluateTextToDecimal(ExcelBuffer."Cell Value as Text");
        FieldRef.VALUE := COPYSTR(ExcelBuffer."Cell Value as Text",1,FieldRef.LENGTH);
      end;
    END;

    LOCAL PROCEDURE TryEvaluateTextToDecimal@10(ValueAsText@1000 : Text);
    VAR
      DecimalValue@1001 : Decimal;
    BEGIN
      if not EVALUATE(DecimalValue,ValueAsText) then
        ERROR(ValueIsNotValidDecimalErr,ValueAsText);
    END;

    LOCAL PROCEDURE InitNewCustomerRecordRef@20(VAR RecRef@1000 : RecordRef;RowNo@1001 : Integer);
    VAR
      DummyCustomer@1003 : Record Customer;
      FieldRef@1002 : FieldRef;
    BEGIN
      RecRef.INIT;
      FieldRef := RecRef.FIELD(DummyCustomer.FIELDNO("No."));
      FieldRef.VALUE := FORMAT(RowNo);
    END;

    LOCAL PROCEDURE InitNewItemRecordRef@5(VAR RecRef@1000 : RecordRef;RowNo@1001 : Integer);
    VAR
      DummyItem@1003 : Record Item;
      FieldRef@1002 : FieldRef;
    BEGIN
      RecRef.INIT;
      FieldRef := RecRef.FIELD(DummyItem.FIELDNO("No."));
      FieldRef.VALUE := FORMAT(RowNo);
    END;

    LOCAL PROCEDURE CreateCustomersFromBuffer@29(VAR TempCustomer@1000 : TEMPORARY Record Customer);
    VAR
      Customer@1001 : Record Customer;
      ConfigTemplateHeader@1002 : Record "Config. Template Header";
    BEGIN
      GetCustomerConfigTemplate(ConfigTemplateHeader);

      if TempCustomer.FINDSET then
        repeat
          CreateCustomerFromBuffer(TempCustomer,Customer);
          UpdateRecordFromTemplate(ConfigTemplateHeader,Customer);
        until TempCustomer.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateItemsFromBuffer@6(VAR TempItem@1000 : TEMPORARY Record Item);
    VAR
      ConfigTemplateHeader@1002 : Record "Config. Template Header";
      Item@1001 : Record Item;
    BEGIN
      GetItemConfigTemplate(ConfigTemplateHeader);

      if TempItem.FINDSET then
        repeat
          CreateItemFromBuffer(TempItem,Item);
          UpdateRecordFromTemplate(ConfigTemplateHeader,Item);
        until TempItem.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateCustomerFromBuffer@28(VAR TempCustomer@1000 : TEMPORARY Record Customer;VAR Customer@1001 : Record Customer);
    BEGIN
      Customer.TRANSFERFIELDS(TempCustomer);
      Customer."No." := '';
      Customer.INSERT(true);
    END;

    LOCAL PROCEDURE CreateItemFromBuffer@32(VAR TempItem@1000 : TEMPORARY Record Item;VAR Item@1001 : Record Item);
    BEGIN
      Item.TRANSFERFIELDS(TempItem);
      Item."No." := '';
      Item.INSERT(true);
    END;

    LOCAL PROCEDURE FillRecordRefFromExcelBuffer@11(VAR ExcelBuffer@1001 : Record "Excel Buffer";VAR O365FieldExcelMapping@1000 : Record "O365 Field Excel Mapping";VAR RecRef@1002 : RecordRef);
    BEGIN
      if ExcelBuffer.FINDSET then
        repeat
          AddFieldValueToRecordRef(ExcelBuffer,O365FieldExcelMapping,RecRef);
        until ExcelBuffer.NEXT = 0;
    END;

    PROCEDURE FillCustomerFieldsMappingBuffer@8(VAR O365FieldExcelMapping@1001 : Record "O365 Field Excel Mapping");
    VAR
      DummyCustomer@1000 : Record Customer;
    BEGIN
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Customer,DummyCustomer.FIELDNO(Name));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Customer,DummyCustomer.FIELDNO("Phone No."));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Customer,DummyCustomer.FIELDNO("E-Mail"));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Customer,DummyCustomer.FIELDNO(Address));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Customer,DummyCustomer.FIELDNO("Post Code"));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Customer,DummyCustomer.FIELDNO(City));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Customer,DummyCustomer.FIELDNO(County));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Customer,DummyCustomer.FIELDNO("Country/Region Code"));
    END;

    PROCEDURE FillItemFieldsMappingBuffer@15(VAR O365FieldExcelMapping@1001 : Record "O365 Field Excel Mapping");
    VAR
      DummyItem@1000 : Record Item;
    BEGIN
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Item,DummyItem.FIELDNO(Description));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Item,DummyItem.FIELDNO("Unit Price"));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Item,DummyItem.FIELDNO("Base Unit of Measure"));
      AddFieldToMapping(O365FieldExcelMapping,DATABASE::Item,DummyItem.FIELDNO("Tax Group Code"));
    END;

    LOCAL PROCEDURE AddFieldToMapping@25(VAR O365FieldExcelMapping@1002 : Record "O365 Field Excel Mapping";TableID@1000 : Integer;FieldID@1001 : Integer);
    BEGIN
      with O365FieldExcelMapping do begin
        INIT;
        "Table ID" := TableID;
        "Field ID" := FieldID;
        INSERT;
      end;
    END;

    PROCEDURE AutomapColumns@7(VAR O365FieldExcelMapping@1000 : Record "O365 Field Excel Mapping";VAR ExcelBuffer@1001 : Record "Excel Buffer") MappingFound : Boolean;
    BEGIN
      ExcelBuffer.SETRANGE("Row No.",1);
      if ExcelBuffer.FINDSET then
        repeat
          MappingFound := MappingFound or AdjustColumnMapping(O365FieldExcelMapping,ExcelBuffer);
        until ExcelBuffer.NEXT = 0;
    END;

    LOCAL PROCEDURE AdjustColumnMapping@26(VAR O365FieldExcelMapping@1002 : Record "O365 Field Excel Mapping";ExcelBuffer@1000 : Record "Excel Buffer") : Boolean;
    BEGIN
      if O365FieldExcelMapping.FINDSET then
        repeat
          O365FieldExcelMapping.CALCFIELDS("Field Name");
          if LOWERCASE(O365FieldExcelMapping."Field Name") = LOWERCASE(ExcelBuffer."Cell Value as Text") then begin
            O365FieldExcelMapping."Excel Column No." := ExcelBuffer."Column No.";
            O365FieldExcelMapping.MODIFY;
            exit(true);
          end;
        until O365FieldExcelMapping.NEXT = 0;

      exit(false);
    END;

    LOCAL PROCEDURE GetCustomerConfigTemplate@23(VAR ConfigTemplateHeader@1000 : Record "Config. Template Header");
    VAR
      O365SalesInitialSetup@1002 : Record "O365 Sales Initial Setup";
    BEGIN
      O365SalesInitialSetup.GET;
      O365SalesInitialSetup.TESTFIELD("Default Customer Template");
      ConfigTemplateHeader.GET(O365SalesInitialSetup."Default Customer Template");
    END;

    LOCAL PROCEDURE GetItemConfigTemplate@33(VAR ConfigTemplateHeader@1000 : Record "Config. Template Header");
    VAR
      O365SalesInitialSetup@1002 : Record "O365 Sales Initial Setup";
    BEGIN
      O365SalesInitialSetup.GET;
      O365SalesInitialSetup.TESTFIELD("Default Item Template");
      ConfigTemplateHeader.GET(O365SalesInitialSetup."Default Item Template");
    END;

    LOCAL PROCEDURE UpdateRecordFromTemplate@12(VAR ConfigTemplateHeader@1003 : Record "Config. Template Header";RecVar@1000 : Variant);
    VAR
      ConfigTemplateMgt@1001 : Codeunit "Config. Template Management";
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(RecVar);
      ConfigTemplateMgt.UpdateRecord(ConfigTemplateHeader,RecRef);
    END;

    BEGIN
    END.
  }
}

