OBJECT Page 400 Purchase Invoice Statistics
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Editable=false;
    CaptionML=ENU=Purchase Invoice Statistics;
    LinksAllowed=false;
    SourceTable="Purch. Inv. Header";
    PageType=ListPlus;
    OnAfterGetRecord=VAR
                       VendLedgEntry@1000 : Record "Vendor Ledger Entry";
                     BEGIN
                       CLEARALL;

                       if "Currency Code" = '' then
                         Currency.InitRoundingPrecision
                       else
                         Currency.GET("Currency Code");

                       PurchInvLine.SETRANGE("Document No.","No.");

                       if PurchInvLine.FIND('-') then
                         repeat
                           VendAmount := VendAmount + PurchInvLine.Amount;
                           AmountInclVAT := AmountInclVAT + PurchInvLine."Amount Including VAT";
                           if "Prices Including VAT" then
                             InvDiscAmount := InvDiscAmount + PurchInvLine."Inv. Discount Amount" / (1 + PurchInvLine."VAT %" / 100)
                           else
                             InvDiscAmount := InvDiscAmount + PurchInvLine."Inv. Discount Amount";
                           LineQty := LineQty + PurchInvLine.Quantity;
                           TotalNetWeight := TotalNetWeight + (PurchInvLine.Quantity * PurchInvLine."Net Weight");
                           TotalGrossWeight := TotalGrossWeight + (PurchInvLine.Quantity * PurchInvLine."Gross Weight");
                           TotalVolume := TotalVolume + (PurchInvLine.Quantity * PurchInvLine."Unit Volume");
                           if PurchInvLine."Units per Parcel" > 0 then
                             TotalParcels := TotalParcels + ROUND(PurchInvLine.Quantity / PurchInvLine."Units per Parcel",1,'>');
                           if PurchInvLine."VAT %" <> VATPercentage then
                             if VATPercentage = 0 then
                               VATPercentage := PurchInvLine."VAT %"
                             else
                               VATPercentage := -1;
                         until PurchInvLine.NEXT = 0;
                       VATAmount := AmountInclVAT - VendAmount;
                       InvDiscAmount := ROUND(InvDiscAmount,Currency."Amount Rounding Precision");

                       if VATPercentage <= 0 then
                         VATAmountText := Text000
                       else
                         VATAmountText := STRSUBSTNO(Text001,VATPercentage);

                       if "Currency Code" = '' then
                         AmountLCY := VendAmount
                       else
                         AmountLCY :=
                           CurrExchRate.ExchangeAmtFCYToLCY(
                             WORKDATE,"Currency Code",VendAmount,"Currency Factor");

                       VendLedgEntry.SETCURRENTKEY("Document No.");
                       VendLedgEntry.SETRANGE("Document No.","No.");
                       VendLedgEntry.SETRANGE("Document Type",VendLedgEntry."Document Type"::Invoice);
                       VendLedgEntry.SETRANGE("Vendor No.","Pay-to Vendor No.");
                       if VendLedgEntry.FINDFIRST then
                         AmountLCY := VendLedgEntry."Purchase (LCY)";

                       if not Vend.GET("Pay-to Vendor No.") then
                         CLEAR(Vend);
                       Vend.CALCFIELDS("Balance (LCY)");

                       PurchInvLine.CalcVATAmountLines(Rec,TempVATAmountLine);
                       CurrPage.SubForm.PAGE.SetTempVATAmountLine(TempVATAmountLine);
                       CurrPage.SubForm.PAGE.InitGlobals("Currency Code",false,false,false,false,"VAT Base Discount %");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 24  ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General }

    { 2   ;2   ;Field     ;
                Name=Amount;
                CaptionML=ENU=Amount;
                ToolTipML=ENU=Specifies the net amount of all the lines in the purchase document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=VendAmount + InvDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 6   ;2   ;Field     ;
                Name=InvDiscAmount;
                CaptionML=ENU=Inv. Discount Amount;
                ToolTipML=ENU=Specifies the invoice discount amount for the purchase document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=InvDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                ImplicitType=Decimal }

    { 12  ;2   ;Field     ;
                Name=VendAmount;
                CaptionML=ENU=Total;
                ToolTipML=ENU=Specifies the total amount, less any invoice discount amount, and excluding VAT for the purchase document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=VendAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                ImplicitType=Decimal }

    { 16  ;2   ;Field     ;
                Name=VATAmount;
                CaptionML=ENU=VAT Amount;
                ToolTipML=ENU=Specifies the total VAT amount that has been calculated for all the lines in the purchase document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=VATAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT(VATAmountText);
                ImplicitType=Decimal }

    { 20  ;2   ;Field     ;
                Name=AmountInclVAT;
                CaptionML=ENU=Total Incl. VAT;
                ToolTipML=ENU=Specifies the total amount, including VAT, that will be posted to the vendor's account for all the lines in the purchase document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AmountInclVAT;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                ImplicitType=Decimal }

    { 26  ;2   ;Field     ;
                Name=AmountLCY;
                CaptionML=ENU=Purchase (LCY);
                ToolTipML=ENU=Specifies your total purchases.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AmountLCY;
                AutoFormatType=1;
                ImplicitType=Decimal }

    { 3   ;2   ;Field     ;
                Name=LineQty;
                CaptionML=ENU=Quantity;
                ToolTipML=ENU=Specifies the total quantity of G/L account entries, items and/or resources in the purchase document.;
                ApplicationArea=#Basic,#Suite;
                DecimalPlaces=0:5;
                SourceExpr=LineQty;
                ImplicitType=Decimal }

    { 13  ;2   ;Field     ;
                Name=TotalParcels;
                CaptionML=ENU=Parcels;
                ToolTipML=ENU=Specifies the total number of parcels in the purchase document.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalParcels;
                ImplicitType=Decimal }

    { 7   ;2   ;Field     ;
                Name=TotalNetWeight;
                CaptionML=ENU=Net Weight;
                ToolTipML=ENU=Specifies the total net weight of the items in the purchase document.;
                ApplicationArea=#Basic,#Suite;
                DecimalPlaces=0:5;
                SourceExpr=TotalNetWeight;
                ImplicitType=Decimal }

    { 10  ;2   ;Field     ;
                Name=TotalGrossWeight;
                CaptionML=ENU=Gross Weight;
                ToolTipML=ENU=Specifies the total gross weight of the items in the purchase document.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalGrossWeight;
                ImplicitType=Decimal }

    { 17  ;2   ;Field     ;
                Name=TotalVolume;
                CaptionML=ENU=Volume;
                ToolTipML=ENU=Specifies the total volume of the items in the purchase document.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalVolume;
                ImplicitType=Decimal }

    { 18  ;1   ;Part      ;
                Name=SubForm;
                ApplicationArea=#Basic,#Suite;
                PagePartID="VAT Specification Subform";
                Editable=FALSE;
                PartType=Page }

    { 1901061301;1;Group  ;
                Name=Vendor;
                CaptionML=ENU=Vendor }

    { 21  ;2   ;Field     ;
                Name=Balance (LCY);
                CaptionML=ENU=Balance (LCY);
                ToolTipML=ENU=Specifies the balance in LCY on the vendor's account.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Vend."Balance (LCY)";
                AutoFormatType=1 }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=VAT Amount';
      Text001@1001 : TextConst 'ENU=%1% VAT';
      CurrExchRate@1002 : Record "Currency Exchange Rate";
      PurchInvLine@1003 : Record "Purch. Inv. Line";
      Vend@1004 : Record Vendor;
      TempVATAmountLine@1005 : TEMPORARY Record "VAT Amount Line";
      Currency@1018 : Record Currency;
      VendAmount@1006 : Decimal;
      AmountInclVAT@1007 : Decimal;
      InvDiscAmount@1008 : Decimal;
      AmountLCY@1009 : Decimal;
      LineQty@1010 : Decimal;
      TotalNetWeight@1011 : Decimal;
      TotalGrossWeight@1012 : Decimal;
      TotalVolume@1013 : Decimal;
      TotalParcels@1014 : Decimal;
      VATAmount@1015 : Decimal;
      VATPercentage@1016 : Decimal;
      VATAmountText@1017 : Text[30];

    BEGIN
    END.
  }
}

