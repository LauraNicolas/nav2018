OBJECT Codeunit 881 OCR - Receive from Service
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=VAR
            IncomingDocument@1000 : Record "Incoming Document";
            JobQueueEntry@1002 : Record "Job Queue Entry";
            JobQueueEntryFound@1001 : Boolean;
          BEGIN
            GetDocuments;
            JobQueueEntryFound :=
              JobQueueEntry.FindJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,CODEUNIT::"OCR - Receive from Service");
            if JobQueueEntryFound then begin
              IncomingDocument.SETFILTER(
                "OCR Status",'%1|%2',
                IncomingDocument."OCR Status"::Sent,
                IncomingDocument."OCR Status"::"Awaiting Verification");
              if IncomingDocument.ISEMPTY then
                JobQueueEntry.SetStatus(JobQueueEntry.Status::"On Hold")
              else
                if JobQueueEntry.Status = JobQueueEntry.Status::"On Hold" then
                  JobQueueEntry.Restart;
            end;
          END;

  }
  CODE
  {
    VAR
      DownloadCountMsg@1000 : TextConst '@@@="%1 = a number, e.g. 0, 1, 4.";ENU=%1 documents have been received.';
      AwaitingCountMsg@1001 : TextConst '@@@="%1 = a number, e.g. 0, 1, 4.";ENU=You have %1 documents that require you to manually verify the OCR values before the documents can be received.';

    [Internal]
    PROCEDURE GetDocuments@1();
    VAR
      IncomingDocument@1003 : Record "Incoming Document";
      OCRServiceMgt@1000 : Codeunit "OCR Service Mgt.";
      ResultMsg@1004 : Text;
      DownloadedDocCount@1002 : Integer;
      AwaitingDocCount@1001 : Integer;
    BEGIN
      DownloadedDocCount := OCRServiceMgt.GetDocuments('');

      IncomingDocument.SETRANGE("OCR Status",IncomingDocument."OCR Status"::"Awaiting Verification");
      AwaitingDocCount := IncomingDocument.COUNT;

      ResultMsg := STRSUBSTNO(DownloadCountMsg,DownloadedDocCount);
      if AwaitingDocCount > 0 then
        ResultMsg := STRSUBSTNO('%1\\%2',ResultMsg,STRSUBSTNO(AwaitingCountMsg,AwaitingDocCount));
      MESSAGE(ResultMsg);
    END;

    BEGIN
    END.
  }
}

