OBJECT Report 5756 Create Warehouse Location
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Warehouse Location;
    ProcessingOnly=true;
    OnPreReport=VAR
                  WhseEntry@1000 : Record "Warehouse Entry";
                BEGIN
                  if LocCode = '' then
                    ERROR(Text001);
                  if AdjBinCode = '' then
                    ERROR(Text002);

                  ItemLedgEntry.RESET;
                  ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
                  if ItemLedgEntry.FIND('-') then
                    repeat
                      ItemLedgEntry.SETRANGE("Item No.",ItemLedgEntry."Item No.");
                      ItemLedgEntry.SETRANGE(Open,true);
                      ItemLedgEntry.SETRANGE("Location Code",LocCode);
                      Found := ItemLedgEntry.FIND('-');
                      if Found and not HideValidationDialog then
                        if not CONFIRM(STRSUBSTNO('%1 %2 %3 %4 %5 %6 %7 %8',
                               Text010,Text011,Text012,Text013,Text014,
                               Text015,STRSUBSTNO(Text016,LocCode),Text017),false)
                        then
                          CurrReport.QUIT;
                      ItemLedgEntry.SETRANGE("Location Code");
                      ItemLedgEntry.SETRANGE(Open);
                      ItemLedgEntry.FIND('+');
                      ItemLedgEntry.SETRANGE("Item No.");
                    until (ItemLedgEntry.NEXT = 0) or Found;

                  if not Found then
                    ERROR(Text018,Location.TABLECAPTION,Location.FIELDCAPTION(Code),LocCode);
                  CLEAR(ItemLedgEntry);

                  WhseEntry.SETRANGE("Location Code",LocCode);
                  if not WhseEntry.ISEMPTY then
                    ERROR(
                      Text019,LocCode,WhseEntry.TABLECAPTION);

                  TempWhseJnlLine.RESET;
                  TempWhseJnlLine.DELETEALL;

                  LastLineNo := 0;

                  with ItemLedgEntry do begin
                    SETCURRENTKEY(
                      "Item No.","Location Code",Open,"Variant Code","Unit of Measure Code","Lot No.","Serial No.");

                    Location.GET(LocCode);
                    Location.TESTFIELD("Adjustment Bin Code",'');
                    CheckWhseDocs;

                    Bin.GET(LocCode,AdjBinCode);

                    if FIND('-') then begin
                      if not HideValidationDialog then begin
                        Window.OPEN(STRSUBSTNO(Text020,"Location Code") + Text003);
                        i := 1;
                        CountItemLedgEntries := COUNT;
                      end;

                      repeat
                        if not HideValidationDialog then begin
                          Window.UPDATE(100,i);
                          Window.UPDATE(102,ROUND(i / CountItemLedgEntries * 10000,1));
                        end;

                        SETRANGE("Item No.","Item No.");
                        if FIND('-') then begin
                          SETRANGE("Location Code",LocCode);
                          SETRANGE(Open,true);
                          if FIND('-') then
                            repeat
                              SETRANGE("Variant Code","Variant Code");
                              if FIND('-') then
                                repeat
                                  SETRANGE("Unit of Measure Code","Unit of Measure Code");
                                  if FIND('-') then
                                    repeat
                                      SETRANGE("Lot No.","Lot No.");
                                      if FIND('-') then
                                        repeat
                                          SETRANGE("Serial No.","Serial No.");

                                          CALCSUMS("Remaining Quantity");

                                          if "Remaining Quantity" < 0 then
                                            ERROR(
                                              STRSUBSTNO(Text005,BuildErrorText) +
                                              STRSUBSTNO(Text009,ItemsWithNegativeInventory.OBJECTID));

                                          if "Remaining Quantity" > 0 then
                                            CreateWhseJnlLine;
                                          FIND('+');
                                          SETRANGE("Serial No.");
                                        until NEXT = 0;

                                      FIND('+');
                                      SETRANGE("Lot No.");
                                    until NEXT = 0;

                                  FIND('+');
                                  SETRANGE("Unit of Measure Code")
                                until NEXT = 0;

                              FIND('+');
                              SETRANGE("Variant Code");
                            until NEXT = 0;
                        end;

                        SETRANGE(Open);
                        SETRANGE("Location Code");
                        FIND('+');
                        if not HideValidationDialog then
                          i := i + COUNT;
                        SETRANGE("Item No.");
                      until NEXT = 0;
                    end;
                  end;
                END;

    OnPostReport=VAR
                   WhseJnlRegisterLine@1000 : Codeunit "Whse. Jnl.-Register Line";
                 BEGIN
                   Location."Bin Mandatory" := true;
                   Location.VALIDATE("Directed Put-away and Pick",true);
                   Location.VALIDATE("Adjustment Bin Code",AdjBinCode);
                   Location.MODIFY;

                   if TempWhseJnlLine.FIND('-') then
                     repeat
                       WhseJnlRegisterLine.RegisterWhseJnlLine(TempWhseJnlLine);
                     until TempWhseJnlLine.NEXT = 0;

                   if not HideValidationDialog then begin
                     Window.CLOSE;
                     MESSAGE(Text004);
                   end;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  Name=Location Code;
                  CaptionML=ENU=Location Code;
                  ToolTipML=ENU="Specifies the location where the warehouse activity takes place. ";
                  ApplicationArea=#Warehouse;
                  SourceExpr=LocCode;
                  OnValidate=BEGIN
                               Location.GET(LocCode);
                               Location.TESTFIELD("Bin Mandatory",false);
                               Location.TESTFIELD("Use As In-Transit",false);
                               Location.TESTFIELD("Directed Put-away and Pick",false);
                             END;

                  OnLookup=BEGIN
                             CLEAR(Location);
                             if LocCode <> '' then
                               Location.Code := LocCode;
                             if PAGE.RUNMODAL(0,Location) = ACTION::LookupOK then begin
                               Location.TESTFIELD("Bin Mandatory",false);
                               Location.TESTFIELD("Use As In-Transit",false);
                               Location.TESTFIELD("Directed Put-away and Pick",false);
                               LocCode := Location.Code;
                             end;
                           END;
                            }

      { 3   ;2   ;Field     ;
                  Name=Adjustment Bin Code;
                  CaptionML=ENU=Adjustment Bin Code;
                  ToolTipML=ENU=Specifies the code of the item on the bin list.;
                  ApplicationArea=#Warehouse;
                  SourceExpr=AdjBinCode;
                  OnValidate=BEGIN
                               if AdjBinCode <> '' then
                                 if LocCode <> '' then
                                   Bin.GET(LocCode,AdjBinCode)
                                 else begin
                                   Bin.SETRANGE(Code,AdjBinCode);
                                   Bin.FINDFIRST;
                                   LocCode := Bin."Location Code";
                                 end;
                             END;

                  OnLookup=BEGIN
                             Bin.RESET;
                             if LocCode <> '' then
                               Bin.SETRANGE("Location Code",LocCode);

                             if PAGE.RUNMODAL(0,Bin) = ACTION::LookupOK then begin
                               if LocCode = '' then
                                 LocCode := Bin."Location Code";
                               AdjBinCode := Bin.Code;
                             end;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempWhseJnlLine@1007 : TEMPORARY Record "Warehouse Journal Line";
      Item@1020 : Record Item;
      ItemLedgEntry@1011 : Record "Item Ledger Entry";
      ItemUnitOfMeasure@1001 : Record "Item Unit of Measure";
      Location@1002 : Record Location;
      Bin@1003 : Record Bin;
      ItemsWithNegativeInventory@1010 : Report "Items with Negative Inventory";
      WMSMgt@1019 : Codeunit "WMS Management";
      Window@1013 : Dialog;
      LocCode@1000 : Code[10];
      Text001@1042 : TextConst 'ENU=Enter a location code.';
      Text002@1012 : TextConst 'ENU=Enter an adjustment bin code.';
      Text003@1004 : TextConst 'ENU=Count #100##### @102@@@@@@@@';
      AdjBinCode@1009 : Code[20];
      i@1005 : Integer;
      CountItemLedgEntries@1029 : Integer;
      LastLineNo@1008 : Integer;
      Text004@1015 : TextConst 'ENU=The conversion was successfully completed.';
      Text005@1016 : TextConst 'ENU=Negative inventory was found in the location. You must clear this negative inventory in the program before you can proceed with the conversion.\\%1.\\';
      HideValidationDialog@1018 : Boolean;
      Text006@1022 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 is not completely posted yet.\\Post or delete all of them before restarting the conversion batch job.';
      Text007@1023 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 is not completely registered yet.\\Register or delete all of them before restarting the conversion batch job.';
      Text008@1024 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 exists.\\Delete all of them before restarting the conversion batch job.';
      Text009@1030 : TextConst 'ENU=Run %1 for a report of all negative inventory in the location.';
      Text010@1039 : TextConst 'ENU=Inventory exists on this location. By choosing Yes from this warning, you are confirming that you want to enable this location to use Warehouse Management Systems by running a batch job to create warehouse entries for the inventory in this location.\\';
      Text011@1038 : TextConst 'ENU="If you want to proceed, you must first ensure that no negative inventory exists in the location. Negative inventory is not allowed in a location that uses warehouse management logic and must be cleared by posting a suitable quantity to inventory. "';
      Text012@1037 : TextConst 'ENU=You can perform a check for negative inventory by using the Items with Negative Inventory report.\\';
      Text013@1036 : TextConst 'ENU="If you can confirm that no negative inventory exists in the location, proceed with the conversion batch job. If negative inventory is found, the batch job will stop with an error message. "';
      Text014@1035 : TextConst 'ENU=The result of this batch job is that initial warehouse entries will be created. You must balance these initial warehouse entries on the adjustment bin by posting a warehouse physical inventory journal or a warehouse item journal to assign zones and bins to items.\';
      Text015@1034 : TextConst 'ENU=You must create zones and bins before posting a warehouse physical inventory.\\';
      Text016@1033 : TextConst 'ENU=Location %1 will be a warehouse management location after the batch job has run successfully. This conversion cannot be reversed or undone after it has run.';
      Text017@1032 : TextConst 'ENU=\\Do you really want to proceed?';
      Text018@1031 : TextConst 'ENU=There is nothing to convert for %1 %2 ''%3''.';
      Text019@1041 : TextConst 'ENU=Location %1 cannot be converted because at least one %2 exists for this location.';
      Text020@1006 : TextConst 'ENU=Location %1 will be converted to a WMS location.\\This might take some time so please be patient.';
      Found@1017 : Boolean;

    LOCAL PROCEDURE CheckWhseDocs@5();
    VAR
      WhseRcptHeader@1000 : Record "Warehouse Receipt Header";
      WarehouseShipmentHeader@1001 : Record "Warehouse Shipment Header";
      WhseActivHeader@1002 : Record "Warehouse Activity Header";
      WhseWkshLine@1003 : Record "Whse. Worksheet Line";
    BEGIN
      WhseRcptHeader.SETRANGE("Location Code",Location.Code);
      if not WhseRcptHeader.ISEMPTY then
        ERROR(
          Text006,
          Location.Code,
          WhseRcptHeader.TABLECAPTION);

      WarehouseShipmentHeader.SETRANGE("Location Code",Location.Code);
      if not WarehouseShipmentHeader.ISEMPTY then
        ERROR(
          Text006,
          Location.Code,
          WarehouseShipmentHeader.TABLECAPTION);

      WhseActivHeader.SETCURRENTKEY("Location Code");
      WhseActivHeader.SETRANGE("Location Code",Location.Code);
      if WhseActivHeader.FINDFIRST then
        ERROR(
          Text007,
          Location.Code,
          WhseActivHeader.Type);

      WhseWkshLine.SETRANGE("Location Code",Location.Code);
      if not WhseWkshLine.ISEMPTY then
        ERROR(
          Text008,
          Location.Code,
          WhseWkshLine.TABLECAPTION);
    END;

    LOCAL PROCEDURE CreateWhseJnlLine@4();
    BEGIN
      LastLineNo := LastLineNo + 10000;

      with ItemLedgEntry do begin
        TempWhseJnlLine.INIT;
        TempWhseJnlLine."Entry Type" := TempWhseJnlLine."Entry Type"::"Positive Adjmt.";
        TempWhseJnlLine."Line No." := LastLineNo;
        TempWhseJnlLine."Location Code" := "Location Code";
        TempWhseJnlLine."Registering Date" := TODAY;
        TempWhseJnlLine."Item No." := "Item No.";
        TempWhseJnlLine.Quantity := ROUND("Remaining Quantity" / "Qty. per Unit of Measure",0.00001);
        TempWhseJnlLine."Qty. (Base)" := "Remaining Quantity";
        TempWhseJnlLine."Qty. (Absolute)" := ROUND(ABS("Remaining Quantity") / "Qty. per Unit of Measure",0.00001);
        TempWhseJnlLine."Qty. (Absolute, Base)" := ABS("Remaining Quantity");
        TempWhseJnlLine."User ID" := USERID;
        TempWhseJnlLine."Variant Code" := "Variant Code";
        if "Unit of Measure Code" = '' then begin
          Item.GET("Item No.");
          "Unit of Measure Code" := Item."Base Unit of Measure";
        end;
        TempWhseJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        TempWhseJnlLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        TempWhseJnlLine."Lot No." := "Lot No.";
        TempWhseJnlLine."Serial No." := "Serial No.";
        TempWhseJnlLine.VALIDATE("Zone Code",Bin."Zone Code");
        TempWhseJnlLine."Bin Code" := AdjBinCode;
        TempWhseJnlLine."To Bin Code" := AdjBinCode;
        GetItemUnitOfMeasure("Item No.","Unit of Measure Code");
        TempWhseJnlLine.Cubage :=
          TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Cubage;
        TempWhseJnlLine.Weight :=
          TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Weight;

        WMSMgt.CheckWhseJnlLine(TempWhseJnlLine,0,0,false);
        TempWhseJnlLine.INSERT;
      end;
    END;

    LOCAL PROCEDURE GetItemUnitOfMeasure@15(ItemNo@1000 : Code[20];UOMCode@1001 : Code[10]);
    BEGIN
      if (ItemUnitOfMeasure."Item No." <> ItemNo) or
         (ItemUnitOfMeasure.Code <> UOMCode)
      then
        if not ItemUnitOfMeasure.GET(ItemNo,UOMCode) then
          ItemUnitOfMeasure.INIT;
    END;

    LOCAL PROCEDURE BuildErrorText@7() : Text[250];
    VAR
      Text@1000 : Text[250];
    BEGIN
      with ItemLedgEntry do begin
        Text :=
          STRSUBSTNO(
            '%1: %2, %3: %4',
            FIELDCAPTION("Location Code"),"Location Code",
            FIELDCAPTION("Item No."),"Item No.");
        if "Variant Code" <> '' then
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Variant Code"),"Variant Code");
        if "Unit of Measure Code" <> '' then
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Unit of Measure Code"),
              "Unit of Measure Code");
        if "Lot No." <> '' then
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Lot No."),"Lot No.");
        if "Serial No." <> '' then
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Serial No."),"Serial No.");
      end;
      exit(Text);
    END;

    PROCEDURE InitializeRequest@2(LocationCode@1000 : Code[10];AdjustmentBinCode@1001 : Code[20]);
    BEGIN
      LocCode := LocationCode;
      AdjBinCode := AdjustmentBinCode;
    END;

    PROCEDURE SetHideValidationDialog@7380(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

