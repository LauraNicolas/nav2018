OBJECT Codeunit 1314 Purch. Doc. From Sales Doc.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CreatePurchInvOptionQst@1001 : TextConst 'ENU=All Lines,Selected Lines';
      CreatePurchInvInstructionTxt@1000 : TextConst 'ENU=A purchase invoice will be created. Select which sales invoice lines to use.';
      SelectVentorTxt@1002 : TextConst 'ENU=Select a vendor to buy from.';
      TypeNotSupportedErr@1003 : TextConst '@@@=Line or Document type;ENU=Type %1 is not supported.';
      NoPurchaseOrdersCreatedErr@1004 : TextConst 'ENU=No purchase orders are created.';

    [External]
    PROCEDURE CreatePurchaseInvoice@2(SalesHeader@1000 : Record "Sales Header";VAR SelectedSalesLine@1001 : Record "Sales Line");
    VAR
      Vendor@1008 : Record Vendor;
      PurchaseHeader@1007 : Record "Purchase Header";
      SalesLine@1005 : Record "Sales Line";
      OptionNumber@1002 : Integer;
    BEGIN
      OptionNumber := DIALOG.STRMENU(CreatePurchInvOptionQst,1,CreatePurchInvInstructionTxt);

      if OptionNumber = 0 then
        exit;

      case OptionNumber of
        0:
          exit;
        1:
          begin
            SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
            SalesLine.SETRANGE("Document No.",SalesHeader."No.");
          end;
        2:
          SalesLine.COPY(SelectedSalesLine);
      end;

      if SelectVendor(Vendor,SalesLine) then begin
        CreatePurchaseHeader(PurchaseHeader,SalesHeader,Vendor);
        CopySalesLinesToPurchaseLines(PurchaseHeader,SalesLine);
        PAGE.RUN(PAGE::"Purchase Invoice",PurchaseHeader);
      end;
    END;

    [External]
    PROCEDURE CreatePurchaseOrder@6(SalesHeader@1000 : Record "Sales Header");
    VAR
      TempManufacturingUserTemplate@1002 : TEMPORARY Record "Manufacturing User Template";
      RequisitionLine@1003 : Record "Requisition Line";
      PurchaseHeader@1004 : Record "Purchase Header";
      TempDocumentEntry@1006 : TEMPORARY Record "Document Entry";
      MakeSupplyOrdersYesNo@1005 : Codeunit "Make Supply Orders (Yes/No)";
      PurchOrderFromSalesOrder@1008 : Page "Purch. Order From Sales Order";
      NoFilter@1001 : Text;
    BEGIN
      TempManufacturingUserTemplate.INIT;
      TempManufacturingUserTemplate."User ID" := USERID;
      TempManufacturingUserTemplate."Make Orders" := TempManufacturingUserTemplate."Make Orders"::"The Active Order";
      TempManufacturingUserTemplate."Create Purchase Order" :=
        TempManufacturingUserTemplate."Create Purchase Order"::"Make Purch. Orders";
      TempManufacturingUserTemplate."Create Production Order" := TempManufacturingUserTemplate."Create Production Order"::" ";
      TempManufacturingUserTemplate."Create Transfer Order" := TempManufacturingUserTemplate."Create Transfer Order"::" ";
      TempManufacturingUserTemplate."Create Assembly Order" := TempManufacturingUserTemplate."Create Assembly Order"::" ";
      TempManufacturingUserTemplate.INSERT;

      PurchOrderFromSalesOrder.LOOKUPMODE(true);
      PurchOrderFromSalesOrder.SetSalesOrderNo(SalesHeader."No.");
      if PurchOrderFromSalesOrder.RUNMODAL <> ACTION::LookupOK then
        exit;

      PurchOrderFromSalesOrder.GETRECORD(RequisitionLine);
      RequisitionLine.SETRANGE("Demand Order No.",SalesHeader."No.");
      RequisitionLine.SETRANGE("Demand Subtype",SalesHeader."Document Type"::Order);
      RequisitionLine.SETRANGE(Level);
      RequisitionLine.SETFILTER(Quantity,'>%1',0);
      if not RequisitionLine.ISEMPTY then begin
        MakeSupplyOrdersYesNo.SetManufUserTemplate(TempManufacturingUserTemplate);
        MakeSupplyOrdersYesNo.SetBlockForm;

        MakeSupplyOrdersYesNo.SetCreatedDocumentBuffer(TempDocumentEntry);
        MakeSupplyOrdersYesNo.RUN(RequisitionLine);
      end;

      TempDocumentEntry.SETRANGE("Table ID",DATABASE::"Purchase Header");
      if TempDocumentEntry.FINDSET then
        repeat
          if PurchaseHeader.GET(TempDocumentEntry."Document Type",TempDocumentEntry."Document No.") then
            BuildFilter(NoFilter,PurchaseHeader."No.");
        until TempDocumentEntry.NEXT = 0;

      if NoFilter = '' then
        ERROR(NoPurchaseOrdersCreatedErr);

      PurchaseHeader.SETFILTER("No.",NoFilter);
      PurchaseHeader.SETRANGE("Document Type",PurchaseHeader."Document Type"::Order);

      case PurchaseHeader.COUNT of
        0:
          ERROR(NoPurchaseOrdersCreatedErr);
        1:
          PAGE.RUN(PAGE::"Purchase Order",PurchaseHeader);
        else
          PAGE.RUN(PAGE::"Purchase Order List",PurchaseHeader);
      end;
    END;

    LOCAL PROCEDURE CreatePurchaseHeader@5(VAR PurchaseHeader@1002 : Record "Purchase Header";SalesHeader@1000 : Record "Sales Header";Vendor@1001 : Record Vendor);
    BEGIN
      PurchaseHeader.INIT;

      if SalesHeader."Document Type" in [SalesHeader."Document Type"::Invoice,SalesHeader."Document Type"::Order] then
        PurchaseHeader.VALIDATE("Document Type",PurchaseHeader."Document Type"::Invoice)
      else
        ERROR(TypeNotSupportedErr,FORMAT(SalesHeader."Document Type"));

      PurchaseHeader.InitRecord;
      PurchaseHeader.VALIDATE("Buy-from Vendor No.",Vendor."No.");
      PurchaseHeader.INSERT(true);
    END;

    LOCAL PROCEDURE CopySalesLinesToPurchaseLines@7(PurchaseHeader@1001 : Record "Purchase Header";VAR SalesLine@1002 : Record "Sales Line");
    VAR
      PurchaseLine@1000 : Record "Purchase Line";
      PurchaseLineNo@1003 : Integer;
    BEGIN
      PurchaseLineNo := 0;
      if SalesLine.FIND('-') then
        repeat
          CLEAR(PurchaseLine);
          PurchaseLine.INIT;
          PurchaseLine."Document No." := PurchaseHeader."No.";
          PurchaseLine."Document Type" := PurchaseHeader."Document Type";

          PurchaseLineNo := PurchaseLineNo + 10000;
          PurchaseLine."Line No." := PurchaseLineNo;

          case SalesLine.Type of
            SalesLine.Type::" ":
              PurchaseLine.Type := PurchaseLine.Type::" ";
            SalesLine.Type::Item:
              PurchaseLine.Type := PurchaseLine.Type::Item;
            else
              ERROR(TypeNotSupportedErr,FORMAT(SalesLine.Type));
          end;

          PurchaseLine.VALIDATE("No.",SalesLine."No.");
          PurchaseLine.Description := SalesLine.Description;

          if PurchaseLine."No." <> '' then begin
            PurchaseLine.VALIDATE("Buy-from Vendor No.",PurchaseHeader."Buy-from Vendor No.");
            PurchaseLine.VALIDATE("Pay-to Vendor No.",PurchaseHeader."Pay-to Vendor No.");
            PurchaseLine.VALIDATE(Quantity,SalesLine.Quantity);
            PurchaseLine.VALIDATE("Unit of Measure Code",SalesLine."Unit of Measure Code");
          end;

          PurchaseLine.INSERT(true);
        until SalesLine.NEXT = 0;
    END;

    LOCAL PROCEDURE SelectVendor@1(VAR Vendor@1000 : Record Vendor;VAR SelectedSalesLine@1001 : Record "Sales Line") : Boolean;
    VAR
      SalesLine@1005 : Record "Sales Line";
      Item@1007 : Record Item;
      VendorList@1003 : Page "Vendor List";
      VendorNo@1008 : Code[20];
      DefaultVendorFound@1006 : Boolean;
    BEGIN
      SalesLine.COPY(SelectedSalesLine);

      SalesLine.SETRANGE(Type,SalesLine.Type::Item);
      SalesLine.SETFILTER("No.",'<>%1','');
      if SalesLine.FINDSET then begin
        Item.GET(SalesLine."No.");
        VendorNo := Item."Vendor No.";
        DefaultVendorFound := (VendorNo <> '');

        while DefaultVendorFound and (SalesLine.NEXT <> 0) do begin
          Item.GET(SalesLine."No.");
          DefaultVendorFound := (VendorNo = Item."Vendor No.");
        end;

        if DefaultVendorFound then begin
          Vendor.GET(VendorNo);
          exit(true);
        end;
      end;

      VendorList.LOOKUPMODE(true);
      VendorList.CAPTION(SelectVentorTxt);
      if VendorList.RUNMODAL = ACTION::LookupOK then begin
        VendorList.GETRECORD(Vendor);
        exit(true);
      end;

      exit(false);
    END;

    LOCAL PROCEDURE BuildFilter@3(VAR InitialFilter@1000 : Text;NewValue@1001 : Text);
    BEGIN
      if STRPOS(InitialFilter,NewValue) = 0 then begin
        if STRLEN(InitialFilter) > 0 then
          InitialFilter += '|';
        InitialFilter += NewValue;
      end;
    END;

    BEGIN
    END.
  }
}

