OBJECT Codeunit 9500 Debugger Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    SingleInstance=true;
    OnRun=VAR
            Uri@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
            UriPartial@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.UriPartial";
            UrlString@1002 : Text;
          BEGIN
            // Generates a URL like dynamicsnav://host:port/instance//debug<?tenant=tenantId>
            UrlString := GETURL(CLIENTTYPE::Windows);
            Uri := Uri.Uri(UrlString);
            UrlString := Uri.GetLeftPart(UriPartial.Path) + DebuggerUrlTok + Uri.Query;

            HYPERLINK(UrlString);
          END;

  }
  CODE
  {
    VAR
      ClientAddin@1002 : Record "Add-in";
      DebuggedSession@1006 : Record "Active Session";
      DebuggerTaskPage@1000 : Page Debugger;
      Text000Err@1003 : TextConst 'ENU=Cannot process debugger break. The debugger is not active.';
      LastErrorMesssageIsNew@1007 : Boolean;
      LastErrorMessage@1005 : Text;
      CodeViewerControlRegistered@1001 : Boolean;
      ActionState@1004 : 'None,RunningCodeAction,CodeTrackingAction,BreakAfterRunningCodeAction,BreakAfterCodeTrackingAction';
      DebuggerUrlTok@1008 : TextConst '@@@={Locked};ENU=debug';
      ClientAddinDescriptionTxt@1009 : TextConst '@@@=%1 - product name;ENU=%1 Code Viewer control add-in';

    [Internal]
    PROCEDURE OpenDebuggerTaskPage@2();
    BEGIN
      if not CodeViewerControlRegistered then begin
        ClientAddin.INIT;
        ClientAddin."Add-in Name" := 'Microsoft.Dynamics.Nav.Client.CodeViewer';
        ClientAddin."Public Key Token" := '31bf3856ad364e35';
        ClientAddin.Description := STRSUBSTNO(ClientAddinDescriptionTxt,PRODUCTNAME.FULL);
        if ClientAddin.INSERT then;
        CodeViewerControlRegistered := true;
      end;

      if not DEBUGGER.ISACTIVE then
        DebuggerTaskPage.RUN
      else
        DebuggerTaskPage.CLOSE;
    END;

    [Internal]
    PROCEDURE ProcessDebuggerBreak@3(ErrorMessage@1000 : Text);
    BEGIN
      LastErrorMessage := ErrorMessage;
      LastErrorMesssageIsNew := true;

      if DEBUGGER.ISACTIVE then begin
        if ActionState = ActionState::CodeTrackingAction then
          ActionState := ActionState::BreakAfterCodeTrackingAction
        else
          if ActionState = ActionState::RunningCodeAction then
            ActionState := ActionState::BreakAfterRunningCodeAction;

        RefreshDebuggerTaskPage;
      end else
        ERROR(Text000Err);
    END;

    [External]
    PROCEDURE GetLastErrorMessage@4(VAR IsNew@1000 : Boolean) Message : Text;
    BEGIN
      Message := LastErrorMessage;
      IsNew := LastErrorMesssageIsNew;
      LastErrorMesssageIsNew := false;
    END;

    [External]
    PROCEDURE RefreshDebuggerTaskPage@1();
    BEGIN
      DebuggerTaskPage.ACTIVATE(true);
    END;

    [External]
    PROCEDURE AddWatch@5(Path@1000 : Text[1024];Refresh@1002 : Boolean);
    VAR
      DebuggerWatch@1001 : Record "Debugger Watch";
    BEGIN
      if Path <> '' then begin
        DebuggerWatch.SETRANGE(Path,Path);
        if DebuggerWatch.ISEMPTY then begin
          DebuggerWatch.INIT;
          DebuggerWatch.Path := Path;
          DebuggerWatch.INSERT(true);

          if Refresh then
            RefreshDebuggerTaskPage;
        end;
      end;
    END;

    LOCAL PROCEDURE LastIndexOf@6(Path@1000 : Text[1024];Character@1001 : Char;Index@1002 : Integer) : Integer;
    VAR
      CharPos@1003 : Integer;
    BEGIN
      if Path = '' then
        exit(0);

      if Index <= 0 then
        exit(0);

      if Index > STRLEN(Path) then
        Index := STRLEN(Path);

      CharPos := Index;

      if Path[CharPos] = Character then
        exit(CharPos);
      if CharPos = 1 then
        exit(0);

      repeat
        CharPos := CharPos - 1
      until (CharPos = 1) or (Path[CharPos] = Character);

      if Path[CharPos] = Character then
        exit(CharPos);

      exit(0);
    END;

    [External]
    PROCEDURE RemoveQuotes@7(Variable@1000 : Text[1024]) VarWithoutQuotes : Text[1024];
    BEGIN
      if Variable = '' then
        exit(Variable);

      if (STRLEN(Variable) >= 2) and (Variable[1] = '"') and (Variable[STRLEN(Variable)] = '"') then
        VarWithoutQuotes := COPYSTR(Variable,2,STRLEN(Variable) - 2)
      else
        VarWithoutQuotes := Variable;
    END;

    LOCAL PROCEDURE IsInRecordContext@8(Path@1000 : Text[1024];Record@1001 : Text) : Boolean;
    VAR
      Index@1002 : Integer;
      Position@1003 : Integer;
      CurrentContext@1004 : Text[250];
    BEGIN
      if Path = '' then
        exit(false);

      if Record = '' then // Empty record name means all paths match
        exit(true);

      Index := STRLEN(Path);

      if Path[Index] = '"' then begin
        Position := LastIndexOf(Path,'"',Index - 1);
        if Position <= 1 then
          exit(false);
        if Path[Position - 1] <> '.' then
          exit(false);
        Index := Position - 1; // set index on first '.' from the end
      end else begin
        Position := LastIndexOf(Path,'.',Index);
        if Position <= 1 then
          exit(false);
        Index := Position; // set index on first '.' from the end
      end;

      Index := Index - 1;
      Position := LastIndexOf(Path,'.',Index);

      if Position <= 1 then  // second '.' not found - context not found
        exit(false);

      Index := Position - 1;
      Position := LastIndexOf(Path,'.',Index);

      CurrentContext := COPYSTR(Path,Position + 1,Index - Position);
      exit(LOWERCASE(CurrentContext) = LOWERCASE(Record));
    END;

    [External]
    PROCEDURE ShouldBeInTooltip@10(Path@1000 : Text[1024];LeftContext@1001 : Text) : Boolean;
    BEGIN
      exit((STRPOS(Path,'."<Globals>"') = 0) and (STRPOS(Path,'.Keys.') = 0) and
        ((STRPOS(Path,'."<Global Text Constants>".') = 0) or (STRPOS(Path,'"<Globals>"."<Global Text Constants>".') > 0)) and
        IsInRecordContext(Path,LeftContext));
    END;

    [Internal]
    PROCEDURE GetDebuggedSession@11(VAR DebuggedSessionRec@1000 : Record "Active Session");
    BEGIN
      DebuggedSessionRec := DebuggedSession;
    END;

    [External]
    PROCEDURE SetDebuggedSession@12(DebuggedSessionRec@1000 : Record "Active Session");
    BEGIN
      DebuggedSession := DebuggedSessionRec;
    END;

    [External]
    PROCEDURE SetRunningCodeAction@13();
    BEGIN
      ActionState := ActionState::RunningCodeAction;
    END;

    [External]
    PROCEDURE SetCodeTrackingAction@16();
    BEGIN
      ActionState := ActionState::CodeTrackingAction;
    END;

    [External]
    PROCEDURE IsBreakAfterRunningCodeAction@14() : Boolean;
    BEGIN
      exit(ActionState = ActionState::BreakAfterRunningCodeAction);
    END;

    [External]
    PROCEDURE IsBreakAfterCodeTrackingAction@17() : Boolean;
    BEGIN
      exit(ActionState = ActionState::BreakAfterCodeTrackingAction);
    END;

    [External]
    PROCEDURE ResetActionState@15();
    BEGIN
      ActionState := ActionState::None;
    END;

    BEGIN
    END.
  }
}

