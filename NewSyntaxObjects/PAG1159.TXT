OBJECT Page 1159 Purchase Documents
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Purchase Documents;
    SourceTable="Vendor Ledger Entry";
    SourceTableView=SORTING("Entry No.")
                    ORDER(Descending);
    DataCaptionFields="Vendor No.";
    PageType=CardPart;
    OnOpenPage=BEGIN
                 SETFILTER("Due Date",'<%1',WORKDATE);
                 SETRANGE("Document Type","Document Type"::Invoice);
                 SETFILTER("Remaining Amt. (LCY)" ,'<>0');
                 ASCENDING := true;
               END;

    OnAfterGetRecord=VAR
                       Vendor@1000 : Record Vendor;
                     BEGIN
                       StyleTxt := SetStyle;
                       Vendor.GET("Vendor No.");
                       VendorName := Vendor.Name;
                     END;

    OnModifyRecord=BEGIN
                     CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",Rec);
                     exit(false);
                   END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      Name=ActionContainer7;
                      ActionContainerType=ActionItems }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=Due Date;
                ToolTipML=ENU=Specifies when the purchase document is due.;
                ApplicationArea=#All;
                SourceExpr="Due Date";
                StyleExpr=StyleTxt;
                ImplicitType=Date }

    { 8   ;2   ;Field     ;
                Name=Document Type;
                ToolTipML=ENU=Specifies the type of document.;
                ApplicationArea=#All;
                SourceExpr="Document Type";
                ImplicitType=Option }

    { 4   ;2   ;Field     ;
                Name=Document No.;
                ToolTipML=ENU=Specifies the purchase document number.;
                ApplicationArea=#All;
                SourceExpr="Document No.";
                StyleExpr=StyleTxt;
                ImplicitType=Code20 }

    { 5   ;2   ;Field     ;
                Name=Vendor Name;
                CaptionML=ENU=Vendor Name;
                ToolTipML=ENU=Specifies name of the Vendor.;
                ApplicationArea=#All;
                SourceExpr=VendorName;
                ImplicitType=Text250 }

    { 6   ;2   ;Field     ;
                Name=Remaining Amount LCY;
                CaptionML=ENU=Remaining Amount. (LCY);
                ToolTipML=ENU="Specifies the net amount of in the local currency. The amount is calculated using the Remaining Quantity, Line Discount %, and Unit Price (LCY) fields. ";
                ApplicationArea=#All;
                SourceExpr="Remaining Amt. (LCY)";
                OnDrillDown=VAR
                              Company@2000 : Record Company;
                              HyperLinkUrl@1001 : Text[500];
                            BEGIN
                              Company.GET(COMPANYNAME);
                              if Company."Evaluation Company" then
                                HyperLinkUrl := GETURL(CLIENTTYPE::Web,COMPANYNAME,OBJECTTYPE::Page,574) +
                                  '&profile=BUSINESS%20MANAGER' + STRSUBSTNO(FilterForRemAmtDrillDwnTxt,"Entry No.")
                              else
                                HyperLinkUrl := GETURL(CLIENTTYPE::Web,COMPANYNAME,OBJECTTYPE::Page,574) + STRSUBSTNO(FilterForRemAmtDrillDwnTxt,"Entry No.");
                              HYPERLINK(HyperLinkUrl);
                            END;

                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      StyleTxt@1000 : Text;
      VendorName@1001 : Text[250];
      FilterForRemAmtDrillDwnTxt@1002 : TextConst '@@@="%1 - Entry No. for Vendor ledger entry. ";ENU="&filter=''Detailed Vendor Ledg. Entry''.''Vendor Ledger Entry No.'' IS ''%1''"';

    PROCEDURE SetFilterForOverduePurInvoiceAmount@3();
    BEGIN
      RESET;
      SETFILTER("Due Date",'<%1',WORKDATE);
      SETRANGE("Document Type","Document Type"::Invoice);
      SETFILTER("Remaining Amt. (LCY)" ,'<>0');
      ASCENDING := true;
      CurrPage.UPDATE;
    END;

    PROCEDURE SetFilterForPurchDocsDueToday@2();
    BEGIN
      RESET;
      SETRANGE(Open,true);
      SETFILTER("Document Type",'Invoice|Credit Memo');
      SETFILTER("Due Date",'<=%1',WORKDATE);
      ASCENDING := true;
      CurrPage.UPDATE;
    END;

    PROCEDURE SetFilterForPurchInvoicesDueNextWeek@4();
    BEGIN
      RESET;
      SETRANGE(Open,true);
      SETFILTER("Document Type",'Invoice|Credit Memo');
      SETFILTER("Due Date",'%1..%2',CALCDATE('<1D>',WORKDATE),CALCDATE('<1W>',WORKDATE));
      ASCENDING := true;
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

