OBJECT Codeunit 8610 Questionnaire Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The value of the key field %1 has not been filled in for questionnaire %2.';
      XMLDOMMgt@1002 : Codeunit "XML DOM Management";
      ConfigProgressBar@1023 : Codeunit "Config. Progress Bar";
      ConfigValidateMgt@1003 : Codeunit "Config. Validate Management";
      FileMgt@1016 : Codeunit "File Management";
      Text001@1008 : TextConst 'ENU=Exporting questionnaire';
      Text002@1009 : TextConst 'ENU=Importing questionnaire';
      Text005@1010 : TextConst 'ENU=Could not create the XML schema.';
      Text007@1026 : TextConst 'ENU=Applying answers';
      Text008@1025 : TextConst 'ENU=Updating questionnaire';
      Text019@1017 : TextConst 'ENU=Could not create an Excel instance.';
      Text020@1014 : TextConst 'ENU=Excel does not contain an XML schema.';
      Text022@1005 : TextConst 'ENU=Creating Excel worksheet';
      XlApp@1015 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.ApplicationClass" RUNONCLIENT;
      XlBook@1007 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.WorkbookClass" RUNONCLIENT;
      XlSheet@1006 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.WorksheetClass" RUNONCLIENT;
      WrkBkWriter@1012 : DotNet "'Microsoft.Dynamics.Nav.OpenXml, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.OpenXml.Spreadsheet.WorkbookWriter" RUNONCLIENT;
      XlHelper@1001 : DotNet "'Microsoft.Dynamics.Nav.Integration.Office, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Integration.Office.Excel.ExcelHelper" RUNONCLIENT;
      ExportToExcel@1018 : Boolean;
      Text024@1020 : TextConst 'ENU=Download';
      Text025@1021 : TextConst 'ENU=*.*|*.*';
      Text026@1022 : TextConst 'ENU=Default';
      CalledFromCode@1024 : Boolean;
      Text028@1019 : TextConst 'ENU=Import File';
      Text029@1004 : TextConst '@@@="Only translate ''XML Files'' {Split=r""[\|\(]\*\.[^ |)]*[|) ]?""}";ENU=XML file (*.xml)|*.xml';

    [Internal]
    PROCEDURE UpdateQuestions@1(ConfigQuestionArea@1000 : Record "Config. Question Area");
    VAR
      ConfigQuestion@1002 : Record "Config. Question";
      Field@1001 : Record Field;
      ConfigPackageMgt@1003 : Codeunit "Config. Package Management";
      NextQuestionNo@1007 : Integer;
    BEGIN
      if ConfigQuestionArea."Table ID" = 0 then
        exit;

      ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
      ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);
      if ConfigQuestion.FINDLAST then
        NextQuestionNo := ConfigQuestion."No." + 1
      else
        NextQuestionNo := 1;

      ConfigPackageMgt.SetFieldFilter(Field,ConfigQuestionArea."Table ID",0);
      if Field.FINDSET then
        repeat
          ConfigQuestion.INIT;
          ConfigQuestion."Questionnaire Code" := ConfigQuestionArea."Questionnaire Code";
          ConfigQuestion."Question Area Code" := ConfigQuestionArea.Code;
          ConfigQuestion."No." := NextQuestionNo;
          ConfigQuestion."Table ID" := ConfigQuestionArea."Table ID";
          ConfigQuestion."Field ID" := Field."No.";
          if not QuestionExist(ConfigQuestion) then begin
            UpdateQuestion(ConfigQuestion);
            ConfigQuestion."Answer Option" := BuildAnswerOption(ConfigQuestionArea."Table ID",Field."No.");
            ConfigQuestion.INSERT;
            NextQuestionNo := NextQuestionNo + 1;
          end;
        until Field.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateQuestion@2(VAR ConfigQuestion@1000 : Record "Config. Question");
    VAR
      RecRef@1002 : RecordRef;
      FieldRef@1003 : FieldRef;
    BEGIN
      with ConfigQuestion do begin
        if Question <> '' then
          exit;
        if "Table ID" = 0 then
          exit;
        RecRef.OPEN("Table ID");
        FieldRef := RecRef.FIELD("Field ID");
        Question := FieldRef.CAPTION + '?';
      end;
    END;

    [Internal]
    PROCEDURE UpdateQuestionnaire@3(ConfigQuestionnaire@1000 : Record "Config. Questionnaire") : Boolean;
    VAR
      ConfigQuestionArea@1001 : Record "Config. Question Area";
    BEGIN
      if ConfigQuestionnaire.Code = '' then
        exit;

      ConfigQuestionArea.RESET;
      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      if ConfigQuestionArea.FINDSET then begin
        ConfigProgressBar.Init(ConfigQuestionArea.COUNT,1,Text008);
        repeat
          ConfigProgressBar.Update(ConfigQuestionArea.Code);
          UpdateQuestions(ConfigQuestionArea);
        until ConfigQuestionArea.NEXT = 0;
        ConfigProgressBar.Close;
        exit(true);
      end;
      exit(false);
    END;

    LOCAL PROCEDURE QuestionExist@12(ConfigQuestion@1000 : Record "Config. Question") : Boolean;
    VAR
      ConfigQuestion2@1001 : Record "Config. Question";
    BEGIN
      ConfigQuestion2.RESET;
      ConfigQuestion2.SETCURRENTKEY("Questionnaire Code","Question Area Code","Field ID");
      ConfigQuestion2.SETRANGE("Questionnaire Code",ConfigQuestion."Questionnaire Code");
      ConfigQuestion2.SETRANGE("Question Area Code",ConfigQuestion."Question Area Code");
      ConfigQuestion2.SETRANGE("Field ID",ConfigQuestion."Field ID");
      exit(not ConfigQuestion2.ISEMPTY);
    END;

    [Internal]
    PROCEDURE BuildAnswerOption@8(TableID@1007 : Integer;FieldID@1006 : Integer) : Text[250];
    VAR
      Field@1008 : Record Field;
      RecRef@1001 : RecordRef;
      FieldRef@1000 : FieldRef;
      BooleanText@1002 : Text[30];
    BEGIN
      Field.SETRANGE(TableNo,TableID);
      Field.SETRANGE("No.",FieldID);

      if not Field.FINDFIRST then
        exit;

      case Field.Type of
        Field.Type::Option:
          begin
            RecRef.OPEN(Field.TableNo);
            FieldRef := RecRef.FIELD(Field."No.");
            exit(FieldRef.OPTIONCAPTION);
          end;
        Field.Type::Boolean:
          begin
            BooleanText := FORMAT(true) + ',' + FORMAT(false);
            exit(BooleanText)
          end;
        else
          exit(FORMAT(Field.Type));
      end;
    END;

    [Internal]
    PROCEDURE ApplyAnswers@30(ConfigQuestionnaire@1001 : Record "Config. Questionnaire") : Boolean;
    VAR
      ConfigQuestionArea@1000 : Record "Config. Question Area";
    BEGIN
      ConfigQuestionArea.RESET;
      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      if ConfigQuestionArea.FINDSET then begin
        ConfigProgressBar.Init(ConfigQuestionArea.COUNT,1,Text007);
        repeat
          ConfigProgressBar.Update(ConfigQuestionArea.Code);
          ApplyAnswer(ConfigQuestionArea);
        until ConfigQuestionArea.NEXT = 0;
        ConfigProgressBar.Close;
        exit(true);
      end;
      exit(false);
    END;

    [Internal]
    PROCEDURE ApplyAnswer@7(ConfigQuestionArea@1001 : Record "Config. Question Area");
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      if ConfigQuestionArea."Table ID" = 0 then
        exit;

      RecRef.OPEN(ConfigQuestionArea."Table ID");
      RecRef.INIT;

      InsertRecordWithKeyFields(RecRef,ConfigQuestionArea);
      ModifyRecordWithOtherFields(RecRef,ConfigQuestionArea);
    END;

    LOCAL PROCEDURE InsertRecordWithKeyFields@40(VAR RecRef@1000 : RecordRef;ConfigQuestionArea@1002 : Record "Config. Question Area");
    VAR
      ConfigQuestion@1001 : Record "Config. Question";
      RecRef1@1006 : RecordRef;
      KeyRef@1003 : KeyRef;
      FieldRef@1004 : FieldRef;
      KeyFieldCount@1005 : Integer;
    BEGIN
      ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
      ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);

      KeyRef := RecRef.KEYINDEX(1);
      for KeyFieldCount := 1 to KeyRef.FIELDCOUNT do begin
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        ConfigQuestion.SETRANGE("Field ID",FieldRef.NUMBER);
        if ConfigQuestion.FINDFIRST then begin
          ConfigValidateMgt.ValidateFieldValue(RecRef,FieldRef,ConfigQuestion.Answer,false,GLOBALLANGUAGE);
        end else
          if KeyRef.FIELDCOUNT <> 1 then
            ERROR(STRSUBSTNO(Text000,FieldRef.NAME,ConfigQuestionArea.Code));
      end;

      RecRef1 := RecRef.DUPLICATE;

      if RecRef1.FIND then begin
        RecRef := RecRef1;
        exit
      end;

      RecRef.INSERT(true);
    END;

    LOCAL PROCEDURE ModifyRecordWithOtherFields@39(VAR RecRef@1004 : RecordRef;ConfigQuestionArea@1002 : Record "Config. Question Area");
    VAR
      ConfigQuestion@1003 : Record "Config. Question";
      TempConfigPackageField@1006 : TEMPORARY Record "Config. Package Field";
      ConfigPackageManagement@1005 : Codeunit "Config. Package Management";
      FieldRef@1001 : FieldRef;
      ErrorText@1000 : Text[250];
    BEGIN
      ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
      ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);

      if ConfigQuestion.FINDSET then
        repeat
          TempConfigPackageField.DELETEALL;
          if ConfigQuestion.Answer <> '' then begin
            FieldRef := RecRef.FIELD(ConfigQuestion."Field ID");
            ConfigValidateMgt.ValidateFieldValue(RecRef,FieldRef,ConfigQuestion.Answer,false,GLOBALLANGUAGE);
            ConfigPackageManagement.GetFieldsOrder(RecRef,'',TempConfigPackageField);
            ErrorText := ConfigValidateMgt.ValidateFieldRefRelationAgainstCompanyData(FieldRef,TempConfigPackageField);
            if ErrorText <> '' then
              ERROR(ErrorText);
          end;
        until ConfigQuestion.NEXT = 0;
      RecRef.MODIFY(true);
    END;

    [Internal]
    PROCEDURE ExportQuestionnaireAsXML@4(XMLDataFile@1000 : Text;VAR ConfigQuestionnaire@1001 : Record "Config. Questionnaire") : Boolean;
    VAR
      QuestionnaireXML@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ToFile@1007 : Text[1024];
      FileName@1006 : Text;
      Exported@1009 : Boolean;
    BEGIN
      QuestionnaireXML := QuestionnaireXML.XmlDocument;

      GenerateQuestionnaireXMLDocument(QuestionnaireXML,ConfigQuestionnaire);

      Exported := true;
      if not ExportToExcel then begin
        FileName := XMLDataFile;
        ToFile := Text026 + '.xml';

        if not CalledFromCode then
          FileName := FileMgt.ServerTempFileName('.xml');
        QuestionnaireXML.Save(FileName);
        if not CalledFromCode then
          Exported := FileMgt.DownloadHandler(FileName,Text024,'',Text025,ToFile);
      end else begin
        FileName := XMLDataFile;
        QuestionnaireXML.Save(FileName);
      end;

      exit(Exported);
    END;

    [Internal]
    PROCEDURE GenerateQuestionnaireXMLDocument@1100(QuestionnaireXML@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";VAR ConfigQuestionnaire@1001 : Record "Config. Questionnaire");
    VAR
      ConfigQuestionArea@1005 : Record "Config. Question Area";
      RecRef@1002 : RecordRef;
      DocumentNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      XMLDOMMgt.LoadXMLDocumentFromText(
        '<?xml version="1.0" encoding="UTF-16" standalone="yes"?><Questionnaire></Questionnaire>',QuestionnaireXML);

      DocumentNode := QuestionnaireXML.DocumentElement;

      RecRef.GETTABLE(ConfigQuestionnaire);
      CreateFieldSubtree(RecRef,DocumentNode);

      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      if ConfigQuestionArea.FINDSET then begin
        ConfigProgressBar.Init(ConfigQuestionArea.COUNT,1,Text001);
        repeat
          ConfigProgressBar.Update(ConfigQuestionArea.Code);
          CreateQuestionNodes(QuestionnaireXML,ConfigQuestionArea);
        until ConfigQuestionArea.NEXT = 0;
        ConfigProgressBar.Close;
      end;
    END;

    [Internal]
    PROCEDURE ImportQuestionnaireAsXMLFromClient@15() : Boolean;
    VAR
      ServerFileName@1000 : Text;
    BEGIN
      ServerFileName := FileMgt.ServerTempFileName('.xml');
      if UPLOAD(Text028,'',Text029,'',ServerFileName) then
        exit(ImportQuestionnaireAsXML(ServerFileName));

      exit(false);
    END;

    [Internal]
    PROCEDURE ImportQuestionnaireAsXML@5(XMLDataFile@1000 : Text) : Boolean;
    VAR
      QuestionnaireXML@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      XMLDOMMgt.LoadXMLDocumentFromFile(XMLDataFile,QuestionnaireXML);

      exit(ImportQuestionnaireXMLDocument(QuestionnaireXML));
    END;

    [Internal]
    PROCEDURE ImportQuestionnaireXMLDocument@1108(QuestionnaireXML@1104 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument") : Boolean;
    VAR
      ConfigQuestionnaire@1003 : Record "Config. Questionnaire";
      ConfigQuestionArea@1007 : Record "Config. Question Area";
      ConfigQuestion@1000 : Record "Config. Question";
      QuestionAreaNodes@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      QuestionAreaNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      QuestionNodes@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      QuestionnaireNode@1103 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      AreaNodeCount@1009 : Integer;
      NodeCount@1010 : Integer;
    BEGIN
      QuestionnaireNode := QuestionnaireXML.SelectSingleNode('//Questionnaire');

      UpdateInsertQuestionnaireField(ConfigQuestionnaire,QuestionnaireNode);
      QuestionAreaNodes := QuestionnaireNode.SelectNodes('child::*[position() >= 3]');

      ConfigProgressBar.Init(QuestionAreaNodes.Count,1,Text002);

      for AreaNodeCount := 0 to QuestionAreaNodes.Count - 1 do begin
        QuestionAreaNode := QuestionAreaNodes.Item(AreaNodeCount);
        ConfigProgressBar.Update(GetNodeValue(QuestionAreaNode,'Code'));
        ConfigQuestionArea."Questionnaire Code" := ConfigQuestionnaire.Code;
        UpdateInsertQuestionAreaFields(ConfigQuestionArea,QuestionAreaNode);

        QuestionNodes := QuestionAreaNode.SelectNodes('ConfigQuestion');
        for NodeCount := 0 to QuestionNodes.Count - 1 do begin
          ConfigQuestion.INIT;
          ConfigQuestion."Questionnaire Code" := ConfigQuestionArea."Questionnaire Code";
          ConfigQuestion."Question Area Code" := ConfigQuestionArea.Code;
          ConfigQuestion."Table ID" := ConfigQuestionArea."Table ID";
          UpdateInsertQuestionFields(ConfigQuestion,QuestionNodes.Item(NodeCount))
        end;
      end;

      ConfigProgressBar.Close;
      exit(true);
    END;

    [Internal]
    PROCEDURE ExportQuestionnaireToExcel@9(ExcelFile@1001 : Text;VAR ConfigQuestionnaire@1000 : Record "Config. Questionnaire") : Boolean;
    VAR
      ConfigQuestionnaire1@1019 : Record "Config. Questionnaire";
      ConfigQuestionArea@1018 : Record "Config. Question Area";
      ConfigQuestion@1002 : Record "Config. Question";
      ConfigQuestionnaireSchema@1017 : XMLport "Config. Questionnaire Schema";
      ListObject@1012 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.ListObject" RUNONCLIENT;
      ListColumns@1008 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.ListColumns" RUNONCLIENT;
      ListColumn@1006 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.ListColumn" RUNONCLIENT;
      Range@1005 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.Range" RUNONCLIENT;
      XMLMap@1004 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.XmlMap" RUNONCLIENT;
      YesNo@1003 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.XlYesNoGuess" RUNONCLIENT;
      OStream@1016 : OutStream;
      TempSchemaFile@1015 : File;
      TempConfigQuestionnaireFileName@1007 : Text;
      TempConfigQuestionnaireFNCL@1009 : Text;
      TempSchemaFileName@1010 : Text;
      TempSchemaFileNameCL@1011 : Text;
      TempXLSFile@1020 : Text;
      WrkShtNo@1014 : Integer;
    BEGIN
      ConfigQuestionnaire1.SETRANGE(Code,ConfigQuestionnaire.Code);
      TempSchemaFile.CREATETEMPFILE;
      TempSchemaFileName := TempSchemaFile.NAME + '.xsd';
      TempSchemaFile.CLOSE;

      TempSchemaFile.CREATE(TempSchemaFileName);
      TempSchemaFile.CREATEOUTSTREAM(OStream);
      ConfigQuestionnaireSchema.SETDESTINATION(OStream);
      ConfigQuestionnaireSchema.SETTABLEVIEW(ConfigQuestionnaire1);
      if not ConfigQuestionnaireSchema.EXPORT then
        ERROR(Text005);

      TempSchemaFile.CLOSE;

      ExportToExcel := true;
      CalledFromCode := true;
      TempConfigQuestionnaireFileName := FileMgt.ServerTempFileName('');
      ExportQuestionnaireAsXML(TempConfigQuestionnaireFileName,ConfigQuestionnaire);
      ExportToExcel := false;

      TempConfigQuestionnaireFNCL := FileMgt.DownloadTempFile(TempConfigQuestionnaireFileName);
      TempSchemaFileNameCL := FileMgt.DownloadTempFile(TempSchemaFileName);
      WrkShtNo := 1;

      TempXLSFile := FileMgt.ClientTempFileName('xlsx');
      CreateBook(TempXLSFile);

      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      if ConfigQuestionArea.FINDSET then
        repeat
          WrkShtNo += 1;
          WrkBkWriter.AddWorksheet(FORMAT(WrkShtNo));
        until ConfigQuestionArea.NEXT = 0;
      WrkBkWriter.Close;

      OpenBook(TempXLSFile,false);
      XlApp.DisplayAlerts(false);

      XlBook.XmlMaps.Add(TempSchemaFileNameCL,'Questionnaire');
      XMLMap := XlBook.XmlMaps.Item(1);

      XlSheet := XlBook.Worksheets.Item(ConfigQuestionArea.COUNT + 1);

      if ConfigQuestionnaire.Description <> '' then
        XlSheet.Name := ConfigQuestionnaire.Description
      else
        XlSheet.Name := ConfigQuestionnaire.Code;

      Range := XlSheet.Range('A1');
      Range.Value := ConfigQuestionnaire.Code;
      Range.XPath.SetValue(XMLMap,'/Questionnaire/' + GetElementName(ConfigQuestionnaire.FIELDNAME(Code)),'',false);

      Range := XlSheet.Range('B1');
      Range.Value := ConfigQuestionnaire.Description;
      Range.XPath.SetValue(XMLMap,'/Questionnaire/' + GetElementName(ConfigQuestionnaire.FIELDNAME(Description)),'',false);

      WrkShtNo := 1;
      ConfigQuestionArea.SETRANGE("Questionnaire Code",ConfigQuestionnaire.Code);
      ConfigProgressBar.Init(ConfigQuestionArea.COUNT,1,Text022);
      if ConfigQuestionArea.FINDSET then begin
        repeat
          ConfigProgressBar.Update(ConfigQuestionArea.Code);
          ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
          ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);

          XlSheet := XlBook.Worksheets.Item(WrkShtNo);
          WrkShtNo += 1;

          if ConfigQuestionArea.Description <> '' then
            XlSheet.Name := ConfigQuestionArea.Description
          else
            if ConfigQuestionArea.Code <> ConfigQuestionArea."Questionnaire Code" then
              XlSheet.Name := ConfigQuestionArea.Code
            else
              XlSheet.Name := ConfigQuestionArea."Questionnaire Code" + ' ' + ConfigQuestionArea.Code;

          Range := XlSheet.Range('A1');
          Range.Value := ConfigQuestionArea.Code;
          Range.XPath.SetValue(XMLMap,
            '/Questionnaire/' +
            GetElementName(ConfigQuestionArea.Code) + 'Questions/' + GetElementName(ConfigQuestionArea.FIELDNAME(Code)),
            '',false);

          Range := XlSheet.Range('B1');
          Range.Value := ConfigQuestionArea.Description;
          Range.XPath.SetValue(XMLMap,
            '/Questionnaire/' +
            GetElementName(ConfigQuestionArea.Code) + 'Questions/' + GetElementName(ConfigQuestionArea.FIELDNAME(Description)),
            '',false);

          Range := XlSheet.Range('C1');
          Range.Value := ConfigQuestionArea."Table ID";
          Range.XPath.SetValue(XMLMap,
            '/Questionnaire/' +
            GetElementName(ConfigQuestionArea.Code) + 'Questions/' + GetElementName(ConfigQuestionArea.FIELDNAME("Table ID")),
            '',false);

          if ConfigQuestion.FINDFIRST then begin
            Range := XlSheet.Range('A2',FORMAT(GetXLColumnID(7)) + '2');

            ListObject := XlSheet.ListObjects.Add(1,Range,true,YesNo.xlNo,'');
            ListColumns := ListObject.ListColumns;

            ListColumn := ListColumns.Item(1);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION("No.");
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME("No.")),'',true);

            ListColumn := ListColumns.Item(2);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION(Question);
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME(Question)),'',true);

            ListColumn := ListColumns.Item(3);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION("Answer Option");
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME("Answer Option")),'',true);

            ListColumn := ListColumns.Item(4);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION(Answer);
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME(Answer)),'',true);

            ListColumn := ListColumns.Item(5);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION(Reference);
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME(Reference)),'',true);

            ListColumn := ListColumns.Item(6);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION("Field ID");
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME("Field ID")),'',true);

            ListColumn := ListColumns.Item(7);
            ListColumn.Name := ConfigQuestion.FIELDCAPTION("Question Origin");
            ListColumn.XPath.SetValue(XMLMap,
              '/Questionnaire/' +
              (GetElementName(ConfigQuestionArea.Code) + 'Questions/') +
              'ConfigQuestion/' + GetElementName(ConfigQuestion.FIELDNAME("Question Origin")),'',true);

            XMLMap.Import(TempConfigQuestionnaireFNCL,true);
          end;

          XlSheet.Columns.Range('F:F').EntireColumn.Hidden := true;
        until ConfigQuestionArea.NEXT = 0;
      end;

      if FileMgt.GetExtension(ExcelFile) = '' then
        ExcelFile += '.xlsx';
      XlBook.SaveCopyAs(ExcelFile);

      CloseXlApp;

      FILE.ERASE(TempSchemaFileName);
      FILE.ERASE(TempConfigQuestionnaireFileName);

      ConfigProgressBar.Close;

      exit(true);
    END;

    [Internal]
    PROCEDURE ImportQuestionnaireFromExcel@24(XLSDataFile@1000 : Text) Imported : Boolean;
    VAR
      XmlMaps@1003 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.XmlMaps" RUNONCLIENT;
      XmlMap@1002 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.XmlMap" RUNONCLIENT;
      TmpXmlFile@1005 : File;
      InStream@1006 : InStream;
      XMLDataFileClient@1004 : Text;
      XMLDataFileServer@1001 : Text;
    BEGIN
      XlApp := XlApp.ApplicationClass;
      if ISNULL(XlApp) then
        ERROR(Text019);

      XlHelper.CallOpen(XlApp,XLSDataFile);
      XlBook := XlApp.ActiveWorkbook;
      XMLDataFileClient := '';
      XmlMaps := XlBook.XmlMaps;
      if XmlMaps.Count <> 0 then begin
        TmpXmlFile.CREATETEMPFILE;
        TmpXmlFile.CREATEINSTREAM(InStream);
        DOWNLOADFROMSTREAM(InStream,'',FileMgt.Magicpath,'',XMLDataFileClient);
        TmpXmlFile.CLOSE;

        XmlMap := XmlMaps.Item(1);
        XmlMap.Export(XMLDataFileClient,true);

        XMLDataFileServer := FileMgt.ServerTempFileName('.xml');
        UPLOAD('',FileMgt.Magicpath,'',XMLDataFileClient,XMLDataFileServer);

        if ImportQuestionnaireAsXML(XMLDataFileServer) then
          Imported := true;
        CloseXlApp;
      end else begin
        CloseXlApp;
        ERROR(Text020);
      end;

      exit(Imported);
    END;

    LOCAL PROCEDURE CreateQuestionNodes@10(QuestionnaireXML@1104 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";ConfigQuestionArea@1000 : Record "Config. Question Area");
    VAR
      ConfigQuestion@1001 : Record "Config. Question";
      DocumentElement@1103 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      QuestionAreaNode@1102 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      QuestionNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      RecRef@1012 : RecordRef;
      QuestionRecRef@1002 : RecordRef;
    BEGIN
      DocumentElement := QuestionnaireXML.DocumentElement;
      QuestionAreaNode := QuestionnaireXML.CreateElement(GetElementName(ConfigQuestionArea.Code + 'Questions'));
      DocumentElement.AppendChild(QuestionAreaNode);

      RecRef.GETTABLE(ConfigQuestionArea);
      CreateFieldSubtree(RecRef,QuestionAreaNode);

      ConfigQuestion.SETRANGE("Questionnaire Code",ConfigQuestionArea."Questionnaire Code");
      ConfigQuestion.SETRANGE("Question Area Code",ConfigQuestionArea.Code);
      if ConfigQuestion.FINDSET then
        repeat
          QuestionNode := QuestionnaireXML.CreateElement(GetElementName(ConfigQuestion.TABLENAME));
          QuestionAreaNode.AppendChild(QuestionNode);

          QuestionRecRef.GETTABLE(ConfigQuestion);
          CreateFieldSubtree(QuestionRecRef,QuestionNode);
        until ConfigQuestion.NEXT = 0;
    END;

    [External]
    PROCEDURE GetElementName@22(NameIn@1000 : Text[250]) : Text[250];
    BEGIN
      NameIn := DELCHR(NameIn,'=','?''`');
      NameIn := DELCHR(CONVERTSTR(NameIn,'<>,./\+-&()%:','             '),'=',' ');
      NameIn := DELCHR(NameIn,'=',' ');
      exit(NameIn);
    END;

    LOCAL PROCEDURE CreateFieldSubtree@6(VAR RecRef@1001 : RecordRef;VAR Node@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement");
    VAR
      Field@1006 : Record Field;
      FieldRef@1003 : FieldRef;
      FieldNode@1101 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XmlDom@1102 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      i@1002 : Integer;
    BEGIN
      XmlDom := Node.OwnerDocument;
      for i := 1 to RecRef.FIELDCOUNT do begin
        FieldRef := RecRef.FIELDINDEX(i);
        if not FieldException(RecRef.NUMBER,FieldRef.NUMBER) then begin
          FieldNode := XmlDom.CreateElement(GetElementName(FieldRef.NAME));

          if Field.GET(RecRef.NUMBER,FieldRef.NUMBER) then begin
            if Field.Class = Field.Class::FlowField then
              FieldRef.CALCFIELD;
            FieldNode.InnerText := FORMAT(FieldRef.VALUE);

            XMLDOMMgt.AddAttribute(FieldNode,'fieldlength',FORMAT(Field.Len));
          end;
          Node.AppendChild(FieldNode);
        end;
      end;
    END;

    LOCAL PROCEDURE GetNodeValue@26(VAR RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";FieldNodeName@1001 : Text[250]) : Text[250];
    VAR
      FieldNode@1101 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      FieldNode := RecordNode.SelectSingleNode(FieldNodeName);
      exit(FieldNode.InnerText);
    END;

    LOCAL PROCEDURE UpdateInsertQuestionnaireField@18(VAR ConfigQuestionnaire@1011 : Record "Config. Questionnaire";RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.OPEN(DATABASE::"Config. Questionnaire");

      ValidateRecordFields(RecRef,RecordNode);

      RecRef.SETTABLE(ConfigQuestionnaire);
    END;

    LOCAL PROCEDURE UpdateInsertQuestionAreaFields@25(VAR ConfigQuestionArea@1002 : Record "Config. Question Area";RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(ConfigQuestionArea);

      ValidateRecordFields(RecRef,RecordNode);

      RecRef.SETTABLE(ConfigQuestionArea);
    END;

    LOCAL PROCEDURE UpdateInsertQuestionFields@27(VAR ConfigQuestion@1011 : Record "Config. Question";RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      Field@1001 : Record Field;
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(ConfigQuestion);

      ValidateRecordFields(RecRef,RecordNode);

      RecRef.SETTABLE(ConfigQuestion);

      if Field.GET(ConfigQuestion."Table ID",ConfigQuestion."Field ID") then
        ModifyConfigQuestionAnswer(ConfigQuestion,Field);
    END;

    LOCAL PROCEDURE FieldNodeExists@36(VAR RecordNode@1100 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";FieldNodeName@1001 : Text[250]) : Boolean;
    VAR
      FieldNode@1101 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      FieldNode := RecordNode.SelectSingleNode(FieldNodeName);

      if not ISNULL(FieldNode) then
        exit(true);
    END;

    LOCAL PROCEDURE CreateBook@33(XlsFile@1000 : Text);
    BEGIN
      WrkBkWriter := WrkBkWriter.Create(XlsFile);
      if ISNULL(WrkBkWriter) then
        ERROR(Text019);
    END;

    LOCAL PROCEDURE GetXLColumnID@14(ColumnNo@1003 : Integer) : Text[10];
    VAR
      ExcelBuf@1000 : Record "Excel Buffer";
    BEGIN
      ExcelBuf.INIT;
      ExcelBuf.VALIDATE("Column No.",ColumnNo);
      exit(ExcelBuf.xlColID);
    END;

    LOCAL PROCEDURE FieldException@11(TableID@1000 : Integer;FieldID@1001 : Integer) : Boolean;
    VAR
      ConfigQuestionArea@1004 : Record "Config. Question Area";
      ConfigQuestion@1005 : Record "Config. Question";
    BEGIN
      case TableID of
        DATABASE::"Config. Questionnaire":
          exit(false);
        DATABASE::"Config. Question Area":
          exit(FieldID in [ConfigQuestionArea.FIELDNO("Questionnaire Code"),
                           ConfigQuestionArea.FIELDNO("Table Name")]);
        DATABASE::"Config. Question":
          exit(FieldID in [ConfigQuestion.FIELDNO("Questionnaire Code"),
                           ConfigQuestion.FIELDNO("Question Area Code"),
                           ConfigQuestion.FIELDNO("Table ID")]);
      end;
    END;

    [External]
    PROCEDURE SetCalledFromCode@13();
    BEGIN
      CalledFromCode := true;
    END;

    LOCAL PROCEDURE CloseXlApp@19();
    BEGIN
      CLEAR(WrkBkWriter);
      CLEAR(XlSheet);
      CLEAR(XlBook);
      XlApp.Quit;
      CLEAR(XlApp);
    END;

    LOCAL PROCEDURE ValidateKeyFields@16(RecRef@1000 : RecordRef;RecordNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      KeyRef@1001 : KeyRef;
      FieldRef@1002 : FieldRef;
      KeyFieldCount@1003 : Integer;
    BEGIN
      KeyRef := RecRef.KEYINDEX(1);
      for KeyFieldCount := 1 to KeyRef.FIELDCOUNT do begin
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        if FieldNodeExists(RecordNode,GetElementName(FieldRef.NAME)) then
          ConfigValidateMgt.ValidateFieldValue(
            RecRef,FieldRef,GetNodeValue(RecordNode,GetElementName(FieldRef.NAME)),false,GLOBALLANGUAGE);
      end;
    END;

    LOCAL PROCEDURE ValidateFields@20(RecRef@1000 : RecordRef;RecordNode@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      Field@1001 : Record Field;
      FieldRef@1002 : FieldRef;
    BEGIN
      Field.SETRANGE(TableNo,RecRef.NUMBER);
      if Field.FINDSET then
        repeat
          FieldRef := RecRef.FIELD(Field."No.");
          if FieldNodeExists(RecordNode,GetElementName(FieldRef.NAME)) then
            ConfigValidateMgt.ValidateFieldValue(
              RecRef,FieldRef,GetNodeValue(RecordNode,GetElementName(FieldRef.NAME)),false,GLOBALLANGUAGE)
        until Field.NEXT = 0;
    END;

    LOCAL PROCEDURE ValidateRecordFields@21(RecRef@1000 : RecordRef;RecordNode@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      RecRef1@1001 : RecordRef;
    BEGIN
      ValidateKeyFields(RecRef,RecordNode);

      RecRef1 := RecRef.DUPLICATE;
      if not RecRef1.FIND then
        RecRef.INSERT(true);

      ValidateFields(RecRef,RecordNode);

      RecRef.MODIFY(true);
    END;

    [Internal]
    PROCEDURE ModifyConfigQuestionAnswer@23(VAR ConfigQuestion@1000 : Record "Config. Question";Field@1001 : Record Field);
    VAR
      DateFormula@1003 : DateFormula;
      OptionInt@1002 : Integer;
    BEGIN
      case Field.Type of
        Field.Type::Option,
        Field.Type::Boolean:
          begin
            if ConfigQuestion.Answer <> '' then begin
              OptionInt := ConfigValidateMgt.GetOptionNo(ConfigQuestion.Answer,ConfigQuestion."Answer Option");
              ConfigQuestion."Answer Option" :=
                BuildAnswerOption(ConfigQuestion."Table ID",ConfigQuestion."Field ID");
              if OptionInt <> -1 then
                ConfigQuestion.Answer := SELECTSTR(OptionInt + 1,ConfigQuestion."Answer Option");
            end else begin
              ConfigQuestion.Answer := '';
              ConfigQuestion."Answer Option" :=
                BuildAnswerOption(ConfigQuestion."Table ID",ConfigQuestion."Field ID");
            end;
            ConfigQuestion.MODIFY;
          end;
        Field.Type::DateFormula:
          begin
            EVALUATE(DateFormula,ConfigQuestion.Answer);
            ConfigQuestion.Answer := FORMAT(DateFormula);
            ConfigQuestion.MODIFY;
          end;
      end;
    END;

    LOCAL PROCEDURE OpenBook@34(XLSDataFile@1000 : Text;Visible@1001 : Boolean);
    BEGIN
      XlApp := XlApp.ApplicationClass;
      if ISNULL(XlApp) then
        ERROR(Text019);
      XlHelper.CallOpen(XlApp,XLSDataFile);
      XlBook := XlApp.ActiveWorkbook;
      XlApp.Visible(Visible);
    END;

    BEGIN
    END.
  }
}

