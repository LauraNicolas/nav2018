OBJECT Codeunit 234 Gen. Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20030521D;
    Time=120000T;
    Version List=NAVW13.70;
  }
  PROPERTIES
  {
    TableNo="Gen. Journal Batch";
    OnRun=BEGIN
            GenJnlBatch.COPY(Rec);
            Code;
            Rec := GenJnlBatch;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the report(s)?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.';
      GenJnlTemplate@1004 : Record "Gen. Journal Template";
      GenJnlBatch@1005 : Record "Gen. Journal Batch";
      GenJnlLine@1006 : Record "Gen. Journal Line";
      GLReg@1007 : Record "G/L Register";
      CustLedgEntry@1010 : Record "Cust. Ledger Entry";
      VendLedgEntry@1011 : Record "Vendor Ledger Entry";
      GenJnlPostBatch@1008 : Codeunit "Gen. Jnl.-Post Batch";
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with GenJnlBatch do begin
        GenJnlTemplate.GET("Journal Template Name");
        if GenJnlTemplate."Force Posting Report" or
           (GenJnlTemplate."Cust. Receipt Report ID" = 0) and (GenJnlTemplate."Vendor Receipt Report ID" = 0)
        then
          GenJnlTemplate.TESTFIELD("Posting Report ID");

        if not CONFIRM(Text000,false) then
          exit;

        FIND('-');
        repeat
          GenJnlLine."Journal Template Name" := "Journal Template Name";
          GenJnlLine."Journal Batch Name" := Name;
          GenJnlLine."Line No." := 1;
          CLEAR(GenJnlPostBatch);
          if GenJnlPostBatch.RUN(GenJnlLine) then begin
            MARK(false);
            if GLReg.GET(GenJnlLine."Line No.") then begin
              if GenJnlTemplate."Cust. Receipt Report ID" <> 0 then begin
                CustLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
                REPORT.RUN(GenJnlTemplate."Cust. Receipt Report ID",false,false,CustLedgEntry);
              end;
              if GenJnlTemplate."Vendor Receipt Report ID" <> 0 then begin
                VendLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
                REPORT.RUN(GenJnlTemplate."Vendor Receipt Report ID",false,false,VendLedgEntry);
              end;
              if GenJnlTemplate."Posting Report ID" <> 0 then begin
                GLReg.SETRECFILTER;
                REPORT.RUN(GenJnlTemplate."Posting Report ID",false,false,GLReg);
              end;
            end;
          end else begin
            MARK(true);
            JnlWithErrors := true;
          end;
        until NEXT = 0;

        if not JnlWithErrors then
          MESSAGE(Text001)
        else
          MESSAGE(
            Text002 +
            Text003);

        if not FIND('=><') then begin
          RESET;
          Name := '';
        end;
      end;
    END;

    BEGIN
    END.
  }
}

