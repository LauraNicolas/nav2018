OBJECT Report 593 Intrastat - Make Disk Tax Auth
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Intrastat - Make Disk Tax Auth;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  FileName := FileMgt.ServerTempFileName('');

                  IntrastatJnlLine4.COPYFILTERS("Intrastat Jnl. Line");
                  if FileName = '' then
                    ERROR(Text000);
                  IntraFile.TEXTMODE := true;
                  IntraFile.WRITEMODE := true;
                  IntraFile.CREATE(FileName);
                END;

  }
  DATASET
  {
    { 2880;    ;DataItem;Intrastat Jnl. Batch;
               DataItemTable="Intrastat Jnl. Batch";
               DataItemTableView=SORTING("Journal Template Name",Name);
               OnPreDataItem=BEGIN
                               IntrastatJnlLine4.COPYFILTER("Journal Template Name","Journal Template Name");
                               IntrastatJnlLine4.COPYFILTER("Journal Batch Name",Name);
                             END;

               OnAfterGetRecord=BEGIN
                                  TESTFIELD(Reported,false);
                                  IntraReferenceNo := "Statistics Period" + '000000';
                                END;

               ReqFilterFields="Journal Template Name",Name }

    { 9905;1   ;DataItem;Intrastat Jnl. Line ;
               DataItemTable="Intrastat Jnl. Line";
               DataItemTableView=SORTING(Type,"Country/Region Code","Tariff No.","Transaction Type","Transport Method");
               OnAfterGetRecord=BEGIN
                                  if ("Tariff No." = '') and
                                     ("Country/Region Code" = '') and
                                     ("Transaction Type" = '') and
                                     ("Transport Method" = '') and
                                     ("Total Weight" = 0)
                                  then
                                    CurrReport.SKIP;

                                  TESTFIELD("Tariff No.");
                                  TESTFIELD("Country/Region Code");
                                  TESTFIELD("Transaction Type");
                                  TESTFIELD("Total Weight");
                                  if "Supplementary Units" then
                                    TESTFIELD(Quantity);
                                  CompoundField :=
                                    FORMAT("Country/Region Code",10) + FORMAT(DELCHR("Tariff No."),10) +
                                    FORMAT("Transaction Type",10) + FORMAT("Transport Method",10);

                                  if (TempType <> Type) or (STRLEN(TempCompoundField) = 0) then begin
                                    TempType := Type;
                                    TempCompoundField := CompoundField;
                                    IntraReferenceNo := COPYSTR(IntraReferenceNo,1,4) + FORMAT(Type,1,2) + '01001';
                                  end else
                                    if TempCompoundField <> CompoundField then begin
                                      TempCompoundField := CompoundField;
                                      if COPYSTR(IntraReferenceNo,8,3) = '999' then
                                        IntraReferenceNo := INCSTR(COPYSTR(IntraReferenceNo,1,7)) + '001'
                                      else
                                        IntraReferenceNo := INCSTR(IntraReferenceNo);
                                    end;

                                  "Internal Ref. No." := IntraReferenceNo;
                                  MODIFY;
                                END;

               ReqFilterFields=Type;
               DataItemLink="Journal Template Name"=FIELD("Journal Template Name"),"Journal Batch Name"=FIELD(Name) }

    { 2177;1   ;DataItem;IntrastatJnlLine2   ;
               DataItemTable="Intrastat Jnl. Line";
               DataItemTableView=SORTING("Internal Ref. No.");
               OnPreDataItem=BEGIN
                               CompanyInfo.GET;
                               VATRegNo := CONVERTSTR(CompanyInfo."VAT Registration No.",Text001,'    ');
                               IntraFile.WRITE(FORMAT('00' + FORMAT(VATRegNo,8) + Text002,80));
                               IntraFile.WRITE(FORMAT('0100004',80));

                               SETRANGE("Internal Ref. No.",COPYSTR(IntraReferenceNo,1,4),COPYSTR(IntraReferenceNo,1,4) + '9');
                               CurrReport.CREATETOTALS(Quantity,"Statistical Value","Total Weight");

                               IntrastatJnlLine3.SETCURRENTKEY("Internal Ref. No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  if ("Tariff No." = '') and
                                     ("Country/Region Code" = '') and
                                     ("Transaction Type" = '') and
                                     ("Transport Method" = '') and
                                     ("Total Weight" = 0)
                                  then
                                    CurrReport.SKIP;
                                  "Tariff No." := DELCHR("Tariff No.");

                                  TotalWeightAmt += "Total Weight";
                                  QuantityAmt += Quantity;
                                  StatisticalValueAmt += "Statistical Value";

                                  IntrastatJnlLine5.COPY(IntrastatJnlLine2);
                                  if IntrastatJnlLine5.NEXT = 1 then begin
                                    if (DELCHR(IntrastatJnlLine5."Tariff No.") = "Tariff No.") and
                                       (IntrastatJnlLine5."Country/Region Code" = "Country/Region Code") and
                                       (IntrastatJnlLine5."Transaction Type" = "Transaction Type") and
                                       (IntrastatJnlLine5."Transport Method" = "Transport Method")
                                    then
                                      GroupTotal := false
                                    else
                                      GroupTotal := true;
                                  end else
                                    GroupTotal := true;

                                  if GroupTotal then begin
                                    WriteGrTotalsToFile(TotalWeightAmt,QuantityAmt,StatisticalValueAmt);
                                    StatisticalValueTotalAmt += StatisticalValueAmt;
                                    TotalWeightAmt := 0;
                                    QuantityAmt := 0;
                                    StatisticalValueAmt := 0;
                                  end;
                                END;

               OnPostDataItem=BEGIN
                                if not Receipt then
                                  IntraFile.WRITE(
                                    FORMAT(
                                      '02000' + FORMAT(IntraReferenceNo,4) + '100000' +
                                      FORMAT(VATRegNo,8) + '1' + FORMAT(IntraReferenceNo,4),
                                      80));
                                if not Shipment then
                                  IntraFile.WRITE(
                                    FORMAT(
                                      '02000' + FORMAT(IntraReferenceNo,4) + '200000' +
                                      FORMAT(VATRegNo,8) + '2' + FORMAT(IntraReferenceNo,4),
                                      80));
                                IntraFile.WRITE(FORMAT('10' + DecimalNumeralZeroFormat(StatisticalValueTotalAmt,16),80));
                                IntraFile.CLOSE;

                                "Intrastat Jnl. Batch".Reported := true;
                                "Intrastat Jnl. Batch".MODIFY;

                                if ServerFileName = '' then
                                  FileMgt.DownloadHandler(FileName,'','',FileMgt.GetToFilterText('',DefaultFilenameTxt),DefaultFilenameTxt)
                                else
                                  FileMgt.CopyServerFile(FileName,ServerFileName,true);
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=VAR
                   IntrastatSetup@1000 : Record "Intrastat Setup";
                 BEGIN
                   if not IntrastatSetup.GET then
                     exit;

                   if IntrastatSetup."Report Receipts" and IntrastatSetup."Report Shipments" then
                     exit;

                   if IntrastatSetup."Report Receipts" then
                     "Intrastat Jnl. Line".SETRANGE(Type,"Intrastat Jnl. Line".Type::Receipt)
                   else
                     if IntrastatSetup."Report Shipments" then
                       "Intrastat Jnl. Line".SETRANGE(Type,"Intrastat Jnl. Line".Type::Shipment)
                 END;

    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the file name.';
      Text001@1001 : TextConst 'ENU=WwWw';
      Text002@1002 : TextConst 'ENU=INTRASTAT';
      Text003@1003 : TextConst 'ENU=It is not possible to display %1 in a field with a length of %2.';
      IntrastatJnlLine3@1004 : Record "Intrastat Jnl. Line";
      IntrastatJnlLine4@1005 : Record "Intrastat Jnl. Line";
      IntrastatJnlLine5@1026 : Record "Intrastat Jnl. Line";
      CompanyInfo@1006 : Record "Company Information";
      Country@1007 : Record "Country/Region";
      FileMgt@1030 : Codeunit "File Management";
      IntraFile@1008 : File;
      QuantityAmt@1023 : Decimal;
      StatisticalValueAmt@1024 : Decimal;
      StatisticalValueTotalAmt@1028 : Decimal;
      TotalWeightAmt@1025 : Decimal;
      FileName@1009 : Text;
      IntraReferenceNo@1010 : Text[10];
      CompoundField@1011 : Text[40];
      TempCompoundField@1012 : Text[40];
      ServerFileName@1020 : Text;
      TempType@1013 : Integer;
      NoOfEntries@1014 : Text[3];
      Receipt@1015 : Boolean;
      Shipment@1016 : Boolean;
      VATRegNo@1017 : Code[20];
      ImportExport@1018 : Code[1];
      OK@1019 : Boolean;
      DefaultFilenameTxt@1022 : TextConst '@@@={Locked};ENU=Default.txt';
      GroupTotal@1027 : Boolean;

    LOCAL PROCEDURE DecimalNumeralZeroFormat@1(DecimalNumeral@1000 : Decimal;Length@1001 : Integer) : Text[250];
    BEGIN
      exit(TextZeroFormat(DELCHR(FORMAT(ROUND(ABS(DecimalNumeral),1,'<'),0,1)),Length));
    END;

    LOCAL PROCEDURE TextZeroFormat@2(Text@1000 : Text[250];Length@1001 : Integer) : Text[250];
    BEGIN
      if STRLEN(Text) > Length then
        ERROR(
          Text003,
          Text,Length);
      exit(PADSTR('',Length - STRLEN(Text),'0') + Text);
    END;

    [External]
    PROCEDURE InitializeRequest@4(newServerFileName@1000 : Text);
    BEGIN
      ServerFileName := newServerFileName;
    END;

    [Internal]
    PROCEDURE WriteGrTotalsToFile@3(TotalWeightAmt@1000 : Decimal;QuantityAmt@1001 : Decimal;StatisticalValueAmt@1002 : Decimal);
    BEGIN
      with IntrastatJnlLine2 do begin
        OK := COPYSTR("Internal Ref. No.",8,3) = '001';
        if OK then begin
          IntrastatJnlLine3.SETRANGE(
            "Internal Ref. No.",
            COPYSTR("Internal Ref. No.",1,7) + '000',
            COPYSTR("Internal Ref. No.",1,7) + '999');
          IntrastatJnlLine3.FINDLAST;
          NoOfEntries := COPYSTR(IntrastatJnlLine3."Internal Ref. No.",8,3);
        end;
        ImportExport := INCSTR(FORMAT(Type,1,2));

        if Type = Type::Receipt then
          Receipt := true
        else
          Shipment := true;
        Country.GET("Country/Region Code");
        Country.TESTFIELD("Intrastat Code");

        if OK then
          IntraFile.WRITE(
            FORMAT(
              '02' +
              TextZeroFormat(DELCHR(NoOfEntries),3) +
              FORMAT(COPYSTR(IntrastatJnlLine3."Internal Ref. No.",1,7) + '000',10) +
              FORMAT(VATRegNo,8) + FORMAT(ImportExport,1) + FORMAT(IntraReferenceNo,4),
              80));

        IntraFile.WRITE(
          FORMAT(
            '03' +
            TextZeroFormat(COPYSTR("Internal Ref. No.",8,3),3) +
            FORMAT("Internal Ref. No.",10) + FORMAT(Country."Intrastat Code",3) + FORMAT("Transaction Type",2) +
            '0' + FORMAT("Transport Method",1) + PADSTR("Tariff No.",9,'0') +
            DecimalNumeralZeroFormat(ROUND(TotalWeightAmt,1,'>'),15) +
            DecimalNumeralZeroFormat(QuantityAmt,10) +
            DecimalNumeralZeroFormat(StatisticalValueAmt,15),
            80));
      end;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

