OBJECT Table 7861 MS- PayPal Standard Template
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=MS- PayPal Standard Template;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code }
    { 2   ;   ;Name                ;Text250       ;CaptionML=ENU=Name;
                                                   NotBlank=true }
    { 3   ;   ;Description         ;Text250       ;CaptionML=ENU=Description;
                                                   NotBlank=true }
    { 8   ;   ;Terms of Service    ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=ENU=Terms of Service }
    { 11  ;   ;Logo                ;BLOB          ;CaptionML=ENU=Logo;
                                                   SubType=Bitmap }
    { 12  ;   ;Target URL          ;BLOB          ;CaptionML=ENU=Target URL;
                                                   SubType=Bitmap }
    { 13  ;   ;Logo URL            ;BLOB          ;CaptionML=ENU=Logo URL;
                                                   SubType=Bitmap }
    { 14  ;   ;Logo Last Update DateTime;DateTime ;CaptionML=ENU=Logo Last Update DateTime }
    { 15  ;   ;Logo Update Frequency;Duration     ;CaptionML=ENU=Logo Update Frequency }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
    { 1   ;Description         ;Description                              }
  }
  CODE
  {
    VAR
      UpdatingPayPalLogoFailedTxt@1000 : TextConst 'ENU=Updating PayPal logo failed.';
      PayPalContextTxt@1001 : TextConst 'ENU=PayPal Standard';

    PROCEDURE GetTargetURL@10() : Text;
    VAR
      InStream@1000 : InStream;
      TargetURL@1002 : Text;
    BEGIN
      TargetURL := '';
      CALCFIELDS("Target URL");
      if "Target URL".HASVALUE then begin
        "Target URL".CREATEINSTREAM(InStream);
        InStream.READ(TargetURL);
      end;
      exit(TargetURL);
    END;

    PROCEDURE SetTargetURL@20(TargetURL@1000 : Text);
    VAR
      WebRequestHelper@1002 : Codeunit "Web Request Helper";
      OutStream@1001 : OutStream;
    BEGIN
      WebRequestHelper.IsValidUri(TargetURL);
      WebRequestHelper.IsHttpUrl(TargetURL);
      WebRequestHelper.IsSecureHttpUrl(TargetURL);

      "Target URL".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(TargetURL);
      MODIFY;
    END;

    PROCEDURE SetTargetURLNoVerification@120(TargetURL@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Target URL".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(TargetURL);
      MODIFY;
    END;

    PROCEDURE GetLogoURL@2() : Text;
    VAR
      InStream@1000 : InStream;
      LogoURL@1002 : Text;
    BEGIN
      LogoURL := '';
      CALCFIELDS("Logo URL");
      if "Logo URL".HASVALUE then begin
        "Logo URL".CREATEINSTREAM(InStream);
        InStream.READ(LogoURL);
      end;
      exit(LogoURL);
    END;

    PROCEDURE SetLogoURL@1(LogoURL@1000 : Text);
    VAR
      WebRequestHelper@1002 : Codeunit "Web Request Helper";
      OutStream@1001 : OutStream;
    BEGIN
      WebRequestHelper.IsValidUri(LogoURL);
      WebRequestHelper.IsHttpUrl(LogoURL);
      WebRequestHelper.IsSecureHttpUrl(LogoURL);

      "Logo URL".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(LogoURL);
      MODIFY;
    END;

    PROCEDURE SetLogoURLNoVerification@131(LogoURL@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Logo URL".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(LogoURL);
      MODIFY;
    END;

    PROCEDURE RefreshLogoIfNeeded@5();
    BEGIN
      if ("Logo Last Update DateTime" <= (CURRENTDATETIME - "Logo Update Frequency")) and ("Logo Update Frequency" <> 0) then
        if UpdateLogoFromURL(GetLogoURL) then
          exit;

      CALCFIELDS(Logo);
    END;

    PROCEDURE UpdateLogoFromURL@18(LogoURL@1000 : Text) : Boolean;
    VAR
      TempBlob@1006 : TEMPORARY Record TempBlob;
      ActivityLog@1001 : Record "Activity Log";
      ResponseInStream@1002 : InStream;
      LogoOutStream@1005 : OutStream;
    BEGIN
      if LogoURL = '' then
        exit(false);

      TempBlob.INIT;
      TempBlob.Blob.CREATEINSTREAM(ResponseInStream);
      TempBlob.INSERT;

      if not DownloadLogo(ResponseInStream,LogoURL) then begin
        ActivityLog.LogActivity(Rec,ActivityLog.Status::Failed,PayPalContextTxt,UpdatingPayPalLogoFailedTxt,GETLASTERRORTEXT);
        exit(false);
      end;

      Logo.CREATEOUTSTREAM(LogoOutStream);
      COPYSTREAM(LogoOutStream,ResponseInStream);
      "Logo Last Update DateTime" := CURRENTDATETIME;
      MODIFY(true);
      exit(true);
    END;

    LOCAL PROCEDURE DownloadLogo@3(VAR ResponseInStream@1000 : InStream;LogoURL@1004 : Text) : Boolean;
    VAR
      HttpWebRequestMgt@1005 : Codeunit "Http Web Request Mgt.";
      HttpStatusCode@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
    BEGIN
      HttpWebRequestMgt.Initialize(LogoURL);
      HttpWebRequestMgt.DisableUI;
      exit(HttpWebRequestMgt.GetResponse(ResponseInStream,HttpStatusCode,ResponseHeaders));
    END;

    BEGIN
    END.
  }
}

