OBJECT Table 318 Tax Area
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               SetLastModifiedDateTime;
             END;

    OnModify=BEGIN
               SetLastModifiedDateTime;
             END;

    OnRename=BEGIN
               SetLastModifiedDateTime;
             END;

    CaptionML=ENU=Tax Area;
    LookupPageID="Tax Area List";
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 2   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 10  ;   ;Last Modified Date Time;DateTime   ;CaptionML=ENU=Last Modified Date Time;
                                                   Editable=false }
    { 8000;   ;Id                  ;GUID          ;CaptionML=ENU=Id }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE CreateTaxArea@1(NewTaxAreaCode@1000 : Code[20];City@1001 : Text[50];State@1002 : Text[50]);
    BEGIN
      INIT;
      Code := NewTaxAreaCode;
      Description := NewTaxAreaCode;
      if INSERT then;

      if City <> '' then
        CreateTaxAreaLine(Code,COPYSTR(City,1,10));
      if State <> '' then
        CreateTaxAreaLine(Code,COPYSTR(State,1,10));
      if (City = '') and (State = '') then
        CreateTaxAreaLine(Code,COPYSTR(NewTaxAreaCode,1,10));
    END;

    LOCAL PROCEDURE CreateTaxAreaLine@4(NewTaxArea@1000 : Code[20];NewJurisdictionCode@1001 : Code[10]);
    VAR
      TaxAreaLine@1002 : Record "Tax Area Line";
      TaxJurisdiction@1003 : Record "Tax Jurisdiction";
    BEGIN
      if TaxAreaLine.GET(NewTaxArea,NewJurisdictionCode) then
        exit;
      TaxAreaLine.INIT;
      TaxAreaLine."Tax Area" := NewTaxArea;
      TaxAreaLine."Tax Jurisdiction Code" := NewJurisdictionCode;
      TaxAreaLine.INSERT;
      TaxJurisdiction.CreateTaxJurisdiction(NewJurisdictionCode);
    END;

    PROCEDURE GetDescriptionInCurrentLanguage@5() : Text[50];
    VAR
      TaxAreaTranslation@1001 : Record "Tax Area Translation";
      Language@1002 : Record Language;
    BEGIN
      if TaxAreaTranslation.GET(Code,Language.GetUserLanguage) then
        exit(TaxAreaTranslation.Description);
      exit(Description);
    END;

    LOCAL PROCEDURE SetLastModifiedDateTime@16();
    BEGIN
      "Last Modified Date Time" := CURRENTDATETIME;
    END;

    BEGIN
    END.
  }
}

