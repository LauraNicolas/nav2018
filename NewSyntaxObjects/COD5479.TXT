OBJECT Codeunit 5479 Graph Mgt - Customer Payments
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GraphMgtJournal@1000 : Codeunit "Graph Mgt - Journal";

    PROCEDURE SetCustomerPaymentsTemplateAndBatch@1(VAR GenJournalLine@1001 : Record "Gen. Journal Line";CustomerPaymentBatchName@1003 : Code[10]);
    BEGIN
      GenJournalLine.VALIDATE("Journal Template Name",GraphMgtJournal.GetDefaultCustomerPaymentsTemplateName);
      GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");

      GenJournalLine.VALIDATE("Journal Batch Name",CustomerPaymentBatchName);
      GenJournalLine.SETRANGE("Journal Batch Name",CustomerPaymentBatchName);
    END;

    PROCEDURE SetCustomerPaymentsFilters@9(VAR GenJournalLine@1000 : Record "Gen. Journal Line");
    BEGIN
      GenJournalLine.SETRANGE("Document Type",GenJournalLine."Document Type"::Payment);
      GenJournalLine.SETRANGE("Account Type",GenJournalLine."Account Type"::Customer);
      GenJournalLine.SETRANGE("Applies-to Doc. Type",GenJournalLine."Applies-to Doc. Type"::Invoice);
      GenJournalLine.SETRANGE("Journal Template Name",GraphMgtJournal.GetDefaultCustomerPaymentsTemplateName);
    END;

    PROCEDURE SetCustomerPaymentsValues@10(VAR GenJournalLine@1000 : Record "Gen. Journal Line";TempGenJournalLine@1001 : TEMPORARY Record "Gen. Journal Line");
    VAR
      DummyDate@1002 : Date;
    BEGIN
      GenJournalLine.VALIDATE("Account Type",GenJournalLine."Account Type"::Customer);
      if not ISNULLGUID(TempGenJournalLine."Account Id") then
        GenJournalLine.VALIDATE("Account Id",TempGenJournalLine."Account Id");
      GenJournalLine.VALIDATE("Account No.",TempGenJournalLine."Account No.");
      if TempGenJournalLine."Contact Graph Id" <> '' then
        GenJournalLine.VALIDATE("Contact Graph Id",TempGenJournalLine."Contact Graph Id");
      if TempGenJournalLine."Posting Date" <> DummyDate then
        GenJournalLine.VALIDATE("Posting Date",TempGenJournalLine."Posting Date");
      GenJournalLine.VALIDATE("Document Type",GenJournalLine."Document Type"::Payment);
      GenJournalLine.VALIDATE("Document No.",TempGenJournalLine."Document No.");
      GenJournalLine.VALIDATE("External Document No.",TempGenJournalLine."External Document No.");
      GenJournalLine.VALIDATE(Amount,TempGenJournalLine.Amount);
      GenJournalLine.VALIDATE("Applies-to Doc. Type",GenJournalLine."Applies-to Doc. Type"::Invoice);
      GenJournalLine."Applies-to Doc. No." := TempGenJournalLine."Applies-to Doc. No.";
      if not ISNULLGUID(TempGenJournalLine."Applies-to Invoice Id") then
        GenJournalLine.VALIDATE("Applies-to Invoice Id",TempGenJournalLine."Applies-to Invoice Id");
      if TempGenJournalLine.Description <> '' then
        GenJournalLine.VALIDATE(Description,TempGenJournalLine.Description);
      GenJournalLine.VALIDATE(Comment,TempGenJournalLine.Comment);
      GenJournalLine.VALIDATE("Bal. Account No.",'');
    END;

    PROCEDURE UpdateIntegrationRecords@2(OnlyItemsWithoutId@1000 : Boolean);
    VAR
      GenJnlLine@1003 : Record "Gen. Journal Line";
      GraphMgtGeneralTools@1002 : Codeunit "Graph Mgt - General Tools";
      GenJnlLineRecordRef@1001 : RecordRef;
    BEGIN
      GenJnlLineRecordRef.OPEN(DATABASE::"Gen. Journal Line");
      GraphMgtGeneralTools.UpdateIntegrationRecords(GenJnlLineRecordRef,GenJnlLine.FIELDNO(Id),OnlyItemsWithoutId);
    END;

    LOCAL PROCEDURE EnableAccountODataWebService@1008();
    BEGIN
      UpdateIntegrationRecords(false);
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnCustomerPayments@1016(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummyGenJnlLine@1004 : Record "Gen. Journal Line";
      GraphMgtGeneralTools@1003 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(
        RecRef,NewId,Handled,DATABASE::"Gen. Journal Line",DummyGenJnlLine.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@1017(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummyGenJnlLine@1004 : Record "Gen. Journal Line";
      GraphMgtGeneralTools@1003 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      GraphMgtGeneralTools.HandleGetPredefinedIdValue(
        Id,RecRef,Handled,DATABASE::"Gen. Journal Line",DummyGenJnlLine.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@1003();
    BEGIN
      EnableAccountODataWebService;
      UpdateIds;
    END;

    PROCEDURE UpdateIds@4();
    VAR
      GenJournalLine@1000 : Record "Gen. Journal Line";
    BEGIN
      with GenJournalLine do begin
        SETRANGE("Account Type","Account Type"::Customer);

        if FINDSET then
          repeat
            UpdateCustomerID;
            UpdateGraphContactId;
            UpdateAppliesToInvoiceID;
            UpdateJournalBatchID;
            MODIFY(false);
          until NEXT = 0;
      end;
    END;

    BEGIN
    END.
  }
}

