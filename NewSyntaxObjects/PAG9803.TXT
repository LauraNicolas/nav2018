OBJECT Page 9803 Permissions
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Permissions;
    SourceTable=Permission;
    DelayedInsert=true;
    PopulateAllFields=true;
    DataCaptionFields="Object Type","Object Name";
    PageType=Worksheet;
    SourceTableTemporary=true;
    PromotedActionCategoriesML=ENU=New,Process,Report,Read,Insert,Modify,Delete,Execute;
    ShowFilter=false;
    OnOpenPage=VAR
                 PermissionSet@1000 : Record "Permission Set";
               BEGIN
                 if CurrentRoleID = '' then
                   if GETFILTER("Role ID") <> '' then
                     CurrentRoleID := GETRANGEMIN("Role ID")
                   else
                     if PermissionSet.FINDFIRST then
                       CurrentRoleID := PermissionSet."Role ID";
                 RESET;
                 FillTempPermissions;
               END;

    OnAfterGetRecord=BEGIN
                       SetObjectZeroName(Rec);
                       ZeroObjStyleExpr := "Object ID" = 0;
                       IsValidatedObjectID := false;
                       IsNewRecord := false;
                     END;

    OnNewRecord=BEGIN
                  ActivateControls;
                  PermissionRecExists := false;
                  IsNewRecord := true;
                  IsValidatedObjectID := false;
                END;

    OnInsertRecord=VAR
                     Permission@1000 : Record Permission;
                   BEGIN
                     if ("Object ID" = 0) and ((Show = Show::All) or IsValidatedObjectID) then
                       exit(false);
                     if ("Execute Permission" = "Execute Permission"::" ") and
                        ("Read Permission" = "Read Permission"::" ") and
                        ("Insert Permission" = "Insert Permission"::" ") and
                        ("Modify Permission" = "Modify Permission"::" ") and
                        ("Delete Permission" = "Delete Permission"::" ")
                     then
                       exit(false);

                     if "Object Type" = "Object Type"::"Table Data" then
                       "Execute Permission" := "Execute Permission"::" "
                     else begin
                       "Read Permission" := "Read Permission"::" ";
                       "Insert Permission" := "Insert Permission"::" ";
                       "Modify Permission" := "Modify Permission"::" ";
                       "Delete Permission" := "Delete Permission"::" ";
                     end;
                     Permission := Rec;
                     Permission.INSERT;
                     if AddRelatedTables then
                       DoAddRelatedTables(Rec);
                     Rec := Permission;
                     SetObjectZeroName(Rec);
                     PermissionRecExists := true;
                     IsNewRecord := false;
                     ZeroObjStyleExpr := "Object ID" = 0;
                     exit(true);
                   END;

    OnModifyRecord=BEGIN
                     ModifyRecord(Rec);
                     PermissionRecExists := true;
                     IsNewRecord := false;
                     exit(MODIFY);
                   END;

    OnDeleteRecord=VAR
                     Permission@1000 : Record Permission;
                   BEGIN
                     if (Show = Show::All) and ("Object ID" <> 0) then
                       exit(false);
                     Permission := Rec;
                     Permission.FIND;
                     exit(Permission.DELETE);
                   END;

    OnAfterGetCurrRecord=VAR
                           Permission@1000 : Record Permission;
                         BEGIN
                           ActivateControls;
                           SetObjectZeroName(Rec);
                           if not IsNewRecord then begin
                             Permission := Rec;
                             PermissionRecExists := Permission.FIND;
                           end else
                             PermissionRecExists := false;
                           AllowChangePrimaryKey := not PermissionRecExists and (Show = Show::"Only In Permission Set");
                           ZeroObjStyleExpr := PermissionRecExists and ("Object ID" = 0);
                         END;

    ActionList=ACTIONS
    {
      { 12      ;    ;ActionContainer;
                      Name=Set Permission;
                      CaptionML=ENU=Set Permission;
                      ActionContainerType=ActionItems }
      { 18      ;1   ;ActionGroup;
                      Name=Read Permission;
                      CaptionML=ENU=Read Permission;
                      ActionContainerType=NewDocumentItems;
                      Image=Ledger }
      { 41      ;2   ;ActionGroup;
                      Name=Allow Read;
                      CaptionML=ENU=Allow Read;
                      Image=Confirm }
      { 16      ;3   ;Action    ;
                      Name=AllowReadYes;
                      CaptionML=ENU=Yes;
                      ToolTipML=ENU=Allow read access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Approve;
                      OnAction=BEGIN
                                 UpdateSelected('R',"Read Permission"::Yes);
                               END;
                                }
      { 15      ;3   ;Action    ;
                      Name=AllowReadNo;
                      CaptionML=ENU=No;
                      ToolTipML=ENU=Disallow;
                      ApplicationArea=#Basic,#Suite;
                      Image=Reject;
                      OnAction=BEGIN
                                 UpdateSelected('R',"Read Permission"::" ");
                               END;
                                }
      { 17      ;3   ;Action    ;
                      Name=AllowReadIndirect;
                      CaptionML=ENU=Indirect;
                      ToolTipML=ENU=Allow indirect read access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Indent;
                      OnAction=BEGIN
                                 UpdateSelected('R',"Read Permission"::Indirect);
                               END;
                                }
      { 22      ;1   ;ActionGroup;
                      Name=Insert Permission;
                      CaptionML=ENU=Insert Permission;
                      ActionContainerType=NewDocumentItems;
                      Image=FiledPosted }
      { 43      ;2   ;ActionGroup;
                      Name=Allow Insertion;
                      CaptionML=ENU=Allow Insertion;
                      Image=Confirm }
      { 20      ;3   ;Action    ;
                      Name=AllowInsertYes;
                      CaptionML=ENU=Yes;
                      ToolTipML=ENU=Allow insertion access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Approve;
                      OnAction=BEGIN
                                 UpdateSelected('I',"Insert Permission"::Yes);
                               END;
                                }
      { 21      ;3   ;Action    ;
                      Name=AllowInsertNo;
                      CaptionML=ENU=No;
                      ToolTipML=ENU=Disallow;
                      ApplicationArea=#Basic,#Suite;
                      Image=Reject;
                      OnAction=BEGIN
                                 UpdateSelected('I',"Insert Permission"::" ");
                               END;
                                }
      { 19      ;3   ;Action    ;
                      Name=AllowInsertIndirect;
                      CaptionML=ENU=Indirect;
                      ToolTipML=ENU=Allow indirect insertion access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Indent;
                      OnAction=BEGIN
                                 UpdateSelected('I',"Insert Permission"::Indirect);
                               END;
                                }
      { 26      ;1   ;ActionGroup;
                      Name=Modify Permission;
                      CaptionML=ENU=Modify Permission;
                      ActionContainerType=NewDocumentItems;
                      Image=Statistics }
      { 44      ;2   ;ActionGroup;
                      Name=Allow Modification;
                      CaptionML=ENU=Allow Modification;
                      Image=Confirm }
      { 24      ;3   ;Action    ;
                      Name=AllowModifyYes;
                      CaptionML=ENU=Yes;
                      ToolTipML=ENU=Allow modification access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Approve;
                      OnAction=BEGIN
                                 UpdateSelected('M',"Modify Permission"::Yes);
                               END;
                                }
      { 25      ;3   ;Action    ;
                      Name=AllowModifyNo;
                      CaptionML=ENU=No;
                      ToolTipML=ENU=Disallow;
                      ApplicationArea=#Basic,#Suite;
                      Image=Reject;
                      OnAction=BEGIN
                                 UpdateSelected('M',"Modify Permission"::" ");
                               END;
                                }
      { 23      ;3   ;Action    ;
                      Name=AllowModifyIndirect;
                      CaptionML=ENU=Indirect;
                      ToolTipML=ENU=Allow indirect modification access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Indent;
                      OnAction=BEGIN
                                 UpdateSelected('M',"Modify Permission"::Indirect);
                               END;
                                }
      { 30      ;1   ;ActionGroup;
                      Name=Delete Permission;
                      CaptionML=ENU=Delete Permission;
                      ActionContainerType=NewDocumentItems;
                      Image=Transactions }
      { 45      ;2   ;ActionGroup;
                      Name=Allow Deletion;
                      CaptionML=ENU=Allow Deletion;
                      Image=Confirm }
      { 28      ;3   ;Action    ;
                      Name=AllowDeleteYes;
                      CaptionML=ENU=Yes;
                      ToolTipML=ENU=Allow delete access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Approve;
                      OnAction=BEGIN
                                 UpdateSelected('D',"Delete Permission"::Yes);
                               END;
                                }
      { 29      ;3   ;Action    ;
                      Name=AllowDeleteNo;
                      CaptionML=ENU=No;
                      ToolTipML=ENU=Disallow;
                      ApplicationArea=#Basic,#Suite;
                      Image=Reject;
                      OnAction=BEGIN
                                 UpdateSelected('D',"Delete Permission"::" ");
                               END;
                                }
      { 27      ;3   ;Action    ;
                      Name=AllowDeleteIndirect;
                      CaptionML=ENU=Indirect;
                      ToolTipML=ENU=Allow indirect delete access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Indent;
                      OnAction=BEGIN
                                 UpdateSelected('D',"Delete Permission"::Indirect);
                               END;
                                }
      { 33      ;1   ;ActionGroup;
                      Name=Execute Permission;
                      CaptionML=ENU=Execute Permission;
                      ActionContainerType=NewDocumentItems;
                      Image=Transactions }
      { 46      ;2   ;ActionGroup;
                      Name=Allow Execution;
                      CaptionML=ENU=Allow Execution;
                      Image=Confirm }
      { 31      ;3   ;Action    ;
                      Name=AllowExecuteYes;
                      CaptionML=ENU=Yes;
                      ToolTipML=ENU=Allow execution access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Approve;
                      OnAction=BEGIN
                                 UpdateSelected('X',"Execute Permission"::Yes);
                               END;
                                }
      { 32      ;3   ;Action    ;
                      Name=AllowExecuteNo;
                      CaptionML=ENU=No;
                      ToolTipML=ENU=Disallow;
                      ApplicationArea=#Basic,#Suite;
                      Image=Reject;
                      OnAction=BEGIN
                                 UpdateSelected('X',"Execute Permission"::" ");
                               END;
                                }
      { 14      ;3   ;Action    ;
                      Name=AllowExecuteIndirect;
                      CaptionML=ENU=Indirect;
                      ToolTipML=ENU=Allow indirect execution access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Indent;
                      OnAction=BEGIN
                                 UpdateSelected('X',"Execute Permission"::Indirect);
                               END;
                                }
      { 37      ;1   ;ActionGroup;
                      Name=All Permissions;
                      CaptionML=ENU=All Permissions;
                      ActionContainerType=NewDocumentItems;
                      Image=Transactions }
      { 47      ;2   ;ActionGroup;
                      Name=Allow All;
                      CaptionML=ENU=Allow All;
                      Image=Confirm }
      { 35      ;3   ;Action    ;
                      Name=AllowAllYes;
                      CaptionML=ENU=Yes;
                      ToolTipML=ENU=Allow full access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Approve;
                      OnAction=BEGIN
                                 UpdateSelected('*',"Read Permission"::Yes);
                               END;
                                }
      { 36      ;3   ;Action    ;
                      Name=AllowAllNo;
                      CaptionML=ENU=No;
                      ToolTipML=ENU=Disallow;
                      ApplicationArea=#Basic,#Suite;
                      Image=Reject;
                      OnAction=BEGIN
                                 UpdateSelected('*',"Read Permission"::" ");
                               END;
                                }
      { 34      ;3   ;Action    ;
                      Name=AllowAllIndirect;
                      CaptionML=ENU=Indirect;
                      ToolTipML=ENU=Allow indirect full access.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Indent;
                      OnAction=BEGIN
                                 UpdateSelected('*',"Read Permission"::Indirect);
                               END;
                                }
      { 52      ;1   ;ActionGroup;
                      Name=Manage Permission Sets;
                      CaptionML=ENU=Manage Permission Sets }
      { 54      ;2   ;Action    ;
                      Name=AddRelatedTablesAction;
                      CaptionML=ENU=Add Read Permission to Related Tables;
                      ToolTipML=ENU=Define read access to related tables.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Relationship;
                      OnAction=BEGIN
                                 AddRelatedTablesToSelected;
                               END;
                                }
      { 53      ;2   ;Action    ;
                      Name=IncludeExclude;
                      CaptionML=ENU=Include/Exclude Permission Set;
                      ToolTipML=ENU=Add or exclude a specific permission set.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=Edit;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 AddSubtractPermissionSet@1000 : Report "Add/Subtract Permission Set";
                               BEGIN
                                 AddSubtractPermissionSet.SetDestination(CurrentRoleID);
                                 AddSubtractPermissionSet.RUNMODAL;
                                 FillTempPermissions;
                               END;
                                }
      { 55      ;2   ;Action    ;
                      Name=ImportPermissions;
                      CaptionML=ENU=Import Permissions;
                      ToolTipML=ENU=Import a file with permissions.;
                      ApplicationArea=#Basic,#Suite;
                      Image=Import;
                      OnAction=BEGIN
                                 SETRANGE("Role ID","Role ID");
                                 XMLPORT.RUN(XMLPORT::"Import/Export Permissions",false,true,Rec);
                                 RESET;
                                 FillTempPermissions;
                               END;
                                }
      { 51      ;1   ;ActionGroup;
                      Name=Code Coverage Actions;
                      CaptionML=ENU=Record Permissions;
                      ActionContainerType=ActionItems }
      { 50      ;2   ;Action    ;
                      Name=Start;
                      CaptionML=ENU=Start;
                      ToolTipML=ENU=Start recording UI activities to generate the required permission set.;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NOT PermissionLoggingRunning;
                      Image=Start;
                      OnAction=BEGIN
                                 if not CONFIRM(StartRecordingQst) then
                                   exit;
                                 LogTablePermissions.Start;
                                 PermissionLoggingRunning := true;
                               END;
                                }
      { 48      ;2   ;Action    ;
                      Name=Stop;
                      CaptionML=ENU=Stop;
                      ToolTipML=ENU=Stop recording.;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=PermissionLoggingRunning;
                      Image=Stop;
                      OnAction=VAR
                                 TempTablePermissionBuffer@1000 : TEMPORARY Record "Table Permission Buffer";
                               BEGIN
                                 LogTablePermissions.Stop(TempTablePermissionBuffer);
                                 PermissionLoggingRunning := false;
                                 if not CONFIRM(AddPermissionsQst) then
                                   exit;
                                 AddLoggedPermissions(TempTablePermissionBuffer);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 38  ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 42  ;2   ;Group     ;
                Name=Group42;
                GroupType=Group }

    { 39  ;3   ;Field     ;
                Name=CurrentRoleID;
                CaptionML=ENU=Role ID;
                ToolTipML=ENU=Specifies the ID of the role that the permissions apply to.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CurrentRoleID;
                TableRelation="Permission Set"."Role ID";
                Importance=Promoted;
                OnValidate=BEGIN
                             FillTempPermissions;
                           END;

                ImplicitType=Code20 }

    { 40  ;3   ;Field     ;
                Name=Show;
                CaptionML=ENU=Show;
                ToolTipML=ENU=Specifies if the selected value is shown in the window.;
                OptionCaptionML=ENU=Only In Permission Set,All;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Show;
                OnValidate=BEGIN
                             FillTempPermissions;
                           END;

                ImplicitType=Option }

    { 49  ;2   ;Field     ;
                Name=AddRelatedTables;
                CaptionML=ENU=Add Read Permission to Related Tables;
                ToolTipML=ENU=Specifies that all tables that are related to the selected table will be added to the window with Read permission.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AddRelatedTables;
                ImplicitType=Boolean }

    { 2   ;1   ;Group     ;
                Name=Group;
                CaptionML=ENU=AllPermission;
                GroupType=Repeater }

    { 13  ;2   ;Field     ;
                Name=PermissionSet;
                CaptionML=ENU=Permission Set;
                ToolTipML=ENU=Specifies the ID of the permission sets that exist in the current database. This field is used internally.;
                ApplicationArea=#Advanced;
                SourceExpr="Role ID";
                Visible=FALSE;
                Enabled=FALSE;
                ImplicitType=Code20 }

    { 11  ;2   ;Field     ;
                Name=Object Type;
                ToolTipML=ENU=Specifies the type of object that the permissions apply to in the current database.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Object Type";
                Enabled=AllowChangePrimaryKey;
                Style=Strong;
                StyleExpr=ZeroObjStyleExpr;
                OnValidate=BEGIN
                             ActivateControls;
                           END;

                ImplicitType=Option }

    { 10  ;2   ;Field     ;
                Name=Object ID;
                ToolTipML=ENU=Specifies the ID of the object to which the permissions apply.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Object ID";
                Enabled=AllowChangePrimaryKey;
                LookupPageID="All Objects with Caption";
                Style=Strong;
                StyleExpr=ZeroObjStyleExpr;
                OnValidate=BEGIN
                             IsValidatedObjectID := false;
                             ActivateControls;
                           END;

                ImplicitType=Integer }

    { 9   ;2   ;Field     ;
                Name=ObjectName;
                CaptionML=ENU=Object Name;
                ToolTipML=ENU=Specifies the name of the object to which the permissions apply.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ObjectName;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=ZeroObjStyleExpr;
                ImplicitType=Text }

    { 8   ;2   ;Field     ;
                Name=Read Permission;
                ToolTipML=ENU=Specifies information about whether the permission set has read permission to this object. The values for the field are blank, Yes, and Indirect. Indirect means permission only through another object. If the field is empty, the permission set does not have read permission.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Read Permission";
                Enabled=IsTableData;
                Style=Strong;
                StyleExpr=ZeroObjStyleExpr;
                ImplicitType=Option }

    { 7   ;2   ;Field     ;
                Name=Insert Permission;
                ToolTipML=ENU=Specifies information about whether the permission set has insert permission to this object. The values for the field are blank, Yes, and Indirect. Indirect means permission only through another object. If the field is empty, the permission set does not have insert permission.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Insert Permission";
                Enabled=IsTableData;
                Style=Strong;
                StyleExpr=ZeroObjStyleExpr;
                ImplicitType=Option }

    { 6   ;2   ;Field     ;
                Name=Modify Permission;
                ToolTipML=ENU=Specifies information about whether the permission set has modify permission to this object. The values for the field are blank, Yes, and Indirect. Indirect means permission only through another object. If the field is empty, the permission set does not have modify permission.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Modify Permission";
                Enabled=IsTableData;
                Style=Strong;
                StyleExpr=ZeroObjStyleExpr;
                ImplicitType=Option }

    { 5   ;2   ;Field     ;
                Name=Delete Permission;
                ToolTipML=ENU=Specifies information about whether the permission set has delete permission to this object. The values for the field are blank, Yes, and Indirect. Indirect means permission only through another object. If the field is empty, the permission set does not have delete permission.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Delete Permission";
                Enabled=IsTableData;
                Style=Strong;
                StyleExpr=ZeroObjStyleExpr;
                ImplicitType=Option }

    { 4   ;2   ;Field     ;
                Name=Execute Permission;
                ToolTipML=ENU=Specifies information about whether the permission set has execute permission to this object. The values for the field are blank, Yes, and Indirect. Indirect means permission only through another object. If the field is empty, the permission set does not have execute permission.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Execute Permission";
                Enabled=NOT IsTableData;
                Style=Strong;
                StyleExpr=ZeroObjStyleExpr;
                ImplicitType=Option }

    { 3   ;2   ;Field     ;
                Name=Security Filter;
                ToolTipML=ENU=Specifies the security filter that is being applied to this permission set to limit the access that this permission set has to the data contained in this table.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Security Filter";
                Enabled=IsTableData;
                Style=Strong;
                StyleExpr=ZeroObjStyleExpr;
                OnAssistEdit=VAR
                               TableFilter@1000 : Record "Table Filter";
                               TableFilterPage@1001 : Page "Table Filter";
                             BEGIN
                               TableFilter.FILTERGROUP(2);
                               TableFilter.SETRANGE("Table Number","Object ID");
                               TableFilter.FILTERGROUP(0);
                               TableFilterPage.SETTABLEVIEW(TableFilter);
                               TableFilterPage.SetSourceTable(FORMAT("Security Filter"),"Object ID","Object Name");
                               if ACTION::OK = TableFilterPage.RUNMODAL then
                                 EVALUATE("Security Filter",TableFilterPage.CreateTextTableFilter(false));
                             END;

                ImplicitType=TableFilter }

  }
  CODE
  {
    VAR
      LogTablePermissions@1013 : Codeunit "Log Table Permissions";
      CurrentRoleID@1000 : Code[20];
      Show@1002 : '"Only In Permission Set",All';
      AddRelatedTables@1006 : Boolean;
      IsTableData@1001 : Boolean INDATASET;
      IsNewRecord@1010 : Boolean;
      IsValidatedObjectID@1012 : Boolean;
      PermissionRecExists@1008 : Boolean;
      AllowChangePrimaryKey@1004 : Boolean;
      AddPermissionsQst@1007 : TextConst 'ENU=Do you want to add the recorded permissions?';
      StartRecordingQst@1014 : TextConst 'ENU=Do you want to start the recording now?';
      AllObjTxt@1009 : TextConst '@@@="%1= type name, e.g. Table Data or Report or Page";ENU=All objects of type %1';
      ZeroObjStyleExpr@1011 : Boolean;
      PermissionLoggingRunning@1003 : Boolean;
      ObjectName@1005 : Text;

    LOCAL PROCEDURE FillTempPermissions@1003();
    VAR
      TempPermission@1000 : TEMPORARY Record Permission;
      Permission@1002 : Record Permission;
    BEGIN
      TempPermission.COPY(Rec,true);
      TempPermission.RESET;
      TempPermission.DELETEALL;
      FILTERGROUP(2);
      SETRANGE("Role ID",CurrentRoleID);
      Permission.SETRANGE("Role ID",CurrentRoleID);
      FILTERGROUP(0);

      if Permission.FIND('-') then
        repeat
          TempPermission := Permission;
          TempPermission.INSERT;
        until Permission.NEXT = 0;

      if Show = Show::All then
        FillTempPermissionsForAllObjects(TempPermission);
      IsNewRecord := false;
      if FIND('=<>') then;
      CurrPage.UPDATE(false);
    END;

    LOCAL PROCEDURE FillTempPermissionsForAllObjects@5(VAR Permission@1005 : Record Permission);
    VAR
      TempPermission@1007 : TEMPORARY Record Permission;
      AllObj@1006 : Record AllObj;
    BEGIN
      AllObj.SETRANGE("Object Type");
      TempPermission.COPY(Permission,true);
      TempPermission.INIT;
      if AllObj.FINDSET then
        repeat
          TempPermission."Object Type" := AllObj."Object Type";
          TempPermission."Object ID" := AllObj."Object ID";
          TempPermission."Read Permission" := "Read Permission"::" ";
          TempPermission."Insert Permission" := "Insert Permission"::" ";
          TempPermission."Modify Permission" := "Modify Permission"::" ";
          TempPermission."Delete Permission" := "Delete Permission"::" ";
          TempPermission."Execute Permission" := "Execute Permission"::" ";
          SetObjectZeroName(TempPermission);
          if TempPermission.INSERT then;
        until AllObj.NEXT = 0;
    END;

    LOCAL PROCEDURE ActivateControls@1();
    BEGIN
      IsTableData := "Object Type" = "Object Type"::"Table Data"
    END;

    LOCAL PROCEDURE ModifyRecord@59(VAR ModifiedPermission@1000 : Record Permission);
    VAR
      Permission@1001 : Record Permission;
      IsNewPermission@1003 : Boolean;
    BEGIN
      Permission.LOCKTABLE;
      IsNewPermission :=
        not Permission.GET(ModifiedPermission."Role ID",ModifiedPermission."Object Type",ModifiedPermission."Object ID");
      if IsNewPermission then begin
        Permission.TRANSFERFIELDS(ModifiedPermission,true);
        Permission.INSERT;
      end else begin
        Permission.TRANSFERFIELDS(ModifiedPermission,false);
        Permission.MODIFY;
      end;

      if (Permission."Read Permission" = 0) and
         (Permission."Insert Permission" = 0) and
         (Permission."Modify Permission" = 0) and
         (Permission."Delete Permission" = 0) and
         (Permission."Execute Permission" = 0)
      then begin
        Permission.DELETE;
        if Show = Show::"Only In Permission Set" then
          ModifiedPermission.DELETE;
        IsNewPermission := false;
      end;
      if IsNewPermission and AddRelatedTables then
        DoAddRelatedTables(ModifiedPermission);
    END;

    LOCAL PROCEDURE UpdateSelected@55(RIMDX@1000 : Text[1];PermissionOption@1001 : Option);
    VAR
      TempPermission@1003 : TEMPORARY Record Permission;
      OrgPermission@1002 : Record Permission;
    BEGIN
      OrgPermission := Rec;
      TempPermission.COPY(Rec,true);
      CurrPage.SETSELECTIONFILTER(TempPermission);

      if TempPermission.FINDSET then
        repeat
          case RIMDX of
            'R':
              if TempPermission."Object Type" = "Object Type"::"Table Data" then
                TempPermission."Read Permission" := PermissionOption;
            'I':
              if TempPermission."Object Type" = "Object Type"::"Table Data" then
                TempPermission."Insert Permission" := PermissionOption;
            'M':
              if TempPermission."Object Type" = "Object Type"::"Table Data" then
                TempPermission."Modify Permission" := PermissionOption;
            'D':
              if TempPermission."Object Type" = "Object Type"::"Table Data" then
                TempPermission."Delete Permission" := PermissionOption;
            'X':
              if TempPermission."Object Type" <> "Object Type"::"Table Data" then
                TempPermission."Execute Permission" := PermissionOption;
            '*':
              begin
                if TempPermission."Object Type" = "Object Type"::"Table Data" then begin
                  TempPermission."Read Permission" := PermissionOption;
                  TempPermission."Insert Permission" := PermissionOption;
                  TempPermission."Modify Permission" := PermissionOption;
                  TempPermission."Delete Permission" := PermissionOption;
                end else
                  TempPermission."Execute Permission" := PermissionOption;
              end;
          end;
          ModifyRecord(TempPermission);
          if GET(TempPermission."Role ID",TempPermission."Object Type",TempPermission."Object ID") then begin
            Rec := TempPermission;
            MODIFY;
          end;
        until TempPermission.NEXT = 0;

      Rec := OrgPermission;
      if FIND then;
    END;

    LOCAL PROCEDURE AddRelatedTablesToSelected@8();
    VAR
      TempPermission@1003 : TEMPORARY Record Permission;
    BEGIN
      TempPermission.COPY(Rec,true);
      CurrPage.SETSELECTIONFILTER(TempPermission);
      if TempPermission.FINDSET then
        repeat
          DoAddRelatedTables(TempPermission);
        until TempPermission.NEXT = 0;
      if FIND then;
    END;

    LOCAL PROCEDURE AddLoggedPermissions@11(VAR TablePermissionBuffer@1001 : Record "Table Permission Buffer");
    BEGIN
      TablePermissionBuffer.SETRANGE("Session ID",SESSIONID);
      if TablePermissionBuffer.FINDSET then
        repeat
          AddPermission(CurrentRoleID,
            TablePermissionBuffer."Object Type",
            TablePermissionBuffer."Object ID",
            TablePermissionBuffer."Read Permission",
            TablePermissionBuffer."Insert Permission",
            TablePermissionBuffer."Modify Permission",
            TablePermissionBuffer."Delete Permission",
            TablePermissionBuffer."Execute Permission");
        until TablePermissionBuffer.NEXT = 0;
      TablePermissionBuffer.DELETEALL;
    END;

    LOCAL PROCEDURE DoAddRelatedTables@7(VAR Permission@1000 : Record Permission);
    VAR
      TableRelationsMetadata@1002 : Record "Table Relations Metadata";
    BEGIN
      if Permission."Object Type" <> Permission."Object Type"::"Table Data" then
        exit;
      if Permission."Object ID" = 0 then
        exit;

      TableRelationsMetadata.SETRANGE("Table ID",Permission."Object ID");
      TableRelationsMetadata.SETFILTER("Related Table ID",'>0&<>%1',Permission."Object ID");
      if TableRelationsMetadata.FINDSET then
        repeat
          AddPermission(
            CurrentRoleID,"Object Type"::"Table Data",TableRelationsMetadata."Related Table ID","Read Permission"::Yes,
            "Insert Permission"::" ","Modify Permission"::" ","Delete Permission"::" ","Execute Permission"::" ");
        until TableRelationsMetadata.NEXT = 0;
    END;

    LOCAL PROCEDURE AddPermission@12(RoleID@1000 : Code[20];ObjectType@1001 : Option;ObjectID@1002 : Integer;AddRead@1007 : Option;AddInsert@1004 : Option;AddModify@1005 : Option;AddDelete@1006 : Option;AddExecute@1008 : Option) : Boolean;
    VAR
      Permission@1003 : Record Permission;
      LogTablePermissions@1009 : Codeunit "Log Table Permissions";
    BEGIN
      if not GET(RoleID,ObjectType,ObjectID) then begin
        INIT;
        "Role ID" := RoleID;
        "Object Type" := ObjectType;
        "Object ID" := ObjectID;
        "Read Permission" := "Read Permission"::" ";
        "Insert Permission" := "Insert Permission"::" ";
        "Modify Permission" := "Modify Permission"::" ";
        "Delete Permission" := "Delete Permission"::" ";
        "Execute Permission" := "Execute Permission"::" ";
        INSERT;
        Permission.TRANSFERFIELDS(Rec,true);
        Permission.INSERT;
      end;

      "Read Permission" := LogTablePermissions.GetMaxPermission("Read Permission",AddRead);
      "Insert Permission" := LogTablePermissions.GetMaxPermission("Insert Permission",AddInsert);
      "Modify Permission" := LogTablePermissions.GetMaxPermission("Modify Permission",AddModify);
      "Delete Permission" := LogTablePermissions.GetMaxPermission("Delete Permission",AddDelete);
      "Execute Permission" := LogTablePermissions.GetMaxPermission("Execute Permission",AddExecute);

      SetObjectZeroName(Rec);
      MODIFY;
      Permission.LOCKTABLE;
      if not Permission.GET(RoleID,ObjectType,ObjectID) then begin
        Permission.TRANSFERFIELDS(Rec,true);
        Permission.INSERT;
      end else begin
        Permission.TRANSFERFIELDS(Rec,false);
        Permission.MODIFY;
      end;
      exit(true);
    END;

    LOCAL PROCEDURE SetObjectZeroName@9(VAR Permission@1000 : Record Permission);
    BEGIN
      if Permission."Object ID" <> 0 then begin
        Permission.CALCFIELDS("Object Name");
        ObjectName := Permission."Object Name";
      end else
        ObjectName := COPYSTR(STRSUBSTNO(AllObjTxt,Permission."Object Type"),1,MAXSTRLEN(Permission."Object Name"));
    END;

    BEGIN
    END.
  }
}

