OBJECT Table 1002 Job Task Dimension
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               if "Dimension Value Code" = '' then
                 ERROR(Text001,TABLECAPTION);

               UpdateGlobalDim("Dimension Value Code");
             END;

    OnModify=BEGIN
               UpdateGlobalDim("Dimension Value Code");
             END;

    OnDelete=BEGIN
               UpdateGlobalDim('');
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=ENU=Job Task Dimension;
  }
  FIELDS
  {
    { 1   ;   ;Job No.             ;Code20        ;TableRelation="Job Task"."Job No.";
                                                   CaptionML=ENU=Job No.;
                                                   NotBlank=true;
                                                   Editable=false }
    { 2   ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE ("Job No."=FIELD("Job No."));
                                                   CaptionML=ENU=Job Task No.;
                                                   NotBlank=true }
    { 3   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension.Code;
                                                   OnValidate=BEGIN
                                                                if not DimMgt.CheckDim("Dimension Code") then
                                                                  ERROR(DimMgt.GetDimErr);
                                                                "Dimension Value Code" := '';
                                                              END;

                                                   CaptionML=ENU=Dimension Code }
    { 4   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE ("Dimension Code"=FIELD("Dimension Code"));
                                                   OnValidate=BEGIN
                                                                if not DimMgt.CheckDimValue("Dimension Code","Dimension Value Code") then
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=ENU=Dimension Value Code }
    { 5   ;   ;Multiple Selection Action;Option   ;CaptionML=ENU=Multiple Selection Action;
                                                   OptionCaptionML=ENU=" ,Change,Delete";
                                                   OptionString=" ",Change,Delete }
  }
  KEYS
  {
    {    ;"Job No.","Job Task No.","Dimension Code";
                                                   Clustered=true }
    {    ;"Dimension Code"                         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimMgt@1000 : Codeunit DimensionManagement;
      Text000@1002 : TextConst 'ENU=You cannot rename a %1.';
      Text001@1001 : TextConst 'ENU="At least one dimension value code must have a value. Enter a value or delete the %1. "';

    LOCAL PROCEDURE UpdateGlobalDim@2(DimensionValue@1001 : Code[20]);
    VAR
      JobTask@1000 : Record "Job Task";
      GLSEtup@1002 : Record "General Ledger Setup";
    BEGIN
      GLSEtup.GET;
      if "Dimension Code" = GLSEtup."Global Dimension 1 Code" then begin
        JobTask.GET("Job No.","Job Task No.");
        JobTask."Global Dimension 1 Code" := DimensionValue;
        JobTask.MODIFY(true);
      end else
        if "Dimension Code" = GLSEtup."Global Dimension 2 Code" then begin
          JobTask.GET("Job No.","Job Task No.");
          JobTask."Global Dimension 2 Code" := DimensionValue;
          JobTask.MODIFY(true);
        end;
    END;

    BEGIN
    END.
  }
}

