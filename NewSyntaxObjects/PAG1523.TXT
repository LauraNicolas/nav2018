OBJECT Page 1523 Workflow Response Options
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Workflow Response Options;
    SourceTable="Workflow Step Argument";
    PageType=CardPart;
    ShowFilter=false;
    OnOpenPage=VAR
                 ApprovalUserSetup@1000 : Page "Approval User Setup";
               BEGIN
                 NoArguments := NoArgumentsTxt;
                 ApprovalUserSetupLabel := STRSUBSTNO(OpenPageTxt,ApprovalUserSetup.CAPTION);
                 HideExternalUsers;
               END;

    OnFindRecord=BEGIN
                   exit(FIND(Which));
                 END;

    OnAfterGetRecord=BEGIN
                       SetVisibilityOptions;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           SetVisibilityOptions;
                           GetEventTable;
                           CALCFIELDS("Field Caption");
                           TableFieldCaption := "Field Caption";
                           ApplyAllValues := ("Field No." = 0);
                         END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Set Up Arguments;
                ContainerType=ContentArea }

    { 5   ;1   ;Group     ;
                Name=Group5;
                GroupType=Group }

    { 14  ;2   ;Group     ;
                Name=Group14;
                Visible="Response Option Group" = 'GROUP 0';
                GroupType=Group }

    { 16  ;3   ;Field     ;
                Name=NoArguments;
                ApplicationArea=#Suite;
                SourceExpr=NoArguments;
                Editable=FALSE;
                ImplicitType=Text;
                ShowCaption=false }

    { 7   ;2   ;Group     ;
                Name=Group7;
                Visible="Response Option Group" = 'GROUP 1';
                GroupType=Group }

    { 2   ;3   ;Field     ;
                Name=General Journal Template Name;
                ToolTipML=ENU=Specifies the name of the general journal template that is used for this workflow step argument.;
                ApplicationArea=#Suite;
                SourceExpr="General Journal Template Name";
                OnValidate=BEGIN
                             CurrPage.UPDATE(true)
                           END;

                ImplicitType=Code10;
                ShowMandatory=True }

    { 3   ;3   ;Field     ;
                Name=General Journal Batch Name;
                ToolTipML=ENU=Specifies the name of the general journal batch that is used for this workflow step argument.;
                ApplicationArea=#Suite;
                SourceExpr="General Journal Batch Name";
                OnValidate=BEGIN
                             CurrPage.UPDATE(true)
                           END;

                ImplicitType=Code10;
                ShowMandatory=True }

    { 8   ;2   ;Group     ;
                Name=Group8;
                Visible="Response Option Group" = 'GROUP 2';
                GroupType=Group }

    { 9   ;3   ;Field     ;
                Name=Link Target Page Approvals;
                CaptionML=ENU=Link Target Page;
                ToolTipML=ENU=Specifies a specific page that opens when a user chooses the link in a notification. If you do not fill this field, the page showing the involved record will open.;
                ApplicationArea=#Suite;
                SourceExpr="Link Target Page";
                ImplicitType=Integer }

    { 15  ;3   ;Field     ;
                Name=Custom Link Approvals;
                CaptionML=ENU=Custom Link;
                ToolTipML=ENU=Specifies a link that is inserted in the notification to link to a custom location.;
                ApplicationArea=#Suite;
                SourceExpr="Custom Link";
                ImplicitType=Text250 }

    { 23  ;2   ;Group     ;
                Name=Group23;
                Visible="Response Option Group" = 'GROUP 3';
                GroupType=Group }

    { 22  ;3   ;Field     ;
                Name=Notification User ID;
                CaptionML=ENU=Recipient User ID;
                ToolTipML=ENU=Specifies the ID of the user that will be notified in connection with this workflow step argument.;
                ApplicationArea=#Suite;
                SourceExpr="Notification User ID";
                OnValidate=BEGIN
                             CurrPage.UPDATE(true)
                           END;

                ImplicitType=Code50;
                ShowMandatory=True }

    { 20  ;3   ;Field     ;
                Name=Link Target Page;
                CaptionML=ENU=Link Target Page;
                ToolTipML=ENU=Specifies a specific page that opens when a user chooses the link in a notification. If you do not fill this field, the page showing the involved record will open.;
                ApplicationArea=#Suite;
                SourceExpr="Link Target Page";
                ImplicitType=Integer }

    { 19  ;3   ;Field     ;
                Name=Custom Link;
                ToolTipML=ENU=Specifies a link that is inserted in the notification to link to a custom location.;
                ApplicationArea=#Suite;
                SourceExpr="Custom Link";
                ImplicitType=Text250 }

    { 6   ;2   ;Group     ;
                Name=Group6;
                Visible="Response Option Group" = 'GROUP 4';
                GroupType=Group }

    { 17  ;3   ;Field     ;
                Name=MessageField;
                CaptionML=ENU=Message;
                ToolTipML=ENU=Specifies the message that will be shown as a response.;
                ApplicationArea=#Suite;
                SourceExpr=Message;
                OnValidate=BEGIN
                             CurrPage.UPDATE(true)
                           END;

                ImplicitType=Text250 }

    { 10  ;2   ;Group     ;
                Name=Group10;
                Visible="Response Option Group" = 'GROUP 5';
                GroupType=Group }

    { 25  ;3   ;Field     ;
                Name=Show Confirmation Message;
                ToolTipML=ENU=Specifies that a confirmation message is shown to users after they request an approval.;
                ApplicationArea=#Suite;
                SourceExpr="Show Confirmation Message";
                ImplicitType=Boolean }

    { 24  ;3   ;Field     ;
                Name=Due Date Formula;
                ToolTipML=ENU=Specifies in how many days the approval request must be resolved from the date when it was sent.;
                ApplicationArea=#Suite;
                SourceExpr="Due Date Formula";
                ImplicitType=DateFormula }

    { 21  ;3   ;Field     ;
                Name=Delegate After;
                ToolTipML=ENU=Specifies if and when an approval request will automatically be delegated to the relevant substitute. You can select to automatically delegate one, two, or five days after the date when the approval was requested.;
                ApplicationArea=#Suite;
                SourceExpr="Delegate After";
                ImplicitType=Option }

    { 11  ;3   ;Field     ;
                Name=Approver Type;
                ToolTipML=ENU=Specifies who is notified first about approval requests.;
                ApplicationArea=#Suite;
                SourceExpr="Approver Type";
                OnValidate=BEGIN
                             SetVisibilityOptions;
                             CurrPage.UPDATE(true)
                           END;

                ImplicitType=Option }

    { 4   ;3   ;Group     ;
                Name=Group4;
                Visible=ShowApprovalLimitType;
                GroupType=Group }

    { 12  ;4   ;Field     ;
                Name=Approver Limit Type;
                ToolTipML=ENU=Specifies how approvers' approval limits affect when approval request entries are created for them. A qualified approver is an approver whose approval limit is above the value on the approval request.;
                ApplicationArea=#Suite;
                SourceExpr="Approver Limit Type";
                OnValidate=BEGIN
                             CurrPage.UPDATE(true)
                           END;

                ImplicitType=Option }

    { 18  ;3   ;Group     ;
                Name=Group18;
                Visible=NOT ShowApprovalLimitType;
                GroupType=Group }

    { 13  ;4   ;Field     ;
                Name=Workflow User Group Code;
                ToolTipML=ENU=Specifies the workflow user group that is used in connection with this workflow step argument.;
                ApplicationArea=#Suite;
                SourceExpr="Workflow User Group Code";
                OnValidate=BEGIN
                             CurrPage.UPDATE(true)
                           END;

                ImplicitType=Code20;
                ShowMandatory=True }

    { 34  ;3   ;Group     ;
                Name=Group34;
                Visible=ShowApproverUserId;
                GroupType=Group }

    { 33  ;4   ;Field     ;
                Name=ApproverId;
                CaptionML=ENU=Approver ID;
                ToolTipML=ENU=Specifies the approver.;
                ApplicationArea=#Suite;
                SourceExpr="Approver User ID";
                OnValidate=BEGIN
                             CurrPage.UPDATE(true)
                           END;

                ImplicitType=Code50;
                ShowMandatory=True }

    { 26  ;3   ;Field     ;
                Name=ApprovalUserSetupLabel;
                ApplicationArea=#Suite;
                SourceExpr=ApprovalUserSetupLabel;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              PAGE.RUNMODAL(PAGE::"Approval User Setup");
                            END;

                ImplicitType=Text;
                ShowCaption=false }

    { 27  ;2   ;Group     ;
                Name=Group27;
                Visible="Response Option Group" = 'GROUP 6';
                GroupType=Group }

    { 28  ;3   ;Field     ;
                Name=TableFieldRevert;
                CaptionML=ENU=Field;
                ToolTipML=ENU=Specifies the field in which a change can occur that the workflow monitors.;
                ApplicationArea=#Suite;
                SourceExpr=TableFieldCaption;
                OnValidate=BEGIN
                             ValidateFieldCaption;
                           END;

                OnLookup=BEGIN
                           GetEventTable;
                           Text := LookupFieldCaption(FORMAT("Table No."),'');
                           exit(Text <> '')
                         END;

                ImplicitType=Text;
                ShowMandatory=True }

    { 29  ;2   ;Group     ;
                Name=Group29;
                Visible="Response Option Group" = 'GROUP 7';
                GroupType=Group }

    { 31  ;3   ;Field     ;
                Name=ApplyAllValues;
                CaptionML=ENU=Apply All New Values;
                ToolTipML=ENU=Specifies that all the new, approved values will be applied to the record.;
                ApplicationArea=#Suite;
                SourceExpr=ApplyAllValues;
                OnValidate=BEGIN
                             if ApplyAllValues then begin
                               "Table No." := 0;
                               "Field No." := 0;
                               CurrPage.UPDATE(true);
                             end;
                           END;

                ImplicitType=Boolean }

    { 32  ;3   ;Group     ;
                Name=Group32;
                Visible=NOT ApplyAllValues;
                GroupType=Group }

    { 30  ;4   ;Field     ;
                Name=TableFieldApply;
                CaptionML=ENU=Field;
                ToolTipML=ENU=Specifies the field in which a change can occur that the workflow monitors.;
                ApplicationArea=#Suite;
                SourceExpr=TableFieldCaption;
                OnValidate=BEGIN
                             ValidateFieldCaption;
                           END;

                OnLookup=BEGIN
                           Text := LookupFieldCaptionForApplyNewValues;
                           exit(Text <> '')
                         END;

                ImplicitType=Text;
                ShowMandatory=TRUE }

    { 35  ;2   ;Group     ;
                Name=Group35;
                Visible="Response Option Group" = 'GROUP 8';
                GroupType=Group }

    { 36  ;3   ;Field     ;
                Name=Response Type;
                ToolTipML=ENU=Specifies the response type for the workflow response. You cannot set options for this.;
                ApplicationArea=#Suite;
                SourceExpr="Response Type";
                OnValidate=BEGIN
                             SetVisibilityOptions;
                             CurrPage.UPDATE(true)
                           END;

                ImplicitType=Option }

    { 37  ;3   ;Group     ;
                Name=Group37;
                Visible=ShowResponseUserID;
                GroupType=Group }

    { 38  ;4   ;Field     ;
                Name=ResponseUserId;
                CaptionML=ENU=Response User ID;
                ToolTipML=ENU=Specifies the user necessary for an acceptable response.;
                ApplicationArea=#Suite;
                SourceExpr="Response User ID";
                OnValidate=BEGIN
                             CurrPage.UPDATE(true);
                           END;

                ImplicitType=Code50;
                ShowMandatory=True }

  }
  CODE
  {
    VAR
      NoArguments@1003 : Text;
      NoArgumentsTxt@1000 : TextConst 'ENU=You cannot set options for this workflow response.';
      ShowApprovalLimitType@1001 : Boolean;
      ShowApproverUserId@1004 : Boolean;
      ApprovalUserSetupLabel@1005 : Text;
      OpenPageTxt@1007 : TextConst '@@@=%1 is the page that will be opened when clicking the control;ENU=Open %1';
      TableFieldCaption@1009 : Text;
      ApplyAllValues@1002 : Boolean;
      ShowResponseUserID@1006 : Boolean;

    LOCAL PROCEDURE GetEventTable@1();
    VAR
      WorkflowStep@1001 : Record "Workflow Step";
      WorkflowStepEvent@1002 : Record "Workflow Step";
      WorkflowEvent@1000 : Record "Workflow Event";
    BEGIN
      WorkflowStep.SETRANGE(Argument,ID);
      if WorkflowStep.FINDFIRST then
        if WorkflowStep.HasParentEvent(WorkflowStepEvent) then begin
          WorkflowEvent.GET(WorkflowStepEvent."Function Name");
          "Table No." := WorkflowEvent."Table ID";
        end;
    END;

    LOCAL PROCEDURE SetVisibilityOptions@2();
    BEGIN
      CALCFIELDS("Response Option Group");
      ShowApprovalLimitType := "Approver Type" <> "Approver Type"::"Workflow User Group";
      ShowApproverUserId := ShowApprovalLimitType and ("Approver Limit Type" = "Approver Limit Type"::"Specific Approver");
      ShowResponseUserID := "Response Type" = "Response Type"::"User ID";
    END;

    LOCAL PROCEDURE LookupFieldCaption@4(TableNoFilter@1001 : Text;FieldNoFilter@1002 : Text) : Text;
    VAR
      Field@1000 : Record Field;
    BEGIN
      Field.FILTERGROUP(2);
      Field.SETFILTER(Type,STRSUBSTNO('%1|%2|%3|%4|%5|%6|%7|%8|%9|%10|%11|%12',
          Field.Type::Boolean,
          Field.Type::Text,
          Field.Type::Code,
          Field.Type::Decimal,
          Field.Type::Integer,
          Field.Type::BigInteger,
          Field.Type::Date,
          Field.Type::Time,
          Field.Type::DateTime,
          Field.Type::DateFormula,
          Field.Type::Option,
          Field.Type::Duration));
      Field.SETRANGE(Class,Field.Class::Normal);

      Field.SETFILTER(TableNo,TableNoFilter);
      Field.SETFILTER("No.",FieldNoFilter);
      Field.TableNo := "Table No.";
      Field."No." := "Field No.";
      if PAGE.RUNMODAL(PAGE::"Field List",Field) = ACTION::LookupOK then begin
        "Table No." := Field.TableNo;
        exit(Field."Field Caption");
      end;
      exit('');
    END;

    LOCAL PROCEDURE LookupFieldCaptionForApplyNewValues@17() : Text;
    VAR
      WorkflowStepApply@1001 : Record "Workflow Step";
      WorkflowStepRevert@1000 : Record "Workflow Step";
      WorkflowStepArgument@1004 : Record "Workflow Step Argument";
      WorkflowResponseHandling@1002 : Codeunit "Workflow Response Handling";
      FilterForField@1003 : Text;
      FilterForTable@1005 : Text;
      Separator@1007 : Text[1];
      AddSeparator@1006 : Boolean;
    BEGIN
      WorkflowStepApply.SETRANGE(Argument,ID);
      if WorkflowStepApply.FINDFIRST then begin
        WorkflowStepRevert.SETRANGE("Workflow Code",WorkflowStepApply."Workflow Code");
        WorkflowStepRevert.SETRANGE("Function Name",WorkflowResponseHandling.RevertValueForFieldCode);

        if WorkflowStepRevert.FINDSET then
          repeat
            WorkflowStepArgument.GET(WorkflowStepRevert.Argument);
            if WorkflowStepArgument."Field No." <> 0 then begin
              if AddSeparator then
                Separator := '|';
              AddSeparator := true;
              FilterForTable += Separator + FORMAT(WorkflowStepArgument."Table No.");
              FilterForField += Separator + FORMAT(WorkflowStepArgument."Field No.");
            end;
          until WorkflowStepRevert.NEXT = 0;

        exit(LookupFieldCaption(FilterForTable,FilterForField));
      end;
      exit('');
    END;

    LOCAL PROCEDURE ValidateFieldCaption@5();
    VAR
      Field@1000 : Record Field;
    BEGIN
      if TableFieldCaption <> '' then begin
        Field.SETRANGE(TableNo,"Table No.");
        Field.SETRANGE("Field Caption",TableFieldCaption);
        Field.FINDFIRST;
        "Field No." := Field."No."
      end else
        "Field No." := 0;

      CurrPage.UPDATE(true);
    END;

    BEGIN
    END.
  }
}

