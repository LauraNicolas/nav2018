OBJECT Codeunit 1415 Automatic Import of Bank Stmt.
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="Job Queue Entry";
    OnRun=VAR
            BankAccount@1002 : Record "Bank Account";
            DataExchDef@1001 : Record "Data Exch. Def";
            DataExch@1003 : Record "Data Exch.";
            BankAccReconciliation@1004 : Record "Bank Acc. Reconciliation";
            DummyBankAccReconciliationLine@1005 : Record "Bank Acc. Reconciliation Line";
            RecRef@1000 : RecordRef;
            LastStatementNo@1006 : Code[20];
          BEGIN
            TESTFIELD("Record ID to Process");
            RecRef.GET("Record ID to Process");
            RecRef.SETTABLE(BankAccount);

            if not BankAccount."Automatic Stmt. Import Enabled" then
              exit;

            BankAccount.GetDataExchDef(DataExchDef);

            DataExch."Related Record" := BankAccount.RECORDID;
            if not DataExch.ImportFileContent(DataExchDef) then
              exit;

            BankAccount.LOCKTABLE;
            LastStatementNo := BankAccount."Last Statement No.";
            BankAccReconciliation.CreateNewBankPaymentAppBatch(BankAccount."No.",BankAccReconciliation);

            if not BankAccReconciliation.ImportStatement(BankAccReconciliation,DataExch) then begin
              DeleteBankAccReconciliation(BankAccReconciliation,BankAccount,LastStatementNo);
              exit;
            end;

            if not DummyBankAccReconciliationLine.LinesExist(BankAccReconciliation) then begin
              DeleteBankAccReconciliation(BankAccReconciliation,BankAccount,LastStatementNo);
              exit;
            end;

            COMMIT;
            BankAccReconciliation.ProcessStatement(BankAccReconciliation);
          END;

  }
  CODE
  {

    LOCAL PROCEDURE DeleteBankAccReconciliation@2(VAR BankAccReconciliation@1000 : Record "Bank Acc. Reconciliation";VAR BankAccount@1002 : Record "Bank Account";LastStatementNo@1001 : Code[20]);
    BEGIN
      BankAccReconciliation.DELETE;
      BankAccount.FIND;
      BankAccount."Last Statement No." := LastStatementNo;
      BankAccount.MODIFY;
      COMMIT;
    END;

    BEGIN
    END.
  }
}

