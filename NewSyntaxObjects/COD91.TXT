OBJECT Codeunit 91 Purch.-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Purchase Header";
    EventSubscriberInstance=Manual;
    OnRun=VAR
            PurchaseHeader@1000 : Record "Purchase Header";
          BEGIN
            if not FIND then
              ERROR(NothingToPostErr);

            PurchaseHeader.COPY(Rec);
            Code(PurchaseHeader);
            Rec := PurchaseHeader;
          END;

  }
  CODE
  {
    VAR
      ReceiveInvoiceQst@1000 : TextConst 'ENU=&Receive,&Invoice,Receive &and Invoice';
      PostConfirmQst@1001 : TextConst '@@@="%1 = Document Type";ENU=Do you want to post the %1?';
      ShipInvoiceQst@1002 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice';
      NothingToPostErr@1007 : TextConst 'ENU=There is nothing to post.';

    LOCAL PROCEDURE Code@1(VAR PurchaseHeader@1002 : Record "Purchase Header");
    VAR
      PurchSetup@1000 : Record "Purchases & Payables Setup";
      PurchPostViaJobQueue@1001 : Codeunit "Purchase Post via Job Queue";
      HideDialog@1003 : Boolean;
    BEGIN
      HideDialog := false;

      OnBeforeConfirmPost(PurchaseHeader,HideDialog);
      if not HideDialog then
        if not ConfirmPost(PurchaseHeader) then
          exit;

      PurchSetup.GET;
      if PurchSetup."Post with Job Queue" then
        PurchPostViaJobQueue.EnqueuePurchDoc(PurchaseHeader)
      else
        CODEUNIT.RUN(CODEUNIT::"Purch.-Post",PurchaseHeader)
    END;

    LOCAL PROCEDURE ConfirmPost@4(VAR PurchaseHeader@1001 : Record "Purchase Header") : Boolean;
    VAR
      Selection@1000 : Integer;
    BEGIN
      with PurchaseHeader do begin
        case "Document Type" of
          "Document Type"::Order:
            begin
              Selection := STRMENU(ReceiveInvoiceQst,3);
              if Selection = 0 then
                exit(false);
              Receive := Selection in [1,3];
              Invoice := Selection in [2,3];
            end;
          "Document Type"::"Return Order":
            begin
              Selection := STRMENU(ShipInvoiceQst,3);
              if Selection = 0 then
                exit(false);
              Ship := Selection in [1,3];
              Invoice := Selection in [2,3];
            end
          else
            if not CONFIRM(PostConfirmQst,false,LOWERCASE(FORMAT("Document Type"))) then
              exit(false);
        end;
        "Print Posted Documents" := false;
      end;
      exit(true);
    END;

    [External]
    PROCEDURE Preview@2(VAR PurchaseHeader@1000 : Record "Purchase Header");
    VAR
      GenJnlPostPreview@1002 : Codeunit "Gen. Jnl.-Post Preview";
      PurchPostYesNo@1001 : Codeunit "Purch.-Post (Yes/No)";
    BEGIN
      BINDSUBSCRIPTION(PurchPostYesNo);
      GenJnlPostPreview.Preview(PurchPostYesNo,PurchaseHeader);
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnRunPreview@3(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1002 : Variant);
    VAR
      PurchaseHeader@1003 : Record "Purchase Header";
      PurchPost@1004 : Codeunit "Purch.-Post";
    BEGIN
      with PurchaseHeader do begin
        COPY(RecVar);
        Ship := "Document Type" = "Document Type"::"Return Order";
        Receive := "Document Type" = "Document Type"::Order;
        Invoice := true;
      end;
      PurchPost.SetPreviewMode(true);
      Result := PurchPost.RUN(PurchaseHeader);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeConfirmPost@6(VAR PurchaseHeader@1000 : Record "Purchase Header";VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

