OBJECT Report 5685 Copy Fixed Asset
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    Permissions=TableData "FA Depreciation Book"=ri;
    CaptionML=ENU=Copy Fixed Asset;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  DefaultDim.LOCKTABLE;
                  FADeprBook.LOCKTABLE;
                  FA.LOCKTABLE;
                  if FANo = '' then
                    ERROR(Text000,FA.TABLECAPTION,FA.FIELDCAPTION("No."));
                  if (FirstFANo = '') and not UseFANoSeries then
                    ERROR(Text001);
                  FA.GET(FANo);
                  FADeprBook."FA No." := FANo;
                  FADeprBook.SETRANGE("FA No.",FANo);
                  DefaultDim."Table ID" := DATABASE::"Fixed Asset";
                  DefaultDim."No." := FANo;
                  DefaultDim.SETRANGE("Table ID",DATABASE::"Fixed Asset");
                  DefaultDim.SETRANGE("No.",FANo);
                  DefaultDim2 := DefaultDim;
                  for I := 1 to NumberofCopies do begin
                    FA2 := FA;
                    FA2."No." := '';
                    FA2."Last Date Modified" := 0D;
                    FA2."Main Asset/Component" := FA2."Main Asset/Component"::" ";
                    FA2."Component of Main Asset" := '';
                    if UseFANoSeries then
                      FA2.INSERT(true)
                    else begin
                      FA2."No." := FirstFANo;
                      if NumberofCopies > 1 then
                        FirstFANo := INCSTR(FirstFANo);
                      if FA2."No." = '' then
                        ERROR(Text002,FA.TABLECAPTION,FA.FIELDCAPTION("No."));
                      FA2.INSERT(true);
                    end;
                    if DefaultDim.FIND('-') then
                      repeat
                        DefaultDim2 := DefaultDim;
                        DefaultDim2."No." := FA2."No.";
                        DefaultDim2.INSERT(true);
                      until DefaultDim.NEXT = 0;
                    if FADeprBook.FIND('-') then
                      repeat
                        FADeprBook2 := FADeprBook;
                        FADeprBook2."FA No." := FA2."No.";
                        FADeprBook2.INSERT(true);
                      until FADeprBook.NEXT = 0;
                    if FA2.FIND then begin;
                      FA2."Last Date Modified" := 0D;
                      FA2.MODIFY;
                    end;
                  end;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   if NumberofCopies < 1 then
                     NumberofCopies := 1;
                   FANo := FANo2;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  Name=Copy from FA No.;
                  CaptionML=ENU=Copy from FA No.;
                  ToolTipML=ENU=Specifies the number of the fixed asset that you want to copy from.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=FANo;
                  TableRelation="Fixed Asset" }

      { 3   ;2   ;Field     ;
                  Name=No. of Copies;
                  CaptionML=ENU=No. of Copies;
                  ToolTipML=ENU=Specifies the number of new fixed asset that you want to create.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=NumberofCopies;
                  MinValue=1 }

      { 9   ;2   ;Field     ;
                  Name=First FA No.;
                  CaptionML=ENU=First FA No.;
                  ToolTipML=ENU=Specifies the number of the first fixed asset. If No. of Copies is greater than 1, the First FA No. field must include a number, for example FA045.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=FirstFANo;
                  OnValidate=BEGIN
                               if FirstFANo <> '' then
                                 UseFANoSeries := false;
                             END;
                              }

      { 7   ;2   ;Field     ;
                  Name=Use FA No. Series;
                  CaptionML=ENU=Use FA No. Series;
                  ToolTipML=ENU=Specifies if you want the new fixed asset to have a number from the number series specified in Fixed Asset Nos. field in the Fixed Asset Setup window.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=UseFANoSeries;
                  OnValidate=BEGIN
                               if UseFANoSeries then
                                 FirstFANo := '';
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DefaultDim@1008 : Record "Default Dimension";
      DefaultDim2@1009 : Record "Default Dimension";
      FA@1004 : Record "Fixed Asset";
      FA2@1006 : Record "Fixed Asset";
      FADeprBook@1010 : Record "FA Depreciation Book";
      FADeprBook2@1011 : Record "FA Depreciation Book";
      FANo@1000 : Code[20];
      FANo2@1015 : Code[20];
      FirstFANo@1002 : Code[20];
      UseFANoSeries@1001 : Boolean;
      NumberofCopies@1003 : Integer;
      I@1005 : Integer;
      Text000@1012 : TextConst '@@@="%1: TABLECAPTION(Fixed Asset); %2: Field(No.)";ENU=You must specify a number in the Copy from %1 %2 field.';
      Text001@1013 : TextConst 'ENU=You must specify a number in First FA No. field or use the FA No. Series.';
      Text002@1014 : TextConst '@@@="%1: TABLECAPTION(Fixed Asset); %2: Field(No.)";ENU=You must include a number in the First FA %1 %2 field.';

    PROCEDURE SetFANo@1(NewFANo@1000 : Code[20]);
    BEGIN
      FANo2 := NewFANo;
    END;

    PROCEDURE InitializeRequest@2(NewFANo@1000 : Code[20];NewNumberofCopies@1001 : Integer;NewFirstFANo@1002 : Code[20];NewUseFANoSeries@1003 : Boolean);
    BEGIN
      NumberofCopies := NewNumberofCopies;
      FirstFANo := NewFirstFANo;
      UseFANoSeries := NewUseFANoSeries;
      FANo := NewFANo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

