OBJECT Codeunit 7305 Whse. Jnl.-B.Register
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo="Warehouse Journal Batch";
    OnRun=BEGIN
            WhseJnlBatch.COPY(Rec);
            Code;
            COPY(WhseJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to register the journals?';
      Text001@1001 : TextConst 'ENU=The journals were successfully registered.';
      Text002@1002 : TextConst 'ENU="It was not possible to register all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully registered are now marked.';
      WhseJnlTemplate@1004 : Record "Warehouse Journal Template";
      WhseJnlBatch@1005 : Record "Warehouse Journal Batch";
      WhseJnlLine@1006 : Record "Warehouse Journal Line";
      WhseJnlRegisterBatch@1007 : Codeunit "Whse. Jnl.-Register Batch";
      JnlWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with WhseJnlBatch do begin
        WhseJnlTemplate.GET("Journal Template Name");
        WhseJnlTemplate.TESTFIELD("Force Registering Report",false);

        if not CONFIRM(Text000,false) then
          exit;

        FIND('-');
        repeat
          WhseJnlLine."Journal Template Name" := "Journal Template Name";
          WhseJnlLine."Journal Batch Name" := Name;
          WhseJnlLine."Location Code" := "Location Code";
          WhseJnlLine."Line No." := 10000;
          CLEAR(WhseJnlRegisterBatch);
          if WhseJnlRegisterBatch.RUN(WhseJnlLine) then
            MARK(false)
          else begin
            MARK(true);
            JnlWithErrors := true;
          end;
        until NEXT = 0;

        if not JnlWithErrors then
          MESSAGE(Text001)
        else
          MESSAGE(
            Text002 +
            Text003);

        if not FIND('=><') then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Location Code","Location Code");
          FILTERGROUP(0);
          Name := '';
        end;
      end;
    END;

    BEGIN
    END.
  }
}

