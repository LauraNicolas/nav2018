OBJECT Codeunit 7323 Whse.-Act.-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Warehouse Activity Line";
    OnRun=BEGIN
            WhseActivLine.COPY(Rec);
            Code;
            COPY(WhseActivLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=&Receive,Receive &and Invoice';
      WhseActivLine@1007 : Record "Warehouse Activity Line";
      WhseActivityPost@1008 : Codeunit "Whse.-Activity-Post";
      Selection@1003 : Integer;
      Text001@1000 : TextConst 'ENU=&Ship,Ship &and Invoice';
      Text002@1001 : TextConst 'ENU=Do you want to post the %1 and %2?';
      PrintDoc@1004 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with WhseActivLine do begin
        if "Activity Type" = "Activity Type"::"Invt. Put-away" then begin
          if ("Source Document" = "Source Document"::"Prod. Output") or
             ("Source Document" = "Source Document"::"Inbound Transfer") or
             ("Source Document" = "Source Document"::"Prod. Consumption")
          then begin
            if not CONFIRM(Text002,false,"Activity Type","Source Document") then
              exit;
          end else begin
            Selection := STRMENU(Text000,2);
            if Selection = 0 then
              exit;
          end;
        end else
          if ("Source Document" = "Source Document"::"Prod. Consumption") or
             ("Source Document" = "Source Document"::"Outbound Transfer")
          then begin
            if not CONFIRM(Text002,false,"Activity Type","Source Document") then
              exit;
          end else begin
            Selection := STRMENU(Text001,2);
            if Selection = 0 then
              exit;
          end;

        WhseActivityPost.SetInvoiceSourceDoc(Selection = 2);
        WhseActivityPost.PrintDocument(PrintDoc);
        WhseActivityPost.RUN(WhseActivLine);
        CLEAR(WhseActivityPost);
      end;
    END;

    [External]
    PROCEDURE PrintDocument@2(SetPrint@1000 : Boolean);
    BEGIN
      PrintDoc := SetPrint;
    END;

    BEGIN
    END.
  }
}

