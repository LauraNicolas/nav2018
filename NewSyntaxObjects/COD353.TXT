OBJECT Codeunit 353 Item Availability Forms Mgt
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text012@1000 : TextConst '@@@="%1=FieldCaption, %2=OldDate, %3=NewDate";ENU=Do you want to change %1 from %2 to %3?';
      ItemAvailByBOMLevel@1003 : Page "Item Availability by BOM Level";
      ForecastName@1001 : Code[10];
      AvailabilityType@1002 : 'Date,Variant,Location,Bin,Event,BOM';

    LOCAL PROCEDURE CalcItemPlanningFields@13(VAR Item@1000 : Record Item;CalculateTransferQuantities@1002 : Boolean);
    VAR
      JobPlanningLine@1001 : Record "Job Planning Line";
    BEGIN
      with Item do begin
        INIT;
        CALCFIELDS(
          "Qty. on Purch. Order",
          "Qty. on Sales Order",
          "Qty. on Service Order",
          Inventory,
          "Net Change",
          "Scheduled Receipt (Qty.)",
          "Scheduled Need (Qty.)",
          "Planned Order Receipt (Qty.)",
          "FP Order Receipt (Qty.)",
          "Rel. Order Receipt (Qty.)",
          "Planned Order Release (Qty.)",
          "Purch. Req. Receipt (Qty.)",
          "Planning Issues (Qty.)",
          "Purch. Req. Release (Qty.)");

        if JobPlanningLine.READPERMISSION then
          CALCFIELDS("Qty. on Job Order");
        CALCFIELDS(
          "Qty. on Assembly Order",
          "Qty. on Asm. Component",
          "Qty. on Purch. Return",
          "Qty. on Sales Return");
        if CalculateTransferQuantities then
          CALCFIELDS(
            "Trans. Ord. Shipment (Qty.)",
            "Qty. in Transit",
            "Trans. Ord. Receipt (Qty.)");
      end;
    END;

    [External]
    PROCEDURE CalculateNeed@5(VAR Item@1004 : Record Item;VAR GrossRequirement@1003 : Decimal;VAR PlannedOrderReceipt@1002 : Decimal;VAR ScheduledReceipt@1001 : Decimal;VAR PlannedOrderReleases@1000 : Decimal);
    VAR
      TransOrdShipmentQty@1005 : Decimal;
      QtyinTransit@1006 : Decimal;
      TransOrdReceiptQty@1007 : Decimal;
    BEGIN
      CalcItemPlanningFields(Item,true);

      with Item do begin
        if GETFILTER("Location Filter") = '' then begin
          TransOrdShipmentQty := 0;
          QtyinTransit := 0;
          TransOrdReceiptQty := 0;
        end else begin
          TransOrdShipmentQty := "Trans. Ord. Shipment (Qty.)";
          QtyinTransit := "Qty. in Transit";
          TransOrdReceiptQty := "Trans. Ord. Receipt (Qty.)";
        end;
        GrossRequirement :=
          "Qty. on Sales Order" +
          "Qty. on Service Order" +
          "Qty. on Job Order" +
          "Scheduled Need (Qty.)" +
          TransOrdShipmentQty +
          "Planning Issues (Qty.)" +
          "Qty. on Asm. Component" +
          "Qty. on Purch. Return";
        PlannedOrderReceipt :=
          "Planned Order Receipt (Qty.)" +
          "Purch. Req. Receipt (Qty.)";
        ScheduledReceipt :=
          "FP Order Receipt (Qty.)" +
          "Rel. Order Receipt (Qty.)" +
          "Qty. on Purch. Order" +
          QtyinTransit +
          TransOrdReceiptQty +
          "Qty. on Assembly Order" +
          "Qty. on Sales Return";
        PlannedOrderReleases :=
          "Planned Order Release (Qty.)" +
          "Purch. Req. Release (Qty.)";
      end;
    END;

    LOCAL PROCEDURE CalcProjAvailableBalance@1(VAR Item@1000 : Record Item) : Decimal;
    VAR
      Item2@1005 : Record Item;
      GrossRequirement@1004 : Decimal;
      PlannedOrderReceipt@1003 : Decimal;
      ScheduledReceipt@1002 : Decimal;
      PlannedOrderReleases@1001 : Decimal;
    BEGIN
      Item2.COPY(Item);
      Item2.SETRANGE("Date Filter",0D,Item.GETRANGEMAX("Date Filter"));
      CalculateNeed(Item2,GrossRequirement,PlannedOrderReceipt,ScheduledReceipt,PlannedOrderReleases);
      exit(Item2.Inventory + PlannedOrderReceipt + ScheduledReceipt - GrossRequirement);
    END;

    LOCAL PROCEDURE CalcProjAvailableBalance2@4(Inventory@1008 : Decimal;GrossRequirement@1007 : Decimal;PlannedOrderReceipt@1006 : Decimal;ScheduledReceipt@1005 : Decimal) : Decimal;
    BEGIN
      exit(Inventory + PlannedOrderReceipt + ScheduledReceipt - GrossRequirement);
    END;

    [External]
    PROCEDURE CalcAvailQuantities@14(VAR Item@1001 : Record Item;IsBalanceAtDate@1009 : Boolean;VAR GrossRequirement@1002 : Decimal;VAR PlannedOrderRcpt@1003 : Decimal;VAR ScheduledRcpt@1004 : Decimal;VAR PlannedOrderReleases@1005 : Decimal;VAR ProjAvailableBalance@1006 : Decimal;VAR ExpectedInventory@1007 : Decimal;VAR QtyAvailable@1008 : Decimal);
    VAR
      AvailableMgt@1000 : Codeunit "Available Management";
    BEGIN
      CalculateNeed(Item,GrossRequirement,PlannedOrderRcpt,ScheduledRcpt,PlannedOrderReleases);
      if IsBalanceAtDate then
        ProjAvailableBalance :=
          CalcProjAvailableBalance2(Item.Inventory,GrossRequirement,PlannedOrderRcpt,ScheduledRcpt)
      else
        ProjAvailableBalance := CalcProjAvailableBalance(Item);
      ExpectedInventory := AvailableMgt.ExpectedQtyOnHand(Item,true,0,QtyAvailable,DMY2DATE(31,12,9999));
    END;

    [External]
    PROCEDURE ShowItemLedgerEntries@10(VAR Item@1002 : Record Item;NetChange@1001 : Boolean);
    VAR
      ItemLedgEntry@1000 : Record "Item Ledger Entry";
    BEGIN
      ItemLedgEntry.FindLinesWithItemToPlan(Item,NetChange);
      PAGE.RUN(0,ItemLedgEntry);
    END;

    [External]
    PROCEDURE ShowSalesLines@2(VAR Item@1001 : Record Item);
    VAR
      SalesLine@1000 : Record "Sales Line";
    BEGIN
      SalesLine.FindLinesWithItemToPlan(Item,SalesLine."Document Type"::Order);
      PAGE.RUN(0,SalesLine);
    END;

    [External]
    PROCEDURE ShowServLines@9(VAR Item@1001 : Record Item);
    VAR
      ServLine@1000 : Record "Service Line";
    BEGIN
      ServLine.FindLinesWithItemToPlan(Item);
      PAGE.RUN(0,ServLine);
    END;

    [External]
    PROCEDURE ShowJobPlanningLines@12(VAR Item@1001 : Record Item);
    VAR
      JobPlanningLine@1000 : Record "Job Planning Line";
    BEGIN
      JobPlanningLine.FindLinesWithItemToPlan(Item);
      PAGE.RUN(0,JobPlanningLine);
    END;

    [External]
    PROCEDURE ShowPurchLines@3(VAR Item@1001 : Record Item);
    VAR
      PurchLine@1000 : Record "Purchase Line";
    BEGIN
      PurchLine.FindLinesWithItemToPlan(Item,PurchLine."Document Type"::Order);
      PAGE.RUN(0,PurchLine);
    END;

    [External]
    PROCEDURE ShowSchedReceipt@7(VAR Item@1001 : Record Item);
    VAR
      ProdOrderLine@1000 : Record "Prod. Order Line";
    BEGIN
      ProdOrderLine.FindLinesWithItemToPlan(Item,true);
      PAGE.RUN(0,ProdOrderLine);
    END;

    [External]
    PROCEDURE ShowSchedNeed@8(VAR Item@1001 : Record Item);
    VAR
      ProdOrderComp@1000 : Record "Prod. Order Component";
    BEGIN
      ProdOrderComp.FindLinesWithItemToPlan(Item,true);
      PAGE.RUN(0,ProdOrderComp);
    END;

    [External]
    PROCEDURE ShowTransLines@6(VAR Item@1002 : Record Item;What@1000 : Integer);
    VAR
      TransLine@1001 : Record "Transfer Line";
    BEGIN
      case What of
        Item.FIELDNO("Trans. Ord. Shipment (Qty.)"):
          TransLine.FindLinesWithItemToPlan(Item,false,false);
        Item.FIELDNO("Qty. in Transit"),
        Item.FIELDNO("Trans. Ord. Receipt (Qty.)"):
          TransLine.FindLinesWithItemToPlan(Item,true,false);
      end;
      PAGE.RUN(0,TransLine);
    END;

    [External]
    PROCEDURE ShowAsmOrders@16(VAR Item@1001 : Record Item);
    VAR
      AssemblyHeader@1000 : Record "Assembly Header";
    BEGIN
      AssemblyHeader.FindLinesWithItemToPlan(Item,AssemblyHeader."Document Type"::Order);
      PAGE.RUN(0,AssemblyHeader);
    END;

    [External]
    PROCEDURE ShowAsmCompLines@15(VAR Item@1001 : Record Item);
    VAR
      AssemblyLine@1000 : Record "Assembly Line";
    BEGIN
      AssemblyLine.FindLinesWithItemToPlan(Item,AssemblyLine."Document Type"::Order);
      PAGE.RUN(0,AssemblyLine);
    END;

    [External]
    PROCEDURE ShowItemAvailLineList@11(VAR Item@1002 : Record Item;What@1000 : Integer);
    VAR
      ItemCopy@1003 : Record Item;
      ItemAvailLineList@1001 : Page "Item Availability Line List";
    BEGIN
      ItemCopy.COPY(Item);
      CalcItemPlanningFields(ItemCopy,ItemCopy.GETFILTER("Location Filter") <> '');
      ItemAvailLineList.Init(What,ItemCopy);
      ItemAvailLineList.RUNMODAL;
    END;

    [External]
    PROCEDURE ShowItemAvailFromItem@42(VAR Item@1004 : Record Item;AvailabilityType@1000 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      NewDate@1006 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with Item do begin
        TESTFIELD("No.");

        case AvailabilityType of
          AvailabilityType::Date:
            ShowItemAvailByDate(Item,'',NewDate,NewDate);
          AvailabilityType::Variant:
            ShowItemAvailVariant(Item,'',NewVariantCode,NewVariantCode);
          AvailabilityType::Location:
            ShowItemAvailByLoc(Item,'',NewLocationCode,NewLocationCode);
          AvailabilityType::"Event":
            ShowItemAvailByEvent(Item,'',NewDate,NewDate,false);
          AvailabilityType::BOM:
            ShowItemAvailByBOMLevel(Item,'',NewDate,NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromSalesLine@17(VAR SalesLine@1004 : Record "Sales Line";AvailabilityType@1000 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1005 : Record Item;
      AsmHeader@1003 : Record "Assembly Header";
      NewDate@1006 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with SalesLine do begin
        TESTFIELD(Type,Type::Item);
        TESTFIELD("No.");
        Item.RESET;
        Item.GET("No.");
        FilterItem(Item,"Location Code","Variant Code","Shipment Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Shipment Date"),"Shipment Date",NewDate) then
              VALIDATE("Shipment Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            if ShowItemAvailByEvent(Item,FIELDCAPTION("Shipment Date"),"Shipment Date",NewDate,false) then
              VALIDATE("Shipment Date",NewDate);
          AvailabilityType::BOM:
            if AsmToOrderExists(AsmHeader) then
              ShowItemAvailFromAsmHeader(AsmHeader,AvailabilityType)
            else
              if ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Shipment Date"),"Shipment Date",NewDate) then
                VALIDATE("Shipment Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromPurchLine@22(VAR PurchLine@1004 : Record "Purchase Line";AvailabilityType@1000 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1005 : Record Item;
      NewDate@1010 : Date;
      NewVariantCode@1007 : Code[10];
      NewLocationCode@1006 : Code[10];
    BEGIN
      with PurchLine do begin
        TESTFIELD(Type,Type::Item);
        TESTFIELD("No.");
        Item.RESET;
        Item.GET("No.");
        FilterItem(Item,"Location Code","Variant Code","Expected Receipt Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Expected Receipt Date"),"Expected Receipt Date",NewDate) then
              VALIDATE("Expected Receipt Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            if ShowItemAvailByEvent(Item,FIELDCAPTION("Expected Receipt Date"),"Expected Receipt Date",NewDate,false) then
              VALIDATE("Expected Receipt Date",NewDate);
          AvailabilityType::BOM:
            if ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Expected Receipt Date"),"Expected Receipt Date",NewDate) then
              VALIDATE("Expected Receipt Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromReqLine@18(VAR ReqLine@1007 : Record "Requisition Line";AvailabilityType@1005 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1008 : Record Item;
      NewDate@1003 : Date;
      NewVariantCode@1001 : Code[10];
      NewLocationCode@1000 : Code[10];
    BEGIN
      with ReqLine do begin
        TESTFIELD(Type,Type::Item);
        TESTFIELD("No.");
        Item.RESET;
        Item.GET("No.");
        FilterItem(Item,"Location Code","Variant Code","Due Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            begin
              Item.SETRANGE("Date Filter");

              ForecastName := '';
              FindCurrForecastName(ForecastName);

              if ShowItemAvailByEvent(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate,true) then
                VALIDATE("Due Date",NewDate);
            end;
          AvailabilityType::BOM:
            if ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromProdOrderLine@19(VAR ProdOrderLine@1005 : Record "Prod. Order Line";AvailabilityType@1003 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1007 : Record Item;
      NewDate@1006 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with ProdOrderLine do begin
        TESTFIELD("Item No.");
        Item.RESET;
        Item.GET("Item No.");
        FilterItem(Item,"Location Code","Variant Code","Due Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            if ShowItemAvailByEvent(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate,false) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::BOM:
            if ShowCustomProdItemAvailByBOMLevel(ProdOrderLine,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromProdOrderComp@26(VAR ProdOrderComp@1005 : Record "Prod. Order Component";AvailabilityType@1003 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1007 : Record Item;
      NewDate@1006 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with ProdOrderComp do begin
        TESTFIELD("Item No.");
        Item.RESET;
        Item.GET("Item No.");
        FilterItem(Item,"Location Code","Variant Code","Due Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            if ShowItemAvailByEvent(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate,false) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::BOM:
            if ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromTransLine@27(VAR TransLine@1008 : Record "Transfer Line";AvailabilityType@1006 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1004 : Record Item;
      NewDate@1003 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with TransLine do begin
        TESTFIELD("Item No.");
        Item.RESET;
        Item.GET("Item No.");
        FilterItem(Item,"Transfer-from Code","Variant Code","Shipment Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Shipment Date"),"Shipment Date",NewDate) then
              VALIDATE("Shipment Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Transfer-from Code"),"Transfer-from Code",NewLocationCode) then
              VALIDATE("Transfer-from Code",NewLocationCode);
          AvailabilityType::"Event":
            if ShowItemAvailByEvent(Item,FIELDCAPTION("Shipment Date"),"Shipment Date",NewDate,false) then
              VALIDATE("Shipment Date",NewDate);
          AvailabilityType::BOM:
            if ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Shipment Date"),"Shipment Date",NewDate) then
              VALIDATE("Shipment Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromWhseActivLine@28(VAR WhseActivLine@1005 : Record "Warehouse Activity Line";AvailabilityType@1003 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1007 : Record Item;
      NewDate@1006 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with WhseActivLine do begin
        TESTFIELD("Item No.");
        Item.RESET;
        Item.GET("Item No.");
        FilterItem(Item,"Location Code","Variant Code","Due Date");

        case AvailabilityType of
          AvailabilityType::Date:
            ShowItemAvailByDate(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate);
          AvailabilityType::Variant:
            ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode);
          AvailabilityType::Location:
            ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode);
          AvailabilityType::"Event":
            ShowItemAvailByEvent(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate,false);
          AvailabilityType::BOM:
            ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromServLine@31(VAR ServLine@1005 : Record "Service Line";AvailabilityType@1003 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      ServHeader@1007 : Record "Service Header";
      Item@1008 : Record Item;
      NewDate@1006 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with ServLine do begin
        ServHeader.GET("Document Type","Document No.");
        TESTFIELD(Type,Type::Item);
        TESTFIELD("No.");
        Item.RESET;
        Item.GET("No.");
        FilterItem(Item,"Location Code","Variant Code",ServHeader."Response Date");

        case AvailabilityType of
          AvailabilityType::Date:
            ShowItemAvailByDate(Item,ServHeader.FIELDCAPTION("Response Date"),ServHeader."Response Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            ShowItemAvailByEvent(Item,ServHeader.FIELDCAPTION("Response Date"),ServHeader."Response Date",NewDate,false);
          AvailabilityType::BOM:
            ShowItemAvailByBOMLevel(Item,ServHeader.FIELDCAPTION("Response Date"),ServHeader."Response Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromWhseRcptLine@29(VAR WhseRcptLine@1008 : Record "Warehouse Receipt Line";AvailabilityType@1006 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1004 : Record Item;
      NewDate@1003 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with WhseRcptLine do begin
        TESTFIELD("Item No.");
        Item.RESET;
        Item.GET("Item No.");
        FilterItem(Item,"Location Code","Variant Code","Due Date");

        case AvailabilityType of
          AvailabilityType::Date:
            ShowItemAvailByDate(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate);
          AvailabilityType::Variant:
            ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode);
          AvailabilityType::Location:
            ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode);
          AvailabilityType::"Event":
            ShowItemAvailByEvent(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate,false);
          AvailabilityType::BOM:
            ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromItemJnlLine@30(VAR ItemJnlLine@1004 : Record "Item Journal Line";AvailabilityType@1000 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1006 : Record Item;
      NewDate@1005 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with ItemJnlLine do begin
        TESTFIELD("Item No.");
        Item.RESET;
        Item.GET("Item No.");
        FilterItem(Item,"Location Code","Variant Code","Posting Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Posting Date"),"Posting Date",NewDate) then
              VALIDATE("Posting Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            if ShowItemAvailByEvent(Item,FIELDCAPTION("Posting Date"),"Posting Date",NewDate,false) then
              VALIDATE("Posting Date",NewDate);
          AvailabilityType::BOM:
            if ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Posting Date"),"Posting Date",NewDate) then
              VALIDATE("Posting Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromAsmHeader@33(VAR AsmHeader@1010 : Record "Assembly Header";AvailabilityType@1008 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1004 : Record Item;
      NewDate@1003 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with AsmHeader do begin
        TESTFIELD("Item No.");
        Item.RESET;
        Item.GET("Item No.");
        FilterItem(Item,"Location Code","Variant Code","Due Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            if ShowItemAvailByEvent(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate,false) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::BOM:
            if ShowCustomAsmItemAvailByBOMLevel(AsmHeader,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromAsmLine@34(VAR AsmLine@1010 : Record "Assembly Line";AvailabilityType@1008 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1004 : Record Item;
      NewDate@1003 : Date;
      NewVariantCode@1002 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with AsmLine do begin
        TESTFIELD(Type,Type::Item);
        TESTFIELD("No.");
        Item.RESET;
        Item.GET("No.");
        FilterItem(Item,"Location Code","Variant Code","Due Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            if ShowItemAvailByEvent(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate,false) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::BOM:
            if ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
        end;
      end;
    END;

    [External]
    PROCEDURE ShowItemAvailFromPlanningComp@35(VAR PlanningComp@1005 : Record "Planning Component";AvailabilityType@1000 : 'Date,Variant,Location,Bin,Event,BOM');
    VAR
      Item@1007 : Record Item;
      NewDate@1006 : Date;
      NewVariantCode@1003 : Code[10];
      NewLocationCode@1001 : Code[10];
    BEGIN
      with PlanningComp do begin
        TESTFIELD("Item No.");
        Item.RESET;
        Item.GET("Item No.");
        FilterItem(Item,"Location Code","Variant Code","Due Date");

        case AvailabilityType of
          AvailabilityType::Date:
            if ShowItemAvailByDate(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
          AvailabilityType::Variant:
            if ShowItemAvailVariant(Item,FIELDCAPTION("Variant Code"),"Variant Code",NewVariantCode) then
              VALIDATE("Variant Code",NewVariantCode);
          AvailabilityType::Location:
            if ShowItemAvailByLoc(Item,FIELDCAPTION("Location Code"),"Location Code",NewLocationCode) then
              VALIDATE("Location Code",NewLocationCode);
          AvailabilityType::"Event":
            begin
              ForecastName := '';
              FindCurrForecastName(ForecastName);

              if ShowItemAvailByEvent(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate,true) then
                VALIDATE("Due Date",NewDate);
            end;
          AvailabilityType::BOM:
            if ShowItemAvailByBOMLevel(Item,FIELDCAPTION("Due Date"),"Due Date",NewDate) then
              VALIDATE("Due Date",NewDate);
        end;
      end;
    END;

    LOCAL PROCEDURE ShowItemAvailByEvent@20(VAR Item@1003 : Record Item;FieldCaption@1002 : Text[80];OldDate@1001 : Date;VAR NewDate@1000 : Date;IncludeForecast@1005 : Boolean) : Boolean;
    VAR
      ItemAvailByEvent@1004 : Page "Item Availability by Event";
    BEGIN
      if FieldCaption <> '' then
        ItemAvailByEvent.LOOKUPMODE(true);
      ItemAvailByEvent.SetItem(Item);
      if IncludeForecast then begin
        ItemAvailByEvent.SetIncludePlan(true);
        if ForecastName <> '' then
          ItemAvailByEvent.SetForecastName(ForecastName);
      end;
      if ItemAvailByEvent.RUNMODAL = ACTION::LookupOK then begin
        NewDate := ItemAvailByEvent.GetSelectedDate;
        if (NewDate <> 0D) and (NewDate <> OldDate) then
          if CONFIRM(Text012,true,FieldCaption,OldDate,NewDate) then
            exit(true);
      end;
    END;

    LOCAL PROCEDURE ShowItemAvailByLoc@21(VAR Item@1003 : Record Item;FieldCaption@1002 : Text[80];OldLocationCode@1001 : Code[20];VAR NewLocationCode@1000 : Code[20]) : Boolean;
    VAR
      ItemAvailByLoc@1004 : Page "Item Availability by Location";
    BEGIN
      Item.SETRANGE("Location Filter");
      if FieldCaption <> '' then
        ItemAvailByLoc.LOOKUPMODE(true);
      ItemAvailByLoc.SETRECORD(Item);
      ItemAvailByLoc.SETTABLEVIEW(Item);
      if ItemAvailByLoc.RUNMODAL = ACTION::LookupOK then begin
        NewLocationCode := ItemAvailByLoc.GetLastLocation;
        if OldLocationCode <> NewLocationCode then
          if CONFIRM(Text012,true,FieldCaption,OldLocationCode,NewLocationCode) then
            exit(true);
      end;
    END;

    LOCAL PROCEDURE ShowItemAvailByDate@23(VAR Item@1000 : Record Item;FieldCaption@1003 : Text[80];OldDate@1001 : Date;VAR NewDate@1002 : Date) : Boolean;
    VAR
      ItemAvailByPeriods@1004 : Page "Item Availability by Periods";
    BEGIN
      Item.SETRANGE("Date Filter");
      if FieldCaption <> '' then
        ItemAvailByPeriods.LOOKUPMODE(true);
      ItemAvailByPeriods.SETRECORD(Item);
      ItemAvailByPeriods.SETTABLEVIEW(Item);
      if ItemAvailByPeriods.RUNMODAL = ACTION::LookupOK then begin
        NewDate := ItemAvailByPeriods.GetLastDate;
        if OldDate <> NewDate then
          if CONFIRM(Text012,true,FieldCaption,OldDate,NewDate) then
            exit(true);
      end;
    END;

    LOCAL PROCEDURE ShowItemAvailVariant@24(VAR Item@1000 : Record Item;FieldCaption@1003 : Text[80];OldVariant@1002 : Code[20];VAR NewVariant@1001 : Code[20]) : Boolean;
    VAR
      ItemAvailByVariant@1004 : Page "Item Availability by Variant";
    BEGIN
      Item.SETRANGE("Variant Filter");
      if FieldCaption <> '' then
        ItemAvailByVariant.LOOKUPMODE(true);
      ItemAvailByVariant.SETRECORD(Item);
      ItemAvailByVariant.SETTABLEVIEW(Item);
      if ItemAvailByVariant.RUNMODAL = ACTION::LookupOK then begin
        NewVariant := ItemAvailByVariant.GetLastVariant;
        if OldVariant <> NewVariant then
          if CONFIRM(Text012,true,FieldCaption,OldVariant,NewVariant) then
            exit(true);
      end;
    END;

    LOCAL PROCEDURE ShowItemAvailByBOMLevel@25(VAR Item@1003 : Record Item;FieldCaption@1002 : Text[80];OldDate@1001 : Date;VAR NewDate@1000 : Date) : Boolean;
    BEGIN
      CLEAR(ItemAvailByBOMLevel);
      Item.SETRANGE("Date Filter");
      ItemAvailByBOMLevel.InitItem(Item);
      ItemAvailByBOMLevel.InitDate(OldDate);
      exit(ShowBOMLevelAbleToMake(FieldCaption,OldDate,NewDate));
    END;

    LOCAL PROCEDURE ShowCustomAsmItemAvailByBOMLevel@43(VAR AsmHeader@1003 : Record "Assembly Header";FieldCaption@1002 : Text[80];OldDate@1001 : Date;VAR NewDate@1000 : Date) : Boolean;
    BEGIN
      CLEAR(ItemAvailByBOMLevel);
      ItemAvailByBOMLevel.InitAsmOrder(AsmHeader);
      ItemAvailByBOMLevel.InitDate(OldDate);
      exit(ShowBOMLevelAbleToMake(FieldCaption,OldDate,NewDate));
    END;

    LOCAL PROCEDURE ShowCustomProdItemAvailByBOMLevel@49(VAR ProdOrderLine@1003 : Record "Prod. Order Line";FieldCaption@1002 : Text[80];OldDate@1001 : Date;VAR NewDate@1000 : Date) : Boolean;
    BEGIN
      CLEAR(ItemAvailByBOMLevel);
      ItemAvailByBOMLevel.InitProdOrder(ProdOrderLine);
      ItemAvailByBOMLevel.InitDate(OldDate);
      exit(ShowBOMLevelAbleToMake(FieldCaption,OldDate,NewDate));
    END;

    LOCAL PROCEDURE ShowBOMLevelAbleToMake@46(FieldCaption@1002 : Text[80];OldDate@1001 : Date;VAR NewDate@1000 : Date) : Boolean;
    BEGIN
      if FieldCaption <> '' then
        ItemAvailByBOMLevel.LOOKUPMODE(true);
      if ItemAvailByBOMLevel.RUNMODAL = ACTION::LookupOK then begin
        NewDate := ItemAvailByBOMLevel.GetSelectedDate;
        if OldDate <> NewDate then
          if CONFIRM(Text012,true,FieldCaption,OldDate,NewDate) then
            exit(true);
      end;
    END;

    LOCAL PROCEDURE FilterItem@38(VAR Item@1000 : Record Item;LocationCode@1001 : Code[20];VariantCode@1002 : Code[20];Date@1003 : Date);
    BEGIN
      Item.SETRANGE("No.",Item."No.");
      Item.SETRANGE("Date Filter",0D,Date);
      Item.SETRANGE("Variant Filter",VariantCode);
      Item.SETRANGE("Location Filter",LocationCode);
    END;

    [External]
    PROCEDURE ByEvent@36() : Integer;
    BEGIN
      exit(AvailabilityType::"Event");
    END;

    [External]
    PROCEDURE ByLocation@37() : Integer;
    BEGIN
      exit(AvailabilityType::Location);
    END;

    [External]
    PROCEDURE ByVariant@39() : Integer;
    BEGIN
      exit(AvailabilityType::Variant);
    END;

    [External]
    PROCEDURE ByPeriod@40() : Integer;
    BEGIN
      exit(AvailabilityType::Date);
    END;

    [External]
    PROCEDURE ByBOM@41() : Integer;
    BEGIN
      exit(AvailabilityType::BOM);
    END;

    BEGIN
    END.
  }
}

