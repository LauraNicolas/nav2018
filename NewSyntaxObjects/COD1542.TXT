OBJECT Codeunit 1542 Workflow Webhook Responses
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SendNotificationToWebhookTxt@1000 : TextConst 'ENU=Send a record notification to a webhook.';

    [External]
    PROCEDURE SendNotificationToWebhookCode@1() : Code[128];
    BEGIN
      exit(UPPERCASE('SendNotificationToWebhook'));
    END;

    [EventSubscriber(Codeunit,1521,OnAddWorkflowResponsePredecessorsToLibrary)]
    LOCAL PROCEDURE AddWorkflowWebhookEventResponseCombinationsToLibrary@2(ResponseFunctionName@1000 : Code[128]);
    VAR
      WorkflowEventHandling@1001 : Codeunit "Workflow Event Handling";
      WorkflowResponseHandling@1002 : Codeunit "Workflow Response Handling";
    BEGIN
      case ResponseFunctionName of
        SendNotificationToWebhookCode:
          begin
            WorkflowResponseHandling.AddResponsePredecessor(SendNotificationToWebhookCode,
              WorkflowEventHandling.RunWorkflowOnSendCustomerForApprovalCode);
            WorkflowResponseHandling.AddResponsePredecessor(SendNotificationToWebhookCode,
              WorkflowEventHandling.RunWorkflowOnSendGeneralJournalBatchForApprovalCode);
            WorkflowResponseHandling.AddResponsePredecessor(SendNotificationToWebhookCode,
              WorkflowEventHandling.RunWorkflowOnSendGeneralJournalLineForApprovalCode);
            WorkflowResponseHandling.AddResponsePredecessor(SendNotificationToWebhookCode,
              WorkflowEventHandling.RunWorkflowOnSendItemForApprovalCode);
            WorkflowResponseHandling.AddResponsePredecessor(SendNotificationToWebhookCode,
              WorkflowEventHandling.RunWorkflowOnSendPurchaseDocForApprovalCode);
            WorkflowResponseHandling.AddResponsePredecessor(SendNotificationToWebhookCode,
              WorkflowEventHandling.RunWorkflowOnSendSalesDocForApprovalCode);
            WorkflowResponseHandling.AddResponsePredecessor(SendNotificationToWebhookCode,
              WorkflowEventHandling.RunWorkflowOnSendVendorForApprovalCode);
          end;
      end;
    END;

    [EventSubscriber(Codeunit,1521,OnAddWorkflowResponsesToLibrary)]
    LOCAL PROCEDURE AddWorkflowWebhookResponsesToLibrary@3();
    VAR
      WorkflowResponseHandling@1000 : Codeunit "Workflow Response Handling";
    BEGIN
      WorkflowResponseHandling.AddResponseToLibrary(SendNotificationToWebhookCode,0,SendNotificationToWebhookTxt,'GROUP 8');
    END;

    [EventSubscriber(Codeunit,1521,OnExecuteWorkflowResponse)]
    LOCAL PROCEDURE ExecuteWorkflowWebhookResponses@9(VAR ResponseExecuted@1000 : Boolean;Variant@1001 : Variant;xVariant@1002 : Variant;ResponseWorkflowStepInstance@1003 : Record "Workflow Step Instance");
    VAR
      WorkflowResponse@1004 : Record "Workflow Response";
    BEGIN
      if WorkflowResponse.GET(ResponseWorkflowStepInstance."Function Name") then
        case WorkflowResponse."Function Name" of
          SendNotificationToWebhookCode:
            begin
              SendNotificationToWebhook(Variant,ResponseWorkflowStepInstance);
              ResponseExecuted := true;
            end;
        end;
    END;

    LOCAL PROCEDURE SendNotificationToWebhook@6(VAR Variant@1000 : Variant;VAR WorkflowStepInstance@1002 : Record "Workflow Step Instance");
    VAR
      WorkflowWebhookManagement@1001 : Codeunit "Workflow Webhook Management";
      RecRef@1003 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Variant);
      WorkflowWebhookManagement.GenerateRequest(RecRef,WorkflowStepInstance);
    END;

    BEGIN
    END.
  }
}

