OBJECT Page 5540 Item Availability by Timeline
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Item Availability by Timeline;
    InsertAllowed=false;
    DeleteAllowed=false;
    LinksAllowed=false;
    SourceTable="Timeline Event Change";
    DataCaptionExpr=PageCaption;
    SourceTableView=SORTING("Due Date")
                    ORDER(Ascending);
    PageType=Worksheet;
    SourceTableTemporary=true;
    OnInit=BEGIN
             BlockCurrRecNotifications := false;
             IgnoreChanges := false;
             TemplateNameNewSupply := '';
             WorksheetNameNewSupply := '';
             State := State::" ";
           END;

    OnOpenPage=BEGIN
                 ItemSelected := Item."No." <> '';
               END;

    OnAfterGetRecord=BEGIN
                       Description := TimelineEventDescription;
                       ActionMsg := ActionMessage;
                     END;

    OnQueryClosePage=BEGIN
                       if not (State = State::"Unsaved Changes") then
                         exit(true);

                       exit(CONFIRM(Text003));
                     END;

    OnAfterGetCurrRecord=BEGIN
                           if (not BlockCurrRecNotifications) and (CurrReferenceNo <> "Reference No.") then begin
                             CurrReferenceNo := "Reference No.";
                             CurrPage.Visualization.SelectTransaction("Reference No.");
                           end;
                         END;

    ActionList=ACTIONS
    {
      { 3       ;0   ;ActionContainer;
                      Name=ActionContainer3;
                      ActionContainerType=ActionItems }
      { 21      ;1   ;Action    ;
                      Name=Delete;
                      ShortCutKey=Ctrl+Delete;
                      CaptionML=ENU=Delete;
                      ToolTipML=ENU=Delete the record.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=Delete;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 if not CONFIRM(Text009,true) then
                                   exit;

                                 DeleteSelectedRecords;
                               END;
                                }
      { 6       ;1   ;Action    ;
                      Name=<Reload>;
                      CaptionML=ENU=Reload;
                      ToolTipML=ENU=Update the window with values created by other users since you opened the window.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      PromotedIsBig=true;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 InitAndCreateTimelineEvents;
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=<TransferChanges>;
                      CaptionML=ENU=Save Changes;
                      ToolTipML=ENU=Transfer the changes to the planning worksheet lines.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      PromotedIsBig=true;
                      Image=SuggestLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 TransferChangesToPlanningWksh;
                                 InitAndCreateTimelineEvents;
                                 CurrPage.UPDATE(false);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Options;
                CaptionML=ENU=Options;
                GroupType=Group }

    { 30  ;2   ;Field     ;
                Name=<ItemNo>;
                CaptionML=ENU=Item No.;
                ToolTipML=ENU=Specifies the number of the item you want to view item availability for.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ItemNo;
                TableRelation=Item;
                Importance=Promoted;
                OnValidate=VAR
                             ItemNo2@1001 : Code[20];
                           BEGIN
                             if ItemNo <> Item."No." then begin
                               ItemNo2 := ItemNo;
                               Item.RESET;
                               Item.GET(ItemNo);
                               if LocationFilter <> '' then
                                 Item.SETFILTER("Location Filter",LocationFilter);
                               ItemSelected := true;
                               InitAndCreateTimelineEvents;
                               VariantFilter := Item."Variant Filter";
                               ItemNo := ItemNo2;
                             end;
                           END;

                ImplicitType=Code20 }

    { 35  ;2   ;Field     ;
                Name=<VariantFilter>;
                CaptionML=ENU=Variant Filter;
                ToolTipML=ENU=Specifies a variant code to view only the projected available balance for that variant of the item.;
                ApplicationArea=#Advanced;
                SourceExpr=VariantFilter;
                Importance=Promoted;
                OnValidate=VAR
                             VariantFilter2@1001 : Text;
                           BEGIN
                             if VariantFilter <> Item.GETFILTER("Variant Filter") then begin
                               CalcItemAvailTimeline.FindVariantWithinFilter(ItemNo,VariantFilter);

                               VariantFilter2 := VariantFilter;
                               Item.SETRANGE("Variant Filter");
                               if VariantFilter <> '' then
                                 Item.SETFILTER("Variant Filter",VariantFilter);
                               InitAndCreateTimelineEvents;
                               VariantFilter := VariantFilter2;
                             end;
                           END;

                OnLookup=VAR
                           ItemVariant@1001 : Record "Item Variant";
                           ItemVariants@1000 : Page "Item Variants";
                         BEGIN
                           ItemVariant.SETRANGE("Item No.",ItemNo);
                           ItemVariants.SETTABLEVIEW(ItemVariant);
                           ItemVariants.LOOKUPMODE := true;
                           if ItemVariants.RUNMODAL = ACTION::LookupOK then begin
                             ItemVariants.GETRECORD(ItemVariant);
                             Text := ItemVariant.Code;
                             exit(true);
                           end;
                           exit(false);
                         END;

                ImplicitType=Text }

    { 4   ;2   ;Field     ;
                Name=<LocationFilter>;
                CaptionML=ENU=Location Filter;
                ToolTipML=ENU=Specifies a location code to view only the projected available balance for the item at that location.;
                ApplicationArea=#Advanced;
                SourceExpr=LocationFilter;
                Importance=Promoted;
                OnValidate=VAR
                             LocationFilter2@1001 : Text;
                           BEGIN
                             if LocationFilter <> Item.GETFILTER("Location Filter") then begin
                               CalcItemAvailTimeline.FindLocationWithinFilter(LocationFilter);

                               LocationFilter2 := LocationFilter;
                               Item.SETRANGE("Location Filter");
                               if LocationFilter <> '' then
                                 Item.SETFILTER("Location Filter",LocationFilter);
                               InitAndCreateTimelineEvents;
                               LocationFilter := LocationFilter2;
                             end;
                           END;

                OnLookup=VAR
                           Location@1001 : Record Location;
                           LocationList@1000 : Page "Location List";
                         BEGIN
                           LocationList.SETTABLEVIEW(Location);
                           LocationList.LOOKUPMODE := true;
                           if LocationList.RUNMODAL = ACTION::LookupOK then begin
                             LocationList.GETRECORD(Location);
                             Text := Location.Code;
                             exit(true);
                           end;
                           exit(false);
                         END;

                ImplicitType=Text }

    { 13  ;2   ;Field     ;
                Name=<LastUpdateTime>;
                CaptionML=ENU=Last Updated;
                ToolTipML=ENU=Specifies when the availability figures in the Item Availability by Timeline window were last updated.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=LastUpdateTime;
                Importance=Promoted;
                Editable=FALSE;
                ImplicitType=DateTime }

    { 9   ;2   ;Field     ;
                Name=<ForecastName>;
                CaptionML=ENU=Forecast Name;
                ToolTipML=ENU=Specifies a production forecast you want to include as anticipated demand in the graphical view of projected available balance.;
                ApplicationArea=#Advanced;
                SourceExpr=ForecastName;
                TableRelation="Production Forecast Name";
                Importance=Promoted;
                OnValidate=VAR
                             ForecastName2@1001 : Code[10];
                           BEGIN
                             ForecastName2 := ForecastName;
                             InitAndCreateTimelineEvents;
                             ForecastName := ForecastName2;
                           END;

                ImplicitType=Code10 }

    { 16  ;2   ;Field     ;
                Name=<IncludeBlanketOrders>;
                CaptionML=ENU=Include Blanket Sales Orders;
                ToolTipML=ENU=Specifies if you want to include anticipated demand from blanket sales orders in the graphical view of projected available balance.;
                ApplicationArea=#Advanced;
                SourceExpr=IncludeBlanketOrders;
                Importance=Promoted;
                OnValidate=VAR
                             IncludeBlanketOrders2@1001 : Boolean;
                           BEGIN
                             IncludeBlanketOrders2 := IncludeBlanketOrders;
                             InitAndCreateTimelineEvents;
                             IncludeBlanketOrders := IncludeBlanketOrders2;
                           END;

                ImplicitType=Boolean }

    { 8   ;1   ;Group     ;
                Name=Timeline;
                CaptionML=ENU=Timeline;
                GroupType=Group }

    { 15  ;2   ;Field     ;
                Name=Visualization;
                ApplicationArea=#Basic,#Suite;
                Editable=FALSE;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.TimelineVisualization;PublicKeyToken=31bf3856ad364e35] }

    { 10  ;1   ;Group     ;
                Name=changeListGroup;
                CaptionML=ENU=Event Changes;
                GroupType=GridLayout }

    { 20  ;2   ;Group     ;
                Name=changeList;
                CaptionML=ENU=Change List;
                GroupType=Repeater }

    { 5   ;3   ;Field     ;
                Name=ActionMessage;
                CaptionML=ENU=Action Message;
                ToolTipML=ENU=Specifies the action to take to rebalance the demand-supply situation shown in the graph on the Timeline FastTab.;
                OptionCaptionML=ENU=" ,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel";
                ApplicationArea=#Manufacturing;
                SourceExpr=ActionMsg;
                Editable=FALSE;
                ImplicitType=Option }

    { 2   ;3   ;Field     ;
                Name=Description;
                CaptionML=ENU=Description;
                ToolTipML=ENU=Specifies the description of the planning worksheet lines that are represented on the Timeline FastTab.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              ShowDocument("Reference No.");
                            END;

                ImplicitType=Text250 }

    { 17  ;3   ;Field     ;
                Name=Original Due Date;
                ToolTipML=ENU=Specifies the due date that is stated on the existing supply order, when an action message proposes to reschedule the order.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Original Due Date";
                ImplicitType=Date }

    { 14  ;3   ;Field     ;
                Name=Due Date;
                ToolTipML=ENU=Specifies the due date of the suggested supply order that the line on the Event Changes FastTab represents.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Due Date";
                OnValidate=BEGIN
                             if "Due Date" <> xRec."Due Date" then begin
                               TESTFIELD("Due Date");
                               CurrPage.Visualization.RescheduleTransaction("Reference No.",CREATEDATETIME("Due Date",TempTimelineEvent.DefaultTime));
                               State := State::"Unsaved Changes";
                             end;
                           END;

                ImplicitType=Date }

    { 12  ;3   ;Field     ;
                Name=Original Quantity;
                ToolTipML=ENU=Specifies the quantity on the supply order, when an action message suggests a change to the quantity on the order.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Original Quantity";
                ImplicitType=Decimal }

    { 11  ;3   ;Field     ;
                Name=Quantity;
                ToolTipML=ENU=Specifies the quantity of the suggested supply order that the line on the Event Changes FastTab represents.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             if Quantity <> xRec.Quantity then begin
                               CurrPage.Visualization.ChangeTransactionQuantity("Reference No.",Quantity);
                               State := State::"Unsaved Changes";
                             end;
                           END;

                ImplicitType=Decimal }

    { 18  ;3   ;Field     ;
                Name=Reference No.;
                ToolTipML=ENU=Specifies the reference number for the item in the timeline event change table.;
                ApplicationArea=#Advanced;
                SourceExpr="Reference No.";
                Visible=false;
                ImplicitType=Text200 }

  }
  CODE
  {
    VAR
      Item@1004 : Record Item;
      TempTimelineEvent@1015 : TEMPORARY Record "Timeline Event";
      CalcItemAvailTimeline@1023 : Codeunit "Calc. Item Avail. Timeline";
      ItemNo@1003 : Code[20];
      LocationFilter@1009 : Text;
      VariantFilter@1008 : Text;
      ForecastName@1005 : Code[10] INDATASET;
      TemplateNameNewSupply@1018 : Code[10];
      WorksheetNameNewSupply@1017 : Code[10];
      Description@1006 : Text[250];
      ActionMsg@1013 : '" ",New,"Change Qty.",Reschedule,"Resched. & Chg. Qty.",Cancel';
      State@1024 : '" ",Initialized,"Unsaved Changes"';
      LastUpdateTime@1016 : DateTime;
      IncludeBlanketOrders@1014 : Boolean INDATASET;
      Text001@1022 : TextConst 'ENU=The changes have been saved to the planning or requisition worksheet.';
      CurrReferenceNo@1057 : Text[200];
      BlockCurrRecNotifications@1007 : Boolean;
      Text002@1012 : TextConst 'ENU=The changes have been saved to the planning or requisition worksheet.\\New supply is saved to worksheet Template Name %1, Batch Name %2, Location Code %3, and Variant Code %4.';
      Text003@1019 : TextConst 'ENU=All unsaved changes will not be saved. Are you sure that you want to close the window?';
      Text004@1020 : TextConst 'ENU=Show Projected Available Balance';
      Text005@1021 : TextConst 'ENU=Include Planning Suggestions';
      Text006@1000 : TextConst 'ENU=Workdate';
      Text007@1001 : TextConst 'ENU=Today';
      IgnoreChanges@1025 : Boolean;
      ItemSelected@1002 : Boolean INDATASET;
      Text008@1010 : TextConst 'ENU=New Supply %1';
      Text009@1011 : TextConst 'ENU=Do you want to delete?';

    [Internal]
    PROCEDURE InitAndCreateTimelineEvents@14();
    BEGIN
      InitTimeline;
      CreateTimelineEvents;
      SetScenarioTitles;
      State := State::Initialized;
    END;

    LOCAL PROCEDURE InitTimeline@1();
    VAR
      timeSpan@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.TimeSpan";
    BEGIN
      CurrPage.Visualization.RemoveSpecialDates;
      CurrPage.Visualization.AddSpecialDate(CREATEDATETIME(WORKDATE,TempTimelineEvent.DefaultTime),Text006);
      CurrPage.Visualization.AddSpecialDate(CREATEDATETIME(TODAY,TempTimelineEvent.DefaultTime),Text007);
      CurrPage.Visualization.TransactionPrecision := Decimals(Item."Rounding Precision");
      CurrPage.Visualization.TransactionUnitOfMeasure := Item."Base Unit of Measure";
      CurrPage.Visualization.InitialLevelDateMargin := timeSpan.FromDays(CalcItemAvailTimeline.InitialTimespanDays);
      CurrPage.Visualization.FinalLevelDateMargin := timeSpan.FromDays(CalcItemAvailTimeline.FinalTimespanDays);
    END;

    LOCAL PROCEDURE UpdateItemRequestFields@11(VAR Item@1000 : Record Item);
    BEGIN
      ItemNo := Item."No.";
      LocationFilter := '';
      if Item.GETFILTER("Location Filter") <> '' then
        LocationFilter := Item.GETFILTER("Location Filter");
      VariantFilter := '';
      if Item.GETFILTER("Variant Filter") <> '' then
        VariantFilter := Item.GETFILTER("Variant Filter");
    END;

    LOCAL PROCEDURE PageCaption@5() : Text[250];
    BEGIN
      exit(STRSUBSTNO('%1 %2',Item."No.",Item.Description));
    END;

    [External]
    PROCEDURE SetItem@3(VAR NewItem@1000 : Record Item);
    BEGIN
      Item.COPY(NewItem);
      UpdateItemRequestFields(Item);
    END;

    [External]
    PROCEDURE SetForecastName@2(NewForcastName@1000 : Code[10]);
    BEGIN
      ForecastName := NewForcastName;
    END;

    [External]
    PROCEDURE SetWorksheet@8(NewTemplateName@1001 : Code[10];NewWorksheetName@1000 : Code[10]);
    BEGIN
      TemplateNameNewSupply := NewTemplateName;
      WorksheetNameNewSupply := NewWorksheetName;
    END;

    LOCAL PROCEDURE ExportDataToTimeline@19(VAR TimelineEvent@1003 : Record "Timeline Event");
    VAR
      transactionTable@1002 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionDataTable";
      visualizationScenarios@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.TimelineVisualization, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.TimelineVisualization.VisualizationScenarios";
      WithForecast@1001 : Integer;
      WithForecastAndAdjustments@1004 : Integer;
    BEGIN
      TimelineEvent.TransferToTransactionTable(TimelineEvent,transactionTable);

      WithForecast := 2; // Enum value for CoreTransactionsWithForecast check box
      WithForecastAndAdjustments := 4; // Enum value for CoreTransactionsWithForecastAndAdjustments check box
      visualizationScenarios := WithForecast + WithForecastAndAdjustments; // Show the specified check boxes
      CurrPage.Visualization.SetTransactions(transactionTable,visualizationScenarios);
    END;

    LOCAL PROCEDURE ImportChangesFromTimeline@18();
    VAR
      changeTable@1003 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionChangesDataTable";
    BEGIN
      BlockCurrRecNotifications := true;

      changeTable := CurrPage.Visualization.GetTransactionChanges;
      TransferFromTransactionChangeTable(Rec,changeTable);
      SetCurrentChangeRec(CurrReferenceNo);

      BlockCurrRecNotifications := false;
    END;

    LOCAL PROCEDURE CreateTimelineEvents@17();
    BEGIN
      RESET;
      DELETEALL;
      TempTimelineEvent.DELETEALL;
      Item.SETRANGE("Drop Shipment Filter",false);
      CalcItemAvailTimeline.Initialize(Item,ForecastName,IncludeBlanketOrders,0D,true);
      CalcItemAvailTimeline.CreateTimelineEvents(TempTimelineEvent);
      ExportDataToTimeline(TempTimelineEvent);
      LastUpdateTime := CURRENTDATETIME;
    END;

    LOCAL PROCEDURE DeleteSelectedRecords@10();
    VAR
      TempTimelineEventChange@1001 : TEMPORARY Record "Timeline Event Change";
    BEGIN
      TempTimelineEventChange.COPY(Rec,true);
      CurrPage.SETSELECTIONFILTER(TempTimelineEventChange);
      if TempTimelineEventChange.FINDSET then begin
        IgnoreChanges := true;
        repeat
          CurrPage.Visualization.DeleteTransaction(TempTimelineEventChange."Reference No.");
        until TempTimelineEventChange.NEXT = 0;
        ImportChangesFromTimeline;
        IgnoreChanges := false;
        if (State = State::"Unsaved Changes") and (COUNT = 0) then
          State := State::Initialized;
      end;
    END;

    LOCAL PROCEDURE ShowDocument@4(RefNo@1001 : Text[255]);
    VAR
      ReferenceNo@1000 : Integer;
    BEGIN
      if EVALUATE(ReferenceNo,RefNo) then begin
        TempTimelineEvent.GET(ReferenceNo);
        CalcItemAvailTimeline.ShowDocument(TempTimelineEvent."Source Document ID");
      end;
    END;

    LOCAL PROCEDURE SetCurrentChangeRec@20(RefNo@1000 : Text[255]);
    BEGIN
      if ISEMPTY or (RefNo = '') then
        exit;

      SETRANGE("Reference No.",RefNo);
      if FINDFIRST then begin
        CurrPage.SETRECORD(Rec);
        CurrPage.UPDATE(false);
      end;
      SETRANGE("Reference No.");
    END;

    LOCAL PROCEDURE TimelineEventDescription@12() : Text[250];
    VAR
      ReferenceNo@1000 : Integer;
    BEGIN
      if NewSupply then
        exit(STRSUBSTNO(Text008,FORMAT("Due Date")));

      if EVALUATE(ReferenceNo,"Reference No.") then
        if TempTimelineEvent.GET(ReferenceNo) then
          exit(TempTimelineEvent.Description);

      exit("Reference No.");
    END;

    LOCAL PROCEDURE SetScenarioTitles@6();
    VAR
      visualizationScenarios@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.TimelineVisualization, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.TimelineVisualization.VisualizationScenarios";
    BEGIN
      CurrPage.Visualization.SetScenarioTitle(visualizationScenarios.CoreTransactionsWithForecast,Text004);
      CurrPage.Visualization.SetScenarioTitle(visualizationScenarios.CoreTransactionsWithForecastAndAdjustments,Text005);
    END;

    LOCAL PROCEDURE TransferChangesToPlanningWksh@7();
    VAR
      LocationCodeNewSupply@1002 : Code[10];
      VariantCodeNewSupply@1001 : Code[10];
      NewSupplyTransfer@1000 : Boolean;
    BEGIN
      NewSupplyTransfer :=
        CalcItemAvailTimeline.TransferChangesToPlanningWksh(
          Rec,ItemNo,LocationFilter,VariantFilter,TemplateNameNewSupply,WorksheetNameNewSupply);

      if not NewSupplyTransfer then
        MESSAGE(Text001)
      else begin
        if LocationCodeNewSupply = '' then
          LocationCodeNewSupply := CalcItemAvailTimeline.BlankValue;
        if VariantCodeNewSupply = '' then
          VariantCodeNewSupply := CalcItemAvailTimeline.BlankValue;
        MESSAGE(Text002,TemplateNameNewSupply,WorksheetNameNewSupply,LocationCodeNewSupply,VariantCodeNewSupply)
      end;
    END;

    LOCAL PROCEDURE Decimals@29(Precision@1000 : Decimal) Decimals : Integer;
    BEGIN
      if (Precision <= 0) or (Precision > 1) then
        exit(DefaultPrecision);

      Decimals := 0;

      while (Decimals <= MaxPrecision) and (ROUND(Precision,1,'>') > Precision) do begin
        Decimals += 1;
        Precision *= 10;
      end;

      exit(Decimals);
    END;

    LOCAL PROCEDURE DefaultPrecision@35() : Integer;
    BEGIN
      exit(5);
    END;

    LOCAL PROCEDURE MaxPrecision@39() : Integer;
    BEGIN
      exit(5);
    END;

    EVENT Visualization@-5::TransactionActivated@30(refNo@1000 : Text);
    BEGIN
      ShowDocument(refNo);
    END;

    EVENT Visualization@-5::SelectedTransactionChanged@31(refNo@1000 : Text);
    BEGIN
      SetCurrentChangeRec(refNo);
    END;

    EVENT Visualization@-5::TransactionValueChanged@32(refNo@1000 : Text);
    BEGIN
      if IgnoreChanges then
        exit;

      if refNo <> '' then begin // Specific Transaction Value Changed
        State := State::"Unsaved Changes";
        if NewSupply then
          CurrReferenceNo := COPYSTR(refNo,1,MAXSTRLEN("Reference No."));
      end;

      ImportChangesFromTimeline;
    END;

    EVENT Visualization@-5::AddInReady@33();
    BEGIN
      if ItemSelected then
        InitAndCreateTimelineEvents;
    END;

    BEGIN
    END.
  }
}

