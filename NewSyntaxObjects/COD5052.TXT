OBJECT Codeunit 5052 AttachmentManagement
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Send attachments...\\';
      Text001@1001 : TextConst 'ENU=Preparing';
      Text002@1002 : TextConst 'ENU=Deliver misc.';
      Text008@1003 : TextConst 'ENU=You must select an interaction template with an attachment.';
      AttachmentTok@1004 : TextConst 'ENU=Attachment';

    [Internal]
    PROCEDURE InsertAttachment@14(AttachmentNo@1000 : Integer) : Integer;
    VAR
      Attachment@1001 : Record Attachment;
      Attachment3@1003 : Record Attachment;
    BEGIN
      if AttachmentNo <> 0 then begin
        Attachment.GET(AttachmentNo);
        if Attachment."Storage Type" = Attachment."Storage Type"::Embedded then
          Attachment.CALCFIELDS("Attachment File");
        Attachment3 := Attachment; // Remember "from" attachment
      end;

      Attachment.INSERT(true);

      if AttachmentNo <> 0 then
        // New attachment is based on old attachment
        TransferAttachment(Attachment3,Attachment); // Transfer attachments of different types.

      exit(Attachment."No.");
    END;

    [Internal]
    PROCEDURE Send@1(VAR DeliverySorter@1000 : Record "Delivery Sorter");
    VAR
      TempDeliverySorterHtml@1014 : TEMPORARY Record "Delivery Sorter";
      TempDeliverySorterWord@1002 : TEMPORARY Record "Delivery Sorter";
      TempDeliverySorterOther@1003 : TEMPORARY Record "Delivery Sorter";
      InteractLogEntry@1004 : Record "Interaction Log Entry";
      WordManagement@1006 : Codeunit WordManagement;
    BEGIN
      ProcessDeliverySorter(DeliverySorter,TempDeliverySorterHtml,TempDeliverySorterWord,TempDeliverySorterOther);

      if TempDeliverySorterWord.FINDFIRST then
        WordManagement.Merge(TempDeliverySorterWord);

      if TempDeliverySorterHtml.FINDFIRST then
        DeliverHTMLEmail(TempDeliverySorterHtml,InteractLogEntry);

      if TempDeliverySorterOther.FINDFIRST then
        DeliverEmailWithAttachment(TempDeliverySorterOther,InteractLogEntry);
    END;

    [Internal]
    PROCEDURE SendUsingExchange@15(VAR DeliverySorter@1000 : Record "Delivery Sorter");
    VAR
      TempDeliverySorterHtml@1014 : TEMPORARY Record "Delivery Sorter";
      TempDeliverySorterWord@1002 : TEMPORARY Record "Delivery Sorter";
      TempDeliverySorterOther@1003 : TEMPORARY Record "Delivery Sorter";
      InteractLogEntry@1004 : Record "Interaction Log Entry";
      WordManagement@1006 : Codeunit WordManagement;
      ExchangeWebServicesServer@1001 : Codeunit "Exchange Web Services Server";
    BEGIN
      ProcessDeliverySorter(DeliverySorter,TempDeliverySorterHtml,TempDeliverySorterWord,TempDeliverySorterOther);

      if TempDeliverySorterWord.FINDFIRST then
        WordManagement.Merge(TempDeliverySorterWord);

      InitializeExchange(ExchangeWebServicesServer);
      if TempDeliverySorterHtml.FINDFIRST then
        DeliverHTMLEmailViaExchange(ExchangeWebServicesServer,TempDeliverySorterHtml,InteractLogEntry);

      if TempDeliverySorterOther.FINDFIRST then
        DeliverEmailWithAttachmentViaExchange(ExchangeWebServicesServer,TempDeliverySorterOther,InteractLogEntry);
    END;

    LOCAL PROCEDURE TransferAttachment@5(FromAttachment@1000 : Record Attachment;VAR ToAttachment@1001 : Record Attachment);
    VAR
      RMSetup@1002 : Record "Marketing Setup";
      FileName@1003 : Text;
    BEGIN
      // Transfer attachments of different types

      if (FromAttachment."Storage Type" = FromAttachment."Storage Type"::Embedded) and
         (ToAttachment."Storage Type" = ToAttachment."Storage Type"::"Disk File")
      then begin
        FileName := ToAttachment.ConstDiskFileName;
        FromAttachment.ExportAttachmentToServerFile(FileName); // Export blob to UNC location
        with ToAttachment do begin
          CLEAR("Attachment File");
          RMSetup.GET;
          RMSetup.TESTFIELD("Attachment Storage Location");
          "Storage Pointer" := RMSetup."Attachment Storage Location";
          MODIFY;
        end;
      end;

      if (FromAttachment."Storage Type" = FromAttachment."Storage Type"::"Disk File") and
         (ToAttachment."Storage Type" = ToAttachment."Storage Type"::"Disk File")
      then begin
        // Copy external attachment (to new storage)
        RMSetup.GET;
        RMSetup.TESTFIELD("Attachment Storage Location");
        ToAttachment."Storage Pointer" := RMSetup."Attachment Storage Location";
        ToAttachment.MODIFY;
        FILE.COPY(FromAttachment.ConstDiskFileName,ToAttachment.ConstDiskFileName);
      end;

      if (FromAttachment."Storage Type" = FromAttachment."Storage Type"::"Disk File") and
         (ToAttachment."Storage Type" = ToAttachment."Storage Type"::Embedded)
      then
        // Transfer External to Embedded attachment
        with ToAttachment do begin
          ImportAttachmentFromServerFile(FromAttachment.ConstDiskFileName,true,false); // Import file from UNC location
          "File Extension" := FromAttachment."File Extension";
          "Storage Pointer" := '';
          MODIFY;
        end;
    END;

    [External]
    PROCEDURE InteractionEMail@6(VAR InteractLogEntry@1000 : Record "Interaction Log Entry") : Text[80];
    VAR
      Cont@1001 : Record Contact;
      ContAltAddr@1002 : Record "Contact Alt. Address";
    BEGIN
      if InteractLogEntry."Contact Alt. Address Code" = '' then begin
        Cont.GET(InteractLogEntry."Contact No.");
        exit(Cont."E-Mail");
      end;
      ContAltAddr.GET(InteractLogEntry."Contact No.",InteractLogEntry."Contact Alt. Address Code");
      if ContAltAddr."E-Mail" <> '' then
        exit(ContAltAddr."E-Mail");

      Cont.GET(InteractLogEntry."Contact No.");
      exit(Cont."E-Mail");
    END;

    [External]
    PROCEDURE InteractionFax@7(VAR InteractLogEntry@1000 : Record "Interaction Log Entry") : Text[30];
    VAR
      Cont@1001 : Record Contact;
      ContAltAddr@1002 : Record "Contact Alt. Address";
    BEGIN
      if InteractLogEntry."Contact Alt. Address Code" = '' then begin
        Cont.GET(InteractLogEntry."Contact No.");
        exit(Cont."Fax No.");
      end;
      ContAltAddr.GET(InteractLogEntry."Contact No.",InteractLogEntry."Contact Alt. Address Code");
      if ContAltAddr."Fax No." <> '' then
        exit(ContAltAddr."Fax No.");

      Cont.GET(InteractLogEntry."Contact No.");
      exit(Cont."Fax No.");
    END;

    [Internal]
    PROCEDURE GenerateHTMLContent@9(VAR Attachment@1001 : Record Attachment;SegmentLine@1000 : Record "Segment Line") : Boolean;
    BEGIN
      if not Attachment.IsHTML then
        exit(false);

      if Attachment.IsHTMLReady then
        exit(true);

      exit(GenerateHTMLReadyAttachmentFromCustomLayout(Attachment,SegmentLine));
    END;

    [Internal]
    PROCEDURE LoadHTMLContent@3(VAR Attachment@1001 : Record Attachment;SegmentLine@1000 : Record "Segment Line") : Text;
    BEGIN
      if not Attachment.IsHTML then
        exit('');

      if Attachment.IsHTMLReady then
        exit(Attachment.Read);

      exit(LoadHTMLContentFromCustomLayoutAttachment(Attachment,SegmentLine));
    END;

    LOCAL PROCEDURE LoadHTMLContentFromCustomLayoutAttachment@51(VAR Attachment@1003 : Record Attachment;SegmentLine@1004 : Record "Segment Line") Result : Text;
    VAR
      TempAttachment@1000 : TEMPORARY Record Attachment;
      FileName@1001 : Text;
    BEGIN
      Result := '';
      FileName := GenerateHTMLReadyContentFromCustomLayoutAttachment(Attachment,SegmentLine);
      if FileName = '' then
        exit;

      TempAttachment.INIT;
      TempAttachment.ImportAttachmentFromServerFile(FileName,true,true);
      TempAttachment."No." := 0;
      TempAttachment.INSERT;
      Result := TempAttachment.Read;
      TempAttachment.RemoveAttachment(false);
    END;

    LOCAL PROCEDURE GenerateHTMLReadyAttachmentFromCustomLayout@36(VAR Attachment@1001 : Record Attachment;SegmentLine@1002 : Record "Segment Line") : Boolean;
    VAR
      FileName@1000 : Text;
    BEGIN
      FileName := GenerateHTMLReadyContentFromCustomLayoutAttachment(Attachment,SegmentLine);
      if FileName = '' then
        exit(false);

      Attachment.DELETE;
      Attachment.INIT;
      Attachment.ImportAttachmentFromServerFile(FileName,true,true);
      Attachment."No." := 0;
      Attachment.INSERT;

      exit(true);
    END;

    LOCAL PROCEDURE GenerateHTMLReadyContentFromCustomLayoutAttachment@30(VAR Attachment@1001 : Record Attachment;SegmentLine@1002 : Record "Segment Line") FileName : Text;
    VAR
      ReportLayoutSelection@1004 : Record "Report Layout Selection";
      EmailMerge@1006 : Report "Email Merge";
      FileMgt@1003 : Codeunit "File Management";
      ContentBodyText@1000 : Text;
      CustomLayoutNo@1010 : Code[20];
    BEGIN
      CLEAR(EmailMerge);
      FileName := FileMgt.ServerTempFileName('html');
      if FileName = '' then
        exit;

      Attachment.ReadHTMLCustomLayoutAttachment(ContentBodyText,CustomLayoutNo);
      ReportLayoutSelection.SetTempLayoutSelected(CustomLayoutNo);
      EmailMerge.InitializeRequest(SegmentLine,ContentBodyText);
      EmailMerge.SAVEASHTML(FileName);
      ReportLayoutSelection.SetTempLayoutSelected('');
    END;

    LOCAL PROCEDURE InitializeExchange@16(VAR ExchangeWebServicesServer@1001 : Codeunit "Exchange Web Services Server");
    VAR
      User@1000 : Record User;
    BEGIN
      COMMIT;
      User.SETRANGE("User Name",USERID);
      if not User.FINDFIRST and not InitializeExchangeWithToken(ExchangeWebServicesServer,User."Authentication Email") then
        if not InitializeExchangeWithCredentials(ExchangeWebServicesServer) then
          ERROR('');
    END;

    [TryFunction]
    LOCAL PROCEDURE InitializeExchangeWithCredentials@73(VAR ExchangeWebServicesServer@1001 : Codeunit "Exchange Web Services Server");
    VAR
      TempOfficeAdminCredentials@1003 : TEMPORARY Record "Office Admin. Credentials";
      WebCredentials@1002 : DotNet "'Microsoft.Exchange.WebServices, Version=15.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Exchange.WebServices.Data.WebCredentials";
      WebCredentialsLogin@1000 : Text[250];
    BEGIN
      TempOfficeAdminCredentials.INIT;
      TempOfficeAdminCredentials.INSERT;
      COMMIT;
      CLEARLASTERROR;
      if PAGE.RUNMODAL(PAGE::"Office 365 Credentials",TempOfficeAdminCredentials) <> ACTION::LookupOK then
        ERROR('');
      WebCredentialsLogin := TempOfficeAdminCredentials.Email;
      WebCredentials := WebCredentials.WebCredentials(WebCredentialsLogin,TempOfficeAdminCredentials.GetPassword);
      TempOfficeAdminCredentials.DELETE;
      ExchangeWebServicesServer.Initialize(
        WebCredentialsLogin,ExchangeWebServicesServer.ProdEndpoint,WebCredentials,false);
    END;

    [TryFunction]
    LOCAL PROCEDURE InitializeExchangeWithToken@34(VAR ExchangeWebServicesServer@1004 : Codeunit "Exchange Web Services Server";AuthenticationEmail@1003 : Text[250]);
    VAR
      ExchangeServiceSetup@1000 : Record "Exchange Service Setup";
      AzureADMgt@1001 : Codeunit "Azure AD Mgt.";
      AccessToken@1002 : Text;
    BEGIN
      AccessToken := AzureADMgt.GetAccessToken(AzureADMgt.GetO365Resource,AzureADMgt.GetO365ResourceName,false);

      if AccessToken <> '' then begin
        ExchangeWebServicesServer.InitializeWithOAuthToken(AccessToken,ExchangeWebServicesServer.GetEndpoint);
        exit;
      end;

      ExchangeServiceSetup.GET;

      ExchangeWebServicesServer.InitializeWithCertificate(
        ExchangeServiceSetup."Azure AD App. ID",ExchangeServiceSetup."Azure AD App. Cert. Thumbprint",
        ExchangeServiceSetup."Azure AD Auth. Endpoint",ExchangeServiceSetup."Exchange Service Endpoint",
        ExchangeServiceSetup."Exchange Resource Uri");

      ExchangeWebServicesServer.SetImpersonatedIdentity(AuthenticationEmail);
    END;

    LOCAL PROCEDURE GetSenderSalesPersonEmail@23(VAR InteractLogEntry@1000 : Record "Interaction Log Entry") : Text;
    VAR
      SalesPersonPurchaser@1001 : Record "Salesperson/Purchaser";
    BEGIN
      SalesPersonPurchaser.GET(InteractLogEntry."Salesperson Code");
      exit(SalesPersonPurchaser."E-Mail");
    END;

    LOCAL PROCEDURE DeliverHTMLEmail@25(VAR TempDeliverySorterHtml@1001 : TEMPORARY Record "Delivery Sorter";VAR InteractLogEntry@1003 : Record "Interaction Log Entry");
    VAR
      Attachment@1005 : Record Attachment;
      FileMgt@1004 : Codeunit "File Management";
      EmailBodyFilePath@1006 : Text;
    BEGIN
      InteractLogEntry.LOCKTABLE;
      repeat
        InteractLogEntry.GET(TempDeliverySorterHtml."No.");

        if TempDeliverySorterHtml."Correspondence Type" = TempDeliverySorterHtml."Correspondence Type"::Email then begin
          GetAttachment(Attachment,TempDeliverySorterHtml."Attachment No.",false);
          EmailBodyFilePath := FileMgt.ServerTempFileName('HTML');
          Attachment.ExportAttachmentToServerFile(EmailBodyFilePath);

          COMMIT;
          SendHTMLEmail(
            TempDeliverySorterHtml,InteractLogEntry,EmailBodyFilePath);
          // Clean up
          FileMgt.DeleteServerFile(EmailBodyFilePath)
        end else
          SetDeliveryState(InteractLogEntry,false);
      until TempDeliverySorterHtml.NEXT = 0;
    END;

    LOCAL PROCEDURE DeliverHTMLEmailViaExchange@20(VAR ExchangeWebServicesServer@1002 : Codeunit "Exchange Web Services Server";VAR TempDeliverySorterHtml@1001 : TEMPORARY Record "Delivery Sorter";VAR InteractLogEntry@1003 : Record "Interaction Log Entry");
    VAR
      Attachment@1005 : Record Attachment;
      FileMgt@1004 : Codeunit "File Management";
      EmailBodyFilePath@1006 : Text;
    BEGIN
      InteractLogEntry.LOCKTABLE;
      repeat
        InteractLogEntry.GET(TempDeliverySorterHtml."No.");

        if TempDeliverySorterHtml."Correspondence Type" = TempDeliverySorterHtml."Correspondence Type"::Email then begin
          GetAttachment(Attachment,TempDeliverySorterHtml."Attachment No.",false);
          EmailBodyFilePath := FileMgt.ServerTempFileName('HTML');
          Attachment.ExportAttachmentToServerFile(EmailBodyFilePath);

          COMMIT;
          SendHTMLEmailViaExchange(
            ExchangeWebServicesServer,TempDeliverySorterHtml,InteractLogEntry,Attachment);
          // Clean up
          FileMgt.DeleteServerFile(EmailBodyFilePath)
        end else
          SetDeliveryState(InteractLogEntry,false);
      until TempDeliverySorterHtml.NEXT = 0;
    END;

    LOCAL PROCEDURE DeliverEmailWithAttachment@19(VAR TempDeliverySorterOther@1001 : TEMPORARY Record "Delivery Sorter";VAR InteractLogEntry@1004 : Record "Interaction Log Entry");
    VAR
      FileMgt@1011 : Codeunit "File Management";
      AttachmentFileFullName@1006 : Text;
      EmailBodyFilePath@1005 : Text;
    BEGIN
      repeat
        InteractLogEntry.LOCKTABLE;
        InteractLogEntry.GET(TempDeliverySorterOther."No.");
        if TempDeliverySorterOther."Correspondence Type" = TempDeliverySorterOther."Correspondence Type"::Email then begin
          // Export the attachment to the client TEMP directory, giving it a GUID
          AttachmentFileFullName := PrepareServerAttachment(TempDeliverySorterOther."Attachment No.");
          EmailBodyFilePath := PrepareDummyEmailBody;

          COMMIT;
          SendEmailWithAttachment(
            TempDeliverySorterOther,InteractLogEntry,AttachmentFileFullName,EmailBodyFilePath);
          // Clean up
          FileMgt.DeleteServerFile(AttachmentFileFullName);
          FileMgt.DeleteServerFile(EmailBodyFilePath);
        end else
          SetDeliveryState(InteractLogEntry,false);
      until TempDeliverySorterOther.NEXT = 0;
    END;

    LOCAL PROCEDURE DeliverEmailWithAttachmentViaExchange@28(VAR ExchangeWebServicesServer@1010 : Codeunit "Exchange Web Services Server";VAR TempDeliverySorterOther@1001 : TEMPORARY Record "Delivery Sorter";VAR InteractLogEntry@1004 : Record "Interaction Log Entry");
    VAR
      Contact@1009 : Record Contact;
      FileMgt@1011 : Codeunit "File Management";
      Window@1000 : Dialog;
      I@1002 : Integer;
      NoOfAttachments@1003 : Integer;
      AttachmentFileFullName@1006 : Text;
    BEGIN
      Window.OPEN(
        Text000 +
        '#1############ @2@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@\' +
        '#3############ @4@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

      Window.UPDATE(1,Text001);
      Window.UPDATE(2,10000);
      Window.UPDATE(3,Text002);
      I := 0;
      NoOfAttachments := TempDeliverySorterOther.COUNT;
      repeat
        InteractLogEntry.LOCKTABLE;
        InteractLogEntry.GET(TempDeliverySorterOther."No.");
        if TempDeliverySorterOther."Correspondence Type" = TempDeliverySorterOther."Correspondence Type"::Email then begin
          // Export the attachment to the server TEMP directory, giving it a GUID
          AttachmentFileFullName := PrepareServerAttachment(
              TempDeliverySorterOther."Attachment No.");

          // Send the mail
          Contact.GET(InteractLogEntry."Contact No.");
          SendEmailWithAttachmentViaExchange(
            ExchangeWebServicesServer,TempDeliverySorterOther,InteractLogEntry,AttachmentFileFullName);
          // Clean up
          FileMgt.DeleteServerFile(AttachmentFileFullName);
        end else
          SetDeliveryState(InteractLogEntry,false);
        I := I + 1;
        Window.UPDATE(4,ROUND(I / NoOfAttachments * 10000,1));
      until TempDeliverySorterOther.NEXT = 0;
      Window.CLOSE;
    END;

    LOCAL PROCEDURE SendHTMLEmail@8(VAR TempDeliverySorterHtml@1001 : TEMPORARY Record "Delivery Sorter";VAR InteractLogEntry@1003 : Record "Interaction Log Entry";EmailBodyFilePath@1006 : Text);
    VAR
      DocumentMailing@1005 : Codeunit "Document-Mailing";
      IsSent@1007 : Boolean;
    BEGIN
      IsSent :=
        DocumentMailing.EmailFileWithSubject(
          '','',EmailBodyFilePath,
          TempDeliverySorterHtml.Subject,InteractionEMail(InteractLogEntry),false);

      SetDeliveryState(InteractLogEntry,IsSent);
    END;

    LOCAL PROCEDURE SendHTMLEmailViaExchange@17(VAR ExchangeWebServicesServer@1002 : Codeunit "Exchange Web Services Server";VAR TempDeliverySorterHtml@1001 : TEMPORARY Record "Delivery Sorter";VAR InteractLogEntry@1003 : Record "Interaction Log Entry";Attachment@1000 : Record Attachment);
    VAR
      IsSent@1007 : Boolean;
    BEGIN
      IsSent :=
        ExchangeWebServicesServer.SendEmailMessageWithAttachment(
          TempDeliverySorterHtml.Subject,InteractionEMail(InteractLogEntry),
          Attachment.Read,'',GetSenderSalesPersonEmail(InteractLogEntry));

      SetDeliveryState(InteractLogEntry,IsSent);
    END;

    LOCAL PROCEDURE SendEmailWithAttachment@12(TempDeliverySorterOther@1002 : TEMPORARY Record "Delivery Sorter";InteractLogEntry@1003 : Record "Interaction Log Entry";AttachmentFileFullName@1004 : Text;EmailBodyFilePath@1000 : Text);
    VAR
      DocumentMailing@1001 : Codeunit "Document-Mailing";
      IsSent@1005 : Boolean;
    BEGIN
      IsSent :=
        DocumentMailing.EmailFileWithSubject(
          AttachmentFileFullName,GetAttachmentFileDefaultName(TempDeliverySorterOther."Attachment No."),
          EmailBodyFilePath,TempDeliverySorterOther.Subject,InteractionEMail(InteractLogEntry),false);

      SetDeliveryState(InteractLogEntry,IsSent);
    END;

    LOCAL PROCEDURE SendEmailWithAttachmentViaExchange@33(VAR ExchangeWebServicesServer@1001 : Codeunit "Exchange Web Services Server";TempDeliverySorterOther@1002 : TEMPORARY Record "Delivery Sorter";InteractLogEntry@1003 : Record "Interaction Log Entry";AttachmentFileFullName@1004 : Text);
    VAR
      IsSent@1005 : Boolean;
    BEGIN
      IsSent :=
        ExchangeWebServicesServer.SendEmailMessageWithAttachment(
          TempDeliverySorterOther.Subject,InteractionEMail(InteractLogEntry),
          '',AttachmentFileFullName,GetSenderSalesPersonEmail(InteractLogEntry));
      SetDeliveryState(InteractLogEntry,IsSent);
    END;

    LOCAL PROCEDURE PrepareServerAttachment@40(AttachmentNo@1000 : Integer) : Text;
    VAR
      Attachment@1003 : Record Attachment;
      FileManagement@1002 : Codeunit "File Management";
      TempFileFullName@1001 : Text;
      AttachmentFileFullName@1004 : Text;
    BEGIN
      GetAttachment(Attachment,AttachmentNo,true);
      TempFileFullName := FileManagement.ServerTempFileName('');
      Attachment.ExportAttachmentToServerFile(TempFileFullName);
      AttachmentFileFullName := FileManagement.CombinePath(
          FileManagement.GetDirectoryName(TempFileFullName),AttachmentTok + '.' + Attachment."File Extension");
      FileManagement.CopyServerFile(TempFileFullName,AttachmentFileFullName,true);
      FileManagement.DeleteServerFile(TempFileFullName);
      exit(AttachmentFileFullName);
    END;

    LOCAL PROCEDURE SetDeliveryState@4(VAR InteractLogEntry@1000 : Record "Interaction Log Entry";IsSent@1001 : Boolean);
    BEGIN
      if IsSent then
        InteractLogEntry."Delivery Status" := InteractLogEntry."Delivery Status"::" "
      else
        InteractLogEntry."Delivery Status" := InteractLogEntry."Delivery Status"::Error;
      InteractLogEntry.MODIFY;
      COMMIT;
    END;

    LOCAL PROCEDURE ProcessDeliverySorter@10(VAR DeliverySorter@1004 : Record "Delivery Sorter";VAR TempDeliverySorterHtml@1003 : TEMPORARY Record "Delivery Sorter";VAR TempDeliverySorterWord@1002 : TEMPORARY Record "Delivery Sorter";VAR TempDeliverySorterOther@1001 : TEMPORARY Record "Delivery Sorter");
    VAR
      Attachment@1006 : Record Attachment;
      WordManagement@1007 : Codeunit WordManagement;
      ClientTypeManagement@1009 : Codeunit ClientTypeManagement;
      Window@1000 : Dialog;
      NoOfAttachments@1005 : Integer;
      I@1008 : Integer;
    BEGIN
      Window.OPEN(
        Text000 +
        '#1############ @2@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@\' +
        '#3############ @4@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

      Window.UPDATE(1,Text001);
      Window.UPDATE(3,Text002);

      if DeliverySorter.FIND('-') then begin
        NoOfAttachments := DeliverySorter.COUNT;
        repeat
          DeliverySorter.TESTFIELD("Correspondence Type");
          if not Attachment.GET(DeliverySorter."Attachment No.") then
            ERROR(Text008);
          case true of
            Attachment."File Extension" = 'HTML':
              begin
                TempDeliverySorterHtml := DeliverySorter;
                TempDeliverySorterHtml.INSERT;
              end;
            WordManagement.IsWordDocumentExtension(Attachment."File Extension") and not
            ClientTypeManagement.IsCommonWebClientType:
              begin
                TempDeliverySorterWord := DeliverySorter;
                TempDeliverySorterWord.INSERT;
              end;
            else begin
              TempDeliverySorterOther := DeliverySorter;
              TempDeliverySorterOther.INSERT;
            end;
          end;
          I := I + 1;
          Window.UPDATE(2,ROUND(I / NoOfAttachments * 10000,1));
        until DeliverySorter.NEXT = 0;
      end;
      Window.CLOSE;
    END;

    LOCAL PROCEDURE GetAttachment@46(VAR Attachment@1000 : Record Attachment;AttachmentNo@1001 : Integer;CheckExtension@1002 : Boolean);
    BEGIN
      Attachment.GET(AttachmentNo);
      if CheckExtension then
        Attachment.TESTFIELD("File Extension");
      Attachment.CALCFIELDS("Attachment File");
    END;

    LOCAL PROCEDURE PrepareDummyEmailBody@18() : Text;
    VAR
      FileMgt@1000 : Codeunit "File Management";
      OStream@1003 : OutStream;
      EmailBodyFile@1002 : File;
      EmailBodyFilePath@1001 : Text;
    BEGIN
      EmailBodyFilePath := FileMgt.ServerTempFileName('HTML');
      EmailBodyFile.CREATE(EmailBodyFilePath);
      EmailBodyFile.CREATEOUTSTREAM(OStream);
      OStream.WRITETEXT('<html><body></body></html>');
      EmailBodyFile.CLOSE;
      exit(EmailBodyFilePath);
    END;

    LOCAL PROCEDURE GetAttachmentFileDefaultName@11(AttachmentNo@1000 : Integer) : Text;
    VAR
      Attachment@1001 : Record Attachment;
    BEGIN
      Attachment.GET(AttachmentNo);
      exit(AttachmentTok + '.' + Attachment."File Extension");
    END;

    BEGIN
    END.
  }
}

