OBJECT Codeunit 111 Vend. Entry-SetAppl.ID
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Vendor Ledger Entry"=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      VendEntryApplID@1000 : Code[50];

    [External]
    PROCEDURE SetApplId@1(VAR VendLedgEntry@1000 : Record "Vendor Ledger Entry";ApplyingVendLedgEntry@1001 : Record "Vendor Ledger Entry";AppliesToID@1005 : Code[50]);
    VAR
      TempVendLedgEntry@1003 : TEMPORARY Record "Vendor Ledger Entry";
      VendLedgEntryToUpdate@1002 : Record "Vendor Ledger Entry";
    BEGIN
      VendLedgEntry.LOCKTABLE;
      if VendLedgEntry.FINDSET then begin
        // Make Applies-to ID
        if VendLedgEntry."Applies-to ID" <> '' then
          VendEntryApplID := ''
        else begin
          VendEntryApplID := AppliesToID;
          if VendEntryApplID = '' then begin
            VendEntryApplID := USERID;
            if VendEntryApplID = '' then
              VendEntryApplID := '***';
          end;
        end;
        repeat
          TempVendLedgEntry := VendLedgEntry;
          TempVendLedgEntry.INSERT;
        until VendLedgEntry.NEXT = 0;
      end;

      if TempVendLedgEntry.FINDSET then
        repeat
          VendLedgEntryToUpdate.COPY(TempVendLedgEntry);
          VendLedgEntryToUpdate.TESTFIELD(Open,true);
          VendLedgEntryToUpdate."Applies-to ID" := VendEntryApplID;
          if VendLedgEntryToUpdate."Applies-to ID" = '' then begin
            VendLedgEntryToUpdate."Accepted Pmt. Disc. Tolerance" := false;
            VendLedgEntryToUpdate."Accepted Payment Tolerance" := 0;
          end;

          if ((VendLedgEntryToUpdate."Amount to Apply" <> 0) and (VendEntryApplID = '')) or
             (VendEntryApplID = '')
          then
            VendLedgEntryToUpdate."Amount to Apply" := 0
          else
            if VendLedgEntryToUpdate."Amount to Apply" = 0 then begin
              VendLedgEntryToUpdate.CALCFIELDS("Remaining Amount");
              if VendLedgEntryToUpdate."Remaining Amount" <> 0 then
                VendLedgEntryToUpdate."Amount to Apply" := VendLedgEntryToUpdate."Remaining Amount";
            end;

          if VendLedgEntryToUpdate."Entry No." = ApplyingVendLedgEntry."Entry No." then
            VendLedgEntryToUpdate."Applying Entry" := ApplyingVendLedgEntry."Applying Entry";
          VendLedgEntryToUpdate.MODIFY;
        until TempVendLedgEntry.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

