OBJECT Report 950 Create Time Sheets
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Time Sheets;
    ProcessingOnly=true;
    OnInitReport=BEGIN
                   ResourcesSetup.GET;
                 END;

    OnPreReport=VAR
                  UserSetup@1005 : Record "User Setup";
                  i@1000 : Integer;
                  LastDate@1001 : Date;
                  FirstAccPeriodStartingDate@1002 : Date;
                  LastAccPeriodStartingDate@1003 : Date;
                BEGIN
                  if (not UserSetup.GET(USERID) or not UserSetup."Time Sheet Admin.") and UserSetup.WRITEPERMISSION then begin
                    if CONFIRM(OpenUserSetupQst,true) then
                      PAGE.RUN(PAGE::"User Setup");
                    ERROR('');
                  end;

                  if not UserSetup."Time Sheet Admin." then
                    ERROR(Text002);

                  if StartingDate = 0D then
                    ERROR(Text004,Text005);

                  if NoOfPeriods = 0 then
                    ERROR(Text004,Text006);

                  ResourcesSetup.TESTFIELD("Time Sheet Nos.");

                  EndingDate := CALCDATE('<1W>',StartingDate);

                  LastDate := StartingDate;
                  for i := 1 to NoOfPeriods do
                    LastDate := CALCDATE('<1W>',LastDate);

                  AccountingPeriod.SETFILTER("Starting Date",'..%1',StartingDate);
                  AccountingPeriod.FINDLAST;
                  FirstAccPeriodStartingDate := AccountingPeriod."Starting Date";

                  AccountingPeriod.SETFILTER("Starting Date",'..%1',LastDate);
                  AccountingPeriod.FINDLAST;
                  LastAccPeriodStartingDate := AccountingPeriod."Starting Date";

                  AccountingPeriod.SETRANGE("Starting Date",FirstAccPeriodStartingDate,LastAccPeriodStartingDate);
                  AccountingPeriod.FINDSET;
                  repeat
                    AccountingPeriod.TESTFIELD(Closed,false);
                  until AccountingPeriod.NEXT = 0;
                END;

    OnPostReport=BEGIN
                   if not HideDialog then
                     MESSAGE(Text003,TimeSheetCounter);
                 END;

  }
  DATASET
  {
    { 5444;    ;DataItem;Integer             ;
               DataItemTable=Integer;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               SETRANGE(Number,1,NoOfPeriods);
                             END;

               OnAfterGetRecord=BEGIN
                                  EndingDate := CALCDATE('<1W>',StartingDate) - 1;
                                END;
                                 }

    { 5508;1   ;DataItem;Resource            ;
               DataItemTable=Resource;
               DataItemTableView=WHERE("Use Time Sheet"=CONST(true));
               OnPreDataItem=BEGIN
                               if HidResourceFilter <> '' then
                                 SETFILTER("No.",HidResourceFilter);
                             END;

               OnAfterGetRecord=VAR
                                  TimeSheetMgt@1000 : Codeunit "Time Sheet Management";
                                BEGIN
                                  if CheckExistingPeriods then begin
                                    TimeSheetHeader.INIT;
                                    TimeSheetHeader."No." := NoSeriesMgt.GetNextNo(ResourcesSetup."Time Sheet Nos.",TODAY,true);
                                    TimeSheetHeader."Starting Date" := StartingDate;
                                    TimeSheetHeader."Ending Date" := EndingDate;
                                    TimeSheetHeader.VALIDATE("Resource No.","No.");
                                    TimeSheetHeader.INSERT(true);
                                    TimeSheetCounter += 1;

                                    if CreateLinesFromJobPlanning then
                                      TimeSheetMgt.CreateLinesFromJobPlanning(TimeSheetHeader);
                                  end;
                                END;

               OnPostDataItem=BEGIN
                                StartingDate := CALCDATE('<1W>',StartingDate);
                              END;

               ReqFilterFields="No.",Type }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=VAR
                   TimeSheetMgt@1000 : Codeunit "Time Sheet Management";
                 BEGIN
                   if NoOfPeriods = 0 then
                     NoOfPeriods := 1;

                   if TimeSheetHeader.FINDLAST then
                     StartingDate := TimeSheetHeader."Ending Date" + 1
                   else
                     StartingDate := TimeSheetMgt.FindNearestTimeSheetStartDate(WORKDATE);
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  Name=Starting Date;
                  CaptionML=ENU=Starting Date;
                  ToolTipML=ENU=Specifies the first date of the time sheet.;
                  ApplicationArea=#Jobs;
                  SourceExpr=StartingDate;
                  OnValidate=BEGIN
                               ValidateStartingDate;
                             END;
                              }

      { 2   ;2   ;Field     ;
                  Name=No. of Periods;
                  CaptionML=ENU=No. of Periods;
                  ToolTipML=ENU=Specifies the number of periods that the time sheet covers, such as 1 or 4.;
                  ApplicationArea=#Jobs;
                  SourceExpr=NoOfPeriods;
                  MinValue=1 }

      { 4   ;2   ;Field     ;
                  Name=Create Lines From Job Planning;
                  CaptionML=ENU=Create Lines From Job Planning;
                  ToolTipML=ENU=Specifies if you want to create time sheet lines that are based on job planning lines.;
                  ApplicationArea=#Jobs;
                  SourceExpr=CreateLinesFromJobPlanning }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      AccountingPeriod@1016 : Record "Accounting Period";
      ResourcesSetup@1009 : Record "Resources Setup";
      TimeSheetHeader@1005 : Record "Time Sheet Header";
      NoSeriesMgt@1010 : Codeunit NoSeriesManagement;
      HidResourceFilter@1001 : Code[250];
      StartingDate@1008 : Date;
      EndingDate@1000 : Date;
      TimeSheetCounter@1012 : Integer;
      NoOfPeriods@1006 : Integer;
      CreateLinesFromJobPlanning@1013 : Boolean;
      Text002@1011 : TextConst 'ENU=Time sheet administrator only is allowed to create time sheets.';
      Text003@1002 : TextConst 'ENU=%1 time sheets have been created.';
      Text004@1003 : TextConst 'ENU=%1 must be filled in.';
      Text005@1004 : TextConst 'ENU=Starting Date';
      Text006@1014 : TextConst 'ENU=No. of Periods';
      Text010@1019 : TextConst 'ENU=Starting Date must be %1.';
      HideDialog@1017 : Boolean;
      OpenUserSetupQst@1007 : TextConst 'ENU=You aren''t allowed to run this report. If you want, you can give yourself the Time Sheet Admin. rights, and then try again.\\ Do you want to do that now?';

    [External]
    PROCEDURE InitParameters@1(NewStartingDate@1000 : Date;NewNoOfPeriods@1002 : Integer;NewResourceFilter@1001 : Code[250];NewCreateLinesFromJobPlanning@1003 : Boolean;NewHideDialog@1004 : Boolean);
    BEGIN
      CLEARALL;
      ResourcesSetup.GET;
      StartingDate := NewStartingDate;
      NoOfPeriods := NewNoOfPeriods;
      HidResourceFilter := NewResourceFilter;
      CreateLinesFromJobPlanning := NewCreateLinesFromJobPlanning;
      HideDialog := NewHideDialog;
    END;

    LOCAL PROCEDURE CheckExistingPeriods@3() : Boolean;
    BEGIN
      TimeSheetHeader.SETRANGE("Resource No.",Resource."No.");
      TimeSheetHeader.SETRANGE("Starting Date",StartingDate);
      TimeSheetHeader.SETRANGE("Ending Date",EndingDate);
      if TimeSheetHeader.FINDFIRST then
        exit(false);

      exit(true);
    END;

    LOCAL PROCEDURE ValidateStartingDate@9();
    BEGIN
      if DATE2DWY(StartingDate,1) <> ResourcesSetup."Time Sheet First Weekday" + 1 then
        ERROR(Text010,ResourcesSetup."Time Sheet First Weekday");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

