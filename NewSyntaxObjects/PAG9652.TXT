OBJECT Page 9652 Report Layout Selection
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Report Layout Selection;
    InsertAllowed=false;
    DeleteAllowed=false;
    SourceTable="Report Layout Selection";
    PageType=List;
    SourceTableTemporary=true;
    OnOpenPage=BEGIN
                 SelectedCompany := COMPANYNAME;
               END;

    OnFindRecord=BEGIN
                   if not IsInitialized then
                     InitializeData;
                   exit(FIND(Which));
                 END;

    OnAfterGetRecord=BEGIN
                       GetRec;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           GetRec;
                         END;

    ActionList=ACTIONS
    {
      { 3       ;    ;ActionContainer;
                      Name=ActionContainer3;
                      ActionContainerType=ActionItems }
      { 4       ;1   ;Action    ;
                      Name=Customizations;
                      CaptionML=ENU=Custom Layouts;
                      ToolTipML=ENU=View or edit the custom layouts that are available for a report.;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page "Custom Report Layouts";
                      RunPageLink="Report ID"=FIELD("Report ID");
                      Promoted=true;
                      Image=Report;
                      PromotedCategory=Process }
      { 9       ;1   ;Action    ;
                      Name=RunReport;
                      CaptionML=ENU=Run Report;
                      ToolTipML=ENU=Run a test report.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=Report;
                      PromotedCategory=Report;
                      OnAction=BEGIN
                                 REPORT.RUN("Report ID");
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=BulkUpdate;
                      CaptionML=ENU=Update All layouts;
                      ToolTipML=ENU=Update specific report layouts or all custom report layouts that might be affected by dataset changes.;
                      ApplicationArea=#Basic,#Suite;
                      Image=UpdateXML;
                      OnAction=VAR
                                 DocumentReportMgt@1000 : Codeunit "Document Report Mgt.";
                               BEGIN
                                 DocumentReportMgt.BulkUpgrade(false);
                               END;
                                }
      { 16      ;1   ;Action    ;
                      Name=TestUpdate;
                      CaptionML=ENU=Test Layout Updates;
                      ToolTipML=ENU=Check if there are any updates detected.;
                      ApplicationArea=#Basic,#Suite;
                      Image=TestReport;
                      OnAction=VAR
                                 DocumentReportMgt@1000 : Codeunit "Document Report Mgt.";
                               BEGIN
                                 DocumentReportMgt.BulkUpgrade(true);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 11  ;0   ;Container ;
                Name=Container11;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                Name=Company;
                CaptionML=ENU=Company;
                GroupType=Group }

    { 12  ;2   ;Field     ;
                Name=SelectedCompany;
                CaptionML=ENU=Company Name;
                ToolTipML=ENU=Specifies the name of the company that is used for the report.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SelectedCompany;
                TableRelation=Company;
                Importance=Promoted;
                OnValidate=BEGIN
                             CurrPage.UPDATE(false);
                           END;

                ImplicitType=Text30 }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 15  ;2   ;Field     ;
                Name=Report ID;
                ToolTipML=ENU=Specifies the ID of the report that uses the report layout.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Report ID";
                Editable=FALSE;
                ImplicitType=Integer }

    { 6   ;2   ;Field     ;
                Name=Report Name;
                ToolTipML=ENU=Specifies the name of the selected report layout.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Report Name";
                Editable=FALSE;
                ImplicitType=Text80 }

    { 2   ;2   ;Field     ;
                Name=Type;
                CaptionML=ENU=Selected Layout;
                ToolTipML=ENU=Specifies the report layout that is currently used on the report.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Type;
                OnValidate=BEGIN
                             UpdateRec;
                             COMMIT;
                             LookupCustomLayout;
                           END;

                ImplicitType=Option }

    { 13  ;2   ;Field     ;
                Name=Custom Report Layout Code;
                ToolTipML=ENU=Specifies the custom report layout.;
                ApplicationArea=#Advanced;
                SourceExpr="Custom Report Layout Code";
                TableRelation="Custom Report Layout" WHERE ("Report ID"=FIELD("Report ID"));
                Visible=FALSE;
                OnValidate=BEGIN
                             ReportLayoutSelection.VALIDATE("Custom Report Layout Code",ReportLayoutSelection."Custom Report Layout Code");
                             UpdateRec;
                           END;

                ImplicitType=Code20 }

    { 5   ;2   ;Field     ;
                Name=CustomLayoutDescription;
                CaptionML=ENU=Custom Layout Description;
                ToolTipML=ENU=Specifies the description for a custom layout that is used by the report when the Selected Layout field is set to Custom.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CustomLayoutDescription;
                OnValidate=VAR
                             CustomReportLayout2@1000 : Record "Custom Report Layout";
                           BEGIN
                             CustomReportLayout2.SETCURRENTKEY("Report ID","Company Name",Type);
                             CustomReportLayout2.SETRANGE("Report ID",ReportLayoutSelection."Report ID");
                             CustomReportLayout2.SETFILTER("Company Name",'%1|%2','',SelectedCompany);
                             CustomReportLayout2.SETFILTER(
                               Description,STRSUBSTNO('@*%1*',CustomLayoutDescription));
                             if not CustomReportLayout2.FINDFIRST then
                               ERROR(CouldNotFindCustomReportLayoutErr,CustomLayoutDescription);

                             if CustomReportLayout2.Code <> "Custom Report Layout Code" then begin
                               VALIDATE("Custom Report Layout Code",CustomReportLayout2.Code);
                               UpdateRec;
                             end;
                             CurrPage.UPDATE(false);
                           END;

                OnLookup=BEGIN
                           LookupCustomLayout;
                         END;

                ImplicitType=Text }

    { 8   ;0   ;Container ;
                Name=Container8;
                ContainerType=FactBoxArea }

    { 7   ;1   ;Part      ;
                Name=Custom Layouts;
                CaptionML=ENU=Custom Layouts;
                ApplicationArea=#Basic,#Suite;
                SubPageLink="Report ID"=FIELD("Report ID");
                PagePartID="Report Layouts Part";
                PartType=Page;
                ShowFilter=false;
                UpdatePropagation=Both }

  }
  CODE
  {
    VAR
      ReportLayoutSelection@1000 : Record "Report Layout Selection";
      SelectedCompany@1004 : Text[30];
      WrongCompanyErr@1005 : TextConst 'ENU=You cannot select a layout that is specific to another company.';
      CustomLayoutDescription@1002 : Text;
      IsInitialized@1001 : Boolean;
      CouldNotFindCustomReportLayoutErr@1003 : TextConst '@@@=%1 Description of custom report layout;ENU=There is no custom report layout with %1 in the description.';

    LOCAL PROCEDURE UpdateRec@18();
    BEGIN
      if ReportLayoutSelection.GET("Report ID",SelectedCompany) then begin
        ReportLayoutSelection := Rec;
        ReportLayoutSelection."Report Name" := '';
        ReportLayoutSelection."Company Name" := SelectedCompany;
        ReportLayoutSelection.MODIFY;
      end else begin
        CLEAR(ReportLayoutSelection);
        ReportLayoutSelection := Rec;
        ReportLayoutSelection."Report Name" := '';
        ReportLayoutSelection."Company Name" := SelectedCompany;
        ReportLayoutSelection.INSERT(true);
      end;
    END;

    LOCAL PROCEDURE GetRec@2();
    BEGIN
      if not GET("Report ID",'') then
        exit;
      if not ReportLayoutSelection.GET("Report ID",SelectedCompany) then begin
        ReportLayoutSelection.INIT;
        ReportLayoutSelection.Type := GetDefaultType("Report ID");
      end;
      Type := ReportLayoutSelection.Type;
      "Custom Report Layout Code" := ReportLayoutSelection."Custom Report Layout Code";
      CALCFIELDS("Report Layout Description");
      CustomLayoutDescription := "Report Layout Description";
      MODIFY;
    END;

    LOCAL PROCEDURE LookupCustomLayout@5();
    BEGIN
      if Type <> Type::"Custom Layout" then
        exit;

      if not SelectReportLayout then
        exit;

      GetRec;
      if (Type = Type::"Custom Layout") and
         ("Custom Report Layout Code" = '')
      then begin
        VALIDATE(Type,GetDefaultType("Report ID"));
        UpdateRec;
      end;
      CurrPage.UPDATE(false);
    END;

    LOCAL PROCEDURE SelectReportLayout@3() : Boolean;
    VAR
      CustomReportLayout2@1000 : Record "Custom Report Layout";
      OK@1001 : Boolean;
    BEGIN
      CustomReportLayout2.FILTERGROUP(4);
      CustomReportLayout2.SETRANGE("Report ID","Report ID");
      CustomReportLayout2.FILTERGROUP(0);
      CustomReportLayout2.SETFILTER("Company Name",'%1|%2',SelectedCompany,'');
      OK := PAGE.RUNMODAL(PAGE::"Custom Report Layouts",CustomReportLayout2) = ACTION::LookupOK;
      if OK then begin
        if CustomReportLayout2.FIND then begin
          if not (CustomReportLayout2."Company Name" in [SelectedCompany,'']) then
            ERROR(WrongCompanyErr);
          "Custom Report Layout Code" := CustomReportLayout2.Code;
          Type := Type::"Custom Layout";
          UpdateRec;
        end
      end else
        if Type = Type::"Custom Layout" then
          if CustomReportLayout2.ISEMPTY then begin
            Type := GetDefaultType("Report ID");
            "Custom Report Layout Code" := '';
            UpdateRec;
          end;
      exit(OK);
    END;

    LOCAL PROCEDURE InitializeData@1();
    VAR
      ReportMetadata@1000 : Record "Report Metadata";
    BEGIN
      ReportMetadata.SETRANGE(ProcessingOnly,false);
      if ReportMetadata.FINDSET then
        repeat
          INIT;
          "Report ID" := ReportMetadata.ID;
          "Report Name" := ReportMetadata.Caption;
          INSERT;
        until ReportMetadata.NEXT = 0;
      if FINDFIRST then;
      IsInitialized := true;
    END;

    BEGIN
    END.
  }
}

