OBJECT Codeunit 248 VAT Lookup Ext. Data Hndl
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="VAT Registration Log";
    Permissions=TableData "VAT Registration Log"=rimd;
    OnRun=BEGIN
            VATRegistrationLog := Rec;

            LookupVatRegistrationFromWebService(true);

            Rec := VATRegistrationLog;
          END;

  }
  CODE
  {
    VAR
      NamespaceTxt@1003 : TextConst '@@@={Locked};ENU=urn:ec.europa.eu:taxud:vies:services:checkVat:types';
      VATRegistrationLog@1000 : Record "VAT Registration Log";
      VATRegistrationLogMgt@1002 : Codeunit "VAT Registration Log Mgt.";
      VatRegNrValidationWebServiceURLTxt@1001 : TextConst '@@@={Locked};ENU=http://ec.europa.eu/taxation_customs/vies/services/checkVatService';
      MissingWebServiceURLErr@1004 : TextConst 'ENU=The VAT Registration No. Validation URL is not specified in the General Ledger Setup window.';
      VATRegistrationURL@1005 : Text;

    LOCAL PROCEDURE LookupVatRegistrationFromWebService@7(ShowErrors@1000 : Boolean);
    VAR
      RequestBodyTempBlob@1002 : Record TempBlob;
    BEGIN
      RequestBodyTempBlob.INIT;

      SendRequestToVatRegistrationService(RequestBodyTempBlob,ShowErrors);

      InsertLogEntry(RequestBodyTempBlob);

      COMMIT;
    END;

    LOCAL PROCEDURE SendRequestToVatRegistrationService@1(VAR BodyTempBlob@1004 : Record TempBlob;ShowErrors@1003 : Boolean);
    VAR
      VATRegNoSrvConfig@1005 : Record "VAT Reg. No. Srv Config";
      SOAPWebServiceRequestMgt@1001 : Codeunit "SOAP Web Service Request Mgt.";
      ResponseInStream@1002 : InStream;
      InStream@1006 : InStream;
      ResponseOutStream@1008 : OutStream;
    BEGIN
      PrepareSOAPRequestBody(BodyTempBlob);

      BodyTempBlob.Blob.CREATEINSTREAM(InStream);
      VATRegistrationURL := VATRegNoSrvConfig.GetVATRegNoURL;
      if VATRegistrationURL = '' then
        ERROR(MissingWebServiceURLErr);
      SOAPWebServiceRequestMgt.SetGlobals(InStream,VATRegistrationURL,'','');
      SOAPWebServiceRequestMgt.DisableHttpsCheck;
      SOAPWebServiceRequestMgt.SetTimeout(60000);

      if SOAPWebServiceRequestMgt.SendRequestToWebService then begin
        SOAPWebServiceRequestMgt.GetResponseContent(ResponseInStream);

        BodyTempBlob.Blob.CREATEOUTSTREAM(ResponseOutStream);
        COPYSTREAM(ResponseOutStream,ResponseInStream);
      end else
        if ShowErrors then
          SOAPWebServiceRequestMgt.ProcessFaultResponse('');
    END;

    LOCAL PROCEDURE PrepareSOAPRequestBody@12(VAR BodyTempBlob@1000 : Record TempBlob);
    VAR
      XMLDOMMgt@1006 : Codeunit "XML DOM Management";
      BodyContentInputStream@1004 : InStream;
      BodyContentOutputStream@1005 : OutStream;
      BodyContentXmlDoc@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      EnvelopeXmlNode@1012 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CreatedXmlNode@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      BodyTempBlob.Blob.CREATEINSTREAM(BodyContentInputStream);
      BodyContentXmlDoc := BodyContentXmlDoc.XmlDocument;

      XMLDOMMgt.AddRootElementWithPrefix(BodyContentXmlDoc,'checkVatApprox','',NamespaceTxt,EnvelopeXmlNode);
      XMLDOMMgt.AddElement(EnvelopeXmlNode,'countryCode',VATRegistrationLog.GetCountryCode,NamespaceTxt,CreatedXmlNode);
      XMLDOMMgt.AddElement(EnvelopeXmlNode,'vatNumber',VATRegistrationLog.GetVATRegNo,NamespaceTxt,CreatedXmlNode);

      CLEAR(BodyTempBlob.Blob);
      BodyTempBlob.Blob.CREATEOUTSTREAM(BodyContentOutputStream);
      BodyContentXmlDoc.Save(BodyContentOutputStream);
    END;

    LOCAL PROCEDURE InsertLogEntry@4(ResponseBodyTempBlob@1000 : Record TempBlob);
    VAR
      XMLDOMManagement@1001 : Codeunit "XML DOM Management";
      XMLDocOut@1012 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      InStream@1009 : InStream;
    BEGIN
      ResponseBodyTempBlob.Blob.CREATEINSTREAM(InStream);
      XMLDOMManagement.LoadXMLDocumentFromInStream(InStream,XMLDocOut);

      VATRegistrationLogMgt.LogVerification(VATRegistrationLog,XMLDocOut,NamespaceTxt);
    END;

    [External]
    PROCEDURE GetVATRegNrValidationWebServiceURL@5() : Text[250];
    BEGIN
      exit(VatRegNrValidationWebServiceURLTxt);
    END;

    BEGIN
    END.
  }
}

