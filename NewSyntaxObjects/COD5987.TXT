OBJECT Codeunit 5987 Serv-Posting Journals Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Invoice Post. Buffer"=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ServiceHeader@1002 : Record "Service Header";
      Currency@1018 : Record Currency;
      CurrExchRate@1012 : Record "Currency Exchange Rate";
      SalesSetup@1013 : Record "Sales & Receivables Setup";
      TempValueEntryRelation@1011 : TEMPORARY Record "Value Entry Relation";
      ServITRMgt@1001 : Codeunit "Serv-Item Tracking Rsrv. Mgt.";
      GenJnlPostLine@1004 : Codeunit "Gen. Jnl.-Post Line";
      ItemJnlPostLine@1000 : Codeunit "Item Jnl.-Post Line";
      ResJnlPostLine@1005 : Codeunit "Res. Jnl.-Post Line";
      ServLedgEntryPostSale@1025 : Codeunit "ServLedgEntries-Post";
      TimeSheetMgt@1010 : Codeunit "Time Sheet Management";
      WhseJnlRegisterLine@1007 : Codeunit "Whse. Jnl.-Register Line";
      GenJnlLineDocNo@1016 : Code[20];
      GenJnlLineExtDocNo@1006 : Code[20];
      SrcCode@1003 : Code[10];
      Consume@1008 : Boolean;
      Invoice@1009 : Boolean;
      ItemJnlRollRndg@1015 : Boolean;
      ServiceLinePostingDate@1020 : Date;

    [External]
    PROCEDURE Initialize@9(VAR TempServHeader@1000 : Record "Service Header";TmpConsume@1003 : Boolean;TmpInvoice@1004 : Boolean);
    VAR
      SrcCodeSetup@1001 : Record "Source Code Setup";
    BEGIN
      ServiceHeader := TempServHeader;
      SetPostingOptions(TmpConsume,TmpInvoice);
      SrcCodeSetup.GET;
      SalesSetup.GET;
      SrcCode := SrcCodeSetup."Service Management";
      Currency.Initialize(ServiceHeader."Currency Code");
      ItemJnlRollRndg := false;
      GenJnlLineDocNo := '';
      GenJnlLineExtDocNo := '';
    END;

    [External]
    PROCEDURE Finalize@7();
    BEGIN
      CLEAR(GenJnlPostLine);
      CLEAR(ResJnlPostLine);
      CLEAR(ItemJnlPostLine);
      CLEAR(ServLedgEntryPostSale);
    END;

    [External]
    PROCEDURE SetPostingOptions@23(PassedConsume@1001 : Boolean;PassedInvoice@1002 : Boolean);
    BEGIN
      Consume := PassedConsume;
      Invoice := PassedInvoice;
    END;

    [External]
    PROCEDURE SetItemJnlRollRndg@12(PassedItemJnlRollRndg@1000 : Boolean);
    BEGIN
      ItemJnlRollRndg := PassedItemJnlRollRndg;
    END;

    [External]
    PROCEDURE SetGenJnlLineDocNos@21(DocNo@1001 : Code[20];ExtDocNo@1002 : Code[20]);
    BEGIN
      GenJnlLineDocNo := DocNo;
      GenJnlLineExtDocNo := ExtDocNo;
    END;

    LOCAL PROCEDURE IsWarehouseShipment@29() : Boolean;
    VAR
      WarehouseShipmentLine@1000 : Record "Warehouse Shipment Line";
    BEGIN
      with WarehouseShipmentLine do begin
        SETRANGE("Source Type",DATABASE::"Service Line");
        SETRANGE("Source Subtype",1);
        SETRANGE("Source No.",ServiceHeader."No.");
        exit(not ISEMPTY);
      end;
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10];VAR Location@1001 : Record Location);
    BEGIN
      if LocationCode = '' then
        Location.GetLocationSetup(LocationCode,Location)
      else
        if Location.Code <> LocationCode then
          Location.GET(LocationCode);
    END;

    [Internal]
    PROCEDURE PostItemJnlLine@10(VAR ServiceLine@1000 : Record "Service Line";QtyToBeShipped@1001 : Decimal;QtyToBeShippedBase@1002 : Decimal;QtyToBeConsumed@1016 : Decimal;QtyToBeConsumedBase@1017 : Decimal;QtyToBeInvoiced@1003 : Decimal;QtyToBeInvoicedBase@1004 : Decimal;ItemLedgShptEntryNo@1005 : Integer;VAR TrackingSpecification@1009 : Record "Tracking Specification";VAR TempTrackingSpecificationInv@1024 : Record "Tracking Specification";VAR TempHandlingSpecification@1023 : Record "Tracking Specification";VAR TempTrackingSpecification@1008 : TEMPORARY Record "Tracking Specification";VAR ServShptHeader@1006 : Record "Service Shipment Header";ServShptLineDocNo@1022 : Code[20]) : Integer;
    VAR
      ItemJnlLine@1014 : Record "Item Journal Line";
      Location@1013 : Record Location;
      TempWhseJnlLine@1012 : TEMPORARY Record "Warehouse Journal Line";
      TempWhseJnlLine2@1011 : TEMPORARY Record "Warehouse Journal Line";
      RemAmt@1021 : Decimal;
      RemDiscAmt@1019 : Decimal;
      PostWhseJnlLine@1010 : Boolean;
      CheckApplFromItemEntry@1020 : Boolean;
    BEGIN
      CLEAR(ItemJnlPostLine);
      if not ItemJnlRollRndg then begin
        RemAmt := 0;
        RemDiscAmt := 0;
      end;

      with ItemJnlLine do begin
        INIT;
        CopyFromServHeader(ServiceHeader);
        CopyFromServLine(ServiceLine);

        CopyTrackingFromSpec(TrackingSpecification);

        if QtyToBeShipped = 0 then begin
          if ServiceLine."Document Type" = ServiceLine."Document Type"::"Credit Memo" then
            "Document Type" := "Document Type"::"Service Credit Memo"
          else
            "Document Type" := "Document Type"::"Service Invoice";
          if QtyToBeConsumed <> 0 then begin
            "Entry Type" := "Entry Type"::"Negative Adjmt.";
            "Document No." := ServShptLineDocNo;
            "External Document No." := '';
            "Document Type" := "Document Type"::"Service Shipment";
          end else begin
            "Document No." := GenJnlLineDocNo;
            "External Document No." := GenJnlLineExtDocNo;
          end;
          "Posting No. Series" := ServiceHeader."Posting No. Series";
        end else begin
          if ServiceLine."Document Type" <> ServiceLine."Document Type"::"Credit Memo" then begin
            "Document Type" := "Document Type"::"Service Shipment";
            "Document No." := ServShptHeader."No.";
            "Posting No. Series" := ServShptHeader."No. Series";
          end;
          if (QtyToBeInvoiced <> 0) or (QtyToBeConsumed <> 0) then begin
            if QtyToBeConsumed <> 0 then
              "Entry Type" := "Entry Type"::"Negative Adjmt.";
            "Invoice No." := GenJnlLineDocNo;
            "External Document No." := GenJnlLineExtDocNo;
            if "Document No." = '' then begin
              if ServiceLine."Document Type" = ServiceLine."Document Type"::"Credit Memo" then
                "Document Type" := "Document Type"::"Service Credit Memo"
              else
                "Document Type" := "Document Type"::"Service Invoice";
              "Document No." := GenJnlLineDocNo;
            end;
            "Posting No. Series" := ServiceHeader."Posting No. Series";
          end;
          if (QtyToBeConsumed <> 0) and ("Document No." = '') then
            "Document No." := ServShptLineDocNo;
        end;

        "Document Line No." := ServiceLine."Line No.";
        Quantity := -QtyToBeShipped;
        "Quantity (Base)" := -QtyToBeShippedBase;
        if QtyToBeInvoiced <> 0 then begin
          "Invoiced Quantity" := -QtyToBeInvoiced;
          "Invoiced Qty. (Base)" := -QtyToBeInvoicedBase;
        end else
          if QtyToBeConsumed <> 0 then begin
            "Invoiced Quantity" := -QtyToBeConsumed;
            "Invoiced Qty. (Base)" := -QtyToBeConsumedBase;
          end;
        "Unit Cost" := ServiceLine."Unit Cost (LCY)";
        "Source Currency Code" := ServiceHeader."Currency Code";
        "Unit Cost (ACY)" := ServiceLine."Unit Cost";
        "Value Entry Type" := "Value Entry Type"::"Direct Cost";
        "Applies-from Entry" := ServiceLine."Appl.-from Item Entry";

        if Invoice and (QtyToBeInvoiced <> 0) then begin
          Amount := -(ServiceLine.Amount * (QtyToBeInvoiced / ServiceLine."Qty. to Invoice") - RemAmt);
          if ServiceHeader."Prices Including VAT" then
            "Discount Amount" :=
              -((ServiceLine."Line Discount Amount" + ServiceLine."Inv. Discount Amount") / (1 + ServiceLine."VAT %" / 100) *
                (QtyToBeInvoiced / ServiceLine."Qty. to Invoice") - RemDiscAmt)
          else
            "Discount Amount" :=
              -((ServiceLine."Line Discount Amount" + ServiceLine."Inv. Discount Amount") *
                (QtyToBeInvoiced / ServiceLine."Qty. to Invoice") - RemDiscAmt);
        end else
          if Consume and (QtyToBeConsumed <> 0) then begin
            Amount := -(ServiceLine.Amount * QtyToBeConsumed - RemAmt);
            "Discount Amount" :=
              -(ServiceLine."Line Discount Amount" * QtyToBeConsumed - RemDiscAmt);
          end;

        if (QtyToBeInvoiced <> 0) or (QtyToBeConsumed <> 0) then begin
          RemAmt := Amount - ROUND(Amount);
          RemDiscAmt := "Discount Amount" - ROUND("Discount Amount");
          Amount := ROUND(Amount);
          "Discount Amount" := ROUND("Discount Amount");
        end else begin
          if ServiceHeader."Prices Including VAT" then
            Amount :=
              -((QtyToBeShipped *
                 ServiceLine."Unit Price" * (1 - ServiceLine."Line Discount %" / 100) / (1 + ServiceLine."VAT %" / 100)) - RemAmt)
          else
            Amount :=
              -((QtyToBeShipped * ServiceLine."Unit Price" * (1 - ServiceLine."Line Discount %" / 100)) - RemAmt);
          RemAmt := Amount - ROUND(Amount);
          if ServiceHeader."Currency Code" <> '' then
            Amount :=
              ROUND(
                CurrExchRate.ExchangeAmtFCYToLCY(
                  ServiceLine."Posting Date",ServiceHeader."Currency Code",
                  Amount,ServiceHeader."Currency Factor"))
          else
            Amount := ROUND(Amount);
        end;

        "Source Code" := SrcCode;
        "Item Shpt. Entry No." := ItemLedgShptEntryNo;
        "Invoice-to Source No." := ServiceLine."Bill-to Customer No.";

        if SalesSetup."Exact Cost Reversing Mandatory" and (ServiceLine.Type = ServiceLine.Type::Item) then
          if ServiceLine."Document Type" = ServiceLine."Document Type"::"Credit Memo" then
            CheckApplFromItemEntry := ServiceLine.Quantity > 0
          else
            CheckApplFromItemEntry := ServiceLine.Quantity < 0;

        if (ServiceLine."Location Code" <> '') and (ServiceLine.Type = ServiceLine.Type::Item) and (Quantity <> 0) then begin
          GetLocation(ServiceLine."Location Code",Location);
          if ((ServiceLine."Document Type" in [ServiceLine."Document Type"::Invoice,ServiceLine."Document Type"::"Credit Memo"]) and
              Location."Directed Put-away and Pick") or
             (Location."Bin Mandatory" and not IsWarehouseShipment)
          then begin
            CreateWhseJnlLine(ItemJnlLine,ServiceLine,TempWhseJnlLine,Location);
            PostWhseJnlLine := true;
          end;
        end;

        if QtyToBeShippedBase <> 0 then
          if ServiceLine."Document Type" = ServiceLine."Document Type"::"Credit Memo" then
            ServITRMgt.TransServLineToItemJnlLine(ServiceLine,ItemJnlLine,QtyToBeShippedBase,CheckApplFromItemEntry)
          else
            ServITRMgt.TransferReservToItemJnlLine(
              ServiceLine,ItemJnlLine,-QtyToBeShippedBase,CheckApplFromItemEntry);

        if CheckApplFromItemEntry then
          ServiceLine.TESTFIELD("Appl.-from Item Entry");

        ItemJnlPostLine.RunWithCheck(ItemJnlLine);

        ItemJnlPostLine.CollectValueEntryRelation(TempValueEntryRelation,'');

        if ItemJnlPostLine.CollectTrackingSpecification(TempHandlingSpecification) then
          ServITRMgt.InsertTempHandlngSpecification(DATABASE::"Service Line",
            ServiceLine,TempHandlingSpecification,
            TempTrackingSpecification,TempTrackingSpecificationInv,
            QtyToBeInvoiced <> 0);

        if PostWhseJnlLine then begin
          ServITRMgt.SplitWhseJnlLine(TempWhseJnlLine,TempWhseJnlLine2,TempTrackingSpecification,false);
          if TempWhseJnlLine2.FIND('-') then
            repeat
              WhseJnlRegisterLine.RegisterWhseJnlLine(TempWhseJnlLine2);
            until TempWhseJnlLine2.NEXT = 0;
        end;
        exit("Item Shpt. Entry No.");
      end;
    END;

    LOCAL PROCEDURE CreateWhseJnlLine@7302(ItemJnlLine@1000 : Record "Item Journal Line";ServLine@1001 : Record "Service Line";VAR TempWhseJnlLine@1002 : TEMPORARY Record "Warehouse Journal Line";Location@1005 : Record Location);
    VAR
      WMSMgmt@1004 : Codeunit "WMS Management";
      WhseMgt@1003 : Codeunit "Whse. Management";
    BEGIN
      with ServLine do begin
        WMSMgmt.CheckAdjmtBin(Location,ItemJnlLine.Quantity,true);
        WMSMgmt.CreateWhseJnlLine(ItemJnlLine,0,TempWhseJnlLine,false);
        TempWhseJnlLine."Source Type" := DATABASE::"Service Line";
        TempWhseJnlLine."Source Subtype" := "Document Type";
        TempWhseJnlLine."Source Code" := SrcCode;
        TempWhseJnlLine."Source Document" := WhseMgt.GetSourceDocument(TempWhseJnlLine."Source Type",TempWhseJnlLine."Source Subtype");
        TempWhseJnlLine."Source No." := "Document No.";
        TempWhseJnlLine."Source Line No." := "Line No.";
        case "Document Type" of
          "Document Type"::Order:
            TempWhseJnlLine."Reference Document" :=
              TempWhseJnlLine."Reference Document"::"Posted Shipment";
          "Document Type"::Invoice:
            TempWhseJnlLine."Reference Document" :=
              TempWhseJnlLine."Reference Document"::"Posted S. Inv.";
          "Document Type"::"Credit Memo":
            TempWhseJnlLine."Reference Document" :=
              TempWhseJnlLine."Reference Document"::"Posted S. Cr. Memo";
        end;
        TempWhseJnlLine."Reference No." := ItemJnlLine."Document No.";
      end;
    END;

    [Internal]
    PROCEDURE PostInvoicePostBufferLine@2(VAR InvoicePostBuffer@1002 : Record "Invoice Post. Buffer";DocType@1003 : Integer;DocNo@1004 : Code[20];ExtDocNo@1005 : Code[20]);
    VAR
      GenJnlLine@1000 : Record "Gen. Journal Line";
    BEGIN
      with GenJnlLine do begin
        InitNewLine(
          ServiceLinePostingDate,ServiceHeader."Document Date",ServiceHeader."Posting Description",
          InvoicePostBuffer."Global Dimension 1 Code",InvoicePostBuffer."Global Dimension 2 Code",
          InvoicePostBuffer."Dimension Set ID",ServiceHeader."Reason Code");

        CopyDocumentFields(DocType,DocNo,ExtDocNo,SrcCode,'');

        CopyFromServiceHeader(ServiceHeader);
        CopyFromInvoicePostBuffer(InvoicePostBuffer);
        "Gen. Posting Type" := "Gen. Posting Type"::Sale;

        GenJnlPostLine.RunWithCheck(GenJnlLine);
      end;
    END;

    [Internal]
    PROCEDURE PostCustomerEntry@3(VAR TotalServiceLine@1004 : Record "Service Line";VAR TotalServiceLineLCY@1005 : Record "Service Line";DocType@1003 : Integer;DocNo@1002 : Code[20];ExtDocNo@1001 : Code[20]);
    VAR
      GenJnlLine@1000 : Record "Gen. Journal Line";
    BEGIN
      with GenJnlLine do begin
        InitNewLine(
          ServiceLinePostingDate,ServiceHeader."Document Date",ServiceHeader."Posting Description",
          ServiceHeader."Shortcut Dimension 1 Code",ServiceHeader."Shortcut Dimension 2 Code",
          ServiceHeader."Dimension Set ID",ServiceHeader."Reason Code");

        CopyDocumentFields(DocType,DocNo,ExtDocNo,SrcCode,'');

        "Account Type" := "Account Type"::Customer;
        "Account No." := ServiceHeader."Bill-to Customer No.";
        CopyFromServiceHeader(ServiceHeader);
        SetCurrencyFactor(ServiceHeader."Currency Code",ServiceHeader."Currency Factor");

        CopyFromServiceHeaderApplyTo(ServiceHeader);
        CopyFromServiceHeaderPayment(ServiceHeader);

        Amount := -TotalServiceLine."Amount Including VAT";
        "Source Currency Amount" := -TotalServiceLine."Amount Including VAT";
        "Amount (LCY)" := -TotalServiceLineLCY."Amount Including VAT";
        "Sales/Purch. (LCY)" := -TotalServiceLineLCY.Amount;
        "Profit (LCY)" := -(TotalServiceLineLCY.Amount - TotalServiceLineLCY."Unit Cost (LCY)");
        "Inv. Discount (LCY)" := -TotalServiceLineLCY."Inv. Discount Amount";
        "System-Created Entry" := true;

        GenJnlPostLine.RunWithCheck(GenJnlLine);
      end;
    END;

    [Internal]
    PROCEDURE PostBalancingEntry@11(VAR TotalServiceLine@1002 : Record "Service Line";VAR TotalServiceLineLCY@1006 : Record "Service Line";DocType@1005 : Integer;DocNo@1004 : Code[20];ExtDocNo@1003 : Code[20]);
    VAR
      CustLedgEntry@1000 : Record "Cust. Ledger Entry";
      GenJnlLine@1001 : Record "Gen. Journal Line";
    BEGIN
      CustLedgEntry.FINDLAST;
      with GenJnlLine do begin
        InitNewLine(
          ServiceLinePostingDate,ServiceHeader."Document Date",ServiceHeader."Posting Description",
          ServiceHeader."Shortcut Dimension 1 Code",ServiceHeader."Shortcut Dimension 2 Code",
          ServiceHeader."Dimension Set ID",ServiceHeader."Reason Code");

        if ServiceHeader."Document Type" = ServiceHeader."Document Type"::"Credit Memo" then
          CopyDocumentFields("Document Type"::Refund,DocNo,ExtDocNo,SrcCode,'')
        else
          CopyDocumentFields("Document Type"::Payment,DocNo,ExtDocNo,SrcCode,'');

        "Account Type" := "Account Type"::Customer;
        "Account No." := ServiceHeader."Bill-to Customer No.";
        CopyFromServiceHeader(ServiceHeader);
        SetCurrencyFactor(ServiceHeader."Currency Code",ServiceHeader."Currency Factor");

        SetApplyToDocNo(ServiceHeader,GenJnlLine,DocType,DocNo);

        Amount := TotalServiceLine."Amount Including VAT" + CustLedgEntry."Remaining Pmt. Disc. Possible";
        "Source Currency Amount" := Amount;
        CustLedgEntry.CALCFIELDS(Amount);
        if CustLedgEntry.Amount = 0 then
          "Amount (LCY)" := TotalServiceLineLCY."Amount Including VAT"
        else
          "Amount (LCY)" :=
            TotalServiceLineLCY."Amount Including VAT" +
            ROUND(CustLedgEntry."Remaining Pmt. Disc. Possible" / CustLedgEntry."Adjusted Currency Factor");

        GenJnlPostLine.RunWithCheck(GenJnlLine);
      end;
    END;

    LOCAL PROCEDURE SetApplyToDocNo@17(ServiceHeader@1000 : Record "Service Header";VAR GenJnlLine@1001 : Record "Gen. Journal Line";DocType@1002 : Option;DocNo@1003 : Code[20]);
    BEGIN
      with GenJnlLine do begin
        if ServiceHeader."Bal. Account Type" = ServiceHeader."Bal. Account Type"::"Bank Account" then
          "Bal. Account Type" := "Bal. Account Type"::"Bank Account";
        "Bal. Account No." := ServiceHeader."Bal. Account No.";
        "Applies-to Doc. Type" := DocType;
        "Applies-to Doc. No." := DocNo;
      end;
    END;

    [External]
    PROCEDURE PostResJnlLineShip@8(VAR ServiceLine@1002 : Record "Service Line";DocNo@1003 : Code[20];ExtDocNo@1004 : Code[20]);
    VAR
      ResJnlLine@1000 : Record "Res. Journal Line";
    BEGIN
      if ServiceLine."Time Sheet No." <> '' then
        TimeSheetMgt.CheckServiceLine(ServiceLine);

      PostResJnlLine(
        ServiceHeader,ServiceLine,
        DocNo,ExtDocNo,SrcCode,ServiceHeader."Posting No. Series",
        ResJnlLine."Entry Type"::Usage,-ServiceLine."Qty. to Ship",
        ServiceLine.Amount / ServiceLine."Qty. to Ship",-ServiceLine.Amount);

      TimeSheetMgt.CreateTSLineFromServiceLine(ServiceLine,GenJnlLineDocNo,true);
    END;

    [External]
    PROCEDURE PostResJnlLineSale@66(VAR ServiceLine@1002 : Record "Service Line";DocNo@1003 : Code[20];ExtDocNo@1004 : Code[20]);
    VAR
      ResJnlLine@1000 : Record "Res. Journal Line";
    BEGIN
      PostResJnlLine(
        ServiceHeader,ServiceLine,DocNo,ExtDocNo,SrcCode,ServiceHeader."Posting No. Series",
        ResJnlLine."Entry Type"::Sale,-ServiceLine."Qty. to Invoice",
        -ServiceLine.Amount / ServiceLine.Quantity,-ServiceLine.Amount);
    END;

    [External]
    PROCEDURE PostResJnlLineConsume@22(VAR ServiceLine@1002 : Record "Service Line";VAR ServShptHeader@1005 : Record "Service Shipment Header");
    VAR
      ResJnlLine@1004 : Record "Res. Journal Line";
    BEGIN
      if ServiceLine."Time Sheet No." <> '' then
        TimeSheetMgt.CheckServiceLine(ServiceLine);

      PostResJnlLine(
        ServiceHeader,ServiceLine,
        ServShptHeader."No.",'',SrcCode,ServShptHeader."No. Series",
        ResJnlLine."Entry Type"::Usage,-ServiceLine."Qty. to Consume",0,0);

      TimeSheetMgt.CreateTSLineFromServiceLine(ServiceLine,GenJnlLineDocNo,false);
    END;

    LOCAL PROCEDURE PostResJnlLine@4(ServiceHeader@1001 : Record "Service Header";ServiceLine@1010 : Record "Service Line";DocNo@1002 : Code[20];ExtDocNo@1003 : Code[35];SrcCode@1004 : Code[10];PostingNoSeries@1005 : Code[20];EntryType@1006 : Option;Qty@1007 : Decimal;UnitPrice@1008 : Decimal;TotalPrice@1009 : Decimal);
    VAR
      ResJnlLine@1000 : Record "Res. Journal Line";
    BEGIN
      with ResJnlLine do begin
        INIT;
        CopyDocumentFields(DocNo,ExtDocNo,SrcCode,PostingNoSeries);
        CopyFromServHeader(ServiceHeader);
        CopyFromServLine(ServiceLine);

        "Entry Type" := EntryType;
        Quantity := Qty;
        "Unit Cost" := ServiceLine."Unit Cost (LCY)";
        "Total Cost" := ServiceLine."Unit Cost (LCY)" * Quantity;
        "Unit Price" := UnitPrice;
        "Total Price" := TotalPrice;

        ResJnlPostLine.RunWithCheck(ResJnlLine);
      end;
    END;

    [External]
    PROCEDURE InitServiceRegister@19(VAR NextServLedgerEntryNo@1000 : Integer;VAR NextWarrantyLedgerEntryNo@1001 : Integer);
    BEGIN
      ServLedgEntryPostSale.InitServiceRegister(NextServLedgerEntryNo,NextWarrantyLedgerEntryNo);
    END;

    [External]
    PROCEDURE FinishServiceRegister@14(VAR nextServEntryNo@1000 : Integer;VAR nextWarrantyEntryNo@1001 : Integer);
    BEGIN
      ServLedgEntryPostSale.FinishServiceRegister(nextServEntryNo,nextWarrantyEntryNo);
    END;

    [Internal]
    PROCEDURE InsertServLedgerEntry@6(VAR NextEntryNo@1002 : Integer;VAR ServiceHeader@1006 : Record "Service Header";VAR ServiceLine@1000 : Record "Service Line";VAR ServItemLine@1003 : Record "Service Item Line";Qty@1001 : Decimal;DocNo@1005 : Code[20]) : Integer;
    BEGIN
      exit(
        ServLedgEntryPostSale.InsertServLedgerEntry(NextEntryNo,ServiceHeader,ServiceLine,ServItemLine,Qty,DocNo));
    END;

    [Internal]
    PROCEDURE InsertServLedgerEntrySale@16(VAR passedNextEntryNo@1005 : Integer;VAR ServHeader@1004 : Record "Service Header";VAR ServLine@1003 : Record "Service Line";VAR ServItemLine@1006 : Record "Service Item Line";Qty@1002 : Decimal;QtyToCharge@1001 : Decimal;GenJnlLineDocNo@1000 : Code[20];DocLineNo@1007 : Integer);
    BEGIN
      ServLedgEntryPostSale.InsertServLedgerEntrySale(
        passedNextEntryNo,ServHeader,ServLine,ServItemLine,Qty,QtyToCharge,GenJnlLineDocNo,DocLineNo);
    END;

    [Internal]
    PROCEDURE CreateCreditEntry@20(VAR passedNextEntryNo@1003 : Integer;VAR ServHeader@1002 : Record "Service Header";VAR ServLine@1001 : Record "Service Line";GenJnlLineDocNo@1000 : Code[20]);
    BEGIN
      ServLedgEntryPostSale.CreateCreditEntry(passedNextEntryNo,ServHeader,ServLine,GenJnlLineDocNo);
    END;

    [Internal]
    PROCEDURE InsertWarrantyLedgerEntry@1(VAR NextWarrantyEntryNo@1001 : Integer;VAR ServiceHeader@1002 : Record "Service Header";VAR ServiceLine@1000 : Record "Service Line";VAR ServItemLine@1003 : Record "Service Item Line";Qty@1006 : Decimal;GenJnlLineDocNo@1005 : Code[20]) : Integer;
    BEGIN
      exit(
        ServLedgEntryPostSale.InsertWarrantyLedgerEntry(
          NextWarrantyEntryNo,ServiceHeader,ServiceLine,ServItemLine,Qty,GenJnlLineDocNo));
    END;

    [External]
    PROCEDURE CalcSLEDivideAmount@18(Qty@1000 : Decimal;VAR passedServHeader@1001 : Record "Service Header";VAR passedTempServLine@1002 : Record "Service Line";VAR passedVATAmountLine@1003 : Record "VAT Amount Line");
    BEGIN
      ServLedgEntryPostSale.CalcDivideAmount(Qty,passedServHeader,passedTempServLine,passedVATAmountLine);
    END;

    [External]
    PROCEDURE TestSrvCostDirectPost@13(ServLineNo@1000 : Code[20]);
    VAR
      ServCost@1001 : Record "Service Cost";
      GLAcc@1002 : Record "G/L Account";
    BEGIN
      ServCost.GET(ServLineNo);
      GLAcc.GET(ServCost."Account No.");
      GLAcc.TESTFIELD("Direct Posting",true);
    END;

    [External]
    PROCEDURE TestGLAccDirectPost@15(ServLineNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record "G/L Account";
    BEGIN
      GLAcc.GET(ServLineNo);
      GLAcc.TESTFIELD("Direct Posting",true);
    END;

    [External]
    PROCEDURE CollectValueEntryRelation@5(VAR PassedValueEntryRelation@1000 : Record "Value Entry Relation";RowId@1001 : Text[100]);
    BEGIN
      TempValueEntryRelation.RESET;
      PassedValueEntryRelation.RESET;

      if TempValueEntryRelation.FINDSET then
        repeat
          PassedValueEntryRelation := TempValueEntryRelation;
          PassedValueEntryRelation."Source RowId" := RowId;
          PassedValueEntryRelation.INSERT;
        until TempValueEntryRelation.NEXT = 0;

      TempValueEntryRelation.DELETEALL;
    END;

    [Internal]
    PROCEDURE PostJobJnlLine@38(VAR ServHeader@1004 : Record "Service Header";ServLine@1012 : Record "Service Line";QtyToBeConsumed@1018 : Decimal) : Boolean;
    VAR
      JobJnlLine@1000 : Record "Job Journal Line";
      SourceCodeSetup@1006 : Record "Source Code Setup";
      ServiceCost@1011 : Record "Service Cost";
      Job@1016 : Record Job;
      JT@1015 : Record "Job Task";
      Item@1017 : Record Item;
      JobJnlPostLine@1008 : Codeunit "Job Jnl.-Post Line";
      CurrencyFactor@1001 : Decimal;
      UnitPriceLCY@1003 : Decimal;
    BEGIN
      with ServLine do begin
        if ("Job No." = '') or (QtyToBeConsumed = 0) then
          exit(false);

        TESTFIELD("Job Task No.");
        Job.LOCKTABLE;
        JT.LOCKTABLE;
        Job.GET("Job No.");
        JT.GET("Job No.","Job Task No.");

        JobJnlLine.INIT;

        JobJnlLine.DontCheckStdCost;

        JobJnlLine.VALIDATE("Job No.","Job No.");
        JobJnlLine.VALIDATE("Job Task No.","Job Task No.");
        JobJnlLine.VALIDATE("Line Type","Job Line Type");
        JobJnlLine.VALIDATE("Posting Date","Posting Date");
        JobJnlLine."Job Posting Only" := true;
        JobJnlLine."No." := "No.";

        case Type of
          Type::"G/L Account":
            JobJnlLine.Type := JobJnlLine.Type::"G/L Account";
          Type::Item:
            JobJnlLine.Type := JobJnlLine.Type::Item;
          Type::Resource:
            JobJnlLine.Type := JobJnlLine.Type::Resource;
          Type::Cost:
            begin
              ServiceCost.SETRANGE(Code,"No.");
              ServiceCost.FINDFIRST;
              JobJnlLine.Type := JobJnlLine.Type::"G/L Account";
              JobJnlLine."No." := ServiceCost."Account No.";
            end;
        end; // Case Type
        JobJnlLine.VALIDATE("No.");
        JobJnlLine.Description := Description;
        JobJnlLine."Description 2" := "Description 2";

        JobJnlLine."Variant Code" := "Variant Code";

        JobJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        JobJnlLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        JobJnlLine.VALIDATE(Quantity,-QtyToBeConsumed);

        JobJnlLine."Document No." := ServHeader."Shipping No.";
        JobJnlLine."Service Order No." := "Document No.";
        JobJnlLine."External Document No." := ServHeader."Shipping No.";
        JobJnlLine."Posted Service Shipment No." := ServHeader."Shipping No.";

        if Type = Type::Item then begin
          Item.GET("No.");
          if Item."Costing Method" = Item."Costing Method"::Standard then
            JobJnlLine.VALIDATE("Unit Cost (LCY)",Item."Standard Cost")
          else
            JobJnlLine.VALIDATE("Unit Cost (LCY)","Unit Cost (LCY)")
        end else
          JobJnlLine.VALIDATE("Unit Cost (LCY)","Unit Cost (LCY)");

        if "Currency Code" = Job."Currency Code" then
          JobJnlLine.VALIDATE("Unit Price","Unit Price");
        if "Currency Code" <> '' then begin
          Currency.GET("Currency Code");
          Currency.TESTFIELD("Amount Rounding Precision");
          CurrencyFactor := CurrExchRate.ExchangeRate("Posting Date","Currency Code");
          UnitPriceLCY :=
            ROUND(CurrExchRate.ExchangeAmtFCYToLCY("Posting Date","Currency Code","Unit Price",CurrencyFactor),
              Currency."Amount Rounding Precision");
          JobJnlLine.VALIDATE("Unit Price (LCY)",UnitPriceLCY);
        end else
          JobJnlLine.VALIDATE("Unit Price (LCY)","Unit Price");

        JobJnlLine.VALIDATE("Line Discount %","Line Discount %");

        JobJnlLine."Job Planning Line No." := "Job Planning Line No.";
        JobJnlLine."Remaining Qty." := "Job Remaining Qty.";
        JobJnlLine."Remaining Qty. (Base)" := "Job Remaining Qty. (Base)";

        JobJnlLine."Location Code" := "Location Code";
        JobJnlLine."Entry Type" := JobJnlLine."Entry Type"::Usage;

        JobJnlLine."Posting Group" := "Posting Group";
        JobJnlLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        JobJnlLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        JobJnlLine."Customer Price Group" := "Customer Price Group";

        SourceCodeSetup.GET;
        JobJnlLine."Source Code" := SourceCodeSetup."Service Management";

        JobJnlLine."Work Type Code" := "Work Type Code";

        JobJnlLine."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
        JobJnlLine."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
        JobJnlLine."Dimension Set ID" := "Dimension Set ID";
      end;

      JobJnlPostLine.RunWithCheck(JobJnlLine);
      exit(true);
    END;

    [External]
    PROCEDURE SetPostingDate@39(PostingDate@1000 : Date);
    BEGIN
      ServiceLinePostingDate := PostingDate;
    END;

    BEGIN
    END.
  }
}

