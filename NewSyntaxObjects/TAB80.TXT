OBJECT Table 80 Gen. Journal Template
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Page ID");
             END;

    OnDelete=BEGIN
               GenJnlAlloc.SETRANGE("Journal Template Name",Name);
               GenJnlAlloc.DELETEALL;
               GenJnlLine.SETRANGE("Journal Template Name",Name);
               GenJnlLine.DELETEALL(true);
               GenJnlBatch.SETRANGE("Journal Template Name",Name);
               GenJnlBatch.DELETEALL;
             END;

    CaptionML=ENU=Gen. Journal Template;
    LookupPageID="General Journal Template List";
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=ENU=Name;
                                                   NotBlank=true }
    { 2   ;   ;Description         ;Text80        ;CaptionML=ENU=Description }
    { 5   ;   ;Test Report ID      ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Report));
                                                   CaptionML=ENU=Test Report ID }
    { 6   ;   ;Page ID             ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Page));
                                                   OnValidate=BEGIN
                                                                if "Page ID" = 0 then
                                                                  VALIDATE(Type);
                                                              END;

                                                   CaptionML=ENU=Page ID }
    { 7   ;   ;Posting Report ID   ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Report));
                                                   CaptionML=ENU=Posting Report ID }
    { 8   ;   ;Force Posting Report;Boolean       ;CaptionML=ENU=Force Posting Report }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "Test Report ID" := REPORT::"General Journal - Test";
                                                                "Posting Report ID" := REPORT::"G/L Register";
                                                                SourceCodeSetup.GET;
                                                                case Type of
                                                                  Type::General:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."General Journal";
                                                                      "Page ID" := PAGE::"General Journal";
                                                                    end;
                                                                  Type::Sales:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Sales Journal";
                                                                      "Page ID" := PAGE::"Sales Journal";
                                                                    end;
                                                                  Type::Purchases:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Purchase Journal";
                                                                      "Page ID" := PAGE::"Purchase Journal";
                                                                    end;
                                                                  Type::"Cash Receipts":
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Cash Receipt Journal";
                                                                      "Page ID" := PAGE::"Cash Receipt Journal";
                                                                    end;
                                                                  Type::Payments:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Payment Journal";
                                                                      "Page ID" := PAGE::"Payment Journal";
                                                                    end;
                                                                  Type::Assets:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Fixed Asset G/L Journal";
                                                                      "Page ID" := PAGE::"Fixed Asset G/L Journal";
                                                                    end;
                                                                  Type::Intercompany:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."IC General Journal";
                                                                      "Page ID" := PAGE::"IC General Journal";
                                                                    end;
                                                                  Type::Jobs:
                                                                    begin
                                                                      "Source Code" := SourceCodeSetup."Job G/L Journal";
                                                                      "Page ID" := PAGE::"Job G/L Journal";
                                                                    end;
                                                                end;

                                                                if Recurring then
                                                                  "Page ID" := PAGE::"Recurring General Journal";
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs;
                                                   OptionString=General,Sales,Purchases,"Cash Receipts",Payments,Assets,Intercompany,Jobs }
    { 10  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   OnValidate=BEGIN
                                                                GenJnlLine.SETRANGE("Journal Template Name",Name);
                                                                GenJnlLine.MODIFYALL("Source Code","Source Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Source Code }
    { 11  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=ENU=Reason Code }
    { 12  ;   ;Recurring           ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Type);
                                                                if Recurring then
                                                                  TESTFIELD("No. Series",'');
                                                              END;

                                                   CaptionML=ENU=Recurring }
    { 15  ;   ;Test Report Caption ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Test Report ID")));
                                                   CaptionML=ENU=Test Report Caption;
                                                   Editable=false }
    { 16  ;   ;Page Caption        ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Page),
                                                                                                                "Object ID"=FIELD("Page ID")));
                                                   CaptionML=ENU=Page Caption;
                                                   Editable=false }
    { 17  ;   ;Posting Report Caption;Text250     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Posting Report ID")));
                                                   CaptionML=ENU=Posting Report Caption;
                                                   Editable=false }
    { 18  ;   ;Force Doc. Balance  ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Force Doc. Balance }
    { 19  ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                "Bal. Account No." := '';
                                                              END;

                                                   CaptionML=ENU=Bal. Account Type;
                                                   OptionCaptionML=ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset;
                                                   OptionString="G/L Account",Customer,Vendor,"Bank Account","Fixed Asset" }
    { 20  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF ("Bal. Account Type"=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF ("Bal. Account Type"=CONST(Customer)) Customer
                                                                 ELSE IF ("Bal. Account Type"=CONST(Vendor)) Vendor
                                                                 ELSE IF ("Bal. Account Type"=CONST("Bank Account")) "Bank Account"
                                                                 ELSE IF ("Bal. Account Type"=CONST("Fixed Asset")) "Fixed Asset";
                                                   OnValidate=BEGIN
                                                                if "Bal. Account Type" = "Bal. Account Type"::"G/L Account" then
                                                                  CheckGLAcc("Bal. Account No.");
                                                              END;

                                                   CaptionML=ENU=Bal. Account No. }
    { 21  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if "No. Series" <> '' then begin
                                                                  if Recurring then
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  if "No. Series" = "Posting No. Series" then
                                                                    "Posting No. Series" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No. Series }
    { 22  ;   ;Posting No. Series  ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if ("Posting No. Series" = "No. Series") and ("Posting No. Series" <> '') then
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                              END;

                                                   CaptionML=ENU=Posting No. Series }
    { 23  ;   ;Copy VAT Setup to Jnl. Lines;Boolean;
                                                   InitValue=true;
                                                   OnValidate=BEGIN
                                                                if "Copy VAT Setup to Jnl. Lines" <> xRec."Copy VAT Setup to Jnl. Lines" then begin
                                                                  GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                  GenJnlBatch.MODIFYALL("Copy VAT Setup to Jnl. Lines","Copy VAT Setup to Jnl. Lines");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Copy VAT Setup to Jnl. Lines }
    { 24  ;   ;Allow VAT Difference;Boolean       ;OnValidate=BEGIN
                                                                if "Allow VAT Difference" <> xRec."Allow VAT Difference" then begin
                                                                  GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                  GenJnlBatch.MODIFYALL("Allow VAT Difference","Allow VAT Difference");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Allow VAT Difference }
    { 25  ;   ;Cust. Receipt Report ID;Integer    ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Report));
                                                   AccessByPermission=TableData Customer=R;
                                                   CaptionML=ENU=Cust. Receipt Report ID }
    { 26  ;   ;Cust. Receipt Report Caption;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Cust. Receipt Report ID")));
                                                   AccessByPermission=TableData Customer=R;
                                                   CaptionML=ENU=Cust. Receipt Report Caption;
                                                   Editable=false }
    { 27  ;   ;Vendor Receipt Report ID;Integer   ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Report));
                                                   AccessByPermission=TableData Vendor=R;
                                                   CaptionML=ENU=Vendor Receipt Report ID }
    { 28  ;   ;Vendor Receipt Report Caption;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Report),
                                                                                                                "Object ID"=FIELD("Vendor Receipt Report ID")));
                                                   AccessByPermission=TableData Vendor=R;
                                                   CaptionML=ENU=Vendor Receipt Report Caption;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description,Type                    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'ENU=must not be %1';
      GenJnlBatch@1003 : Record "Gen. Journal Batch";
      GenJnlLine@1004 : Record "Gen. Journal Line";
      GenJnlAlloc@1005 : Record "Gen. Jnl. Allocation";
      SourceCodeSetup@1006 : Record "Source Code Setup";

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record "G/L Account";
    BEGIN
      if AccNo <> '' then begin
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",true);
      end;
    END;

    BEGIN
    END.
  }
}

