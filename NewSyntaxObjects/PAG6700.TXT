OBJECT Page 6700 Exchange Sync. Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Service Password"=rimd;
    CaptionML=ENU=Exchange Sync. Setup;
    InsertAllowed=false;
    LinksAllowed=false;
    SourceTable="Exchange Sync";
    PageType=Card;
    PromotedActionCategoriesML=ENU=New,Process,Report,Navigate;
    OnOpenPage=VAR
                 User@1000 : Record User;
                 AzureADMgt@1001 : Codeunit "Azure AD Mgt.";
               BEGIN
                 RESET;
                 GetUser(User);

                 if User."Authentication Email" = '' then
                   ERROR(EmailMissingErr);

                 ExchangeAccountUserName := User."Authentication Email";

                 if not GET(USERID) then begin
                   INIT;
                   "User ID" := USERID;
                   "Folder ID" := PRODUCTNAME.SHORT;
                   INSERT;
                   COMMIT;
                 end;

                 PasswordRequired := AzureADMgt.GetAccessToken(AzureADMgt.GetO365Resource,AzureADMgt.GetO365ResourceName,false) = '';

                 if (ExchangeAccountUserName <> '') and (not ISNULLGUID("Exchange Account Password Key")) then
                   ExchangeAccountPasswordTemp := '**********';
               END;

    ActionList=ACTIONS
    {
      { 6       ;    ;ActionContainer;
                      Name=ActionContainer6;
                      ActionContainerType=ActionItems }
      { 18      ;1   ;ActionGroup;
                      Name=Process;
                      CaptionML=ENU=Process;
                      Image=Action }
      { 20      ;2   ;Action    ;
                      Name=Validate Exchange Connection;
                      CaptionML=ENU=Validate Exchange Connection;
                      ToolTipML=ENU=Test that the provided exchange server connection works.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=ValidateEmailLoggingSetup;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ProgressWindow.OPEN(ProgressWindowMsg);

                                 if O365SyncManagement.CreateExchangeConnection(Rec) then
                                   MESSAGE(ConnectionSuccessMsg)
                                 else begin
                                   ProgressWindow.CLOSE;
                                   ERROR(ConnectionFailureErr);
                                 end;

                                 ProgressWindow.CLOSE;
                               END;
                                }
      { 29      ;1   ;ActionGroup;
                      Name=Navigate;
                      CaptionML=ENU=Navigate }
      { 30      ;2   ;Action    ;
                      Name=SetupBookingSync;
                      CaptionML=ENU=Bookings Sync. Setup;
                      ToolTipML=ENU=Open the Bookings Sync. Setup page.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=BookingsLogo;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 if PasswordRequired and ISNULLGUID("Exchange Account Password Key") then
                                   ERROR(PasswordMissingErr);

                                 PAGE.RUNMODAL(PAGE::"Booking Sync. Setup");
                               END;
                                }
      { 4       ;2   ;Action    ;
                      Name=SetupContactSync;
                      CaptionML=ENU=Contact Sync. Setup;
                      ToolTipML=ENU=Open the Contact Sync. Setup page.;
                      ApplicationArea=#Basic,#Suite;
                      Promoted=true;
                      Image=ExportSalesPerson;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 if PasswordRequired and ISNULLGUID("Exchange Account Password Key") then
                                   ERROR(PasswordMissingErr);

                                 PAGE.RUNMODAL(PAGE::"Contact Sync. Setup",Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                Name=User ID;
                Lookup=false;
                ToolTipML=ENU=Specifies the ID of the user who posted the entry, to be used, for example, in the change log.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User ID";
                Editable=false;
                ImplicitType=Code50 }

    { 5   ;2   ;Field     ;
                Name=ExchangeAccountUserName;
                ExtendedDatatype=E-Mail;
                CaptionML=ENU=Authentication Email;
                ToolTipML=ENU=Specifies the email address that you use to authenticate yourself on the Exchange server.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ExchangeAccountUserName;
                Enabled=false;
                Editable=false;
                ImplicitType=Text250 }

    { 19  ;2   ;Field     ;
                Name=ExchangeAccountPasswordTemp;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Exchange Account Password;
                ToolTipML=ENU=Specifies the password of the user account that has access to Exchange.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ExchangeAccountPasswordTemp;
                Visible=PasswordRequired;
                OnValidate=BEGIN
                             SetExchangeAccountPassword(ExchangeAccountPasswordTemp);
                             COMMIT;
                           END;

                ImplicitType=Text }

  }
  CODE
  {
    VAR
      O365SyncManagement@1002 : Codeunit "O365 Sync. Management";
      ProgressWindow@1008 : Dialog;
      ExchangeAccountPasswordTemp@1000 : Text;
      ProgressWindowMsg@1004 : TextConst 'ENU=Validating the connection to Exchange.';
      ConnectionSuccessMsg@1006 : TextConst 'ENU=Connected successfully to Exchange.';
      ExchangeAccountUserName@1001 : Text[250];
      ConnectionFailureErr@1005 : TextConst 'ENU=Cannot connect to Exchange. Check your user name, password and Folder ID, and then try again.';
      EmailMissingErr@1007 : TextConst 'ENU=You must specify an authentication email address for this user.';
      PasswordMissingErr@1009 : TextConst 'ENU=You must specify your Exchange credentials for this user first.';
      PasswordRequired@1010 : Boolean;

    LOCAL PROCEDURE GetUser@3(VAR User@1000 : Record User) : Boolean;
    BEGIN
      with User do begin
        SETRANGE("User Name",USERID);
        if FINDFIRST then
          exit(true);
      end;
    END;

    BEGIN
    END.
  }
}

