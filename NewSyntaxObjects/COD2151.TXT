OBJECT Codeunit 2151 O365 Sales Email Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [Internal]
    PROCEDURE ShowEmailDialog@2(DocumentNo@1000 : Code[20]) : Boolean;
    VAR
      SalesInvoiceHeader@1001 : Record "Sales Invoice Header";
      TempEmailItem@1004 : TEMPORARY Record "Email Item";
      ReportSelections@1010 : Record "Report Selections";
      TempReportSelections@1005 : TEMPORARY Record "Report Selections";
      EmailParameter@1003 : Record "Email Parameter";
      DocumentMailing@1008 : Codeunit "Document-Mailing";
      O365HTMLTemplMgt@1006 : Codeunit "O365 HTML Templ. Mgt.";
      O365SalesEmailDialog@1002 : Page "O365 Sales Email Dialog";
      DocumentRecordVariant@1013 : Variant;
      CustomerNo@1014 : Code[20];
      EmailAddress@1007 : Text[250];
      ReportUsage@1009 : Integer;
    BEGIN
      if not InitializeDocumentSpecificVariables(DocumentRecordVariant,ReportUsage,CustomerNo,DocumentNo) then
        exit;

      if not ReportSelections.GetEmailBody(
           TempEmailItem."Body File Path",ReportUsage,DocumentRecordVariant,CustomerNo,EmailAddress)
      then
        ;
      if ReportSelections.FindEmailAttachmentUsage(ReportUsage,CustomerNo,TempReportSelections) then begin
        // Create attachment
        TempReportSelections.GetPdfReport(
          TempEmailItem."Attachment File Path",ReportUsage,
          DocumentRecordVariant,CustomerNo);

        DocumentMailing.GetAttachmentFileName(
          TempEmailItem."Attachment Name",
          DocumentNo,
          SalesInvoiceHeader.GetDefaultEmailDocumentName,
          ReportUsage);
      end;

      TempEmailItem.Subject := DocumentMailing.GetEmailSubject(DocumentNo,SalesInvoiceHeader.GetDefaultEmailDocumentName,ReportUsage);
      TempEmailItem."Send to" := EmailAddress;
      TempEmailItem.AddCcBcc;
      TempEmailItem.AttachIncomingDocuments(DocumentNo);
      TempEmailItem.INSERT(true);
      COMMIT;

      O365SalesEmailDialog.SetValues(DocumentRecordVariant,TempEmailItem);
      if O365SalesEmailDialog.RUNMODAL = ACTION::OK then begin
        O365SalesEmailDialog.GETRECORD(TempEmailItem);
        if EmailAddress <> TempEmailItem."Send to" then
          O365HTMLTemplMgt.ReplaceBodyFileSendTo(
            TempEmailItem."Body File Path",EmailAddress,TempEmailItem."Send to");

        SetEmailOnDocument(DocumentNo,TempEmailItem."Send to");
        EmailParameter.SaveParameterValueWithReportUsage(
          DocumentNo,ReportUsage,EmailParameter."Parameter Type"::Subject,TempEmailItem.Subject);
        exit(true);
      end;
      O365SalesEmailDialog.GETRECORD(TempEmailItem);
      EmailParameter.SaveParameterValueWithReportUsage(
        DocumentNo,ReportUsage,EmailParameter."Parameter Type"::Subject,TempEmailItem.Subject);
    END;

    LOCAL PROCEDURE InitializeDocumentSpecificVariables@12(VAR DocumentRecordVariant@1001 : Variant;VAR ReportUsage@1005 : Integer;VAR CustomerNo@1000 : Code[20];DocumentNo@1002 : Code[20]) : Boolean;
    VAR
      SalesInvoiceHeader@1003 : Record "Sales Invoice Header";
      SalesHeader@1004 : Record "Sales Header";
      ReportSelections@1006 : Record "Report Selections";
    BEGIN
      if SalesInvoiceHeader.GET(DocumentNo) then begin
        SalesInvoiceHeader.SETRECFILTER;
        DocumentRecordVariant := SalesInvoiceHeader;
        CustomerNo := SalesInvoiceHeader."Bill-to Customer No.";
        ReportUsage := ReportSelections.Usage::"S.Invoice";
        exit(true);
      end;

      SalesHeader.SETFILTER("Document Type",'%1|%2',SalesHeader."Document Type"::Quote,SalesHeader."Document Type"::Invoice);
      SalesHeader.SETRANGE("No.",DocumentNo);
      if SalesHeader.FINDFIRST then begin
        SalesHeader.SETRECFILTER;
        DocumentRecordVariant := SalesHeader;
        CustomerNo := SalesHeader."Bill-to Customer No.";
        if SalesHeader."Document Type" = SalesHeader."Document Type"::Invoice then
          ReportUsage := ReportSelections.Usage::"S.Invoice Draft"
        else
          ReportUsage := ReportSelections.Usage::"S.Quote";
        exit(true);
      end;
      exit(false);
    END;

    LOCAL PROCEDURE SetEmailOnDocument@3(DocumentNo@1000 : Code[20];SendTo@1001 : Text[250]);
    VAR
      SalesHeader@1002 : Record "Sales Header";
      SalesInvoiceHeader@1003 : Record "Sales Invoice Header";
      Customer@1005 : Record Customer;
      SellToCustomerNo@1004 : Code[20];
    BEGIN
      if SalesInvoiceHeader.GET(DocumentNo) then
        SellToCustomerNo := SalesInvoiceHeader."Sell-to Customer No."
      else begin
        SalesHeader.SETFILTER("Document Type",'%1|%2',SalesHeader."Document Type"::Quote,SalesHeader."Document Type"::Invoice);
        SalesHeader.SETRANGE("No.",DocumentNo);
        if SalesHeader.FINDFIRST then
          SellToCustomerNo := SalesHeader."Sell-to Customer No."
        else
          exit;
      end;

      if Customer.GET(SellToCustomerNo) and (Customer."E-Mail" <> SendTo) then begin
        Customer.VALIDATE("E-Mail",COPYSTR(SendTo,1,MAXSTRLEN(Customer."E-Mail")));
        Customer.MODIFY(true);
      end;
    END;

    BEGIN
    END.
  }
}

