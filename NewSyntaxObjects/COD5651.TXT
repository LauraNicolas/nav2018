OBJECT Codeunit 5651 Insurance Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Insurance Journal Line";
    OnRun=BEGIN
            GLSetup.GET;
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5';
      Text001@1001 : TextConst 'ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5';
      GLSetup@1002 : Record "General Ledger Setup";
      FASetup@1003 : Record "FA Setup";
      DimMgt@1004 : Codeunit DimensionManagement;
      CallNo@1005 : Integer;

    [External]
    PROCEDURE RunCheck@1(VAR InsuranceJnlLine@1000 : Record "Insurance Journal Line");
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      with InsuranceJnlLine do begin
        if "Insurance No." = '' then
          exit;
        TESTFIELD("Insurance No.");
        TESTFIELD("Document No.");
        TESTFIELD("Posting Date");
        TESTFIELD("FA No.");
        if CallNo = 0 then begin
          FASetup.GET;
          FASetup.TESTFIELD("Insurance Depr. Book");
        end;
        CallNo := 1;

        if not DimMgt.CheckDimIDComb("Dimension Set ID") then
          ERROR(
            Text000,
            TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
            DimMgt.GetDimCombErr);

        TableID[1] := DATABASE::Insurance;
        No[1] := "Insurance No.";
        if not DimMgt.CheckDimValuePosting(TableID,No,"Dimension Set ID") then
          if "Line No." <> 0 then
            ERROR(
              Text001,
              TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
              DimMgt.GetDimValuePostingErr)
          else
            ERROR(DimMgt.GetDimValuePostingErr);
      end;
    END;

    BEGIN
    END.
  }
}

