OBJECT Codeunit 7111 Insert Analysis Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE InsertItems@12(VAR AnalysisLine@1000 : Record "Analysis Line");
    VAR
      Item@1001 : Record Item;
      ItemList@1002 : Page "Item List";
      ItemCount@1003 : Integer;
      AnalysisLineNo@1004 : Integer;
    BEGIN
      ItemList.LOOKUPMODE(true);
      if ItemList.RUNMODAL = ACTION::LookupOK then begin
        ItemList.SetSelection(Item);
        ItemCount := Item.COUNT;
        if ItemCount > 0 then begin
          MoveAnalysisLines(AnalysisLine,AnalysisLineNo,ItemCount);

          if Item.FIND('-') then
            repeat
              InsertAnalysisLine(
                AnalysisLine,AnalysisLineNo,
                Item.Description,Item."No.",AnalysisLine.Type::Item,false,0);
            until Item.NEXT = 0;
        end;
      end;
    END;

    [External]
    PROCEDURE InsertCust@42(VAR AnalysisLine@1000 : Record "Analysis Line");
    VAR
      Cust@1001 : Record Customer;
      CustList@1002 : Page "Customer List";
      CustCount@1003 : Integer;
      AnalysisLineNo@1004 : Integer;
    BEGIN
      CustList.LOOKUPMODE(true);
      if CustList.RUNMODAL = ACTION::LookupOK then begin
        CustList.SetSelection(Cust);
        CustCount := Cust.COUNT;
        if CustCount > 0 then begin
          MoveAnalysisLines(AnalysisLine,AnalysisLineNo,CustCount);

          if Cust.FIND('-') then
            repeat
              InsertAnalysisLine(
                AnalysisLine,AnalysisLineNo,
                Cust.Name,Cust."No.",AnalysisLine.Type::Customer,false,0);
            until Cust.NEXT = 0;
        end;
      end;
    END;

    [External]
    PROCEDURE InsertVend@57(VAR AnalysisLine@1000 : Record "Analysis Line");
    VAR
      Vend@1001 : Record Vendor;
      VendList@1002 : Page "Vendor List";
      VendCount@1003 : Integer;
      AnalysisLineNo@1004 : Integer;
    BEGIN
      VendList.LOOKUPMODE(true);
      if VendList.RUNMODAL = ACTION::LookupOK then begin
        VendList.SetSelection(Vend);
        VendCount := Vend.COUNT;
        if VendCount > 0 then begin
          MoveAnalysisLines(AnalysisLine,AnalysisLineNo,VendCount);

          if Vend.FIND('-') then
            repeat
              InsertAnalysisLine(
                AnalysisLine,AnalysisLineNo,
                Vend.Name,Vend."No.",AnalysisLine.Type::Vendor,false,0);
            until Vend.NEXT = 0;
        end;
      end;
    END;

    [External]
    PROCEDURE InsertItemGrDim@56(VAR AnalysisLine@1000 : Record "Analysis Line");
    VAR
      InventorySetup@1005 : Record "Inventory Setup";
    BEGIN
      InventorySetup.GET;
      InventorySetup.TESTFIELD("Item Group Dimension Code");
      InsertGroup(
        AnalysisLine,
        InventorySetup."Item Group Dimension Code",
        AnalysisLine.Type::"Item Group");
    END;

    [External]
    PROCEDURE InsertCustGrDim@58(VAR AnalysisLine@1000 : Record "Analysis Line");
    VAR
      SalesSetup@1001 : Record "Sales & Receivables Setup";
    BEGIN
      SalesSetup.GET;
      SalesSetup.TESTFIELD("Customer Group Dimension Code");
      InsertGroup(
        AnalysisLine,
        SalesSetup."Customer Group Dimension Code",
        AnalysisLine.Type::"Customer Group");
    END;

    [External]
    PROCEDURE InsertSalespersonPurchaser@25(VAR AnalysisLine@1000 : Record "Analysis Line");
    VAR
      SalesSetup@1001 : Record "Sales & Receivables Setup";
    BEGIN
      SalesSetup.GET;
      SalesSetup.TESTFIELD("Salesperson Dimension Code");
      InsertGroup(
        AnalysisLine,
        SalesSetup."Salesperson Dimension Code",
        AnalysisLine.Type::"Sales/Purchase person");
    END;

    LOCAL PROCEDURE InsertGroup@3(VAR AnalysisLine@1000 : Record "Analysis Line";GroupDimCode@1006 : Code[20];TotalingType@1007 : Integer);
    VAR
      DimVal@1001 : Record "Dimension Value";
      DimValList@1002 : Page "Dimension Value List";
      DimValCount@1003 : Integer;
      AnalysisLineNo@1004 : Integer;
    BEGIN
      DimVal.FILTERGROUP := 2;
      DimVal.SETRANGE("Dimension Code",GroupDimCode);
      DimVal.FILTERGROUP := 0;
      DimValList.SETTABLEVIEW(DimVal);
      DimValList.LOOKUPMODE(true);
      if DimValList.RUNMODAL = ACTION::LookupOK then begin
        DimValList.SetSelection(DimVal);
        DimValCount := DimVal.COUNT;
        if DimValCount > 0 then begin
          MoveAnalysisLines(AnalysisLine,AnalysisLineNo,DimValCount);

          if DimVal.FIND('-') then
            repeat
              InsertAnalysisLine(
                AnalysisLine,AnalysisLineNo,
                DimVal.Name,DimVal.Code,TotalingType,
                DimVal."Dimension Value Type" <> DimVal."Dimension Value Type"::Standard,
                DimVal.Indentation);
            until DimVal.NEXT = 0;
        end;
      end;
    END;

    LOCAL PROCEDURE MoveAnalysisLines@1(VAR AnalysisLine@1000 : Record "Analysis Line";VAR AnalysisLineNo@1002 : Integer;NewLineCount@1003 : Integer);
    VAR
      i@1001 : Integer;
    BEGIN
      with AnalysisLine do begin
        AnalysisLineNo := "Line No.";
        SETRANGE("Analysis Area","Analysis Area");
        SETRANGE("Analysis Line Template Name","Analysis Line Template Name");
        if FIND('+') then
          repeat
            i := "Line No.";
            if i >= AnalysisLineNo then begin
              DELETE;
              "Line No." := i + 10000 * NewLineCount;
              INSERT(true);
            end;
          until (i <= AnalysisLineNo) or (NEXT(-1) = 0);

        if AnalysisLineNo = 0 then
          AnalysisLineNo := 10000;
      end;
    END;

    LOCAL PROCEDURE InsertAnalysisLine@2(VAR AnalysisLine@1001 : Record "Analysis Line";VAR AnalysisLineNo@1000 : Integer;Text@1002 : Text[50];No@1003 : Code[20];Type2@1004 : Integer;Bold2@1006 : Boolean;Indent@1008 : Integer);
    BEGIN
      with AnalysisLine do begin
        INIT;
        "Line No." := AnalysisLineNo;
        AnalysisLineNo := AnalysisLineNo + 10000;
        Description := Text;
        Range := No;
        "Row Ref. No." := COPYSTR(No,1,MAXSTRLEN("Row Ref. No."));
        Type := Type2;
        Bold := Bold2;
        Indentation := Indent;
        INSERT(true);
      end;
    END;

    BEGIN
    END.
  }
}

