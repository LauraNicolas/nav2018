OBJECT Table 279 Extended Text Header
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields="No.","Language Code","Text No.";
    OnInsert=BEGIN
               SetUniqueTextNo;

               if "Table Name" = "Table Name"::Resource then begin
                 "Purchase Quote" := false;
                 "Purchase Invoice" := false;
                 "Purchase Blanket Order" := false;
                 "Purchase Order" := false;
                 "Purchase Credit Memo" := false;
               end;
             END;

    OnDelete=VAR
               ExtTextLine@1000 : Record "Extended Text Line";
             BEGIN
               ExtTextLine.SETRANGE("Table Name","Table Name");
               ExtTextLine.SETRANGE("No.","No.");
               ExtTextLine.SETRANGE("Language Code","Language Code");
               ExtTextLine.SETRANGE("Text No.","Text No.");
               ExtTextLine.DELETEALL(true);
             END;

    OnRename=BEGIN
               if ("Table Name" <> xRec."Table Name") or ("No." <> xRec."No.") then
                 ERROR(STRSUBSTNO(RenameRecordErr,FIELDCAPTION("Table Name"),FIELDCAPTION("No.")));

               SetUniqueTextNo;

               RecreateTextLines;
             END;

    CaptionML=ENU=Extended Text Header;
    LookupPageID="Extended Text List";
  }
  FIELDS
  {
    { 1   ;   ;Table Name          ;Option        ;CaptionML=ENU=Table Name;
                                                   OptionCaptionML=ENU=Standard Text,G/L Account,Item,Resource;
                                                   OptionString="Standard Text","G/L Account",Item,Resource }
    { 2   ;   ;No.                 ;Code20        ;TableRelation=IF ("Table Name"=CONST("Standard Text")) "Standard Text"
                                                                 ELSE IF ("Table Name"=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF ("Table Name"=CONST(Item)) Item
                                                                 ELSE IF ("Table Name"=CONST(Resource)) Resource;
                                                   CaptionML=ENU=No. }
    { 3   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   OnValidate=BEGIN
                                                                if "Language Code" <> '' then
                                                                  "All Language Codes" := false;
                                                              END;

                                                   CaptionML=ENU=Language Code }
    { 4   ;   ;Text No.            ;Integer       ;CaptionML=ENU=Text No.;
                                                   Editable=false }
    { 5   ;   ;Starting Date       ;Date          ;CaptionML=ENU=Starting Date }
    { 6   ;   ;Ending Date         ;Date          ;CaptionML=ENU=Ending Date }
    { 7   ;   ;All Language Codes  ;Boolean       ;InitValue=true;
                                                   OnValidate=BEGIN
                                                                if "All Language Codes" then
                                                                  "Language Code" := ''
                                                              END;

                                                   CaptionML=ENU=All Language Codes }
    { 8   ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 11  ;   ;Sales Quote         ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Sales Header"=R;
                                                   CaptionML=ENU=Sales Quote }
    { 12  ;   ;Sales Invoice       ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Sales Header"=R;
                                                   CaptionML=ENU=Sales Invoice }
    { 13  ;   ;Sales Order         ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Sales Shipment Header"=R;
                                                   CaptionML=ENU=Sales Order }
    { 14  ;   ;Sales Credit Memo   ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Sales Header"=R;
                                                   CaptionML=ENU=Sales Credit Memo }
    { 15  ;   ;Purchase Quote      ;Boolean       ;InitValue=true;
                                                   OnValidate=BEGIN
                                                                if "Purchase Quote" then
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData "Purchase Header"=R;
                                                   CaptionML=ENU=Purchase Quote }
    { 16  ;   ;Purchase Invoice    ;Boolean       ;InitValue=true;
                                                   OnValidate=BEGIN
                                                                if "Purchase Invoice" then
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData "Purchase Header"=R;
                                                   CaptionML=ENU=Purchase Invoice }
    { 17  ;   ;Purchase Order      ;Boolean       ;InitValue=true;
                                                   OnValidate=BEGIN
                                                                if "Purchase Order" then
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData "Purch. Rcpt. Header"=R;
                                                   CaptionML=ENU=Purchase Order }
    { 18  ;   ;Purchase Credit Memo;Boolean       ;InitValue=true;
                                                   OnValidate=BEGIN
                                                                if "Purchase Credit Memo" then
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData "Purchase Header"=R;
                                                   CaptionML=ENU=Purchase Credit Memo }
    { 19  ;   ;Reminder            ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData Customer=R;
                                                   CaptionML=ENU=Reminder }
    { 20  ;   ;Finance Charge Memo ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData Customer=R;
                                                   CaptionML=ENU=Finance Charge Memo }
    { 21  ;   ;Sales Blanket Order ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Sales Shipment Header"=R;
                                                   CaptionML=ENU=Sales Blanket Order }
    { 22  ;   ;Purchase Blanket Order;Boolean     ;InitValue=true;
                                                   OnValidate=BEGIN
                                                                if "Purchase Blanket Order" then
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData "Purch. Rcpt. Header"=R;
                                                   CaptionML=ENU=Purchase Blanket Order }
    { 23  ;   ;Prepmt. Sales Invoice;Boolean      ;InitValue=true;
                                                   AccessByPermission=TableData "Sales Header"=R;
                                                   CaptionML=ENU=Prepmt. Sales Invoice }
    { 24  ;   ;Prepmt. Sales Credit Memo;Boolean  ;InitValue=true;
                                                   AccessByPermission=TableData "Sales Header"=R;
                                                   CaptionML=ENU=Prepmt. Sales Credit Memo }
    { 25  ;   ;Prepmt. Purchase Invoice;Boolean   ;InitValue=true;
                                                   AccessByPermission=TableData "Purchase Header"=R;
                                                   CaptionML=ENU=Prepmt. Purchase Invoice }
    { 26  ;   ;Prepmt. Purchase Credit Memo;Boolean;
                                                   InitValue=true;
                                                   AccessByPermission=TableData "Purchase Header"=R;
                                                   CaptionML=ENU=Prepmt. Purchase Credit Memo }
    { 5900;   ;Service Order       ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Service Header"=R;
                                                   CaptionML=ENU=Service Order }
    { 5901;   ;Service Quote       ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Service Header"=R;
                                                   CaptionML=ENU=Service Quote }
    { 5902;   ;Service Invoice     ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Service Header"=R;
                                                   CaptionML=ENU=Service Invoice }
    { 5903;   ;Service Credit Memo ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Service Header"=R;
                                                   CaptionML=ENU=Service Credit Memo }
    { 6600;   ;Sales Return Order  ;Boolean       ;InitValue=true;
                                                   AccessByPermission=TableData "Return Receipt Header"=R;
                                                   CaptionML=ENU=Sales Return Order }
    { 6605;   ;Purchase Return Order;Boolean      ;InitValue=true;
                                                   AccessByPermission=TableData "Return Shipment Header"=R;
                                                   CaptionML=ENU=Purchase Return Order }
  }
  KEYS
  {
    {    ;"Table Name","No.","Language Code","Text No.";
                                                   Clustered=true }
    {    ;"Table Name","No.","Language Code","All Language Codes","Starting Date","Ending Date" }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      UntitledMsg@1002 : TextConst 'ENU=untitled';
      Text001@1001 : TextConst 'ENU=You cannot purchase resources.';
      RenameRecordErr@1000 : TextConst '@@@=%1 is TableName Field %2 is No.Table Field;ENU=You cannot rename %1 or %2.';

    LOCAL PROCEDURE NoResourcePurch@1();
    BEGIN
      if "Table Name" = "Table Name"::Resource then
        ERROR(Text001);
    END;

    LOCAL PROCEDURE SetUniqueTextNo@5();
    VAR
      ExtTextHeader@1000 : Record "Extended Text Header";
    BEGIN
      ExtTextHeader.SETRANGE("Table Name","Table Name");
      ExtTextHeader.SETRANGE("No.","No.");
      ExtTextHeader.SETRANGE("Language Code","Language Code");

      if ExtTextHeader.FINDLAST then
        "Text No." := ExtTextHeader."Text No." + 1
      else
        "Text No." := 1;
    END;

    LOCAL PROCEDURE RecreateTextLines@2();
    VAR
      ExtTextLine@1000 : Record "Extended Text Line";
      TmpExtTextLine@1001 : Record "Extended Text Line";
    BEGIN
      ExtTextLine.SETRANGE("Table Name","Table Name");
      ExtTextLine.SETRANGE("No.","No.");
      ExtTextLine.SETRANGE("Language Code",xRec."Language Code");
      ExtTextLine.SETRANGE("Text No.",xRec."Text No.");

      if ExtTextLine.FIND('-') then
        repeat
          TmpExtTextLine := ExtTextLine;
          TmpExtTextLine."Text No." := "Text No.";
          TmpExtTextLine."Language Code" := "Language Code";
          TmpExtTextLine.INSERT;
        until ExtTextLine.NEXT = 0;

      ExtTextLine.DELETEALL;
    END;

    [External]
    PROCEDURE GetCaption@3() : Text[80];
    VAR
      GLAcc@1000 : Record "G/L Account";
      Item@1001 : Record Item;
      Res@1002 : Record Resource;
      StandardText@1003 : Record "Standard Text";
      Descr@1004 : Text[50];
    BEGIN
      if "Text No." <> 0 then begin
        case "Table Name" of
          "Table Name"::"Standard Text":
            begin
              if StandardText.Code <> "No." then
                StandardText.GET("No.");
              Descr := StandardText.Description;
            end;
          "Table Name"::"G/L Account":
            begin
              if GLAcc."No." <> "No." then
                GLAcc.GET("No.");
              Descr := GLAcc.Name;
            end;
          "Table Name"::Item:
            begin
              if Item."No." <> "No." then
                Item.GET("No.");
              Descr := Item.Description;
            end;
          "Table Name"::Resource:
            begin
              if Res."No." <> "No." then
                Res.GET("No.");
              Descr := Res.Name;
            end;
        end;
        exit(STRSUBSTNO('%1 %2 %3 %4',"No.",Descr,"Language Code",FORMAT("Text No.")));
      end;
      exit(UntitledMsg);
    END;

    BEGIN
    END.
  }
}

