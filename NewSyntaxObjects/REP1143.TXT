OBJECT Report 1143 Import Cost Budget from Excel
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Import Cost Budget from Excel;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  if ToCostBudgetName = '' then
                    ERROR(STRSUBSTNO(Text000));

                  if CostType.FIND('-') then begin
                    repeat
                      TempCostType.INIT;
                      TempCostType := CostType;
                      TempCostType.INSERT;
                    until CostType.NEXT = 0;
                  end;

                  ExcelBuffer.LOCKTABLE;
                  "Cost Budget Buffer".LOCKTABLE;
                  CostBudgetEntry.SETRANGE("Budget Name",ToCostBudgetName);
                  if not CostBudgetName.GET(ToCostBudgetName) then
                    CLEAR(CostBudgetName);

                  CostAccSetup.GET;

                  ExcelBuffer.OpenBook(ServerFileName,SheetName);
                  ExcelBuffer.ReadSheet;

                  AnalyzeData;
                END;

    OnPostReport=BEGIN
                   ExcelBuffer.DELETEALL;
                   "Cost Budget Buffer".DELETEALL;
                 END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;Cost Budget Buffer  ;
               DataItemTable="Cost Budget Buffer";
               DataItemTableView=SORTING("Cost Type No.","Cost Center Code","Cost Object Code",Date);
               OnPreDataItem=BEGIN
                               RecNo := 0;

                               if not CostBudgetName.GET(ToCostBudgetName) then begin
                                 if not CONFIRM(Text001,false,CostBudgetName.TABLECAPTION,ToCostBudgetName) then
                                   CurrReport.BREAK;
                                 CostBudgetName.Name := ToCostBudgetName;
                                 CostBudgetName.INSERT;
                               end else begin
                                 if not CONFIRM(Text003,false,LOWERCASE(FORMAT(SELECTSTR(ImportOption + 1,Text027))),ToCostBudgetName) then
                                   CurrReport.BREAK;
                               end;

                               if CostBudgetEntry3.FINDLAST then
                                 EntryNo := CostBudgetEntry3."Entry No." + 1
                               else
                                 EntryNo := 1;
                               LastEntryNoBeforeImport := CostBudgetEntry3."Entry No.";
                             END;

               OnAfterGetRecord=BEGIN
                                  RecNo := RecNo + 1;
                                  if RecNo = 1 then begin
                                    Window.OPEN(
                                      Text028 +
                                      '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
                                    TotalRecNo := COUNT;
                                    Window.UPDATE(1,0);
                                  end;
                                  if ImportOption = ImportOption::"Replace entries" then begin
                                    CostBudgetEntry.SETRANGE("Cost Type No.","Cost Type No.");
                                    CostBudgetEntry.SETRANGE(Date,Date);
                                    CostBudgetEntry.SETFILTER("Entry No.",'<=%1',LastEntryNoBeforeImport);
                                    if not CostBudgetEntry.ISEMPTY then
                                      CostBudgetEntry.DELETEALL(true);
                                  end;

                                  if Amount = 0 then
                                    CurrReport.SKIP;
                                  if not IsLineTypeCostType("Cost Type No.") then
                                    CurrReport.SKIP;
                                  CostBudgetEntry.INIT;
                                  CostBudgetEntry."Entry No." := EntryNo;
                                  CostBudgetEntry."Budget Name" := ToCostBudgetName;
                                  CostBudgetEntry."Cost Type No." := "Cost Type No.";
                                  CostBudgetEntry.Date := Date;
                                  CostBudgetEntry.Amount := ROUND(Amount);
                                  CostBudgetEntry.Description := Description;
                                  CostBudgetEntry."Cost Center Code" := "Cost Center Code";
                                  CostBudgetEntry."Cost Object Code" := "Cost Object Code";
                                  CostBudgetEntry.INSERT(true);
                                  EntryNo := EntryNo + 1;
                                  Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));
                                END;

               OnPostDataItem=BEGIN
                                if RecNo > 0 then
                                  MESSAGE(Text004,CostBudgetEntry.TABLECAPTION,RecNo);
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnQueryClosePage=VAR
                         FileMgt@1001 : Codeunit "File Management";
                       BEGIN
                         if CloseAction = ACTION::OK then begin
                           ServerFileName := FileMgt.UploadFile(Text006,ExcelFileExtensionTok);
                           if ServerFileName = '' then
                             exit(false);

                           SheetName := ExcelBuffer.SelectSheetsName(ServerFileName);
                           if SheetName = '' then
                             exit(false);
                         end;
                       END;

    }
    CONTROLS
    {
      { 9   ;0   ;Container ;
                  Name=Container9;
                  ContainerType=ContentArea }

      { 8   ;1   ;Group     ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 3   ;2   ;Field     ;
                  Name=Budget Name;
                  CaptionML=ENU=Budget Name;
                  ToolTipML=ENU=Specifies the name of the budget.;
                  ApplicationArea=#CostAccounting;
                  SourceExpr=ToCostBudgetName;
                  TableRelation="Cost Budget Name" }

      { 2   ;2   ;Field     ;
                  Name=Option;
                  CaptionML=ENU=Option;
                  ToolTipML=ENU=Specifies whether the budget entries are added from Excel to budget entries that are currently in cost accounting, or whether the entries are replaced in cost accounting with the budget entries from Excel.;
                  OptionCaptionML=ENU=Replace entries,Add entries;
                  ApplicationArea=#Advanced;
                  SourceExpr=ImportOption }

      { 1   ;2   ;Field     ;
                  Name=Description;
                  CaptionML=ENU=Description;
                  ToolTipML=ENU=Specifies the description of what you are importing from Excel.;
                  ApplicationArea=#Advanced;
                  SourceExpr=Description }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ExcelBuffer@1021 : Record "Excel Buffer";
      CostBudgetEntry@1020 : Record "Cost Budget Entry";
      CostAccSetup@1019 : Record "Cost Accounting Setup";
      CostType@1018 : Record "Cost Type";
      TempCostType@1017 : TEMPORARY Record "Cost Type";
      CostBudgetName@1016 : Record "Cost Budget Name";
      CostBudgetEntry3@1015 : Record "Cost Budget Entry";
      Window@1007 : Dialog;
      ServerFileName@1025 : Text;
      SheetName@1013 : Text[250];
      ToCostBudgetName@1012 : Code[10];
      EntryNo@1011 : Integer;
      LastEntryNoBeforeImport@1010 : Integer;
      TotalRecNo@1009 : Integer;
      RecNo@1008 : Integer;
      Description@1006 : Text[50];
      ImportOption@1005 : '"Replace entries","Add entries"';
      Text000@1038 : TextConst 'ENU=You must specify a budget name to which to import.';
      Text001@1037 : TextConst 'ENU=Do you want to create %1 with the name %2?';
      Text003@1035 : TextConst 'ENU=Are you sure that you want to %1 for the budget name %2?';
      Text004@1034 : TextConst 'ENU=%1 table has been successfully updated with %2 entries.';
      Text006@1032 : TextConst 'ENU=Import Excel File';
      Text007@1031 : TextConst 'ENU=Analyzing Data...\\';
      Text009@1029 : TextConst 'ENU=Cost Type No';
      Text011@1027 : TextConst 'ENU=The text %1 can only be specified once in the Excel worksheet.';
      Text014@1026 : TextConst 'ENU=Date';
      Text017@1023 : TextConst 'ENU=Cost Center Code';
      Text018@1022 : TextConst 'ENU=Cost Object Code';
      Text019@1004 : TextConst 'ENU=You cannot import %1 value, which is not available in the %2 table.';
      Text023@1003 : TextConst 'ENU=You cannot import the same information more than once.';
      Text025@1002 : TextConst 'ENU=Cost Types have not been found in the Excel worksheet.';
      Text026@1001 : TextConst 'ENU=Dates have not been recognized in the Excel worksheet.';
      Text027@1000 : TextConst 'ENU=Replace entries,Add entries';
      Text028@1024 : TextConst 'ENU=Importing from Excel worksheet';
      ExcelFileExtensionTok@1014 : TextConst '@@@={Locked};ENU=.xlsx';

    LOCAL PROCEDURE AnalyzeData@2();
    VAR
      TempExcelBuffer@1000 : TEMPORARY Record "Excel Buffer";
      CostBudgetBuffer@1001 : Record "Cost Budget Buffer";
      CostBudgetBuffer2@1002 : Record "Cost Budget Buffer";
      CostCenter@1004 : Record "Cost Center";
      CostObject@1007 : Record "Cost Object";
      HeaderRowNo@1003 : Integer;
      TestDateTime@1005 : DateTime;
      OldRowNo@1006 : Integer;
    BEGIN
      Window.OPEN(
        Text007 +
        '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
      Window.UPDATE(1,0);
      TotalRecNo := ExcelBuffer.COUNT;
      RecNo := 0;
      CostBudgetBuffer2.INIT;
      CostBudgetBuffer.DELETEALL;

      if ExcelBuffer.FIND('-') then begin
        repeat
          RecNo := RecNo + 1;
          Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));
          case true of
            STRPOS(UPPERCASE(ExcelBuffer."Cell Value as Text"),UPPERCASE(Text009)) <> 0:
              begin
                if HeaderRowNo = 0 then begin
                  HeaderRowNo := ExcelBuffer."Row No.";
                  InsertTempExcelBuffer(ExcelBuffer,TempExcelBuffer,Text009);
                end else
                  ERROR(Text011,Text009);
              end;
            STRPOS(UPPERCASE(ExcelBuffer."Cell Value as Text"),UPPERCASE(Text017)) <> 0:
              begin
                if HeaderRowNo = ExcelBuffer."Row No." then
                  InsertTempExcelBuffer(ExcelBuffer,TempExcelBuffer,Text017)
                else
                  if (HeaderRowNo < ExcelBuffer."Row No.") and (HeaderRowNo <> 0) then
                    ERROR(Text011,Text017);
              end;
            STRPOS(UPPERCASE(ExcelBuffer."Cell Value as Text"),UPPERCASE(Text018)) <> 0:
              begin
                if HeaderRowNo = ExcelBuffer."Row No." then
                  InsertTempExcelBuffer(ExcelBuffer,TempExcelBuffer,Text018)
                else
                  if (HeaderRowNo < ExcelBuffer."Row No.") and (HeaderRowNo <> 0) then
                    ERROR(Text011,Text018);
              end;
            ExcelBuffer."Row No." = HeaderRowNo:
              begin
                TempExcelBuffer := ExcelBuffer;
                if EVALUATE(TestDateTime,TempExcelBuffer."Cell Value as Text") then begin
                  TempExcelBuffer."Cell Value as Text" := FORMAT(DT2DATE(TestDateTime));
                  TempExcelBuffer.Comment := Text014;
                  TempExcelBuffer.INSERT;
                end;
              end;
            (ExcelBuffer."Row No." > HeaderRowNo) and (HeaderRowNo > 0):
              begin
                if ExcelBuffer."Row No." <> OldRowNo then begin
                  OldRowNo := ExcelBuffer."Row No.";
                  CLEAR(CostBudgetBuffer2);
                end;
                TempExcelBuffer.SETRANGE("Column No.",ExcelBuffer."Column No.");
                if TempExcelBuffer.FIND('-') then
                  case TempExcelBuffer.Comment of
                    Text009:
                      begin
                        TempCostType.SETRANGE(
                          "No.",
                          COPYSTR(
                            ExcelBuffer."Cell Value as Text",
                            1,MAXSTRLEN(CostBudgetBuffer2."Cost Type No.")));
                        if TempCostType.FINDFIRST then
                          CostBudgetBuffer2."Cost Type No." :=
                            COPYSTR(
                              ExcelBuffer."Cell Value as Text",
                              1,MAXSTRLEN(CostBudgetBuffer2."Cost Type No."))
                        else
                          CostBudgetBuffer2."Cost Type No." := '';
                      end;
                    Text017:
                      begin
                        if CostCenter.GET(COPYSTR(
                               ExcelBuffer."Cell Value as Text",1,MAXSTRLEN(CostBudgetBuffer2."Cost Center Code")))
                        then
                          CostBudgetBuffer2."Cost Center Code" := CostCenter.Code
                        else
                          ERROR(Text019,CostBudgetBuffer2.FIELDCAPTION("Cost Center Code"),CostCenter.TABLECAPTION);
                      end;
                    Text018:
                      begin
                        if CostObject.GET(COPYSTR(
                               ExcelBuffer."Cell Value as Text",1,MAXSTRLEN(CostBudgetBuffer2."Cost Object Code")))
                        then
                          CostBudgetBuffer2."Cost Object Code" := CostObject.Code
                        else
                          ERROR(Text019,CostBudgetBuffer2.FIELDCAPTION("Cost Object Code"),CostObject.TABLECAPTION);
                      end;
                    Text014:
                      if CostBudgetBuffer2."Cost Type No." <> '' then begin
                        CostBudgetBuffer := CostBudgetBuffer2;
                        EVALUATE(CostBudgetBuffer.Date,TempExcelBuffer."Cell Value as Text");
                        EVALUATE(CostBudgetBuffer.Amount,ExcelBuffer."Cell Value as Text");
                        if not CostBudgetBuffer.FIND('=') then
                          CostBudgetBuffer.INSERT
                        else
                          ERROR(Text023);
                      end;
                  end;
              end;
          end;
        until ExcelBuffer.NEXT = 0;
      end;

      Window.CLOSE;
      TempExcelBuffer.RESET;
      TempExcelBuffer.SETRANGE(Comment,Text009);
      if not TempExcelBuffer.FINDFIRST then
        ERROR(Text025);
      TempExcelBuffer.SETRANGE(Comment,Text014);
      if not TempExcelBuffer.FINDFIRST then
        ERROR(Text026);
    END;

    PROCEDURE SetGLBudgetName@3(NewToCostBudgetName@1000 : Code[10]);
    BEGIN
      ToCostBudgetName := NewToCostBudgetName;
    END;

    LOCAL PROCEDURE IsLineTypeCostType@7(CostTypeNo@1001 : Code[20]) : Boolean;
    VAR
      CostType@1000 : Record "Cost Type";
    BEGIN
      if not CostType.GET(CostTypeNo) then
        exit(false);
      exit(CostType.Type = CostType.Type::"Cost Type");
    END;

    LOCAL PROCEDURE InsertTempExcelBuffer@9(VAR ExcelBuffer@1003 : Record "Excel Buffer";VAR TempExcelBuffer@1002 : TEMPORARY Record "Excel Buffer";Text@1001 : Text[250]);
    BEGIN
      TempExcelBuffer := ExcelBuffer;
      TempExcelBuffer.Comment := Text;
      TempExcelBuffer.INSERT;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

