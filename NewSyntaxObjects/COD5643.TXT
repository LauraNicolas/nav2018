OBJECT Codeunit 5643 FA Reclass. Transfer Batch
{
  OBJECT-PROPERTIES
  {
    Date=20150915D;
    Time=120000T;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo="FA Reclass. Journal Line";
    Permissions=TableData "FA Reclass. Journal Batch"=imd;
    OnRun=BEGIN
            FAReclassJnlLine.COPY(Rec);
            Code;
            Rec := FAReclassJnlLine;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot exceed %1 characters';
      Text001@1001 : TextConst 'ENU=Journal Batch Name    #1##########\\';
      Text002@1002 : TextConst 'ENU=Checking lines        #2######\';
      Text003@1003 : TextConst 'ENU=Posting lines         #3###### @4@@@@@@@@@@@@@';
      FAReclassJnlLine@1004 : Record "FA Reclass. Journal Line";
      FAReclassJnlTempl@1005 : Record "FA Reclass. Journal Template";
      FAReclassJnlBatch@1006 : Record "FA Reclass. Journal Batch";
      FAReclassJnlLine2@1007 : Record "FA Reclass. Journal Line";
      FAReclassJnlLine3@1008 : Record "FA Reclass. Journal Line";
      FAReclassCheckLine@1009 : Codeunit "FA Reclass. Check Line";
      FAReclassTransferLine@1010 : Codeunit "FA Reclass. Transfer Line";
      Window@1011 : Dialog;
      OneFAReclassDone@1012 : Boolean;
      LineCounter@1013 : Integer;
      StartLineNo@1014 : Integer;
      NoOfRecords@1015 : Integer;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with FAReclassJnlLine do begin
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");
        LOCKTABLE;

        FAReclassJnlTempl.GET("Journal Template Name");
        FAReclassJnlBatch.GET("Journal Template Name","Journal Batch Name");
        if STRLEN(INCSTR(FAReclassJnlBatch.Name)) > MAXSTRLEN(FAReclassJnlBatch.Name) then
          FAReclassJnlBatch.FIELDERROR(
            Name,
            STRSUBSTNO(
              Text000,
              MAXSTRLEN(FAReclassJnlBatch.Name)));

        if not FIND('=><') then begin
          "Line No." := 0;
          exit;
        end;

        Window.OPEN(
          Text001 +
          Text002 +
          Text003);
        Window.UPDATE(1,"Journal Batch Name");

        LineCounter := 0;
        StartLineNo := "Line No.";
        repeat
          LineCounter := LineCounter + 1;
          Window.UPDATE(2,LineCounter);
          FAReclassCheckLine.RUN(FAReclassJnlLine);
          if NEXT = 0 then
            FIND('-');
        until "Line No." = StartLineNo;
        NoOfRecords := LineCounter;

        LineCounter := 0;
        OneFAReclassDone := false;
        SETCURRENTKEY("Journal Template Name","Journal Batch Name","FA Posting Date");
        FIND('-');
        repeat
          LineCounter := LineCounter + 1;
          Window.UPDATE(3,LineCounter);
          Window.UPDATE(4,ROUND(LineCounter / NoOfRecords * 10000,1));
          FAReclassTransferLine.FAReclassLine(FAReclassJnlLine,OneFAReclassDone);
        until NEXT = 0;

        INIT;
        if OneFAReclassDone then
          "Line No." := 1
        else
          "Line No." := 0;

        if OneFAReclassDone then begin
          FAReclassJnlLine2.COPYFILTERS(FAReclassJnlLine);
          FAReclassJnlLine2.SETFILTER("FA No.",'<>%1','');
          if FAReclassJnlLine2.FINDLAST then; // Remember the last line
          DELETEALL;

          FAReclassJnlLine3.SETRANGE("Journal Template Name","Journal Template Name");
          FAReclassJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
          if not FAReclassJnlLine3.FINDLAST then
            if INCSTR("Journal Batch Name") <> '' then begin
              FAReclassJnlBatch.GET("Journal Template Name","Journal Batch Name");
              FAReclassJnlBatch.DELETE;
              FAReclassJnlBatch.Name := INCSTR("Journal Batch Name");
              if FAReclassJnlBatch.INSERT then;
              "Journal Batch Name" := FAReclassJnlBatch.Name;
            end;

          FAReclassJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
          if not FAReclassJnlLine3.FINDLAST then begin
            FAReclassJnlLine3.INIT;
            FAReclassJnlLine3."Journal Template Name" := "Journal Template Name";
            FAReclassJnlLine3."Journal Batch Name" := "Journal Batch Name";
            FAReclassJnlLine3."Line No." := 10000;
            FAReclassJnlBatch.GET("Journal Template Name","Journal Batch Name");
            FAReclassJnlLine3."Posting Date" := FAReclassJnlLine2."Posting Date";
            FAReclassJnlLine3.INSERT;
          end;
        end;

        COMMIT;
        CLEAR(FAReclassCheckLine);
        CLEAR(FAReclassTransferLine);
      end;
    END;

    BEGIN
    END.
  }
}

