OBJECT Codeunit 9990 Code Coverage Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    SingleInstance=true;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Timer@1000 : DotNet "'Microsoft.Dynamics.Nav.Timer, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Timer" WITHEVENTS;
      BackupErr@1001 : TextConst 'ENU=Code Coverage Backup encountered an error: %1.';
      FormatStringTxt@1002 : TextConst 'ENU=<Day,2>_<Month,2>_<Year>_<Hours24,2>_<Minutes,2>.';
      BackupFilePath@1007 : Text[1024];
      SummaryFilePath@1008 : Text[1024];
      BackupPathFormatTxt@1003 : TextConst 'ENU=CodeCoverageBackup_%1.txt';
      SummaryPathFormatTxt@1004 : TextConst 'ENU=CodeCoverageSummary_%1.xml';
      StartCoverageCleanupQst@1011 : TextConst '@@@="%1 = Total number of objects; %2 = Number of modified objects.";ENU=The Code Coverage Result Set contains lines that correspond to %1 objects, of which %2 are marked as modified. If you continue, code coverage lines that correspond to %3 objects will be deleted.\\This action cannot be undone.\\Are you sure that you want to continue?';
      AllAreModifiedMsg@1010 : TextConst 'ENU=All objects that correspond to the code coverage line result set are marked as modified.\There are no lines to remove.';
      NoneAreModifiedMsg@1009 : TextConst 'ENU=None of the objects that correspond to the lines in the code coverage result set are marked as modified in Object Designer. No lines will be removed. You must mark the objects that correspond to the lines that you want to keep in the code coverage result set.';
      ApplicationBaseline@1014 : Integer;
      IsRunning@1005 : Boolean;
      CodeCovNotRunningErr@1006 : TextConst 'ENU=Code coverage is not running.';
      CodeCovAlreadyRunningErr@1012 : TextConst 'ENU=Code coverage is already running.';
      StartedByApp@1013 : Boolean;
      CannotNestAppCovErr@1015 : TextConst 'ENU=Cannot nest multiple calls to StartApplicationCoverage.';
      MultiSession@1016 : Boolean;

    [External]
    PROCEDURE Start@7(MultiSessionValue@1000 : Boolean);
    BEGIN
      MultiSession := MultiSessionValue;
      if IsRunning then
        ERROR(CodeCovAlreadyRunningErr);
      CODECOVERAGELOG(true,MultiSession);
      IsRunning := true;
    END;

    [External]
    PROCEDURE Stop@8();
    BEGIN
      if not IsRunning then
        ERROR(CodeCovNotRunningErr);
      CODECOVERAGELOG(false,MultiSession);
      IsRunning := false;
    END;

    [External]
    PROCEDURE Refresh@1();
    BEGIN
      CODECOVERAGEREFRESH;
    END;

    [External]
    PROCEDURE Clear@9();
    VAR
      CodeCoverage@1000 : Record "Code Coverage";
    BEGIN
      CodeCoverage.DELETEALL;
    END;

    [External]
    PROCEDURE Import@5();
    BEGIN
      CODECOVERAGELOAD;
    END;

    [Internal]
    PROCEDURE Include@4(VAR Object@1000 : Record Object);
    BEGIN
      CODECOVERAGEINCLUDE(Object);
    END;

    [External]
    PROCEDURE Running@22() : Boolean;
    BEGIN
      exit(IsRunning);
    END;

    [External]
    PROCEDURE StartApplicationCoverage@25();
    BEGIN
      if IsRunning and StartedByApp then
        ERROR(CannotNestAppCovErr);

      if not IsRunning then begin
        StartedByApp := true;
        Start(false);
      end;

      // Establish baseline
      ApplicationBaseline := 0;
      ApplicationBaseline := ApplicationHits;
    END;

    [External]
    PROCEDURE StopApplicationCoverage@26();
    BEGIN
      if StartedByApp then
        Stop;
      StartedByApp := false;
    END;

    [External]
    PROCEDURE ApplicationHits@21() NoOFLines : Integer;
    VAR
      CodeCoverage@1000 : Record "Code Coverage";
    BEGIN
      Refresh;
      CodeCoverage.SETRANGE("Line Type",CodeCoverage."Line Type"::Code);
      CodeCoverage.SETFILTER("No. of Hits",'>%1',0);
      // excluding Code Coverage range 9900..9999 from calculation
      CodeCoverage.SETFILTER("Object ID",'..9989|10000..129999|150000..');
      if CodeCoverage.FINDSET then
        repeat
          NoOFLines += CodeCoverage."No. of Hits";
        until CodeCoverage.NEXT = 0;

      // Subtract baseline to produce delta
      NoOFLines -= ApplicationBaseline;
    END;

    [External]
    PROCEDURE CoveragePercent@13(NoCodeLines@1000 : Integer;NoCodeLinesHit@1001 : Integer) : Decimal;
    BEGIN
      if NoCodeLines > 0 then
        exit(NoCodeLinesHit / NoCodeLines);

      exit(1.0)
    END;

    [Internal]
    PROCEDURE ObjectCoverage@6(VAR CodeCoverage@1000 : Record "Code Coverage";VAR NoCodeLines@1002 : Integer;VAR NoCodeLinesHit@1003 : Integer) : Decimal;
    VAR
      CodeCoverage2@1001 : Record "Code Coverage";
    BEGIN
      NoCodeLines := 0;
      NoCodeLinesHit := 0;

      CodeCoverage2.SETPOSITION(CodeCoverage.GETPOSITION);
      CodeCoverage2.SETRANGE("Object Type",CodeCoverage."Object Type");
      CodeCoverage2.SETRANGE("Object ID",CodeCoverage."Object ID");

      repeat
        if CodeCoverage2."Line Type" = CodeCoverage2."Line Type"::Code then begin
          NoCodeLines += 1;
          if CodeCoverage2."No. of Hits" > 0 then
            NoCodeLinesHit += 1;
        end
      until (CodeCoverage2.NEXT = 0) or
              (CodeCoverage2."Line Type" = CodeCoverage2."Line Type"::Object);

      exit(CoveragePercent(NoCodeLines,NoCodeLinesHit))
    END;

    [Internal]
    PROCEDURE ObjectsCoverage@18(VAR CodeCoverage@1000 : Record "Code Coverage";VAR NoCodeLines@1002 : Integer;VAR NoCodeLinesHit@1003 : Integer) : Decimal;
    VAR
      CodeCoverage2@1001 : Record "Code Coverage";
    BEGIN
      NoCodeLines := 0;
      NoCodeLinesHit := 0;

      CodeCoverage2.COPYFILTERS(CodeCoverage);
      CodeCoverage2.SETFILTER("Line Type",'Code');
      repeat
        NoCodeLines += 1;
        if CodeCoverage2."No. of Hits" > 0 then
          NoCodeLinesHit += 1;
      until CodeCoverage2.NEXT = 0;

      exit(CoveragePercent(NoCodeLines,NoCodeLinesHit))
    END;

    [Internal]
    PROCEDURE FunctionCoverage@11(VAR CodeCoverage@1000 : Record "Code Coverage";VAR NoCodeLines@1002 : Integer;VAR NoCodeLinesHit@1003 : Integer) : Decimal;
    VAR
      CodeCoverage2@1001 : Record "Code Coverage";
    BEGIN
      NoCodeLines := 0;
      NoCodeLinesHit := 0;

      CodeCoverage2.SETPOSITION(CodeCoverage.GETPOSITION);
      CodeCoverage2.SETRANGE("Object Type",CodeCoverage."Object Type");
      CodeCoverage2.SETRANGE("Object ID",CodeCoverage."Object ID");

      repeat
        if CodeCoverage2."Line Type" = CodeCoverage2."Line Type"::Code then begin
          NoCodeLines += 1;
          if CodeCoverage2."No. of Hits" > 0 then
            NoCodeLinesHit += 1;
        end
      until (CodeCoverage2.NEXT = 0) or
              (CodeCoverage2."Line Type" = CodeCoverage2."Line Type"::Object) or
              (CodeCoverage2."Line Type" = CodeCoverage2."Line Type"::"Trigger/Function");

      exit(CoveragePercent(NoCodeLines,NoCodeLinesHit))
    END;

    [Internal]
    PROCEDURE CreateBackupFile@10(BackupPath@1000 : Text);
    VAR
      BackupStream@1002 : OutStream;
      BackupFile@1001 : File;
    BEGIN
      Refresh;

      BackupFile.CREATE(BackupPath);
      BackupFile.CREATEOUTSTREAM(BackupStream);
      XMLPORT.EXPORT(XMLPORT::"Code Coverage Detailed",BackupStream);
      BackupFile.CLOSE;
    END;

    [Internal]
    PROCEDURE CreateSummaryFile@2(SummaryPath@1002 : Text);
    VAR
      SummaryStream@1001 : OutStream;
      SummaryFile@1000 : File;
    BEGIN
      Refresh;

      SummaryFile.CREATE(SummaryPath);
      SummaryFile.CREATEOUTSTREAM(SummaryStream);
      XMLPORT.EXPORT(XMLPORT::"Code Coverage Summary",SummaryStream);
      SummaryFile.CLOSE;
    END;

    [Internal]
    PROCEDURE StartAutomaticBackup@12(TimeInterval@1002 : Integer;BackupPath@1001 : Text[1024];SummaryPath@1000 : Text[1024]);
    VAR
      Object@1003 : Record Object;
    BEGIN
      Include(Object); // Load all objects
      Start(false); // Start code coverage

      // Setup Timer and File Paths
      if ISNULL(Timer) then
        Timer := Timer.Timer;
      UpdateAutomaticBackupSettings(TimeInterval,BackupPath,SummaryPath);
    END;

    [External]
    PROCEDURE UpdateAutomaticBackupSettings@3(TimeInterval@1000 : Integer;BackupPath@1001 : Text[1024];SummaryPath@1002 : Text[1024]);
    BEGIN
      if not ISNULL(Timer) then begin
        Timer.Stop;
        Timer.Interval := TimeInterval * 60000;
        BackupFilePath := BackupPath;
        SummaryFilePath := SummaryPath;
        Timer.Start;
      end;
    END;

    [Internal]
    PROCEDURE CleanupCodeCoverage@17();
    VAR
      TempObject@1001 : TEMPORARY Record Object;
      ModifiedMarkCount@1002 : Integer;
    BEGIN
      DetermineModifiedObjects(TempObject,ModifiedMarkCount);
      if not TakeAction(TempObject.COUNT,ModifiedMarkCount) then
        exit;
      RemoveModifiedObjectLines(TempObject);
    END;

    LOCAL PROCEDURE DetermineModifiedObjects@16(VAR TempObject@1001 : TEMPORARY Record Object;VAR ModifiedMarkCount@1002 : Integer);
    VAR
      CodeCoverage@1003 : Record "Code Coverage";
      Window@1000 : Dialog;
      i@1004 : Integer;
      N@1005 : Integer;
    BEGIN
      TempObject.DELETEALL;
      ModifiedMarkCount := 0;
      if CodeCoverage.FINDSET then begin
        Window.OPEN('Analyzing @1@@@@@@@@@@@@@@@@@@');
        N := CodeCoverage.COUNT;
        with TempObject do
          repeat
            i += 1;
            if i mod 100 = 0 then
              Window.UPDATE(1,ROUND(i / N * 10000,1));
            Type := CodeCoverage."Object Type";
            "Company Name" := '';
            ID := CodeCoverage."Object ID";
            if INSERT then
              CheckModified(TempObject,ModifiedMarkCount);
          until CodeCoverage.NEXT = 0;

        Window.CLOSE;
      end;
    END;

    LOCAL PROCEDURE CheckModified@19(TempObject@1004 : TEMPORARY Record Object;VAR ModifiedMarkCount@1005 : Integer);
    BEGIN
      if ObjectModified(TempObject) then
        ModifiedMarkCount += 1;
    END;

    LOCAL PROCEDURE ObjectModified@20(TempObject@1007 : Record Object) : Boolean;
    VAR
      Object@1025 : Record Object;
    BEGIN
      if not Object.GET(TempObject.Type,COMPANYNAME,TempObject.ID) then
        Object.GET(TempObject.Type,'',TempObject.ID);
      exit(Object.Modified);
    END;

    LOCAL PROCEDURE RemoveModifiedObjectLines@15(VAR TempObject@1000 : TEMPORARY Record Object);
    VAR
      CodeCoverage@1001 : Record "Code Coverage";
      Window@1002 : Dialog;
      i@1003 : Integer;
      N@1004 : Integer;
    BEGIN
      TempObject.FINDSET;
      Window.OPEN('Deleting @1@@@@@@@@@@@@@@@@@@');
      N := TempObject.COUNT;
      repeat
        i += 1;
        if i mod 100 = 0 then
          Window.UPDATE(1,ROUND(i / N * 10000,1));
        if not ObjectModified(TempObject) then begin
          CodeCoverage.SETRANGE("Object Type",TempObject.Type);
          CodeCoverage.SETRANGE("Object ID",TempObject.ID);
          CodeCoverage.DELETEALL;
        end;
      until TempObject.NEXT = 0;
      Window.CLOSE;
    END;

    LOCAL PROCEDURE TakeAction@14(ObjCount@1009 : Integer;ModifiedMarkCount@1000 : Integer) : Boolean;
    BEGIN
      if ObjCount = 0 then
        exit(false);
      if ModifiedMarkCount = 0 then begin
        MESSAGE(NoneAreModifiedMsg);
        exit(false);
      end;
      if ObjCount = ModifiedMarkCount then begin
        MESSAGE(AllAreModifiedMsg);
        exit(false);
      end;
      exit(CONFIRM(StartCoverageCleanupQst,false,ObjCount,ModifiedMarkCount,ObjCount - ModifiedMarkCount));
    END;

    EVENT Timer@1000::Elapsed@12(sender@1001 : Variant;e@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.EventArgs");
    BEGIN
      CreateBackupFile(BackupFilePath + STRSUBSTNO(BackupPathFormatTxt,FORMAT(CURRENTDATETIME,0,FormatStringTxt)));
      CreateSummaryFile(SummaryFilePath + STRSUBSTNO(SummaryPathFormatTxt,FORMAT(CURRENTDATETIME,0,FormatStringTxt)));
    END;

    EVENT Timer@1000::ExceptionOccurred@13(sender@1001 : Variant;e@1000 : DotNet "'Microsoft.Dynamics.Nav.Timer, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.ExceptionOccurredEventArgs");
    BEGIN
      ERROR(BackupErr,e.Exception.Message);
    END;

    BEGIN
    END.
  }
}

