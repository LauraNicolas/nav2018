OBJECT Codeunit 9750 Web Service Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [Internal]
    PROCEDURE CreateWebService@1(ObjectType@1002 : Option;ObjectId@1003 : Integer;ObjectName@1004 : Text;Published@1005 : Boolean);
    VAR
      AllObj@1000 : Record AllObj;
      WebService@1006 : Record "Web Service";
      WebServiceName@1001 : Text;
    BEGIN
      AllObj.GET(ObjectType,ObjectId);
      WebServiceName := GetWebServiceName(ObjectName,AllObj."Object Name");

      // If the web service already exists, modify it accoridingly, otherwise add it
      if WebService.GET(ObjectType,WebServiceName) then begin
        ModifyWebService(WebService,AllObj,WebServiceName,Published);
        WebService.MODIFY;
      end else begin
        WebService.INIT;
        ModifyWebService(WebService,AllObj,WebServiceName,Published);
        WebService.INSERT;
      end
    END;

    LOCAL PROCEDURE ModifyWebService@10(VAR WebService@1003 : Record "Web Service";AllObj@1000 : Record AllObj;WebServiceName@1001 : Text;Published@1002 : Boolean);
    BEGIN
      WebService."Object Type" := AllObj."Object Type";
      WebService."Object ID" := AllObj."Object ID";
      WebService."Service Name" := COPYSTR(WebServiceName,1,MAXSTRLEN(WebService."Service Name"));
      WebService.Published := Published;
    END;

    [Internal]
    PROCEDURE CreateTenantWebService@3(ObjectType@1002 : Option;ObjectId@1003 : Integer;ObjectName@1004 : Text;Published@1005 : Boolean);
    VAR
      AllObj@1000 : Record AllObj;
      TenantWebService@1006 : Record "Tenant Web Service";
      WebServiceName@1001 : Text;
    BEGIN
      AllObj.GET(ObjectType,ObjectId);
      WebServiceName := GetWebServiceName(ObjectName,AllObj."Object Name");

      // If the web service already exists, modify it accoridingly, otherwise add it
      if TenantWebService.GET(ObjectType,WebServiceName) then begin
        ModifyTenantWebService(TenantWebService,AllObj,WebServiceName,Published);
        TenantWebService.MODIFY;
      end else begin
        TenantWebService.INIT;
        ModifyTenantWebService(TenantWebService,AllObj,WebServiceName,Published);
        TenantWebService.INSERT;
      end
    END;

    LOCAL PROCEDURE ModifyTenantWebService@2(VAR TenantWebService@1003 : Record "Tenant Web Service";AllObj@1000 : Record AllObj;WebServiceName@1001 : Text;Published@1002 : Boolean);
    BEGIN
      TenantWebService."Object Type" := AllObj."Object Type";
      TenantWebService."Object ID" := AllObj."Object ID";
      TenantWebService."Service Name" := COPYSTR(WebServiceName,1,MAXSTRLEN(TenantWebService."Service Name"));
      TenantWebService.Published := Published;
    END;

    LOCAL PROCEDURE GetWebServiceName@8(ServiceName@1000 : Text;ObjectName@1001 : Text) : Text;
    BEGIN
      // If a name is not specified, the object's name will be used
      if ServiceName <> '' then
        exit(ServiceName);

      exit(DELCHR(ObjectName,'=',' '));
    END;

    BEGIN
    END.
  }
}

