OBJECT Page 5985 Service Item Replacement
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Service Item Replacement;
    PageType=StandardDialog;
    OnInit=BEGIN
             CopyComponentsEnable := true;
           END;

    OnOpenPage=BEGIN
                 ServItem.GET(ServItemNo);
                 OldSerialNo := ServItem."Serial No.";
                 ServItemVariantCode := ServItem."Variant Code";
                 if not Item.GET(ServItem."Item No.") then
                   CLEAR(Item);

                 ServItem.CALCFIELDS("Service Item Components");
                 Item.CALCFIELDS("Assembly BOM");
                 CopyComponentsEnable := ServItem."Service Item Components" or Item."Assembly BOM"
               END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1 }

    { 30  ;2   ;Group     ;
                Name=Old Service Item;
                CaptionML=ENU=Old Service Item }

    { 17  ;3   ;Field     ;
                Name=ServItemNo;
                CaptionML=ENU=Service Item No.;
                ApplicationArea=#Service;
                SourceExpr=ServItemNo;
                Editable=FALSE }

    { 2   ;3   ;Field     ;
                Name=Item No.;
                CaptionML=ENU=Item No.;
                ApplicationArea=#Service;
                SourceExpr=Item."No.";
                Editable=FALSE }

    { 5   ;3   ;Field     ;
                Name=Item Description;
                CaptionML=ENU=Item Description;
                ApplicationArea=#Service;
                SourceExpr=Item.Description;
                Editable=FALSE }

    { 25  ;3   ;Field     ;
                Name=ServItemVariantCode;
                CaptionML=ENU=Variant Code;
                ApplicationArea=#Advanced;
                SourceExpr=ServItemVariantCode;
                Editable=FALSE }

    { 6   ;3   ;Field     ;
                Name=OldSerialNo;
                CaptionML=ENU=Serial No.;
                ApplicationArea=#ItemTracking;
                SourceExpr=OldSerialNo;
                Editable=FALSE }

    { 31  ;2   ;Group     ;
                Name=New Service Item;
                CaptionML=ENU=New Service Item }

    { 12  ;3   ;Field     ;
                Name=Item."No.";
                CaptionML=ENU=Item No.;
                ApplicationArea=#Service;
                SourceExpr=Item."No.";
                Editable=FALSE }

    { 13  ;3   ;Field     ;
                Name=Item.Description;
                CaptionML=ENU=Item Description;
                ApplicationArea=#Service;
                SourceExpr=Item.Description;
                Editable=FALSE }

    { 23  ;3   ;Field     ;
                Name=VariantCode;
                CaptionML=ENU=Variant Code;
                ApplicationArea=#Advanced;
                SourceExpr=VariantCode;
                OnValidate=BEGIN
                             if VariantCode <> '' then begin
                               ItemVariant.RESET;
                               ItemVariant.SETRANGE("Item No.",ItemNo);
                               ItemVariant.SETRANGE(Code,VariantCode);
                               if not ItemVariant.FINDFIRST then
                                 ERROR(
                                   Text000,
                                   ItemVariant.TABLECAPTION,ItemNo,VariantCode);
                             end;
                           END;

                OnLookup=BEGIN
                           ItemVariant.RESET;
                           ItemVariant.SETRANGE("Item No.",ItemNo);
                           if PAGE.RUNMODAL(PAGE::"Item Variants",ItemVariant) = ACTION::LookupOK then
                             VariantCode := ItemVariant.Code;
                         END;
                          }

    { 14  ;3   ;Field     ;
                Name=NewSerialNo;
                CaptionML=ENU=Serial No.;
                ApplicationArea=#ItemTracking;
                SourceExpr=NewSerialNo;
                OnAssistEdit=VAR
                               ItemLedgEntry@1000 : Record "Item Ledger Entry";
                             BEGIN
                               CLEAR(ItemLedgEntry);
                               ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
                               ItemLedgEntry.SETRANGE("Item No.",ItemNo);
                               ItemLedgEntry.SETRANGE(Open,true);
                               ItemLedgEntry.SETRANGE("Variant Code",VariantCode);
                               ItemLedgEntry.SETFILTER("Serial No.",'<>%1','');
                               if PAGE.RUNMODAL(0,ItemLedgEntry) = ACTION::LookupOK then
                                 NewSerialNo := ItemLedgEntry."Serial No.";
                             END;
                              }

    { 28  ;3   ;Field     ;
                Name=CopyComponents;
                CaptionML=ENU=Copy Components From;
                OptionCaptionML=ENU=None,Item BOM,Old Service Item,Old Service Item w/o Serial No.;
                ApplicationArea=#Service;
                SourceExpr=CopyComponentsFrom;
                Enabled=CopyComponentsEnable;
                OnValidate=BEGIN
                             case CopyComponentsFrom of
                               CopyComponentsFrom::"Item BOM":
                                 if not Item."Assembly BOM" then
                                   ERROR(
                                     Text002,
                                     Item.FIELDCAPTION("Assembly BOM"),
                                     Item.TABLECAPTION,
                                     Item.FIELDCAPTION("No."),
                                     Item."No.");
                               CopyComponentsFrom::"Old Service Item",
                               CopyComponentsFrom::"Old Service Item w/o Serial No.":
                                 if not ServItem."Service Item Components" then
                                   ERROR(
                                     Text002,
                                     ServItem.FIELDCAPTION("Service Item Components"),
                                     ServItem.TABLECAPTION,
                                     ServItem.FIELDCAPTION("No."),
                                     ServItem."No.")
                             end;
                           END;
                            }

    { 15  ;2   ;Field     ;
                Name=Replacement;
                CaptionML=ENU=Replacement;
                OptionCaptionML=ENU=Temporary,Permanent;
                ApplicationArea=#Service;
                SourceExpr=Replacement }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst '@@@=The Item Variant LS-10PC , LS-10PC-B does not exists.;ENU=The %1 %2,%3 does not exists.';
      Item@1001 : Record Item;
      ServItem@1002 : Record "Service Item";
      ItemVariant@1004 : Record "Item Variant";
      Replacement@1005 : 'Temporary,Permanent';
      OldSerialNo@1007 : Code[20];
      NewSerialNo@1008 : Code[20];
      ServItemNo@1009 : Code[20];
      ItemNo@1010 : Code[20];
      VariantCode@1011 : Code[10];
      ServItemVariantCode@1013 : Code[10];
      CopyComponentsFrom@1006 : 'None,"Item BOM","Old Service Item","Old Service Item w/o Serial No."';
      Text002@1003 : TextConst '@@@=There is no Assembly BOM in the Item No. 1002;ENU=There is no %1 in the %2 %3 %4.';
      CopyComponentsEnable@19054375 : Boolean INDATASET;

    [External]
    PROCEDURE SetValues@1(ServItemNo2@1002 : Code[20];ItemNo2@1003 : Code[20];VariantCode2@1004 : Code[10]);
    BEGIN
      ServItemNo := ServItemNo2;
      ItemNo := ItemNo2;
      VariantCode := VariantCode2;
    END;

    [External]
    PROCEDURE ReturnSerialNo@2() : Text[20];
    BEGIN
      exit(NewSerialNo);
    END;

    [External]
    PROCEDURE ReturnReplacement@4() : Integer;
    BEGIN
      exit(Replacement);
    END;

    [External]
    PROCEDURE ReturnVariantCode@3() : Text[10];
    BEGIN
      exit(VariantCode);
    END;

    [External]
    PROCEDURE ReturnCopyComponentsFrom@5() : Integer;
    BEGIN
      exit(CopyComponentsFrom);
    END;

    [External]
    PROCEDURE SetParameters@6(VariantCodeFrom@1000 : Code[10];NewSerialNoFrom@1001 : Code[20];NewCopyComponentsFrom@1002 : Option;ReplacementFrom@1003 : Option);
    BEGIN
      VariantCode := VariantCodeFrom;
      NewSerialNo := NewSerialNoFrom;
      CopyComponentsFrom := NewCopyComponentsFrom;
      Replacement := ReplacementFrom;
    END;

    BEGIN
    END.
  }
}

