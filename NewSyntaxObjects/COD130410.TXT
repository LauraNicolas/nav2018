OBJECT Codeunit 130410 Sys. Warmup Test Runner
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Subtype=TestRunner;
    TestIsolation=Codeunit;
    OnRun=BEGIN
            CODEUNIT.RUN(CODEUNIT::"Sys. Warmup Scenarios");
          END;

  }
  CODE
  {

    [EventSubscriber(Codeunit,1,OnAfterCompanyOpen,"",Skip,Skip)]
    LOCAL PROCEDURE WarmUpOnAfterCompanyOpen@1();
    VAR
      O365GettingStarted@1001 : Record "O365 Getting Started";
      CompanyInformationMgt@1002 : Codeunit "Company Information Mgt.";
      PermissionManager@1003 : Codeunit "Permission Manager";
    BEGIN
      if not GUIALLOWED then
        exit;

      if not CompanyInformationMgt.IsDemoCompany then
        exit;

      if not PermissionManager.SoftwareAsAService then
        exit;

      if not O365GettingStarted.ISEMPTY then
        exit;

      if not IsFirstLogon then
        exit;

      TASKSCHEDULER.CREATETASK(CODEUNIT::"Sys. Warmup Test Runner",0,true,COMPANYNAME,CURRENTDATETIME + 10000); // Add 10s
    END;

    LOCAL PROCEDURE IsFirstLogon@2() : Boolean;
    VAR
      SessionEvent@1000 : Record "Session Event";
      ActiveSession@1001 : Record "Active Session";
    BEGIN
      ActiveSession.GET(SERVICEINSTANCEID,SESSIONID);
      SessionEvent.SETRANGE("Database Name",ActiveSession."Database Name");
      SessionEvent.SETRANGE("Event Type",SessionEvent."Event Type"::Logon);
      SessionEvent.SETFILTER("Client Type",'%1|%2|%3|%4|%5',
        SessionEvent."Client Type"::Desktop,
        SessionEvent."Client Type"::Phone,
        SessionEvent."Client Type"::Tablet,
        SessionEvent."Client Type"::"Web Client",
        SessionEvent."Client Type"::"Windows Client");
      SessionEvent.SETFILTER("Session Unique ID",'<>%1',ActiveSession."Session Unique ID");
      exit(SessionEvent.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

