OBJECT Table 5813 Inventory Posting Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               CheckSetupUsage;
             END;

    CaptionML=ENU=Inventory Posting Setup;
  }
  FIELDS
  {
    { 1   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=ENU=Location Code }
    { 2   ;   ;Invt. Posting Group Code;Code20    ;TableRelation="Inventory Posting Group";
                                                   CaptionML=ENU=Invt. Posting Group Code;
                                                   NotBlank=true }
    { 6   ;   ;Inventory Account   ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                GLAccountCategoryMgt.CheckGLAccount(
                                                                  "Inventory Account",false,false,GLAccountCategory."Account Category"::Assets,GLAccountCategoryMgt.GetInventory);
                                                              END;

                                                   OnLookup=BEGIN
                                                              GLAccountCategoryMgt.LookupGLAccount(
                                                                "Inventory Account",GLAccountCategory."Account Category"::Assets,GLAccountCategoryMgt.GetInventory);
                                                            END;

                                                   CaptionML=ENU=Inventory Account }
    { 20  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 40  ;   ;Used in Ledger Entries;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count("Value Entry" WHERE ("Location Code"=FIELD("Location Code"),
                                                                                          "Inventory Posting Group"=FIELD("Invt. Posting Group Code")));
                                                   CaptionML=ENU=Used in Ledger Entries;
                                                   Editable=false }
    { 5800;   ;Inventory Account (Interim);Code20 ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                GLAccountCategoryMgt.CheckGLAccount(
                                                                  "Inventory Account (Interim)",false,false,GLAccountCategory."Account Category"::Assets,GLAccountCategoryMgt.GetInventory);
                                                              END;

                                                   OnLookup=BEGIN
                                                              GLAccountCategoryMgt.LookupGLAccount(
                                                                "Inventory Account",GLAccountCategory."Account Category"::Assets,GLAccountCategoryMgt.GetInventory);
                                                            END;

                                                   CaptionML=ENU=Inventory Account (Interim) }
    { 99000750;;WIP Account        ;Code20        ;TableRelation="G/L Account";
                                                   AccessByPermission=TableData "Production Order"=R;
                                                   CaptionML=ENU=WIP Account }
    { 99000753;;Material Variance Account;Code20  ;TableRelation="G/L Account";
                                                   CaptionML=ENU=Material Variance Account }
    { 99000754;;Capacity Variance Account;Code20  ;TableRelation="G/L Account";
                                                   CaptionML=ENU=Capacity Variance Account }
    { 99000755;;Mfg. Overhead Variance Account;Code20;
                                                   TableRelation="G/L Account";
                                                   CaptionML=ENU=Mfg. Overhead Variance Account }
    { 99000756;;Cap. Overhead Variance Account;Code20;
                                                   TableRelation="G/L Account";
                                                   CaptionML=ENU=Cap. Overhead Variance Account }
    { 99000757;;Subcontracted Variance Account;Code20;
                                                   TableRelation="G/L Account";
                                                   AccessByPermission=TableData "Production Order"=R;
                                                   CaptionML=ENU=Subcontracted Variance Account }
  }
  KEYS
  {
    {    ;"Location Code","Invt. Posting Group Code";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLAccountCategory@1001 : Record "G/L Account Category";
      GLAccountCategoryMgt@1002 : Codeunit "G/L Account Category Mgt.";
      YouCannotDeleteErr@1004 : TextConst '@@@="%1 = Location Code; %2 = Posting Group";ENU=You cannot delete %1 %2.';
      PostingSetupMgt@1003 : Codeunit PostingSetupManagement;

    LOCAL PROCEDURE CheckSetupUsage@1();
    BEGIN
      CALCFIELDS("Used in Ledger Entries");
      if "Used in Ledger Entries" > 0 then
        ERROR(YouCannotDeleteErr,"Location Code","Invt. Posting Group Code");
    END;

    [External]
    PROCEDURE GetCapacityVarianceAccount@7() : Code[20];
    BEGIN
      if "Capacity Variance Account" = '' then
        PostingSetupMgt.SendInvtPostingSetupNotification(Rec,FIELDCAPTION("Capacity Variance Account"));
      TESTFIELD("Capacity Variance Account");
      exit("Capacity Variance Account");
    END;

    [External]
    PROCEDURE GetCapOverheadVarianceAccount@9() : Code[20];
    BEGIN
      if "Cap. Overhead Variance Account" = '' then
        PostingSetupMgt.SendInvtPostingSetupNotification(Rec,FIELDCAPTION("Cap. Overhead Variance Account"));
      TESTFIELD("Cap. Overhead Variance Account");
      exit("Cap. Overhead Variance Account");
    END;

    [External]
    PROCEDURE GetInventoryAccount@3() : Code[20];
    BEGIN
      if "Inventory Account" = '' then
        PostingSetupMgt.SendInvtPostingSetupNotification(Rec,FIELDCAPTION("Inventory Account"));
      TESTFIELD("Inventory Account");
      exit("Inventory Account");
    END;

    [External]
    PROCEDURE GetInventoryAccountInterim@4() : Code[20];
    BEGIN
      if "Inventory Account (Interim)" = '' then
        PostingSetupMgt.SendInvtPostingSetupNotification(Rec,FIELDCAPTION("Inventory Account (Interim)"));
      TESTFIELD("Inventory Account (Interim)");
      exit("Inventory Account (Interim)");
    END;

    [External]
    PROCEDURE GetMaterialVarianceAccount@6() : Code[20];
    BEGIN
      if "Material Variance Account" = '' then
        PostingSetupMgt.SendInvtPostingSetupNotification(Rec,FIELDCAPTION("Material Variance Account"));
      TESTFIELD("Material Variance Account");
      exit("Material Variance Account");
    END;

    [External]
    PROCEDURE GetMfgOverheadVarianceAccount@8() : Code[20];
    BEGIN
      if "Mfg. Overhead Variance Account" = '' then
        PostingSetupMgt.SendInvtPostingSetupNotification(Rec,FIELDCAPTION("Mfg. Overhead Variance Account"));
      TESTFIELD("Mfg. Overhead Variance Account");
      exit("Mfg. Overhead Variance Account");
    END;

    [External]
    PROCEDURE GetSubcontractedVarianceAccount@10() : Code[20];
    BEGIN
      if "Subcontracted Variance Account" = '' then
        PostingSetupMgt.SendInvtPostingSetupNotification(Rec,FIELDCAPTION("Subcontracted Variance Account"));
      TESTFIELD("Subcontracted Variance Account");
      exit("Subcontracted Variance Account");
    END;

    [External]
    PROCEDURE GetWIPAccount@5() : Code[20];
    BEGIN
      if "WIP Account" = '' then
        PostingSetupMgt.SendInvtPostingSetupNotification(Rec,FIELDCAPTION("WIP Account"));
      TESTFIELD("WIP Account");
      exit("WIP Account");
    END;

    PROCEDURE SuggestSetupAccounts@2();
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      if "Inventory Account" = '' then
        SuggestAccount(RecRef,FIELDNO("Inventory Account"));
      if "Inventory Account" = '' then
        SuggestAccount(RecRef,FIELDNO("Inventory Account (Interim)"));
      if "WIP Account" = '' then
        SuggestAccount(RecRef,FIELDNO("WIP Account"));
      if "Material Variance Account" = '' then
        SuggestAccount(RecRef,FIELDNO("Material Variance Account"));
      if "Capacity Variance Account" = '' then
        SuggestAccount(RecRef,FIELDNO("Capacity Variance Account"));
      if "Mfg. Overhead Variance Account" = '' then
        SuggestAccount(RecRef,FIELDNO("Mfg. Overhead Variance Account"));
      if "Cap. Overhead Variance Account" = '' then
        SuggestAccount(RecRef,FIELDNO("Cap. Overhead Variance Account"));
      if "Subcontracted Variance Account" = '' then
        SuggestAccount(RecRef,FIELDNO("Subcontracted Variance Account"));
      RecRef.MODIFY;
    END;

    LOCAL PROCEDURE SuggestAccount@34(VAR RecRef@1000 : RecordRef;AccountFieldNo@1001 : Integer);
    VAR
      TempAccountUseBuffer@1002 : TEMPORARY Record "Account Use Buffer";
      RecFieldRef@1003 : FieldRef;
      InvtPostingSetupRecRef@1005 : RecordRef;
      InvtPostingSetupFieldRef@1004 : FieldRef;
    BEGIN
      InvtPostingSetupRecRef.OPEN(DATABASE::"Inventory Posting Setup");

      InvtPostingSetupRecRef.RESET;
      InvtPostingSetupFieldRef := InvtPostingSetupRecRef.FIELD(FIELDNO("Invt. Posting Group Code"));
      InvtPostingSetupFieldRef.SETFILTER('<>%1',"Invt. Posting Group Code");
      InvtPostingSetupFieldRef := InvtPostingSetupRecRef.FIELD(FIELDNO("Location Code"));
      InvtPostingSetupFieldRef.SETRANGE("Location Code");
      TempAccountUseBuffer.UpdateBuffer(InvtPostingSetupRecRef,AccountFieldNo);

      InvtPostingSetupRecRef.CLOSE;

      TempAccountUseBuffer.RESET;
      TempAccountUseBuffer.SETCURRENTKEY("No. of Use");
      if TempAccountUseBuffer.FINDLAST then begin
        RecFieldRef := RecRef.FIELD(AccountFieldNo);
        RecFieldRef.VALUE(TempAccountUseBuffer."Account No.");
      end;
    END;

    BEGIN
    END.
  }
}

