OBJECT Codeunit 5636 FA. Jnl.-Post
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="FA Journal Line";
    EventSubscriberInstance=Manual;
    OnRun=BEGIN
            FAJnlLine.COPY(Rec);
            Code;
            COPY(FAJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.';
      FAJnlTemplate@1005 : Record "FA Journal Template";
      FAJnlLine@1006 : Record "FA Journal Line";
      FAJnlPostBatch@1007 : Codeunit "FA Jnl.-Post Batch";
      TempJnlBatchName@1008 : Code[10];
      PreviewMode@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with FAJnlLine do begin
        FAJnlTemplate.GET("Journal Template Name");
        FAJnlTemplate.TESTFIELD("Force Posting Report",false);
        if FAJnlTemplate.Recurring and (GETFILTER("FA Posting Date") <> '') then
          FIELDERROR("FA Posting Date",Text000);

        if not PreviewMode then
          if not CONFIRM(Text001,false) then
            exit;

        TempJnlBatchName := "Journal Batch Name";

        FAJnlPostBatch.SetPreviewMode(PreviewMode);
        FAJnlPostBatch.RUN(FAJnlLine);

        if not PreviewMode then begin
          if "Line No." = 0 then
            MESSAGE(Text002)
          else
            if TempJnlBatchName = "Journal Batch Name" then
              MESSAGE(Text003)
            else
              MESSAGE(
                Text004,
                "Journal Batch Name");

          if not FIND('=><') or (TempJnlBatchName <> "Journal Batch Name") then begin
            RESET;
            FILTERGROUP := 2;
            SETRANGE("Journal Template Name","Journal Template Name");
            SETRANGE("Journal Batch Name","Journal Batch Name");
            FILTERGROUP := 0;
            "Line No." := 1;
          end;
        end;
      end;
    END;

    [Internal]
    PROCEDURE Preview@2(VAR FAJournalLine@1000 : Record "FA Journal Line");
    VAR
      GenJnlPostPreview@1001 : Codeunit "Gen. Jnl.-Post Preview";
      FAJnlPost@1002 : Codeunit "FA. Jnl.-Post";
    BEGIN
      BINDSUBSCRIPTION(FAJnlPost);
      GenJnlPostPreview.Preview(FAJnlPost,FAJournalLine);
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnRunPreview@3(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1002 : Variant);
    VAR
      FAJnlPost@1003 : Codeunit "FA. Jnl.-Post";
    BEGIN
      FAJnlPost := Subscriber;
      PreviewMode := true;
      Result := FAJnlPost.RUN(RecVar);
    END;

    BEGIN
    END.
  }
}

