OBJECT Page 6550 Whse. Item Tracking Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Whse. Item Tracking Line"=rimd;
    CaptionML=ENU=Whse. Item Tracking Lines;
    SourceTable="Whse. Item Tracking Line";
    DelayedInsert=true;
    PopulateAllFields=true;
    DataCaptionFields="Item No.","Variant Code",Description;
    PageType=Worksheet;
    OnInit=BEGIN
             ExpirationDateEditable := true;
             NewExpirationDateEditable := true;
             NewLotNoEditable := true;
             NewSerialNoEditable := true;
             QtyToHandleBaseEditable := true;
             ButtonLineVisible := true;
             QtyToHandleBaseVisible := true;
             Handle3Visible := true;
             Handle2Visible := true;
             Handle1Visible := true;
           END;

    OnOpenPage=BEGIN
                 UpdateUndefinedQty;
                 SaveItemTrkgLine(TempInitialTrkgLine);
               END;

    OnClosePage=BEGIN
                  if FormUpdated then
                    if not UpdateUndefinedQty then
                      RestoreInitialTrkgLine
                    else
                      if not CopyToReservEntry then
                        RestoreInitialTrkgLine;
                END;

    OnAfterGetRecord=BEGIN
                       ExpirationDateOnFormat;
                     END;

    OnInsertRecord=VAR
                     WhseItemTrackingLine2@1000 : Record "Whse. Item Tracking Line";
                   BEGIN
                     FormUpdated := true;
                     if ("Serial No." = '') and ("Lot No." = '') then
                       exit(false);
                     if WhseItemTrackingLine2.FINDLAST then;
                     "Entry No." := WhseItemTrackingLine2."Entry No." + 1;
                     exit(true);
                   END;

    OnModifyRecord=BEGIN
                     FormUpdated := true;
                   END;

    OnDeleteRecord=BEGIN
                     FormUpdated := true;
                     DELETE; // to ensure correct recalculation
                     CalculateSums;
                   END;

    OnQueryClosePage=BEGIN
                       if FormUpdated then
                         if not UpdateUndefinedQty then
                           exit(CONFIRM(Text002));
                     END;

    OnAfterGetCurrRecord=BEGIN
                           UpdateExpDateEditable;
                         END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      Name=ActionContainer1900000003;
                      ActionContainerType=RelatedInformation }
      { 34      ;1   ;ActionGroup;
                      Name=ButtonLineReclass;
                      CaptionML=ENU=&Line;
                      Visible=ButtonLineReclassVisible;
                      Image=Line }
      { 37      ;2   ;Action    ;
                      Name=Reclass_SerialNoInfoCard;
                      CaptionML=ENU=Serial No. Information Card;
                      ApplicationArea=#ItemTracking;
                      RunObject=Page "Serial No. Information List";
                      RunPageLink="Item No."=FIELD("Item No."),
                                  "Variant Code"=FIELD("Variant Code"),
                                  "Serial No."=FIELD("Serial No.");
                      Image=SNInfo;
                      OnAction=BEGIN
                                 TESTFIELD("Serial No.");
                               END;
                                }
      { 42      ;2   ;Action    ;
                      Name=Reclass_LotNoInfoCard;
                      CaptionML=ENU=Lot No. Information Card;
                      ApplicationArea=#Advanced;
                      RunObject=Page "Lot No. Information List";
                      RunPageLink="Item No."=FIELD("Item No."),
                                  "Variant Code"=FIELD("Variant Code"),
                                  "Lot No."=FIELD("Lot No.");
                      Image=LotInfo;
                      OnAction=BEGIN
                                 TESTFIELD("Lot No.");
                               END;
                                }
      { 44      ;2   ;Separator ;
                      Name=Separator44 }
      { 45      ;2   ;Action    ;
                      Name=New S&erial No. Information;
                      CaptionML=ENU=New S&erial No. Information;
                      ApplicationArea=#Advanced;
                      Image=NewSerialNoProperties;
                      OnAction=VAR
                                 SerialNoInfoNew@1000 : Record "Serial No. Information";
                                 SerialNoInfoForm@1001 : Page "Serial No. Information Card";
                               BEGIN
                                 TESTFIELD("New Serial No.");

                                 CLEAR(SerialNoInfoForm);
                                 SerialNoInfoForm.InitWhse(Rec);

                                 SerialNoInfoNew.SETRANGE("Item No.","Item No.");
                                 SerialNoInfoNew.SETRANGE("Variant Code","Variant Code");
                                 SerialNoInfoNew.SETRANGE("Serial No.","New Serial No.");

                                 SerialNoInfoForm.SETTABLEVIEW(SerialNoInfoNew);
                                 SerialNoInfoForm.RUN;
                               END;
                                }
      { 55      ;2   ;Action    ;
                      Name=New L&ot No. Information;
                      CaptionML=ENU=New L&ot No. Information;
                      ApplicationArea=#Advanced;
                      RunPageOnRec=false;
                      Image=NewLotProperties;
                      OnAction=VAR
                                 LotNoInfoNew@1000 : Record "Lot No. Information";
                                 LotNoInfoForm@1001 : Page "Lot No. Information Card";
                               BEGIN
                                 TESTFIELD("New Lot No.");

                                 CLEAR(LotNoInfoForm);
                                 LotNoInfoForm.InitWhse(Rec);

                                 LotNoInfoNew.SETRANGE("Item No.","Item No.");
                                 LotNoInfoNew.SETRANGE("Variant Code","Variant Code");
                                 LotNoInfoNew.SETRANGE("Lot No.","New Lot No.");

                                 LotNoInfoForm.SETTABLEVIEW(LotNoInfoNew);
                                 LotNoInfoForm.RUN;
                               END;
                                }
      { 20      ;1   ;ActionGroup;
                      Name=ButtonLine;
                      CaptionML=ENU=&Line;
                      Visible=ButtonLineVisible;
                      Image=Line }
      { 21      ;2   ;Action    ;
                      Name=Line_SerialNoInfoCard;
                      CaptionML=ENU=Serial No. Information Card;
                      ApplicationArea=#ItemTracking;
                      RunObject=Page "Serial No. Information List";
                      RunPageLink="Item No."=FIELD("Item No."),
                                  "Variant Code"=FIELD("Variant Code"),
                                  "Serial No."=FIELD("Serial No.");
                      Image=SNInfo;
                      OnAction=BEGIN
                                 TESTFIELD("Serial No.");
                               END;
                                }
      { 33      ;2   ;Action    ;
                      Name=Line_LotNoInforCard;
                      CaptionML=ENU=Lot No. Information Card;
                      ApplicationArea=#Advanced;
                      RunObject=Page "Lot No. Information List";
                      RunPageLink="Item No."=FIELD("Item No."),
                                  "Variant Code"=FIELD("Variant Code"),
                                  "Lot No."=FIELD("Lot No.");
                      Image=LotInfo;
                      OnAction=BEGIN
                                 TESTFIELD("Lot No.");
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 59  ;1   ;Group     ;
                Name=Group59 }

    { 1903651101;2;Group  ;
                Name=Group1903651101;
                GroupType=FixedLayout }

    { 1903866701;3;Group  ;
                Name=Source;
                CaptionML=ENU=Source }

    { 38  ;4   ;Field     ;
                Name=TextCaption;
                ApplicationArea=#Advanced;
                SourceExpr=TextCaption;
                Editable=FALSE;
                ShowCaption=false }

    { 29  ;4   ;Field     ;
                Name=SourceQuantityArray[1];
                CaptionML=ENU=Quantity;
                ToolTipML=ENU=Specifies the quantity of the item that corresponds to the warehouse tracking line.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=SourceQuantityArray[1];
                Editable=FALSE }

    { 31  ;4   ;Field     ;
                Name=Handle1;
                CaptionML=ENU=Qty. to Handle;
                ToolTipML=ENU=Specifies the item-tracked quantity to be handled. The quantities must correspond to those of the document line.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=SourceQuantityArray[2];
                Visible=Handle1Visible;
                Editable=FALSE }

    { 1901742101;3;Group  ;
                Name=Item Tracking;
                CaptionML=ENU=Item Tracking }

    { 51  ;4   ;Field     ;
                Name=Text003;
                ApplicationArea=#Advanced;
                SourceExpr=Text003;
                Visible=FALSE;
                ImplicitType=TextConst }

    { 35  ;4   ;Field     ;
                Name=TotalWhseItemTrackingLine."Quantity (Base)";
                CaptionML=ENU=Quantity;
                ToolTipML=ENU=Specifies the quantity of the item that corresponds to the warehouse tracking line.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalWhseItemTrackingLine."Quantity (Base)";
                Editable=FALSE }

    { 36  ;4   ;Field     ;
                Name=Handle2;
                CaptionML=ENU=Qty. to Ship/Receive;
                ToolTipML=ENU=Specifies the quantities handled in warehouse activity lines.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalWhseItemTrackingLine."Qty. to Handle (Base)";
                Visible=Handle2Visible;
                Editable=FALSE }

    { 1903866601;3;Group  ;
                Name=Undefined;
                CaptionML=ENU=Undefined }

    { 52  ;4   ;Field     ;
                Name=Field52;
                ApplicationArea=#Advanced;
                SourceExpr=Text003;
                Visible=FALSE;
                ImplicitType=TextConst }

    { 40  ;4   ;Field     ;
                Name=Quantity3;
                ApplicationArea=#Advanced;
                BlankZero=true;
                SourceExpr=UndefinedQtyArray[1];
                Editable=FALSE;
                ShowCaption=false }

    { 41  ;4   ;Field     ;
                Name=Handle3;
                ApplicationArea=#Advanced;
                BlankZero=true;
                SourceExpr=UndefinedQtyArray[2];
                Visible=Handle3Visible;
                Editable=FALSE;
                ShowCaption=false }

    { 43  ;1   ;Group     ;
                Name=Group43 }

    { 503 ;2   ;Field     ;
                Name=Item Tracking Code;
                Lookup=true;
                CaptionML=ENU=Item Tracking Code;
                ToolTipML=ENU=Specifies the code for the warehouse item to be tracked.;
                ApplicationArea=#Advanced;
                SourceExpr=ItemTrackingCode.Code;
                Editable=FALSE;
                OnLookup=BEGIN
                           PAGE.RUNMODAL(0,ItemTrackingCode);
                         END;
                          }

    { 505 ;2   ;Field     ;
                Name=Description;
                CaptionML=ENU=Description;
                ToolTipML=ENU=Specifies the description of the warehouse item.;
                ApplicationArea=#Advanced;
                SourceExpr=ItemTrackingCode.Description;
                Editable=FALSE }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 22  ;2   ;Field     ;
                Name=Serial No.;
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#ItemTracking;
                SourceExpr="Serial No.";
                OnValidate=BEGIN
                             SerialNoOnAfterValidate;
                           END;

                OnAssistEdit=VAR
                               MaxQuantity@1106000000 : Decimal;
                             BEGIN
                               if ColorOfQuantityArray[1] = 0 then
                                 MaxQuantity := UndefinedQtyArray[1] + ("Quantity (Base)" - "Quantity Handled (Base)");

                               LookUpTrackingSummary(Rec,0,MaxQuantity,-1,true);
                               CurrPage.UPDATE;
                               CalculateSums;
                             END;

                ImplicitType=Code20 }

    { 10  ;2   ;Field     ;
                Name=New Serial No.;
                ToolTipML=ENU=Specifies a new serial number that replaces the number in the Serial No. field, when you post the warehouse item reclassification journal.;
                ApplicationArea=#ItemTracking;
                SourceExpr="New Serial No.";
                Visible=NewSerialNoVisible;
                Editable=NewSerialNoEditable;
                ImplicitType=Code20 }

    { 24  ;2   ;Field     ;
                Name=Lot No.;
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#ItemTracking;
                SourceExpr="Lot No.";
                OnValidate=BEGIN
                             LotNoOnAfterValidate;
                           END;

                OnAssistEdit=VAR
                               MaxQuantity@1106000000 : Decimal;
                             BEGIN
                               if ColorOfQuantityArray[1] = 0 then
                                 MaxQuantity := UndefinedQtyArray[1] + ("Quantity (Base)" - "Quantity Handled (Base)");

                               LookUpTrackingSummary(Rec,1,MaxQuantity,-1,true);
                               CurrPage.UPDATE;
                               CalculateSums;
                             END;

                ImplicitType=Code20 }

    { 46  ;2   ;Field     ;
                Name=New Lot No.;
                ToolTipML=ENU=Specifies a new lot number that replaces the number in the Lot No. field, when you post the warehouse item reclassification journal.;
                ApplicationArea=#ItemTracking;
                SourceExpr="New Lot No.";
                Visible=NewLotNoVisible;
                Editable=NewLotNoEditable;
                ImplicitType=Code20 }

    { 53  ;2   ;Field     ;
                Name=Expiration Date;
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#ItemTracking;
                SourceExpr="Expiration Date";
                Visible=FALSE;
                Editable=ExpirationDateEditable;
                ImplicitType=Date }

    { 16  ;2   ;Field     ;
                Name=New Expiration Date;
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#ItemTracking;
                SourceExpr="New Expiration Date";
                Visible=NewExpirationDateVisible;
                Editable=NewExpirationDateEditable;
                ImplicitType=Date }

    { 48  ;2   ;Field     ;
                Name=Warranty Date;
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#Advanced;
                SourceExpr="Warranty Date";
                Visible=FALSE;
                ImplicitType=Date }

    { 2   ;2   ;Field     ;
                Name=Item No.;
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#Advanced;
                SourceExpr="Item No.";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Code20 }

    { 4   ;2   ;Field     ;
                Name=Variant Code;
                ToolTipML=ENU=Specifies the variant of the item on the line.;
                ApplicationArea=#Advanced;
                SourceExpr="Variant Code";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Code10 }

    { 6   ;2   ;Field     ;
                Name=Field6;
                ToolTipML=ENU=Specifies the same as the field with the same name in the Item Tracking Lines window.;
                ApplicationArea=#Advanced;
                SourceExpr=Description;
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Text50 }

    { 8   ;2   ;Field     ;
                Name=Location Code;
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#Advanced;
                SourceExpr="Location Code";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Code10 }

    { 12  ;2   ;Field     ;
                Name=Quantity;
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#Advanced;
                SourceExpr="Quantity (Base)";
                OnValidate=BEGIN
                             QuantityBaseOnAfterValidate;
                           END;

                ImplicitType=Decimal }

    { 14  ;2   ;Field     ;
                Name=Qty. to Handle (Base);
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#Advanced;
                SourceExpr="Qty. to Handle (Base)";
                Visible=QtyToHandleBaseVisible;
                Editable=QtyToHandleBaseEditable;
                OnValidate=BEGIN
                             QtytoHandleBaseOnAfterValidate;
                           END;

                ImplicitType=Decimal }

    { 18  ;2   ;Field     ;
                Name=Quantity Handled (Base);
                ToolTipML=ENU=Specifies the same as the field in the Item Tracking Lines window.;
                ApplicationArea=#Advanced;
                SourceExpr="Quantity Handled (Base)";
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 1900000007;0;Container;
                Name=Container1900000007;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Name=Part1900383207;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Name=Part1905767507;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      WhseWorksheetLine@1007 : Record "Whse. Worksheet Line";
      TotalWhseItemTrackingLine@1002 : Record "Whse. Item Tracking Line";
      TempInitialTrkgLine@1009 : TEMPORARY Record "Whse. Item Tracking Line";
      Item@1005 : Record Item;
      ItemTrackingCode@1006 : Record "Item Tracking Code";
      ItemTrackingMgt@1010 : Codeunit "Item Tracking Management";
      UndefinedQtyArray@1001 : ARRAY [2] OF Decimal;
      SourceQuantityArray@1000 : ARRAY [2] OF Decimal;
      ColorOfQuantityArray@1003 : ARRAY [2] OF Integer;
      Text001@1004 : TextConst 'ENU=Line';
      Text002@1008 : TextConst 'ENU=The corrections cannot be saved as excess quantity has been defined.\Close the form anyway?';
      FormSourceType@1011 : Integer;
      FormUpdated@1012 : Boolean;
      Reclass@1014 : Boolean;
      Text003@1013 : TextConst 'ENU=Placeholder';
      Handle1Visible@19064734 : Boolean INDATASET;
      Handle2Visible@19067235 : Boolean INDATASET;
      Handle3Visible@19058196 : Boolean INDATASET;
      QtyToHandleBaseVisible@19036968 : Boolean INDATASET;
      NewSerialNoVisible@19031772 : Boolean INDATASET;
      NewLotNoVisible@19006815 : Boolean INDATASET;
      NewExpirationDateVisible@19041101 : Boolean INDATASET;
      ButtonLineReclassVisible@19076729 : Boolean INDATASET;
      ButtonLineVisible@19043118 : Boolean INDATASET;
      QtyToHandleBaseEditable@19075992 : Boolean INDATASET;
      NewSerialNoEditable@19030864 : Boolean INDATASET;
      NewLotNoEditable@19020282 : Boolean INDATASET;
      NewExpirationDateEditable@19056874 : Boolean INDATASET;
      ExpirationDateEditable@19023942 : Boolean INDATASET;

    LOCAL PROCEDURE TextCaption@1() : Text[30];
    VAR
      PostedWhseRcptLine@1000 : Record "Posted Whse. Receipt Line";
      WhseShipmentLine@1001 : Record "Warehouse Shipment Line";
    BEGIN
      case "Source Type" of
        DATABASE::"Posted Whse. Receipt Line":
          exit(PostedWhseRcptLine.TABLECAPTION);
        DATABASE::"Warehouse Shipment Line":
          exit(WhseShipmentLine.TABLECAPTION);
        else
          exit(Text001);
      end;
    END;

    [External]
    PROCEDURE SetSource@3(WhseWrkshLine@1000 : Record "Whse. Worksheet Line";SourceType@1001 : Integer);
    VAR
      WhseSNRequired@1003 : Boolean;
      WhseLNRequired@1004 : Boolean;
    BEGIN
      FormUpdated := false;
      FormSourceType := SourceType;
      WhseWorksheetLine := WhseWrkshLine;
      GetItem(WhseWorksheetLine."Item No.");
      ItemTrackingMgt.CheckWhseItemTrkgSetup(
        WhseWorksheetLine."Item No.",WhseSNRequired,WhseLNRequired,true);

      SetControlsAsHandle;
      Reclass := IsReclass(FormSourceType,WhseWorksheetLine."Worksheet Template Name");
      SetControlsAsReclass;

      SetFilters(Rec,FormSourceType);
      ItemTrackingMgt.UpdateQuantities(
        WhseWorksheetLine,TotalWhseItemTrackingLine,SourceQuantityArray,UndefinedQtyArray,FormSourceType);
      UpdateColorOfQty;
    END;

    LOCAL PROCEDURE SetControlsAsHandle@122();
    VAR
      SetAccess@1002 : Boolean;
    BEGIN
      SetAccess := FormSourceType <> DATABASE::"Warehouse Journal Line";
      Handle1Visible := SetAccess;
      Handle2Visible := SetAccess;
      Handle3Visible := SetAccess;
      QtyToHandleBaseVisible := SetAccess;
      QtyToHandleBaseEditable := SetAccess;
    END;

    LOCAL PROCEDURE SetControlsAsReclass@126();
    BEGIN
      NewSerialNoVisible := Reclass;
      NewSerialNoEditable := Reclass;
      NewLotNoVisible := Reclass;
      NewLotNoEditable := Reclass;
      NewExpirationDateVisible := Reclass;
      NewExpirationDateEditable := Reclass;
      ButtonLineReclassVisible := Reclass;
      ButtonLineVisible := not Reclass;
    END;

    [External]
    PROCEDURE SetFilters@12(VAR WhseItemTrkgLine2@1000 : Record "Whse. Item Tracking Line";SourceType@1001 : Integer);
    BEGIN
      with WhseItemTrkgLine2 do begin
        FILTERGROUP := 2;
        SETRANGE("Source Type",SourceType);
        SETRANGE("Location Code",WhseWorksheetLine."Location Code");
        SETRANGE("Item No.",WhseWorksheetLine."Item No.");
        SETRANGE("Variant Code",WhseWorksheetLine."Variant Code");
        SETRANGE("Qty. per Unit of Measure",WhseWorksheetLine."Qty. per Unit of Measure");

        case SourceType of
          DATABASE::"Posted Whse. Receipt Line",
          DATABASE::"Warehouse Shipment Line",
          DATABASE::"Whse. Internal Put-away Line",
          DATABASE::"Whse. Internal Pick Line",
          DATABASE::"Assembly Line",
          DATABASE::"Internal Movement Line":
            begin
              SETRANGE("Source ID",WhseWorksheetLine."Whse. Document No.");
              SETRANGE("Source Ref. No.",WhseWorksheetLine."Whse. Document Line No.");
            end;
          DATABASE::"Prod. Order Component":
            begin
              SETRANGE("Source Subtype",WhseWorksheetLine."Source Subtype");
              SETRANGE("Source ID",WhseWorksheetLine."Source No.");
              SETRANGE("Source Prod. Order Line",WhseWorksheetLine."Source Line No.");
              SETRANGE("Source Ref. No.",WhseWorksheetLine."Source Subline No.");
            end;
          DATABASE::"Whse. Worksheet Line",
          DATABASE::"Warehouse Journal Line":
            begin
              SETRANGE("Source Batch Name",WhseWorksheetLine."Worksheet Template Name");
              SETRANGE("Source ID",WhseWorksheetLine.Name);
              SETRANGE("Source Ref. No.",WhseWorksheetLine."Line No.");
            end;
        end;
        FILTERGROUP := 0;
      end;
    END;

    LOCAL PROCEDURE UpdateExpDateColor@41();
    BEGIN
      if BlockExpDate then;
    END;

    LOCAL PROCEDURE UpdateExpDateEditable@42();
    BEGIN
      ExpirationDateEditable := not BlockExpDate;
    END;

    LOCAL PROCEDURE BlockExpDate@15() : Boolean;
    BEGIN
      exit(
        ("Buffer Status2" = "Buffer Status2"::"ExpDate blocked") or
        (WhseWorksheetLine."Qty. (Base)" < 0) or
        Reclass or
        (FormSourceType in
         [DATABASE::"Whse. Worksheet Line",
          DATABASE::"Posted Whse. Receipt Line",
          DATABASE::"Whse. Internal Put-away Line"]));
    END;

    LOCAL PROCEDURE CalculateSums@2();
    BEGIN
      ItemTrackingMgt.CalculateSums(
        WhseWorksheetLine,TotalWhseItemTrackingLine,
        SourceQuantityArray,UndefinedQtyArray,FormSourceType);
      UpdateColorOfQty;
    END;

    LOCAL PROCEDURE UpdateUndefinedQty@5() QtyIsValid : Boolean;
    BEGIN
      QtyIsValid := ItemTrackingMgt.UpdateUndefinedQty(TotalWhseItemTrackingLine,SourceQuantityArray,UndefinedQtyArray);
      UpdateColorOfQty;
    END;

    LOCAL PROCEDURE UpdateColorOfQty@119();
    BEGIN
      ColorOfQuantityArray[1] := GetQtyColor(SourceQuantityArray[1],TotalWhseItemTrackingLine."Quantity (Base)");
      ColorOfQuantityArray[2] := GetQtyColor(SourceQuantityArray[2],TotalWhseItemTrackingLine."Qty. to Handle (Base)");
    END;

    LOCAL PROCEDURE GetQtyColor@14(SourceQty@1000 : Decimal;TrackingQty@1001 : Decimal) : Integer;
    BEGIN
      if ABS(SourceQty) < ABS(TrackingQty) then
        exit(255);
      exit(0);
    END;

    LOCAL PROCEDURE GetItem@4(ItemNo@1000 : Code[20]);
    BEGIN
      if Item."No." <> ItemNo then begin
        Item.GET(ItemNo);
        Item.TESTFIELD("Item Tracking Code");
        if ItemTrackingCode.Code <> Item."Item Tracking Code" then
          ItemTrackingCode.GET(Item."Item Tracking Code");
      end;
    END;

    LOCAL PROCEDURE SaveItemTrkgLine@6(VAR TempWhseItemTrkgLine@1001 : TEMPORARY Record "Whse. Item Tracking Line");
    VAR
      WhseItemTrkgLine2@1000 : Record "Whse. Item Tracking Line";
    BEGIN
      SetFilters(WhseItemTrkgLine2,FormSourceType);
      TempWhseItemTrkgLine.RESET;
      TempWhseItemTrkgLine.DELETEALL;
      if WhseItemTrkgLine2.FIND('-') then
        repeat
          TempWhseItemTrkgLine := WhseItemTrkgLine2;
          TempWhseItemTrkgLine.INSERT;
        until WhseItemTrkgLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE RestoreInitialTrkgLine@9();
    VAR
      WhseItemTrkgLine2@1000 : Record "Whse. Item Tracking Line";
    BEGIN
      SetFilters(WhseItemTrkgLine2,FormSourceType);
      WhseItemTrkgLine2.DELETEALL;
      if TempInitialTrkgLine.FIND('-') then
        repeat
          WhseItemTrkgLine2 := TempInitialTrkgLine;
          WhseItemTrkgLine2.INSERT;
        until TempInitialTrkgLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyToReservEntry@10() : Boolean;
    VAR
      WhseItemTrkgLine2@1000 : Record "Whse. Item Tracking Line";
      SourceWhseItemTrkgLine@1006 : TEMPORARY Record "Whse. Item Tracking Line";
      WhseShptLine@1002 : Record "Warehouse Shipment Line";
      ProdOrderComp@1003 : Record "Prod. Order Component";
      QuantityBase@1005 : Decimal;
      DueDate@1004 : Date;
      Updated@1001 : Boolean;
    BEGIN
      SetFilters(WhseItemTrkgLine2,FormSourceType);

      if WhseItemTrkgLine2.FIND('-') then
        SourceWhseItemTrkgLine := WhseItemTrkgLine2
      else
        if TempInitialTrkgLine.FIND('-') then
          SourceWhseItemTrkgLine := TempInitialTrkgLine
        else
          exit(true);

      case FormSourceType of
        DATABASE::"Prod. Order Component":
          begin
            ProdOrderComp.GET(SourceWhseItemTrkgLine."Source Subtype",SourceWhseItemTrkgLine."Source ID",
              SourceWhseItemTrkgLine."Source Prod. Order Line",SourceWhseItemTrkgLine."Source Ref. No.");
            QuantityBase := ProdOrderComp."Expected Qty. (Base)";
            DueDate := ProdOrderComp."Due Date";
            Updated := UpdateReservEntry(
                SourceWhseItemTrkgLine."Source Type",
                SourceWhseItemTrkgLine."Source Subtype",
                SourceWhseItemTrkgLine."Source ID",
                SourceWhseItemTrkgLine."Source Prod. Order Line",
                SourceWhseItemTrkgLine."Source Ref. No.",
                SourceWhseItemTrkgLine,QuantityBase,DueDate);
          end;
        DATABASE::"Warehouse Shipment Line":
          begin
            WhseShptLine.GET(SourceWhseItemTrkgLine."Source ID",SourceWhseItemTrkgLine."Source Ref. No.");
            QuantityBase := WhseShptLine."Qty. (Base)";
            DueDate := WhseShptLine."Due Date";
            Updated := UpdateReservEntry(
                WhseShptLine."Source Type",
                WhseShptLine."Source Subtype",
                WhseShptLine."Source No.",
                0,
                WhseShptLine."Source Line No.",
                SourceWhseItemTrkgLine,QuantityBase,DueDate);
          end;
        else
          exit(true);
      end;
      exit(Updated)
    END;

    LOCAL PROCEDURE UpdateReservEntry@7(SourceType@1000 : Integer;SourceSubtype@1001 : Integer;SourceID@1002 : Code[20];SourceProdOrderLine@1003 : Integer;SourceRefNo@1004 : Integer;SourceWhseItemTrkgLine@1005 : TEMPORARY Record "Whse. Item Tracking Line";QuantityBase@1013 : Decimal;DueDate@1012 : Date) : Boolean;
    VAR
      TempTrkgSpec@1006 : TEMPORARY Record "Tracking Specification";
      SourceSpecification@1009 : Record "Tracking Specification";
      WhseItemTrkgLine2@1014 : Record "Whse. Item Tracking Line";
      ItemTrackingMgt@1007 : Codeunit "Item Tracking Management";
      LastEntryNo@1008 : Integer;
    BEGIN
      if TempInitialTrkgLine.FIND('-') then
        repeat
          TempTrkgSpec.TRANSFERFIELDS(TempInitialTrkgLine);
          TempTrkgSpec."Quantity (Base)" *= -1;
          TempTrkgSpec."Entry No." := LastEntryNo + 1;
          TempTrkgSpec.INSERT;
          LastEntryNo := TempTrkgSpec."Entry No.";
        until TempInitialTrkgLine.NEXT = 0;

      SetFilters(WhseItemTrkgLine2,FormSourceType);
      if WhseItemTrkgLine2.FIND('-') then
        repeat
          TempTrkgSpec.TRANSFERFIELDS(WhseItemTrkgLine2);
          TempTrkgSpec."Entry No." := LastEntryNo + 1;
          TempTrkgSpec.INSERT;
          LastEntryNo := TempTrkgSpec."Entry No.";
        until WhseItemTrkgLine2.NEXT = 0;

      SourceSpecification."Source Type" := SourceType;
      SourceSpecification."Source Subtype" := SourceSubtype;
      SourceSpecification."Source ID" := SourceID;
      SourceSpecification."Source Batch Name" := '';
      SourceSpecification."Source Prod. Order Line" := SourceProdOrderLine;
      SourceSpecification."Source Ref. No." := SourceRefNo;
      SourceSpecification."Quantity (Base)" := QuantityBase;
      SourceSpecification."Item No." := SourceWhseItemTrkgLine."Item No.";
      SourceSpecification."Variant Code" := SourceWhseItemTrkgLine."Variant Code";
      SourceSpecification."Location Code" := SourceWhseItemTrkgLine."Location Code";
      SourceSpecification.Description := SourceWhseItemTrkgLine.Description;
      SourceSpecification."Qty. per Unit of Measure" := SourceWhseItemTrkgLine."Qty. per Unit of Measure";
      ItemTrackingMgt.SetGlobalParameters(SourceSpecification,TempTrkgSpec,DueDate);
      exit(ItemTrackingMgt.RUN);
    END;

    [External]
    PROCEDURE InsertItemTrackingLine@8(WhseWrkshLine@1004 : Record "Whse. Worksheet Line";SerialNo@1000 : Code[20];LotNo@1001 : Code[20];ExpirationDate@1005 : Date;QtyToEmpty@1002 : Decimal);
    VAR
      WhseItemTrackingLine2@1003 : Record "Whse. Item Tracking Line";
    BEGIN
      UpdateUndefinedQty;
      SaveItemTrkgLine(TempInitialTrkgLine);
      "Lot No." := LotNo;
      "Serial No." := SerialNo;
      "Expiration Date" := ExpirationDate;
      "Qty. per Unit of Measure" := WhseWrkshLine."Qty. per Unit of Measure";
      VALIDATE("Quantity (Base)",QtyToEmpty);
      "Source Type" := FormSourceType;
      "Source ID" := WhseWorksheetLine."Whse. Document No.";
      "Source Ref. No." := WhseWorksheetLine."Whse. Document Line No.";
      "Source Batch Name" := WhseWrkshLine."Worksheet Template Name";
      "Location Code" := WhseWorksheetLine."Location Code";
      "Item No." := WhseWorksheetLine."Item No.";
      "Variant Code" := WhseWrkshLine."Variant Code";
      if ("Expiration Date" <> 0D) and (FormSourceType = DATABASE::"Internal Movement Line") then
        InitExpirationDate;
      if WhseItemTrackingLine2.FINDLAST then;
      "Entry No." := WhseItemTrackingLine2."Entry No." + 1;
      INSERT;
    END;

    LOCAL PROCEDURE SerialNoOnAfterValidate@19074494();
    BEGIN
      UpdateExpDateEditable;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE LotNoOnAfterValidate@19045288();
    BEGIN
      UpdateExpDateEditable;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE QuantityBaseOnAfterValidate@19029188();
    BEGIN
      CurrPage.UPDATE;
      CalculateSums;
    END;

    LOCAL PROCEDURE QtytoHandleBaseOnAfterValidate@19004517();
    BEGIN
      CurrPage.UPDATE;
      CalculateSums;
    END;

    LOCAL PROCEDURE ExpirationDateOnFormat@19045111();
    BEGIN
      UpdateExpDateColor;
    END;

    BEGIN
    {
      Function button Line exist in two overlayed versions to make dynamic show/hide/enable of
      individual menu items possible.
    }
    END.
  }
}

