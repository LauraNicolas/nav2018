OBJECT Table 171 Standard Sales Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LOCKTABLE;
               StdSalesCode.GET("Standard Sales Code");
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=ENU=Standard Sales Line;
  }
  FIELDS
  {
    { 1   ;   ;Standard Sales Code ;Code10        ;TableRelation="Standard Sales Code";
                                                   CaptionML=ENU=Standard Sales Code;
                                                   Editable=false }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   Editable=false }
    { 3   ;   ;Type                ;Option        ;OnValidate=VAR
                                                                OldType@1000 : Integer;
                                                              BEGIN
                                                                OldType := Type;
                                                                INIT;
                                                                Type := OldType;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)";
                                                   OptionString=" ","G/L Account",Item,Resource,"Fixed Asset","Charge (Item)" }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(" ")) "Standard Text"
                                                                 ELSE IF (Type=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST("Fixed Asset")) "Fixed Asset"
                                                                 ELSE IF (Type=CONST("Charge (Item)")) "Item Charge";
                                                   OnValidate=VAR
                                                                GLAcc@1001 : Record "G/L Account";
                                                                Item@1002 : Record Item;
                                                                Res@1005 : Record Resource;
                                                                ItemCharge@1006 : Record "Item Charge";
                                                                FA@1003 : Record "Fixed Asset";
                                                                StdTxt@1004 : Record "Standard Text";
                                                                StdSalesCode@1000 : Record "Standard Sales Code";
                                                              BEGIN
                                                                Quantity := 0;
                                                                "Amount Excl. VAT" := 0;
                                                                "Unit of Measure Code" := '';
                                                                Description := '';
                                                                if "No." = '' then
                                                                  exit;
                                                                StdSalesCode.GET("Standard Sales Code");
                                                                case Type of
                                                                  Type::" ":
                                                                    begin
                                                                      StdTxt.GET("No.");
                                                                      Description := StdTxt.Description;
                                                                    end;
                                                                  Type::"G/L Account":
                                                                    begin
                                                                      GLAcc.GET("No.");
                                                                      GLAcc.CheckGLAcc;
                                                                      GLAcc.TESTFIELD("Direct Posting",true);
                                                                      Description := GLAcc.Name;
                                                                    end;
                                                                  Type::Item:
                                                                    begin
                                                                      Item.GET("No.");
                                                                      Item.TESTFIELD(Blocked,false);
                                                                      Item.TESTFIELD("Gen. Prod. Posting Group");
                                                                      if Item.Type = Item.Type::Inventory then
                                                                        Item.TESTFIELD("Inventory Posting Group");
                                                                      "Unit of Measure Code" := Item."Sales Unit of Measure";
                                                                      Description := Item.Description;
                                                                      "Variant Code" := '';
                                                                    end;
                                                                  Type::Resource:
                                                                    begin
                                                                      Res.GET("No.");
                                                                      Res.TESTFIELD(Blocked,false);
                                                                      Res.TESTFIELD("Gen. Prod. Posting Group");
                                                                      "Unit of Measure Code" := Res."Base Unit of Measure";
                                                                      Description := Res.Name;
                                                                    end;
                                                                  Type::"Fixed Asset":
                                                                    begin
                                                                      FA.GET("No.");
                                                                      FA.TESTFIELD(Inactive,false);
                                                                      FA.TESTFIELD(Blocked,false);
                                                                      Description := FA.Description;
                                                                    end;
                                                                  Type::"Charge (Item)":
                                                                    begin
                                                                      ItemCharge.GET("No.");
                                                                      Description := ItemCharge.Description;
                                                                    end;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 5   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 6   ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD(Type);
                                                              END;

                                                   CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5;
                                                   BlankZero=true }
    { 7   ;   ;Amount Excl. VAT    ;Decimal       ;OnValidate=BEGIN
                                                                if (Type <> Type::"G/L Account") and (Type <> Type::"Charge (Item)") then
                                                                  ERROR(Text001,FIELDCAPTION(Type),Type);
                                                              END;

                                                   CaptionML=ENU=Amount Excl. VAT;
                                                   BlankZero=true;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=GetCurrency }
    { 8   ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE ("Item No."=FIELD("No."))
                                                                 ELSE "Unit of Measure";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type);
                                                              END;

                                                   CaptionML=ENU=Unit of Measure Code }
    { 9   ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 10  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 11  ;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE ("Item No."=FIELD("No."));
                                                   OnValidate=VAR
                                                                Item@1001 : Record Item;
                                                                ItemVariant@1000 : Record "Item Variant";
                                                              BEGIN
                                                                if "Variant Code" = '' then begin
                                                                  if Type = Type::Item then begin
                                                                    Item.GET("No.");
                                                                    Description := Item.Description;
                                                                  end;
                                                                  exit;
                                                                end;

                                                                TESTFIELD(Type,Type::Item);
                                                                ItemVariant.GET("No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                              END;

                                                   CaptionML=ENU=Variant Code }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Standard Sales Code","Line No."        ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      StdSalesCode@1001 : Record "Standard Sales Code";
      DimMgt@1000 : Codeunit DimensionManagement;
      Text000@1003 : TextConst 'ENU=You cannot rename a %1.';
      Text001@1005 : TextConst 'ENU=%1 must not be %2.';
      CommentLbl@1002 : TextConst 'ENU=Comment';

    [External]
    PROCEDURE EmptyLine@5() : Boolean;
    BEGIN
      exit(("No." = '') and (Quantity = 0))
    END;

    [External]
    PROCEDURE InsertLine@1() : Boolean;
    BEGIN
      exit((Type = Type::" ") or (not EmptyLine));
    END;

    LOCAL PROCEDURE GetCurrency@2() : Code[10];
    VAR
      StdSalesCode@1000 : Record "Standard Sales Code";
    BEGIN
      if StdSalesCode.GET("Standard Sales Code") then
        exit(StdSalesCode."Currency Code");

      exit('');
    END;

    [External]
    PROCEDURE ShowDimensions@3();
    BEGIN
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2',"Standard Sales Code","Line No."));
      DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    [External]
    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    [External]
    PROCEDURE LookupShortcutDimCode@28(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    [External]
    PROCEDURE ShowShortcutDimCode@27(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      DimMgt.GetShortcutDimensions("Dimension Set ID",ShortcutDimCode);
    END;

    PROCEDURE FormatType@4() : Text[20];
    BEGIN
      if Type = Type::" " then
        exit(CommentLbl);

      exit(FORMAT(Type));
    END;

    BEGIN
    END.
  }
}

