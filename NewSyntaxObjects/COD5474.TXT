OBJECT Codeunit 5474 Graph Mgt - Sales Header
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE UpdateIntegrationRecords@11(OnlyItemsWithoutId@1000 : Boolean);
    VAR
      DummySalesHeader@1002 : Record "Sales Header";
      GraphMgtGeneralTools@1001 : Codeunit "Graph Mgt - General Tools";
      SalesHeaderRecordRef@1019 : RecordRef;
    BEGIN
      SalesHeaderRecordRef.OPEN(DATABASE::"Sales Header");
      GraphMgtGeneralTools.UpdateIntegrationRecords(SalesHeaderRecordRef,DummySalesHeader.FIELDNO(Id),OnlyItemsWithoutId);
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnItem@19(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummySalesHeader@1003 : Record "Sales Header";
      GraphMgtGeneralTools@1006 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      if not CheckSupportedTable(RecRef) then
        exit;

      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(RecRef,NewId,Handled,
        RecRef.NUMBER,DummySalesHeader.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@21(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummySalesHeader@1003 : Record "Sales Header";
      GraphMgtGeneralTools@1007 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      if not CheckSupportedTable(RecRef) then
        exit;

      GraphMgtGeneralTools.HandleGetPredefinedIdValue(Id,RecRef,Handled,
        RecRef.NUMBER,DummySalesHeader.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@16();
    BEGIN
      UpdateIntegrationRecords(false);
      UpdateIds;
    END;

    LOCAL PROCEDURE CheckSupportedTable@3(VAR RecRef@1000 : RecordRef) : Boolean;
    VAR
      SalesHeader@1002 : Record "Sales Header";
    BEGIN
      if RecRef.NUMBER = DATABASE::"Sales Header" then begin
        RecRef.SETTABLE(SalesHeader);
        exit((SalesHeader."Document Type" <> SalesHeader."Document Type"::Order) and
          (SalesHeader."Document Type" <> SalesHeader."Document Type"::Invoice));
      end;
      exit(false);
    END;

    PROCEDURE UpdateIds@6();
    VAR
      SalesInvoiceEntityAggregate@1000 : Record "Sales Invoice Entity Aggregate";
    BEGIN
      with SalesInvoiceEntityAggregate do begin
        if FINDSET then
          repeat
            UpdateReferencedRecordIds;
            MODIFY(false);
          until NEXT = 0;
      end;
    END;

    BEGIN
    {
      // This Graph Mgt code unit is used to generate id fields for all
      // sales docs other than invoice and order. If special logic is required
      // for any of these sales docs, create a seperate code unit.
    }
    END.
  }
}

