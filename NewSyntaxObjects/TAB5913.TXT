OBJECT Table 5913 Loaner
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields="No.",Description;
    OnInsert=BEGIN
               if "No." = '' then begin
                 ServMgtSetup.GET;
                 ServMgtSetup.TESTFIELD("Loaner Nos.");
                 NoSeriesMgt.InitSeries(ServMgtSetup."Loaner Nos.",xRec."No. Series",0D,"No.","No. Series");
               end;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               ServCommentLine@1000 : Record "Service Comment Line";
             BEGIN
               CALCFIELDS(Lent,"Document No.");

               if Lent then
                 ERROR(Text000);

               if not Blocked then
                 ERROR(Text001,TABLECAPTION,FIELDCAPTION(Blocked));

               CLEAR(LoanerEntry);
               LoanerEntry.SETCURRENTKEY("Loaner No.");
               LoanerEntry.SETRANGE("Loaner No.","No.");
               LoanerEntry.DELETEALL;

               ServCommentLine.RESET;
               ServCommentLine.SETRANGE("Table Name",ServCommentLine."Table Name"::Loaner);
               ServCommentLine.SETRANGE("Table Subtype",0);
               ServCommentLine.SETRANGE("No.","No.");
               ServCommentLine.DELETEALL;
             END;

    CaptionML=ENU=Loaner;
    LookupPageID="Loaner List";
    DrillDownPageID="Loaner List";
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                if "No." <> xRec."No." then begin
                                                                  ServMgtSetup.GET;
                                                                  NoSeriesMgt.TestManual(ServMgtSetup."Loaner Nos.");
                                                                  "No. Series" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 2   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 4   ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2 }
    { 5   ;   ;Unit of Measure Code;Code10        ;TableRelation=IF ("Item No."=CONST('<>''')) "Item Unit of Measure".Code WHERE ("Item No."=FIELD("Item No."))
                                                                 ELSE "Unit of Measure";
                                                   CaptionML=ENU=Unit of Measure Code }
    { 6   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                if "Item No." <> '' then begin
                                                                  Item.GET("Item No.");
                                                                  Description := Item.Description;
                                                                  "Description 2" := Item."Description 2";
                                                                end else begin
                                                                  Description := '';
                                                                  "Description 2" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Item No. }
    { 7   ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=false }
    { 8   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Service Comment Line" WHERE ("Table Name"=CONST(Loaner),
                                                                                                   "Table Subtype"=CONST("0"),
                                                                                                   "No."=FIELD("No.")));
                                                   CaptionML=ENU=Comment;
                                                   Editable=false }
    { 9   ;   ;Blocked             ;Boolean       ;CaptionML=ENU=Blocked }
    { 11  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=false }
    { 12  ;   ;Document No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Loaner Entry"."Document No." WHERE ("Loaner No."=FIELD("No."),
                                                                                                           Lent=CONST(true)));
                                                   CaptionML=ENU=Document No.;
                                                   Editable=false }
    { 13  ;   ;Lent                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Loaner Entry" WHERE ("Loaner No."=FIELD("No."),
                                                                                           Lent=CONST(true)));
                                                   CaptionML=ENU=Lent;
                                                   Editable=false }
    { 14  ;   ;Serial No.          ;Code20        ;CaptionML=ENU=Serial No. }
    { 15  ;   ;Document Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Loaner Entry"."Document Type" WHERE ("Loaner No."=FIELD("No."),
                                                                                                            Lent=CONST(true)));
                                                   CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=" ,Quote,Order";
                                                   OptionString=" ",Quote,Order;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"No."                                   ;Clustered=true }
    {    ;Description                              }
    {    ;"Item No."                               }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;"No.",Description,"Item No."             }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot delete a loaner that is lent.';
      Text001@1001 : TextConst 'ENU=You can only delete a %1 that is %2.';
      ServMgtSetup@1002 : Record "Service Mgt. Setup";
      Item@1005 : Record Item;
      Loaner@1003 : Record Loaner;
      LoanerEntry@1004 : Record "Loaner Entry";
      NoSeriesMgt@1006 : Codeunit NoSeriesManagement;

    [External]
    PROCEDURE AssistEdit@2(OldLoaner@1000 : Record Loaner) : Boolean;
    BEGIN
      with Loaner do begin
        Loaner := Rec;
        ServMgtSetup.GET;
        ServMgtSetup.TESTFIELD("Loaner Nos.");
        if NoSeriesMgt.SelectSeries(ServMgtSetup."Loaner Nos.",OldLoaner."No. Series","No. Series") then begin
          NoSeriesMgt.SetSeries("No.");
          Rec := Loaner;
          exit(true);
        end;
      end;
    END;

    BEGIN
    END.
  }
}

