OBJECT Page 2338 BC O365 Payments Settings
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Payments;
    PageType=CardPart;
    OnInit=BEGIN
             Intialize;
           END;

    OnQueryClosePage=BEGIN
                       O365SalesInitialSetup.GET;
                       if PaymentTermsCode <> O365SalesInitialSetup."Default Payment Terms Code" then
                         UpdateDefaultPaymentTerms;

                       if PaymentMethodCode <> O365SalesInitialSetup."Default Payment Method Code" then
                         UpdateDefaultPaymentMethod;
                     END;

  }
  CONTROLS
  {
    { 3   ;    ;Container ;
                Name=Container3;
                ContainerType=ContentArea }

    { 5   ;1   ;Group     ;
                Name=Group5;
                GroupType=Group;
                InstructionalTextML=ENU=You can change payment terms and method for each invoice. }

    { 1   ;1   ;Field     ;
                Name=PaymentTermsCode;
                CaptionML=ENU=Payment terms;
                ToolTipML=ENU=Specifies the payment terms that you select from on customer cards to define when the customer must pay, such as within 14 days.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=PaymentTermsCode;
                TableRelation="Payment Terms" WHERE ("Discount %"=CONST(0));
                OnLookup=VAR
                           O365PaymentTerms@1000 : Record "O365 Payment Terms";
                           CloseAction@1001 : Action;
                         BEGIN
                           CloseAction := PAGE.RUNMODAL(PAGE::"O365 Payment Terms List",O365PaymentTerms);
                           if CloseAction <> ACTION::LookupOK then
                             exit;

                           PaymentTermsCode := O365PaymentTerms.Code;
                         END;

                QuickEntry=FALSE }

    { 4   ;1   ;Field     ;
                Name=PaymentMethodCode;
                CaptionML=ENU=Payment method;
                ToolTipML=ENU=Specifies the payment methods that you select from on customer cards to define how the customer must pay, such as bank transfer.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=PaymentMethodCode;
                TableRelation="Payment Method" WHERE ("Use for Invoicing"=CONST(true));
                OnLookup=VAR
                           O365PaymentMethod@1001 : Record "O365 Payment Method";
                           CloseAction@1000 : Action;
                         BEGIN
                           CloseAction := PAGE.RUNMODAL(PAGE::"O365 Payment Method List",O365PaymentMethod);
                           if CloseAction <> ACTION::LookupOK then
                             exit;

                           PaymentMethodCode := O365PaymentMethod.Code;
                         END;

                QuickEntry=FALSE }

  }
  CODE
  {
    VAR
      O365SalesInitialSetup@1001 : Record "O365 Sales Initial Setup";
      PaymentTermsCode@1000 : Code[10];
      PaymentMethodCode@1002 : Code[10];

    LOCAL PROCEDURE Intialize@2();
    BEGIN
      O365SalesInitialSetup.GET;
      PaymentTermsCode := O365SalesInitialSetup."Default Payment Terms Code";
      PaymentMethodCode := O365SalesInitialSetup."Default Payment Method Code";
    END;

    LOCAL PROCEDURE UpdateDefaultPaymentTerms@3();
    VAR
      Customer@1002 : Record Customer;
      SalesHeader@1001 : Record "Sales Header";
      ConfigTemplateManagement@1000 : Codeunit "Config. Template Management";
    BEGIN
      ConfigTemplateManagement.ReplaceDefaultValueForAllTemplates(
        DATABASE::Customer,Customer.FIELDNO("Payment Terms Code"),PaymentTermsCode);
      SalesHeader.SETRANGE("Payment Terms Code",O365SalesInitialSetup."Default Payment Terms Code");
      SalesHeader.MODIFYALL("Payment Terms Code",PaymentTermsCode,true);
      Customer.SETRANGE("Payment Terms Code",O365SalesInitialSetup."Default Payment Terms Code");
      Customer.MODIFYALL("Payment Terms Code",PaymentTermsCode,true);
      O365SalesInitialSetup."Default Payment Terms Code" := PaymentTermsCode;
      O365SalesInitialSetup.MODIFY(true);
    END;

    LOCAL PROCEDURE UpdateDefaultPaymentMethod@4();
    VAR
      Customer@1002 : Record Customer;
      SalesHeader@1001 : Record "Sales Header";
      ConfigTemplateManagement@1000 : Codeunit "Config. Template Management";
    BEGIN
      Customer.SETRANGE("Payment Method Code",O365SalesInitialSetup."Default Payment Method Code");
      Customer.MODIFYALL("Payment Method Code",PaymentMethodCode,true);
      SalesHeader.SETRANGE("Payment Method Code",O365SalesInitialSetup."Default Payment Method Code");
      SalesHeader.MODIFYALL("Payment Method Code",PaymentMethodCode,true);
      ConfigTemplateManagement.ReplaceDefaultValueForAllTemplates(
        DATABASE::Customer,Customer.FIELDNO("Payment Method Code"),PaymentMethodCode);
      O365SalesInitialSetup."Default Payment Method Code" := PaymentMethodCode;
      O365SalesInitialSetup.MODIFY(true);
    END;

    BEGIN
    END.
  }
}

