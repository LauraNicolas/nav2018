OBJECT Table 1878 VAT Assisted Setup Templates
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=VAT Assisted Setup Templates;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 2   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 3   ;   ;Default VAT Bus. Posting Grp;Code20;TableRelation="VAT Assisted Setup Bus. Grp.".Code WHERE (Selected=CONST(true));
                                                   CaptionML=ENU=Default VAT Bus. Posting Grp }
    { 4   ;   ;Default VAT Prod. Posting Grp;Code20;
                                                   TableRelation="VAT Setup Posting Groups"."VAT Prod. Posting Group" WHERE (Selected=CONST(true));
                                                   CaptionML=ENU=Default VAT Prod. Posting Grp }
    { 5   ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE PopulateRecFromTemplates@1();
    VAR
      ConfigTemplateHeader@1000 : Record "Config. Template Header";
      Customer@1003 : Record Customer;
      Item@1004 : Record Item;
      ConfigTemplateLine@1001 : Record "Config. Template Line";
    BEGIN
      DELETEALL;
      if ConfigTemplateHeader.FINDSET then
        repeat
          Code := ConfigTemplateHeader.Code;
          Description := ConfigTemplateHeader.Description;
          "Table ID" := ConfigTemplateHeader."Table ID";

          if
             (ConfigTemplateHeader."Table ID" = DATABASE::Customer) or
             (ConfigTemplateHeader."Table ID" = DATABASE::Vendor)
          then
            if ConfigTemplateLine.GetLine(ConfigTemplateLine,ConfigTemplateHeader.Code,Customer.FIELDNO("VAT Bus. Posting Group")) then
              "Default VAT Bus. Posting Grp" :=
                COPYSTR(ConfigTemplateLine."Default Value",1,MAXSTRLEN("Default VAT Bus. Posting Grp"));

          if ConfigTemplateHeader."Table ID" = DATABASE::Item then
            if ConfigTemplateLine.GetLine(ConfigTemplateLine,ConfigTemplateHeader.Code,Item.FIELDNO("VAT Prod. Posting Group")) then
              "Default VAT Prod. Posting Grp" :=
                COPYSTR(ConfigTemplateLine."Default Value",1,MAXSTRLEN("Default VAT Prod. Posting Grp"));

          INSERT;
        until ConfigTemplateHeader.NEXT = 0;
    END;

    PROCEDURE ValidateCustomerTemplate@2(VAR VATValidationError@1003 : Text) : Boolean;
    BEGIN
      exit(ValidateTemplates(DATABASE::Customer,VATValidationError));
    END;

    PROCEDURE ValidateVendorTemplate@5(VAR VATValidationError@1001 : Text) : Boolean;
    BEGIN
      exit(ValidateTemplates(DATABASE::Vendor,VATValidationError));
    END;

    PROCEDURE ValidateItemTemplate@7(VAR VATValidationError@1001 : Text) : Boolean;
    BEGIN
      exit(ValidateTemplates(DATABASE::Item,VATValidationError));
    END;

    LOCAL PROCEDURE ValidateTemplates@4(TableID@1000 : Integer;VAR VATValidationError@1002 : Text) : Boolean;
    VAR
      VATAssistedSetupBusGrp@1001 : Record "VAT Assisted Setup Bus. Grp.";
      VATSetupPostingGroups@1003 : Record "VAT Setup Posting Groups";
      VATAssistedSetupTemplates@1004 : Record "VAT Assisted Setup Templates";
    BEGIN
      VATAssistedSetupTemplates.SETRANGE("Table ID",TableID);
      VATAssistedSetupBusGrp.SETRANGE(Selected,true);
      VATSetupPostingGroups.SETRANGE(Selected,true);

      if VATAssistedSetupTemplates.FINDSET then
        repeat
          with VATAssistedSetupTemplates do begin
            if ("Default VAT Bus. Posting Grp" <> '') and
               (("Table ID" = DATABASE::Customer) or ("Table ID" = DATABASE::Vendor))
            then begin
              VATAssistedSetupBusGrp.SETRANGE(Code,"Default VAT Bus. Posting Grp");
              if not VATAssistedSetupBusGrp.FINDFIRST then begin
                VATValidationError := "Default VAT Bus. Posting Grp";
                exit(false);
              end;
            end;

            if ("Default VAT Prod. Posting Grp" <> '') and
               ("Table ID" = DATABASE::Item)
            then begin
              VATSetupPostingGroups.SETRANGE("VAT Prod. Posting Group","Default VAT Prod. Posting Grp");
              if not VATSetupPostingGroups.FINDFIRST then begin
                VATValidationError := "Default VAT Prod. Posting Grp";
                exit(false);
              end;
            end;
          end;
        until VATAssistedSetupTemplates.NEXT = 0;
      exit(true);
    END;

    BEGIN
    END.
  }
}

