OBJECT Codeunit 1294 OCR Service Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Bank Data Conv. Service Setup"=r;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      MissingCredentialsQst@1000 : TextConst '@@@="%1=error message. %2=OCR Service Setup";ENU=%1\ Do you want to open %2 to specify the missing values?';
      MissingCredentialsErr@1001 : TextConst '@@@="%1 = OCR Service Setup";ENU=You must fill the User Name, Password, and Authorization Key fields.';
      OCRServiceSetup@1003 : Record "OCR Service Setup";
      AuthCookie@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.Cookie";
      ConnectionSuccessMsg@1006 : TextConst 'ENU=Connection succeeded.';
      ConnectionFailedErr@1005 : TextConst 'ENU=The connection failed. Check that the User Name, Password, and Authorization Key fields are filled correctly.';
      NoFileContentErr@1004 : TextConst 'ENU=The file is empty.';
      InitiateUploadMsg@1010 : TextConst 'ENU=Initiate document upload.';
      GetDocumentConfirmMsg@1019 : TextConst 'ENU=Acknowledge document receipt.';
      DocumentDownloadedTxt@1018 : TextConst '@@@="%1 = Document Identifier (usually a guid)";ENU=Downloaded %1';
      UploadFileMsg@1013 : TextConst 'ENU=Send to OCR service.';
      AuthenticateMsg@1015 : TextConst 'ENU=Log in to OCR service.';
      GetNewDocumentsMsg@1016 : TextConst 'ENU=Get received OCR documents.';
      GetDocumentMsg@1017 : TextConst 'ENU=Receive OCR document.';
      UploadFileFailedMsg@1014 : TextConst '@@@="%1 = Response from OCR service, this will probably be an XML string";ENU=The document failed to upload. Service Error: %1';
      UploadTotalSuccessMsg@1011 : TextConst '@@@="%1 = Number of documents to be uploaded";ENU=Notify OCR service that %1 documents are ready for upload.';
      NewDocumentsTotalMsg@1020 : TextConst '@@@="%1 = Number of documents downloaded (e.g. 5)";ENU=Downloaded %1 documents';
      ImportSuccessMsg@1007 : TextConst 'ENU=The document was successfully received.';
      DocumentNotReadyMsg@1008 : TextConst 'ENU=The document cannot be received yet. Try again in a few moments.';
      NotUploadedErr@1009 : TextConst 'ENU=You must upload the image first.';
      NotValidDocIDErr@1110 : TextConst '@@@=%1 is the value.;ENU=Received document ID %1 contains invalid characters.';
      LoggingConstTxt@1012 : TextConst 'ENU=OCR Service';
      UploadSuccessMsg@1021 : TextConst 'ENU=The document was successfully sent to the OCR service.';
      NoOCRDataCorrectionMsg@1022 : TextConst 'ENU=You have made no OCR data corrections.';
      VerifyMsg@1023 : TextConst 'ENU=The document is awaiting your manual verification on the OCR service site.\\Choose the Awaiting Verification link in the OCR Status field.';
      FailedMsg@1024 : TextConst 'ENU=The document failed to be processed.';
      MethodGetTok@1038 : TextConst '@@@={Locked};ENU=GET';
      MethodPutTok@1037 : TextConst '@@@={Locked};ENU=PUT';
      MethodPostTok@1039 : TextConst '@@@={Locked};ENU=POST';
      MethodDeleteTok@1040 : TextConst '@@@={Locked};ENU=DELETE';

    [External]
    PROCEDURE SetURLsToDefaultRSO@2(VAR OCRServiceSetup@1000 : Record "OCR Service Setup");
    BEGIN
      OCRServiceSetup."Sign-up URL" := 'https://store.readsoftonline.com/nav';
      OCRServiceSetup."Service URL" := 'https://services.readsoftonline.com';
      OCRServiceSetup."Sign-in URL" := 'https://nav.readsoftonline.com';
    END;

    [External]
    PROCEDURE CheckCredentials@4();
    VAR
      OCRServiceSetup@1000 : Record "OCR Service Setup";
    BEGIN
      with OCRServiceSetup do begin
        if not HasCredentials(OCRServiceSetup) then
          if CONFIRM(STRSUBSTNO(GetCredentialsQstText),true) then begin
            COMMIT;
            PAGE.RUNMODAL(PAGE::"OCR Service Setup",OCRServiceSetup);
          end;

        if not HasCredentials(OCRServiceSetup) then
          ERROR(GetCredentialsErrText);
      end;
    END;

    LOCAL PROCEDURE HasCredentials@34(OCRServiceSetup@1000 : Record "OCR Service Setup") : Boolean;
    BEGIN
      with OCRServiceSetup do
        exit(
          GET and
          HasPassword("Password Key") and
          HasPassword("Authorization Key") and
          ("User Name" <> ''));
    END;

    [External]
    PROCEDURE GetCredentialsErrText@3() : Text;
    BEGIN
      exit(MissingCredentialsErr);
    END;

    [External]
    PROCEDURE GetCredentialsQstText@8() : Text;
    VAR
      OCRServiceSetup@1000 : Record "OCR Service Setup";
    BEGIN
      exit(STRSUBSTNO(MissingCredentialsQst,GetCredentialsErrText,OCRServiceSetup.TABLECAPTION));
    END;

    [Internal]
    PROCEDURE Authenticate@5() : Boolean;
    VAR
      ResponseString@1000 : Text;
    BEGIN
      if not TryAuthenticate(ResponseString) then
        exit(false);

      if STRPOS(ResponseString,'<Status>Success</Status>') < 1 then
        LogActivityFailed(OCRServiceSetup.RECORDID,AuthenticateMsg,ConnectionFailedErr)
      else
        LogActivitySucceeded(OCRServiceSetup.RECORDID,AuthenticateMsg,'');

      exit(true);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryAuthenticate@27(VAR ResponseString@1006 : Text);
    VAR
      TempBlob@1003 : Record TempBlob;
      HttpWebRequestMgt@1000 : Codeunit "Http Web Request Mgt.";
      HttpStatusCode@1004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
      InStr@1002 : InStream;
    BEGIN
      GetOcrServiceSetup(false);
      HttpWebRequestMgt.Initialize(STRSUBSTNO('%1/authentication/rest/authenticate',OCRServiceSetup."Service URL"));
      HttpWebRequestMgt.DisableUI;
      RsoAddHeaders(HttpWebRequestMgt);
      HttpWebRequestMgt.SetMethod(MethodPostTok);
      HttpWebRequestMgt.AddBodyAsText(
        STRSUBSTNO(
          '<AuthenticationCredentials><UserName>%1</UserName><Password>%2</Password>' +
          '<AuthenticationType>SetCookie</AuthenticationType></AuthenticationCredentials>',
          OCRServiceSetup."User Name",OCRServiceSetup.GetPassword(OCRServiceSetup."Password Key")));
      TempBlob.INIT;
      TempBlob.Blob.CREATEINSTREAM(InStr);
      HttpWebRequestMgt.GetResponse(InStr,HttpStatusCode,ResponseHeaders);
      InStr.READTEXT(ResponseString);
      HttpWebRequestMgt.GetCookie(AuthCookie);
    END;

    [Internal]
    PROCEDURE UpdateOrganizationInfo@16(VAR OCRServiceSetup@1001 : Record "OCR Service Setup");
    VAR
      XMLDOMManagement@1008 : Codeunit "XML DOM Management";
      XMLRootNode@1014 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLNode@1010 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ResponseStr@1000 : InStream;
    BEGIN
      if not RsoGetRequest('accounts/rest/currentcustomer',ResponseStr) then
        ERROR(GETLASTERRORTEXT);
      XMLDOMManagement.LoadXMLNodeFromInStream(ResponseStr,XMLRootNode);
      if XMLDOMManagement.FindNode(XMLRootNode,'Id',XMLNode) then
        OCRServiceSetup."Customer ID" := COPYSTR(XMLNode.InnerText,1,MAXSTRLEN(OCRServiceSetup."Customer ID"));
      if XMLDOMManagement.FindNode(XMLRootNode,'Name',XMLNode) then
        OCRServiceSetup."Customer Name" := COPYSTR(XMLNode.InnerText,1,MAXSTRLEN(OCRServiceSetup."Customer Name"));
      if XMLDOMManagement.FindNode(XMLRootNode,'ActivationStatus',XMLNode) then
        OCRServiceSetup."Customer Status" := COPYSTR(XMLNode.InnerText,1,MAXSTRLEN(OCRServiceSetup."Customer Status"));
      RsoGetRequest('users/rest/currentuser',ResponseStr);
      XMLDOMManagement.LoadXMLNodeFromInStream(ResponseStr,XMLRootNode);

      if XMLDOMManagement.FindNode(XMLRootNode,'OrganizationId',XMLNode) then
        OCRServiceSetup."Organization ID" := COPYSTR(XMLNode.InnerText,1,MAXSTRLEN(OCRServiceSetup."Organization ID"));
      OCRServiceSetup.MODIFY;
    END;

    [Internal]
    PROCEDURE UpdateOcrDocumentTemplates@9();
    VAR
      OCRServiceDocumentTemplate@1003 : Record "OCR Service Document Template";
      XMLDOMManagement@1002 : Codeunit "XML DOM Management";
      XMLRootNode@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLNode2@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ResponseStr@1005 : InStream;
    BEGIN
      GetOcrServiceSetup(false);
      OCRServiceSetup.TESTFIELD("Organization ID");

      RsoGetRequest(STRSUBSTNO('accounts/rest/customers/%1/userconfiguration',OCRServiceSetup."Organization ID"),ResponseStr);
      XMLDOMManagement.LoadXMLNodeFromInStream(ResponseStr,XMLRootNode);

      OCRServiceDocumentTemplate.LOCKTABLE;
      OCRServiceDocumentTemplate.DELETEALL;
      foreach XMLNode in XMLRootNode.SelectNodes('AvailableDocumentTypes/UserConfigurationDocumentType') do begin
        OCRServiceDocumentTemplate.INIT;
        XMLNode2 := XMLNode.SelectSingleNode('SystemName');
        OCRServiceDocumentTemplate.Code := COPYSTR(XMLNode2.InnerText,1,MAXSTRLEN(OCRServiceDocumentTemplate.Code));
        XMLNode2 := XMLNode.SelectSingleNode('Name');
        OCRServiceDocumentTemplate.Name := COPYSTR(XMLNode2.InnerText,1,MAXSTRLEN(OCRServiceDocumentTemplate.Name));
        OCRServiceDocumentTemplate.INSERT;
      end;
    END;

    [Internal]
    PROCEDURE RsoGetRequest@13(PathQuery@1015 : Text;VAR ResponseStr@1000 : InStream) : Boolean;
    BEGIN
      exit(RsoRequest(PathQuery,MethodGetTok,'',ResponseStr));
    END;

    [TryFunction]
    [Internal]
    PROCEDURE RsoGetRequestBinary@26(PathQuery@1015 : Text;VAR ResponseStr@1000 : InStream;VAR ContentType@1005 : Text);
    VAR
      HttpWebRequestMgt@1001 : Codeunit "Http Web Request Mgt.";
      HttpStatusCode@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
    BEGIN
      GetOcrServiceSetup(true);

      HttpWebRequestMgt.Initialize(STRSUBSTNO('%1/%2',OCRServiceSetup."Service URL",PathQuery));
      HttpWebRequestMgt.DisableUI;
      RsoAddCookie(HttpWebRequestMgt);
      RsoAddHeaders(HttpWebRequestMgt);
      HttpWebRequestMgt.SetMethod(MethodGetTok);
      HttpWebRequestMgt.CreateInstream(ResponseStr);
      HttpWebRequestMgt.GetResponse(ResponseStr,HttpStatusCode,ResponseHeaders);
      ContentType := ResponseHeaders.Item('Content-Type');
    END;

    [Internal]
    PROCEDURE RsoPutRequest@10(PathQuery@1015 : Text;Data@1001 : Text;VAR ResponseStr@1000 : InStream) : Boolean;
    BEGIN
      exit(RsoRequest(PathQuery,MethodPutTok,Data,ResponseStr));
    END;

    [Internal]
    PROCEDURE RsoPostRequest@41(PathQuery@1015 : Text;Data@1000 : Text;VAR ResponseStr@1001 : InStream) : Boolean;
    BEGIN
      exit(RsoRequest(PathQuery,MethodPostTok,Data,ResponseStr));
    END;

    [Internal]
    PROCEDURE RsoDeleteRequest@57(PathQuery@1015 : Text;Data@1000 : Text;VAR ResponseStr@1001 : InStream) : Boolean;
    BEGIN
      exit(RsoRequest(PathQuery,MethodDeleteTok,Data,ResponseStr));
    END;

    PROCEDURE RsoRequest@14(PathQuery@1015 : Text;RequestAction@1002 : Code[6];BodyText@1004 : Text;VAR ResponseStr@1003 : InStream) : Boolean;
    VAR
      HttpWebRequestMgt@1000 : Codeunit "Http Web Request Mgt.";
      HttpStatusCode@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1006 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
    BEGIN
      GetOcrServiceSetup(true);

      HttpWebRequestMgt.Initialize(STRSUBSTNO('%1/%2',OCRServiceSetup."Service URL",PathQuery));
      HttpWebRequestMgt.DisableUI;
      RsoAddCookie(HttpWebRequestMgt);
      RsoAddHeaders(HttpWebRequestMgt);
      HttpWebRequestMgt.SetMethod(RequestAction);
      if BodyText <> '' then
        HttpWebRequestMgt.AddBodyAsText(BodyText);
      HttpWebRequestMgt.CreateInstream(ResponseStr);
      exit(HttpWebRequestMgt.GetResponse(ResponseStr,HttpStatusCode,ResponseHeaders));
    END;

    LOCAL PROCEDURE RsoAddHeaders@6(VAR HttpWebRequestMgt@1000 : Codeunit "Http Web Request Mgt.");
    BEGIN
      HttpWebRequestMgt.AddHeader('x-rs-version','2011-10-14');
      HttpWebRequestMgt.AddHeader('x-rs-key',OCRServiceSetup.GetPassword(OCRServiceSetup."Authorization Key"));
      HttpWebRequestMgt.AddHeader('x-rs-culture','en-US');
      HttpWebRequestMgt.AddHeader('x-rs-uiculture','en-US');
    END;

    LOCAL PROCEDURE RsoAddCookie@11(VAR HttpWebRequestMgt@1000 : Codeunit "Http Web Request Mgt.");
    BEGIN
      if ISNULL(AuthCookie) then
        if not Authenticate then
          ERROR(GETLASTERRORTEXT);
      if AuthCookie.Expired then
        if not Authenticate then
          ERROR(GETLASTERRORTEXT);

      HttpWebRequestMgt.SetCookie(AuthCookie);
    END;

    LOCAL PROCEDURE URLEncode@19(InText@1000 : Text) : Text;
    VAR
      SystemWebHttpUtility@1001 : DotNet "'System.Web, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Web.HttpUtility";
    BEGIN
      SystemWebHttpUtility := SystemWebHttpUtility.HttpUtility;
      exit(SystemWebHttpUtility.UrlEncode(InText));
    END;

    [External]
    PROCEDURE DateConvertYYYYMMDD2XML@25(YYYYMMDD@1000 : Text) : Text;
    BEGIN
      if STRLEN(YYYYMMDD) <> 8 then
        exit(YYYYMMDD);
      exit(STRSUBSTNO('%1-%2-%3',COPYSTR(YYYYMMDD,1,4),COPYSTR(YYYYMMDD,5,2),COPYSTR(YYYYMMDD,7,2)));
    END;

    [External]
    PROCEDURE DateConvertXML2YYYYMMDD@37(XMLDate@1000 : Text) : Text;
    BEGIN
      exit(DELCHR(XMLDate,'=','-'))
    END;

    LOCAL PROCEDURE GetOcrServiceSetup@61(VerifyEnable@1001 : Boolean);
    BEGIN
      GetOcrServiceSetupExtended(OCRServiceSetup,VerifyEnable);
    END;

    PROCEDURE GetOcrServiceSetupExtended@1(VAR OCRServiceSetup@1001 : Record "OCR Service Setup";VerifyEnable@1000 : Boolean);
    BEGIN
      OCRServiceSetup.GET;
      if OCRServiceSetup."Service URL" <> '' then
        exit;
      if VerifyEnable then
        OCRServiceSetup.CheckEnabled;
      OCRServiceSetup.TESTFIELD("User Name");
      OCRServiceSetup.TESTFIELD("Service URL");
    END;

    [Internal]
    PROCEDURE StartUpload@18(NumberOfUploads@1000 : Integer) : Boolean;
    VAR
      ResponseStr@1001 : InStream;
      ResponseText@1002 : Text;
    BEGIN
      if NumberOfUploads < 1 then
        exit(false);

      // Initialize upload
      RsoGetRequest(STRSUBSTNO('files/rest/requestupload?targetCount=%1',NumberOfUploads),ResponseStr);
      ResponseStr.READTEXT(ResponseText);
      if ResponseText = '' then begin
        LogActivityFailed(OCRServiceSetup.RECORDID,InitiateUploadMsg,'');
        exit(false);
      end;

      LogActivitySucceeded(OCRServiceSetup.RECORDID,InitiateUploadMsg,STRSUBSTNO(UploadTotalSuccessMsg,NumberOfUploads));
      exit(true);
    END;

    [Internal]
    PROCEDURE UploadImage@17(VAR TempBlob@1008 : Record TempBlob;FileName@1010 : Text;ExternalReference@1014 : Text[50];Template@1002 : Code[20];LoggingRecordId@1003 : RecordID) : Boolean;
    VAR
      HttpRequestURL@1000 : Text;
      APIPart@1001 : Text;
    BEGIN
      GetOcrServiceSetup(true);
      APIPart := STRSUBSTNO(
          'files/rest/image2?filename=%1&customerid=&batchexternalid=%2&buyerid=&documenttype=%3&sortingmethod=OneDocumentPerFile',
          URLEncode(FileName),ExternalReference,Template);
      HttpRequestURL := STRSUBSTNO('%1/%2',OCRServiceSetup."Service URL",APIPart);
      exit(UploadFile(TempBlob,HttpRequestURL,'*/*','application/octet-stream',LoggingRecordId));
    END;

    [Internal]
    PROCEDURE UploadLearningDocument@36(VAR TempBlob@1008 : Record TempBlob;DocumentId@1014 : Text;LoggingRecordId@1002 : RecordID) : Boolean;
    VAR
      HttpRequestURL@1003 : Text;
      APIPart@1000 : Text;
    BEGIN
      GetOcrServiceSetup(true);
      APIPart := STRSUBSTNO('documents/rest/%1/learningdocument',DocumentId);
      HttpRequestURL := STRSUBSTNO('%1/%2',OCRServiceSetup."Service URL",APIPart);
      exit(UploadFile(TempBlob,HttpRequestURL,'','',LoggingRecordId));
    END;

    LOCAL PROCEDURE UploadFile@39(VAR TempBlob@1001 : Record TempBlob;HttpRequestURL@1002 : Text;HttpRequestReturnType@1003 : Text;HttpRequestContentType@1004 : Text;LoggingRecordId@1000 : RecordID) : Boolean;
    VAR
      HttpWebRequestMgt@1009 : Codeunit "Http Web Request Mgt.";
      OfficeMgt@1010 : Codeunit "Office Management";
      HttpStatusCode@1008 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1007 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
      ResponseStr@1006 : InStream;
      ResponseText@1005 : Text;
    BEGIN
      if not TempBlob.Blob.HASVALUE then
        LogActivityFailed(LoggingRecordId,UploadFileMsg,NoFileContentErr); // throws error

      GetOcrServiceSetup(true);

      HttpWebRequestMgt.Initialize(HttpRequestURL);
      HttpWebRequestMgt.SetTraceLogEnabled(false); // Activity Log will log for us
      HttpWebRequestMgt.DisableUI;
      RsoAddCookie(HttpWebRequestMgt);
      RsoAddHeaders(HttpWebRequestMgt);
      if HttpRequestReturnType <> '' then
        HttpWebRequestMgt.SetReturnType(HttpRequestReturnType);
      if HttpRequestContentType <> '' then
        HttpWebRequestMgt.SetContentType(HttpRequestContentType);
      HttpWebRequestMgt.SetMethod(MethodPostTok);
      HttpWebRequestMgt.AddBodyBlob(TempBlob);
      HttpWebRequestMgt.CreateInstream(ResponseStr);

      if not HttpWebRequestMgt.GetResponse(ResponseStr,HttpStatusCode,ResponseHeaders) then begin
        if HttpWebRequestMgt.ProcessFaultXMLResponse('','/ServiceError/Message','','') then;
        LogActivityFailed(LoggingRecordId,UploadFileMsg,'');
        exit(false); // in case error text is empty
      end;

      ResponseStr.READTEXT(ResponseText);

      if ResponseText = '<BoolValue xmlns:i="http://www.w3.org/2001/XMLSchema-instance"><Value>true</Value></BoolValue>' then begin
        LogActivitySucceeded(LoggingRecordId,UploadFileMsg,'');
        if GUIALLOWED and (not OfficeMgt.IsAvailable) then
          MESSAGE(UploadSuccessMsg);
        exit(true);
      end;

      LogActivityFailed(LoggingRecordId,UploadFileMsg,STRSUBSTNO(UploadFileFailedMsg,ResponseText));
    END;

    [Internal]
    PROCEDURE UploadAttachment@21(VAR TempBlob@1008 : Record TempBlob;FileName@1010 : Text;ExternalReference@1014 : Text[50];Template@1000 : Code[20];RelatedRecordId@1001 : RecordID) : Boolean;
    BEGIN
      if not TempBlob.Blob.HASVALUE then
        ERROR(NoFileContentErr);

      if not StartUpload(1) then
        exit(false);

      exit(UploadImage(TempBlob,FileName,ExternalReference,Template,RelatedRecordId));
    END;

    [Internal]
    PROCEDURE UploadCorrectedOCRFile@33(IncomingDocument@1001 : Record "Incoming Document") : Boolean;
    VAR
      TempBlob@1000 : Record TempBlob;
      DocumentId@1002 : Text;
    BEGIN
      if not IncomingDocument."OCR Data Corrected" then begin
        MESSAGE(NoOCRDataCorrectionMsg);
        exit;
      end;

      DocumentId := GetOCRServiceDocumentReference(IncomingDocument);
      CorrectOCRFile(IncomingDocument,TempBlob);
      if not TempBlob.Blob.HASVALUE then
        ERROR(NoFileContentErr);

      if not StartUpload(1) then
        exit(false);

      exit(UploadLearningDocument(TempBlob,DocumentId,IncomingDocument.RECORDID));
    END;

    [Internal]
    PROCEDURE CorrectOCRFile@38(IncomingDocument@1001 : Record "Incoming Document";VAR TempBlob@1000 : Record TempBlob);
    VAR
      OCRFileXMLRootNode@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OutStream@1003 : OutStream;
    BEGIN
      ValidateUpdatedOCRFields(IncomingDocument);

      GetOriginalOCRXMLRootNode(IncomingDocument,OCRFileXMLRootNode);

      with IncomingDocument do begin
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Vendor Name"));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Vendor Invoice No."));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Order No."));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Document Date"));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Due Date"));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Amount Excl. VAT"));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Amount Incl. VAT"));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("VAT Amount"));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Currency Code"));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Vendor VAT Registration No."));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Vendor IBAN"));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Vendor Bank Branch No."));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Vendor Bank Account No."));
        CorrectOCRFileNode(OCRFileXMLRootNode,IncomingDocument,FIELDNO("Vendor Phone No."));
      end;
      TempBlob.INIT;
      TempBlob.Blob.CREATEOUTSTREAM(OutStream);
      OCRFileXMLRootNode.OwnerDocument.Save(OutStream);
    END;

    [Internal]
    PROCEDURE CorrectOCRFileNode@32(VAR OCRFileXMLRootNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";IncomingDocument@1001 : Record "Incoming Document";FieldNo@1003 : Integer);
    VAR
      XMLDOMManagement@1002 : Codeunit "XML DOM Management";
      CorrectionXMLNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      EmptyCorrectionXMLNode@1010 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CorrectionXMLNodeParent@1009 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      PositionXMLNode@1008 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      IncomingDocumentRecRef@1005 : RecordRef;
      IncomingDocumentFieldRef@1006 : FieldRef;
      XPath@1014 : Text;
      CorrectionValue@1007 : Text;
      CorrectionNeeded@1011 : Boolean;
      CorrectionValueAsDecimal@1012 : Decimal;
      OriginalValueAsDecimal@1013 : Decimal;
    BEGIN
      IncomingDocumentRecRef.GETTABLE(IncomingDocument);
      XPath := IncomingDocument.GetDataExchangePath(FieldNo);
      if XPath = '' then
        exit;
      if XMLDOMManagement.FindNode(OCRFileXMLRootNode,XPath,CorrectionXMLNode) then begin
        IncomingDocumentFieldRef := IncomingDocumentRecRef.FIELD(FieldNo);

        case FORMAT(IncomingDocumentFieldRef.TYPE) of
          'Date':
            begin
              CorrectionValue := DateConvertXML2YYYYMMDD(FORMAT(IncomingDocumentFieldRef.VALUE,0,9));
              CorrectionNeeded := CorrectionXMLNode.InnerText <> CorrectionValue;
            end;
          'Decimal':
            begin
              CorrectionValueAsDecimal := IncomingDocumentFieldRef.VALUE;
              CorrectionValue := FORMAT(IncomingDocumentFieldRef.VALUE,0,9);
              if EVALUATE(OriginalValueAsDecimal,CorrectionXMLNode.InnerText,9) then;
              CorrectionNeeded := OriginalValueAsDecimal <> CorrectionValueAsDecimal;
            end;
          else begin
            CorrectionValue := FORMAT(IncomingDocumentFieldRef.VALUE,0,9);
            CorrectionNeeded := CorrectionXMLNode.InnerText <> CorrectionValue;
          end;
        end;

        if CorrectionNeeded then begin
          if XMLDOMManagement.FindNode(CorrectionXMLNode,'../Position',PositionXMLNode) then
            PositionXMLNode.InnerText := '0, 0, 0, 0';
          if CorrectionValue = '' then begin
            CorrectionXMLNodeParent := CorrectionXMLNode.ParentNode;
            EmptyCorrectionXMLNode := CorrectionXMLNodeParent.OwnerDocument.CreateElement(CorrectionXMLNode.Name);
            CorrectionXMLNodeParent.ReplaceChild(EmptyCorrectionXMLNode,CorrectionXMLNode);
          end else
            CorrectionXMLNode.InnerText := CorrectionValue
        end;
      end;
    END;

    [External]
    PROCEDURE ValidateUpdatedOCRFields@28(IncomingDocument@1000 : Record "Incoming Document");
    BEGIN
      IncomingDocument.TESTFIELD("Vendor Name");
    END;

    [Internal]
    PROCEDURE GetOriginalOCRXMLRootNode@49(IncomingDocument@1004 : Record "Incoming Document";VAR OriginalXMLRootNode@1007 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      IncomingDocumentAttachment@1000 : Record "Incoming Document Attachment";
      TempBlob@1001 : Record TempBlob;
      XMLDOMManagement@1003 : Codeunit "XML DOM Management";
      InStream@1002 : InStream;
      IncDocAttachmentRecRef@1005 : RecordRef;
      OriginalXMLContentFieldRef@1006 : FieldRef;
    BEGIN
      IncomingDocument.TESTFIELD(Posted,false);
      IncomingDocumentAttachment.SETRANGE("Incoming Document Entry No.",IncomingDocument."Entry No.");
      IncomingDocumentAttachment.SETRANGE("Generated from OCR",true);
      IncomingDocumentAttachment.SETRANGE(Default,true);
      if not IncomingDocumentAttachment.FINDFIRST then
        exit;
      IncDocAttachmentRecRef.GETTABLE(IncomingDocumentAttachment);
      OriginalXMLContentFieldRef := IncDocAttachmentRecRef.FIELD(IncomingDocumentAttachment.FIELDNO(Content));
      OriginalXMLContentFieldRef.CALCFIELD;

      TempBlob.INIT;
      TempBlob.Blob := OriginalXMLContentFieldRef.VALUE;
      TempBlob.Blob.CREATEINSTREAM(InStream);
      XMLDOMManagement.LoadXMLNodeFromInStream(InStream,OriginalXMLRootNode);
    END;

    [External]
    PROCEDURE GetOCRServiceDocumentReference@35(IncomingDocument@1004 : Record "Incoming Document") : Text[50];
    VAR
      IncomingDocumentAttachment@1000 : Record "Incoming Document Attachment";
    BEGIN
      IncomingDocumentAttachment.SETRANGE("Incoming Document Entry No.",IncomingDocument."Entry No.");
      IncomingDocumentAttachment.SETRANGE("Generated from OCR",true);
      if not IncomingDocumentAttachment.FINDFIRST then
        exit('');
      exit(IncomingDocumentAttachment."OCR Service Document Reference");
    END;

    [Internal]
    PROCEDURE GetDocumentList@15(VAR ResponseStr@1001 : InStream) : Boolean;
    BEGIN
      exit(RsoGetRequest('currentuser/documents?pageIndex=0&pageSize=1000',ResponseStr));
    END;

    [Internal]
    PROCEDURE GetDocuments@12(ExternalBatchFilter@1002 : Text) : Integer;
    VAR
      TypeHelper@1005 : Codeunit "Type Helper";
      XMLDOMManagement@1008 : Codeunit "XML DOM Management";
      XMLRootNode@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ChildNode@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ResponseStr@1009 : InStream;
      ExternalBatchId@1004 : Text[50];
      DocId@1007 : Text[50];
      DocCount@1003 : Integer;
    BEGIN
      GetOcrServiceSetup(true);

      RsoGetRequest(STRSUBSTNO('documents/rest/customers/%1/outputdocuments',OCRServiceSetup."Customer ID"),ResponseStr);

      XMLDOMManagement.LoadXMLNodeFromInStream(ResponseStr,XMLRootNode);

      foreach XMLNode in XMLRootNode.ChildNodes do begin
        ChildNode := XMLNode.SelectSingleNode('BatchExternalId');
        ExternalBatchId := ChildNode.InnerText;
        if (ExternalBatchFilter = '') or (ExternalBatchFilter = ExternalBatchId) then
          foreach ChildNode in XMLNode.SelectNodes('DocumentId') do begin
            DocId := ChildNode.InnerText;

            if not TypeHelper.IsMatch(DocId,'^[a-zA-Z0-9\-\{\}]*$') then
              ERROR(NotValidDocIDErr,DocId);

            DocCount += DownloadDocument(ExternalBatchId,DocId);

            if DocCount > GetMaxDocDownloadCount then begin
              LogActivitySucceeded(OCRServiceSetup.RECORDID,GetNewDocumentsMsg,STRSUBSTNO(NewDocumentsTotalMsg,DocCount));
              exit(DocCount);
            end;
          end;
      end;

      LogActivitySucceeded(OCRServiceSetup.RECORDID,GetNewDocumentsMsg,STRSUBSTNO(NewDocumentsTotalMsg,DocCount));

      if (ExternalBatchFilter <> '') and (DocCount > 0) then
        exit(DocCount);

      if ExternalBatchFilter <> '' then
        GetDocumentStatus(ExternalBatchFilter)
      else
        GetDocumentsExcludeProcessed;

      exit(DocCount);
    END;

    [Internal]
    PROCEDURE GetDocumentsExcludeProcessed@23();
    VAR
      IncomingDocument@1009 : Record "Incoming Document";
      IncomingDocumentAttachment@1011 : Record "Incoming Document Attachment";
      TempIncomingDocumentAttachment@1010 : TEMPORARY Record "Incoming Document Attachment";
    BEGIN
      GetOcrServiceSetup(true);

      IncomingDocument.SETRANGE("OCR Status",IncomingDocument."OCR Status"::Sent);
      if not IncomingDocument.FINDSET then
        exit;

      repeat
        IncomingDocumentAttachment.SETRANGE("Incoming Document Entry No.",IncomingDocument."Entry No.");
        IncomingDocumentAttachment.SETRANGE(Default,true);
        IncomingDocumentAttachment.FINDFIRST;
        TempIncomingDocumentAttachment := IncomingDocumentAttachment;
        TempIncomingDocumentAttachment.INSERT;
      until IncomingDocument.NEXT = 0;

      GetBatches(TempIncomingDocumentAttachment,'');
    END;

    [Internal]
    PROCEDURE GetDocumentStatus@43(ExternalBatchFilter@1002 : Text);
    VAR
      IncomingDocument@1009 : Record "Incoming Document";
      IncomingDocumentAttachment@1011 : Record "Incoming Document Attachment";
      TempIncomingDocumentAttachment@1010 : TEMPORARY Record "Incoming Document Attachment";
    BEGIN
      GetOcrServiceSetup(true);

      IncomingDocumentAttachment.SETRANGE("External Document Reference",ExternalBatchFilter);
      IncomingDocumentAttachment.SETRANGE(Default,true);
      IncomingDocumentAttachment.FINDFIRST;

      IncomingDocument.GET(IncomingDocumentAttachment."Incoming Document Entry No.");
      if IncomingDocument."OCR Status" <> IncomingDocument."OCR Status"::Sent then
        exit;

      TempIncomingDocumentAttachment := IncomingDocumentAttachment;
      TempIncomingDocumentAttachment.INSERT;

      GetBatches(TempIncomingDocumentAttachment,ExternalBatchFilter);
    END;

    [Internal]
    PROCEDURE GetDocumentForAttachment@7(VAR IncomingDocumentAttachment@1003 : Record "Incoming Document Attachment") : Integer;
    VAR
      IncomingDocument@1000 : Record "Incoming Document";
      Status@1001 : Integer;
    BEGIN
      if IncomingDocumentAttachment."External Document Reference" = '' then
        ERROR(NotUploadedErr);

      if GetDocuments(IncomingDocumentAttachment."External Document Reference") > 0 then
        Status := IncomingDocument."OCR Status"::Success
      else begin
        IncomingDocument.GET(IncomingDocumentAttachment."Incoming Document Entry No.");
        Status := IncomingDocument."OCR Status";
      end;

      case Status of
        IncomingDocument."OCR Status"::Success:
          MESSAGE(ImportSuccessMsg);
        IncomingDocument."OCR Status"::"Awaiting Verification":
          MESSAGE(VerifyMsg);
        IncomingDocument."OCR Status"::Error:
          MESSAGE(FailedMsg);
        IncomingDocument."OCR Status"::Sent: // Pending Result
          MESSAGE(DocumentNotReadyMsg);
        else
          MESSAGE(DocumentNotReadyMsg);
      end;

      exit(Status);
    END;

    LOCAL PROCEDURE GetBatchDocuments@42(VAR XMLRootNode@1009 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";BatchFilter@1001 : Text);
    VAR
      XMLDOMManagement@1007 : Codeunit "XML DOM Management";
      ResponseStr@1005 : InStream;
      Path@1000 : Text;
      PageSize@1004 : Integer;
      CurrentPage@1003 : Integer;
    BEGIN
      PageSize := 200;
      CurrentPage := 0;
      Path := STRSUBSTNO(
          'documents/rest/customers/%1/batches/%2/documents?pageIndex=%3&pageSize=%4',OCRServiceSetup."Customer ID",
          BatchFilter,CurrentPage,PageSize);
      RsoGetRequest(Path,ResponseStr);
      XMLDOMManagement.LoadXMLNodeFromInStream(ResponseStr,XMLRootNode);
    END;

    LOCAL PROCEDURE GetBatchesApi@79(VAR XMLRootNode@1009 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";ExternalBatchFilter@1001 : Text);
    VAR
      XMLDOMManagement@1007 : Codeunit "XML DOM Management";
      ResponseStr@1005 : InStream;
      Path@1000 : Text;
      PageSize@1004 : Integer;
      CurrentPage@1003 : Integer;
    BEGIN
      PageSize := 200;
      CurrentPage := 0;
      if ExternalBatchFilter <> '' then
        Path := STRSUBSTNO(
            'documents/rest/customers/%1/batches?pageIndex=%2&pageSize=%3&externalId=%4',OCRServiceSetup."Customer ID",
            CurrentPage,PageSize,ExternalBatchFilter)
      else
        Path := STRSUBSTNO(
            'documents/rest/customers/%1/batches?pageIndex=%2&pageSize=%3&excludeProcessed=1',OCRServiceSetup."Customer ID",
            CurrentPage,PageSize);

      RsoGetRequest(Path,ResponseStr);
      XMLDOMManagement.LoadXMLNodeFromInStream(ResponseStr,XMLRootNode);
    END;

    LOCAL PROCEDURE GetBatches@48(VAR TempIncomingDocumentAttachment@1011 : TEMPORARY Record "Incoming Document Attachment";ExternalBatchFilter@1001 : Text);
    VAR
      IncomingDocument@1010 : Record "Incoming Document";
      XMLDOMManagement@1007 : Codeunit "XML DOM Management";
      SendIncomingDocumentToOCR@1006 : Codeunit "Send Incoming Document to OCR";
      XMLRootNode@1005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CurrentPage@1003 : Integer;
      TotalPages@1002 : Integer;
    BEGIN
      repeat
        GetBatchesApi(XMLRootNode,ExternalBatchFilter);
        EVALUATE(TotalPages,XMLDOMManagement.FindNodeText(XMLRootNode,'//PageCount'));

        XMLDOMManagement.FindNode(XMLRootNode,'//Batches',XMLRootNode);
        FindDocumentFromList(XMLRootNode,TempIncomingDocumentAttachment);

        CurrentPage += 1;
      until (TempIncomingDocumentAttachment.COUNT = 0) or (CurrentPage > TotalPages);

      if TempIncomingDocumentAttachment.FINDSET then
        repeat
          IncomingDocument.GET(TempIncomingDocumentAttachment."Incoming Document Entry No.");
          SendIncomingDocumentToOCR.SetStatusToFailed(IncomingDocument);
        until TempIncomingDocumentAttachment.NEXT = 0;
    END;

    [Internal]
    PROCEDURE GetDocumentId@50(ExternalBatchFilter@1000 : Text) : Text;
    VAR
      XMLDOMManagement@1003 : Codeunit "XML DOM Management";
      XMLRootNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      BatchID@1005 : Text;
      DocumentID@1006 : Text;
    BEGIN
      GetOcrServiceSetup(true);

      GetBatchesApi(XMLRootNode,ExternalBatchFilter);
      BatchID := XMLDOMManagement.FindNodeText(XMLRootNode,'/PagedBatches/Batches/Batch/Id');

      GetBatchDocuments(XMLRootNode,BatchID);
      DocumentID := XMLDOMManagement.FindNodeText(XMLRootNode,'/PagedDocuments/Documents/Document/Id');

      exit(DocumentID);
    END;

    LOCAL PROCEDURE DownloadDocument@22(ExternalBatchId@1003 : Text[50];DocId@1005 : Text[50]) : Integer;
    VAR
      IncomingDocument@1000 : Record "Incoming Document";
      IncomingDocumentAttachment@1009 : Record "Incoming Document Attachment";
      XMLDOMManagement@1006 : Codeunit "XML DOM Management";
      SendIncomingDocumentToOCR@1007 : Codeunit "Send Incoming Document to OCR";
      ImageInStr@1011 : InStream;
      ResponseStr@1010 : InStream;
      XMLRootNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      AttachmentName@1001 : Text[250];
      ContentType@1008 : Text[50];
    BEGIN
      RsoGetRequest(STRSUBSTNO('documents/rest/%1',DocId),ResponseStr);
      if not XMLDOMManagement.LoadXMLNodeFromInStream(ResponseStr,XMLRootNode) then begin
        LogActivityFailed(OCRServiceSetup.RECORDID,GetDocumentMsg,'');
        exit(0);
      end;

      if ExternalBatchId <> '' then
        IncomingDocumentAttachment.SETRANGE("External Document Reference",ExternalBatchId);
      if (ExternalBatchId <> '') and IncomingDocumentAttachment.FINDFIRST then begin
        IncomingDocument.GET(IncomingDocumentAttachment."Incoming Document Entry No.");
        AttachmentName := IncomingDocumentAttachment.Name;
      end else begin  // New Incoming Document
        AttachmentName := COPYSTR(XMLDOMManagement.FindNodeText(XMLRootNode,'OriginalFilename'),1,MAXSTRLEN(AttachmentName));
        IncomingDocument.INIT;
        IncomingDocument.CreateIncomingDocument(AttachmentName,'');
        IncomingDocumentAttachment.SETRANGE("External Document Reference");
        if RsoGetRequestBinary(STRSUBSTNO('documents/rest/file/%1/image',DocId),ImageInStr,ContentType) then
          IncomingDocument.AddAttachmentFromStream(
            IncomingDocumentAttachment,AttachmentName,GetExtensionFromContentType(AttachmentName,ContentType),ImageInStr);
      end;
      IncomingDocument.CheckNotCreated;
      IncomingDocumentAttachment.SETRANGE("External Document Reference");
      IncomingDocument.AddAttachmentFromStream(IncomingDocumentAttachment,AttachmentName,'xml',ResponseStr);
      IncomingDocumentAttachment."Generated from OCR" := true;
      IncomingDocumentAttachment."OCR Service Document Reference" := DocId;
      IncomingDocumentAttachment.VALIDATE(Default,true);
      IncomingDocumentAttachment.MODIFY;

      IncomingDocument.GET(IncomingDocument."Entry No.");
      SendIncomingDocumentToOCR.SetStatusToReceived(IncomingDocument);

      UpdateIncomingDocWithOCRData(IncomingDocument,XMLRootNode);
      LogActivitySucceeded(IncomingDocument.RECORDID,GetDocumentMsg,STRSUBSTNO(DocumentDownloadedTxt,DocId));

      RsoPutRequest(
        STRSUBSTNO('documents/rest/%1/downloaded',DocId),
        '<UploadDataCollection xmlns:i="http://www.w3.org/2001/XMLSchema-instance" />',ResponseStr);
      LogActivitySucceeded(IncomingDocument.RECORDID,GetDocumentConfirmMsg,STRSUBSTNO(DocumentDownloadedTxt,DocId));
      exit(1);
    END;

    [Internal]
    PROCEDURE UpdateIncomingDocWithOCRData@24(VAR IncomingDocument@1000 : Record "Incoming Document";VAR XMLRootNode@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      Vendor@1006 : Record Vendor;
      IncomingDocumentAttachment@1003 : Record "Incoming Document Attachment";
      XMLDOMManagement@1004 : Codeunit "XML DOM Management";
    BEGIN
      with IncomingDocument do begin
        if "Data Exchange Type" = '' then
          exit;

        IncomingDocumentAttachment.SETRANGE("Incoming Document Entry No.","Entry No.");
        IncomingDocumentAttachment.SETRANGE("Generated from OCR",true);
        IncomingDocumentAttachment.SETRANGE(Default,true);
        if not IncomingDocumentAttachment.FINDFIRST then
          exit;
        IncomingDocumentAttachment.ExtractHeaderFields(XMLRootNode,IncomingDocument);
        GET("Entry No.");

        if XMLDOMManagement.FindNodeText(XMLRootNode,'HeaderFields/HeaderField/Text[../Type/text() = "creditinvoice"]') =
           'true'
        then
          "Document Type" := "Document Type"::"Purchase Credit Memo";

        if not ISNULLGUID("Vendor Id") then begin
          Vendor.SETRANGE(Id,"Vendor Id");
          if Vendor.FINDFIRST then
            VALIDATE("Vendor No.",Vendor."No.");
        end else
          if "Vendor VAT Registration No." <> '' then begin
            Vendor.SETRANGE("VAT Registration No.","Vendor VAT Registration No.");
            if Vendor.FINDFIRST then
              VALIDATE("Vendor No.",Vendor."No.");
          end;

        MODIFY;
      end;
    END;

    PROCEDURE LogActivitySucceeded@31(RelatedRecordID@1001 : RecordID;ActivityDescription@1002 : Text;ActivityMessage@1003 : Text);
    VAR
      ActivityLog@1000 : Record "Activity Log";
    BEGIN
      ActivityLog.LogActivity(RelatedRecordID,ActivityLog.Status::Success,LoggingConstTxt,
        ActivityDescription,ActivityMessage);
    END;

    PROCEDURE LogActivityFailed@29(RelatedRecordID@1001 : RecordID;ActivityDescription@1002 : Text;ActivityMessage@1003 : Text);
    VAR
      ActivityLog@1000 : Record "Activity Log";
    BEGIN
      ActivityMessage := GETLASTERRORTEXT + ' ' + ActivityMessage;
      CLEARLASTERROR;

      ActivityLog.LogActivity(RelatedRecordID,ActivityLog.Status::Failed,LoggingConstTxt,
        ActivityDescription,ActivityMessage);

      COMMIT;

      if DELCHR(ActivityMessage,'<>',' ') <> '' then
        ERROR(ActivityMessage);
    END;

    [EventSubscriber(Table,1400,OnRegisterServiceConnection)]
    [External]
    PROCEDURE HandleOCRRegisterServiceConnection@20(VAR ServiceConnection@1003 : Record "Service Connection");
    VAR
      OCRServiceSetup@1001 : Record "OCR Service Setup";
      RecRef@1002 : RecordRef;
    BEGIN
      if not OCRServiceSetup.GET then begin
        OCRServiceSetup.INIT;
        OCRServiceSetup.INSERT(true);
      end;
      RecRef.GETTABLE(OCRServiceSetup);

      if OCRServiceSetup.Enabled then
        ServiceConnection.Status := ServiceConnection.Status::Enabled
      else
        ServiceConnection.Status := ServiceConnection.Status::Disabled;
      with OCRServiceSetup do
        ServiceConnection.InsertServiceConnection(
          ServiceConnection,RecRef.RECORDID,TABLENAME,"Service URL",PAGE::"OCR Service Setup");
    END;

    LOCAL PROCEDURE GetMaxDocDownloadCount@30() : Integer;
    BEGIN
      exit(1000);
    END;

    LOCAL PROCEDURE GetDocumentSimplifiedStatus@45(ObjectStatus@1000 : Integer) : Integer;
    VAR
      IncomingDocument@1001 : Record "Incoming Document";
    BEGIN
      // Status definitions can be found at http://docs.readsoftonline.com/help/eng/partner/#reference/batch-statuses.htm%3FTocPath%3DReference%7C_____6
      // Status codes can be found at https://services.readsoftonline.com/documentation/rest?s=-940536891&m=173766930
      case ObjectStatus of
        0: // 'BATCHCREATED'
          exit(IncomingDocument."OCR Status"::Sent);
        1: // 'BATCHINPUTVALIDATIONFAILED'
          exit(IncomingDocument."OCR Status"::Error);
        3: // 'BATCHPENDINGPROCESSSTART'
          exit(IncomingDocument."OCR Status"::Sent);
        7: // 'BATCHCLASSIFICATIONINPROGRESS'
          exit(IncomingDocument."OCR Status"::Sent);
        10: // 'BATCHPENDINGCORRECTION'
          exit(IncomingDocument."OCR Status"::"Awaiting Verification");
        15: // 'BATCHEXTRACTIONINPROGRESS'
          exit(IncomingDocument."OCR Status"::Sent);
        20: // 'BATCHMANUALVERIFICATION'
          exit(IncomingDocument."OCR Status"::"Awaiting Verification");
        23: // 'BATCHREQUESTINFORMATION'
          exit(IncomingDocument."OCR Status"::"Awaiting Verification");
        25: // 'BATCHAPPROVALINPROGRESS'
          exit(IncomingDocument."OCR Status"::Sent);
        26: // 'BATCHPENDINGREGISTRATION'
          exit(IncomingDocument."OCR Status"::Sent);
        27: // 'BATCHREGISTRATIONINPROGRESS'
          exit(IncomingDocument."OCR Status"::Sent);
        28: // 'BATCHPENDINGPOST'
          exit(IncomingDocument."OCR Status"::Sent);
        29: // 'BATCHPOSTINPROGRESS'
          exit(IncomingDocument."OCR Status"::Sent);
        30: // 'BATCHPENDINGEXPORT'
          exit(IncomingDocument."OCR Status"::Sent);
        33: // 'BATCHEXPORTINPROGRESS'
          exit(IncomingDocument."OCR Status"::Success);
        35: // 'BATCHEXPORTFAILED'
          exit(IncomingDocument."OCR Status"::Error);
        40: // 'BATCHSUCCESSFULLYPROCESSED'
          exit(IncomingDocument."OCR Status"::Sent);
        50: // 'BATCHREJECTED'
          exit(IncomingDocument."OCR Status"::Error);
        100: // 'BATCHDELETED'
          exit(IncomingDocument."OCR Status"::Error);
        200: // 'BATCHPREPROCESSINGINPROGRESS'
          exit(IncomingDocument."OCR Status"::Sent);
        13: // 'BATCHMANUALSEPERATION'
          exit(IncomingDocument."OCR Status"::"Awaiting Verification");
        14: // 'BATCHSEPERATIONINPROGRESS'
          exit(IncomingDocument."OCR Status"::Sent);
        95: // 'BATCHDELETEINPROGRESS'
          exit(IncomingDocument."OCR Status"::Error);
        else
          exit(IncomingDocument."OCR Status"::" ");
      end;
    END;

    LOCAL PROCEDURE FindDocumentFromList@51(VAR XMLRootNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR TempIncomingDocumentAttachment@1001 : TEMPORARY Record "Incoming Document Attachment");
    VAR
      IncomingDocument@1009 : Record "Incoming Document";
      XMLDOMManagement@1008 : Codeunit "XML DOM Management";
      SendIncomingDocumentToOCR@1006 : Codeunit "Send Incoming Document to OCR";
      XMLNode@1005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      DocId@1002 : Text;
      DocStatus@1007 : Integer;
      StatusAsInt@1004 : Integer;
    BEGIN
      foreach XMLNode in XMLRootNode.ChildNodes do begin
        if TempIncomingDocumentAttachment.ISEMPTY then
          exit;

        DocId := XMLDOMManagement.FindNodeText(XMLNode,'./ExternalId');
        TempIncomingDocumentAttachment.SETRANGE("External Document Reference",DocId);
        if TempIncomingDocumentAttachment.FINDSET then
          repeat
            EVALUATE(StatusAsInt,XMLDOMManagement.FindNodeText(XMLNode,'./StatusAsInt'));
            DocStatus := GetDocumentSimplifiedStatus(StatusAsInt);
            IncomingDocument.GET(TempIncomingDocumentAttachment."Incoming Document Entry No.");
            case DocStatus of
              IncomingDocument."OCR Status"::Error:
                SendIncomingDocumentToOCR.SetStatusToFailed(IncomingDocument);
              IncomingDocument."OCR Status"::"Awaiting Verification":
                SendIncomingDocumentToOCR.SetStatusToVerify(IncomingDocument);
            end;

            TempIncomingDocumentAttachment.DELETE;
          until TempIncomingDocumentAttachment.NEXT = 0;

        // Remove filter
        TempIncomingDocumentAttachment.SETRANGE("External Document Reference");
        if TempIncomingDocumentAttachment.FINDSET then;
      end;
    END;

    [Internal]
    PROCEDURE TestConnection@46(VAR OCRServiceSetup@1000 : Record "OCR Service Setup");
    BEGIN
      if SetupConnection(OCRServiceSetup) then
        MESSAGE(ConnectionSuccessMsg);
    END;

    [Internal]
    PROCEDURE SetupConnection@40(VAR OCRServiceSetup@1000 : Record "OCR Service Setup") : Boolean;
    BEGIN
      if not HasCredentials(OCRServiceSetup) then
        ERROR(GetCredentialsErrText);
      if not Authenticate then
        ERROR(ConnectionFailedErr);
      UpdateOrganizationInfo(OCRServiceSetup);
      UpdateOcrDocumentTemplates;
      exit(true);
    END;

    [EventSubscriber(Page,189,OnCloseIncomingDocumentFromAction)]
    LOCAL PROCEDURE OnCloseIncomingDocumentHandler@44(VAR IncomingDocument@1001 : Record "Incoming Document");
    BEGIN
      PAGE.RUN(PAGE::"Incoming Document",IncomingDocument);
    END;

    [EventSubscriber(Page,190,OnCloseIncomingDocumentsFromActions)]
    LOCAL PROCEDURE OnCloseIncomingDocumentsHandler@47(VAR IncomingDocument@1001 : Record "Incoming Document");
    BEGIN
      PAGE.RUN(PAGE::"Incoming Documents",IncomingDocument);
    END;

    [External]
    PROCEDURE OcrServiceIsEnable@52() : Boolean;
    BEGIN
      if not OCRServiceSetup.GET then
        exit(false);

      if
         (OCRServiceSetup."Service URL" = '') or
         (OCRServiceSetup.Enabled = false)
      then
        exit(false);

      exit(true);
    END;

    [Internal]
    PROCEDURE GetStatusHyperLink@83(IncomingDocument@1000 : Record "Incoming Document") : Text;
    VAR
      IncomingDocumentAttachment@1001 : Record "Incoming Document Attachment";
      DocumentID@1002 : Text;
    BEGIN
      if IncomingDocument."OCR Status" = IncomingDocument."OCR Status"::"Awaiting Verification" then begin
        IncomingDocument.GetMainAttachment(IncomingDocumentAttachment);
        if IncomingDocumentAttachment."External Document Reference" = '' then
          exit('');

        DocumentID := GetDocumentId(IncomingDocumentAttachment."External Document Reference");
        exit(STRSUBSTNO('%1/documents/%2',OCRServiceSetup."Sign-in URL",DocumentID));
      end;

      if OCRServiceSetup.Enabled and (OCRServiceSetup."Sign-in URL" <> '') then
        exit(OCRServiceSetup."Sign-in URL");
    END;

    LOCAL PROCEDURE GetExtensionFromContentType@66(AttachmentName@1001 : Text;ContentType@1000 : Text) : Text;
    VAR
      FileManagement@1002 : Codeunit "File Management";
    BEGIN
      if STRPOS(ContentType,'application/pdf') <> 0 then
        exit('pdf');
      exit(FileManagement.GetExtension(AttachmentName));
    END;

    BEGIN
    END.
  }
}

