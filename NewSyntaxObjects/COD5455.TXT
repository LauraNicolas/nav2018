OBJECT Codeunit 5455 Graph Data Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            OnCreateIntegrationMappings;
          END;

  }
  CODE
  {

    [External]
    PROCEDURE AddIntgrationFieldMapping@10(MappingName@1000 : Text[20];NavFieldNo@1003 : Integer;IntegrationFieldNo@1001 : Integer;ValidateField@1004 : Boolean);
    VAR
      IntegrationFieldMapping@1002 : Record "Integration Field Mapping";
    BEGIN
      with IntegrationFieldMapping do begin
        INIT;
        "Integration Table Mapping Name" := MappingName;
        "Field No." := NavFieldNo;
        "Integration Table Field No." := IntegrationFieldNo;
        Direction := Direction::Bidirectional;
        "Validate Field" := ValidateField;
        INSERT(true);
      end;
    END;

    [External]
    PROCEDURE AddIntegrationTableMapping@6(MappingName@1002 : Code[20];TableID@1003 : Integer;IntTableID@1004 : Integer;IntTableUIDFldNo@1005 : Integer;IntTableModFldNo@1006 : Integer;ParentName@1007 : Text[20];IntTableDeltaTokenFldNo@1000 : Integer;IntTableChangeKeyFldNo@1008 : Integer;IntTableStateFldNo@1009 : Integer);
    VAR
      IntegrationTableMapping@1001 : Record "Integration Table Mapping";
    BEGIN
      with IntegrationTableMapping do begin
        INIT;
        Name := MappingName;
        "Table ID" := TableID;
        "Integration Table ID" := IntTableID;
        "Integration Table UID Fld. No." := IntTableUIDFldNo;
        "Int. Tbl. Modified On Fld. No." := IntTableModFldNo;
        "Synch. Codeunit ID" := CODEUNIT::"Graph Integration Table Sync";
        Direction := Direction::Bidirectional;
        "Synch. Only Coupled Records" := false;
        "Parent Name" := ParentName;
        "Int. Tbl. Delta Token Fld. No." := IntTableDeltaTokenFldNo;
        "Int. Tbl. ChangeKey Fld. No." := IntTableChangeKeyFldNo;
        "Int. Tbl. State Fld. No." := IntTableStateFldNo;
        if not INSERT(true) then
          MODIFY(true);
      end;
    END;

    [External]
    PROCEDURE ClearIntegrationMapping@4(MappingCode@1002 : Code[20]);
    VAR
      IntegrationTableMapping@1000 : Record "Integration Table Mapping";
    BEGIN
      IntegrationTableMapping.SETRANGE(Name,MappingCode);
      IntegrationTableMapping.DELETEALL(true);
    END;

    [External]
    PROCEDURE CreateIntegrationMapping@21(MappingCode@1000 : Code[20]);
    BEGIN
      if IntegrationMappingExists(MappingCode) then
        ClearIntegrationMapping(MappingCode);
      AddIntegrationMapping(MappingCode);
    END;

    [External]
    PROCEDURE GetGraphRecord@16(VAR GraphRecordRef@1000 : RecordRef;DestinationGraphID@1001 : Text[250];TableID@1002 : Integer) Found : Boolean;
    BEGIN
      OnGetGraphRecord(GraphRecordRef,DestinationGraphID,TableID,Found);
    END;

    [External]
    PROCEDURE GetInboundTableID@17(MappingCode@1000 : Code[20]) TableID : Integer;
    BEGIN
      OnGetInboundTableID(MappingCode,TableID);
    END;

    [External]
    PROCEDURE GetIntegrationTableMapping@12(VAR IntegrationTableMapping@1000 : Record "Integration Table Mapping";MappingCode@1007 : Code[20]);
    VAR
      GraphDataSetup@1001 : Codeunit "Graph Data Setup";
      IntegrationManagement@1002 : Codeunit "Integration Management";
      InsertEnabled@1006 : Boolean;
      ModifyEnabled@1005 : Boolean;
      DeleteEnabled@1004 : Boolean;
      RenameEnabled@1003 : Boolean;
    BEGIN
      if not IntegrationTableMapping.GET(MappingCode) then begin
        GraphDataSetup.CreateIntegrationMapping(MappingCode);
        IntegrationTableMapping.GET(MappingCode)
      end;

      // This code is needed to make sure the integration for the table is set
      IntegrationManagement.GetDatabaseTableTriggerSetup(
        IntegrationTableMapping."Table ID",InsertEnabled,ModifyEnabled,DeleteEnabled,RenameEnabled);
    END;

    [External]
    PROCEDURE GetMappingCodeForTable@18(TableID@1000 : Integer) MappingCode : Code[20];
    BEGIN
      OnGetMappingCodeForTable(TableID,MappingCode);
    END;

    [External]
    PROCEDURE IntegrationMappingExists@2(MappingCode@1002 : Code[20]) : Boolean;
    VAR
      IntegrationTableMapping@1000 : Record "Integration Table Mapping";
      IntegrationFieldMapping@1001 : Record "Integration Field Mapping";
    BEGIN
      if not IntegrationTableMapping.GET(MappingCode) then
        exit(false);

      IntegrationFieldMapping.SETRANGE("Integration Table Mapping Name",MappingCode);
      exit(not IntegrationFieldMapping.ISEMPTY);
    END;

    LOCAL PROCEDURE AddIntegrationMapping@3(MappingCode@1000 : Code[20]);
    VAR
      TableID@1001 : Integer;
    BEGIN
      OnGetInboundTableID(MappingCode,TableID);
      OnAddIntegrationMapping(MappingCode);
    END;

    [Integration]
    LOCAL PROCEDURE OnAddIntegrationMapping@9(MappingCode@1000 : Code[20]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCreateIntegrationMappings@7();
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnGetGraphRecord@1(VAR GraphRecordRef@1000 : RecordRef;DestinationGraphID@1001 : Text[250];TableID@1002 : Integer;VAR Found@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnGetInboundTableID@5(MappingCode@1000 : Code[20];VAR TableID@1001 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnGetMappingCodeForTable@15(TableID@1000 : Integer;VAR MappingCode@1001 : Code[20]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

