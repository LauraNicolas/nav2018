OBJECT Codeunit 7710 Miniform Move Activity List
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="Miniform Header";
    OnRun=VAR
            MiniformMgmt@1000 : Codeunit "Miniform Management";
          BEGIN
            MiniformMgmt.Initialize(
              MiniformHeader,Rec,DOMxmlin,ReturnedNode,
              RootNode,XMLDOMMgt,ADCSCommunication,ADCSUserId,
              CurrentCode,StackCode,WhseEmpId,LocationFilter);

            if Code <> CurrentCode then
              PrepareData
            else
              ProcessSelection;

            CLEAR(DOMxmlin);
          END;

  }
  CODE
  {
    VAR
      MiniformHeader@1023 : Record "Miniform Header";
      MiniformHeader2@1027 : Record "Miniform Header";
      XMLDOMMgt@1003 : Codeunit "XML DOM Management";
      ADCSCommunication@1020 : Codeunit "ADCS Communication";
      ADCSMgt@1026 : Codeunit "ADCS Management";
      RecRef@1014 : RecordRef;
      ReturnedNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      DOMxmlin@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      RootNode@1007 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      Text000@1008 : TextConst 'ENU=Function not Found.';
      Text006@1010 : TextConst 'ENU=No input Node found.';
      TextValue@1001 : Text[250];
      ADCSUserId@1000 : Text[250];
      WhseEmpId@1009 : Text[250];
      LocationFilter@1019 : Text[250];
      CurrentCode@1005 : Text[250];
      PreviousCode@1012 : Text[250];
      StackCode@1013 : Text[250];
      Remark@1002 : Text[250];
      ActiveInputField@1011 : Integer;
      Text009@1015 : TextConst 'ENU=No Documents found.';

    LOCAL PROCEDURE ProcessSelection@8();
    VAR
      WhseActivityHeader@1002 : Record "Warehouse Activity Header";
      FuncGroup@1003 : Record "Miniform Function Group";
      RecId@1001 : RecordID;
      TableNo@1000 : Integer;
    BEGIN
      if XMLDOMMgt.FindNode(RootNode,'Header/Input',ReturnedNode) then
        TextValue := ReturnedNode.InnerText
      else
        ERROR(Text006);

      EVALUATE(TableNo,ADCSCommunication.GetNodeAttribute(ReturnedNode,'TableNo'));
      RecRef.OPEN(TableNo);
      EVALUATE(RecId,ADCSCommunication.GetNodeAttribute(ReturnedNode,'RecordID'));
      if RecRef.GET(RecId) then begin
        RecRef.SETTABLE(WhseActivityHeader);
        WhseActivityHeader.SETCURRENTKEY(Type,"No.");
        WhseActivityHeader.SETRANGE(Type,WhseActivityHeader.Type);
        WhseActivityHeader.SETRANGE("Assigned User ID",WhseEmpId);
        WhseActivityHeader.SETFILTER("Location Code",LocationFilter);
        RecRef.GETTABLE(WhseActivityHeader);
        ADCSCommunication.SetRecRef(RecRef);
      end else begin
        ADCSCommunication.RunPreviousMiniform(DOMxmlin);
        exit;
      end;

      FuncGroup.KeyDef := ADCSCommunication.GetFunctionKey(MiniformHeader.Code,TextValue);
      ActiveInputField := 1;

      case FuncGroup.KeyDef of
        FuncGroup.KeyDef::Esc:
          ADCSCommunication.RunPreviousMiniform(DOMxmlin);
        FuncGroup.KeyDef::First:
          ADCSCommunication.FindRecRef(0,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::LnDn:
          if not ADCSCommunication.FindRecRef(1,MiniformHeader."No. of Records in List") then
            Remark := Text009;
        FuncGroup.KeyDef::LnUp:
          ADCSCommunication.FindRecRef(2,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::Last:
          ADCSCommunication.FindRecRef(3,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::PgDn:
          if not ADCSCommunication.FindRecRef(4,MiniformHeader."No. of Records in List") then
            Remark := Text009;
        FuncGroup.KeyDef::PgUp:
          ADCSCommunication.FindRecRef(5,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::Input:
          begin
            ADCSCommunication.IncreaseStack(DOMxmlin,MiniformHeader.Code);
            ADCSCommunication.GetNextMiniForm(MiniformHeader,MiniformHeader2);
            MiniformHeader2.SaveXMLin(DOMxmlin);
            CODEUNIT.RUN(MiniformHeader2."Handling Codeunit",MiniformHeader2);
          end;
        else
          ERROR(Text000);
      end;

      if not (FuncGroup.KeyDef in [FuncGroup.KeyDef::Esc,FuncGroup.KeyDef::Input]) then
        SendForm(ActiveInputField);
    END;

    LOCAL PROCEDURE PrepareData@4();
    VAR
      WhseActivityHeader@1001 : Record "Warehouse Activity Header";
    BEGIN
      with WhseActivityHeader do begin
        RESET;
        SETRANGE(Type,Type::Movement);
        if WhseEmpId <> '' then begin
          SETRANGE("Assigned User ID",WhseEmpId);
          SETFILTER("Location Code",LocationFilter);
        end;
        if not FINDFIRST then begin
          if ADCSCommunication.GetNodeAttribute(ReturnedNode,'RunReturn') = '0' then begin
            ADCSMgt.SendError(Text009);
            exit;
          end;
          ADCSCommunication.DecreaseStack(DOMxmlin,PreviousCode);
          MiniformHeader2.GET(PreviousCode);
          MiniformHeader2.SaveXMLin(DOMxmlin);
          CODEUNIT.RUN(MiniformHeader2."Handling Codeunit",MiniformHeader2);
        end else begin
          RecRef.GETTABLE(WhseActivityHeader);
          ADCSCommunication.SetRecRef(RecRef);
          ActiveInputField := 1;
          SendForm(ActiveInputField);
        end;
      end;
    END;

    LOCAL PROCEDURE SendForm@1(InputField@1000 : Integer);
    BEGIN
      ADCSCommunication.EncodeMiniForm(MiniformHeader,StackCode,DOMxmlin,InputField,Remark,ADCSUserId);
      ADCSCommunication.GetReturnXML(DOMxmlin);
      ADCSMgt.SendXMLReply(DOMxmlin);
    END;

    BEGIN
    END.
  }
}

