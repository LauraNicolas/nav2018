OBJECT Codeunit 6305 Set Power BI User Config
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ConfPersonalizationMgt@1000 : Codeunit "Conf./Personalization Mgt.";

    PROCEDURE SetUserConfig@2(VAR PowerBIUserConfiguration@1001 : Record "Power BI User Configuration";PageID@1002 : Text) : Boolean;
    BEGIN
      // load existing UserConfig entry to get PowerBI FactBox visibility
      // entry by itself is created on the FactBox page
      PowerBIUserConfiguration.RESET;
      PowerBIUserConfiguration.SETFILTER("Page ID",PageID);
      PowerBIUserConfiguration.SETFILTER("User Security ID",USERSECURITYID);
      PowerBIUserConfiguration.SETFILTER("Profile ID",ConfPersonalizationMgt.GetCurrentProfileIDNoError);
      if not PowerBIUserConfiguration.ISEMPTY then begin
        PowerBIUserConfiguration.FINDFIRST;
        exit(PowerBIUserConfiguration."Report Visibility");
      end;

      // if entry has not been created yet, set PowerBI FactBox visibility to defalut TRUE value
      exit(true);
    END;

    PROCEDURE CreateOrReadUserConfigEntry@3(VAR PowerBIUserConfiguration@1001 : Record "Power BI User Configuration";VAR LastOpenedReportID@1003 : GUID;Context@1000 : Text[50]);
    BEGIN
      // create a new Power BI User Configuration table entry or read one if it exist
      PowerBIUserConfiguration.RESET;
      PowerBIUserConfiguration.SETFILTER("Page ID",Context);
      PowerBIUserConfiguration.SETFILTER("User Security ID",USERSECURITYID);
      PowerBIUserConfiguration.SETFILTER("Profile ID",ConfPersonalizationMgt.GetCurrentProfileIDNoError);
      if PowerBIUserConfiguration.ISEMPTY then begin
        PowerBIUserConfiguration."Page ID" := Context;
        PowerBIUserConfiguration."User Security ID" := USERSECURITYID;
        PowerBIUserConfiguration."Profile ID" := ConfPersonalizationMgt.GetCurrentProfileIDNoError;
        PowerBIUserConfiguration."Report Visibility" := true;
        // SelectedReportId field is set to an empty GUID by default
        CLEAR(LastOpenedReportID);
        PowerBIUserConfiguration.INSERT(true);
        COMMIT;
      end else begin
        PowerBIUserConfiguration.FINDFIRST;
        LastOpenedReportID := PowerBIUserConfiguration."Selected Report ID";
      end;
    END;

    BEGIN
    END.
  }
}

