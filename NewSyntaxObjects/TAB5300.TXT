OBJECT Table 5300 Outlook Synch. Entity
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               if ISNULLGUID("Record GUID") then
                 "Record GUID" := CREATEGUID;
             END;

    OnDelete=VAR
               OutlookSynchUserSetup@1000 : Record "Outlook Synch. User Setup";
             BEGIN
               OSynchDependency.RESET;
               OSynchDependency.SETRANGE("Depend. Synch. Entity Code",Code);
               if not OSynchDependency.ISEMPTY then
                 if not CONFIRM(Text004) then
                   ERROR('');

               OutlookSynchUserSetup.SETRANGE("Synch. Entity Code",Code);
               if not OutlookSynchUserSetup.ISEMPTY then
                 ERROR(Text003,OutlookSynchUserSetup.TABLECAPTION);

               OSynchDependency.DELETEALL;
               OutlookSynchUserSetup.DELETEALL(true);

               OSynchEntityElement.RESET;
               OSynchEntityElement.SETRANGE("Synch. Entity Code",Code);
               OSynchEntityElement.DELETEALL(true);

               OSynchField.RESET;
               OSynchField.SETRANGE("Synch. Entity Code",Code);
               OSynchField.DELETEALL(true);

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;
             END;

    CaptionML=ENU=Outlook Synch. Entity;
    PasteIsValid=false;
    LookupPageID="Outlook Synch. Entity List";
    DrillDownPageID="Outlook Synch. Entity List";
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 2   ;   ;Description         ;Text80        ;OnValidate=VAR
                                                                OutlookSynchUserSetup@1000 : Record "Outlook Synch. User Setup";
                                                              BEGIN
                                                                if Description <> '' then
                                                                  exit;

                                                                OutlookSynchUserSetup.SETRANGE("Synch. Entity Code",Code);
                                                                if not OutlookSynchUserSetup.ISEMPTY then
                                                                  ERROR(Text005,FIELDCAPTION(Description),Code);
                                                              END;

                                                   CaptionML=ENU=Description }
    { 3   ;   ;Table No.           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Table));
                                                   OnValidate=BEGIN
                                                                if "Table No." = xRec."Table No." then
                                                                  exit;

                                                                CheckUserSetup;
                                                                TESTFIELD("Table No.");

                                                                if not OSynchSetupMgt.CheckPKFieldsQuantity("Table No.") then
                                                                  exit;

                                                                if xRec."Table No." <> 0 then begin
                                                                  if not
                                                                     CONFIRM(
                                                                       STRSUBSTNO(
                                                                         Text001,
                                                                         OSynchEntityElement.TABLECAPTION,
                                                                         OSynchField.TABLECAPTION,
                                                                         OSynchFilter.TABLECAPTION,
                                                                         OSynchDependency.TABLECAPTION))
                                                                  then begin
                                                                    "Table No." := xRec."Table No.";
                                                                    exit;
                                                                  end;

                                                                  Condition := '';
                                                                  "Outlook Item" := '';

                                                                  OSynchDependency.RESET;
                                                                  OSynchDependency.SETRANGE("Depend. Synch. Entity Code",Code);
                                                                  OSynchDependency.DELETEALL(true);

                                                                  OSynchEntityElement.RESET;
                                                                  OSynchEntityElement.SETRANGE("Synch. Entity Code",Code);
                                                                  OSynchEntityElement.DELETEALL(true);

                                                                  OSynchField.RESET;
                                                                  OSynchField.SETRANGE("Synch. Entity Code",Code);
                                                                  OSynchField.DELETEALL(true);

                                                                  OSynchFilter.RESET;
                                                                  OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                                                  OSynchFilter.DELETEALL;
                                                                end;

                                                                CALCFIELDS("Table Caption");
                                                              END;

                                                   OnLookup=VAR
                                                              TableNo@1000 : Integer;
                                                            BEGIN
                                                              TableNo := OSynchSetupMgt.ShowTablesList;

                                                              if TableNo <> 0 then
                                                                VALIDATE("Table No.",TableNo);
                                                            END;

                                                   CaptionML=ENU=Table No.;
                                                   BlankZero=true }
    { 4   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Table),
                                                                                                                "Object ID"=FIELD("Table No.")));
                                                   CaptionML=ENU=Table Caption;
                                                   Editable=false }
    { 5   ;   ;Condition           ;Text250       ;OnValidate=VAR
                                                                RecordRef@1000 : RecordRef;
                                                              BEGIN
                                                                RecordRef.OPEN("Table No.");
                                                                RecordRef.SETVIEW(Condition);
                                                                Condition := RecordRef.GETVIEW(false);
                                                              END;

                                                   CaptionML=ENU=Condition;
                                                   Editable=false }
    { 6   ;   ;Outlook Item        ;Text80        ;OnValidate=BEGIN
                                                                TESTFIELD("Outlook Item");
                                                                if not OSynchSetupMgt.ValidateOutlookItemName("Outlook Item") then
                                                                  ERROR(Text002);

                                                                if "Outlook Item" = xRec."Outlook Item" then
                                                                  exit;

                                                                CheckUserSetup;

                                                                if xRec."Outlook Item" = '' then
                                                                  exit;

                                                                if not
                                                                   CONFIRM(
                                                                     STRSUBSTNO(
                                                                       Text001,
                                                                       OSynchEntityElement.TABLECAPTION,
                                                                       OSynchField.TABLECAPTION,
                                                                       OSynchFilter.TABLECAPTION,
                                                                       OSynchDependency.TABLECAPTION))
                                                                then begin
                                                                  "Outlook Item" := xRec."Outlook Item";
                                                                  exit;
                                                                end;

                                                                OSynchDependency.RESET;
                                                                OSynchDependency.SETRANGE("Depend. Synch. Entity Code",Code);
                                                                OSynchDependency.DELETEALL(true);

                                                                OSynchEntityElement.RESET;
                                                                OSynchEntityElement.SETRANGE("Synch. Entity Code",Code);
                                                                OSynchEntityElement.DELETEALL(true);

                                                                OSynchField.RESET;
                                                                OSynchField.SETRANGE("Synch. Entity Code",Code);
                                                                OSynchField.DELETEALL(true);
                                                              END;

                                                   OnLookup=VAR
                                                              ItemName@1000 : Text[50];
                                                            BEGIN
                                                              ItemName := OSynchSetupMgt.ShowOItemsList;

                                                              if ItemName <> '' then
                                                                VALIDATE("Outlook Item",ItemName);
                                                            END;

                                                   CaptionML=ENU=Outlook Item }
    { 7   ;   ;Record GUID         ;GUID          ;CaptionML=ENU=Record GUID;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=true }
    {    ;"Record GUID"                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntityElement@1003 : Record "Outlook Synch. Entity Element";
      OSynchFilter@1002 : Record "Outlook Synch. Filter";
      OSynchField@1001 : Record "Outlook Synch. Field";
      OSynchDependency@1010 : Record "Outlook Synch. Dependency";
      OSynchSetupMgt@1000 : Codeunit "Outlook Synch. Setup Mgt.";
      Text001@1005 : TextConst 'ENU=If you change the value in this field, the %1, %2, %3 and %4 records related to this entity will be deleted.\Do you want to change it anyway?';
      Text002@1004 : TextConst 'ENU=The Outlook item with this name does not exist.\Click the AssistButton to see a list of valid Outlook items';
      Text003@1009 : TextConst 'ENU=You cannot delete this entity because it is set up for synchronization. Please verify %1.';
      Text004@1006 : TextConst 'ENU=There are entities which depend on this entity. If you delete it, the relation to its dependencies will be removed.\Do you want to delete it anyway?';
      Text005@1011 : TextConst 'ENU=The %1 field cannot be blank because the %2 entity is used with synchronization.';
      Text006@1012 : TextConst 'ENU=You cannot change this entity because it is used with synchronization for the user %1.';

    [External]
    PROCEDURE ShowEntityFields@1();
    BEGIN
      TESTFIELD("Outlook Item");
      if "Table No." = 0 then
        FIELDERROR("Table No.");

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",Code);
      OSynchField.SETRANGE("Element No.",0);

      PAGE.RUNMODAL(PAGE::"Outlook Synch. Fields",OSynchField);
    END;

    LOCAL PROCEDURE CheckUserSetup@4();
    VAR
      OSynchUserSetup@1000 : Record "Outlook Synch. User Setup";
    BEGIN
      OSynchUserSetup.RESET;
      OSynchUserSetup.SETRANGE("Synch. Entity Code",Code);
      if OSynchUserSetup.FINDFIRST then
        ERROR(Text006,OSynchUserSetup."User ID");
    END;

    BEGIN
    END.
  }
}

