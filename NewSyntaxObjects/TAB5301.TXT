OBJECT Table 5301 Outlook Synch. Entity Element
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Table No.");

               if ISNULLGUID("Record GUID") then
                 "Record GUID" := CREATEGUID;
             END;

    OnDelete=BEGIN
               OSynchSetupDetail.RESET;
               OSynchSetupDetail.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchSetupDetail.SETRANGE("Element No.","Element No.");
               if not OSynchSetupDetail.ISEMPTY then
                 ERROR(Text001);

               OSynchSetupDetail.DELETEALL(true);

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;

               OSynchField.RESET;
               OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchField.SETRANGE("Element No.","Element No.");
               OSynchField.DELETEALL(true);

               OSynchDependency.RESET;
               OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchDependency.SETRANGE("Element No.","Element No.");
               OSynchDependency.DELETEALL(true);
             END;

    CaptionML=ENU=Outlook Synch. Entity Element;
    PasteIsValid=false;
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   CaptionML=ENU=Synch. Entity Code;
                                                   NotBlank=true }
    { 2   ;   ;Element No.         ;Integer       ;CaptionML=ENU=Element No. }
    { 3   ;   ;Table No.           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Table));
                                                   OnValidate=BEGIN
                                                                if "Table No." <> xRec."Table No." then begin
                                                                  CheckUserSetup;
                                                                  CheckMasterTableNo;
                                                                  TESTFIELD("Table No.");

                                                                  if not OSynchSetupMgt.CheckPKFieldsQuantity("Table No.") then
                                                                    exit;

                                                                  if "Element No." <> 0 then begin
                                                                    if not
                                                                       CONFIRM(
                                                                         STRSUBSTNO(
                                                                           Text003,
                                                                           OSynchField.TABLECAPTION,
                                                                           OSynchFilter.TABLECAPTION,
                                                                           OSynchDependency.TABLECAPTION))
                                                                    then begin
                                                                      "Table No." := xRec."Table No.";
                                                                      exit;
                                                                    end;

                                                                    OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                    OSynchField.SETRANGE("Element No.","Element No.");
                                                                    OSynchField.DELETEALL(true);

                                                                    OSynchFilter.RESET;
                                                                    OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                                                    OSynchFilter.DELETEALL;

                                                                    OSynchDependency.RESET;
                                                                    OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                    OSynchDependency.SETRANGE("Element No.","Element No.");
                                                                    OSynchDependency.DELETEALL(true);

                                                                    "Table Relation" := '';
                                                                  end;
                                                                end;

                                                                CALCFIELDS("Table Caption","No. of Dependencies");
                                                              END;

                                                   OnLookup=VAR
                                                              TableNo@1000 : Integer;
                                                            BEGIN
                                                              CheckMasterTableNo;
                                                              TableNo := OSynchSetupMgt.ShowTablesList;

                                                              if TableNo <> 0 then
                                                                VALIDATE("Table No.",TableNo);
                                                            END;

                                                   CaptionML=ENU=Table No.;
                                                   BlankZero=true }
    { 4   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Table),
                                                                                                                "Object ID"=FIELD("Table No.")));
                                                   CaptionML=ENU=Table Caption;
                                                   Editable=false }
    { 5   ;   ;Table Relation      ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Table Relation");
                                                              END;

                                                   CaptionML=ENU=Table Relation;
                                                   Editable=false }
    { 6   ;   ;Outlook Collection  ;Text80        ;OnValidate=BEGIN
                                                                if "Outlook Collection" <> '' then begin
                                                                  OSynchEntity.GET("Synch. Entity Code");
                                                                  if not OSynchSetupMgt.ValidateOutlookCollectionName("Outlook Collection",OSynchEntity."Outlook Item") then
                                                                    ERROR(Text002);
                                                                  CheckCollectionName;
                                                                end;

                                                                if "Outlook Collection" = xRec."Outlook Collection" then
                                                                  exit;

                                                                CheckUserSetup;
                                                                CheckMasterTableNo;

                                                                if "Element No." = 0 then
                                                                  exit;

                                                                if xRec."Outlook Collection" <> '' then
                                                                  if not
                                                                     CONFIRM(
                                                                       STRSUBSTNO(
                                                                         Text003,
                                                                         OSynchField.TABLECAPTION,
                                                                         OSynchFilter.TABLECAPTION,
                                                                         OSynchDependency.TABLECAPTION))
                                                                  then begin
                                                                    "Outlook Collection" := xRec."Outlook Collection";
                                                                    exit;
                                                                  end;

                                                                OSynchField.RESET;
                                                                OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                OSynchField.SETRANGE("Element No.","Element No.");
                                                                OSynchField.DELETEALL(true);

                                                                OSynchDependency.RESET;
                                                                OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                OSynchDependency.SETRANGE("Element No.","Element No.");
                                                                OSynchDependency.DELETEALL(true);
                                                              END;

                                                   OnLookup=VAR
                                                              CollectionName@1000 : Text[80];
                                                            BEGIN
                                                              CheckMasterTableNo;
                                                              OSynchEntity.GET("Synch. Entity Code");

                                                              OSynchEntity.TESTFIELD("Outlook Item");

                                                              CollectionName := OSynchSetupMgt.ShowOCollectionsList(OSynchEntity."Outlook Item");

                                                              if CollectionName <> '' then
                                                                VALIDATE("Outlook Collection",CollectionName);
                                                            END;

                                                   CaptionML=ENU=Outlook Collection }
    { 7   ;   ;Master Table No.    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Outlook Synch. Entity"."Table No." WHERE (Code=FIELD("Synch. Entity Code")));
                                                   CaptionML=ENU=Master Table No.;
                                                   Editable=false }
    { 8   ;   ;Record GUID         ;GUID          ;CaptionML=ENU=Record GUID;
                                                   Editable=false }
    { 9   ;   ;No. of Dependencies ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Outlook Synch. Dependency" WHERE ("Synch. Entity Code"=FIELD("Synch. Entity Code"),
                                                                                                        "Element No."=FIELD("Element No.")));
                                                   CaptionML=ENU=No. of Dependencies;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Synch. Entity Code","Element No."      ;Clustered=true }
    {    ;"Table No."                              }
    {    ;"Record GUID"                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntity@1003 : Record "Outlook Synch. Entity";
      OSynchFilter@1002 : Record "Outlook Synch. Filter";
      OSynchField@1001 : Record "Outlook Synch. Field";
      OSynchDependency@1012 : Record "Outlook Synch. Dependency";
      OSynchSetupDetail@1011 : Record "Outlook Synch. Setup Detail";
      OSynchSetupMgt@1000 : Codeunit "Outlook Synch. Setup Mgt.";
      Text001@1005 : TextConst 'ENU=You cannot delete this collection because it is used with synchronization.';
      Text002@1007 : TextConst 'ENU=The Outlook item collection with this name does not exist.\Click the AssistButton to see a list of valid collections for this Outlook item.';
      Text003@1004 : TextConst 'ENU=If you change the value in this field, the %1, %2, and %3 records related to this collection will be deleted.\Do you want to change it anyway?';
      Text004@1006 : TextConst 'ENU=You cannot change this collection because it is used with synchronization for user %1.';
      Text005@1008 : TextConst 'ENU="An Outlook item collection with this name already exists.\Identification fields and values:\%1=''''%2'''',%3=''''%4''''."';

    [External]
    PROCEDURE ShowElementFields@1();
    BEGIN
      TESTFIELD("Synch. Entity Code");
      TESTFIELD("Element No.");
      TESTFIELD("Table No.");
      TESTFIELD("Table Relation");
      TESTFIELD("Outlook Collection");

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchField.SETRANGE("Element No.","Element No.");

      PAGE.RUNMODAL(PAGE::"Outlook Synch. Fields",OSynchField);
    END;

    [External]
    PROCEDURE ShowDependencies@3();
    BEGIN
      TESTFIELD("Synch. Entity Code");
      TESTFIELD("Element No.");
      TESTFIELD("Table No.");
      TESTFIELD("Table Relation");
      TESTFIELD("Outlook Collection");

      OSynchDependency.RESET;
      OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchDependency.SETRANGE("Element No.","Element No.");

      PAGE.RUNMODAL(PAGE::"Outlook Synch. Dependencies",OSynchDependency);
      CALCFIELDS("No. of Dependencies");
    END;

    [External]
    PROCEDURE CheckMasterTableNo@2();
    BEGIN
      CALCFIELDS("Master Table No.");
      if "Master Table No." = 0 then begin
        OSynchEntity.GET("Synch. Entity Code");
        OSynchEntity.TESTFIELD("Table No.");
      end;
    END;

    [External]
    PROCEDURE CheckUserSetup@4();
    VAR
      OSynchUserSetup@1000 : Record "Outlook Synch. User Setup";
    BEGIN
      OSynchUserSetup.RESET;
      OSynchUserSetup.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      if not OSynchUserSetup.FINDSET then
        exit;

      repeat
        OSynchUserSetup.CALCFIELDS("No. of Elements");
        if OSynchUserSetup."No. of Elements" > 0 then
          if OSynchSetupDetail.GET(OSynchUserSetup."User ID","Synch. Entity Code","Element No.") then
            ERROR(Text004,OSynchUserSetup."User ID");
      until OSynchUserSetup.NEXT = 0;
    END;

    [External]
    PROCEDURE CheckCollectionName@5();
    VAR
      OSynchEntityElement@1000 : Record "Outlook Synch. Entity Element";
    BEGIN
      OSynchEntityElement.RESET;
      OSynchEntityElement.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchEntityElement.SETRANGE("Outlook Collection","Outlook Collection");
      if OSynchEntityElement.FINDFIRST then
        ERROR(
          Text005,
          OSynchEntityElement.FIELDCAPTION("Synch. Entity Code"),
          OSynchEntityElement."Synch. Entity Code",
          OSynchEntityElement.FIELDCAPTION("Element No."),
          OSynchEntityElement."Element No.");
    END;

    BEGIN
    END.
  }
}

