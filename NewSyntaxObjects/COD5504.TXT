OBJECT Codeunit 5504 Graph Mgt - Tax Area
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE UpdateIntegrationRecords@11(OnlyItemsWithoutId@1000 : Boolean);
    VAR
      DummyTaxArea@1002 : Record "Tax Area";
      DummyVATBusinessPostingGroup@1004 : Record "VAT Business Posting Group";
      DummyVATClause@1005 : Record "VAT Clause";
      GraphMgtGeneralTools@1001 : Codeunit "Graph Mgt - General Tools";
      TaxAreaRecordRef@1019 : RecordRef;
      VATBusinessPostingGroupRecordRef@1003 : RecordRef;
      VATClauseRecordRef@1006 : RecordRef;
    BEGIN
      TaxAreaRecordRef.OPEN(DATABASE::"Tax Area");
      GraphMgtGeneralTools.UpdateIntegrationRecords(TaxAreaRecordRef,DummyTaxArea.FIELDNO(Id),OnlyItemsWithoutId);

      VATBusinessPostingGroupRecordRef.OPEN(DATABASE::"VAT Business Posting Group");
      GraphMgtGeneralTools.UpdateIntegrationRecords(
        VATBusinessPostingGroupRecordRef,DummyVATBusinessPostingGroup.FIELDNO(Id),OnlyItemsWithoutId);

      VATClauseRecordRef.OPEN(DATABASE::"VAT Clause");
      GraphMgtGeneralTools.UpdateIntegrationRecords(VATClauseRecordRef,DummyVATClause.FIELDNO(Id),OnlyItemsWithoutId);
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdField@19(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummyTaxArea@1004 : Record "Tax Area";
      GraphMgtGeneralTools@1006 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      if not CheckSupportedTable(RecRef) then
        exit;

      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(
        RecRef,NewId,Handled,RecRef.NUMBER,DummyTaxArea.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@21(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummyTaxArea@1004 : Record "Tax Area";
      GraphMgtGeneralTools@1007 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      if not CheckSupportedTable(RecRef) then
        exit;

      GraphMgtGeneralTools.HandleGetPredefinedIdValue(Id,RecRef,Handled,RecRef.NUMBER,DummyTaxArea.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@16();
    BEGIN
      UpdateIntegrationRecords(false);
    END;

    LOCAL PROCEDURE CheckSupportedTable@3(VAR RecRef@1000 : RecordRef) : Boolean;
    BEGIN
      exit(RecRef.NUMBER in [DATABASE::"VAT Business Posting Group",DATABASE::"Tax Area",DATABASE::"VAT Clause"]);
    END;

    BEGIN
    END.
  }
}

