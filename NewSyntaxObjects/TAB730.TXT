OBJECT Table 730 Standard Address
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               if not ISTEMPORARY then
                 ERROR(DevMsgNotTemporaryErr);
             END;

    CaptionML=ENU=Standard Address;
  }
  FIELDS
  {
    { 1   ;   ;Related RecordID    ;RecordID      ;CaptionML=ENU=Related RecordID }
    { 2   ;   ;Address Type        ;Option        ;CaptionML=ENU=Address Type;
                                                   OptionCaptionML=ENU=" ,Sell-to,Bill-to";
                                                   OptionString=" ","Sell-to","Bill-to" }
    { 3   ;   ;Address             ;Text50        ;CaptionML=ENU=Address }
    { 4   ;   ;Address 2           ;Text50        ;CaptionML=ENU=Address 2 }
    { 5   ;   ;City                ;Text30        ;TableRelation=IF ("Country/Region Code"=CONST('')) "Post Code".City
                                                                 ELSE IF ("Country/Region Code"=FILTER(<>'')) "Post Code".City WHERE ("Country/Region Code"=FIELD("Country/Region Code"));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) and GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=City }
    { 6   ;   ;Country/Region Code ;Code10        ;TableRelation="Country/Region";
                                                   CaptionML=ENU=Country/Region Code }
    { 7   ;   ;Post Code           ;Code20        ;TableRelation=IF ("Country/Region Code"=CONST('')) "Post Code"
                                                                 ELSE IF ("Country/Region Code"=FILTER(<>'')) "Post Code" WHERE ("Country/Region Code"=FIELD("Country/Region Code"));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) and GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Post Code }
    { 8   ;   ;County              ;Text30        ;CaptionML=ENU=County }
  }
  KEYS
  {
    {    ;"Related RecordID"                      ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PostCode@1000 : Record "Post Code";
      DevMsgNotTemporaryErr@1001 : TextConst 'ENU=This function can only be used when the record is temporary.';
      FormatAddress@1002 : Codeunit "Format Address";

    PROCEDURE ToString@5() FullAddress : Text;
    VAR
      AddressArray@1001 : ARRAY [8] OF Text[90];
      AddressPosition@1000 : Integer;
    BEGIN
      FormatAddress.FormatAddr(AddressArray,'','','',Address,"Address 2",City,"Post Code",County,"Country/Region Code");
      for AddressPosition := 1 to 8 do begin
        AddressArray[AddressPosition] := DELCHR(AddressArray[AddressPosition],'<',', ');
        if AddressArray[AddressPosition] <> '' then begin
          if FullAddress = '' then
            FullAddress := AddressArray[AddressPosition]
          else
            FullAddress += ', ' + AddressArray[AddressPosition]
        end;
      end;
    END;

    PROCEDURE CopyFromCustomer@2(Customer@1000 : Record Customer);
    BEGIN
      INIT;
      "Related RecordID" := Customer.RECORDID;
      Address := Customer.Address;
      "Address 2" := Customer."Address 2";
      City := Customer.City;
      "Country/Region Code" := Customer."Country/Region Code";
      "Post Code" := Customer."Post Code";
      County := Customer.County;
      INSERT(true);
    END;

    PROCEDURE CopyFromCompanyInformation@6(CompanyInformation@1000 : Record "Company Information");
    BEGIN
      INIT;
      "Related RecordID" := CompanyInformation.RECORDID;
      Address := CompanyInformation.Address;
      "Address 2" := CompanyInformation."Address 2";
      City := CompanyInformation.City;
      "Country/Region Code" := CompanyInformation."Country/Region Code";
      "Post Code" := CompanyInformation."Post Code";
      County := CompanyInformation.County;
      INSERT(true);
    END;

    PROCEDURE CopyFromSalesHeaderSellTo@7(SalesHeader@1000 : Record "Sales Header");
    BEGIN
      INIT;
      "Address Type" := "Address Type"::"Sell-to";
      "Related RecordID" := SalesHeader.RECORDID;
      Address := SalesHeader."Sell-to Address";
      "Address 2" := SalesHeader."Sell-to Address 2";
      City := SalesHeader."Sell-to City";
      "Country/Region Code" := SalesHeader."Sell-to Country/Region Code";
      "Post Code" := SalesHeader."Sell-to Post Code";
      County := SalesHeader."Sell-to County";
      INSERT(true);
    END;

    PROCEDURE CopyFromSalesInvoiceHeaderSellTo@11(SalesInvoiceHeader@1000 : Record "Sales Invoice Header");
    BEGIN
      INIT;
      "Address Type" := "Address Type"::"Sell-to";
      "Related RecordID" := SalesInvoiceHeader.RECORDID;
      Address := SalesInvoiceHeader."Sell-to Address";
      "Address 2" := SalesInvoiceHeader."Sell-to Address 2";
      City := SalesInvoiceHeader."Sell-to City";
      "Country/Region Code" := SalesInvoiceHeader."Sell-to Country/Region Code";
      "Post Code" := SalesInvoiceHeader."Sell-to Post Code";
      County := SalesInvoiceHeader."Sell-to County";
      INSERT(true);
    END;

    PROCEDURE SaveToRecord@1();
    VAR
      RecID@1001 : RecordID;
    BEGIN
      RecID := "Related RecordID";
      case RecID.TABLENO of
        DATABASE::Customer:
          SaveToCustomer;
        DATABASE::"Company Information":
          SaveToCompanyInformation;
        DATABASE::"Sales Header":
          case "Address Type" of
            "Address Type"::"Sell-to":
              SaveToSalesHeaderSellTo;
          end;
      end;
    END;

    LOCAL PROCEDURE SaveToCustomer@3();
    VAR
      Customer@1000 : Record Customer;
    BEGIN
      Customer.LOCKTABLE;
      Customer.GET("Related RecordID");
      Customer.VALIDATE(Address,Address);
      Customer.VALIDATE("Address 2","Address 2");
      Customer.VALIDATE(City,City);
      Customer.VALIDATE("Country/Region Code","Country/Region Code");
      Customer.VALIDATE("Post Code","Post Code");
      Customer.VALIDATE(County,County);
      Customer.MODIFY(true);
    END;

    LOCAL PROCEDURE SaveToCompanyInformation@4();
    VAR
      CompanyInformation@1001 : Record "Company Information";
    BEGIN
      CompanyInformation.LOCKTABLE;
      CompanyInformation.GET("Related RecordID");
      CompanyInformation.VALIDATE(Address,Address);
      CompanyInformation.VALIDATE("Address 2","Address 2");
      CompanyInformation.VALIDATE(City,City);
      CompanyInformation.VALIDATE("Country/Region Code","Country/Region Code");
      CompanyInformation.VALIDATE("Post Code","Post Code");
      CompanyInformation.VALIDATE(County,County);
      CompanyInformation.MODIFY(true);
    END;

    LOCAL PROCEDURE SaveToSalesHeaderSellTo@8();
    VAR
      SalesHeader@1001 : Record "Sales Header";
    BEGIN
      SalesHeader.LOCKTABLE;
      SalesHeader.GET("Related RecordID");
      SalesHeader.VALIDATE("Sell-to Address",Address);
      SalesHeader.VALIDATE("Sell-to Address 2","Address 2");
      SalesHeader.VALIDATE("Sell-to City",City);
      SalesHeader.VALIDATE("Sell-to Country/Region Code","Country/Region Code");
      SalesHeader.VALIDATE("Sell-to Post Code","Post Code");
      SalesHeader.VALIDATE("Sell-to County",County);
      SalesHeader.MODIFY(true);
    END;

    BEGIN
    END.
  }
}

