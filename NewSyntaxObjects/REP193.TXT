OBJECT Report 193 Issue Finance Charge Memos
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Issue Finance Charge Memos;
    ProcessingOnly=true;
  }
  DATASET
  {
    { 8733;    ;DataItem;Finance Charge Memo Header;
               DataItemTable="Finance Charge Memo Header";
               DataItemTableView=SORTING("No.");
               ReqFilterHeadingML=ENU=Finance Charge Memo;
               OnPreDataItem=BEGIN
                               if ReplacePostingDate and (PostingDateReq = 0D) then
                                 ERROR(Text000);
                               NoOfRecords := COUNT;
                               if NoOfRecords = 1 then
                                 Window.OPEN(Text001)
                               else begin
                                 Window.OPEN(Text002);
                                 OldTime := TIME;
                               end;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecordNo := RecordNo + 1;
                                  CLEAR(FinChrgMemoIssue);
                                  FinChrgMemoIssue.Set("Finance Charge Memo Header",ReplacePostingDate,PostingDateReq);
                                  if NoOfRecords = 1 then begin
                                    FinChrgMemoIssue.RUN;
                                    MARK := false;
                                  end else begin
                                    NewTime := TIME;
                                    if (NewTime - OldTime > 100) or (NewTime < OldTime) then begin
                                      NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                                      if NewProgress <> OldProgress then begin
                                        Window.UPDATE(1,NewProgress * 100);
                                        OldProgress := NewProgress;
                                      end;
                                      OldTime := TIME;
                                    end;
                                    MARK := not FinChrgMemoIssue.RUN;
                                  end;

                                  if (PrintDoc <> PrintDoc::" ") and not MARK then begin
                                    FinChrgMemoIssue.GetIssuedFinChrgMemo(IssuedFinChrgMemoHeader);
                                    TempIssuedFinChrgMemoHeader := IssuedFinChrgMemoHeader;
                                    TempIssuedFinChrgMemoHeader.INSERT;
                                  end;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                COMMIT;
                                if PrintDoc <> PrintDoc::" " then
                                  if TempIssuedFinChrgMemoHeader.FINDSET then
                                    repeat
                                      IssuedFinChrgMemoHeader := TempIssuedFinChrgMemoHeader;
                                      IssuedFinChrgMemoHeader.SETRECFILTER;
                                      IssuedFinChrgMemoHeader.PrintRecords(false,PrintDoc = PrintDoc::Email,HideDialog);
                                    until TempIssuedFinChrgMemoHeader.NEXT = 0;
                                MARKEDONLY := true;
                                if FIND('-') then
                                  if CONFIRM(Text003,true) then
                                    PAGE.RUNMODAL(0,"Finance Charge Memo Header");
                              END;

               ReqFilterFields="No." }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 3   ;2   ;Field     ;
                  Name=PrintDoc;
                  CaptionML=ENU=Print;
                  ToolTipML=ENU=Specifies if you want the program to print the finance charge memos when they are issued.;
                  ApplicationArea=#Advanced;
                  SourceExpr=PrintDoc }

      { 2   ;2   ;Field     ;
                  Name=Replace Posting Date;
                  CaptionML=ENU=Replace Posting Date;
                  ToolTipML=ENU=Specifies if you want to replace the finance charge memos' posting date with the date entered in the field below.;
                  ApplicationArea=#Advanced;
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  Name=Posting Date;
                  CaptionML=ENU=Posting Date;
                  ToolTipML=ENU=Specifies the posting date. If you place a check mark in the check box above, the program will use this date on all finance charge memos when you post.;
                  ApplicationArea=#Advanced;
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  Name=HideEmailDialog;
                  CaptionML=ENU=Hide Email Dialog;
                  ToolTipML=ENU=Specifies if you want to hide email dialog.;
                  ApplicationArea=#Advanced;
                  SourceExpr=HideDialog }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the posting date.';
      Text001@1001 : TextConst 'ENU=Issuing finance charge memo...';
      Text002@1002 : TextConst 'ENU=Issuing finance charge memos @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'ENU=It was not possible to issue some of the selected finance charge memos.\Do you want to see these finance charge memos?';
      IssuedFinChrgMemoHeader@1005 : Record "Issued Fin. Charge Memo Header";
      TempIssuedFinChrgMemoHeader@1004 : TEMPORARY Record "Issued Fin. Charge Memo Header";
      FinChrgMemoIssue@1006 : Codeunit "FinChrgMemo-Issue";
      Window@1007 : Dialog;
      NoOfRecords@1008 : Integer;
      RecordNo@1009 : Integer;
      NewProgress@1010 : Integer;
      OldProgress@1011 : Integer;
      NewTime@1012 : Time;
      OldTime@1013 : Time;
      PostingDateReq@1014 : Date;
      ReplacePostingDate@1015 : Boolean;
      PrintDoc@1016 : '" ",Print,Email';
      HideDialog@1017 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

