OBJECT Page 2122 O365 Incoming Doc. Attch. List
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Incoming Document Files;
    InsertAllowed=false;
    SourceTable="Incoming Document Attachment";
    PageType=ListPart;
    OnOpenPage=BEGIN
                 CameraAvailable := GetCameraAvailable;
                 if CameraAvailable then
                   CameraProvider := CameraProvider.Create;
               END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      Name=Actions;
                      CaptionML=ENU=Actions;
                      ActionContainerType=ActionItems }
      { 12      ;1   ;Action    ;
                      Name=DrillDown;
                      CaptionML=ENU=View File;
                      ToolTipML=ENU=View the attached file.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      Image=Picture;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 NameDrillDown;
                               END;

                      Gesture=LeftSwipe }
      { 10      ;1   ;Action    ;
                      Name=Delete;
                      CaptionML=ENU=Delete;
                      ToolTipML=ENU=Delete the record.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      Image=Delete;
                      PromotedCategory=Process;
                      Scope=Repeater;
                      OnAction=BEGIN
                                 DELETE;
                                 CurrPage.UPDATE;
                               END;

                      Gesture=RightSwipe }
      { 6       ;1   ;Action    ;
                      Name=Open;
                      ShortCutKey=Return;
                      CaptionML=ENU=Open;
                      ToolTipML=ENU=Open the card for the selected record.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Visible=FALSE;
                      OnAction=BEGIN
                                 NameDrillDown;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                Name=Name;
                ToolTipML=ENU=Specifies the name of the attached file.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Name;
                OnDrillDown=BEGIN
                              NameDrillDown;
                            END;

                ImplicitType=Text250 }

    { 5   ;2   ;Field     ;
                Name=Type;
                ToolTipML=ENU=Specifies the type of the attached file.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Type;
                Editable=FALSE;
                ImplicitType=Option }

    { 3   ;2   ;Field     ;
                Name=File Extension;
                ToolTipML=ENU=Specifies the file type of the attached file.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="File Extension";
                Editable=FALSE;
                ImplicitType=Text30 }

    { 11  ;2   ;Field     ;
                Name=Created Date-Time;
                ToolTipML=ENU=Specifies when the incoming document line was created.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Created Date-Time";
                Editable=FALSE;
                ImplicitType=DateTime }

  }
  CODE
  {
    VAR
      CameraProvider@1003 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraProvider" WITHEVENTS RUNONCLIENT;
      CameraAvailable@1002 : Boolean;
      NotSupportedMsg@1000 : TextConst 'ENU=Cannot view this file type.';
      PhotoLbl@1001 : TextConst '@@@="%1 = a number, e.g. 1, 2, 3,...";ENU=Photo %1';
      MovieLbl@1005 : TextConst '@@@="%1 = a number, e.g. 1, 2, 3,...";ENU=Movie %1';

    LOCAL PROCEDURE NameDrillDown@1();
    BEGIN
      CALCFIELDS(Content);
      if not Content.HASVALUE then
        exit;

      case Type of
        Type::Image:
          PAGE.RUNMODAL(PAGE::"O365 Incoming Doc. Att. Pict.",Rec);
        Type::PDF:
          Export(Name + '.' + "File Extension",true)
        else
          MESSAGE(NotSupportedMsg);
      end;
      CurrPage.UPDATE(false);
    END;

    [External]
    PROCEDURE ImportNewFile@2();
    BEGIN
      if CameraAvailable then
        ImportFromCamera
      else
        ImportFromFileSystem;
    END;

    LOCAL PROCEDURE ImportFromCamera@4();
    VAR
      CameraOptions@1001 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
    BEGIN
      if not CameraAvailable then
        exit;

      CameraOptions := CameraOptions.CameraOptions;
      CameraOptions.SourceType := 'photolibrary';
      CameraProvider.RequestPictureAsync(CameraOptions);
    END;

    LOCAL PROCEDURE ImportFromFileSystem@5();
    VAR
      IncomingDocumentAttachment@1000 : Record "Incoming Document Attachment";
    BEGIN
      IncomingDocumentAttachment.INIT;
      IncomingDocumentAttachment.COPYFILTERS(Rec);
      IncomingDocumentAttachment.NewAttachment;
      case IncomingDocumentAttachment.Type of
        IncomingDocumentAttachment.Type::Image:
          IncomingDocumentAttachment.Name :=
            COPYSTR(
              STRSUBSTNO(PhotoLbl,IncomingDocumentAttachment."Line No." div 10000),1,MAXSTRLEN(IncomingDocumentAttachment.Name));
        IncomingDocumentAttachment.Type::Other:
          if UPPERCASE(IncomingDocumentAttachment."File Extension") = 'MOV' then
            IncomingDocumentAttachment.Name :=
              COPYSTR(
                STRSUBSTNO(MovieLbl,IncomingDocumentAttachment."Line No." div 10000),1,MAXSTRLEN(IncomingDocumentAttachment.Name));
      end;
      IncomingDocumentAttachment.MODIFY;
    END;

    [TryFunction]
    [External]
    PROCEDURE TakeNewPicture@7();
    VAR
      CameraOptions@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
    BEGIN
      if not CameraAvailable then
        exit;
      CameraOptions := CameraOptions.CameraOptions;
      CameraOptions.Quality := 50;
      CameraProvider.RequestPictureAsync(CameraOptions);
    END;

    [External]
    PROCEDURE GetCameraAvailable@3() : Boolean;
    BEGIN
      exit(CameraProvider.IsAvailable);
    END;

    EVENT CameraProvider@1003::PictureAvailable@10(PictureName@1001 : Text;PictureFilePath@1000 : Text);
    VAR
      ImportAttachmentIncDoc@1002 : Codeunit "Import Attachment - Inc. Doc.";
    BEGIN
      if (PictureName = '') or (PictureFilePath = '') then
        exit;

      ImportAttachmentIncDoc.ProcessAndUploadPicture(PictureFilePath,Rec);
    END;

    BEGIN
    END.
  }
}

