OBJECT Codeunit 9510 Document Service Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NoConfigErr@1000 : TextConst 'ENU=No online document configuration was found.';
      MultipleConfigsErr@1001 : TextConst 'ENU=More than one online document configuration was found.';
      SourceFileNotFoundErr@1002 : TextConst '@@@="%1=Full path to the file on disk;%2=the detailed error describing why the document could not be accessed.";ENU=Cannot open the specified document from the following location: %1 due to the following error: %2.';
      RequiredSourceNameErr@1004 : TextConst 'ENU=You must specify a source path for the document.';
      DocumentService@1007 : DotNet "'Microsoft.Dynamics.Nav.DocumentService.Types, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentService.Types.IDocumentService";
      DocumentServiceFactory@1008 : DotNet "'Microsoft.Dynamics.Nav.DocumentService, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentService.DocumentServiceFactory";
      ServiceType@1006 : Text;
      LastServiceType@1009 : Text;
      RequiredTargetNameErr@1005 : TextConst 'ENU=You must specify a name for the document.';
      RequiredTargetURIErr@1011 : TextConst 'ENU=You must specify the URI that you want to open.';
      ValidateConnectionErr@1003 : TextConst 'ENU=Cannot connect because the user name and password have not been specified, or because the connection was canceled.';

    [Internal]
    PROCEDURE TestConnection@9();
    VAR
      DocumentServiceHelper@1000 : DotNet "'Microsoft.Dynamics.Nav.NavUserAccount, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.NavDocumentService.NavDocumentServiceHelper";
    BEGIN
      // Tests connectivity to the Document Service using the current configuration in Dynamics NAV.
      // An error occurrs if unable to successfully connect.
      if not IsConfigured then
        ERROR(NoConfigErr);
      DocumentServiceHelper.Reset;
      SetDocumentService;
      SetProperties;
      if ISNULL(DocumentService.Credentials) then
        ERROR(ValidateConnectionErr);
      DocumentService.ValidateConnection();
      CheckError;
    END;

    [Internal]
    PROCEDURE SaveFile@10(SourcePath@1000 : Text;TargetName@1002 : Text;Overwrite@1001 : Boolean) : Text;
    VAR
      SourceFile@1003 : File;
      SourceStream@1004 : InStream;
    BEGIN
      // Saves a file to the Document Service using the configured location specified in Dynamics NAV.
      // SourcePath: The path to a physical file on the Dynamics NAV server.
      // TargetName: The name which will be given to the file saved to the Document Service.
      // Overwrite: TRUE if the target file should be overwritten.
      // - An error is shown if Overwrite is FALSE and a file with that name already exists.
      // Returns: A URI to the file on the Document Service.

      if SourcePath = '' then
        ERROR(RequiredSourceNameErr);

      if TargetName = '' then
        ERROR(RequiredTargetNameErr);

      if not IsConfigured then
        ERROR(NoConfigErr);

      if not SourceFile.OPEN(SourcePath) then
        ERROR(SourceFileNotFoundErr,SourcePath,GETLASTERRORTEXT);

      SourceFile.CREATEINSTREAM(SourceStream);

      exit(SaveStream(SourceStream,TargetName,Overwrite));
    END;

    [External]
    PROCEDURE IsConfigured@2() : Boolean;
    VAR
      DocumentServiceRec@1000 : Record "Document Service";
    BEGIN
      // Returns TRUE if Dynamics NAV has been configured with a Document Service.

      with DocumentServiceRec do begin
        if COUNT > 1 then
          ERROR(MultipleConfigsErr);

        if not FINDFIRST then
          exit(false);

        if (Location = '') or (Folder = '') then
          exit(false);
      end;

      exit(true);
    END;

    [Internal]
    PROCEDURE IsServiceUri@3(TargetURI@1000 : Text) : Boolean;
    VAR
      DocumentServiceRec@1001 : Record "Document Service";
      IsValid@1002 : Boolean;
    BEGIN
      // Returns TRUE if the TargetURI points to a location on the currently-configured Document Service.

      if TargetURI = '' then
        exit(false);

      with DocumentServiceRec do begin
        if FINDLAST then
          if Location <> '' then begin
            SetDocumentService;
            SetProperties;
            IsValid := DocumentService.IsValidUri(TargetURI);
            CheckError;
            exit(IsValid);
          end
      end;

      exit(false);
    END;

    [Internal]
    PROCEDURE SetServiceType@6(RequestedServiceType@1000 : Text);
    VAR
      DocumentServiceHelper@1001 : DotNet "'Microsoft.Dynamics.Nav.NavUserAccount, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.NavDocumentService.NavDocumentServiceHelper";
    BEGIN
      // Sets the type name of the Document Service.
      // The type must match the DocumentServiceMetadata attribute value on the IDocumentServiceHandler interface
      // exposed by at least one assembly in the Server installation folder.
      // By default, Dynamics NAV uses the SharePoint Online Document Service with type named 'SHAREPOINTONLINE'.
      ServiceType := RequestedServiceType;
      DocumentServiceHelper.SetDocumentServiceType(RequestedServiceType);
    END;

    [External]
    PROCEDURE GetServiceType@7() : Text;
    BEGIN
      // Gets the name of the current Document Service.

      exit(ServiceType);
    END;

    [Internal]
    PROCEDURE OpenDocument@8(TargetURI@1000 : Text);
    BEGIN
      // Navigates to the specified URI on the Document Service from the client device.

      if TargetURI = '' then
        ERROR(RequiredTargetURIErr);

      if not IsConfigured then
        ERROR(NoConfigErr);

      SetDocumentService;
      HYPERLINK(DocumentService.GenerateViewableDocumentAddress(TargetURI));
      CheckError;
    END;

    LOCAL PROCEDURE SetProperties@34();
    VAR
      DocumentServiceRec@1000 : Record "Document Service";
      DocumentServiceHelper@1001 : DotNet "'Microsoft.Dynamics.Nav.NavUserAccount, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.NavDocumentService.NavDocumentServiceHelper";
    BEGIN
      with DocumentServiceRec do begin
        if not FINDFIRST then
          ERROR(NoConfigErr);

        // The Document Service will throw an exception if the property is not known to the service type provider.
        DocumentService.Properties.SetProperty(FIELDNAME(Description),Description);
        DocumentService.Properties.SetProperty(FIELDNAME(Location),Location);
        DocumentService.Properties.SetProperty(FIELDNAME("User Name"),"User Name");
        DocumentService.Properties.SetProperty(FIELDNAME(Password),Password);
        DocumentService.Properties.SetProperty(FIELDNAME("Document Repository"),"Document Repository");
        DocumentService.Properties.SetProperty(FIELDNAME(Folder),Folder);

        DocumentService.Credentials := DocumentServiceHelper.ProvideCredentials();
        if not (DocumentServiceHelper.LastErrorMessage = '') then
          ERROR(DocumentServiceHelper.LastErrorMessage);
      end;
    END;

    LOCAL PROCEDURE SetDocumentService@5();
    VAR
      RequestedServiceType@1000 : Text;
    BEGIN
      // Sets the Document Service for the current Service Type, reusing an existing service if possible.

      RequestedServiceType := GetServiceType;

      if RequestedServiceType = '' then
        RequestedServiceType := 'SHAREPOINTONLINE';

      if LastServiceType <> RequestedServiceType then begin
        DocumentService := DocumentServiceFactory.CreateService(RequestedServiceType);
        LastServiceType := RequestedServiceType;
      end;
    END;

    LOCAL PROCEDURE CheckError@12();
    BEGIN
      // Checks whether the Document Service received an error and displays that error to the user.

      if not ISNULL(DocumentService.LastError) and (DocumentService.LastError.Message <> '') then
        ERROR(DocumentService.LastError.Message);
    END;

    LOCAL PROCEDURE SaveStream@13(Stream@1000 : InStream;TargetName@1002 : Text;Overwrite@1001 : Boolean) : Text;
    VAR
      DocumentURI@1003 : Text;
    BEGIN
      // Saves a stream to the Document Service using the configured location specified in Dynamics NAV.
      SetDocumentService;
      SetProperties;

      DocumentURI := DocumentService.Save(Stream,TargetName,Overwrite);
      CheckError;

      exit(DocumentURI);
    END;

    BEGIN
    {
      Provides functions for the storage of documents to online services such as O365 (Office 365).
    }
    END.
  }
}

