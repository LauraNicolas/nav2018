OBJECT Codeunit 1318 Trial Balance Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ColumnLayoutArr@1019 : ARRAY [9,2] OF Record "Column Layout";
      AccScheduleLineArr@1018 : ARRAY [9] OF Record "Acc. Schedule Line";
      AccScheduleLine@1017 : Record "Acc. Schedule Line";
      TempColumnLayout@1016 : TEMPORARY Record "Column Layout";
      AccSchedManagement@1015 : Codeunit AccSchedManagement;
      PeriodType@1012 : 'Day,Week,Month,Quarter,Year,"Accounting Period"';
      CurrentColumnLayoutName@1001 : Code[10];
      CurrentAccScheduleName@1000 : Code[10];
      LessRowsThanExpectedErr@1002 : TextConst 'ENU=The Trial Balance chart is not set up correctly. There are fewer rows in the account schedules than expected.';
      MoreRowsThanExpectedErr@1003 : TextConst 'ENU=The Trial Balance chart is not set up correctly. There are more rows in the account schedules than expected.';

    [Internal]
    PROCEDURE LoadData@7(VAR DescriptionsArr@1003 : ARRAY [9] OF Text[80];VAR ValuesArr@1002 : ARRAY [9,2] OF Decimal;VAR PeriodCaptionTxt@1001 : ARRAY [2] OF Text;NoOfColumns@1004 : Integer);
    BEGIN
      PeriodType := PeriodType::"Accounting Period";
      Initialize;
      AccSchedManagement.FindPeriod(AccScheduleLine,'',PeriodType);
      UpdateArrays(DescriptionsArr,ValuesArr,PeriodCaptionTxt,NoOfColumns);
    END;

    LOCAL PROCEDURE Initialize@1();
    VAR
      TrialBalanceSetup@1000 : Record "Trial Balance Setup";
    BEGIN
      TrialBalanceSetup.GET;
      TrialBalanceSetup.TESTFIELD("Account Schedule Name");
      TrialBalanceSetup.TESTFIELD("Column Layout Name");

      CurrentColumnLayoutName := TrialBalanceSetup."Column Layout Name";
      CurrentAccScheduleName := TrialBalanceSetup."Account Schedule Name";

      AccSchedManagement.CopyColumnsToTemp(CurrentColumnLayoutName,TempColumnLayout);
      AccSchedManagement.OpenSchedule(CurrentAccScheduleName,AccScheduleLine);
      AccSchedManagement.OpenColumns(CurrentColumnLayoutName,TempColumnLayout);
      AccSchedManagement.CheckAnalysisView(CurrentAccScheduleName,CurrentColumnLayoutName,true);
    END;

    LOCAL PROCEDURE UpdateArrays@131(VAR DescriptionsArr@1002 : ARRAY [9] OF Text[80];VAR ValuesArr@1001 : ARRAY [9,2] OF Decimal;VAR PeriodCaptionTxt@1000 : ARRAY [2] OF Text;NoOfColumns@1008 : Integer);
    VAR
      Offset@1007 : Integer;
      Counter@1006 : Integer;
      FromDate@1005 : Date;
      ToDate@1004 : Date;
      FiscalStartDate@1003 : Date;
      I@1009 : Integer;
      TempNoOfColumns@1010 : Integer;
    BEGIN
      CLEAR(PeriodCaptionTxt);
      Counter := 0;

      if AccScheduleLine.FINDSET then
        repeat
          Counter := Counter + 1;
          if Counter > ARRAYLEN(ValuesArr,1) then
            ERROR(MoreRowsThanExpectedErr);

          DescriptionsArr[Counter] := AccScheduleLine.Description;

          if NoOfColumns = 1 then
            Offset := 1
          else
            Offset := 2;

          if NoOfColumns > Offset then
            TempNoOfColumns := Offset
          else
            TempNoOfColumns := NoOfColumns;

          if AccScheduleLine.Totaling = '' then
            for I := Offset - NoOfColumns + 1 to Offset do
              ValuesArr[Counter,I] := 0;

          if TempColumnLayout.FINDSET then
            repeat
              ValuesArr[Counter,Offset] := AccSchedManagement.CalcCell(AccScheduleLine,TempColumnLayout,false);
              ColumnLayoutArr[Counter,Offset] := TempColumnLayout;
              AccScheduleLineArr[Counter] := AccScheduleLine;
              AccSchedManagement.CalcColumnDates(
                TempColumnLayout."Comparison Date Formula",TempColumnLayout."Comparison Period Formula",
                FromDate,ToDate,FiscalStartDate);
              PeriodCaptionTxt[Offset] := STRSUBSTNO('%1..%2',FromDate,ToDate);
              Offset := Offset - 1;
              TempNoOfColumns := TempNoOfColumns - 1;
            until (TempColumnLayout.NEXT = 0) or (TempNoOfColumns = 0);
        until AccScheduleLine.NEXT = 0;

      if Counter < ARRAYLEN(ValuesArr,1) then
        ERROR(LessRowsThanExpectedErr);
    END;

    [External]
    PROCEDURE DrillDown@5(RowNo@1001 : Integer;ColumnNo@1000 : Integer);
    BEGIN
      TempColumnLayout := ColumnLayoutArr[RowNo,ColumnNo];
      AccScheduleLine := AccScheduleLineArr[RowNo];
      AccSchedManagement.DrillDown(TempColumnLayout,AccScheduleLine,PeriodType::Month);
    END;

    [Internal]
    PROCEDURE NextPeriod@8(VAR DescriptionsArr@1001 : ARRAY [9] OF Text[80];VAR ValuesArr@1000 : ARRAY [9,2] OF Decimal;VAR PeriodCaptionTxt@1003 : ARRAY [2] OF Text;NoOfColumns@1002 : Integer);
    BEGIN
      UpdatePeriod(DescriptionsArr,ValuesArr,PeriodCaptionTxt,'>=',NoOfColumns);
    END;

    [Internal]
    PROCEDURE PreviousPeriod@9(VAR DescriptionsArr@1001 : ARRAY [9] OF Text[80];VAR ValuesArr@1000 : ARRAY [9,2] OF Decimal;VAR PeriodCaptionTxt@1002 : ARRAY [2] OF Text;NoOfColumns@1003 : Integer);
    BEGIN
      UpdatePeriod(DescriptionsArr,ValuesArr,PeriodCaptionTxt,'<=',NoOfColumns);
    END;

    LOCAL PROCEDURE UpdatePeriod@6(VAR DescriptionsArr@1003 : ARRAY [9] OF Text[80];VAR ValuesArr@1002 : ARRAY [9,2] OF Decimal;VAR PeriodCaptionTxt@1001 : ARRAY [2] OF Text;SearchText@1000 : Text[3];NoOfColumns@1004 : Integer);
    BEGIN
      AccSchedManagement.FindPeriod(AccScheduleLine,SearchText,PeriodType);
      UpdateArrays(DescriptionsArr,ValuesArr,PeriodCaptionTxt,NoOfColumns);
    END;

    BEGIN
    END.
  }
}

