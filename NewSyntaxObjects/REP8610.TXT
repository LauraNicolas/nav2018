OBJECT Report 8610 Create G/L Acc. Journal Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create G/L Acc. Journal Lines;
    ProcessingOnly=true;
    OnPostReport=BEGIN
                   MESSAGE(Text004);
                 END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;G/L Account         ;
               DataItemTable="G/L Account";
               DataItemTableView=SORTING("No.");
               OnPreDataItem=BEGIN
                               CheckJournalTemplate;
                               CheckBatchName;
                               CheckPostingDate;

                               GenJnlLine.SETRANGE("Journal Template Name",JournalTemplate);
                               GenJnlLine.SETRANGE("Journal Batch Name",BatchName);
                               if GenJnlLine.FINDLAST then
                                 LineNo := GenJnlLine."Line No." + 10000
                               else
                                 LineNo := 10000;

                               GenJnlBatch.GET(JournalTemplate,BatchName);
                               if TemplateCode <> '' then
                                 StdGenJournal.GET(JournalTemplate,TemplateCode);
                             END;

               OnAfterGetRecord=VAR
                                  StdGenJournalLine@1000 : Record "Standard General Journal Line";
                                BEGIN
                                  GenJnlLine.INIT;
                                  if GetStandardJournalLine then begin
                                    Initialize(StdGenJournal,GenJnlBatch.Name);

                                    StdGenJournalLine.SETRANGE("Journal Template Name",StdGenJournal."Journal Template Name");
                                    StdGenJournalLine.SETRANGE("Standard Journal Code",StdGenJournal.Code);
                                    if StdGenJournalLine.FINDSET then
                                      repeat
                                        CopyGenJnlFromStdJnl(StdGenJournalLine,GenJnlLine);
                                        GenJnlLine.VALIDATE("Document Type",DocumentTypes);
                                        GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"G/L Account");
                                        GenJnlLine.VALIDATE("Account No.","No.");
                                        GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
                                        if PostingDate <> 0D then
                                          GenJnlLine.VALIDATE("Posting Date",PostingDate);
                                        GenJnlLine.MODIFY(true);
                                      until StdGenJournalLine.NEXT = 0;
                                  end else begin
                                    GenJnlLine.VALIDATE("Journal Template Name",JournalTemplate);
                                    GenJnlLine.VALIDATE("Journal Batch Name",BatchName);
                                    GenJnlLine."Line No." := LineNo;
                                    LineNo := LineNo + 10000;

                                    GenJnlLine.VALIDATE("Document Type",DocumentTypes);
                                    GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"G/L Account");
                                    GenJnlLine.VALIDATE("Account No.","No.");
                                    GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");

                                    if PostingDate <> 0D then
                                      GenJnlLine.VALIDATE("Posting Date",PostingDate);

                                    if not GenJnlLine.INSERT(true) then
                                      GenJnlLine.MODIFY(true);
                                  end;
                                END;

               ReqFilterFields="No.","Account Type",Blocked,"Direct Posting","No. 2" }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   if PostingDate = 0D then
                     PostingDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 2   ;2   ;Field     ;
                  Name=Document Type;
                  CaptionML=ENU=Document Type;
                  ToolTipML=ENU=Specifies the document type of the journal line.;
                  OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                  ApplicationArea=#Advanced;
                  SourceExpr=DocumentTypes }

      { 4   ;2   ;Field     ;
                  Name=Posting Date;
                  CaptionML=ENU=Posting Date;
                  ToolTipML=ENU=Specifies the posting date of the journal line.;
                  ApplicationArea=#Advanced;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               CheckPostingDate;
                             END;
                              }

      { 6   ;2   ;Field     ;
                  Name=Journal Template;
                  CaptionML=ENU=Journal Template;
                  ToolTipML=ENU=Specifies the name of journal to which the lines will be added.;
                  ApplicationArea=#Advanced;
                  SourceExpr=JournalTemplate;
                  TableRelation="Gen. Journal Batch".Name;
                  OnValidate=BEGIN
                               CheckJournalTemplate;
                             END;

                  OnLookup=VAR
                             GenJnlTemplate@1001 : Record "Gen. Journal Template";
                             GenJnlTemplates@1000 : Page "General Journal Templates";
                           BEGIN
                             GenJnlTemplate.SETRANGE(Type,GenJnlTemplate.Type::General);
                             GenJnlTemplate.SETRANGE(Recurring,false);
                             GenJnlTemplates.SETTABLEVIEW(GenJnlTemplate);

                             GenJnlTemplates.LOOKUPMODE := true;
                             GenJnlTemplates.EDITABLE := false;
                             if GenJnlTemplates.RUNMODAL = ACTION::LookupOK then begin
                               GenJnlTemplates.GETRECORD(GenJnlTemplate);
                               JournalTemplate := GenJnlTemplate.Name;
                             end;
                           END;
                            }

      { 8   ;2   ;Field     ;
                  Name=Batch Name;
                  CaptionML=ENU=Batch Name;
                  ToolTipML=ENU=Specifies the name of the batch.;
                  ApplicationArea=#Advanced;
                  SourceExpr=BatchName;
                  TableRelation="Gen. Journal Batch".Name;
                  OnValidate=BEGIN
                               CheckBatchName;
                             END;

                  OnLookup=VAR
                             GenJnlBatches@1001 : Page "General Journal Batches";
                           BEGIN
                             if JournalTemplate <> '' then begin
                               GenJnlBatch.SETRANGE("Journal Template Name",JournalTemplate);
                               GenJnlBatches.SETTABLEVIEW(GenJnlBatch);
                             end;

                             GenJnlBatches.LOOKUPMODE := true;
                             GenJnlBatches.EDITABLE := false;
                             if GenJnlBatches.RUNMODAL = ACTION::LookupOK then begin
                               GenJnlBatches.GETRECORD(GenJnlBatch);
                               BatchName := GenJnlBatch.Name;
                             end;
                           END;
                            }

      { 10  ;2   ;Field     ;
                  Name=Standard General Journal;
                  CaptionML=ENU=Standard General Journal;
                  ToolTipML=ENU=Specifies the general journal for which you are creating lines.;
                  ApplicationArea=#Advanced;
                  SourceExpr=TemplateCode;
                  TableRelation="Standard General Journal".Code;
                  OnLookup=VAR
                             StdGenJournal1@1005 : Record "Standard General Journal";
                             StdGenJnls@1000 : Page "Standard General Journals";
                           BEGIN
                             if JournalTemplate <> '' then begin
                               StdGenJournal1.SETRANGE("Journal Template Name",JournalTemplate);
                               StdGenJnls.SETTABLEVIEW(StdGenJournal1);
                             end;

                             StdGenJnls.LOOKUPMODE := true;
                             StdGenJnls.EDITABLE := false;
                             if StdGenJnls.RUNMODAL = ACTION::LookupOK then begin
                               StdGenJnls.GETRECORD(StdGenJournal1);
                               TemplateCode := StdGenJournal1.Code;
                             end;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      StdGenJournal@1007 : Record "Standard General Journal";
      GenJnlBatch@1006 : Record "Gen. Journal Batch";
      GenJnlLine@1005 : Record "Gen. Journal Line";
      LastGenJnlLine@1002 : Record "Gen. Journal Line";
      DocumentTypes@1000 : '" ",Payment,Invoice,"Credit Memo","Finance Charge Memo",Reminder,Refund';
      PostingDate@1001 : Date;
      BatchName@1003 : Code[10];
      TemplateCode@1004 : Code[20];
      LineNo@1008 : Integer;
      Text001@1014 : TextConst 'ENU=Gen. Journal Template name is blank.';
      Text002@1009 : TextConst 'ENU=Gen. Journal Batch name is blank.';
      JournalTemplate@1012 : Text[10];
      Text004@1016 : TextConst 'ENU=General journal lines are successfully created.';
      PostingDateIsEmptyErr@1010 : TextConst 'ENU=The posting date is empty.';

    LOCAL PROCEDURE GetStandardJournalLine@3() : Boolean;
    VAR
      StdGenJounalLine@1000 : Record "Standard General Journal Line";
    BEGIN
      if TemplateCode = '' then
        exit;
      StdGenJounalLine.SETRANGE("Journal Template Name",StdGenJournal."Journal Template Name");
      StdGenJounalLine.SETRANGE("Standard Journal Code",StdGenJournal.Code);
      exit(StdGenJounalLine.FINDFIRST);
    END;

    [External]
    PROCEDURE Initialize@15(VAR StdGenJnl@1001 : Record "Standard General Journal";JnlBatchName@1000 : Code[10]);
    BEGIN
      GenJnlLine."Journal Template Name" := StdGenJnl."Journal Template Name";
      GenJnlLine."Journal Batch Name" := JnlBatchName;
      GenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      LastGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      LastGenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      if LastGenJnlLine.FINDLAST then;

      GenJnlBatch.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlBatch.SETRANGE(Name,JnlBatchName);

      if GenJnlBatch.FINDFIRST then;
    END;

    LOCAL PROCEDURE CopyGenJnlFromStdJnl@1(StdGenJnlLine@1001 : Record "Standard General Journal Line";VAR GenJnlLine@1002 : Record "Gen. Journal Line");
    VAR
      GenJnlManagement@1000 : Codeunit GenJnlManagement;
      Balance@1007 : Decimal;
      TotalBalance@1006 : Decimal;
      ShowBalance@1005 : Boolean;
      ShowTotalBalance@1004 : Boolean;
    BEGIN
      GenJnlLine.INIT;
      GenJnlLine."Line No." := 0;
      GenJnlManagement.CalcBalance(GenJnlLine,LastGenJnlLine,Balance,TotalBalance,ShowBalance,ShowTotalBalance);
      GenJnlLine.SetUpNewLine(LastGenJnlLine,Balance,true);
      if LastGenJnlLine."Line No." <> 0 then
        GenJnlLine."Line No." := LastGenJnlLine."Line No." + 10000
      else
        GenJnlLine."Line No." := 10000;

      GenJnlLine.TRANSFERFIELDS(StdGenJnlLine,false);
      GenJnlLine.UpdateLineBalance;
      GenJnlLine.VALIDATE("Currency Code");

      if GenJnlLine."VAT Prod. Posting Group" <> '' then
        GenJnlLine.VALIDATE("VAT Prod. Posting Group");
      if (GenJnlLine."VAT %" <> 0) and GenJnlBatch."Allow VAT Difference" then
        GenJnlLine.VALIDATE("VAT Amount",StdGenJnlLine."VAT Amount");
      GenJnlLine.VALIDATE("Bal. VAT Prod. Posting Group");

      if GenJnlBatch."Allow VAT Difference" then
        GenJnlLine.VALIDATE("Bal. VAT Amount",StdGenJnlLine."Bal. VAT Amount");
      GenJnlLine.INSERT(true);

      LastGenJnlLine := GenJnlLine;
    END;

    [External]
    PROCEDURE InitializeRequest@2(DocumentTypesFrom@1000 : Option;PostingDateFrom@1001 : Date;JournalTemplateFrom@1002 : Text[10];BatchNameFrom@1003 : Code[10];StandardTemplateCodeFrom@1004 : Code[20]);
    BEGIN
      DocumentTypes := DocumentTypesFrom;
      PostingDate := PostingDateFrom;
      JournalTemplate := JournalTemplateFrom;
      BatchName := BatchNameFrom;
      TemplateCode := StandardTemplateCodeFrom;
    END;

    LOCAL PROCEDURE CheckPostingDate@24();
    BEGIN
      if PostingDate = 0D then
        ERROR(PostingDateIsEmptyErr);
    END;

    LOCAL PROCEDURE CheckBatchName@32();
    BEGIN
      if BatchName = '' then
        ERROR(Text002);
    END;

    LOCAL PROCEDURE CheckJournalTemplate@33();
    BEGIN
      if JournalTemplate = '' then
        ERROR(Text001);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

