OBJECT Codeunit 5454 Graph Sync. Runner - OnDelete
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Integration Record Archive";
    OnRun=VAR
            IntegrationTableMapping@1006 : Record "Integration Table Mapping";
            GraphIntegrationRecord@1007 : Record "Graph Integration Record";
            GraphConnectionSetup@1001 : Codeunit "Graph Connection Setup";
            GraphDataSetup@1002 : Codeunit "Graph Data Setup";
            GraphIntegrationTableSync@1005 : Codeunit "Graph Integration Table Sync";
            GraphSyncRunner@1000 : Codeunit "Graph Sync. Runner";
            GraphRecordRef@1003 : RecordRef;
            GraphIdFieldRef@1011 : FieldRef;
            SynchronizeConnectionName@1004 : Text;
            IntegrationMappingCode@1008 : Code[20];
          BEGIN
            if ISTEMPORARY then
              exit;

            if not GraphSyncRunner.IsGraphSyncEnabled then
              exit;

            if not GraphConnectionSetup.CanRunSync then
              exit;

            GraphConnectionSetup.RegisterConnections;
            SynchronizeConnectionName := GraphConnectionSetup.GetSynchronizeConnectionName("Table ID");
            IntegrationMappingCode := GraphDataSetup.GetMappingCodeForTable("Table ID");

            SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::MicrosoftGraph,SynchronizeConnectionName,true);
            GraphDataSetup.GetIntegrationTableMapping(IntegrationTableMapping,IntegrationMappingCode);

            GraphIntegrationRecord.SETRANGE("Integration ID","Integration ID");
            GraphIntegrationRecord.SETRANGE("Table ID",IntegrationTableMapping."Table ID");
            if GraphIntegrationRecord.FINDFIRST then begin
              GraphRecordRef.OPEN(IntegrationTableMapping."Integration Table ID");
              GraphIdFieldRef := GraphRecordRef.FIELD(IntegrationTableMapping."Integration Table UID Fld. No.");
              GraphIdFieldRef.SETRANGE(GraphIntegrationRecord."Graph ID");
              if GraphRecordRef.FINDFIRST then
                GraphIntegrationTableSync.PerformRecordDeleteToIntegrationTable(IntegrationTableMapping,GraphRecordRef);
            end;
          END;

  }
  CODE
  {

    BEGIN
    END.
  }
}

