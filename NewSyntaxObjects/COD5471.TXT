OBJECT Codeunit 5471 Graph Mgt - Customer
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ProcessComplexTypes@8(VAR Customer@1000 : Record Customer;PaymentTermsJSON@1003 : Text;ShipmentMethodJSON@1005 : Text;PaymentMethodJSON@1001 : Text;PostalAddressJSON@1002 : Text);
    BEGIN
      UpdatePaymentTerms(PaymentTermsJSON,Customer);
      UpdateShipmentMethod(ShipmentMethodJSON,Customer);
      UpdatePaymentMethod(PaymentMethodJSON,Customer);
      UpdatePostalAddress(PostalAddressJSON,Customer);
    END;

    PROCEDURE PaymentTermsToJSON@1(PaymentTermsCode@1000 : Code[10]) JSON : Text;
    VAR
      PaymentTerms@1001 : Record "Payment Terms";
      GraphMgtComplexTypes@1002 : Codeunit "Graph Mgt - Complex Types";
    BEGIN
      if PaymentTerms.GET(PaymentTermsCode) then
        GraphMgtComplexTypes.GetCodeAndDescriptionJSON(PaymentTerms.Code,PaymentTerms.Description,JSON);
    END;

    PROCEDURE ShipmentMethodToJSON@2(ShipmentMethodCode@1000 : Code[10]) JSON : Text;
    VAR
      ShipmentMethod@1001 : Record "Shipment Method";
      GraphMgtComplexTypes@1002 : Codeunit "Graph Mgt - Complex Types";
    BEGIN
      if ShipmentMethod.GET(ShipmentMethodCode) then
        GraphMgtComplexTypes.GetCodeAndDescriptionJSON(ShipmentMethod.Code,ShipmentMethod.Description,JSON);
    END;

    PROCEDURE PaymentMethodToJSON@9(PaymentMethodCode@1000 : Code[10]) JSON : Text;
    VAR
      PaymentMethod@1001 : Record "Payment Method";
      GraphMgtComplexTypes@1002 : Codeunit "Graph Mgt - Complex Types";
    BEGIN
      if PaymentMethod.GET(PaymentMethodCode) then
        GraphMgtComplexTypes.GetCodeAndDescriptionJSON(PaymentMethod.Code,PaymentMethod.Description,JSON);
    END;

    PROCEDURE PostalAddressToJSON@5(Customer@1000 : Record Customer) JSON : Text;
    VAR
      GraphMgtComplexTypes@1001 : Codeunit "Graph Mgt - Complex Types";
    BEGIN
      with Customer do
        GraphMgtComplexTypes.GetPostalAddressJSON(Address,"Address 2",City,County,"Country/Region Code","Post Code",JSON);
    END;

    PROCEDURE UpdateIntegrationRecords@11(OnlyCustomersWithoutId@1000 : Boolean);
    VAR
      DummyCustomer@1002 : Record Customer;
      GraphMgtGeneralTools@1001 : Codeunit "Graph Mgt - General Tools";
      CustomerRecordRef@1019 : RecordRef;
    BEGIN
      CustomerRecordRef.OPEN(DATABASE::Customer);
      GraphMgtGeneralTools.UpdateIntegrationRecords(CustomerRecordRef,DummyCustomer.FIELDNO(Id),OnlyCustomersWithoutId);
    END;

    PROCEDURE UpdatePaymentTerms@13(PaymentTermsJSON@1000 : Text;VAR Customer@1001 : Record Customer);
    VAR
      GraphMgtComplexTypes@1006 : Codeunit "Graph Mgt - Complex Types";
      PreviousCode@1004 : Code[10];
      PreviousJSONString@1005 : Text;
      NewCode@1007 : Code[10];
      NewDescription@1008 : Text[50];
    BEGIN
      if PaymentTermsJSON = '' then
        exit;

      PreviousCode := Customer."Payment Terms Code";
      PreviousJSONString := PaymentTermsToJSON(PreviousCode);

      if PaymentTermsJSON <> PreviousJSONString then begin
        GraphMgtComplexTypes.GetCodeAndDescriptionFromJSON(PaymentTermsJSON,NewCode,NewDescription);
        Customer.VALIDATE("Payment Terms Code",NewCode);
      end;
    END;

    PROCEDURE UpdateShipmentMethod@14(ShipmentMethodJSON@1000 : Text;VAR Customer@1001 : Record Customer);
    VAR
      GraphMgtComplexTypes@1006 : Codeunit "Graph Mgt - Complex Types";
      PreviousCode@1002 : Code[10];
      PreviousJSONString@1003 : Text;
      NewCode@1004 : Code[10];
      NewDescription@1005 : Text[50];
    BEGIN
      if ShipmentMethodJSON = '' then
        exit;

      PreviousCode := Customer."Shipment Method Code";
      PreviousJSONString := ShipmentMethodToJSON(PreviousCode);

      if ShipmentMethodJSON <> PreviousJSONString then begin
        GraphMgtComplexTypes.GetCodeAndDescriptionFromJSON(ShipmentMethodJSON,NewCode,NewDescription);
        Customer.VALIDATE("Shipment Method Code",NewCode);
      end;
    END;

    PROCEDURE UpdatePaymentMethod@15(PaymentMethodJSON@1000 : Text;VAR Customer@1001 : Record Customer);
    VAR
      GraphMgtComplexTypes@1006 : Codeunit "Graph Mgt - Complex Types";
      PreviousCode@1005 : Code[10];
      PreviousJSONString@1004 : Text;
      NewCode@1003 : Code[10];
      NewDescription@1002 : Text[50];
    BEGIN
      if PaymentMethodJSON = '' then
        exit;

      PreviousCode := Customer."Payment Method Code";
      PreviousJSONString := PaymentMethodToJSON(PreviousCode);

      if PaymentMethodJSON <> PreviousJSONString then begin
        GraphMgtComplexTypes.GetCodeAndDescriptionFromJSON(PaymentMethodJSON,NewCode,NewDescription);
        Customer.VALIDATE("Payment Method Code",NewCode);
      end;
    END;

    LOCAL PROCEDURE UpdatePostalAddress@4(PostalAddressJSON@1000 : Text;VAR Customer@1001 : Record Customer);
    VAR
      GraphMgtComplexTypes@1002 : Codeunit "Graph Mgt - Complex Types";
      RecRef@1004 : RecordRef;
    BEGIN
      if PostalAddressJSON = '' then
        exit;

      with Customer do begin
        RecRef.GETTABLE(Customer);
        GraphMgtComplexTypes.ApplyPostalAddressFromJSON(PostalAddressJSON,RecRef,
          FIELDNO(Address),FIELDNO("Address 2"),FIELDNO(City),FIELDNO(County),FIELDNO("Country/Region Code"),FIELDNO("Post Code"));
        RecRef.SETTABLE(Customer);
      end;
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnItem@19(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummyCustomer@1004 : Record Customer;
      GraphMgtGeneralTools@1006 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(RecRef,NewId,Handled,DATABASE::Customer,DummyCustomer.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@21(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummyCustomer@1004 : Record Customer;
      GraphMgtGeneralTools@1007 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      GraphMgtGeneralTools.HandleGetPredefinedIdValue(Id,RecRef,Handled,DATABASE::Customer,DummyCustomer.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@16();
    BEGIN
      UpdateIntegrationRecords(false);
      UpdateIds;
    END;

    PROCEDURE UpdateIds@6();
    VAR
      Customer@1000 : Record Customer;
    BEGIN
      with Customer do begin
        if FINDSET then
          repeat
            UpdateCurrencyId;
            UpdateTaxAreaId;
            UpdatePaymentMethodId;
            UpdatePaymentTermsId;
            UpdateShipmentMethodId;
            MODIFY(false);
          until NEXT = 0;
      end;
    END;

    BEGIN
    END.
  }
}

