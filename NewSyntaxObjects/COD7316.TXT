OBJECT Codeunit 7316 Whse. Int. Put-away Release
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1005 : TextConst 'ENU=There is nothing to release for %1 %2.';
      Text001@1000 : TextConst 'ENU=You cannot reopen the whse. internal put-away because warehouse worksheet lines exist that must first be handled or deleted.';
      Text002@1002 : TextConst 'ENU=You cannot reopen the whse. internal put-away because warehouse activity lines exist that must first be handled or deleted.';

    [External]
    PROCEDURE Release@1(WhseInternalPutAwayHeader@1000 : Record "Whse. Internal Put-away Header");
    VAR
      Location@1004 : Record Location;
      WhsePutawayRqst@1003 : Record "Whse. Put-away Request";
      WhseInternalPutawayLine@1001 : Record "Whse. Internal Put-away Line";
    BEGIN
      with WhseInternalPutAwayHeader do begin
        if Status = Status::Released then
          exit;

        WhseInternalPutawayLine.SETRANGE("No.","No.");
        WhseInternalPutawayLine.SETFILTER(Quantity,'<>0');
        if not WhseInternalPutawayLine.FIND('-') then
          ERROR(Text000,TABLECAPTION,"No.");

        if "Location Code" <> '' then begin
          Location.GET("Location Code");
          Location.TESTFIELD("Require Put-away");
        end else
          CheckPutawayRequired("Location Code");

        repeat
          WhseInternalPutawayLine.TESTFIELD("Item No.");
          WhseInternalPutawayLine.TESTFIELD("Unit of Measure Code");
          if Location."Directed Put-away and Pick" then
            WhseInternalPutawayLine.TESTFIELD("From Zone Code");
          if Location."Bin Mandatory" then
            WhseInternalPutawayLine.TESTFIELD("From Bin Code");
        until WhseInternalPutawayLine.NEXT = 0;

        Status := Status::Released;
        MODIFY;

        CreateWhsePutawayRqst(WhseInternalPutAwayHeader);

        WhsePutawayRqst.SETRANGE(
          "Document Type",WhsePutawayRqst."Document Type"::"Internal Put-away");
        WhsePutawayRqst.SETRANGE("Document No.","No.");
        WhsePutawayRqst.SETRANGE(Status,Status::Open);
        WhsePutawayRqst.DELETEALL(true);

        COMMIT;
      end;
    END;

    [External]
    PROCEDURE Reopen@2(WhseInternalPutAwayHeader@1000 : Record "Whse. Internal Put-away Header");
    VAR
      WhsePutawayRqst@1001 : Record "Whse. Put-away Request";
      WhseWkshLine@1002 : Record "Whse. Worksheet Line";
      WhseActivLine@1003 : Record "Warehouse Activity Line";
    BEGIN
      with WhseInternalPutAwayHeader do begin
        if Status = Status::Open then
          exit;

        WhseWkshLine.SETCURRENTKEY("Whse. Document Type","Whse. Document No.");
        WhseWkshLine.SETRANGE("Whse. Document Type",WhseWkshLine."Whse. Document Type"::"Internal Put-away");
        WhseWkshLine.SETRANGE("Whse. Document No.","No.");
        if not WhseWkshLine.ISEMPTY then
          ERROR(Text001);

        WhseActivLine.SETCURRENTKEY("Whse. Document No.","Whse. Document Type","Activity Type");
        WhseActivLine.SETRANGE("Whse. Document No.","No.");
        WhseActivLine.SETRANGE("Whse. Document Type",WhseActivLine."Whse. Document Type"::"Internal Put-away");
        WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::"Put-away");
        if not WhseActivLine.ISEMPTY then
          ERROR(Text002);

        WhsePutawayRqst.SETRANGE("Document Type",WhsePutawayRqst."Document Type"::"Internal Put-away");
        WhsePutawayRqst.SETRANGE("Document No.","No.");
        WhsePutawayRqst.SETRANGE(Status,Status::Released);
        if WhsePutawayRqst.FIND('-') then
          repeat
            WhsePutawayRqst.Status := Status::Open;
            WhsePutawayRqst.MODIFY;
          until WhsePutawayRqst.NEXT = 0;

        Status := Status::Open;
        MODIFY;
      end;
    END;

    LOCAL PROCEDURE CreateWhsePutawayRqst@3(VAR WhseInternalPutAwayHeader@1000 : Record "Whse. Internal Put-away Header");
    VAR
      WhsePutawayRqst@1001 : Record "Whse. Put-away Request";
    BEGIN
      with WhseInternalPutAwayHeader do begin
        WhsePutawayRqst."Document Type" := WhsePutawayRqst."Document Type"::"Internal Put-away";
        WhsePutawayRqst."Document No." := "No.";
        WhsePutawayRqst.Status := Status;
        WhsePutawayRqst."Location Code" := "Location Code";
        WhsePutawayRqst."Zone Code" := "From Zone Code";
        WhsePutawayRqst."Bin Code" := "From Bin Code";
        "Document Status" := GetDocumentStatus(0);
        WhsePutawayRqst."Completely Put Away" :=
          "Document Status" = "Document Status"::"Completely Put Away";
        if not WhsePutawayRqst.INSERT then
          WhsePutawayRqst.MODIFY;
      end;
    END;

    BEGIN
    END.
  }
}

