OBJECT Report 513 Move IC Trans. to Partner Comp
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Move IC Trans. to Partner Comp;
    ProcessingOnly=true;
  }
  DATASET
  {
    { 1220;    ;DataItem;IC Outbox Transaction;
               DataItemTable="IC Outbox Transaction";
               DataItemTableView=SORTING("Transaction No.","IC Partner Code","Transaction Source","Document Type") ORDER(Ascending);
               OnPreDataItem=BEGIN
                               CompanyInfo.GET;
                               CompanyInfo.TESTFIELD("IC Partner Code");
                               GLSetup.GET;
                               GLSetup.TESTFIELD("LCY Code");
                             END;

               OnAfterGetRecord=BEGIN
                                  if CurrentPartner.Code <> "IC Partner Code" then
                                    CurrentPartner.GET("IC Partner Code");

                                  case "Line Action" of
                                    "Line Action"::"Send to IC Partner":
                                      ICInboxOutboxMgt.OutboxTransToInbox(
                                        "IC Outbox Transaction",TempICInboxTransaction,CompanyInfo."IC Partner Code");
                                    "Line Action"::"Return to Inbox":
                                      RecreateInboxTrans("IC Outbox Transaction");
                                  end;
                                END;

               OnPostDataItem=BEGIN
                                TransferToPartner;
                              END;
                               }

    { 9410;1   ;DataItem;IC Outbox Jnl. Line ;
               DataItemTable="IC Outbox Jnl. Line";
               DataItemTableView=SORTING("Transaction No.","IC Partner Code","Transaction Source","Line No.");
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxJnlLineToInbox(TempICInboxTransaction,"IC Outbox Jnl. Line",TempICInboxJnlLine);
                                END;

               DataItemLink="Transaction No."=FIELD("Transaction No."),"IC Partner Code"=FIELD("IC Partner Code"),"Transaction Source"=FIELD("Transaction Source") }

    { 9924;2   ;DataItem;IC Inbox/Outbox Jnl. Line Dim.;
               DataItemTable="IC Inbox/Outbox Jnl. Line Dim.";
               DataItemTableView=SORTING("Table ID","Transaction No.","IC Partner Code","Transaction Source","Line No.","Dimension Code") ORDER(Ascending) WHERE("Table ID"=CONST(415));
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxJnlLineDimToInbox(
                                      TempICInboxJnlLine,"IC Inbox/Outbox Jnl. Line Dim.",
                                      TempInboxOutboxJnlLineDim,DATABASE::"IC Inbox Jnl. Line");
                                END;

               DataItemLink="IC Partner Code"=FIELD("IC Partner Code"),"Transaction No."=FIELD("Transaction No."),"Line No."=FIELD("Line No.") }

    { 4982;1   ;DataItem;IC Outbox Sales Header;
               DataItemTable="IC Outbox Sales Header";
               DataItemTableView=SORTING("IC Transaction No.","IC Partner Code","Transaction Source");
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxSalesHdrToInbox(TempICInboxTransaction,"IC Outbox Sales Header",TempInboxPurchHeader);
                                END;

               DataItemLink="IC Partner Code"=FIELD("IC Partner Code"),"IC Transaction No."=FIELD("Transaction No."),"Transaction Source"=FIELD("Transaction Source") }

    { 6933;2   ;DataItem;IC Document Dimension SH;
               DataItemTable="IC Document Dimension";
               DataItemTableView=SORTING("Table ID","Transaction No.","IC Partner Code","Transaction Source","Line No.","Dimension Code") ORDER(Ascending) WHERE("Table ID"=CONST(426),"Line No."=CONST(0));
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                                      "IC Document Dimension SH",TempICDocDim,DATABASE::"IC Inbox Purchase Header",
                                      TempInboxPurchHeader."IC Partner Code",TempInboxPurchHeader."Transaction Source");
                                END;

               DataItemLink="Transaction No."=FIELD("IC Transaction No."),"IC Partner Code"=FIELD("IC Partner Code"),"Transaction Source"=FIELD("Transaction Source") }

    { 8929;2   ;DataItem;IC Outbox Sales Line;
               DataItemTable="IC Outbox Sales Line";
               DataItemTableView=SORTING("IC Transaction No.","IC Partner Code","Transaction Source");
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxSalesLineToInbox(TempICInboxTransaction,"IC Outbox Sales Line",TempInboxPurchLine);
                                END;

               DataItemLink="IC Transaction No."=FIELD("IC Transaction No."),"IC Partner Code"=FIELD("IC Partner Code"),"Transaction Source"=FIELD("Transaction Source") }

    { 1776;3   ;DataItem;IC Document Dimension SL;
               DataItemTable="IC Document Dimension";
               DataItemTableView=SORTING("Table ID","Transaction No.","IC Partner Code","Transaction Source","Line No.","Dimension Code") ORDER(Ascending) WHERE("Table ID"=CONST(427));
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                                      "IC Document Dimension SL",TempICDocDim,DATABASE::"IC Inbox Purchase Line",
                                      TempInboxPurchLine."IC Partner Code",TempInboxPurchLine."Transaction Source");
                                END;

               DataItemLink="Transaction No."=FIELD("IC Transaction No."),"IC Partner Code"=FIELD("IC Partner Code"),"Transaction Source"=FIELD("Transaction Source"),"Line No."=FIELD("Line No.") }

    { 5739;1   ;DataItem;IC Outbox Purchase Header;
               DataItemTable="IC Outbox Purchase Header";
               DataItemTableView=SORTING("IC Transaction No.","IC Partner Code","Transaction Source");
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxPurchHdrToInbox(TempICInboxTransaction,"IC Outbox Purchase Header",TempInboxSalesHeader);
                                END;

               DataItemLink="IC Partner Code"=FIELD("IC Partner Code"),"IC Transaction No."=FIELD("Transaction No."),"Transaction Source"=FIELD("Transaction Source") }

    { 8943;2   ;DataItem;IC Document Dimension PH;
               DataItemTable="IC Document Dimension";
               DataItemTableView=SORTING("Table ID","Transaction No.","IC Partner Code","Transaction Source","Line No.","Dimension Code") ORDER(Ascending) WHERE("Table ID"=CONST(428),"Line No."=CONST(0));
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                                      "IC Document Dimension PH",TempICDocDim,DATABASE::"IC Inbox Sales Header",
                                      TempInboxSalesHeader."IC Partner Code",TempInboxSalesHeader."Transaction Source");
                                END;

               DataItemLink="Transaction No."=FIELD("IC Transaction No."),"IC Partner Code"=FIELD("IC Partner Code"),"Transaction Source"=FIELD("Transaction Source") }

    { 4616;2   ;DataItem;IC Outbox Purchase Line;
               DataItemTable="IC Outbox Purchase Line";
               DataItemTableView=SORTING("IC Transaction No.","IC Partner Code","Transaction Source","Line No.");
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxPurchLineToInbox(TempICInboxTransaction,"IC Outbox Purchase Line",TempInboxSalesLine);
                                END;

               DataItemLink="IC Transaction No."=FIELD("IC Transaction No."),"IC Partner Code"=FIELD("IC Partner Code"),"Transaction Source"=FIELD("Transaction Source") }

    { 7965;3   ;DataItem;IC Document Dimension PL;
               DataItemTable="IC Document Dimension";
               DataItemTableView=SORTING("Table ID","Transaction No.","IC Partner Code","Transaction Source","Line No.","Dimension Code") ORDER(Ascending) WHERE("Table ID"=CONST(429));
               OnAfterGetRecord=BEGIN
                                  if "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" then
                                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                                      "IC Document Dimension PL",TempICDocDim,DATABASE::"IC Inbox Sales Line",
                                      TempInboxSalesLine."IC Partner Code",TempInboxSalesLine."Transaction Source");
                                END;

               DataItemLink="Transaction No."=FIELD("IC Transaction No."),"IC Partner Code"=FIELD("IC Partner Code"),"Transaction Source"=FIELD("Transaction Source"),"Line No."=FIELD("Line No.") }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CurrentPartner@1000 : Record "IC Partner";
      CompanyInfo@1001 : Record "Company Information";
      GLSetup@1010 : Record "General Ledger Setup";
      TempICInboxTransaction@1003 : TEMPORARY Record "IC Inbox Transaction";
      TempICInboxJnlLine@1006 : TEMPORARY Record "IC Inbox Jnl. Line";
      TempInboxPurchHeader@1013 : TEMPORARY Record "IC Inbox Purchase Header";
      TempInboxPurchLine@1014 : TEMPORARY Record "IC Inbox Purchase Line";
      TempInboxSalesHeader@1002 : TEMPORARY Record "IC Inbox Sales Header";
      TempInboxSalesLine@1004 : TEMPORARY Record "IC Inbox Sales Line";
      Text001@1007 : TextConst 'ENU=Your IC registration code %1 is not recognized by IC Partner %2.';
      TempInboxOutboxJnlLineDim@1005 : TEMPORARY Record "IC Inbox/Outbox Jnl. Line Dim.";
      TempICDocDim@1008 : TEMPORARY Record "IC Document Dimension";
      ICInboxOutboxMgt@1009 : Codeunit ICInboxOutboxMgt;
      Text002@1011 : TextConst 'ENU=%1 %2 to IC Partner %3 already exists in the IC inbox of IC Partner %3. IC Partner %3 must complete the line action for transaction %2 in their IC inbox.';

    LOCAL PROCEDURE TransferToPartner@9();
    VAR
      PartnerInboxTransaction@1004 : Record "IC Inbox Transaction";
      PartnerInboxJnlLine@1007 : Record "IC Inbox Jnl. Line";
      PartnerInboxSalesHeader@1003 : Record "IC Inbox Sales Header";
      PartnerInboxSalesLine@1002 : Record "IC Inbox Sales Line";
      PartnerInboxPurchHeader@1001 : Record "IC Inbox Purchase Header";
      PartnerInboxPurchLine@1000 : Record "IC Inbox Purchase Line";
      PartnerInboxOutboxJnlLineDim@1010 : Record "IC Inbox/Outbox Jnl. Line Dim.";
      PartnerICDocDim@1008 : Record "IC Document Dimension";
      PartnerICPartner@1005 : Record "IC Partner";
    BEGIN
      PartnerICPartner.CHANGECOMPANY(CurrentPartner."Inbox Details");
      if not PartnerICPartner.GET(CompanyInfo."IC Partner Code") then
        ERROR(Text001,CompanyInfo."IC Partner Code",CurrentPartner.Code);

      PartnerInboxTransaction.CHANGECOMPANY(CurrentPartner."Inbox Details");
      PartnerInboxTransaction.LOCKTABLE;
      if TempICInboxTransaction.FIND('-') then
        repeat
          PartnerInboxTransaction := TempICInboxTransaction;
          if not PartnerInboxTransaction.INSERT then
            ERROR(
              Text002,TempICInboxTransaction.FIELDCAPTION("Transaction No."),
              TempICInboxTransaction."Transaction No.",
              TempICInboxTransaction."IC Partner Code");
        until TempICInboxTransaction.NEXT = 0;

      PartnerInboxJnlLine.CHANGECOMPANY(CurrentPartner."Inbox Details");
      if TempICInboxJnlLine.FIND('-') then
        repeat
          PartnerInboxJnlLine := TempICInboxJnlLine;
          if PartnerInboxJnlLine."Currency Code" = '' then
            PartnerInboxJnlLine."Currency Code" := GLSetup."LCY Code";
          if PartnerInboxJnlLine."Currency Code" = CurrentPartner."Currency Code" then
            PartnerInboxJnlLine."Currency Code" := '';
          PartnerInboxJnlLine.INSERT;
        until TempICInboxJnlLine.NEXT = 0;

      PartnerInboxPurchHeader.CHANGECOMPANY(CurrentPartner."Inbox Details");
      if TempInboxPurchHeader.FIND('-') then
        repeat
          PartnerInboxPurchHeader := TempInboxPurchHeader;
          PartnerInboxPurchHeader."Buy-from Vendor No." := PartnerICPartner."Vendor No.";
          PartnerInboxPurchHeader."Pay-to Vendor No." := PartnerICPartner."Vendor No.";
          PartnerInboxPurchHeader.INSERT;
        until TempInboxPurchHeader.NEXT = 0;

      PartnerInboxPurchLine.CHANGECOMPANY(CurrentPartner."Inbox Details");
      if TempInboxPurchLine.FIND('-') then
        repeat
          PartnerInboxPurchLine := TempInboxPurchLine;
          PartnerInboxPurchLine.INSERT;
        until TempInboxPurchLine.NEXT = 0;

      PartnerInboxSalesHeader.CHANGECOMPANY(CurrentPartner."Inbox Details");
      if TempInboxSalesHeader.FIND('-') then
        repeat
          PartnerInboxSalesHeader := TempInboxSalesHeader;
          PartnerInboxSalesHeader."Sell-to Customer No." := PartnerICPartner."Customer No.";
          PartnerInboxSalesHeader."Bill-to Customer No." := PartnerICPartner."Customer No.";
          PartnerInboxSalesHeader.INSERT;
        until TempInboxSalesHeader.NEXT = 0;

      PartnerInboxSalesLine.CHANGECOMPANY(CurrentPartner."Inbox Details");
      if TempInboxSalesLine.FIND('-') then
        repeat
          PartnerInboxSalesLine := TempInboxSalesLine;
          PartnerInboxSalesLine.INSERT;
        until TempInboxSalesLine.NEXT = 0;

      PartnerInboxOutboxJnlLineDim.CHANGECOMPANY(CurrentPartner."Inbox Details");
      if TempInboxOutboxJnlLineDim.FIND('-') then
        repeat
          PartnerInboxOutboxJnlLineDim := TempInboxOutboxJnlLineDim;
          PartnerInboxOutboxJnlLineDim.INSERT;
        until TempInboxOutboxJnlLineDim.NEXT = 0;

      PartnerICDocDim.CHANGECOMPANY(CurrentPartner."Inbox Details");
      if TempICDocDim.FIND('-') then
        repeat
          PartnerICDocDim := TempICDocDim;
          PartnerICDocDim.INSERT;
        until TempICDocDim.NEXT = 0;

      OnICInboxTransactionCreated(PartnerInboxTransaction,CurrentPartner."Inbox Details");

      TempICInboxTransaction.DELETEALL;
      TempInboxPurchHeader.DELETEALL;
      TempInboxPurchLine.RESET;
      TempInboxPurchLine.DELETEALL;
      TempInboxSalesHeader.DELETEALL;
      TempInboxSalesLine.RESET;
      TempInboxSalesLine.DELETEALL;
      TempICInboxJnlLine.RESET;
      TempICInboxJnlLine.DELETEALL;
      TempInboxOutboxJnlLineDim.DELETEALL;
      TempICDocDim.DELETEALL;
    END;

    PROCEDURE RecreateInboxTrans@1(OutboxTrans@1000 : Record "IC Outbox Transaction");
    VAR
      ICInboxTrans@1001 : Record "IC Inbox Transaction";
      ICInboxJnlLine@1002 : Record "IC Inbox Jnl. Line";
      ICInboxSalesHdr@1003 : Record "IC Inbox Sales Header";
      ICInboxSalesLine@1004 : Record "IC Inbox Sales Line";
      ICInboxPurchHdr@1005 : Record "IC Inbox Purchase Header";
      ICInboxPurchLine@1006 : Record "IC Inbox Purchase Line";
      ICInboxOutboxJnlLineDim@1007 : Record "IC Inbox/Outbox Jnl. Line Dim.";
      HandledICInboxTrans@1014 : Record "Handled IC Inbox Trans.";
      HandledICInboxJnlLine@1013 : Record "Handled IC Inbox Jnl. Line";
      HandledICInboxSalesHdr@1012 : Record "Handled IC Inbox Sales Header";
      HandledICInboxSalesLine@1011 : Record "Handled IC Inbox Sales Line";
      HandledICInboxPurchHdr@1010 : Record "Handled IC Inbox Purch. Header";
      HandledICInboxPurchLine@1009 : Record "Handled IC Inbox Purch. Line";
      HandledICInboxOutboxJnlLineDim@1016 : Record "IC Inbox/Outbox Jnl. Line Dim.";
      ICCommentLine@1017 : Record "IC Comment Line";
      HandledICCommentLine@1018 : Record "IC Comment Line";
    BEGIN
      HandledICInboxTrans.LOCKTABLE;
      ICInboxTrans.LOCKTABLE;

      HandledICInboxTrans.GET(
        OutboxTrans."Transaction No.",OutboxTrans."IC Partner Code",
        ICInboxTrans."Transaction Source"::"Created by Partner",OutboxTrans."Document Type");
      ICInboxTrans.TRANSFERFIELDS(HandledICInboxTrans,true);
      ICInboxTrans."Line Action" := ICInboxTrans."Line Action"::"No Action";
      ICInboxTrans.INSERT;
      HandledICInboxTrans.DELETE;

      HandledICCommentLine.SETRANGE("Table Name",HandledICCommentLine."Table Name"::"Handled IC Inbox Transaction");
      HandledICCommentLine.SETRANGE("Transaction No.",HandledICInboxTrans."Transaction No.");
      HandledICCommentLine.SETRANGE("IC Partner Code",HandledICInboxTrans."IC Partner Code");
      HandledICCommentLine.SETRANGE("Transaction Source",HandledICInboxTrans."Transaction Source");
      if HandledICCommentLine.FIND('-') then
        repeat
          ICCommentLine := HandledICCommentLine;
          ICCommentLine."Table Name" := ICCommentLine."Table Name"::"IC Inbox Transaction";
          ICCommentLine.INSERT;
          HandledICCommentLine.DELETE;
        until HandledICCommentLine.NEXT = 0;

      with HandledICInboxJnlLine do begin
        SETRANGE("Transaction No.",ICInboxTrans."Transaction No.");
        SETRANGE("IC Partner Code",ICInboxTrans."IC Partner Code");
        SETRANGE("Transaction Source",ICInboxTrans."Transaction Source");
        if FIND('-') then
          repeat
            ICInboxJnlLine.TRANSFERFIELDS(HandledICInboxJnlLine,true);
            ICInboxJnlLine.INSERT;
            HandledICInboxOutboxJnlLineDim.SETRANGE("Table ID",DATABASE::"Handled IC Inbox Jnl. Line");
            HandledICInboxOutboxJnlLineDim.SETRANGE("Transaction No.","Transaction No.");
            HandledICInboxOutboxJnlLineDim.SETRANGE("IC Partner Code","IC Partner Code");
            if HandledICInboxOutboxJnlLineDim.FIND('-') then
              repeat
                ICInboxOutboxJnlLineDim := HandledICInboxOutboxJnlLineDim;
                ICInboxOutboxJnlLineDim."Table ID" := DATABASE::"IC Inbox Jnl. Line";
                ICInboxOutboxJnlLineDim.INSERT;
                HandledICInboxOutboxJnlLineDim.DELETE;
              until HandledICInboxOutboxJnlLineDim.NEXT = 0;
            DELETE;
          until NEXT = 0;
      end;

      with HandledICInboxSalesHdr do begin
        SETRANGE("IC Transaction No.",ICInboxTrans."Transaction No.");
        SETRANGE("IC Partner Code",ICInboxTrans."IC Partner Code");
        SETRANGE("Transaction Source",ICInboxTrans."Transaction Source");
        if FIND('-') then
          repeat
            ICInboxSalesHdr.TRANSFERFIELDS(HandledICInboxSalesHdr,true);
            ICInboxSalesHdr.INSERT;
            MoveHandledICDocDim(
              DATABASE::"Handled IC Inbox Sales Header",DATABASE::"IC Inbox Sales Header",
              "IC Transaction No.","IC Partner Code");

            HandledICInboxSalesLine.SETRANGE("IC Transaction No.","IC Transaction No.");
            HandledICInboxSalesLine.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICInboxSalesLine.SETRANGE("Transaction Source","Transaction Source");
            if HandledICInboxSalesLine.FIND('-') then
              repeat
                ICInboxSalesLine.TRANSFERFIELDS(HandledICInboxSalesLine,true);
                ICInboxSalesLine.INSERT;
                MoveHandledICDocDim(
                  DATABASE::"Handled IC Inbox Sales Line",DATABASE::"IC Inbox Sales Line",
                  "IC Transaction No.","IC Partner Code");
                HandledICInboxSalesLine.DELETE;
              until HandledICInboxSalesLine.NEXT = 0;
            DELETE;
          until NEXT = 0;
      end;

      with HandledICInboxPurchHdr do begin
        SETRANGE("IC Transaction No.",ICInboxTrans."Transaction No.");
        SETRANGE("IC Partner Code",ICInboxTrans."IC Partner Code");
        SETRANGE("Transaction Source",ICInboxTrans."Transaction Source");
        if FIND('-') then
          repeat
            ICInboxPurchHdr.TRANSFERFIELDS(HandledICInboxPurchHdr,true);
            ICInboxPurchHdr.INSERT;
            MoveHandledICDocDim(
              DATABASE::"Handled IC Inbox Purch. Header",DATABASE::"IC Inbox Purchase Header",
              "IC Transaction No.","IC Partner Code");

            HandledICInboxPurchLine.SETRANGE("IC Transaction No.","IC Transaction No.");
            HandledICInboxPurchLine.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICInboxPurchLine.SETRANGE("Transaction Source","Transaction Source");
            if HandledICInboxPurchLine.FIND('-') then
              repeat
                ICInboxPurchLine.TRANSFERFIELDS(HandledICInboxPurchLine,true);
                ICInboxPurchLine.INSERT;
                MoveHandledICDocDim(
                  DATABASE::"Handled IC Inbox Purch. Line",DATABASE::"IC Inbox Purchase Line",
                  "IC Transaction No.","IC Partner Code");
                HandledICInboxPurchLine.DELETE;
              until HandledICInboxPurchLine.NEXT = 0;
            DELETE;
          until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE MoveHandledICDocDim@46(FromTableID@1002 : Integer;ToTableID@1007 : Integer;TransactionNo@1003 : Integer;PartnerCode@1004 : Code[20]);
    VAR
      ICDocDim@1000 : Record "IC Document Dimension";
      HandledICDocDim@1001 : Record "IC Document Dimension";
    BEGIN
      HandledICDocDim.SETRANGE("Table ID",FromTableID);
      HandledICDocDim.SETRANGE("Transaction No.",TransactionNo);
      HandledICDocDim.SETRANGE("IC Partner Code",PartnerCode);
      if HandledICDocDim.FIND('-') then
        repeat
          ICDocDim := HandledICDocDim;
          ICDocDim."Table ID" := ToTableID;
          ICDocDim.INSERT;
          HandledICDocDim.DELETE;
        until HandledICDocDim.NEXT = 0;
    END;

    [Integration(TRUE)]
    [External]
    PROCEDURE OnICInboxTransactionCreated@2(VAR ICInboxTransaction@1000 : Record "IC Inbox Transaction";PartnerCompanyName@1001 : Text);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

