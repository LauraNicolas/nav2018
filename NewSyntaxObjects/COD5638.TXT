OBJECT Codeunit 5638 FAJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "FA Journal Template"=imd,
                TableData "FA Journal Batch"=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=ASSETS';
      Text001@1001 : TextConst 'ENU=Fixed Asset Journal';
      Text002@1002 : TextConst 'ENU=Recurring';
      Text003@1003 : TextConst 'ENU=Recurring Fixed Asset Journal';
      Text004@1004 : TextConst 'ENU=DEFAULT';
      Text005@1005 : TextConst 'ENU=Default Journal';
      OldFANo@1006 : Code[20];
      OpenFromBatch@1007 : Boolean;

    [External]
    PROCEDURE TemplateSelection@1(PageID@1004 : Integer;RecurringJnl@1000 : Boolean;VAR FAJnlLine@1003 : Record "FA Journal Line";VAR JnlSelected@1002 : Boolean);
    VAR
      FAJnlTemplate@1001 : Record "FA Journal Template";
    BEGIN
      JnlSelected := true;

      FAJnlTemplate.RESET;
      if not OpenFromBatch then
        FAJnlTemplate.SETRANGE("Page ID",PageID);
      FAJnlTemplate.SETRANGE(Recurring,RecurringJnl);

      case FAJnlTemplate.COUNT of
        0:
          begin
            FAJnlTemplate.INIT;
            FAJnlTemplate.Recurring := RecurringJnl;
            if not RecurringJnl then begin
              FAJnlTemplate.Name := Text000;
              FAJnlTemplate.Description := Text001;
            end else begin
              FAJnlTemplate.Name := Text002;
              FAJnlTemplate.Description := Text003;
            end;
            FAJnlTemplate.VALIDATE("Page ID");
            FAJnlTemplate.INSERT;
            COMMIT;
          end;
        1:
          FAJnlTemplate.FINDFIRST;
        else
          JnlSelected := PAGE.RUNMODAL(0,FAJnlTemplate) = ACTION::LookupOK;
      end;
      if JnlSelected then begin
        FAJnlLine.FILTERGROUP := 2;
        FAJnlLine.SETRANGE("Journal Template Name",FAJnlTemplate.Name);
        FAJnlLine.FILTERGROUP := 0;
        if OpenFromBatch then begin
          FAJnlLine."Journal Template Name" := '';
          PAGE.RUN(FAJnlTemplate."Page ID",FAJnlLine);
        end;
      end;
    END;

    [External]
    PROCEDURE TemplateSelectionFromBatch@10(VAR FAJnlBatch@1000 : Record "FA Journal Batch");
    VAR
      FAJnlLine@1002 : Record "FA Journal Line";
      FAJnlTemplate@1003 : Record "FA Journal Template";
    BEGIN
      OpenFromBatch := true;
      FAJnlTemplate.GET(FAJnlBatch."Journal Template Name");
      FAJnlTemplate.TESTFIELD("Page ID");
      FAJnlBatch.TESTFIELD(Name);

      FAJnlLine.FILTERGROUP := 2;
      FAJnlLine.SETRANGE("Journal Template Name",FAJnlTemplate.Name);
      FAJnlLine.FILTERGROUP := 0;

      FAJnlLine."Journal Template Name" := '';
      FAJnlLine."Journal Batch Name" := FAJnlBatch.Name;
      PAGE.RUN(FAJnlTemplate."Page ID",FAJnlLine);
    END;

    [External]
    PROCEDURE OpenJournal@5(VAR CurrentJnlBatchName@1000 : Code[10];VAR FAJnlLine@1001 : Record "FA Journal Line");
    BEGIN
      CheckTemplateName(FAJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      FAJnlLine.FILTERGROUP := 2;
      FAJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      FAJnlLine.FILTERGROUP := 0;
    END;

    [External]
    PROCEDURE OpenJnlBatch@12(VAR FAJnlBatch@1000 : Record "FA Journal Batch");
    VAR
      FAJnlTemplate@1002 : Record "FA Journal Template";
      FAJnlLine@1004 : Record "FA Journal Line";
      JnlSelected@1003 : Boolean;
    BEGIN
      if FAJnlBatch.GETFILTER("Journal Template Name") <> '' then
        exit;
      FAJnlBatch.FILTERGROUP(2);
      if FAJnlBatch.GETFILTER("Journal Template Name") <> '' then begin
        FAJnlBatch.FILTERGROUP(0);
        exit;
      end;
      FAJnlBatch.FILTERGROUP(0);

      if not FAJnlBatch.FIND('-') then begin
        if not FAJnlTemplate.FINDFIRST then
          TemplateSelection(0,false,FAJnlLine,JnlSelected);
        if FAJnlTemplate.FINDFIRST then
          CheckTemplateName(FAJnlTemplate.Name,FAJnlBatch.Name);
        FAJnlTemplate.SETRANGE(Recurring,true);
        if not FAJnlTemplate.FINDFIRST then
          TemplateSelection(0,true,FAJnlLine,JnlSelected);
        if FAJnlTemplate.FINDFIRST then
          CheckTemplateName(FAJnlTemplate.Name,FAJnlBatch.Name);
        FAJnlTemplate.SETRANGE(Recurring);
      end;
      FAJnlBatch.FIND('-');
      JnlSelected := true;
      FAJnlBatch.CALCFIELDS(Recurring);
      FAJnlTemplate.SETRANGE(Recurring,FAJnlBatch.Recurring);
      if FAJnlBatch.GETFILTER("Journal Template Name") <> '' then
        FAJnlTemplate.SETRANGE(Name,FAJnlBatch.GETFILTER("Journal Template Name"));
      case FAJnlTemplate.COUNT of
        1:
          FAJnlTemplate.FINDFIRST;
        else
          JnlSelected := PAGE.RUNMODAL(0,FAJnlTemplate) = ACTION::LookupOK;
      end;
      if not JnlSelected then
        ERROR('');

      FAJnlBatch.FILTERGROUP(2);
      FAJnlBatch.SETRANGE("Journal Template Name",FAJnlTemplate.Name);
      FAJnlBatch.FILTERGROUP(0);
    END;

    [External]
    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR FAJnlLine@1001 : Record "FA Journal Line");
    VAR
      FAJnlBatch@1002 : Record "FA Journal Batch";
    BEGIN
      FAJnlBatch.GET(FAJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    [External]
    PROCEDURE SetName@6(CurrentJnlBatchName@1000 : Code[10];VAR FAJnlLine@1001 : Record "FA Journal Line");
    BEGIN
      FAJnlLine.FILTERGROUP := 2;
      FAJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      FAJnlLine.FILTERGROUP := 0;
      if FAJnlLine.FIND('-') then;
    END;

    [External]
    PROCEDURE LookupName@7(VAR CurrentJnlBatchName@1000 : Code[10];VAR FAJnlLine@1001 : Record "FA Journal Line");
    VAR
      FAJnlBatch@1002 : Record "FA Journal Batch";
    BEGIN
      COMMIT;
      FAJnlBatch."Journal Template Name" := FAJnlLine.GETRANGEMAX("Journal Template Name");
      FAJnlBatch.Name := FAJnlLine.GETRANGEMAX("Journal Batch Name");
      FAJnlBatch.FILTERGROUP(2);
      FAJnlBatch.SETRANGE("Journal Template Name",FAJnlBatch."Journal Template Name");
      FAJnlBatch.FILTERGROUP(0);
      if PAGE.RUNMODAL(0,FAJnlBatch) = ACTION::LookupOK then begin
        CurrentJnlBatchName := FAJnlBatch.Name;
        SetName(CurrentJnlBatchName,FAJnlLine);
      end;
    END;

    LOCAL PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      FAJnlBatch@1002 : Record "FA Journal Batch";
    BEGIN
      if not FAJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) then begin
        FAJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
        if not FAJnlBatch.FINDFIRST then begin
          FAJnlBatch.INIT;
          FAJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          FAJnlBatch.SetupNewBatch;
          FAJnlBatch.Name := Text004;
          FAJnlBatch.Description := Text005;
          FAJnlBatch.INSERT(true);
          COMMIT;
        end;
        CurrentJnlBatchName := FAJnlBatch.Name;
      end;
    END;

    [External]
    PROCEDURE GetFA@2(FANo@1000 : Code[20];VAR FADescription@1001 : Text[50]);
    VAR
      FA@1002 : Record "Fixed Asset";
    BEGIN
      if FANo <> OldFANo then begin
        FADescription := '';
        if FANo <> '' then
          if FA.GET(FANo) then
            FADescription := FA.Description;
        OldFANo := FANo;
      end;
    END;

    BEGIN
    END.
  }
}

