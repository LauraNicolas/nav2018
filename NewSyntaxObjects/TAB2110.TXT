OBJECT Table 2110 O365 Sales Initial Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=O365 Sales Initial Setup;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Payment Reg. Template Name;Code10  ;TableRelation="Gen. Journal Template".Name;
                                                   CaptionML=ENU=Payment Reg. Template Name }
    { 3   ;   ;Payment Reg. Batch Name;Code10     ;TableRelation="Gen. Journal Batch".Name WHERE ("Journal Template Name"=FIELD("Payment Reg. Template Name"));
                                                   CaptionML=ENU=Payment Reg. Batch Name }
    { 4   ;   ;Is initialized      ;Boolean       ;CaptionML=ENU=Is initialized }
    { 5   ;   ;Default Customer Template;Code10   ;TableRelation="Config. Template Header".Code WHERE ("Table ID"=CONST(18));
                                                   CaptionML=ENU=Default Customer Template }
    { 6   ;   ;Default Item Template;Code10       ;TableRelation="Config. Template Header".Code WHERE ("Table ID"=CONST(27));
                                                   CaptionML=ENU=Default Item Template }
    { 7   ;   ;Default Payment Terms Code;Code10  ;TableRelation="Payment Terms";
                                                   CaptionML=ENU=Default Payment Terms Code }
    { 8   ;   ;Default Payment Method Code;Code10 ;TableRelation="Payment Method";
                                                   CaptionML=ENU=Default Payment Method Code }
    { 9   ;   ;Sales Invoice No. Series;Code20    ;TableRelation="No. Series";
                                                   CaptionML=ENU=Sales Invoice No. Series }
    { 10  ;   ;Posted Sales Inv. No. Series;Code20;TableRelation="No. Series";
                                                   CaptionML=ENU=Posted Sales Inv. No. Series }
    { 11  ;   ;Tax Type            ;Option        ;CaptionML=ENU=Tax Type;
                                                   OptionCaptionML=ENU=VAT,Sales Tax;
                                                   OptionString=VAT,"Sales Tax" }
    { 12  ;   ;Default VAT Bus. Posting Group;Code20;
                                                   TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=Default VAT Bus. Posting Group }
    { 13  ;   ;Normal VAT Prod. Posting Gr.;Code20;TableRelation="VAT Product Posting Group";
                                                   CaptionML=ENU=Normal VAT Prod. Posting Gr. }
    { 14  ;   ;Reduced VAT Prod. Posting Gr.;Code20;
                                                   TableRelation="VAT Product Posting Group";
                                                   CaptionML=ENU=Reduced VAT Prod. Posting Gr. }
    { 15  ;   ;Zero VAT Prod. Posting Gr.;Code20  ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=ENU=Zero VAT Prod. Posting Gr. }
    { 16  ;   ;C2Graph Endpoint    ;Text250       ;CaptionML=ENU=C2Graph Endpoint }
    { 17  ;   ;Sales Quote No. Series;Code20      ;TableRelation="No. Series";
                                                   CaptionML=ENU=Sales Quote No. Series }
    { 18  ;   ;Engage Endpoint     ;Text250       ;CaptionML=ENU=Engage Endpoint }
  }
  KEYS
  {
    {    ;"Primary Key"                           ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE IsUsingSalesTax@1000() : Boolean;
    BEGIN
      if GET then;
      exit("Tax Type" = "Tax Type"::"Sales Tax");
    END;

    PROCEDURE IsUsingVAT@1001() : Boolean;
    BEGIN
      if GET then;
      exit("Tax Type" = "Tax Type"::VAT);
    END;

    PROCEDURE UpdateDefaultPaymentTerms@3(PaymentTermsCode@1003 : Code[10]);
    VAR
      Customer@1002 : Record Customer;
      SalesHeader@1001 : Record "Sales Header";
      O365SalesInitialSetup@1004 : Record "O365 Sales Initial Setup";
      PaymentTerms@1005 : Record "Payment Terms";
      ConfigTemplateManagement@1000 : Codeunit "Config. Template Management";
    BEGIN
      if not O365SalesInitialSetup.GET then
        O365SalesInitialSetup.INSERT(true);

      ConfigTemplateManagement.ReplaceDefaultValueForAllTemplates(
        DATABASE::Customer,Customer.FIELDNO("Payment Terms Code"),PaymentTermsCode);
      Customer.SETRANGE("Payment Terms Code",O365SalesInitialSetup."Default Payment Terms Code");
      Customer.MODIFYALL("Payment Terms Code",PaymentTermsCode,true);
      SalesHeader.SETRANGE("Payment Terms Code",O365SalesInitialSetup."Default Payment Terms Code");
      SalesHeader.MODIFYALL("Payment Terms Code",PaymentTermsCode,true);
      O365SalesInitialSetup."Default Payment Terms Code" := PaymentTermsCode;
      O365SalesInitialSetup.MODIFY(true);

      // Update last modified date time, so users of web services can pick up an update
      if PaymentTerms.GET(PaymentTermsCode) then begin
        PaymentTerms."Last Modified Date Time" := CURRENTDATETIME;
        PaymentTerms.MODIFY(true);
      end;
    END;

    PROCEDURE UpdateDefaultPaymentMethod@4(PaymentMethodCode@1003 : Code[10]);
    VAR
      Customer@1002 : Record Customer;
      SalesHeader@1001 : Record "Sales Header";
      O365SalesInitialSetup@1004 : Record "O365 Sales Initial Setup";
      PaymentMethod@1005 : Record "Payment Method";
      ConfigTemplateManagement@1000 : Codeunit "Config. Template Management";
    BEGIN
      if not O365SalesInitialSetup.GET then
        O365SalesInitialSetup.INSERT(true);

      ConfigTemplateManagement.ReplaceDefaultValueForAllTemplates(
        DATABASE::Customer,Customer.FIELDNO("Payment Method Code"),PaymentMethodCode);
      Customer.SETRANGE("Payment Method Code",O365SalesInitialSetup."Default Payment Method Code");
      Customer.MODIFYALL("Payment Method Code",PaymentMethodCode,true);
      SalesHeader.SETRANGE("Payment Method Code",O365SalesInitialSetup."Default Payment Method Code");
      SalesHeader.MODIFYALL("Payment Method Code",PaymentMethodCode,true);
      O365SalesInitialSetup."Default Payment Method Code" := PaymentMethodCode;
      O365SalesInitialSetup.MODIFY(true);

      // Update last modified date time, so users of web services can pick up an update
      if PaymentMethod.GET(PaymentMethodCode) then begin
        PaymentMethod."Last Modified Date Time" := CURRENTDATETIME;
        PaymentMethod.MODIFY(true);
      end;
    END;

    PROCEDURE IsDefaultPaymentMethod@2(VAR PaymentMethod@1000 : Record "Payment Method") : Boolean;
    BEGIN
      if not GET then
        INSERT(true);

      exit("Default Payment Method Code" = PaymentMethod.Code);
    END;

    PROCEDURE IsDefaultPaymentTerms@5(VAR PaymentTerms@1000 : Record "Payment Terms") : Boolean;
    BEGIN
      if not GET then
        INSERT(true);

      exit("Default Payment Terms Code" = PaymentTerms.Code);
    END;

    BEGIN
    END.
  }
}

