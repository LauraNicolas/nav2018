OBJECT Codeunit 2002 Cortana Tracing
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    SingleInstance=true;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CortanaCategoryTxt@1000 : TextConst '@@@=Locked;ENU=AL Cortana';
      TraceImageAnalysisSuccessTagsTxt@1001 : TextConst '@@@=Locked;ENU="Number of Image Analysis calls: %1;Limit: %2;Period type: %3;Execution time: %4 ms;Confidence: %5."';
      AnalysisStartTime@1002 : DateTime;
      TraceImageAnalysisSuccessTxt@1003 : TextConst '@@@=Locked;ENU="Number of Image Analysis calls: %1;Limit: %2;Period type: %3;Execution time: %4 ms."';

    [EventSubscriber(Codeunit,2020,OnBeforeImageAnalysis)]
    LOCAL PROCEDURE TraceImageAnalysisStart@2(VAR Sender@1000 : Codeunit "Image Analysis Management");
    BEGIN
      AnalysisStartTime := CURRENTDATETIME;
    END;

    [EventSubscriber(Codeunit,2020,OnAfterImageAnalysis)]
    LOCAL PROCEDURE TraceImageAnalysisEnd@1(VAR Sender@1000 : Codeunit "Image Analysis Management";ImageAnalysisResult@1001 : Codeunit "Image Analysis Result");
    VAR
      ImageAnalysisSetup@1005 : Record "Image Analysis Setup";
      LastError@1006 : Text;
      IsUsageLimitError@1007 : Boolean;
      LimitType@1004 : 'Year,Month,Day,Hour';
      LimitValue@1003 : Integer;
      Message@1008 : Text;
      AnalysisDuration@1002 : Integer;
      AnalysisType@1009 : 'Tags,Faces,Color';
    BEGIN
      if Sender.GetLastError(LastError,IsUsageLimitError) then
        SENDTRACETAG('000015X',CortanaCategoryTxt,VERBOSITY::Error,LastError)
      else begin
        Sender.GetLimitParams(LimitType,LimitValue);
        ImageAnalysisSetup.GetCurrentStatus(ImageAnalysisSetup,LimitType);
        AnalysisDuration := CURRENTDATETIME - AnalysisStartTime;
        ImageAnalysisResult.GetLatestAnalysisType(AnalysisType);
        if AnalysisType = AnalysisType::Tags then
          Message := STRSUBSTNO(TraceImageAnalysisSuccessTagsTxt,
              ImageAnalysisSetup."Number of calls",LimitValue,LimitType,AnalysisDuration,
              ImageAnalysisResult.TagConfidence(1))
        else
          Message := STRSUBSTNO(TraceImageAnalysisSuccessTxt,
              ImageAnalysisSetup."Number of calls",LimitValue,LimitType,AnalysisDuration);

        SENDTRACETAG('000015Y',CortanaCategoryTxt,VERBOSITY::Normal,Message);
      end;
    END;

    BEGIN
    END.
  }
}

