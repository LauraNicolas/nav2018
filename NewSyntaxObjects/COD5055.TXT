OBJECT Codeunit 5055 CustVendBank-Update
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo=Contact;
    OnRun=VAR
            ContBusRel@1000 : Record "Contact Business Relation";
          BEGIN
            ContBusRel.SETRANGE("Contact No.","No.");
            ContBusRel.SETFILTER("Link to Table",'<>''''');

            if ContBusRel.FIND('-') then
              repeat
                case ContBusRel."Link to Table" of
                  ContBusRel."Link to Table"::Customer:
                    UpdateCustomer(Rec,ContBusRel);
                  ContBusRel."Link to Table"::Vendor:
                    UpdateVendor(Rec,ContBusRel);
                  ContBusRel."Link to Table"::"Bank Account":
                    UpdateBankAccount(Rec,ContBusRel);
                end;
              until ContBusRel.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      Cust@1001 : Record Customer;
      Vend@1002 : Record Vendor;
      BankAcc@1003 : Record "Bank Account";
      NoSeries@1004 : Code[20];
      PurchaserCode@1005 : Code[20];
      OurContactCode@1006 : Code[20];

    [External]
    PROCEDURE UpdateCustomer@1(VAR Cont@1000 : Record Contact;VAR ContBusRel@1001 : Record "Contact Business Relation");
    VAR
      VATRegistrationLogMgt@1002 : Codeunit "VAT Registration Log Mgt.";
      VATRegNo@1003 : Text[20];
    BEGIN
      with Cust do begin
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
        VATRegNo := "VAT Registration No.";
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;
        MODIFY;
        if ("VAT Registration No." <> '') and ("VAT Registration No." <> VATRegNo) then
          VATRegistrationLogMgt.LogCustomer(Cust);
      end;
    END;

    [External]
    PROCEDURE UpdateVendor@2(VAR Cont@1000 : Record Contact;VAR ContBusRel@1001 : Record "Contact Business Relation");
    VAR
      VATRegistrationLogMgt@1002 : Codeunit "VAT Registration Log Mgt.";
      VATRegNo@1003 : Text[20];
    BEGIN
      with Vend do begin
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
        PurchaserCode := "Purchaser Code";
        VATRegNo := "VAT Registration No.";
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;
        "Purchaser Code" := PurchaserCode;
        MODIFY;
        if ("VAT Registration No." <> '') and ("VAT Registration No." <> VATRegNo) then
          VATRegistrationLogMgt.LogVendor(Vend);
      end;
    END;

    [External]
    PROCEDURE UpdateBankAccount@3(VAR Cont@1000 : Record Contact;VAR ContBusRel@1001 : Record "Contact Business Relation");
    BEGIN
      with BankAcc do begin
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
        OurContactCode := "Our Contact Code";
        VALIDATE("Currency Code",Cont."Currency Code");
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;
        "Our Contact Code" := OurContactCode;
        MODIFY;
      end;
    END;

    BEGIN
    END.
  }
}

