OBJECT Table 416 Handled IC Outbox Trans.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ICOutboxJnlLine@1000 : Record "Handled IC Outbox Jnl. Line";
               ICOutboxSalesHdr@1001 : Record "Handled IC Outbox Sales Header";
               ICOutboxPurchHdr@1002 : Record "Handled IC Outbox Purch. Hdr";
             BEGIN
               case "Source Type" of
                 "Source Type"::Journal:
                   begin
                     ICOutboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
                     ICOutboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
                     ICOutboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
                     ICOutboxJnlLine.DELETEALL(true);
                   end;
                 "Source Type"::"Sales Document":
                   if ICOutboxSalesHdr.GET("Transaction No.","IC Partner Code","Transaction Source") then
                     ICOutboxSalesHdr.DELETE(true);
                 "Source Type"::"Purchase Document":
                   if ICOutboxPurchHdr.GET("Transaction No.","IC Partner Code","Transaction Source") then
                     ICOutboxPurchHdr.DELETE(true);
               end;
               DeleteComments("Transaction No.","IC Partner Code");
             END;

    CaptionML=ENU=Handled IC Outbox Trans.;
  }
  FIELDS
  {
    { 1   ;   ;Transaction No.     ;Integer       ;CaptionML=ENU=Transaction No.;
                                                   Editable=false }
    { 2   ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner";
                                                   CaptionML=ENU=IC Partner Code;
                                                   Editable=false }
    { 3   ;   ;Source Type         ;Option        ;CaptionML=ENU=Source Type;
                                                   OptionCaptionML=ENU=Journal,Sales Document,Purchase Document;
                                                   OptionString=Journal,"Sales Document","Purchase Document";
                                                   Editable=false }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order";
                                                   OptionString=" ",Payment,Invoice,"Credit Memo",Refund,Order,"Return Order";
                                                   Editable=false }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No.;
                                                   Editable=false }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date;
                                                   Editable=false }
    { 8   ;   ;Transaction Source  ;Option        ;CaptionML=ENU=Transaction Source;
                                                   OptionCaptionML=ENU=Rejected by Current Company,Created by Current Company;
                                                   OptionString="Rejected by Current Company","Created by Current Company";
                                                   Editable=false }
    { 9   ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date;
                                                   Editable=false }
    { 11  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Sent to IC Partner,Rejection Sent to IC Partner,Cancelled;
                                                   OptionString="Sent to IC Partner","Rejection Sent to IC Partner",Cancelled;
                                                   Editable=false }
    { 12  ;   ;IC Partner G/L Acc. No.;Code20     ;CaptionML=ENU=IC Partner G/L Acc. No. }
    { 13  ;   ;Source Line No.     ;Integer       ;CaptionML=ENU=Source Line No. }
  }
  KEYS
  {
    {    ;"Transaction No.","IC Partner Code","Transaction Source";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE ShowDetails@1();
    VAR
      HandledICOutboxJnlLine@1000 : Record "Handled IC Outbox Jnl. Line";
      HandledICOutboxSalesHeader@1001 : Record "Handled IC Outbox Sales Header";
      HandledICOutboxPurchHdr@1002 : Record "Handled IC Outbox Purch. Hdr";
      HandledICOutboxJnlLines@1005 : Page "Handled IC Outbox Jnl. Lines";
      HandledICOutboxSalesDoc@1004 : Page "Handled IC Outbox Sales Doc.";
      HandledICOutboxPurchDoc@1003 : Page "Handled IC Outbox Purch. Doc.";
    BEGIN
      case "Source Type" of
        "Source Type"::Journal:
          begin
            HandledICOutboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
            HandledICOutboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICOutboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(HandledICOutboxJnlLines);
            HandledICOutboxJnlLines.SETTABLEVIEW(HandledICOutboxJnlLine);
            HandledICOutboxJnlLines.RUNMODAL;
          end;
        "Source Type"::"Sales Document":
          begin
            HandledICOutboxSalesHeader.SETRANGE("IC Transaction No.","Transaction No.");
            HandledICOutboxSalesHeader.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICOutboxSalesHeader.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(HandledICOutboxSalesDoc);
            HandledICOutboxSalesDoc.SETTABLEVIEW(HandledICOutboxSalesHeader);
            HandledICOutboxSalesDoc.RUNMODAL;
          end;
        "Source Type"::"Purchase Document":
          begin
            HandledICOutboxPurchHdr.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICOutboxPurchHdr.SETRANGE("IC Transaction No.","Transaction No.");
            HandledICOutboxPurchHdr.SETRANGE("Transaction Source","Transaction Source");
            CLEAR(HandledICOutboxPurchDoc);
            HandledICOutboxPurchDoc.SETTABLEVIEW(HandledICOutboxPurchHdr);
            HandledICOutboxPurchDoc.RUNMODAL;
          end;
      end;
    END;

    LOCAL PROCEDURE DeleteComments@2(TransactionNo@1000 : Integer;ICPartnerCode@1001 : Code[20]);
    VAR
      ICCommentLine@1002 : Record "IC Comment Line";
    BEGIN
      ICCommentLine.SETRANGE("Table Name",ICCommentLine."Table Name"::"Handled IC Outbox Transaction");
      ICCommentLine.SETRANGE("Transaction No.",TransactionNo);
      ICCommentLine.SETRANGE("IC Partner Code",ICPartnerCode);
      ICCommentLine.DELETEALL;
    END;

    BEGIN
    END.
  }
}

