OBJECT Codeunit 6724 Booking Appointment - Modify
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Invoiced Booking Item";
    OnRun=VAR
            InvoicedBookingItem@1000 : Record "Invoiced Booking Item";
            BookingManager@1001 : Codeunit "Booking Manager";
          BEGIN
            BookingManager.RegisterAppointmentConnection;

            InvoicedBookingItem.SETRANGE("Document No.","Document No.");
            if InvoicedBookingItem.FINDSET(true,true) then
              repeat
                if Posted then
                  HandlePosted(InvoicedBookingItem)
                else
                  HandleUnposted(InvoicedBookingItem);
              until InvoicedBookingItem.NEXT = 0;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE HandlePosted@1(VAR InvoicedBookingItem@1000 : Record "Invoiced Booking Item");
    VAR
      BookingItem@1002 : Record "Booking Item";
      SalesInvoiceHeader@1001 : Record "Sales Invoice Header";
    BEGIN
      if not BookingItem.GET(InvoicedBookingItem."Booking Item ID") then begin
        InvoicedBookingItem.DELETE;
        exit;
      end;

      SalesInvoiceHeader.SETAUTOCALCFIELDS("Amount Including VAT");
      SalesInvoiceHeader.GET(InvoicedBookingItem."Document No.");
      BookingItem."Invoice Amount" := SalesInvoiceHeader."Amount Including VAT";
      BookingItem."Invoice Status" := BookingItem."Invoice Status"::open;
      BookingItem."Invoice No." := SalesInvoiceHeader."No.";
      BookingItem.SetInvoiceDate(CREATEDATETIME(SalesInvoiceHeader."Document Date",000000T));
      BookingItem.MODIFY;
    END;

    LOCAL PROCEDURE HandleUnposted@2(VAR InvoicedBookingItem@1000 : Record "Invoiced Booking Item");
    VAR
      BookingItem@1002 : Record "Booking Item";
      SalesHeader@1001 : Record "Sales Header";
      OutStream@1003 : OutStream;
    BEGIN
      if not BookingItem.GET(InvoicedBookingItem."Booking Item ID") then begin
        InvoicedBookingItem.DELETE;
        exit;
      end;

      SalesHeader.SETAUTOCALCFIELDS("Amount Including VAT");
      if SalesHeader.GET(SalesHeader."Document Type"::Invoice,InvoicedBookingItem."Document No.") then begin
        BookingItem."Invoice Amount" := SalesHeader."Amount Including VAT";
        BookingItem."Invoice No." := SalesHeader."No.";
        BookingItem."Invoice Status" := BookingItem."Invoice Status"::draft;
        BookingItem.SetInvoiceDate(CREATEDATETIME(SalesHeader."Document Date",000000T));
      end else begin
        CLEAR(BookingItem."Invoice Amount");
        CLEAR(BookingItem."Invoice Date");
        BookingItem."Invoice Date".CREATEOUTSTREAM(OutStream);
        OutStream.WRITETEXT('null');
        CLEAR(BookingItem."Invoice No.");
        CLEAR(BookingItem."Invoice Status");
        InvoicedBookingItem.DELETE;
      end;

      BookingItem.MODIFY;
    END;

    BEGIN
    END.
  }
}

