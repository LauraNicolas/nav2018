OBJECT Report 6032 Post Prepaid Contract Entries
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Service Ledger Entry"=rm;
    CaptionML=ENU=Post Prepaid Contract Entries;
    ProcessingOnly=true;
    OnInitReport=BEGIN
                   PostingDate := WORKDATE;
                   CLEAR(GenJnlPostLine);
                 END;

    OnPostReport=BEGIN
                   if PostPrepaidContracts = PostPrepaidContracts::"Post Prepaid Transactions" then
                     Window.CLOSE;
                 END;

  }
  DATASET
  {
    { 1141;    ;DataItem;Service Ledger Entry;
               DataItemTable="Service Ledger Entry";
               DataItemTableView=SORTING("Service Contract No.") WHERE(Type=CONST("Service Contract"),"Moved from Prepaid Acc."=CONST(false),Open=CONST(false));
               OnPreDataItem=BEGIN
                               if PostPrepaidContracts = PostPrepaidContracts::"Print Only" then begin
                                 CLEAR(PrepaidContractEntriesTest);
                                 PrepaidContractEntriesTest.InitVariables(UntilDate,PostingDate);
                                 PrepaidContractEntriesTest.SETTABLEVIEW("Service Ledger Entry");
                                 PrepaidContractEntriesTest.RUNMODAL;
                                 CurrReport.BREAK;
                               end;

                               if PostPrepaidContracts = PostPrepaidContracts::"Post Prepaid Transactions" then begin
                                 ServContractHdr.SETFILTER("Contract No.",GETFILTER("Service Contract No."));
                                 if ServContractHdr.FIND('-') then begin
                                   repeat
                                     ServContractHdr.CALCFIELDS("No. of Unposted Credit Memos");
                                     if ServContractHdr."No. of Unposted Credit Memos" <> 0 then
                                       ERROR(Text005,Text007,Text008,ServContractHdr."Contract No.",Text006);
                                   until ServContractHdr.NEXT = 0;
                                 end;
                               end;

                               LastContract := '';
                               if UntilDate = 0D then
                                 ERROR(Text000);
                               if PostingDate = 0D then
                                 ERROR(Text001);

                               SETRANGE("Posting Date",0D,UntilDate);

                               NoOfContracts := COUNT;

                               Window.OPEN(
                                 Text002 +
                                 Text003 +
                                 '@2@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

                               ServMgtSetup.GET;
                               ServMgtSetup.TESTFIELD("Prepaid Posting Document Nos.");
                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Service Management");
                               SalesSetup.GET;
                             END;

               OnAfterGetRecord=BEGIN
                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"Service Contract No.");
                                  Window.UPDATE(2,ROUND(Counter / NoOfContracts * 10000,1));

                                  ServLedgEntry.GET("Entry No.");
                                  ServLedgEntry."Moved from Prepaid Acc." := true;
                                  ServLedgEntry.MODIFY;

                                  TempServLedgEntry.SETRANGE("Service Contract No.","Service Contract No.");
                                  TempServLedgEntry.CALCSUMS("Amount (LCY)");
                                  if (TempServLedgEntry."Amount (LCY)" <> 0) and (LastContract <> '') and (LastContract <> "Service Contract No.") then
                                    PostGenJnlLine;

                                  LastContract := "Service Contract No.";

                                  if SalesSetup."Discount Posting" in
                                     [SalesSetup."Discount Posting"::"Line Discounts",SalesSetup."Discount Posting"::"All Discounts"]
                                  then
                                    AmtInclDisc := ROUND(("Amount (LCY)" / (1 - ("Discount %" / 100))))
                                  else
                                    AmtInclDisc := "Amount (LCY)";

                                  TempServLedgEntry.SETRANGE("Dimension Set ID","Dimension Set ID");
                                  if TempServLedgEntry.FINDFIRST then begin
                                    TempServLedgEntry."Amount (LCY)" += AmtInclDisc;
                                    TempServLedgEntry.MODIFY;
                                  end else begin
                                    TempServLedgEntry := "Service Ledger Entry";
                                    TempServLedgEntry."Amount (LCY)" := AmtInclDisc;
                                    TempServLedgEntry.INSERT;
                                  end;
                                END;

               OnPostDataItem=VAR
                                UpdateAnalysisView@1000 : Codeunit "Update Analysis View";
                              BEGIN
                                if PostPrepaidContracts = PostPrepaidContracts::"Post Prepaid Transactions" then begin
                                  TempServLedgEntry.SETRANGE("Dimension Set ID");
                                  TempServLedgEntry.CALCSUMS("Amount (LCY)");
                                  if TempServLedgEntry."Amount (LCY)" <> 0 then begin
                                    PostGenJnlLine;
                                    UpdateAnalysisView.UpdateAll(0,true);
                                  end;
                                end;
                              END;

               ReqFilterFields="Service Contract No." }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 2   ;2   ;Field     ;
                  Name=Post until Date;
                  CaptionML=ENU=Post until Date;
                  ToolTipML=ENU=Specifies the date up to which you want to post prepaid entries. The batch job includes service ledger entries with posting dates on or before this date.;
                  ApplicationArea=#Service;
                  SourceExpr=UntilDate;
                  MultiLine=true }

      { 4   ;2   ;Field     ;
                  Name=Posting Date;
                  CaptionML=ENU=Posting Date;
                  ToolTipML=ENU=Specifies the date that you want to use as the posting date on the service ledger entries.;
                  ApplicationArea=#Service;
                  SourceExpr=PostingDate;
                  MultiLine=true }

      { 5   ;2   ;Field     ;
                  Name=Action;
                  CaptionML=ENU=Action;
                  ToolTipML=ENU=Specifies the desired action relating to prepaid contract entries.;
                  OptionCaptionML=ENU=Post Prepaid Transactions,Print Only;
                  ApplicationArea=#Service;
                  SourceExpr=PostPrepaidContracts }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must fill in the Post Until Date field.';
      Text001@1001 : TextConst 'ENU=You must fill in the Posting Date field.';
      Text002@1002 : TextConst 'ENU=Posting prepaid contract entries...\';
      Text003@1003 : TextConst 'ENU=Service Contract: #1###############\\';
      Text004@1004 : TextConst 'ENU=Service Contract';
      GenJnlLine@1005 : Record "Gen. Journal Line";
      ServLedgEntry@1006 : Record "Service Ledger Entry";
      TempServLedgEntry@1011 : TEMPORARY Record "Service Ledger Entry";
      ServContractAccGr@1028 : Record "Service Contract Account Group";
      ServMgtSetup@1008 : Record "Service Mgt. Setup";
      SourceCodeSetup@1009 : Record "Source Code Setup";
      ServContractHdr@1029 : Record "Service Contract Header";
      SalesSetup@1010 : Record "Sales & Receivables Setup";
      PrepaidContractEntriesTest@1007 : Report "Prepaid Contr. Entries - Test";
      NoSeriesMgt@1012 : Codeunit NoSeriesManagement;
      GenJnlPostLine@1014 : Codeunit "Gen. Jnl.-Post Line";
      Window@1015 : Dialog;
      PostPrepaidContracts@1016 : '"Post Prepaid Transactions","Print Only"';
      DocNo@1017 : Code[20];
      UntilDate@1019 : Date;
      PostingDate@1020 : Date;
      NoOfContracts@1021 : Integer;
      Counter@1022 : Integer;
      AmtInclDisc@1023 : Decimal;
      LastContract@1024 : Code[20];
      Text005@1030 : TextConst 'ENU=You cannot post %1 because %2 %3 has at least one %4 linked to it.';
      Text006@1102601000 : TextConst 'ENU=Unposted Credit Memo';
      Text007@1102601001 : TextConst 'ENU=Prepaid Contract Entries';
      Text008@1102601002 : TextConst 'ENU=Service Contract';

    LOCAL PROCEDURE PostGenJnlLine@1();
    BEGIN
      TempServLedgEntry.RESET;
      if not TempServLedgEntry.FINDSET then
        exit;

      DocNo := NoSeriesMgt.GetNextNo(ServMgtSetup."Prepaid Posting Document Nos.",WORKDATE,true);

      repeat
        GenJnlLine.RESET;
        GenJnlLine.INIT;
        GenJnlLine."Document No." := DocNo;
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
        GenJnlLine."VAT Posting" := GenJnlLine."VAT Posting"::"Manual VAT Entry";
        ServContractAccGr.GET(TempServLedgEntry."Serv. Contract Acc. Gr. Code");
        ServContractAccGr.TESTFIELD("Non-Prepaid Contract Acc.");
        GenJnlLine.VALIDATE("Account No.",ServContractAccGr."Non-Prepaid Contract Acc.");
        GenJnlLine."Posting Date" := PostingDate;
        GenJnlLine.Description := Text004;
        GenJnlLine."External Document No." := TempServLedgEntry."Service Contract No.";
        GenJnlLine.VALIDATE(Amount,TempServLedgEntry."Amount (LCY)");
        GenJnlLine."Shortcut Dimension 1 Code" := TempServLedgEntry."Global Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := TempServLedgEntry."Global Dimension 2 Code";
        GenJnlLine."Dimension Set ID" := TempServLedgEntry."Dimension Set ID";
        GenJnlLine."Source Code" := SourceCodeSetup."Service Management";
        GenJnlLine."System-Created Entry" := true;
        GenJnlLine."Gen. Posting Type" := GenJnlLine."Gen. Posting Type"::" ";
        GenJnlLine."Gen. Bus. Posting Group" := '';
        GenJnlLine."Gen. Prod. Posting Group" := '';
        GenJnlLine."VAT Bus. Posting Group" := '';
        GenJnlLine."VAT Prod. Posting Group" := '';
        GenJnlPostLine.RUN(GenJnlLine);

        ServContractAccGr.GET(TempServLedgEntry."Serv. Contract Acc. Gr. Code");
        ServContractAccGr.TESTFIELD("Prepaid Contract Acc.");
        GenJnlLine.VALIDATE("Account No.",ServContractAccGr."Prepaid Contract Acc.");
        GenJnlLine.VALIDATE(Amount,-TempServLedgEntry."Amount (LCY)");
        GenJnlLine."Shortcut Dimension 1 Code" := TempServLedgEntry."Global Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := TempServLedgEntry."Global Dimension 2 Code";
        GenJnlLine."Dimension Set ID" := TempServLedgEntry."Dimension Set ID";
        GenJnlLine."Gen. Posting Type" := GenJnlLine."Gen. Posting Type"::" ";
        GenJnlLine."Gen. Bus. Posting Group" := '';
        GenJnlLine."Gen. Prod. Posting Group" := '';
        GenJnlLine."VAT Bus. Posting Group" := '';
        GenJnlLine."VAT Prod. Posting Group" := '';
        GenJnlPostLine.RUN(GenJnlLine);
      until TempServLedgEntry.NEXT = 0;

      TempServLedgEntry.RESET;
      TempServLedgEntry.DELETEALL;
    END;

    PROCEDURE InitializeRequest@2(UntilDateFrom@1000 : Date;PostingDateFrom@1001 : Date;PostPrepaidContractsFrom@1002 : '"Post Prepaid Transactions","Print Only"');
    BEGIN
      UntilDate := UntilDateFrom;
      PostingDate := PostingDateFrom;
      PostPrepaidContracts := PostPrepaidContractsFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

