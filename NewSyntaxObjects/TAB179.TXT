OBJECT Table 179 Reversal Entry
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Reversal Entry;
    PasteIsValid=false;
  }
  FIELDS
  {
    { 1   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 2   ;   ;Entry Type          ;Option        ;CaptionML=ENU=Entry Type;
                                                   OptionCaptionML=ENU=" ,G/L Account,Customer,Vendor,Bank Account,Fixed Asset,Maintenance,VAT";
                                                   OptionString=" ","G/L Account",Customer,Vendor,"Bank Account","Fixed Asset",Maintenance,VAT }
    { 3   ;   ;Entry No.           ;Integer       ;TableRelation=IF ("Entry Type"=CONST("G/L Account")) "G/L Entry"
                                                                 ELSE IF ("Entry Type"=CONST(Customer)) "Cust. Ledger Entry"
                                                                 ELSE IF ("Entry Type"=CONST(Vendor)) "Vendor Ledger Entry"
                                                                 ELSE IF ("Entry Type"=CONST("Bank Account")) "Bank Account Ledger Entry"
                                                                 ELSE IF ("Entry Type"=CONST("Fixed Asset")) "FA Ledger Entry"
                                                                 ELSE IF ("Entry Type"=CONST(Maintenance)) "Maintenance Ledger Entry"
                                                                 ELSE IF ("Entry Type"=CONST(VAT)) "VAT Entry";
                                                   CaptionML=ENU=Entry No. }
    { 4   ;   ;G/L Register No.    ;Integer       ;TableRelation="G/L Register";
                                                   CaptionML=ENU=G/L Register No. }
    { 5   ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=ENU=Source Code }
    { 6   ;   ;Journal Batch Name  ;Code10        ;CaptionML=ENU=Journal Batch Name }
    { 7   ;   ;Transaction No.     ;Integer       ;CaptionML=ENU=Transaction No. }
    { 8   ;   ;Source Type         ;Option        ;CaptionML=ENU=Source Type;
                                                   OptionCaptionML=ENU=" ,Customer,Vendor,Bank Account,Fixed Asset,IC Partner";
                                                   OptionString=" ",Customer,Vendor,"Bank Account","Fixed Asset","IC Partner" }
    { 9   ;   ;Source No.          ;Code20        ;TableRelation=IF ("Source Type"=CONST(Customer)) Customer
                                                                 ELSE IF ("Source Type"=CONST(Vendor)) Vendor
                                                                 ELSE IF ("Source Type"=CONST("Bank Account")) "Bank Account"
                                                                 ELSE IF ("Source Type"=CONST("Fixed Asset")) "Fixed Asset";
                                                   CaptionML=ENU=Source No. }
    { 10  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code }
    { 11  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 12  ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 13  ;   ;Debit Amount        ;Decimal       ;CaptionML=ENU=Debit Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 14  ;   ;Credit Amount       ;Decimal       ;CaptionML=ENU=Credit Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 15  ;   ;Amount (LCY)        ;Decimal       ;CaptionML=ENU=Amount (LCY);
                                                   AutoFormatType=1 }
    { 16  ;   ;Debit Amount (LCY)  ;Decimal       ;CaptionML=ENU=Debit Amount (LCY);
                                                   AutoFormatType=1 }
    { 17  ;   ;Credit Amount (LCY) ;Decimal       ;CaptionML=ENU=Credit Amount (LCY);
                                                   AutoFormatType=1 }
    { 18  ;   ;VAT Amount          ;Decimal       ;CaptionML=ENU=VAT Amount;
                                                   AutoFormatType=1 }
    { 19  ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 20  ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                   OptionString=" ",Payment,Invoice,"Credit Memo","Finance Charge Memo",Reminder,Refund }
    { 21  ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 22  ;   ;Account No.         ;Code20        ;CaptionML=ENU=Account No. }
    { 23  ;   ;Account Name        ;Text50        ;CaptionML=ENU=Account Name }
    { 25  ;   ;Bal. Account Type   ;Option        ;CaptionML=ENU=Bal. Account Type;
                                                   OptionCaptionML=ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner;
                                                   OptionString="G/L Account",Customer,Vendor,"Bank Account","Fixed Asset","IC Partner" }
    { 26  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF ("Bal. Account Type"=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF ("Bal. Account Type"=CONST(Customer)) Customer
                                                                 ELSE IF ("Bal. Account Type"=CONST(Vendor)) Vendor
                                                                 ELSE IF ("Bal. Account Type"=CONST("Bank Account")) "Bank Account"
                                                                 ELSE IF ("Bal. Account Type"=CONST("Fixed Asset")) "Fixed Asset";
                                                   CaptionML=ENU=Bal. Account No. }
    { 27  ;   ;FA Posting Category ;Option        ;AccessByPermission=TableData "Fixed Asset"=R;
                                                   CaptionML=ENU=FA Posting Category;
                                                   OptionCaptionML=ENU=" ,Disposal,Bal. Disposal";
                                                   OptionString=" ",Disposal,"Bal. Disposal" }
    { 28  ;   ;FA Posting Type     ;Option        ;AccessByPermission=TableData "Fixed Asset"=R;
                                                   CaptionML=ENU=FA Posting Type;
                                                   OptionCaptionML=ENU=" ,Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Proceeds on Disposal,Salvage Value,Gain/Loss,Book Value on Disposal";
                                                   OptionString=" ","Acquisition Cost",Depreciation,"Write-Down",Appreciation,"Custom 1","Custom 2","Proceeds on Disposal","Salvage Value","Gain/Loss","Book Value on Disposal" }
    { 30  ;   ;Reversal Type       ;Option        ;CaptionML=ENU=Reversal Type;
                                                   OptionCaptionML=ENU=Transaction,Register;
                                                   OptionString=Transaction,Register }
  }
  KEYS
  {
    {    ;"Line No."                              ;Clustered=true }
    {    ;"Entry Type"                             }
    {    ;"Document No.","Posting Date","Entry Type","Entry No." }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLSetup@1000 : Record "General Ledger Setup";
      TempReversalEntry@1001 : TEMPORARY Record "Reversal Entry";
      GLEntry@1007 : Record "G/L Entry";
      CustLedgEntry@1006 : Record "Cust. Ledger Entry";
      VendLedgEntry@1005 : Record "Vendor Ledger Entry";
      BankAccLedgEntry@1004 : Record "Bank Account Ledger Entry";
      VATEntry@1003 : Record "VAT Entry";
      FALedgEntry@1002 : Record "FA Ledger Entry";
      MaintenanceLedgEntry@1009 : Record "Maintenance Ledger Entry";
      GLReg@1008 : Record "G/L Register";
      Text000@1010 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry is either applied to an entry or has been changed by a batch job.';
      FAReg@1015 : Record "FA Register";
      GenJnlCheckLine@1011 : Codeunit "Gen. Jnl.-Check Line";
      Text001@1012 : TextConst 'ENU=You cannot reverse %1 No. %2 because the posting date is not within the allowed posting period.';
      AllowPostingFrom@1014 : Date;
      AllowPostingto@1013 : Date;
      Text002@1016 : TextConst 'ENU=You cannot reverse the transaction because it is out of balance.';
      Text003@1017 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry has a related check ledger entry.';
      Text004@1018 : TextConst 'ENU=You can only reverse entries that were posted from a journal.';
      Text005@1020 : TextConst 'ENU=You cannot reverse %1 No. %2 because the %3 is not within the allowed posting period.';
      Text006@1022 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry is closed.';
      Text007@1021 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry is included in a bank account reconciliation line. The bank reconciliation has not yet been posted.';
      Text008@1019 : TextConst 'ENU=You cannot reverse the transaction because the %1 has been sold.';
      MaxPostingDate@1024 : Date;
      Text009@1025 : TextConst 'ENU=The transaction cannot be reversed, because the %1 has been compressed or a %2 has been deleted.';
      Text010@1023 : TextConst 'ENU=You cannot reverse %1 No. %2 because the register has already been involved in a reversal.';
      Text011@1026 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry has already been involved in a reversal.';
      Text012@1028 : TextConst 'ENU=You cannot reverse register No. %1 because it contains customer or vendor ledger entries that have been posted and applied in the same transaction.\\You must reverse each transaction in register No. %1 separately.';
      Text013@1039 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry has an associated Realized Gain/Loss entry.';
      HideDialog@1030 : Boolean;
      UnrealizedVATReverseErr@1027 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry has an associated Unrealized VAT Entry.';
      HideWarningDialogs@1029 : Boolean;

    [External]
    PROCEDURE ReverseTransaction@8(TransactionNo@1000 : Integer);
    BEGIN
      ReverseEntries(TransactionNo,"Reversal Type"::Transaction);
    END;

    [External]
    PROCEDURE ReverseRegister@9(RegisterNo@1000 : Integer);
    VAR
      GLReg@1001 : Record "G/L Register";
    BEGIN
      GLReg.GET(RegisterNo);
      if GLReg.Reversed then
        ERROR(Text010,GLReg.TABLECAPTION,GLReg."No.");
      if GLReg."Journal Batch Name" = '' then
        TempReversalEntry.TestFieldError;

      ReverseEntries(RegisterNo,"Reversal Type"::Register);
    END;

    LOCAL PROCEDURE ReverseEntries@32(Number@1001 : Integer;RevType@1000 : 'Transaction,Register');
    VAR
      ReversalPost@1002 : Codeunit "Reversal-Post";
    BEGIN
      InsertReversalEntry(Number,RevType);
      TempReversalEntry.SETCURRENTKEY("Document No.","Posting Date","Entry Type","Entry No.");
      if not HideDialog then
        PAGE.RUNMODAL(PAGE::"Reverse Entries",TempReversalEntry)
      else begin
        ReversalPost.SetPrint(false);
        ReversalPost.SetHideDialog(HideWarningDialogs);
        ReversalPost.RUN(TempReversalEntry);
      end;
      TempReversalEntry.DELETEALL;
    END;

    LOCAL PROCEDURE InsertReversalEntry@7(Number@1000 : Integer;RevType@1007 : 'Transaction,Register');
    VAR
      TempRevertTransactionNo@1005 : TEMPORARY Record Integer;
      NextLineNo@1008 : Integer;
    BEGIN
      GLSetup.GET;
      TempReversalEntry.DELETEALL;
      NextLineNo := 1;
      TempRevertTransactionNo.Number := Number;
      TempRevertTransactionNo.INSERT;
      SetReverseFilter(Number,RevType);

      InsertFromCustLedgEntry(TempRevertTransactionNo,Number,RevType,NextLineNo);
      InsertFromVendLedgEntry(TempRevertTransactionNo,Number,RevType,NextLineNo);
      InsertFromBankAccLedgEntry(Number,RevType,NextLineNo);
      InsertFromFALedgEntry(Number,RevType,NextLineNo);
      InsertFromMaintenanceLedgEntry(Number,RevType,NextLineNo);
      InsertFromVATEntry(TempRevertTransactionNo,Number,RevType,NextLineNo);
      InsertFromGLEntry(TempRevertTransactionNo,Number,RevType,NextLineNo);
      if TempReversalEntry.FIND('-') then;
    END;

    [External]
    PROCEDURE CheckEntries@14();
    VAR
      GLAcc@1001 : Record "G/L Account";
      DtldCustLedgEntry@1006 : Record "Detailed Cust. Ledg. Entry";
      DtldVendLedgEntry@1007 : Record "Detailed Vendor Ledg. Entry";
      DateComprReg@1000 : Record "Date Compr. Register";
      BalanceCheckAmount@1012 : Decimal;
      BalanceCheckAddCurrAmount@1010 : Decimal;
    BEGIN
      DtldCustLedgEntry.LOCKTABLE;
      DtldVendLedgEntry.LOCKTABLE;
      GLEntry.LOCKTABLE;
      CustLedgEntry.LOCKTABLE;
      VendLedgEntry.LOCKTABLE;
      BankAccLedgEntry.LOCKTABLE;
      FALedgEntry.LOCKTABLE;
      MaintenanceLedgEntry.LOCKTABLE;
      VATEntry.LOCKTABLE;
      GLReg.LOCKTABLE;
      FAReg.LOCKTABLE;
      GLSetup.GET;
      MaxPostingDate := 0D;
      if not GLEntry.FIND('-') then
        ERROR(Text009,GLEntry.TABLECAPTION,GLAcc.TABLECAPTION);
      if GLEntry.FIND('-') then begin
        if GLEntry."Journal Batch Name" = '' then
          TestFieldError;
        repeat
          CheckGLAcc(GLEntry,BalanceCheckAmount,BalanceCheckAddCurrAmount);
        until GLEntry.NEXT = 0;
      end;
      if (BalanceCheckAmount <> 0) or (BalanceCheckAddCurrAmount <> 0) then
        ERROR(Text002);

      if CustLedgEntry.FIND('-') then
        repeat
          CheckCust(CustLedgEntry);
        until CustLedgEntry.NEXT = 0;

      if VendLedgEntry.FIND('-') then
        repeat
          CheckVend(VendLedgEntry);
        until VendLedgEntry.NEXT = 0;

      if BankAccLedgEntry.FIND('-') then
        repeat
          CheckBankAcc(BankAccLedgEntry);
        until BankAccLedgEntry.NEXT = 0;

      if FALedgEntry.FIND('-') then
        repeat
          CheckFA(FALedgEntry);
        until FALedgEntry.NEXT = 0;

      if MaintenanceLedgEntry.FIND('-') then
        repeat
          CheckMaintenance(MaintenanceLedgEntry);
        until MaintenanceLedgEntry.NEXT = 0;

      if VATEntry.FIND('-') then
        repeat
          CheckVAT(VATEntry);
        until VATEntry.NEXT = 0;
      DateComprReg.CheckMaxDateCompressed(MaxPostingDate,1);
    END;

    LOCAL PROCEDURE CheckGLAcc@13(GLEntry@1000 : Record "G/L Entry";VAR BalanceCheckAmount@1002 : Decimal;VAR BalanceCheckAddCurrAmount@1001 : Decimal);
    VAR
      GLAcc@1003 : Record "G/L Account";
    BEGIN
      GLAcc.GET(GLEntry."G/L Account No.");
      CheckPostingDate(GLEntry."Posting Date",GLEntry.TABLECAPTION,GLEntry."Entry No.");
      GLAcc.TESTFIELD(Blocked,false);
      GLEntry.TESTFIELD("Job No.",'');
      if GLEntry.Reversed then
        AlreadyReversedEntry(GLEntry.TABLECAPTION,GLEntry."Entry No.");
      BalanceCheckAmount := BalanceCheckAmount + GLEntry.Amount;
      if GLSetup."Additional Reporting Currency" <> '' then
        BalanceCheckAddCurrAmount := BalanceCheckAddCurrAmount + GLEntry."Additional-Currency Amount";
    END;

    LOCAL PROCEDURE CheckCust@16(CustLedgEntry@1001 : Record "Cust. Ledger Entry");
    VAR
      Cust@1000 : Record Customer;
    BEGIN
      Cust.GET(CustLedgEntry."Customer No.");
      CheckPostingDate(
        CustLedgEntry."Posting Date",CustLedgEntry.TABLECAPTION,CustLedgEntry."Entry No.");
      Cust.CheckBlockedCustOnJnls(Cust,CustLedgEntry."Document Type",false);
      if CustLedgEntry.Reversed then
        AlreadyReversedEntry(CustLedgEntry.TABLECAPTION,CustLedgEntry."Entry No.");
      CheckDtldCustLedgEntry(CustLedgEntry);
    END;

    LOCAL PROCEDURE CheckVend@17(VendLedgEntry@1001 : Record "Vendor Ledger Entry");
    VAR
      Vend@1000 : Record Vendor;
    BEGIN
      Vend.GET(VendLedgEntry."Vendor No.");
      CheckPostingDate(
        VendLedgEntry."Posting Date",VendLedgEntry.TABLECAPTION,VendLedgEntry."Entry No.");
      Vend.CheckBlockedVendOnJnls(Vend,VendLedgEntry."Document Type",false);
      if VendLedgEntry.Reversed then
        AlreadyReversedEntry(VendLedgEntry.TABLECAPTION,VendLedgEntry."Entry No.");
      CheckDtldVendLedgEntry(VendLedgEntry);
    END;

    LOCAL PROCEDURE CheckBankAcc@18(BankAccLedgEntry@1000 : Record "Bank Account Ledger Entry");
    VAR
      BankAcc@1001 : Record "Bank Account";
      CheckLedgEntry@1002 : Record "Check Ledger Entry";
    BEGIN
      BankAcc.GET(BankAccLedgEntry."Bank Account No.");
      CheckPostingDate(
        BankAccLedgEntry."Posting Date",BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry."Entry No.");
      BankAcc.TESTFIELD(Blocked,false);
      if BankAccLedgEntry.Reversed then
        AlreadyReversedEntry(BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry."Entry No.");
      if not BankAccLedgEntry.Open then
        ERROR(
          Text006,BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry."Entry No.");
      if BankAccLedgEntry."Statement No." <> '' then
        ERROR(
          Text007,BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry."Entry No.");
      CheckLedgEntry.SETRANGE("Bank Account Ledger Entry No.",BankAccLedgEntry."Entry No.");
      if not CheckLedgEntry.ISEMPTY then
        ERROR(
          Text003,BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry."Entry No.");
    END;

    LOCAL PROCEDURE CheckFA@19(FALedgEntry@1000 : Record "FA Ledger Entry");
    VAR
      FA@1001 : Record "Fixed Asset";
      FADeprBook@1002 : Record "FA Depreciation Book";
      DeprCalc@1003 : Codeunit "Depreciation Calculation";
    BEGIN
      FA.GET(FALedgEntry."FA No.");
      CheckPostingDate(
        FALedgEntry."Posting Date",FALedgEntry.TABLECAPTION,FALedgEntry."Entry No.");
      CheckFAPostingDate(
        FALedgEntry."FA Posting Date",FALedgEntry.TABLECAPTION,FALedgEntry."Entry No.");
      FA.TESTFIELD(Blocked,false);
      FA.TESTFIELD(Inactive,false);
      if FALedgEntry.Reversed then
        AlreadyReversedEntry(FALedgEntry.TABLECAPTION,FALedgEntry."Entry No.");
      FALedgEntry.TESTFIELD("Depreciation Book Code");
      FADeprBook.GET(FA."No.",FALedgEntry."Depreciation Book Code");
      if FADeprBook."Disposal Date" <> 0D then
        ERROR(Text008,DeprCalc.FAName(FA,FALedgEntry."Depreciation Book Code"));
      FALedgEntry.TESTFIELD("G/L Entry No.");
    END;

    LOCAL PROCEDURE CheckMaintenance@20(MaintenanceLedgEntry@1000 : Record "Maintenance Ledger Entry");
    VAR
      FA@1001 : Record "Fixed Asset";
      FADeprBook@1002 : Record "FA Depreciation Book";
    BEGIN
      FA.GET(MaintenanceLedgEntry."FA No.");
      CheckPostingDate(
        MaintenanceLedgEntry."Posting Date",MaintenanceLedgEntry.TABLECAPTION,MaintenanceLedgEntry."Entry No.");
      CheckFAPostingDate(
        MaintenanceLedgEntry."FA Posting Date",MaintenanceLedgEntry.TABLECAPTION,MaintenanceLedgEntry."Entry No.");
      FA.TESTFIELD(Blocked,false);
      FA.TESTFIELD(Inactive,false);
      MaintenanceLedgEntry.TESTFIELD("Depreciation Book Code");
      if MaintenanceLedgEntry.Reversed then
        AlreadyReversedEntry(MaintenanceLedgEntry.TABLECAPTION,MaintenanceLedgEntry."Entry No.");
      FADeprBook.GET(FA."No.",MaintenanceLedgEntry."Depreciation Book Code");
      MaintenanceLedgEntry.TESTFIELD("G/L Entry No.");
    END;

    LOCAL PROCEDURE CheckVAT@21(VATEntry@1000 : Record "VAT Entry");
    BEGIN
      CheckPostingDate(VATEntry."Posting Date",VATEntry.TABLECAPTION,VATEntry."Entry No.");
      if VATEntry.Closed then
        ERROR(
          Text006,VATEntry.TABLECAPTION,VATEntry."Entry No.");
      if VATEntry.Reversed then
        AlreadyReversedEntry(VATEntry.TABLECAPTION,VATEntry."Entry No.");
      if VATEntry."Unrealized VAT Entry No." <> 0 then
        ERROR(UnrealizedVATReverseError(VATEntry.TABLECAPTION,VATEntry."Entry No."));
    END;

    LOCAL PROCEDURE CheckDtldCustLedgEntry@2(CustLedgEntry@1000 : Record "Cust. Ledger Entry");
    VAR
      DtldCustLedgEntry@1001 : Record "Detailed Cust. Ledg. Entry";
    BEGIN
      DtldCustLedgEntry.SETCURRENTKEY("Cust. Ledger Entry No.","Entry Type");
      DtldCustLedgEntry.SETRANGE("Cust. Ledger Entry No.",CustLedgEntry."Entry No.");
      DtldCustLedgEntry.SETFILTER("Entry Type",'<>%1',DtldCustLedgEntry."Entry Type"::"Initial Entry");
      DtldCustLedgEntry.SETRANGE(Unapplied,false);
      if not DtldCustLedgEntry.ISEMPTY then
        ERROR(ReversalErrorForChangedEntry(CustLedgEntry.TABLECAPTION,CustLedgEntry."Entry No."));

      DtldCustLedgEntry.RESET;
      DtldCustLedgEntry.SETCURRENTKEY("Transaction No.","Customer No.","Entry Type");
      DtldCustLedgEntry.SETRANGE("Transaction No.",CustLedgEntry."Transaction No.");
      DtldCustLedgEntry.SETRANGE("Customer No.",CustLedgEntry."Customer No.");
      DtldCustLedgEntry.SETFILTER("Entry Type",'%1|%2',
        DtldCustLedgEntry."Entry Type"::"Realized Gain",DtldCustLedgEntry."Entry Type"::"Realized Loss");
      if not DtldCustLedgEntry.ISEMPTY then
        ERROR(Text013,CustLedgEntry.TABLECAPTION,CustLedgEntry."Entry No.");
    END;

    LOCAL PROCEDURE CheckDtldVendLedgEntry@28(VendLedgEntry@1000 : Record "Vendor Ledger Entry");
    VAR
      DtldVendLedgEntry@1001 : Record "Detailed Vendor Ledg. Entry";
    BEGIN
      DtldVendLedgEntry.SETCURRENTKEY("Vendor Ledger Entry No.","Entry Type");
      DtldVendLedgEntry.SETRANGE("Vendor Ledger Entry No.",VendLedgEntry."Entry No.");
      DtldVendLedgEntry.SETFILTER("Entry Type",'<>%1',DtldVendLedgEntry."Entry Type"::"Initial Entry");
      DtldVendLedgEntry.SETRANGE(Unapplied,false);
      if not DtldVendLedgEntry.ISEMPTY then
        ERROR(ReversalErrorForChangedEntry(VendLedgEntry.TABLECAPTION,VendLedgEntry."Entry No."));

      DtldVendLedgEntry.RESET;
      DtldVendLedgEntry.SETCURRENTKEY("Transaction No.","Vendor No.","Entry Type");
      DtldVendLedgEntry.SETRANGE("Transaction No.",VendLedgEntry."Transaction No.");
      DtldVendLedgEntry.SETRANGE("Vendor No.",VendLedgEntry."Vendor No.");
      DtldVendLedgEntry.SETFILTER("Entry Type",'%1|%2',
        DtldVendLedgEntry."Entry Type"::"Realized Gain",DtldVendLedgEntry."Entry Type"::"Realized Loss");
      if not DtldVendLedgEntry.ISEMPTY then
        ERROR(Text013,VendLedgEntry.TABLECAPTION,VendLedgEntry."Entry No.");
    END;

    [External]
    PROCEDURE SetReverseFilter@1(Number@1001 : Integer;RevType@1000 : 'Transaction,Register');
    BEGIN
      if RevType = RevType::Transaction then begin
        GLEntry.SETCURRENTKEY("Transaction No.");
        CustLedgEntry.SETCURRENTKEY("Transaction No.");
        VendLedgEntry.SETCURRENTKEY("Transaction No.");
        BankAccLedgEntry.SETCURRENTKEY("Transaction No.");
        FALedgEntry.SETCURRENTKEY("Transaction No.");
        MaintenanceLedgEntry.SETCURRENTKEY("Transaction No.");
        VATEntry.SETCURRENTKEY("Transaction No.");
        GLEntry.SETRANGE("Transaction No.",Number);
        CustLedgEntry.SETRANGE("Transaction No.",Number);
        VendLedgEntry.SETRANGE("Transaction No.",Number);
        BankAccLedgEntry.SETRANGE("Transaction No.",Number);
        FALedgEntry.SETRANGE("Transaction No.",Number);
        MaintenanceLedgEntry.SETRANGE("Transaction No.",Number);
        VATEntry.SETRANGE("Transaction No.",Number);
      end else begin
        GLReg.GET(Number);
        GLEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        CustLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        VendLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        BankAccLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        FALedgEntry.SETCURRENTKEY("G/L Entry No.");
        FALedgEntry.SETRANGE("G/L Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        MaintenanceLedgEntry.SETCURRENTKEY("G/L Entry No.");
        MaintenanceLedgEntry.SETRANGE("G/L Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        VATEntry.SETRANGE("Entry No.",GLReg."From VAT Entry No.",GLReg."To VAT Entry No.");
      end;
    END;

    [External]
    PROCEDURE CopyReverseFilters@15(VAR GLEntry2@1007 : Record "G/L Entry";VAR CustLedgEntry2@1006 : Record "Cust. Ledger Entry";VAR VendLedgEntry2@1005 : Record "Vendor Ledger Entry";VAR BankAccLedgEntry2@1004 : Record "Bank Account Ledger Entry";VAR VATEntry2@1003 : Record "VAT Entry";VAR FALedgEntry2@1002 : Record "FA Ledger Entry";VAR MaintenanceLedgEntry2@1001 : Record "Maintenance Ledger Entry");
    BEGIN
      GLEntry2.COPY(GLEntry);
      CustLedgEntry2.COPY(CustLedgEntry);
      VendLedgEntry2.COPY(VendLedgEntry);
      BankAccLedgEntry2.COPY(BankAccLedgEntry);
      VATEntry2.COPY(VATEntry);
      FALedgEntry2.COPY(FALedgEntry);
      MaintenanceLedgEntry2.COPY(MaintenanceLedgEntry);
    END;

    [External]
    PROCEDURE ShowGLEntries@22();
    BEGIN
      PAGE.RUN(0,GLEntry);
    END;

    [External]
    PROCEDURE ShowCustLedgEntries@26();
    BEGIN
      PAGE.RUN(0,CustLedgEntry);
    END;

    [External]
    PROCEDURE ShowVendLedgEntries@5();
    BEGIN
      PAGE.RUN(0,VendLedgEntry);
    END;

    [External]
    PROCEDURE ShowBankAccLedgEntries@6();
    BEGIN
      PAGE.RUN(0,BankAccLedgEntry);
    END;

    [External]
    PROCEDURE ShowFALedgEntries@10();
    BEGIN
      PAGE.RUN(0,FALedgEntry);
    END;

    [External]
    PROCEDURE ShowMaintenanceLedgEntries@12();
    BEGIN
      PAGE.RUN(0,MaintenanceLedgEntry);
    END;

    [External]
    PROCEDURE ShowVATEntries@11();
    BEGIN
      PAGE.RUN(0,VATEntry);
    END;

    [External]
    PROCEDURE Caption@3() : Text[250];
    VAR
      GLAcc@1000 : Record "G/L Account";
      GLEntry@1002 : Record "G/L Entry";
      Cust@1001 : Record Customer;
      CustLedgEntry@1003 : Record "Cust. Ledger Entry";
      Vend@1004 : Record Vendor;
      VendLedgEntry@1005 : Record "Vendor Ledger Entry";
      BankAcc@1006 : Record "Bank Account";
      BankAccLedgEntry@1007 : Record "Bank Account Ledger Entry";
      FA@1008 : Record "Fixed Asset";
      FALedgEntry@1009 : Record "FA Ledger Entry";
      MaintenanceLedgEntry@1011 : Record "Maintenance Ledger Entry";
      VATEntry@1010 : Record "VAT Entry";
    BEGIN
      if "Entry Type" = "Entry Type"::"G/L Account" then begin
        if GLEntry.GET("Entry No.") then;
        if GLAcc.GET(GLEntry."G/L Account No.") then;
        exit(STRSUBSTNO('%1 %2 %3',GLAcc.TABLECAPTION,GLAcc."No.",GLAcc.Name));
      end;
      if "Entry Type" = "Entry Type"::Customer then begin
        if CustLedgEntry.GET("Entry No.") then;
        if Cust.GET(CustLedgEntry."Customer No.") then;
        exit(STRSUBSTNO('%1 %2 %3',Cust.TABLECAPTION,Cust."No.",Cust.Name));
      end;
      if "Entry Type" = "Entry Type"::Vendor then begin
        if VendLedgEntry.GET("Entry No.") then;
        if Vend.GET(VendLedgEntry."Vendor No.") then;
        exit(STRSUBSTNO('%1 %2 %3',Vend.TABLECAPTION,Vend."No.",Vend.Name));
      end;
      if "Entry Type" = "Entry Type"::"Bank Account" then begin
        if BankAccLedgEntry.GET("Entry No.") then;
        if BankAcc.GET(BankAccLedgEntry."Bank Account No.") then;
        exit(STRSUBSTNO('%1 %2 %3',BankAcc.TABLECAPTION,BankAcc."No.",BankAcc.Name));
      end;
      if "Entry Type" = "Entry Type"::"Fixed Asset" then begin
        if FALedgEntry.GET("Entry No.") then;
        if FA.GET(FALedgEntry."FA No.") then;
        exit(STRSUBSTNO('%1 %2 %3',FA.TABLECAPTION,FA."No.",FA.Description));
      end;
      if "Entry Type" = "Entry Type"::Maintenance then begin
        if MaintenanceLedgEntry.GET("Entry No.") then;
        if FA.GET(MaintenanceLedgEntry."FA No.") then;
        exit(STRSUBSTNO('%1 %2 %3',FA.TABLECAPTION,FA."No.",FA.Description));
      end;
      if "Entry Type" = "Entry Type"::VAT then
        exit(STRSUBSTNO('%1',VATEntry.TABLECAPTION));
    END;

    LOCAL PROCEDURE CheckPostingDate@23(PostingDate@1000 : Date;Caption@1002 : Text[50];EntryNo@1001 : Integer);
    BEGIN
      if GenJnlCheckLine.DateNotAllowed(PostingDate) then
        ERROR(Text001,Caption,EntryNo);
      if PostingDate > MaxPostingDate then
        MaxPostingDate := PostingDate;
    END;

    LOCAL PROCEDURE CheckFAPostingDate@24(FAPostingDate@1000 : Date;Caption@1004 : Text[50];EntryNo@1003 : Integer);
    VAR
      UserSetup@1001 : Record "User Setup";
      FASetup@1002 : Record "FA Setup";
    BEGIN
      if (AllowPostingFrom = 0D) and (AllowPostingto = 0D) then begin
        if USERID <> '' then
          if UserSetup.GET(USERID) then begin
            AllowPostingFrom := UserSetup."Allow FA Posting From";
            AllowPostingto := UserSetup."Allow FA Posting To";
          end;
        if (AllowPostingFrom = 0D) and (AllowPostingto = 0D) then begin
          FASetup.GET;
          AllowPostingFrom := FASetup."Allow FA Posting From";
          AllowPostingto := FASetup."Allow FA Posting To";
        end;
        if AllowPostingto = 0D then
          AllowPostingto := 99981231D;
      end;
      if (FAPostingDate < AllowPostingFrom) or (FAPostingDate > AllowPostingto) then
        ERROR(Text005,Caption,EntryNo,FALedgEntry.FIELDCAPTION("FA Posting Date"));
      if FAPostingDate > MaxPostingDate then
        MaxPostingDate := FAPostingDate;
    END;

    [External]
    PROCEDURE TestFieldError@4();
    BEGIN
      ERROR(Text004);
    END;

    [External]
    PROCEDURE AlreadyReversedEntry@29(Caption@1000 : Text[50];EntryNo@1001 : Integer);
    BEGIN
      ERROR(Text011,Caption,EntryNo);
    END;

    [Internal]
    PROCEDURE VerifyReversalEntries@25(VAR ReversalEntry2@1000 : Record "Reversal Entry";Number@1002 : Integer;RevType@1001 : 'Transaction,Register') : Boolean;
    BEGIN
      InsertReversalEntry(Number,RevType);
      CLEAR(TempReversalEntry);
      CLEAR(ReversalEntry2);
      if ReversalEntry2.FINDSET then
        repeat
          if TempReversalEntry.NEXT = 0 then
            exit(false);
          if not TempReversalEntry.Equal(ReversalEntry2) then
            exit(false);
        until ReversalEntry2.NEXT = 0;
      exit(TempReversalEntry.NEXT = 0);
    END;

    [External]
    PROCEDURE Equal@27(ReversalEntry2@1000 : Record "Reversal Entry") : Boolean;
    BEGIN
      exit(
        ("Entry Type" = ReversalEntry2."Entry Type") and
        ("Entry No." = ReversalEntry2."Entry No."));
    END;

    [External]
    PROCEDURE ReversalErrorForChangedEntry@31(TableName@1000 : Text[50];EntryNo@1001 : Integer) : Text[1024];
    BEGIN
      exit(STRSUBSTNO(Text000,TableName,EntryNo));
    END;

    [External]
    PROCEDURE SetHideDialog@30(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    [External]
    PROCEDURE SetHideWarningDialogs@41();
    BEGIN
      HideDialog := true;
      HideWarningDialogs := true;
    END;

    LOCAL PROCEDURE UnrealizedVATReverseError@33(TableName@1001 : Text[50];EntryNo@1000 : Integer) : Text;
    BEGIN
      exit(STRSUBSTNO(UnrealizedVATReverseErr,TableName,EntryNo));
    END;

    LOCAL PROCEDURE InsertFromCustLedgEntry@34(VAR TempRevertTransactionNo@1006 : TEMPORARY Record Integer;Number@1001 : Integer;RevType@1002 : 'Transaction,Register';VAR NextLineNo@1003 : Integer);
    VAR
      Cust@1000 : Record Customer;
      DtldCustLedgEntry@1004 : Record "Detailed Cust. Ledg. Entry";
    BEGIN
      DtldCustLedgEntry.SETCURRENTKEY("Transaction No.","Customer No.","Entry Type");
      DtldCustLedgEntry.SETFILTER(
        "Entry Type",'<>%1',DtldCustLedgEntry."Entry Type"::"Initial Entry");
      if CustLedgEntry.FINDSET then
        repeat
          DtldCustLedgEntry.SETRANGE("Transaction No.",CustLedgEntry."Transaction No.");
          DtldCustLedgEntry.SETRANGE("Customer No.",CustLedgEntry."Customer No.");
          if (not DtldCustLedgEntry.ISEMPTY) and (RevType = RevType::Register) then
            ERROR(Text012,Number);

          CLEAR(TempReversalEntry);
          if RevType = RevType::Register then
            TempReversalEntry."G/L Register No." := Number;
          TempReversalEntry."Reversal Type" := RevType;
          TempReversalEntry."Entry Type" := TempReversalEntry."Entry Type"::Customer;
          Cust.GET(CustLedgEntry."Customer No.");
          TempReversalEntry."Account No." := Cust."No.";
          TempReversalEntry."Account Name" := Cust.Name;
          TempReversalEntry.CopyFromCustLedgEntry(CustLedgEntry);
          TempReversalEntry."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 1;
          TempReversalEntry.INSERT;

          DtldCustLedgEntry.SETRANGE(Unapplied,true);
          if DtldCustLedgEntry.FINDSET then
            repeat
              InsertCustTempRevertTransNo(TempRevertTransactionNo,DtldCustLedgEntry."Unapplied by Entry No.");
            until DtldCustLedgEntry.NEXT = 0;
          DtldCustLedgEntry.SETRANGE(Unapplied);
        until CustLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertFromVendLedgEntry@35(VAR TempRevertTransactionNo@1005 : TEMPORARY Record Integer;Number@1002 : Integer;RevType@1001 : 'Transaction,Register';VAR NextLineNo@1000 : Integer);
    VAR
      Vend@1003 : Record Vendor;
      DtldVendLedgEntry@1004 : Record "Detailed Vendor Ledg. Entry";
    BEGIN
      DtldVendLedgEntry.SETCURRENTKEY("Transaction No.","Vendor No.","Entry Type");
      DtldVendLedgEntry.SETFILTER(
        "Entry Type",'<>%1',DtldVendLedgEntry."Entry Type"::"Initial Entry");
      if VendLedgEntry.FINDSET then
        repeat
          DtldVendLedgEntry.SETRANGE("Transaction No.",VendLedgEntry."Transaction No.");
          DtldVendLedgEntry.SETRANGE("Vendor No.",VendLedgEntry."Vendor No.");
          if (not DtldVendLedgEntry.ISEMPTY) and (RevType = RevType::Register) then
            ERROR(Text012,Number);

          CLEAR(TempReversalEntry);
          if RevType = RevType::Register then
            TempReversalEntry."G/L Register No." := Number;
          TempReversalEntry."Reversal Type" := RevType;
          TempReversalEntry."Entry Type" := TempReversalEntry."Entry Type"::Vendor;
          Vend.GET(VendLedgEntry."Vendor No.");
          TempReversalEntry."Account No." := Vend."No.";
          TempReversalEntry."Account Name" := Vend.Name;
          TempReversalEntry.CopyFromVendLedgEntry(VendLedgEntry);
          TempReversalEntry."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 1;
          TempReversalEntry.INSERT;

          DtldVendLedgEntry.SETRANGE(Unapplied,true);
          if DtldVendLedgEntry.FINDSET then
            repeat
              InsertVendTempRevertTransNo(TempRevertTransactionNo,DtldVendLedgEntry."Unapplied by Entry No.");
            until DtldVendLedgEntry.NEXT = 0;
          DtldVendLedgEntry.SETRANGE(Unapplied);
        until VendLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertFromBankAccLedgEntry@36(Number@1002 : Integer;RevType@1001 : 'Transaction,Register';VAR NextLineNo@1000 : Integer);
    VAR
      BankAcc@1003 : Record "Bank Account";
    BEGIN
      if BankAccLedgEntry.FINDSET then
        repeat
          CLEAR(TempReversalEntry);
          if RevType = RevType::Register then
            TempReversalEntry."G/L Register No." := Number;
          TempReversalEntry."Reversal Type" := RevType;
          TempReversalEntry."Entry Type" := TempReversalEntry."Entry Type"::"Bank Account";
          BankAcc.GET(BankAccLedgEntry."Bank Account No.");
          TempReversalEntry."Account No." := BankAcc."No.";
          TempReversalEntry."Account Name" := BankAcc.Name;
          TempReversalEntry.CopyFromBankAccLedgEntry(BankAccLedgEntry);
          TempReversalEntry."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 1;
          TempReversalEntry.INSERT;
        until BankAccLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertFromFALedgEntry@37(Number@1002 : Integer;RevType@1001 : 'Transaction,Register';VAR NextLineNo@1000 : Integer);
    VAR
      FA@1003 : Record "Fixed Asset";
    BEGIN
      if FALedgEntry.FINDSET then
        repeat
          CLEAR(TempReversalEntry);
          if RevType = RevType::Register then
            TempReversalEntry."G/L Register No." := Number;
          TempReversalEntry."Reversal Type" := RevType;
          TempReversalEntry."Entry Type" := TempReversalEntry."Entry Type"::"Fixed Asset";
          FA.GET(FALedgEntry."FA No.");
          TempReversalEntry."Account No." := FA."No.";
          TempReversalEntry."Account Name" := FA.Description;
          TempReversalEntry.CopyFromFALedgEntry(FALedgEntry);
          if FALedgEntry."FA Posting Type" <> FALedgEntry."FA Posting Type"::"Salvage Value" then begin
            TempReversalEntry."Line No." := NextLineNo;
            NextLineNo := NextLineNo + 1;
            TempReversalEntry.INSERT;
          end;
        until FALedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertFromMaintenanceLedgEntry@38(Number@1002 : Integer;RevType@1001 : 'Transaction,Register';VAR NextLineNo@1000 : Integer);
    VAR
      FA@1003 : Record "Fixed Asset";
    BEGIN
      if MaintenanceLedgEntry.FINDSET then
        repeat
          CLEAR(TempReversalEntry);
          if RevType = RevType::Register then
            TempReversalEntry."G/L Register No." := Number;
          TempReversalEntry."Reversal Type" := RevType;
          TempReversalEntry."Entry Type" := TempReversalEntry."Entry Type"::Maintenance;
          FA.GET(MaintenanceLedgEntry."FA No.");
          TempReversalEntry."Account No." := FA."No.";
          TempReversalEntry."Account Name" := FA.Description;
          TempReversalEntry.CopyFromMaintenanceEntry(MaintenanceLedgEntry);
          TempReversalEntry."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 1;
          TempReversalEntry.INSERT;
        until MaintenanceLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertFromVATEntry@39(VAR TempRevertTransactionNo@1004 : TEMPORARY Record Integer;Number@1002 : Integer;RevType@1001 : 'Transaction,Register';VAR NextLineNo@1000 : Integer);
    BEGIN
      TempRevertTransactionNo.FINDSET;
      repeat
        if RevType = RevType::Transaction then
          VATEntry.SETRANGE("Transaction No.",TempRevertTransactionNo.Number);
        if VATEntry.FINDSET then
          repeat
            CLEAR(TempReversalEntry);
            if RevType = RevType::Register then
              TempReversalEntry."G/L Register No." := Number;
            TempReversalEntry."Reversal Type" := RevType;
            TempReversalEntry."Entry Type" := TempReversalEntry."Entry Type"::VAT;
            TempReversalEntry.CopyFromVATEntry(VATEntry);
            TempReversalEntry."Line No." := NextLineNo;
            NextLineNo := NextLineNo + 1;
            TempReversalEntry.INSERT;
          until VATEntry.NEXT = 0;
      until TempRevertTransactionNo.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertFromGLEntry@40(VAR TempRevertTransactionNo@1005 : TEMPORARY Record Integer;Number@1002 : Integer;RevType@1001 : 'Transaction,Register';VAR NextLineNo@1000 : Integer);
    VAR
      GLAcc@1004 : Record "G/L Account";
    BEGIN
      TempRevertTransactionNo.FINDSET;
      repeat
        if RevType = RevType::Transaction then
          GLEntry.SETRANGE("Transaction No.",TempRevertTransactionNo.Number);
        if GLEntry.FINDSET then
          repeat
            CLEAR(TempReversalEntry);
            if RevType = RevType::Register then
              TempReversalEntry."G/L Register No." := Number;
            TempReversalEntry."Reversal Type" := RevType;
            TempReversalEntry."Entry Type" := TempReversalEntry."Entry Type"::"G/L Account";
            if not GLAcc.GET(GLEntry."G/L Account No.") then
              ERROR(Text009,GLEntry.TABLECAPTION,GLAcc.TABLECAPTION);
            TempReversalEntry."Account No." := GLAcc."No.";
            TempReversalEntry."Account Name" := GLAcc.Name;
            TempReversalEntry.CopyFromGLEntry(GLEntry);
            TempReversalEntry."Line No." := NextLineNo;
            NextLineNo := NextLineNo + 1;
            TempReversalEntry.INSERT;
          until GLEntry.NEXT = 0;
      until TempRevertTransactionNo.NEXT = 0;
    END;

    PROCEDURE CopyFromCustLedgEntry@57(CustLedgEntry@1001 : Record "Cust. Ledger Entry");
    BEGIN
      "Entry No." := CustLedgEntry."Entry No.";
      "Posting Date" := CustLedgEntry."Posting Date";
      "Source Code" := CustLedgEntry."Source Code";
      "Journal Batch Name" := CustLedgEntry."Journal Batch Name";
      "Transaction No." := CustLedgEntry."Transaction No.";
      "Currency Code" := CustLedgEntry."Currency Code";
      Description := CustLedgEntry.Description;
      CustLedgEntry.CALCFIELDS(Amount,"Debit Amount","Credit Amount",
        "Amount (LCY)","Debit Amount (LCY)","Credit Amount (LCY)");
      Amount := CustLedgEntry.Amount;
      "Debit Amount" := CustLedgEntry."Debit Amount";
      "Credit Amount" := CustLedgEntry."Credit Amount";
      "Amount (LCY)" := CustLedgEntry."Amount (LCY)";
      "Debit Amount (LCY)" := CustLedgEntry."Debit Amount (LCY)";
      "Credit Amount (LCY)" := CustLedgEntry."Credit Amount (LCY)";
      "Document Type" := CustLedgEntry."Document Type";
      "Document No." := CustLedgEntry."Document No.";
      "Bal. Account Type" := CustLedgEntry."Bal. Account Type";
      "Bal. Account No." := CustLedgEntry."Bal. Account No.";
    END;

    PROCEDURE CopyFromBankAccLedgEntry@53(BankAccLedgEntry@1001 : Record "Bank Account Ledger Entry");
    BEGIN
      "Entry No." := BankAccLedgEntry."Entry No.";
      "Posting Date" := BankAccLedgEntry."Posting Date";
      "Source Code" := BankAccLedgEntry."Source Code";
      "Journal Batch Name" := BankAccLedgEntry."Journal Batch Name";
      "Transaction No." := BankAccLedgEntry."Transaction No.";
      "Currency Code" := BankAccLedgEntry."Currency Code";
      Description := BankAccLedgEntry.Description;
      Amount := BankAccLedgEntry.Amount;
      "Debit Amount" := BankAccLedgEntry."Debit Amount";
      "Credit Amount" := BankAccLedgEntry."Credit Amount";
      "Amount (LCY)" := BankAccLedgEntry."Amount (LCY)";
      "Debit Amount (LCY)" := BankAccLedgEntry."Debit Amount (LCY)";
      "Credit Amount (LCY)" := BankAccLedgEntry."Credit Amount (LCY)";
      "Document Type" := BankAccLedgEntry."Document Type";
      "Document No." := BankAccLedgEntry."Document No.";
      "Bal. Account Type" := BankAccLedgEntry."Bal. Account Type";
      "Bal. Account No." := BankAccLedgEntry."Bal. Account No.";
    END;

    PROCEDURE CopyFromFALedgEntry@51(FALedgEntry@1001 : Record "FA Ledger Entry");
    BEGIN
      "Entry No." := FALedgEntry."Entry No.";
      "Posting Date" := FALedgEntry."Posting Date";
      "FA Posting Category" := FALedgEntry."FA Posting Category";
      "FA Posting Type" := FALedgEntry."FA Posting Type" + 1;
      "Source Code" := FALedgEntry."Source Code";
      "Journal Batch Name" := FALedgEntry."Journal Batch Name";
      "Transaction No." := FALedgEntry."Transaction No.";
      Description := FALedgEntry.Description;
      "Amount (LCY)" := FALedgEntry.Amount;
      "Debit Amount (LCY)" := FALedgEntry."Debit Amount";
      "Credit Amount (LCY)" := FALedgEntry."Credit Amount";
      "VAT Amount" := FALedgEntry."VAT Amount";
      "Document Type" := FALedgEntry."Document Type";
      "Document No." := FALedgEntry."Document No.";
      "Bal. Account Type" := FALedgEntry."Bal. Account Type";
      "Bal. Account No." := FALedgEntry."Bal. Account No.";
    END;

    PROCEDURE CopyFromGLEntry@42(GLEntry@1000 : Record "G/L Entry");
    BEGIN
      "Entry No." := GLEntry."Entry No.";
      "Posting Date" := GLEntry."Posting Date";
      "Source Code" := GLEntry."Source Code";
      "Journal Batch Name" := GLEntry."Journal Batch Name";
      "Transaction No." := GLEntry."Transaction No.";
      "Source Type" := GLEntry."Source Type";
      "Source No." := GLEntry."Source No.";
      Description := GLEntry.Description;
      "Amount (LCY)" := GLEntry.Amount;
      "Debit Amount (LCY)" := GLEntry."Debit Amount";
      "Credit Amount (LCY)" := GLEntry."Credit Amount";
      "VAT Amount" := GLEntry."VAT Amount";
      "Document Type" := GLEntry."Document Type";
      "Document No." := GLEntry."Document No.";
      "Bal. Account Type" := GLEntry."Bal. Account Type";
      "Bal. Account No." := GLEntry."Bal. Account No.";
    END;

    PROCEDURE CopyFromMaintenanceEntry@49(MaintenanceLedgEntry@1001 : Record "Maintenance Ledger Entry");
    BEGIN
      "Entry No." := MaintenanceLedgEntry."Entry No.";
      "Posting Date" := MaintenanceLedgEntry."Posting Date";
      "Source Code" := MaintenanceLedgEntry."Source Code";
      "Journal Batch Name" := MaintenanceLedgEntry."Journal Batch Name";
      "Transaction No." := MaintenanceLedgEntry."Transaction No.";
      Description := MaintenanceLedgEntry.Description;
      "Amount (LCY)" := MaintenanceLedgEntry.Amount;
      "Debit Amount (LCY)" := MaintenanceLedgEntry."Debit Amount";
      "Credit Amount (LCY)" := MaintenanceLedgEntry."Credit Amount";
      "VAT Amount" := MaintenanceLedgEntry."VAT Amount";
      "Document Type" := MaintenanceLedgEntry."Document Type";
      "Document No." := MaintenanceLedgEntry."Document No.";
      "Bal. Account Type" := MaintenanceLedgEntry."Bal. Account Type";
      "Bal. Account No." := MaintenanceLedgEntry."Bal. Account No.";
    END;

    PROCEDURE CopyFromVATEntry@47(VATEntry@1001 : Record "VAT Entry");
    BEGIN
      "Entry No." := VATEntry."Entry No.";
      "Posting Date" := VATEntry."Posting Date";
      "Source Code" := VATEntry."Source Code";
      "Transaction No." := VATEntry."Transaction No.";
      Amount := VATEntry.Amount;
      "Amount (LCY)" := VATEntry.Amount;
      "Document Type" := VATEntry."Document Type";
      "Document No." := VATEntry."Document No.";
    END;

    PROCEDURE CopyFromVendLedgEntry@55(VendLedgEntry@1001 : Record "Vendor Ledger Entry");
    BEGIN
      "Entry No." := VendLedgEntry."Entry No.";
      "Posting Date" := VendLedgEntry."Posting Date";
      "Source Code" := VendLedgEntry."Source Code";
      "Journal Batch Name" := VendLedgEntry."Journal Batch Name";
      "Transaction No." := VendLedgEntry."Transaction No.";
      "Currency Code" := VendLedgEntry."Currency Code";
      Description := VendLedgEntry.Description;
      VendLedgEntry.CALCFIELDS(Amount,"Debit Amount","Credit Amount",
        "Amount (LCY)","Debit Amount (LCY)","Credit Amount (LCY)");
      Amount := VendLedgEntry.Amount;
      "Debit Amount" := VendLedgEntry."Debit Amount";
      "Credit Amount" := VendLedgEntry."Credit Amount";
      "Amount (LCY)" := VendLedgEntry."Amount (LCY)";
      "Debit Amount (LCY)" := VendLedgEntry."Debit Amount (LCY)";
      "Credit Amount (LCY)" := VendLedgEntry."Credit Amount (LCY)";
      "Document Type" := VendLedgEntry."Document Type";
      "Document No." := VendLedgEntry."Document No.";
      "Bal. Account Type" := VendLedgEntry."Bal. Account Type";
      "Bal. Account No." := VendLedgEntry."Bal. Account No.";
    END;

    LOCAL PROCEDURE InsertCustTempRevertTransNo@44(VAR TempRevertTransactionNo@1000 : TEMPORARY Record Integer;CustLedgEntryNo@1001 : Integer);
    VAR
      DtldCustLedgEntry@1002 : Record "Detailed Cust. Ledg. Entry";
    BEGIN
      DtldCustLedgEntry.GET(CustLedgEntryNo);
      if DtldCustLedgEntry."Transaction No." <> 0 then begin
        TempRevertTransactionNo.Number := DtldCustLedgEntry."Transaction No.";
        if TempRevertTransactionNo.INSERT then;
      end;
    END;

    LOCAL PROCEDURE InsertVendTempRevertTransNo@45(VAR TempRevertTransactionNo@1000 : TEMPORARY Record Integer;VendLedgEntryNo@1001 : Integer);
    VAR
      DtldVendLedgEntry@1002 : Record "Detailed Vendor Ledg. Entry";
    BEGIN
      DtldVendLedgEntry.GET(VendLedgEntryNo);
      if DtldVendLedgEntry."Transaction No." <> 0 then begin
        TempRevertTransactionNo.Number := DtldVendLedgEntry."Transaction No.";
        if TempRevertTransactionNo.INSERT then;
      end;
    END;

    BEGIN
    END.
  }
}

