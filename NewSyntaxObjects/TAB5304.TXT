OBJECT Table 5304 Outlook Synch. Field
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields="Synch. Entity Code";
    OnInsert=BEGIN
               TESTFIELD("Field No.");

               if "Table No." <> 0 then
                 TESTFIELD("Table Relation");

               CheckDuplicatedRecords;

               if ISNULLGUID("Record GUID") then
                 "Record GUID" := CREATEGUID;
             END;

    OnModify=BEGIN
               TESTFIELD("Field No.");
               CheckDuplicatedRecords;

               if "Table No." <> 0 then
                 TESTFIELD("Table Relation");
             END;

    OnDelete=VAR
               OSynchFilter@1000 : Record "Outlook Synch. Filter";
               OSynchOptionCorrel@1001 : Record "Outlook Synch. Option Correl.";
             BEGIN
               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;

               OSynchOptionCorrel.RESET;
               OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
               OSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");
               OSynchOptionCorrel.DELETEALL;
             END;

    CaptionML=ENU=Outlook Synch. Field;
    PasteIsValid=false;
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                GetMasterInformation;
                                                              END;

                                                   CaptionML=ENU=Synch. Entity Code;
                                                   NotBlank=true }
    { 2   ;   ;Element No.         ;Integer       ;TableRelation="Outlook Synch. Entity Element"."Element No." WHERE ("Synch. Entity Code"=FIELD("Synch. Entity Code"));
                                                   CaptionML=ENU=Element No. }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 4   ;   ;Master Table No.    ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Table));
                                                   CaptionML=ENU=Master Table No.;
                                                   Editable=false }
    { 5   ;   ;Outlook Object      ;Text80        ;CaptionML=ENU=Outlook Object;
                                                   Editable=false }
    { 6   ;   ;Outlook Property    ;Text80        ;OnValidate=BEGIN
                                                                CheckReadOnlyStatus;

                                                                if "Outlook Property" = xRec."Outlook Property" then
                                                                  exit;

                                                                if not "User-Defined" and ("Outlook Object" = '') then begin
                                                                  if "Element No." = 0 then
                                                                    ERROR(
                                                                      Text009,
                                                                      FIELDCAPTION("Outlook Property"),
                                                                      OSynchEntity.FIELDCAPTION("Outlook Item"));
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Outlook Property"),
                                                                    OSynchEntityElement.FIELDCAPTION("Outlook Collection"));
                                                                end;
                                                                if SetOSynchOptionCorrelFilter(OSynchOptionCorrel) then begin
                                                                  if not CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) then begin
                                                                    "Outlook Property" := xRec."Outlook Property";
                                                                    "User-Defined" := xRec."User-Defined";
                                                                    exit;
                                                                  end;

                                                                  OSynchOptionCorrel.DELETEALL;
                                                                end;

                                                                if "Outlook Property" <> '' then
                                                                  "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              PropertyName@1000 : Text[80];
                                                            BEGIN
                                                              if "Outlook Object" = '' then begin
                                                                if "Element No." = 0 then
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Outlook Property"),
                                                                    OSynchEntity.FIELDCAPTION("Outlook Item"));
                                                                ERROR(
                                                                  Text009,
                                                                  FIELDCAPTION("Outlook Property"),
                                                                  OSynchEntityElement.FIELDCAPTION("Outlook Collection"));
                                                              end;

                                                              if "User-Defined" then
                                                                ERROR(Text001);

                                                              if "Element No." = 0 then
                                                                PropertyName := OSynchSetupMgt.ShowOItemProperties("Outlook Object")
                                                              else begin
                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                PropertyName := OSynchSetupMgt.ShowOCollectionProperties(OSynchEntity."Outlook Item","Outlook Object");
                                                              end;

                                                              if PropertyName <> '' then
                                                                VALIDATE("Outlook Property",PropertyName);
                                                            END;

                                                   CaptionML=ENU=Outlook Property }
    { 7   ;   ;User-Defined        ;Boolean       ;OnValidate=BEGIN
                                                                "Outlook Property" := '';
                                                                if not "User-Defined" then
                                                                  exit;

                                                                VALIDATE("Outlook Property");
                                                              END;

                                                   CaptionML=ENU=User-Defined }
    { 8   ;   ;Search Field        ;Boolean       ;CaptionML=ENU=Search Field }
    { 9   ;   ;Condition           ;Text250       ;CaptionML=ENU=Condition;
                                                   Editable=false }
    { 10  ;   ;Table No.           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Table Caption");

                                                                if "Table No." = xRec."Table No." then
                                                                  exit;

                                                                if not OSynchSetupMgt.CheckPKFieldsQuantity("Table No.") then
                                                                  exit;

                                                                if ("Table Relation" <> '') or SetOSynchOptionCorrelFilter(OSynchOptionCorrel) then begin
                                                                  if not CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) then begin
                                                                    "Table No." := xRec."Table No.";
                                                                    exit;
                                                                  end;

                                                                  OSynchOptionCorrel.DELETEALL;

                                                                  OSynchFilter.RESET;
                                                                  OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                                                  OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
                                                                  OSynchFilter.DELETEALL;

                                                                  "Table Relation" := '';
                                                                end;

                                                                "Field No." := 0;
                                                                "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              TableNo@1000 : Integer;
                                                            BEGIN
                                                              TableNo := OSynchSetupMgt.ShowTablesList;

                                                              if TableNo <> 0 then
                                                                VALIDATE("Table No.",TableNo);
                                                            END;

                                                   CaptionML=ENU=Table No.;
                                                   BlankZero=true }
    { 11  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE ("Object Type"=CONST(Table),
                                                                                                                "Object ID"=FIELD("Table No.")));
                                                   CaptionML=ENU=Table Caption;
                                                   Editable=false }
    { 12  ;   ;Table Relation      ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Table Relation");
                                                                CheckReadOnlyStatus;
                                                              END;

                                                   CaptionML=ENU=Table Relation;
                                                   Editable=false }
    { 13  ;   ;Field No.           ;Integer       ;OnValidate=BEGIN
                                                                TESTFIELD("Field No.");
                                                                CheckReadOnlyStatus;

                                                                if "Field No." = xRec."Field No." then
                                                                  exit;

                                                                if "Table No." = 0 then
                                                                  Field.GET("Master Table No.","Field No.")
                                                                else
                                                                  Field.GET("Table No.","Field No.");

                                                                if Field.Class = Field.Class::FlowFilter then
                                                                  ERROR(Text002,Field.Class);

                                                                if not Field.Enabled then
                                                                  ERROR(Text012,Field.FieldName);

                                                                if "User-Defined" then
                                                                  VALIDATE("Outlook Property",Field."Field Caption");

                                                                if SetOSynchOptionCorrelFilter(OSynchOptionCorrel) then begin
                                                                  if not CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) then begin
                                                                    "Field No." := xRec."Field No.";
                                                                    exit;
                                                                  end;

                                                                  OSynchOptionCorrel.DELETEALL;
                                                                end;

                                                                "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              FieldNo@1000 : Integer;
                                                            BEGIN
                                                              if "Table No." = 0 then
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Master Table No.")
                                                              else
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Table No.");

                                                              if FieldNo <> 0 then
                                                                VALIDATE("Field No.",FieldNo);
                                                            END;

                                                   CaptionML=ENU=Field No.;
                                                   BlankZero=true }
    { 15  ;   ;Read-Only Status    ;Option        ;OnValidate=BEGIN
                                                                CheckReadOnlyStatus;
                                                              END;

                                                   CaptionML=ENU=Read-Only Status;
                                                   OptionCaptionML=ENU=" ,Read-Only in Microsoft Dynamics NAV,Read-Only in Outlook";
                                                   OptionString=" ","Read-Only in Microsoft Dynamics NAV","Read-Only in Outlook";
                                                   Editable=false }
    { 16  ;   ;Field Default Value ;Text250       ;OnValidate=VAR
                                                                RecRef@1000 : RecordRef;
                                                                FldRef@1001 : FieldRef;
                                                                BooleanValue@1002 : Boolean;
                                                              BEGIN
                                                                TESTFIELD("Master Table No.");
                                                                TESTFIELD("Field No.");

                                                                if "Field Default Value" = xRec."Field Default Value" then
                                                                  exit;

                                                                if "Outlook Property" <> '' then
                                                                  ERROR(Text005,FIELDCAPTION("Field Default Value"),FIELDCAPTION("Outlook Property"));

                                                                CLEAR(RecRef);
                                                                CLEAR(FldRef);

                                                                if "Table No." = 0 then begin
                                                                  Field.GET("Master Table No.","Field No.");
                                                                  RecRef.OPEN("Master Table No.",true);
                                                                end else begin
                                                                  Field.GET("Table No.","Field No.");
                                                                  RecRef.OPEN("Table No.",true);
                                                                end;

                                                                if Field.Class = Field.Class::FlowField then
                                                                  ERROR(Text010,Field.Class);

                                                                FldRef := RecRef.FIELD("Field No.");

                                                                if Field.Type = Field.Type::Option then begin
                                                                  if not OSynchTypeConversion.EvaluateOptionField(FldRef,"Field Default Value") then
                                                                    ERROR(Text004,"Field Default Value",FldRef.TYPE,FldRef.OPTIONCAPTION);
                                                                  DefaultValueExpression := FORMAT(OSynchTypeConversion.TextToOptionValue("Field Default Value",FldRef.OPTIONCAPTION))
                                                                end else begin
                                                                  if not EVALUATE(FldRef,"Field Default Value") then
                                                                    ERROR(Text003,FIELDCAPTION("Field Default Value"),FldRef.TYPE);

                                                                  if Field.Type = Field.Type::Boolean then begin
                                                                    EVALUATE(BooleanValue,"Field Default Value");
                                                                    if BooleanValue then
                                                                      DefaultValueExpression := '1'
                                                                    else
                                                                      DefaultValueExpression := '0';
                                                                  end;
                                                                end;

                                                                RecRef.CLOSE;
                                                              END;

                                                   CaptionML=ENU=Field Default Value }
    { 17  ;   ;Record GUID         ;GUID          ;CaptionML=ENU=Record GUID;
                                                   Editable=false }
    { 99  ;   ;DefaultValueExpression;Text250     ;CaptionML=ENU=DefaultValueExpression }
  }
  KEYS
  {
    {    ;"Synch. Entity Code","Element No.","Line No.";
                                                   Clustered=true }
    {    ;"Record GUID"                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Field@1001 : Record Field;
      OSynchEntity@1003 : Record "Outlook Synch. Entity";
      OSynchEntityElement@1004 : Record "Outlook Synch. Entity Element";
      OSynchFilter@1019 : Record "Outlook Synch. Filter";
      OSynchOptionCorrel@1013 : Record "Outlook Synch. Option Correl.";
      OSynchSetupMgt@1000 : Codeunit "Outlook Synch. Setup Mgt.";
      OSynchTypeConversion@1008 : Codeunit "Outlook Synch. Type Conv";
      Text001@1015 : TextConst 'ENU=You cannot choose from a list of Outlook item collections for user-defined fields.';
      Text002@1012 : TextConst 'ENU=You cannot use a %1 field for synchronization.';
      Text003@1011 : TextConst 'ENU=The value of the %1 field cannot be converted to the %2 datatype.';
      Text004@1010 : TextConst 'ENU=The value of the %1 field cannot be converted to the %2 datatype.\The possible option values are: ''%3''.';
      Text005@1009 : TextConst 'ENU=The %1 field should be blank when the %2 field is used.';
      Text006@1006 : TextConst 'ENU=This is not a valid Outlook property name.';
      Text007@1005 : TextConst 'ENU=You cannot synchronize the %1 and the %2 fields because they are both write protected.';
      Text008@1016 : TextConst 'ENU=If you change the value of this field, %1 and %2 records related to this entry will be removed.\Do you want to change this field anyway?';
      Text009@1018 : TextConst 'ENU=You cannot change the %1 field if the %2 is not specified for this entity.';
      Text010@1020 : TextConst '@@@=%1: Field.Class::FlowField;ENU=You cannot use this field for %1 fields.';
      Text011@1007 : TextConst 'ENU=The %1 table cannot be open, because the %2 or %3 fields are empty.\Fill in these fields with the appropriate values and try again.';
      Text012@1021 : TextConst 'ENU=You cannot select the %1 field because it is disabled.';
      Text013@1002 : TextConst 'ENU=You cannot use this value because an Outlook property with this name exists.';
      Text014@1022 : TextConst 'ENU=The entry you are trying to create already exists.';

    LOCAL PROCEDURE GetMasterInformation@2();
    BEGIN
      if "Element No." = 0 then begin
        OSynchEntity.GET("Synch. Entity Code");
        "Master Table No." := OSynchEntity."Table No.";
        "Outlook Object" := OSynchEntity."Outlook Item";
      end else begin
        OSynchEntityElement.GET("Synch. Entity Code","Element No.");
        "Master Table No." := OSynchEntityElement."Table No.";
        "Outlook Object" := OSynchEntityElement."Outlook Collection";
      end;
    END;

    LOCAL PROCEDURE CheckReadOnlyStatus@9();
    VAR
      OSynchProcessLine@1002 : Codeunit "Outlook Synch. Process Line";
      IsReadOnlyOutlook@1000 : Boolean;
      IsReadOnlyNavision@1001 : Boolean;
    BEGIN
      IsReadOnlyOutlook := CheckOtlookPropertyName;

      if ("Outlook Property" <> '') and ("Field No." <> 0) then begin
        if "Table No." = 0 then begin
          Field.GET("Master Table No.","Field No.");
          if OSynchProcessLine.CheckKeyField("Master Table No.","Field No.") or (Field.Class = Field.Class::FlowField) then
            IsReadOnlyNavision := true;
        end else begin
          OSynchFilter.RESET;
          OSynchFilter.SETRANGE("Record GUID","Record GUID");
          OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
          OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
          if OSynchFilter.FIND('-') then
            repeat
              Field.GET(OSynchFilter."Master Table No.",OSynchFilter."Master Table Field No.");
              if OSynchProcessLine.CheckKeyField("Master Table No.",OSynchFilter."Master Table Field No.") or
                 (Field.Class = Field.Class::FlowField)
              then
                IsReadOnlyNavision := true;
            until OSynchFilter.NEXT = 0;
        end;

        if IsReadOnlyOutlook then begin
          if IsReadOnlyNavision then
            ERROR(Text007,"Outlook Property",GetFieldCaption);
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Outlook";
        end else begin
          if IsReadOnlyNavision then
            "Read-Only Status" := "Read-Only Status"::"Read-Only in Microsoft Dynamics NAV"
          else
            "Read-Only Status" := "Read-Only Status"::" ";
        end;
      end else begin
        if "Field No." = 0 then
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Outlook"
        else
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Microsoft Dynamics NAV";
      end;
    END;

    LOCAL PROCEDURE CheckOtlookPropertyName@3() : Boolean;
    VAR
      IsReadOnly@1000 : Boolean;
    BEGIN
      if "Outlook Property" = '' then
        exit(false);

      if "User-Defined" then
        if "Element No." = 0 then begin
          if OSynchSetupMgt.ValidateOItemPropertyName("Outlook Property","Outlook Object",IsReadOnly,true) then
            ERROR(Text013);
        end else begin
          OSynchEntity.GET("Synch. Entity Code");
          if OSynchSetupMgt.ValidateOCollectPropertyName(
               "Outlook Property",
               OSynchEntity."Outlook Item",
               "Outlook Object",
               IsReadOnly,
               true)
          then
            ERROR(Text013);
        end
      else
        if "Element No." = 0 then begin
          if not OSynchSetupMgt.ValidateOItemPropertyName("Outlook Property","Outlook Object",IsReadOnly,false) then
            ERROR(Text006);
        end else begin
          OSynchEntity.GET("Synch. Entity Code");
          if not
             OSynchSetupMgt.ValidateOCollectPropertyName(
               "Outlook Property",
               OSynchEntity."Outlook Item",
               "Outlook Object",
               IsReadOnly,
               false)
          then
            ERROR(Text006);
        end;

      exit(IsReadOnly);
    END;

    LOCAL PROCEDURE CheckDuplicatedRecords@4();
    VAR
      OSynchField@1000 : Record "Outlook Synch. Field";
    BEGIN
      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchField.SETRANGE("Element No.","Element No.");
      OSynchField.SETFILTER("Line No.",'<>%1',"Line No.");
      OSynchField.SETRANGE("Outlook Property","Outlook Property");
      OSynchField.SETRANGE("Table No.","Table No.");
      OSynchField.SETRANGE("Field No.","Field No.");
      if not OSynchField.ISEMPTY then
        ERROR(Text014);
    END;

    [Internal]
    PROCEDURE ShowOOptionCorrelForm@1();
    BEGIN
      if ("Field No." = 0) or ("Outlook Property" = '') then
        ERROR(Text011,
          OSynchOptionCorrel.TABLECAPTION,
          FIELDCAPTION("Field No."),
          FIELDCAPTION("Outlook Property"));
      OSynchSetupMgt.ShowOOptionCorrelForm(Rec);
    END;

    LOCAL PROCEDURE SetOSynchOptionCorrelFilter@5(VAR outlookSynchOptionCorrel@1000 : Record "Outlook Synch. Option Correl.") : Boolean;
    BEGIN
      outlookSynchOptionCorrel.RESET;
      outlookSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      outlookSynchOptionCorrel.SETRANGE("Element No.","Element No.");
      outlookSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");
      exit(not outlookSynchOptionCorrel.ISEMPTY);
    END;

    [External]
    PROCEDURE GetFieldCaption@6() : Text;
    BEGIN
      if "Table No." <> 0 then begin
        if Field.GET("Table No.","Field No.") then
          exit(Field."Field Caption")
      end else
        if Field.GET("Master Table No.","Field No.") then
          exit(Field."Field Caption");

      exit('');
    END;

    BEGIN
    END.
  }
}

