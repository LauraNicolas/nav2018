OBJECT Codeunit 1101 CA Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Cost Journal Line";
    OnRun=BEGIN
            SourceCodeSetup.GET;
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      SourceCodeSetup@1000 : Record "Source Code Setup";
      Text000@1001 : TextConst 'ENU=Cost type or balance cost type must be defined.\Line %1, document %2, amount %3.';
      Text001@1002 : TextConst 'ENU=You cannot define both cost center and cost object.\Line %1, document %2, amount %3.';
      Text002@1003 : TextConst 'ENU=Balance cost center or balance cost object must be defined.\Line %1, document %2, amount %3.';
      Text003@1004 : TextConst 'ENU=You cannot define both balance cost center and balance cost object.\Line %1, document %2, amount %3.';
      Text004@1005 : TextConst 'ENU=Cost center or cost object must be defined. \Line %1, document %2, amount %3.';
      Text005@1006 : TextConst '@@@=starts with "Posting Date";ENU=is not within the permitted range of posting dates';

    [External]
    PROCEDURE RunCheck@3(VAR CostJnlLine@1000 : Record "Cost Journal Line");
    VAR
      CostType@1001 : Record "Cost Type";
      GenJnlCheckLine@1002 : Codeunit "Gen. Jnl.-Check Line";
    BEGIN
      with CostJnlLine do begin
        TESTFIELD("Posting Date");
        TESTFIELD("Document No.");

        SourceCodeSetup.GET;
        TESTFIELD(Amount);

        if ("Cost Type No." = '') and ("Bal. Cost Type No." = '') then
          ERROR(Text000,"Line No.","Document No.",Amount);

        if "Cost Type No." <> '' then begin
          CostType.GET("Cost Type No.");
          CostType.TESTFIELD(Blocked,false);
          CostType.TESTFIELD(Type,CostType.Type::"Cost Type");

          if "Source Code" <> SourceCodeSetup."G/L Entry to CA" then
            if ("Cost Center Code" = '') and ("Cost Object Code" = '') then
              ERROR(Text004,"Line No.","Document No.",Amount);
          if ("Cost Center Code" <> '') and ("Cost Object Code" <> '') then
            ERROR(Text001,"Line No.","Document No.",Amount);
        end;

        if "Bal. Cost Type No." <> '' then begin
          CostType.GET("Bal. Cost Type No.");
          CostType.TESTFIELD(Blocked,false);
          CostType.TESTFIELD(Type,CostType.Type::"Cost Type");

          if ("Bal. Cost Center Code" = '') and ("Bal. Cost Object Code" = '') then
            ERROR(Text002,"Line No.","Document No.",Amount);
          if ("Bal. Cost Center Code" <> '') and ("Bal. Cost Object Code" <> '') then
            ERROR(Text003,"Line No.","Document No.",Amount);
        end;

        if GenJnlCheckLine.DateNotAllowed("Posting Date") then
          FIELDERROR("Posting Date",Text005);
      end;
    END;

    BEGIN
    END.
  }
}

