OBJECT Report 5853 Copy Standard Cost Worksheet
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Standard Cost Worksheet;
    ProcessingOnly=true;
    OnPreReport=VAR
                  StdCostWkshName@1000 : Record "Standard Cost Worksheet Name";
                BEGIN
                  if ToStdCostWkshName = '' then
                    ERROR(STRSUBSTNO(Text003));
                  StdCostWkshName.GET(ToStdCostWkshName);

                  if FromStdCostWkshName.Name = '' then
                    ERROR(STRSUBSTNO(Text004));
                  FromStdCostWkshName.GET(FromStdCostWkshName.Name);

                  ToStdCostWksh.LOCKTABLE
                END;

  }
  DATASET
  {
    { 4691;    ;DataItem;Standard Cost Worksheet;
               DataItemTable="Standard Cost Worksheet";
               DataItemTableView=SORTING("Standard Cost Worksheet Name",Type,"No.");
               OnPreDataItem=BEGIN
                               FromStdCostWkshName.GET(FromStdCostWkshName.Name);
                               SETFILTER("Standard Cost Worksheet Name",FromStdCostWkshName.Name);

                               WindowUpdateDateTime := CURRENTDATETIME;
                               Window.OPEN(Text007 + Text008 + Text009);
                             END;

               OnAfterGetRecord=BEGIN
                                  InsertStdCostWksh;
                                  if CURRENTDATETIME - WindowUpdateDateTime >= 750 then begin
                                    Window.UPDATE(1,Type);
                                    Window.UPDATE(2,"No.");

                                    WindowUpdateDateTime := CURRENTDATETIME;
                                  end;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                if not NoMessage then
                                  MESSAGE(Text010);
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Group     ;
                  Name=Copy from;
                  CaptionML=ENU=Copy from }

      { 2   ;3   ;Field     ;
                  Name=Standard Cost Worksheet Name;
                  CaptionML=ENU=Standard Cost Worksheet Name;
                  ToolTipML=ENU=Specifies the name of the worksheet.;
                  ApplicationArea=#Advanced;
                  SourceExpr=FromStdCostWkshName.Name;
                  TableRelation="Standard Cost Worksheet Name" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text003@1001 : TextConst 'ENU=You must specify a worksheet name to copy to.';
      Text004@1003 : TextConst 'ENU=You must specify a worksheet name to copy from.';
      Text007@1006 : TextConst 'ENU=Copying worksheet...\\';
      Text008@1005 : TextConst 'ENU=Type               #1##########\';
      Text009@1004 : TextConst 'ENU=No.             #2##########\';
      Text010@1009 : TextConst 'ENU=The worksheet has been successfully copied.';
      ToStdCostWksh@1010 : Record "Standard Cost Worksheet";
      FromStdCostWkshName@1000 : Record "Standard Cost Worksheet Name";
      Window@1007 : Dialog;
      ToStdCostWkshName@1026 : Code[10];
      NoMessage@1040 : Boolean;
      WindowUpdateDateTime@1002 : DateTime;

    LOCAL PROCEDURE InsertStdCostWksh@2();
    BEGIN
      with ToStdCostWksh do begin
        ToStdCostWksh := "Standard Cost Worksheet";
        "Standard Cost Worksheet Name" := ToStdCostWkshName;
        if not INSERT(true) then
          MODIFY(true);
      end;
    END;

    PROCEDURE SetCopyToWksh@3(ToStdCostWkshName2@1001 : Code[10]);
    BEGIN
      ToStdCostWkshName := ToStdCostWkshName2;
    END;

    PROCEDURE Initialize@1(FromStdCostWkshName2@1000 : Code[10];ToStdCostWkshName2@1004 : Code[10];NoMessage2@1010 : Boolean);
    BEGIN
      FromStdCostWkshName.Name := FromStdCostWkshName2;
      ToStdCostWkshName := ToStdCostWkshName2;
      NoMessage := NoMessage2;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

