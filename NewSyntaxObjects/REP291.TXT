OBJECT Report 291 Delete Invd Blnkt Sales Orders
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Delete Invd Blnkt Sales Orders;
    ProcessingOnly=true;
  }
  DATASET
  {
    { 6640;    ;DataItem;Sales Header        ;
               DataItemTable="Sales Header";
               DataItemTableView=SORTING("Document Type","No.") WHERE("Document Type"=CONST("Blanket Order"));
               ReqFilterHeadingML=ENU=Blanket Sales Order;
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ATOLink@1002 : Record "Assemble-to-Order Link";
                                  ApprovalsMgmt@1000 : Codeunit "Approvals Mgmt.";
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  SalesLine.RESET;
                                  SalesLine.SETRANGE("Document Type","Document Type");
                                  SalesLine.SETRANGE("Document No.","No.");
                                  SalesLine.SETFILTER("Quantity Invoiced",'<>0');
                                  if SalesLine.FINDFIRST then begin
                                    SalesLine.SETRANGE("Quantity Invoiced");
                                    SalesLine.SETFILTER("Outstanding Quantity",'<>0');
                                    if not SalesLine.FINDFIRST then begin
                                      SalesLine.SETRANGE("Outstanding Quantity");
                                      SalesLine.SETFILTER("Qty. Shipped Not Invoiced",'<>0');
                                      if not SalesLine.FINDFIRST then begin
                                        SalesLine.LOCKTABLE;
                                        if not SalesLine.FINDFIRST then begin
                                          SalesLine.SETRANGE("Qty. Shipped Not Invoiced");
                                          SalesLine2.SETRANGE("Blanket Order No.","No.");
                                          if not SalesLine2.FINDFIRST then begin
                                            SalesLine.SETFILTER("Qty. to Assemble to Order",'<>0');
                                            if SalesLine.FINDSET then
                                              repeat
                                                ATOLink.DeleteAsmFromSalesLine(SalesLine);
                                              until SalesLine.NEXT = 0;
                                            SalesLine.SETRANGE("Qty. to Assemble to Order");

                                            SalesLine.DELETEALL;

                                            SalesCommentLine.SETRANGE("Document Type","Document Type");
                                            SalesCommentLine.SETRANGE("No.","No.");
                                            SalesCommentLine.DELETEALL;

                                            ApprovalsMgmt.DeleteApprovalEntries(RECORDID);

                                            DELETE;

                                            COMMIT;
                                          end;
                                        end;
                                      end;
                                    end;
                                  end;
                                END;

               ReqFilterFields="No.","Sell-to Customer No.","Bill-to Customer No." }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing sales orders #1##########';
      SalesLine@1001 : Record "Sales Line";
      SalesLine2@1002 : Record "Sales Line";
      SalesCommentLine@1003 : Record "Sales Comment Line";
      Window@1005 : Dialog;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

