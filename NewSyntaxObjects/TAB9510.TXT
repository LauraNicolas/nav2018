OBJECT Table 9510 Email Parameter
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Email Parameter;
  }
  FIELDS
  {
    { 1   ;   ;Document No         ;Code20        ;CaptionML=ENU=Document No }
    { 2   ;   ;Document Type       ;Option        ;TableRelation="Report Selections".Usage;
                                                   CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   OptionString=Quote,Order,Invoice,"Credit Memo","Blanket Order","Return Order" }
    { 3   ;   ;Parameter Type      ;Option        ;CaptionML=ENU=Parameter Type;
                                                   OptionCaptionML=ENU=" ,Subject";
                                                   OptionString=" ",Subject }
    { 4   ;   ;Parameter Value     ;Text250       ;CaptionML=ENU=Parameter Value }
  }
  KEYS
  {
    {    ;"Document No","Document Type","Parameter Type";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ParameterNotSupportedErr@1001 : TextConst 'ENU=Report usage is not supported.';

    PROCEDURE GetEntryWithReportUsage@2(DocumentNo@1002 : Code[20];ReportUsage@1001 : Integer;ParameterType@1000 : Option) : Boolean;
    VAR
      ReportSelections@1004 : Record "Report Selections";
      DocumentType@1003 : Option;
    BEGIN
      if not ReportSelections.ReportUsageToDocumentType(DocumentType,ReportUsage) then
        exit(false);
      exit(GET(DocumentNo,DocumentType,ParameterType));
    END;

    PROCEDURE GetParameterValue@1() : Text;
    BEGIN
      exit("Parameter Value");
    END;

    PROCEDURE SaveParameterValue@3(DocumentNo@1000 : Code[20];DocumentType@1001 : Integer;ParameterType@1002 : Option;ParameterValue@1003 : Text);
    VAR
      ParameterAlreadyExists@1004 : Boolean;
    BEGIN
      ParameterAlreadyExists := GET(DocumentNo,DocumentType,ParameterType);
      if not ParameterAlreadyExists then
        INIT;
      "Document No" := DocumentNo;
      "Document Type" := DocumentType;
      "Parameter Type" := ParameterType;
      "Parameter Value" := COPYSTR(ParameterValue,1,MAXSTRLEN("Parameter Value"));
      if ParameterAlreadyExists then
        MODIFY
      else
        INSERT;
    END;

    PROCEDURE SaveParameterValueWithReportUsage@5(DocumentNo@1000 : Code[20];ReportUsage@1001 : Integer;ParameterType@1002 : Option;ParameterValue@1003 : Text);
    VAR
      ReportSelections@1005 : Record "Report Selections";
      DocumentType@1004 : Option;
    BEGIN
      if not ReportSelections.ReportUsageToDocumentType(DocumentType,ReportUsage) then
        ERROR(ParameterNotSupportedErr);
      SaveParameterValue(DocumentNo,DocumentType,ParameterType,ParameterValue);
    END;

    BEGIN
    END.
  }
}

