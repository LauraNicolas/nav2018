OBJECT Table 8632 Config. Record For Processing
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Config. Record For Processing;
  }
  FIELDS
  {
    { 1   ;   ;Package Code        ;Code20        ;TableRelation="Config. Package";
                                                   CaptionML=ENU=Package Code }
    { 2   ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID }
    { 3   ;   ;Rule No.            ;Integer       ;CaptionML=ENU=Rule No. }
    { 4   ;   ;Record No.          ;Integer       ;CaptionML=ENU=Record No. }
  }
  KEYS
  {
    {    ;"Package Code","Table ID","Rule No.","Record No.";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE AddRecord@1(ConfigPackageRecord@1000 : Record "Config. Package Record";ProcessingRuleNo@1001 : Integer);
    BEGIN
      INIT;
      "Package Code" := ConfigPackageRecord."Package Code";
      "Table ID" := ConfigPackageRecord."Table ID";
      "Rule No." := ProcessingRuleNo;
      "Record No." := ConfigPackageRecord."No.";
      INSERT;
    END;

    [External]
    PROCEDURE FindConfigRecord@3(VAR ConfigPackageRecord@1000 : Record "Config. Package Record") : Boolean;
    BEGIN
      exit(ConfigPackageRecord.GET("Package Code","Table ID","Record No."));
    END;

    [External]
    PROCEDURE FindConfigRule@4(VAR ConfigTableProcessingRule@1000 : Record "Config. Table Processing Rule") : Boolean;
    BEGIN
      CLEAR(ConfigTableProcessingRule);
      exit(ConfigTableProcessingRule.GET("Package Code","Table ID","Rule No."));
    END;

    [External]
    PROCEDURE FindInsertedRecord@2(VAR RecRef@1000 : RecordRef) : Boolean;
    VAR
      ConfigPackageField@1003 : Record "Config. Package Field";
      FieldRef@1004 : FieldRef;
      FieldValue@1001 : Text;
    BEGIN
      RecRef.OPEN("Table ID");
      if FindPrimaryKeyFields(ConfigPackageField) then
        repeat
          FieldRef := RecRef.FIELD(ConfigPackageField."Field ID");
          FieldValue := GetFieldValue(ConfigPackageField."Field ID");
          if FieldValue <> '' then
            FieldRef.SETFILTER(FieldValue)
          else
            FieldRef.SETFILTER('%1','');
        until ConfigPackageField.NEXT = 0;
      if RecRef.HASFILTER then
        exit(RecRef.FINDFIRST);
      exit(false);
    END;

    LOCAL PROCEDURE FindPrimaryKeyFields@5(VAR ConfigPackageField@1000 : Record "Config. Package Field") : Boolean;
    BEGIN
      ConfigPackageField.SETRANGE("Package Code","Package Code");
      ConfigPackageField.SETRANGE("Table ID","Table ID");
      ConfigPackageField.SETRANGE("Primary Key",true);
      exit(ConfigPackageField.FINDSET);
    END;

    LOCAL PROCEDURE GetFieldValue@13(FieldId@1001 : Integer) : Text[250];
    VAR
      ConfigPackageData@1000 : Record "Config. Package Data";
    BEGIN
      ConfigPackageData.GET("Package Code","Table ID","Record No.",FieldId);
      exit(ConfigPackageData.Value);
    END;

    BEGIN
    END.
  }
}

