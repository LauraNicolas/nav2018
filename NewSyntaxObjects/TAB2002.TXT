OBJECT Table 2002 Azure Machine Learning Usage
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Azure Machine Learning Usage"=rimd;
    CaptionML=ENU=Azure Machine Learning Usage;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Total Processing Time;Decimal      ;CaptionML=ENU=Total Processing Time;
                                                   MinValue=0;
                                                   Description=Processing time of the all Azure ML requests is in seconds for current month.;
                                                   Editable=false }
    { 3   ;   ;Last Date Updated   ;Date          ;CaptionML=ENU=Last Date Updated;
                                                   Description=Date when the table was updated last time.;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Primary Key"                           ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PermissionManager@1000 : Codeunit "Permission Manager";
      ProcessingTimeLessThanZeroErr@1001 : TextConst 'ENU=The provided Azure ML processing time is less or equal to zero.';

    [External]
    PROCEDURE IncrementTotalProcessingTime@3(AzureMLServiceProcessingTime@1000 : Decimal);
    BEGIN
      if AzureMLServiceProcessingTime <= 0 then
        ERROR(ProcessingTimeLessThanZeroErr);

      if GetSingleInstance then begin
        "Total Processing Time" += AzureMLServiceProcessingTime;
        "Last Date Updated" := TODAY;
        MODIFY(true);
      end;
    END;

    [External]
    PROCEDURE IsAzureMLLimitReached@5(AzureMLUsageLimit@1000 : Decimal) : Boolean;
    BEGIN
      if GetSingleInstance then
        if GetTotalProcessingTime >= AzureMLUsageLimit then
          exit(true);
      exit(false);
    END;

    [External]
    PROCEDURE GetTotalProcessingTime@8() : Decimal;
    BEGIN
      // in case Azure ML is used by other features processing time should be added here
      if GetSingleInstance then
        exit("Total Processing Time");
    END;

    [External]
    PROCEDURE GetSingleInstance@9() : Boolean;
    BEGIN
      if not PermissionManager.SoftwareAsAService then
        exit(false);
      if not FINDFIRST then begin
        INIT;
        "Last Date Updated" := TODAY;
        INSERT;
      end;

      // reset total processing time when new month starts
      if DATE2DMY(TODAY,2) <> DATE2DMY("Last Date Updated",2) then begin
        "Total Processing Time" := 0;
        MODIFY;
      end;
      exit(true);
    END;

    BEGIN
    {
      // This table is used for Azure Machine Learning related features to control that amount of time used by all
      // these features in total does not exceed the limit defined by Azure ML.The table is singleton and used only in SaaS.
    }
    END.
  }
}

