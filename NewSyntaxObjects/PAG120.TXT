OBJECT Page 120 G/L Budget Entries
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=G/L Budget Entries;
    SourceTable="G/L Budget Entry";
    DelayedInsert=true;
    DataCaptionFields="G/L Account No.","Budget Name";
    PageType=List;
    OnInit=BEGIN
             BudgetDimension4CodeEnable := true;
             BudgetDimension3CodeEnable := true;
             BudgetDimension2CodeEnable := true;
             BudgetDimension1CodeEnable := true;
             GlobalDimension2CodeEnable := true;
             GlobalDimension1CodeEnable := true;
             BudgetDimension4CodeVisible := true;
             BudgetDimension3CodeVisible := true;
             BudgetDimension2CodeVisible := true;
             BudgetDimension1CodeVisible := true;
             GlobalDimension2CodeVisible := true;
             GlobalDimension1CodeVisible := true;
             LowestModifiedEntryNo := 2147483647;
           END;

    OnOpenPage=VAR
                 GLBudgetName@1000 : Record "G/L Budget Name";
               BEGIN
                 if GETFILTER("Budget Name") = '' then
                   GLBudgetName.INIT
                 else begin
                   COPYFILTER("Budget Name",GLBudgetName.Name);
                   GLBudgetName.FINDFIRST;
                 end;
                 CurrPage.EDITABLE := not GLBudgetName.Blocked and IsEditable;
                 GLSetup.GET;
                 GlobalDimension1CodeEnable := GLSetup."Global Dimension 1 Code" <> '';
                 GlobalDimension2CodeEnable := GLSetup."Global Dimension 2 Code" <> '';
                 BudgetDimension1CodeEnable := GLBudgetName."Budget Dimension 1 Code" <> '';
                 BudgetDimension2CodeEnable := GLBudgetName."Budget Dimension 2 Code" <> '';
                 BudgetDimension3CodeEnable := GLBudgetName."Budget Dimension 3 Code" <> '';
                 BudgetDimension4CodeEnable := GLBudgetName."Budget Dimension 4 Code" <> '';
                 GlobalDimension1CodeVisible := GLSetup."Global Dimension 1 Code" <> '';
                 GlobalDimension2CodeVisible := GLSetup."Global Dimension 2 Code" <> '';
                 BudgetDimension1CodeVisible := GLBudgetName."Budget Dimension 1 Code" <> '';
                 BudgetDimension2CodeVisible := GLBudgetName."Budget Dimension 2 Code" <> '';
                 BudgetDimension3CodeVisible := GLBudgetName."Budget Dimension 3 Code" <> '';
                 BudgetDimension4CodeVisible := GLBudgetName."Budget Dimension 4 Code" <> '';
               END;

    OnClosePage=VAR
                  UpdateAnalysisView@1000 : Codeunit "Update Analysis View";
                BEGIN
                  if LowestModifiedEntryNo < 2147483647 then
                    UpdateAnalysisView.SetLastBudgetEntryNo(LowestModifiedEntryNo - 1);
                END;

    OnNewRecord=BEGIN
                  if GETFILTER("Budget Name") <> '' then
                    "Budget Name" := GETRANGEMIN("Budget Name");
                  if GLBudgetName.Name <> "Budget Name" then
                    GLBudgetName.GET("Budget Name");
                  if GETFILTER("G/L Account No.") <> '' then
                    "G/L Account No." := GetFirstGLAcc(GETFILTER("G/L Account No."));
                  Date := GetFirstDate(GETFILTER(Date));
                  "User ID" := USERID;

                  if GETFILTER("Global Dimension 1 Code") <> '' then
                    "Global Dimension 1 Code" :=
                      GetFirstDimValue(GLSetup."Global Dimension 1 Code",GETFILTER("Global Dimension 1 Code"));

                  if GETFILTER("Global Dimension 2 Code") <> '' then
                    "Global Dimension 2 Code" :=
                      GetFirstDimValue(GLSetup."Global Dimension 2 Code",GETFILTER("Global Dimension 2 Code"));

                  if GETFILTER("Budget Dimension 1 Code") <> '' then
                    "Budget Dimension 1 Code" :=
                      GetFirstDimValue(GLBudgetName."Budget Dimension 1 Code",GETFILTER("Budget Dimension 1 Code"));

                  if GETFILTER("Budget Dimension 2 Code") <> '' then
                    "Budget Dimension 2 Code" :=
                      GetFirstDimValue(GLBudgetName."Budget Dimension 2 Code",GETFILTER("Budget Dimension 2 Code"));

                  if GETFILTER("Budget Dimension 3 Code") <> '' then
                    "Budget Dimension 3 Code" :=
                      GetFirstDimValue(GLBudgetName."Budget Dimension 3 Code",GETFILTER("Budget Dimension 3 Code"));

                  if GETFILTER("Budget Dimension 4 Code") <> '' then
                    "Budget Dimension 4 Code" :=
                      GetFirstDimValue(GLBudgetName."Budget Dimension 4 Code",GETFILTER("Budget Dimension 4 Code"));

                  if GETFILTER("Business Unit Code") <> '' then
                    "Business Unit Code" := GetFirstBusUnit(GETFILTER("Business Unit Code"));
                END;

    OnModifyRecord=BEGIN
                     if "Entry No." < LowestModifiedEntryNo then
                       LowestModifiedEntryNo := "Entry No.";
                     exit(true);
                   END;

    OnDeleteRecord=BEGIN
                     if "Entry No." < LowestModifiedEntryNo then
                       LowestModifiedEntryNo := "Entry No.";
                     exit(IsEditable);
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      Name=ActionContainer1900000003;
                      ActionContainerType=RelatedInformation }
      { 23      ;1   ;ActionGroup;
                      Name=Ent&ry;
                      CaptionML=ENU=Ent&ry;
                      Image=Entry }
      { 24      ;2   ;Action    ;
                      Name=Dimensions;
                      AccessByPermission=TableData Dimension=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=ENU=Dimensions;
                      ToolTipML=ENU=View or edit dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                      ApplicationArea=#Suite;
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                                 CurrPage.SAVERECORD;
                               END;
                                }
      { 5       ;2   ;Action    ;
                      Name=SetDimensionFilter;
                      Ellipsis=true;
                      CaptionML=ENU=Set Dimension Filter;
                      ToolTipML=ENU=Limit the entries according to dimension filters that you specify.;
                      ApplicationArea=#Suite;
                      Image=Filter;
                      OnAction=BEGIN
                                 SETFILTER("Dimension Set ID",DimensionSetIDFilter.LookupFilter);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Name=Budget Name;
                ToolTipML=ENU=Specifies the name of the G/L budget that the entry belongs to.;
                ApplicationArea=#Suite;
                SourceExpr="Budget Name";
                Editable=FALSE;
                ImplicitType=Code10 }

    { 4   ;2   ;Field     ;
                Name=Date;
                ToolTipML=ENU=Specifies the date of the budget entry.;
                ApplicationArea=#Suite;
                SourceExpr=Date;
                ImplicitType=Date }

    { 6   ;2   ;Field     ;
                Name=G/L Account No.;
                ToolTipML=ENU=Specifies the number of the G/L account that the budget entry applies to, or, the account on the line where the budget figure has been entered.;
                ApplicationArea=#Suite;
                SourceExpr="G/L Account No.";
                ImplicitType=Code20 }

    { 21  ;2   ;Field     ;
                Name=Description;
                ToolTipML=ENU=Specifies a description of the budget figure.;
                ApplicationArea=#Suite;
                SourceExpr=Description;
                ImplicitType=Text50 }

    { 8   ;2   ;Field     ;
                Name=Global Dimension 1 Code;
                ToolTipML=ENU=Specifies the code for the global dimension that is linked to the record or entry for analysis purposes. Two global dimensions, typically for the company's most important activities, are available on all cards, documents, reports, and lists.;
                ApplicationArea=#Suite;
                SourceExpr="Global Dimension 1 Code";
                Visible=GlobalDimension1CodeVisible;
                Enabled=GlobalDimension1CodeEnable;
                ImplicitType=Code20 }

    { 10  ;2   ;Field     ;
                Name=Global Dimension 2 Code;
                ToolTipML=ENU=Specifies the code for the global dimension that is linked to the record or entry for analysis purposes. Two global dimensions, typically for the company's most important activities, are available on all cards, documents, reports, and lists.;
                ApplicationArea=#Suite;
                SourceExpr="Global Dimension 2 Code";
                Visible=GlobalDimension2CodeVisible;
                Enabled=GlobalDimension2CodeEnable;
                ImplicitType=Code20 }

    { 25  ;2   ;Field     ;
                Name=Budget Dimension 1 Code;
                ToolTipML=ENU=Specifies the dimension value code for the Budget Dimension 1 Code the budget entry is linked to.;
                ApplicationArea=#Suite;
                SourceExpr="Budget Dimension 1 Code";
                Visible=BudgetDimension1CodeVisible;
                Enabled=BudgetDimension1CodeEnable;
                ImplicitType=Code20 }

    { 27  ;2   ;Field     ;
                Name=Budget Dimension 2 Code;
                ToolTipML=ENU=Specifies the dimension value code for the Budget Dimension 2 Code the budget entry is linked to.;
                ApplicationArea=#Suite;
                SourceExpr="Budget Dimension 2 Code";
                Visible=BudgetDimension2CodeVisible;
                Enabled=BudgetDimension2CodeEnable;
                ImplicitType=Code20 }

    { 29  ;2   ;Field     ;
                Name=Budget Dimension 3 Code;
                ToolTipML=ENU=Specifies the dimension value code for the Budget Dimension 3 Code the budget entry is linked to.;
                ApplicationArea=#Suite;
                SourceExpr="Budget Dimension 3 Code";
                Visible=BudgetDimension3CodeVisible;
                Enabled=BudgetDimension3CodeEnable;
                ImplicitType=Code20 }

    { 31  ;2   ;Field     ;
                Name=Budget Dimension 4 Code;
                ToolTipML=ENU=Specifies the dimension value code for the Budget Dimension 4 Code the budget entry is linked to.;
                ApplicationArea=#Suite;
                SourceExpr="Budget Dimension 4 Code";
                Visible=BudgetDimension4CodeVisible;
                Enabled=BudgetDimension4CodeEnable;
                ImplicitType=Code20 }

    { 19  ;2   ;Field     ;
                Name=Business Unit Code;
                ToolTipML=ENU=Specifies the code for the business unit that the budget entry is linked to.;
                ApplicationArea=#Advanced;
                SourceExpr="Business Unit Code";
                Visible=FALSE;
                ImplicitType=Code20 }

    { 12  ;2   ;Field     ;
                Name=Amount;
                ToolTipML=ENU=Specifies the amount of the budget entry.;
                ApplicationArea=#Suite;
                SourceExpr=Amount;
                ImplicitType=Decimal }

    { 14  ;2   ;Field     ;
                Name=Entry No.;
                ToolTipML=ENU=Specifies the budget line's entry number.;
                ApplicationArea=#Suite;
                SourceExpr="Entry No.";
                Editable=FALSE;
                ImplicitType=Integer }

    { 3   ;2   ;Field     ;
                Name=Dimension Set ID;
                ToolTipML=ENU=Specifies a reference to a combination of dimension values. The actual values are stored in the Dimension Set Entry table.;
                ApplicationArea=#Suite;
                SourceExpr="Dimension Set ID";
                Visible=FALSE;
                ImplicitType=Integer }

    { 1900000007;0;Container;
                Name=Container1900000007;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Name=Part1900383207;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Name=Part1905767507;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record "General Ledger Setup";
      GLBudgetName@1001 : Record "G/L Budget Name";
      DimensionSetIDFilter@1002 : Page "Dimension Set ID Filter";
      LowestModifiedEntryNo@1004 : Integer;
      GlobalDimension1CodeVisible@19028123 : Boolean INDATASET;
      GlobalDimension2CodeVisible@19054752 : Boolean INDATASET;
      BudgetDimension1CodeVisible@19036733 : Boolean INDATASET;
      BudgetDimension2CodeVisible@19019212 : Boolean INDATASET;
      BudgetDimension3CodeVisible@19014779 : Boolean INDATASET;
      BudgetDimension4CodeVisible@19001818 : Boolean INDATASET;
      GlobalDimension1CodeEnable@19057401 : Boolean INDATASET;
      GlobalDimension2CodeEnable@19039220 : Boolean INDATASET;
      BudgetDimension1CodeEnable@19069571 : Boolean INDATASET;
      BudgetDimension2CodeEnable@19071456 : Boolean INDATASET;
      BudgetDimension3CodeEnable@19074873 : Boolean INDATASET;
      BudgetDimension4CodeEnable@19060014 : Boolean INDATASET;
      IsEditable@1003 : Boolean;

    LOCAL PROCEDURE GetFirstGLAcc@3(GLAccFilter@1000 : Text[250]) : Code[20];
    VAR
      GLAcc@1001 : Record "G/L Account";
    BEGIN
      with GLAcc do begin
        SETFILTER("No.",GLAccFilter);
        if FINDFIRST then
          exit("No.");

        exit('');
      end;
    END;

    LOCAL PROCEDURE GetFirstDate@1(DateFilter@1001 : Text[250]) : Date;
    VAR
      Period@1002 : Record Date;
    BEGIN
      if DateFilter = '' then
        exit(0D);
      with Period do begin
        SETRANGE("Period Type","Period Type"::Date);
        SETFILTER("Period Start",DateFilter);
        if FINDFIRST then
          exit("Period Start");

        exit(0D);
      end;
    END;

    LOCAL PROCEDURE GetFirstDimValue@2(DimCode@1000 : Code[20];DimValFilter@1001 : Text[250]) : Code[20];
    VAR
      DimVal@1002 : Record "Dimension Value";
    BEGIN
      if (DimCode = '') or (DimValFilter = '') then
        exit('');
      with DimVal do begin
        SETRANGE("Dimension Code",DimCode);
        SETFILTER(Code,DimValFilter);
        if FINDFIRST then
          exit(Code);

        exit('');
      end;
    END;

    LOCAL PROCEDURE GetFirstBusUnit@5(BusUnitFilter@1000 : Text[250]) : Code[10];
    VAR
      BusUnit@1001 : Record "Business Unit";
    BEGIN
      with BusUnit do begin
        SETFILTER(Code,BusUnitFilter);
        if FINDFIRST then
          exit(Code);

        exit('');
      end;
    END;

    PROCEDURE SetEditable@6(NewIsEditable@1000 : Boolean);
    BEGIN
      IsEditable := NewIsEditable;
    END;

    BEGIN
    END.
  }
}

