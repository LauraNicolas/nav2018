OBJECT Page 2812 Native - Sales Quotes
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=nativeInvoicingSalesQuotes];
    SourceTable="Sales Quote Entity Buffer";
    DelayedInsert=true;
    PageType=List;
    OnAfterGetRecord=VAR
                       GraphMgtSalesQuoteBuffer@1000 : Codeunit "Graph Mgt - Sales Quote Buffer";
                     BEGIN
                       SetCalculatedFields;
                       GraphMgtSalesQuoteBuffer.RedistributeInvoiceDiscounts(Rec);
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     GraphMgtSalesQuoteBuffer@1001 : Codeunit "Graph Mgt - Sales Quote Buffer";
                   BEGIN
                     CheckCustomer;
                     ProcessBillingPostalAddress;

                     GraphMgtSalesQuoteBuffer.PropagateOnInsert(Rec,TempFieldBuffer);
                     SetDates;

                     UpdateAttachments;
                     UpdateLines;
                     UpdateDiscount;
                     UpdateCoupons;
                     SetNoteForCustomer;

                     SetCalculatedFields;

                     exit(false);
                   END;

    OnModifyRecord=VAR
                     GraphMgtSalesQuoteBuffer@1000 : Codeunit "Graph Mgt - Sales Quote Buffer";
                   BEGIN
                     if xRec.Id <> Id then
                       ERROR(CannotChangeIDErr);

                     ProcessBillingPostalAddress;

                     GraphMgtSalesQuoteBuffer.PropagateOnModify(Rec,TempFieldBuffer);

                     UpdateAttachments;
                     UpdateLines;
                     UpdateDiscount;
                     UpdateCoupons;
                     SetNoteForCustomer;

                     SetCalculatedFields;

                     exit(false);
                   END;

    OnDeleteRecord=VAR
                     GraphMgtSalesQuoteBuffer@1000 : Codeunit "Graph Mgt - Sales Quote Buffer";
                   BEGIN
                     GraphMgtSalesQuoteBuffer.PropagateOnDelete(Rec);

                     exit(false);
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 18  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=id];
                ApplicationArea=#All;
                SourceExpr=Id;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Id));
                           END;

                ImplicitType=GUID }

    { 19  ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number];
                ApplicationArea=#All;
                SourceExpr="No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("No."));
                           END;

                ImplicitType=Code20 }

    { 16  ;2   ;Field     ;
                Name=quoteDate;
                CaptionML=[@@@={Locked};
                           ENU=quoteDate];
                ApplicationArea=#All;
                SourceExpr="Document Date";
                OnValidate=BEGIN
                             DocumentDateVar := "Document Date";
                             DocumentDateSet := true;

                             RegisterFieldSet(FIELDNO("Document Date"));
                           END;

                ImplicitType=Date }

    { 10  ;2   ;Field     ;
                Name=dueDate;
                CaptionML=[@@@={Locked};
                           ENU=dueDate];
                ApplicationArea=#All;
                SourceExpr="Due Date";
                OnValidate=BEGIN
                             DueDateVar := "Due Date";
                             DueDateSet := true;

                             RegisterFieldSet(FIELDNO("Due Date"));
                           END;

                ImplicitType=Date }

    { 24  ;2   ;Field     ;
                Name=validUntilDate;
                CaptionML=[@@@={Locked};
                           ENU=validUntilDate];
                ApplicationArea=#All;
                SourceExpr="Quote Valid Until Date";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Quote Valid Until Date"));
                           END;

                ImplicitType=Date }

    { 25  ;2   ;Field     ;
                Name=status;
                CaptionML=[@@@={Locked};
                           ENU=status];
                ToolTipML=ENU=Specifies the status of the Sales Quote (Draft,Sent,Accepted,Expired).;
                ApplicationArea=#All;
                SourceExpr=Status;
                Editable=FALSE;
                ImplicitType=Option }

    { 9   ;2   ;Field     ;
                Name=acceptedDate;
                CaptionML=[@@@={Locked};
                           ENU=acceptedDate];
                ApplicationArea=#All;
                SourceExpr="Quote Accepted Date";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Quote Accepted Date"));
                           END;

                ImplicitType=Date }

    { 7   ;2   ;Field     ;
                Name=customerId;
                CaptionML=[@@@={Locked};
                           ENU=customerId];
                ApplicationArea=#All;
                SourceExpr="Customer Id";
                OnValidate=BEGIN
                             Customer.SETRANGE(Id,"Customer Id");
                             if not Customer.FINDFIRST then
                               ERROR(CannotFindCustomerErr);

                             "Sell-to Customer No." := Customer."No.";
                             RegisterFieldSet(FIELDNO("Customer Id"));
                             RegisterFieldSet(FIELDNO("Sell-to Customer No."));
                           END;

                ImplicitType=GUID }

    { 6   ;2   ;Field     ;
                Name=graphContactId;
                CaptionML=[@@@={Locked};
                           ENU=contactId];
                ApplicationArea=#All;
                SourceExpr="Contact Graph Id";
                OnValidate=VAR
                             Contact@1000 : Record Contact;
                             Customer@1001 : Record Customer;
                             GraphIntContact@1003 : Codeunit "Graph Int. - Contact";
                           BEGIN
                             RegisterFieldSet(FIELDNO("Contact Graph Id"));

                             if "Contact Graph Id" = '' then
                               ERROR(ContactIdHasToHaveValueErr);

                             if not GraphIntContact.FindOrCreateCustomerFromGraphContactSafe("Contact Graph Id",Customer,Contact) then
                               exit;

                             UpdateCustomerFromGraphContactId(Customer);

                             if Contact."Company No." = Customer."No." then begin
                               VALIDATE("Sell-to Contact No.",Contact."No.");
                               VALIDATE("Sell-to Contact",Contact.Name);

                               RegisterFieldSet(FIELDNO("Sell-to Contact No."));
                               RegisterFieldSet(FIELDNO("Sell-to Contact"));
                             end;
                           END;

                ImplicitType=Text250 }

    { 5   ;2   ;Field     ;
                Name=customerNumber;
                CaptionML=[@@@={Locked};
                           ENU=customerNumber];
                ApplicationArea=#All;
                SourceExpr="Sell-to Customer No.";
                OnValidate=BEGIN
                             if Customer."No." <> '' then
                               exit;

                             if not Customer.GET("Sell-to Customer No.") then
                               ERROR(CannotFindCustomerErr);

                             "Customer Id" := Customer.Id;
                             RegisterFieldSet(FIELDNO("Customer Id"));
                             RegisterFieldSet(FIELDNO("Sell-to Customer No."));
                           END;

                ImplicitType=Code20 }

    { 4   ;2   ;Field     ;
                Name=customerName;
                CaptionML=[@@@={Locked};
                           ENU=customerName];
                ApplicationArea=#All;
                SourceExpr="Sell-to Customer Name";
                Editable=FALSE;
                ImplicitType=Text50 }

    { 26  ;2   ;Field     ;
                Name=customerEmail;
                CaptionML=[@@@={Locked};
                           ENU=customerEmail];
                ToolTipML=ENU=Specifies the email address of the customer;
                ApplicationArea=#All;
                SourceExpr=CustomerEmail;
                Editable=FALSE;
                ImplicitType=Text }

    { 23  ;2   ;Field     ;
                Name=taxLiable;
                CaptionML=ENU=taxLiable;
                ToolTipML=ENU=Specifies if the sales invoice contains sales tax.;
                ApplicationArea=#All;
                SourceExpr="Tax Liable";
                Importance=Additional;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Tax Liable"));
                           END;

                ImplicitType=Boolean }

    { 20  ;2   ;Field     ;
                Name=taxAreaId;
                CaptionML=ENU=taxAreaId;
                ApplicationArea=#All;
                SourceExpr="Tax Area ID";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Tax Area ID"));

                             if IsUsingVAT then
                               RegisterFieldSet(FIELDNO("VAT Bus. Posting Group"))
                             else
                               RegisterFieldSet(FIELDNO("Tax Area Code"));
                           END;

                ImplicitType=GUID }

    { 35  ;2   ;Field     ;
                Name=taxAreaDisplayName;
                CaptionML=ENU=taxAreaDisplayName;
                ToolTipML=ENU=Specifies the tax area display name.;
                ApplicationArea=#All;
                SourceExpr=TaxAreaDisplayName;
                Editable=FALSE;
                ImplicitType=Text }

    { 13  ;2   ;Field     ;
                Name=taxRegistrationNumber;
                CaptionML=ENU=taxRegistrationNumber;
                ApplicationArea=#All;
                SourceExpr="VAT Registration No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("VAT Registration No."));
                           END;

                ImplicitType=Text20 }

    { 3   ;2   ;Field     ;
                Name=billingPostalAddress;
                CaptionML=[@@@={Locked};
                           ENU=billingPostalAddress];
                ToolTipML=ENU=Specifies the billing address of the Sales Invoice.;
                ApplicationArea=#All;
                SourceExpr=BillingPostalAddressJSONText;
                OnValidate=BEGIN
                             BillingPostalAddressSet := true;
                           END;

                ODataEDMType=POSTALADDRESS;
                ImplicitType=Text }

    { 29  ;2   ;Field     ;
                Name=pricesIncludeTax;
                CaptionML=[@@@={Locked};
                           ENU=pricesIncludeTax];
                ApplicationArea=#All;
                SourceExpr="Prices Including VAT";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Prices Including VAT"));
                           END;

                ImplicitType=Boolean }

    { 12  ;2   ;Field     ;
                Name=shipmentMethod;
                CaptionML=[@@@={Locked};
                           ENU=shipmentMethod];
                ApplicationArea=#All;
                SourceExpr="Shipment Method Code";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Shipment Method Code"));
                           END;

                ImplicitType=Code10 }

    { 11  ;2   ;Field     ;
                Name=salesperson;
                CaptionML=[@@@={Locked};
                           ENU=salesperson];
                ApplicationArea=#All;
                SourceExpr="Salesperson Code";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Salesperson Code"));
                           END;

                ImplicitType=Code20 }

    { 8   ;2   ;Field     ;
                Name=lines;
                CaptionML=[@@@={Locked};
                           ENU=lines];
                ToolTipML=ENU=Specifies Sales Invoice Lines;
                ApplicationArea=#All;
                SourceExpr=SalesQuoteLinesJSON;
                OnValidate=BEGIN
                             SalesLinesSet := PreviousSalesQuoteLinesJSON <> SalesQuoteLinesJSON;
                           END;

                ODataEDMType=Collection(NATIVE-SALESQUOTE-LINE);
                ImplicitType=Text }

    { 30  ;2   ;Field     ;
                Name=subtotalAmount;
                CaptionML=ENU=subtotalAmount;
                ApplicationArea=#All;
                SourceExpr="Subtotal Amount";
                Editable=FALSe;
                ImplicitType=Decimal }

    { 14  ;2   ;Field     ;
                Name=discountAmount;
                CaptionML=[@@@={Locked};
                           ENU=discountAmount];
                ApplicationArea=#All;
                SourceExpr="Invoice Discount Amount";
                Editable=False;
                ImplicitType=Decimal }

    { 32  ;2   ;Field     ;
                Name=discountAppliedBeforeTax;
                CaptionML=[@@@={Locked};
                           ENU=discountAppliedBeforeTax];
                ApplicationArea=#All;
                SourceExpr="Discount Applied Before Tax";
                ImplicitType=Boolean }

    { 31  ;2   ;Field     ;
                Name=coupons;
                CaptionML=[@@@={Locked};
                           ENU=coupons];
                ToolTipML=ENU=Specifies Sales Invoice Coupons.;
                ApplicationArea=#All;
                SourceExpr=CouponsJSON;
                OnValidate=BEGIN
                             CouponsSet := PreviousCouponsJSON <> CouponsJSON;
                           END;

                ODataEDMType=Collection(NATIVE-SALESDOCUMENT-COUPON);
                ImplicitType=Text }

    { 22  ;2   ;Field     ;
                Name=totalAmountExcludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalAmountExcludingTax];
                ApplicationArea=#All;
                SourceExpr=Amount;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 21  ;2   ;Field     ;
                Name=totalTaxAmount;
                CaptionML=[@@@={Locked};
                           ENU=totalTaxAmount];
                ToolTipML=ENU=Specifies the total tax amount for the sales invoice.;
                ApplicationArea=#All;
                SourceExpr="Total Tax Amount";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Total Tax Amount"));
                           END;

                ImplicitType=Decimal }

    { 17  ;2   ;Field     ;
                Name=totalAmountIncludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalAmountIncludingTax];
                ApplicationArea=#All;
                SourceExpr="Amount Including VAT";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Amount Including VAT"));
                           END;

                ImplicitType=Decimal }

    { 27  ;2   ;Field     ;
                Name=noteForCustomer;
                CaptionML=ENU=noteForCustomer;
                ToolTipML=ENU=Specifies the note for the customer.;
                ApplicationArea=#All;
                SourceExpr=WorkDescription;
                OnValidate=BEGIN
                             NoteForCustomerSet := true;
                           END;

                ImplicitType=Text }

    { 15  ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=lastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time";
                Editable=FALSE;
                ImplicitType=DateTime }

    { 33  ;2   ;Field     ;
                Name=attachments;
                CaptionML=[@@@={Locked};
                           ENU=attachments];
                ToolTipML=ENU=Specifies Attachments;
                ApplicationArea=#All;
                SourceExpr=AttachmentsJSON;
                OnValidate=BEGIN
                             IsAttachmentsSet := AttachmentsJSON <> PreviousAttachmentsJSON;
                           END;

                ODataEDMType=Collection(NATIVE-ATTACHMENT);
                ImplicitType=Text }

    { 34  ;2   ;Field     ;
                Name=invoiceDiscountCalculation;
                CaptionML=ENU=invoiceDiscountCalculation;
                OptionCaptionML=[@@@={Locked};
                                 ENU=,%,Amount];
                ApplicationArea=#All;
                SourceExpr="Invoice Discount Calculation";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Invoice Discount Calculation"));
                             DiscountAmountSet := true;
                           END;

                ImplicitType=Option }

    { 28  ;2   ;Field     ;
                Name=invoiceDiscountValue;
                CaptionML=ENU=invoiceDiscountValue;
                ApplicationArea=#All;
                SourceExpr="Invoice Discount Value";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Invoice Discount Value"));
                             DiscountAmountSet := true;
                           END;

                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      TempFieldBuffer@1004 : TEMPORARY Record "Field Buffer";
      Customer@1006 : Record Customer;
      DummySalesLine@1002 : Record "Sales Line";
      BillingPostalAddressJSONText@1000 : Text;
      CustomerEmail@1010 : Text;
      SalesQuoteLinesJSON@1003 : Text;
      PreviousSalesQuoteLinesJSON@1001 : Text;
      CouponsJSON@1022 : Text;
      PreviousCouponsJSON@1021 : Text;
      AttachmentsJSON@1024 : Text;
      TaxAreaDisplayName@1030 : Text;
      PreviousAttachmentsJSON@1025 : Text;
      BillingPostalAddressSet@1005 : Boolean;
      CannotFindCustomerErr@1008 : TextConst '@@@={Locked};ENU=The customer cannot be found.';
      ContactIdHasToHaveValueErr@1007 : TextConst '@@@={Locked};ENU=Contact Id must have a value set.';
      CannotChangeIDErr@1009 : TextConst '@@@={Locked};ENU=The id cannot be changed.';
      SalesLinesSet@1015 : Boolean;
      CouponsSet@1023 : Boolean;
      DiscountAmountSet@1014 : Boolean;
      IsAttachmentsSet@1026 : Boolean;
      InvoiceDiscountAmount@1013 : Decimal;
      WorkDescription@1012 : Text;
      CustomerNotProvidedErr@1016 : TextConst '@@@={Locked};ENU=A customerNumber or a customerID must be provided.';
      NoteForCustomerSet@1017 : Boolean;
      DocumentDateSet@1020 : Boolean;
      DocumentDateVar@1019 : Date;
      DueDateSet@1018 : Boolean;
      DueDateVar@1011 : Date;
      CannotFindQuoteErr@1041 : TextConst '@@@={Locked};ENU=The quote cannot be found.';
      NoLinesErr@1042 : TextConst '@@@={Locked};ENU=The quote must have at least one line.';
      CustomerEmailErr@1043 : TextConst '@@@={Locked};ENU=The customer email is not specified.';
      MailNotConfiguredErr@1044 : TextConst '@@@={Locked};ENU=An email account must be configured to send emails.';
      AlreadyAcceptedErr@1045 : TextConst '@@@={Locked};ENU=The quote has already been accepted.';
      NotAcceptedYetErr@1046 : TextConst '@@@={Locked};ENU=The quote is not accepted yet.';
      InvoiceDiscountPctMustBePositiveErr@1028 : TextConst '@@@={Locked};ENU=Invoice discount percentage must be positive.';
      InvoiceDiscountPctMustBeBelowHundredErr@1027 : TextConst '@@@={Locked};ENU=Invoice discount percentage must be below 100.';
      InvoiceDiscountAmtMustBePositiveErr@1029 : TextConst '@@@={Locked};ENU=Invoice discount must be positive.';

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      TempSalesInvoiceLineAggregate@1002 : TEMPORARY Record "Sales Invoice Line Aggregate";
      DummyNativeAPITaxSetup@1005 : Record "Native - API Tax Setup";
      GraphMgtSalesQuote@1000 : Codeunit "Graph Mgt - Sales Quote";
      NativeEDMTypes@1001 : Codeunit "Native - EDM Types";
      NativeCoupons@1003 : Codeunit "Native - Coupons";
      NativeAttachments@1004 : Codeunit "Native - Attachments";
    BEGIN
      BillingPostalAddressJSONText := GraphMgtSalesQuote.BillToCustomerAddressToJSON(Rec);
      if Customer.GET("Sell-to Customer No.") then
        CustomerEmail := Customer."E-Mail";

      LoadLines(TempSalesInvoiceLineAggregate,Rec);
      SalesQuoteLinesJSON := NativeEDMTypes.WriteSalesLinesJSON(TempSalesInvoiceLineAggregate);
      PreviousSalesQuoteLinesJSON := SalesQuoteLinesJSON;

      CouponsJSON := NativeCoupons.WriteCouponsJSON(DummySalesLine."Document Type"::Quote,"No.",false);
      PreviousCouponsJSON := CouponsJSON;

      AttachmentsJSON := NativeAttachments.GenerateAttachmentsJSON(Id);
      PreviousAttachmentsJSON := AttachmentsJSON;
      TaxAreaDisplayName := DummyNativeAPITaxSetup.GetTaxAreaDisplayName("Tax Area ID");

      GetNoteForCustomer;
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(BillingPostalAddressJSONText);
      CLEAR(SalesQuoteLinesJSON);
      CLEAR(PreviousSalesQuoteLinesJSON);
      CLEAR(SalesLinesSet);
      CLEAR(AttachmentsJSON);
      CLEAR(PreviousAttachmentsJSON);
      CLEAR(IsAttachmentsSet);
      CLEAR(WorkDescription);
      CLEAR(TaxAreaDisplayName);
      TempFieldBuffer.DELETEALL;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    VAR
      LastOrderNo@1001 : Integer;
    BEGIN
      LastOrderNo := 1;
      if TempFieldBuffer.FINDLAST then
        LastOrderNo := TempFieldBuffer.Order + 1;

      CLEAR(TempFieldBuffer);
      TempFieldBuffer.Order := LastOrderNo;
      TempFieldBuffer."Table ID" := DATABASE::"Sales Quote Entity Buffer";
      TempFieldBuffer."Field ID" := FieldNo;
      TempFieldBuffer.INSERT;
    END;

    LOCAL PROCEDURE ProcessBillingPostalAddress@5();
    VAR
      GraphMgtSalesQuote@1000 : Codeunit "Graph Mgt - Sales Quote";
    BEGIN
      if not BillingPostalAddressSet then
        exit;

      GraphMgtSalesQuote.ProcessComplexTypes(Rec,BillingPostalAddressJSONText);

      if xRec."Sell-to Address" <> "Sell-to Address" then
        RegisterFieldSet(FIELDNO("Sell-to Address"));

      if xRec."Sell-to Address 2" <> "Sell-to Address 2" then
        RegisterFieldSet(FIELDNO("Sell-to Address 2"));

      if xRec."Sell-to City" <> "Sell-to City" then
        RegisterFieldSet(FIELDNO("Sell-to City"));

      if xRec."Sell-to Country/Region Code" <> "Sell-to Country/Region Code" then
        RegisterFieldSet(FIELDNO("Sell-to Country/Region Code"));

      if xRec."Sell-to Post Code" <> "Sell-to Post Code" then
        RegisterFieldSet(FIELDNO("Sell-to Post Code"));

      if xRec."Sell-to County" <> "Sell-to County" then
        RegisterFieldSet(FIELDNO("Sell-to County"));
    END;

    LOCAL PROCEDURE UpdateCustomerFromGraphContactId@2(VAR Customer@1001 : Record Customer);
    VAR
      UpdateCustomer@1000 : Boolean;
    BEGIN
      UpdateCustomer := "Sell-to Customer No." = '';
      if not UpdateCustomer then begin
        TempFieldBuffer.RESET;
        TempFieldBuffer.SETRANGE("Field ID",FIELDNO("Customer Id"));
        UpdateCustomer := not TempFieldBuffer.FINDFIRST;
        TempFieldBuffer.RESET;
      end;

      if UpdateCustomer then begin
        VALIDATE("Customer Id",Customer.Id);
        VALIDATE("Sell-to Customer No.",Customer."No.");
        RegisterFieldSet(FIELDNO("Customer Id"));
        RegisterFieldSet(FIELDNO("Sell-to Customer No."));
      end;
    END;

    LOCAL PROCEDURE LoadLines@1(VAR TempSalesInvoiceLineAggregate@1000 : TEMPORARY Record "Sales Invoice Line Aggregate";VAR SalesQuoteEntityBuffer@1001 : Record "Sales Quote Entity Buffer");
    VAR
      GraphMgtSalesQuoteBuffer@1002 : Codeunit "Graph Mgt - Sales Quote Buffer";
    BEGIN
      TempSalesInvoiceLineAggregate.SETRANGE("Document Id",SalesQuoteEntityBuffer.Id);
      GraphMgtSalesQuoteBuffer.LoadLines(TempSalesInvoiceLineAggregate,TempSalesInvoiceLineAggregate.GETFILTER("Document Id"));
    END;

    LOCAL PROCEDURE UpdateLines@4();
    VAR
      TempSalesInvoiceLineAggregate@1002 : TEMPORARY Record "Sales Invoice Line Aggregate";
      GraphMgtSalesQuoteBuffer@1000 : Codeunit "Graph Mgt - Sales Quote Buffer";
      NativeEDMTypes@1001 : Codeunit "Native - EDM Types";
    BEGIN
      if not SalesLinesSet then
        exit;

      NativeEDMTypes.ParseSalesLinesJSON(
        DummySalesLine."Document Type"::Quote,SalesQuoteLinesJSON,TempSalesInvoiceLineAggregate,Id);
      TempSalesInvoiceLineAggregate.SETRANGE("Document Id",Id);
      GraphMgtSalesQuoteBuffer.PropagateMultipleLinesUpdate(TempSalesInvoiceLineAggregate);
      FIND;
    END;

    LOCAL PROCEDURE UpdateDiscount@13();
    VAR
      SalesHeader@1002 : Record "Sales Header";
      SalesLine@1009 : Record "Sales Line";
      TotalSalesLine@1008 : Record "Sales Line";
      SalesCalcDiscountByType@1006 : Codeunit "Sales - Calc Discount By Type";
      O365Discounts@1005 : Codeunit "O365 Discounts";
      DocumentTotals@1004 : Codeunit "Document Totals";
      GraphMgtSalesQuoteBuffer@1000 : Codeunit "Graph Mgt - Sales Quote Buffer";
      VatAmount@1003 : Decimal;
      InvoiceDiscountPct@1001 : Decimal;
    BEGIN
      if SalesLinesSet and (not DiscountAmountSet) then begin
        GraphMgtSalesQuoteBuffer.RedistributeInvoiceDiscounts(Rec);
        exit;
      end;

      if not DiscountAmountSet then
        exit;

      case "Invoice Discount Calculation" of
        "Invoice Discount Calculation"::"%":
          begin
            if "Invoice Discount Value" < 0 then
              ERROR(InvoiceDiscountPctMustBePositiveErr);
            if "Invoice Discount Value" > 100 then
              ERROR(InvoiceDiscountPctMustBeBelowHundredErr);
            InvoiceDiscountPct := "Invoice Discount Value";
          end;
        "Invoice Discount Calculation"::Amount:
          begin
            InvoiceDiscountAmount := "Invoice Discount Value";
            if "Invoice Discount Value" < 0 then
              ERROR(InvoiceDiscountAmtMustBePositiveErr);
          end;
      end;

      SalesHeader.GET("Document Type"::Quote,"No.");
      if InvoiceDiscountPct <> 0 then begin
        O365Discounts.ApplyInvoiceDiscountPercentage(SalesHeader,InvoiceDiscountPct);
        SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
        SalesLine.SETRANGE("Document No.",SalesHeader."No.");
        if SalesLine.FINDFIRST then begin
          DocumentTotals.CalculateSalesTotals(TotalSalesLine,VatAmount,SalesLine);
          "Invoice Discount Amount" := TotalSalesLine."Inv. Discount Amount";
        end;
      end else
        SalesCalcDiscountByType.ApplyInvDiscBasedOnAmt(InvoiceDiscountAmount,SalesHeader);
    END;

    LOCAL PROCEDURE UpdateCoupons@14();
    VAR
      NativeEDMTypes@1000 : Codeunit "Native - EDM Types";
    BEGIN
      if not CouponsSet then
        exit;

      NativeEDMTypes.ParseCouponsJSON("Contact Graph Id",DummySalesLine."Document Type"::Quote,"No.",CouponsJSON);
    END;

    LOCAL PROCEDURE UpdateAttachments@7();
    VAR
      NativeAttachments@1001 : Codeunit "Native - Attachments";
    BEGIN
      if not IsAttachmentsSet then
        exit;

      // Here we now know that user has specified different attachments than before.
      // We should patch the attachments. This means:
      // Delete the ones not in use anymore.
      // Add new ones.
      // Keep old ones.

      NativeAttachments.UpdateAttachments(Id,AttachmentsJSON,PreviousAttachmentsJSON);
    END;

    LOCAL PROCEDURE CheckCustomer@3();
    VAR
      BlankGUID@1000 : GUID;
    BEGIN
      if ("Sell-to Customer No." = '') and
         ("Customer Id" = BlankGUID)
      then
        ERROR(CustomerNotProvidedErr);
    END;

    LOCAL PROCEDURE GetNoteForCustomer@9();
    VAR
      SalesHeader@1001 : Record "Sales Header";
      WorkDescriptionInStream@1002 : InStream;
    BEGIN
      CLEAR(WorkDescription);

      SalesHeader.SETAUTOCALCFIELDS("Work Description");
      SalesHeader.GET(SalesHeader."Document Type"::Quote,"No.");
      SalesHeader."Work Description".CREATEINSTREAM(WorkDescriptionInStream);

      WorkDescriptionInStream.READ(WorkDescription);
    END;

    LOCAL PROCEDURE SetNoteForCustomer@22();
    VAR
      SalesHeader@1000 : Record "Sales Header";
      WorkDescriptionOutStream@1001 : OutStream;
    BEGIN
      if not NoteForCustomerSet then
        exit;

      SalesHeader.GET(SalesHeader."Document Type"::Quote,"No.");
      SalesHeader."Work Description".CREATEOUTSTREAM(WorkDescriptionOutStream);
      WorkDescriptionOutStream.WRITE(WorkDescription);
      SalesHeader.MODIFY(true);
      FIND;
    END;

    LOCAL PROCEDURE SetDates@8();
    VAR
      GraphMgtSalesQuoteBuffer@1000 : Codeunit "Graph Mgt - Sales Quote Buffer";
    BEGIN
      if not (DueDateSet or DocumentDateSet) then
        exit;

      TempFieldBuffer.RESET;
      TempFieldBuffer.DELETEALL;

      if DocumentDateSet then begin
        "Document Date" := DocumentDateVar;
        RegisterFieldSet(FIELDNO("Document Date"));
      end;

      if DueDateSet then begin
        "Due Date" := DueDateVar;
        RegisterFieldSet(FIELDNO("Due Date"));
      end;

      GraphMgtSalesQuoteBuffer.PropagateOnModify(Rec,TempFieldBuffer);
      FIND;
    END;

    LOCAL PROCEDURE GetQuote@36(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      SalesHeader.SETRANGE(Id,Id);
      if not SalesHeader.FINDFIRST then
        ERROR(CannotFindQuoteErr);
    END;

    LOCAL PROCEDURE CheckSmtpMailSetup@30();
    VAR
      O365SetupEmail@1001 : Codeunit "O365 Setup Email";
    BEGIN
      if not O365SetupEmail.SMTPEmailIsSetUp then
        ERROR(MailNotConfiguredErr);
    END;

    LOCAL PROCEDURE CheckCustomerEmail@21();
    BEGIN
      if not Customer.GET("Sell-to Customer No.") then
        ERROR(CannotFindCustomerErr);
      if Customer."E-Mail" = '' then
        ERROR(CustomerEmailErr);
    END;

    LOCAL PROCEDURE SendQuote@34(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      if not SalesHeader.SalesLinesExist then
        ERROR(NoLinesErr);
      CheckSmtpMailSetup;
      CheckCustomerEmail;
      SalesHeader.EmailRecords(false);
    END;

    LOCAL PROCEDURE AcceptQuote@26(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      if Status = Status::Accepted then
        ERROR(AlreadyAcceptedErr);
      if not SalesHeader.SalesLinesExist then
        ERROR(NoLinesErr);
      SalesHeader.VALIDATE("Quote Accepted",true);
      SalesHeader.MODIFY;
    END;

    LOCAL PROCEDURE UndoQuoteAccept@32(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      if Status <> Status::Accepted then
        ERROR(NotAcceptedYetErr);
      SalesHeader.VALIDATE("Quote Accepted",false);
      SalesHeader.MODIFY;
    END;

    LOCAL PROCEDURE SetActionResponse@47(VAR ActionContext@1004 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext";VAR SalesHeader@1000 : Record "Sales Header");
    VAR
      ODataActionManagement@1003 : Codeunit "OData Action Management";
    BEGIN
      ODataActionManagement.AddKey(FIELDNO(Id),SalesHeader.Id);
      if SalesHeader."Document Type" = SalesHeader."Document Type"::Invoice then
        ODataActionManagement.SetDeleteResponseLocation(ActionContext,PAGE::"Native - Sales Inv. Entity")
      else
        ODataActionManagement.SetDeleteResponseLocation(ActionContext,PAGE::"Native - Sales Quotes");
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE MakeInvoice@15(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1001 : Record "Sales Header";
      SalesQuoteToInvoice@1005 : Codeunit "Sales-Quote to Invoice";
    BEGIN
      GetQuote(SalesHeader);
      SalesQuoteToInvoice.RUN(SalesHeader);
      SalesQuoteToInvoice.GetSalesInvoiceHeader(SalesHeader);
      SetActionResponse(ActionContext,SalesHeader);
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE Send@17(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1005 : Record "Sales Header";
    BEGIN
      GetQuote(SalesHeader);
      SendQuote(SalesHeader);
      SetActionResponse(ActionContext,SalesHeader);
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE Accept@18(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1005 : Record "Sales Header";
    BEGIN
      GetQuote(SalesHeader);
      AcceptQuote(SalesHeader);
      SetActionResponse(ActionContext,SalesHeader);
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE UndoAccept@20(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1005 : Record "Sales Header";
    BEGIN
      GetQuote(SalesHeader);
      UndoQuoteAccept(SalesHeader);
      SetActionResponse(ActionContext,SalesHeader);
    END;

    BEGIN
    END.
  }
}

