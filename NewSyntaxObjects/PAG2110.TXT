OBJECT Page 2110 O365 Sales Invoice
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Draft Invoice;
    DeleteAllowed=false;
    SourceTable="Sales Header";
    SourceTableView=WHERE("Document Type"=CONST(Invoice));
    PageType=Document;
    RefreshOnActivate=true;
    OnOpenPage=VAR
                 O365SalesInitialSetup@6115 : Record "O365 Sales Initial Setup";
                 IntegrationRecord@1001 : Record "Integration Record";
                 SalesInvoiceHeader@1002 : Record "Sales Invoice Header";
                 BlankRecordId@1003 : RecordID;
                 IdFilter@1000 : Text;
               BEGIN
                 IsUsingVAT := O365SalesInitialSetup.IsUsingVAT;

                 IdFilter := GETFILTER(Id);

                 if IdFilter <> '' then begin
                   if not IntegrationRecord.GET(IdFilter) then
                     ERROR(CannotFindRecordErr);

                   if FORMAT(IntegrationRecord."Record ID") = FORMAT(BlankRecordId) then
                     ERROR(InvoiceWasDeletedErr);

                   case IntegrationRecord."Table ID" of
                     DATABASE::"Sales Header":
                       ;
                     DATABASE::"Sales Invoice Header":
                       begin
                         if not SalesInvoiceHeader.GET(IntegrationRecord."Record ID") then
                           ERROR(CannotFindRecordErr);
                         PAGE.RUN(PAGE::"O365 Posted Sales Invoice",SalesInvoiceHeader);
                         ERROR(''); // Close the page
                       end
                     else
                       ERROR(CannotFindRecordErr);
                   end;
                 end;
               END;

    OnNewRecord=BEGIN
                  CustomerName := '';
                  CustomerEmail := '';
                  WorkDescription := '';
                  SetDefaultPaymentServices;
                END;

    OnInsertRecord=BEGIN
                     "Document Type" := "Document Type"::Invoice;
                   END;

    OnDeleteRecord=VAR
                     CustInvoiceDisc@1000 : Record "Cust. Invoice Disc.";
                   BEGIN
                     ForceExit := true;

                     if CustInvoiceDisc.GET("Invoice Disc. Code","Currency Code",0) then
                       CustInvoiceDisc.DELETE;
                   END;

    OnQueryClosePage=BEGIN
                       exit(O365SalesInvoiceMgmt.OnQueryCloseForSalesHeader(Rec,ForceExit,CustomerName));
                     END;

    OnAfterGetCurrRecord=BEGIN
                           O365SalesInvoiceMgmt.UpdateCustomerFields(Rec,CustomerName,CustomerEmail);
                           IsCompanyContact := O365SalesInvoiceMgmt.IsCustomerCompanyContact("Sell-to Customer No.");
                           WorkDescription := GetWorkDescription;
                           CurrPageEditable := CurrPage.EDITABLE;
                           O365SalesInvoiceMgmt.OnAfterGetSalesHeaderRecord(Rec,CurrencyFormat,TaxAreaDescription);
                           UpdateFieldsOnAfterGetCurrRec;
                           if "Sell-to Customer No." <> xRec."Sell-to Customer No." then
                             O365SalesInvoiceMgmt.CustomerChanged;
                           O365SalesInvoiceMgmt.NotifyOrWarnAboutCoupouns(Rec);
                           O365SalesInvoiceMgmt.GetCouponCodesAndCouponsExists(Rec,CouponCodes,CouponsExistsForCustomer);
                         END;

    ActionList=ACTIONS
    {
      { 22      ;    ;ActionContainer;
                      Name=Invoice;
                      CaptionML=ENU=Invoice;
                      ActionContainerType=ActionItems }
      { 23      ;1   ;Action    ;
                      Name=Post;
                      ShortCutKey=Ctrl+Right;
                      CaptionML=ENU=Send invoice;
                      ToolTipML=ENU=Finalize and send the invoice.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      Enabled=CustomerName <> '';
                      PromotedIsBig=true;
                      Image=Invoicing-Send;
                      PromotedCategory=Process;
                      PromotedOnly=true;
                      OnAction=VAR
                                 O365SendResendInvoice@1001 : Codeunit "O365 Send + Resend Invoice";
                               BEGIN
                                 if O365SendResendInvoice.SendSalesInvoiceOrQuote(Rec) then
                                   ForceExit := true;
                               END;
                                }
      { 3       ;1   ;Action    ;
                      Name=ViewPdf;
                      CaptionML=ENU=Preview invoice;
                      ToolTipML=ENU=View the preview of the invoice before sending.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      Enabled=CustomerName <> '';
                      Image=Invoicing-View;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ReportSelections@1001 : Record "Report Selections";
                                 DocumentPath@1000 : Text[250];
                               BEGIN
                                 SETRECFILTER;
                                 LOCKTABLE;
                                 FIND;
                                 ReportSelections.GetPdfReport(DocumentPath,ReportSelections.Usage::"S.Invoice Draft",Rec,"Sell-to Customer No.");
                                 DOWNLOAD(DocumentPath,'','','',DocumentPath);
                                 FIND;
                               END;
                                }
      { 24      ;1   ;Action    ;
                      Name=SaveForLater;
                      CaptionML=ENU=Save for later;
                      ToolTipML=ENU=Close the invoice and save it for later.;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      Enabled=CustomerName <> '';
                      Image=Invoicing-Save;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ForceExit := true;
                                 CurrPage.CLOSE;
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=DeleteAction;
                      CaptionML=ENU=Discard draft;
                      ToolTipML=ENU=Discards the draft invoice;
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=true;
                      Enabled=CustomerName <> '';
                      Image=Invoicing-Delete;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 CustInvoiceDisc@1000 : Record "Cust. Invoice Disc.";
                               BEGIN
                                 if not CONFIRM(DeleteQst) then
                                   exit;

                                 ForceExit := true;

                                 if CustInvoiceDisc.GET("Invoice Disc. Code","Currency Code",0) then
                                   CustInvoiceDisc.DELETE;
                                 DELETE(true);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 25  ;1   ;Group     ;
                Name=Group25;
                GroupType=Group }

    { 5   ;2   ;Group     ;
                Name=Sell to;
                CaptionML=ENU=Sell to;
                GroupType=Group }

    { 9   ;3   ;Field     ;
                Name=Sell-to Customer Name;
                Lookup=true;
                CaptionML=ENU=Customer Name;
                ToolTipML=ENU=Specifies the customer's name.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sell-to Customer Name";
                Importance=Promoted;
                Editable=CurrPageEditable;
                LookupPageID="O365 Customer Lookup";
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerName(Rec,CustomerName,CustomerEmail);
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress);
                             CurrPage.UPDATE(true);
                           END;

                OnLookup=BEGIN
                           if O365SalesInvoiceMgmt.LookupContactFromSalesHeader(Rec) then
                             CurrPage.UPDATE(true);
                         END;

                ImplicitType=Text50;
                ShowCaption=false;
                QuickEntry=FALSE }

    { 18  ;3   ;Field     ;
                Name=CustomerEmail;
                ExtendedDatatype=E-Mail;
                CaptionML=ENU=Email Address;
                ToolTipML=ENU=Specifies the customer's email address.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=CustomerEmail;
                Editable=CurrPageEditable AND (CustomerName <> '');
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerEmail(Rec,CustomerEmail);
                           END;

                ImplicitType=Text80;
                ShowCaption=false }

    { 2   ;1   ;Group     ;
                Name=Invoice;
                CaptionML=ENU=Invoice Details;
                GroupType=Group }

    { 26  ;2   ;Field     ;
                Name=Due Date;
                ToolTipML=ENU=Specifies when the sales invoice must be paid.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Due Date";
                Importance=Additional;
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             if "Due Date" < "Document Date" then
                               VALIDATE("Due Date","Document Date");
                           END;

                ImplicitType=Date }

    { 6   ;2   ;Field     ;
                Name=Document Date;
                CaptionML=ENU=Invoice Date;
                ToolTipML=ENU=Specifies when the sales invoice was created.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Document Date";
                Importance=Additional;
                Editable=CustomerName <> '';
                OnValidate=VAR
                             PastNotification@1000 : Notification;
                           BEGIN
                             VALIDATE("Posting Date","Document Date");

                             if "Document Date" < WORKDATE then begin
                               PastNotification.ID := CREATEGUID;
                               PastNotification.MESSAGE(DocumentDatePastMsg);
                               PastNotification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
                               PastNotification.SEND;
                             end;
                           END;

                ImplicitType=Date }

    { 19  ;2   ;Field     ;
                Name=Tax Liable;
                CaptionML=ENU=Customer is tax liable;
                ToolTipML=ENU=Specifies if the sales invoice contains sales tax.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Tax Liable";
                Importance=Additional;
                Visible=NOT IsUsingVAT;
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;

                ImplicitType=Boolean }

    { 17  ;2   ;Field     ;
                Name=TaxAreaDescription;
                CaptionML=ENU=Customer tax rate;
                ToolTipML=ENU=Specifies the customer's tax area.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                NotBlank=true;
                SourceExpr=TaxAreaDescription;
                Importance=Additional;
                Visible=NOT IsUsingVAT;
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;

                OnLookup=VAR
                           TaxArea@1000 : Record "Tax Area";
                         BEGIN
                           if PAGE.RUNMODAL(PAGE::"O365 Tax Area List",TaxArea) = ACTION::LookupOK then begin
                             VALIDATE("Tax Area Code",TaxArea.Code);
                             TaxAreaDescription := TaxArea.Description;
                             CurrPage.UPDATE;
                           end;
                         END;

                ImplicitType=Text50 }

    { 37  ;2   ;Field     ;
                Name=VAT Registration No.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="VAT Registration No.";
                Importance=Additional;
                Visible=IsUsingVAT;
                Editable=(IsUsingVAT AND IsCompanyContact);
                ImplicitType=Text20 }

    { 11  ;2   ;Field     ;
                Name=FullAddress;
                CaptionML=ENU=Address;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=FullAddress;
                Importance=Additional;
                Editable=CurrPageEditable AND (CustomerName <> '');
                OnAssistEdit=VAR
                               TempStandardAddress@1000 : TEMPORARY Record "Standard Address";
                             BEGIN
                               CurrPage.SAVERECORD;
                               COMMIT;
                               TempStandardAddress.CopyFromSalesHeaderSellTo(Rec);
                               if PAGE.RUNMODAL(PAGE::"O365 Address",TempStandardAddress) = ACTION::LookupOK then begin
                                 FIND;
                                 O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress);
                                 CurrPage.UPDATE(true);
                               end;
                             END;

                ImplicitType=Text;
                QuickEntry=FALSE }

    { 21  ;1   ;Part      ;
                Name=DummyLines;
                CaptionML=ENU=Line Items;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SubPageLink="Document Type"=FIELD("Document Type"),
                            "Document No."=FIELD("No.");
                PagePartID="O365 Sales Invoice Line Dummy";
                Visible=CustomerName = '';
                Editable=CustomerName <> '';
                PartType=Page;
                UpdatePropagation=Both }

    { 14  ;1   ;Part      ;
                Name=Lines;
                CaptionML=ENU=Line Items;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SubPageLink="Document Type"=FIELD("Document Type"),
                            "Document No."=FIELD("No.");
                PagePartID="O365 Sales Invoice Line Subp.";
                Visible=CustomerName <> '';
                Editable=FALSE;
                PartType=Page;
                UpdatePropagation=Both }

    { 15  ;1   ;Group     ;
                Name=Group15;
                Visible=InvDiscAmountVisible;
                GroupType=Group }

    { 16  ;2   ;Field     ;
                Name=SubTotalAmount;
                CaptionML=ENU=Subtotal;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=SubTotalAmount;
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 28  ;2   ;Field     ;
                Name=Invoice Discount Percent;
                CaptionML=ENU=Discount %;
                ToolTipML=ENU=Specifies a discount amount that is deducted from the value in the Total Incl. VAT field. You can enter or change the amount manually.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Invoice Discount Value";
                Importance=Promoted;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 7   ;2   ;Field     ;
                Name=Amount;
                Lookup=false;
                DrillDown=false;
                CaptionML=ENU=Net Total;
                ToolTipML=ENU=Specifies the total amount on the sales invoice excluding VAT.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Amount;
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat;
                ImplicitType=Decimal }

    { 8   ;2   ;Field     ;
                Name=Amount Including VAT;
                Lookup=false;
                DrillDown=false;
                CaptionML=ENU=Total Including VAT;
                ToolTipML=ENU=Specifies the total amount on the sales invoice including VAT.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Amount Including VAT";
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat;
                Importance=Promoted;
                Style=Strong;
                StyleExpr=TRUE;
                ImplicitType=Decimal }

    { 32  ;1   ;Group     ;
                Name=Group32;
                Visible=NOT InvDiscAmountVisible;
                GroupType=Group }

    { 29  ;2   ;Field     ;
                Name=Amount2;
                Lookup=false;
                DrillDown=false;
                CaptionML=ENU=Net Total;
                ToolTipML=ENU=Specifies the total amount on the sales invoice excluding VAT.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Amount;
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat;
                ImplicitType=Decimal }

    { 20  ;2   ;Field     ;
                Name=AmountIncludingVAT2;
                Lookup=false;
                DrillDown=false;
                CaptionML=ENU=Total Including VAT;
                ToolTipML=ENU=Specifies the total amount on the sales invoice including VAT.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Amount Including VAT";
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat;
                Importance=Promoted;
                Style=Strong;
                StyleExpr=TRUE;
                ImplicitType=Decimal }

    { 12  ;1   ;Group     ;
                Name=Group12;
                Visible=CustomerName <> '';
                GroupType=Group }

    { 10  ;2   ;Field     ;
                Name=DiscountLink;
                DrillDown=true;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=DiscountTxt;
                Enabled=CustomerName <> '';
                Editable=FALSE;
                OnDrillDown=BEGIN
                              if PAGE.RUNMODAL(PAGE::"O365 Sales Invoice Discount",Rec) = ACTION::LookupOK then
                                CurrPage.UPDATE(false);
                            END;

                ImplicitType=Text;
                ShowCaption=false }

    { 13  ;2   ;Field     ;
                Name=NoOfAttachments;
                DrillDown=true;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=NoOfAttachmentsValueTxt;
                Enabled=CustomerName <> '';
                Editable=FALSE;
                OnDrillDown=BEGIN
                              O365SalesInvoiceMgmt.UpdateNoOfAttachmentsLabel(O365SalesAttachmentMgt.EditAttachments(Rec),NoOfAttachmentsValueTxt);
                              CurrPage.UPDATE(false);
                            END;

                ImplicitType=Text;
                ShowCaption=false }

    { 31  ;2   ;Group     ;
                Name=Group31;
                Visible=CouponsExistsForCustomer;
                GroupType=Group }

    { 30  ;3   ;Field     ;
                Name=CouponCodes;
                CaptionML=ENU=Coupons;
                ToolTipML=ENU=Specifies the coupon codes used on this invoice.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=CouponCodes;
                OnAssistEdit=VAR
                               DisplayCoupons@1000 : Codeunit "Display Coupons";
                             BEGIN
                               DisplayCoupons.ShowCouponsForDocument("Document Type","No.");
                             END;

                ImplicitType=Text;
                QuickEntry=FALSE }

    { 27  ;2   ;Field     ;
                Name=WorkDescription;
                CaptionML=ENU=Note for customer;
                ToolTipML=ENU=Specifies the products or service being offered;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=WorkDescription;
                Editable=CurrPageEditable AND (CustomerName <> '');
                MultiLine=true;
                OnValidate=BEGIN
                             SetWorkDescription(WorkDescription);
                           END;

                ImplicitType=Text }

  }
  CODE
  {
    VAR
      O365SalesAttachmentMgt@1006 : Codeunit "O365 Sales Attachment Mgt";
      O365SalesInvoiceMgmt@1023 : Codeunit "O365 Sales Invoice Mgmt";
      CustomerName@1000 : Text[50];
      CustomerEmail@1004 : Text[80];
      WorkDescription@1002 : Text;
      FullAddress@1016 : Text;
      CurrPageEditable@1003 : Boolean;
      IsUsingVAT@6116 : Boolean;
      ForceExit@1012 : Boolean;
      NoOfAttachmentsValueTxt@1011 : Text;
      CurrencyFormat@1013 : Text;
      IsCompanyContact@1090 : Boolean;
      InvDiscAmountVisible@1024 : Boolean;
      InvoiceDiscountAmount@1050 : Decimal;
      CouponsExistsForCustomer@1001 : Boolean;
      SubTotalAmount@1017 : Decimal;
      DiscountTxt@1005 : Text;
      DeleteQst@1009 : TextConst 'ENU=Are you sure?';
      TaxAreaDescription@1018 : Text[50];
      CouponCodes@1021 : Text;
      CannotFindRecordErr@1007 : TextConst 'ENU=Cannot find that invoice.';
      InvoiceWasDeletedErr@1008 : TextConst 'ENU=The invoice was deleted.';
      DocumentDatePastMsg@1010 : TextConst 'ENU=Invoice date is in the past.';

    PROCEDURE SuppressExitPrompt@5();
    BEGIN
      ForceExit := true;
    END;

    LOCAL PROCEDURE UpdateFieldsOnAfterGetCurrRec@1();
    BEGIN
      O365SalesInvoiceMgmt.UpdateNoOfAttachmentsLabel(O365SalesAttachmentMgt.GetNoOfAttachments(Rec),NoOfAttachmentsValueTxt);
      O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress);
      O365SalesInvoiceMgmt.CalcInvoiceDiscountAmount(Rec,SubTotalAmount,DiscountTxt,InvoiceDiscountAmount,InvDiscAmountVisible);
    END;

    BEGIN
    END.
  }
}

