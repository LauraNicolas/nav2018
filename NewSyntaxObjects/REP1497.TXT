OBJECT Report 1497 Trans. Bank Rec. to Gen. Jnl.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Trans. Bank Rec. to Gen. Jnl.;
    ProcessingOnly=true;
    OnPostReport=BEGIN
                   GenJnlManagement.TemplateSelectionFromBatch(GenJnlBatch);
                 END;

  }
  DATASET
  {
    { 3510;    ;DataItem;Bank Acc. Reconciliation;
               DataItemTable="Bank Acc. Reconciliation";
               DataItemTableView=SORTING("Bank Account No.","Statement No.") WHERE("Statement Type"=CONST("Bank Reconciliation"));
               OnPreDataItem=BEGIN
                               SETRANGE("Statement Type",BankAccRecon."Statement Type");
                               SETRANGE("Bank Account No.",BankAccRecon."Bank Account No.");
                               SETRANGE("Statement No.",BankAccRecon."Statement No.");
                             END;
                              }

    { 1210;1   ;DataItem;Bank Acc. Reconciliation Line;
               DataItemTable="Bank Acc. Reconciliation Line";
               DataItemTableView=SORTING("Bank Account No.","Statement No.","Statement Line No.");
               OnPreDataItem=BEGIN
                               GenJnlTemplate.GET(GenJnlLine."Journal Template Name");
                               GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
                               GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
                               if GenJnlBatch.Name <> '' then
                                 GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name)
                               else
                                 GenJnlLine.SETRANGE("Journal Batch Name",'');

                               GenJnlLine.LOCKTABLE;
                               if GenJnlLine.FINDLAST then;
                             END;

               OnAfterGetRecord=VAR
                                  SourceCodeSetup@1001 : Record "Source Code Setup";
                                  NoSeriesMgt@1000 : Codeunit NoSeriesManagement;
                                BEGIN
                                  if (Difference = 0) or (Type > Type::"Bank Account Ledger Entry") then
                                    CurrReport.SKIP;

                                  GenJnlLine.INIT;
                                  GenJnlLine."Line No." := GenJnlLine."Line No." + 10000;
                                  GenJnlLine.VALIDATE("Posting Date","Transaction Date");
                                  SourceCodeSetup.GET;
                                  GenJnlLine."Source Code" := SourceCodeSetup."Trans. Bank Rec. to Gen. Jnl.";
                                  if "Document No." <> '' then
                                    GenJnlLine."Document No." := "Document No."
                                  else
                                    if GenJnlBatch."No. Series" <> '' then
                                      GenJnlLine."Document No." := NoSeriesMgt.GetNextNo(
                                          GenJnlBatch."No. Series","Transaction Date",false);
                                  GenJnlLine."Posting No. Series" := GenJnlBatch."Posting No. Series";

                                  if (GenJnlBatch."Bal. Account No." <> '') and
                                     ((GenJnlBatch."Bal. Account Type" <> GenJnlBatch."Bal. Account Type"::"Bank Account") or
                                      (GenJnlBatch."Bal. Account No." <> "Bank Acc. Reconciliation"."Bank Account No."))
                                  then begin
                                    GenJnlLine.VALIDATE("Bal. Account Type",GenJnlBatch."Bal. Account Type");
                                    GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
                                    GenJnlLine.VALIDATE(Amount,Difference);
                                  end else begin
                                    GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Account Type"::"Bank Account");
                                    GenJnlLine.VALIDATE("Bal. Account No.","Bank Acc. Reconciliation"."Bank Account No.");
                                    GenJnlLine.VALIDATE(Amount,-Difference);
                                  end;

                                  GenJnlLine.Description := Description;
                                  GenJnlLine.INSERT;
                                END;

               DataItemLink="Bank Account No."=FIELD("Bank Account No."),"Statement No."=FIELD("Statement No.") }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 2   ;2   ;Field     ;
                  Name=Gen. Journal Template;
                  CaptionML=ENU=Gen. Journal Template;
                  ToolTipML=ENU=Specifies the general journal template that the entries are placed in.;
                  ApplicationArea=#Basic,#Suite;
                  NotBlank=true;
                  SourceExpr=GenJnlLine."Journal Template Name";
                  TableRelation="Gen. Journal Template" }

      { 3   ;2   ;Field     ;
                  Name=Gen. Journal Batch;
                  Lookup=true;
                  CaptionML=ENU=Gen. Journal Batch;
                  ToolTipML=ENU=Specifies the general journal batch that the entries are placed in.;
                  ApplicationArea=#Basic,#Suite;
                  NotBlank=true;
                  SourceExpr=GenJnlLine."Journal Batch Name";
                  OnValidate=BEGIN
                               GenJnlLine.TESTFIELD("Journal Template Name");
                               GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
                             END;

                  OnLookup=BEGIN
                             GenJnlLine.TESTFIELD("Journal Template Name");
                             GenJnlTemplate.GET(GenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(2);
                             GenJnlBatch.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(0);
                             GenJnlBatch.Name := GenJnlLine."Journal Batch Name";
                             if GenJnlBatch.FIND('=><') then;
                             if PAGE.RUNMODAL(0,GenJnlBatch) = ACTION::LookupOK then begin
                               Text := GenJnlBatch.Name;
                               exit(true);
                             end;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GenJnlTemplate@1000 : Record "Gen. Journal Template";
      GenJnlBatch@1001 : Record "Gen. Journal Batch";
      GenJnlLine@1002 : Record "Gen. Journal Line";
      BankAccRecon@1003 : Record "Bank Acc. Reconciliation";
      GenJnlManagement@1004 : Codeunit GenJnlManagement;

    PROCEDURE SetBankAccRecon@1(VAR UseBankAccRecon@1000 : Record "Bank Acc. Reconciliation");
    BEGIN
      BankAccRecon := UseBankAccRecon;
    END;

    PROCEDURE InitializeRequest@2(GenJnlTemplateName@1000 : Code[10];GenJnlBatchName@1001 : Code[10]);
    BEGIN
      GenJnlLine."Journal Template Name" := GenJnlTemplateName;
      GenJnlLine."Journal Batch Name" := GenJnlBatchName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

