OBJECT Codeunit 92 Purch.-Post + Print
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Purchase Header";
    OnRun=VAR
            PurchHeader@1000 : Record "Purchase Header";
          BEGIN
            PurchHeader.COPY(Rec);
            Code(PurchHeader);
            Rec := PurchHeader;
          END;

  }
  CODE
  {
    VAR
      ReceiveInvoiceQst@1000 : TextConst 'ENU=&Receive,&Invoice,Receive &and Invoice';
      PostAndPrintQst@1001 : TextConst '@@@="%1 = Document Type";ENU=Do you want to post and print the %1?';
      ShipInvoiceQst@1002 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice';

    LOCAL PROCEDURE Code@2(VAR PurchHeader@1002 : Record "Purchase Header");
    VAR
      PurchSetup@1000 : Record "Purchases & Payables Setup";
      PurchasePostViaJobQueue@1001 : Codeunit "Purchase Post via Job Queue";
      HideDialog@1003 : Boolean;
    BEGIN
      HideDialog := false;

      OnBeforeConfirmPost(PurchHeader,HideDialog);
      if not HideDialog then
        if not ConfirmPost(PurchHeader) then
          exit;

      PurchSetup.GET;
      if PurchSetup."Post & Print with Job Queue" then
        PurchasePostViaJobQueue.EnqueuePurchDoc(PurchHeader)
      else begin
        CODEUNIT.RUN(CODEUNIT::"Purch.-Post",PurchHeader);
        GetReport(PurchHeader);
      end;
    END;

    LOCAL PROCEDURE ConfirmPost@9(VAR PurchHeader@1000 : Record "Purchase Header") : Boolean;
    VAR
      Selection@1001 : Integer;
    BEGIN
      with PurchHeader do begin
        case "Document Type" of
          "Document Type"::Order:
            begin
              Selection := STRMENU(ReceiveInvoiceQst,3);
              if Selection = 0 then
                exit(false);
              Receive := Selection in [1,3];
              Invoice := Selection in [2,3];
            end;
          "Document Type"::"Return Order":
            begin
              Selection := STRMENU(ShipInvoiceQst,3);
              if Selection = 0 then
                exit(false);
              Ship := Selection in [1,3];
              Invoice := Selection in [2,3];
            end
          else
            if not CONFIRM(PostAndPrintQst,false,"Document Type") then
              exit(false);
        end;
        "Print Posted Documents" := true;
      end;
      exit(true);
    END;

    [External]
    PROCEDURE GetReport@3(VAR PurchHeader@1000 : Record "Purchase Header");
    BEGIN
      with PurchHeader do
        case "Document Type" of
          "Document Type"::Order:
            begin
              if Receive then
                PrintReceive(PurchHeader);
              if Invoice then
                PrintInvoice(PurchHeader);
            end;
          "Document Type"::Invoice:
            PrintInvoice(PurchHeader);
          "Document Type"::"Return Order":
            begin
              if Ship then
                PrintShip(PurchHeader);
              if Invoice then
                PrintCrMemo(PurchHeader);
            end;
          "Document Type"::"Credit Memo":
            PrintCrMemo(PurchHeader);
        end;
    END;

    LOCAL PROCEDURE PrintReceive@1(PurchHeader@1000 : Record "Purchase Header");
    VAR
      PurchRcptHeader@1001 : Record "Purch. Rcpt. Header";
    BEGIN
      PurchRcptHeader."No." := PurchHeader."Last Receiving No.";
      PurchRcptHeader.SETRECFILTER;
      PurchRcptHeader.PrintRecords(false);
    END;

    LOCAL PROCEDURE PrintInvoice@4(PurchHeader@1000 : Record "Purchase Header");
    VAR
      PurchInvHeader@1001 : Record "Purch. Inv. Header";
    BEGIN
      if PurchHeader."Last Posting No." = '' then
        PurchInvHeader."No." := PurchHeader."No."
      else
        PurchInvHeader."No." := PurchHeader."Last Posting No.";
      PurchInvHeader.SETRECFILTER;
      PurchInvHeader.PrintRecords(false);
    END;

    LOCAL PROCEDURE PrintShip@5(PurchHeader@1000 : Record "Purchase Header");
    VAR
      ReturnShptHeader@1001 : Record "Return Shipment Header";
    BEGIN
      ReturnShptHeader."No." := PurchHeader."Last Return Shipment No.";
      ReturnShptHeader.SETRECFILTER;
      ReturnShptHeader.PrintRecords(false);
    END;

    LOCAL PROCEDURE PrintCrMemo@6(PurchHeader@1000 : Record "Purchase Header");
    VAR
      PurchCrMemoHdr@1001 : Record "Purch. Cr. Memo Hdr.";
    BEGIN
      if PurchHeader."Last Posting No." = '' then
        PurchCrMemoHdr."No." := PurchHeader."No."
      else
        PurchCrMemoHdr."No." := PurchHeader."Last Posting No.";
      PurchCrMemoHdr.SETRECFILTER;
      PurchCrMemoHdr.PrintRecords(false);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeConfirmPost@7(VAR PurchaseHeader@1000 : Record "Purchase Header";VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

