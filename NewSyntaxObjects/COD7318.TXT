OBJECT Codeunit 7318 Bin Create
{
  OBJECT-PROPERTIES
  {
    Date=20150915D;
    Time=120000T;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo="Bin Creation Worksheet Line";
    OnRun=BEGIN
            BinCreateLine.COPY(Rec);
            Code;
            Rec := BinCreateLine;
          END;

  }
  CODE
  {
    VAR
      BinCreateLine@1000 : Record "Bin Creation Worksheet Line";
      Bin@1001 : Record Bin;
      Text000@1002 : TextConst 'ENU=Do you want to create the bin?';
      Text001@1003 : TextConst 'ENU=There is nothing to create.';
      Location@1004 : Record Location;

    LOCAL PROCEDURE Code@1();
    BEGIN
      if not CONFIRM(Text000,false) then
        exit;

      if BinCreateLine.FIND('-') then begin
        repeat
          BinCreate(BinCreateLine);
        until BinCreateLine.NEXT = 0;
        BinCreateLine.DELETEALL;
      end else
        MESSAGE(Text001);
    END;

    LOCAL PROCEDURE BinCreate@2(BinCreateLine2@1000 : Record "Bin Creation Worksheet Line");
    BEGIN
      with BinCreateLine2 do begin
        if EmptyLine then
          exit;

        Bin.INIT;
        Bin.Code := "Bin Code";
        Bin.Description := Description;
        Bin."Location Code" := "Location Code";
        Bin.Dedicated := Dedicated;
        GetLocation("Location Code");
        if Location."Directed Put-away and Pick" then begin
          Bin."Zone Code" := "Zone Code";
          Bin."Bin Type Code" := "Bin Type Code";
          Bin."Warehouse Class Code" := "Warehouse Class Code";
          Bin."Block Movement" := "Block Movement";
          Bin."Special Equipment Code" := "Special Equipment Code";
          Bin."Bin Ranking" := "Bin Ranking";
          Bin."Maximum Cubage" := "Maximum Cubage";
          Bin."Maximum Weight" := "Maximum Weight";
          Bin."Cross-Dock Bin" := "Cross-Dock Bin";
        end;
        Bin.INSERT(true);
      end;
    END;

    LOCAL PROCEDURE GetLocation@3(LocationCode@1000 : Code[10]);
    BEGIN
      if LocationCode <> Location.Code then
        Location.GET(LocationCode);
    END;

    BEGIN
    END.
  }
}

