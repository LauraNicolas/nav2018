OBJECT Codeunit 1230 SEPA DD-Export File
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Direct Debit Collection Entry";
    OnRun=VAR
            DirectDebitCollection@1003 : Record "Direct Debit Collection";
            DirectDebitCollectionEntry@1005 : Record "Direct Debit Collection Entry";
            BankAccount@1004 : Record "Bank Account";
          BEGIN
            DirectDebitCollectionEntry.COPY(Rec);
            TESTFIELD("Direct Debit Collection No.");
            DirectDebitCollection.GET("Direct Debit Collection No.");
            DirectDebitCollection.TESTFIELD("To Bank Account No.");
            BankAccount.GET(DirectDebitCollection."To Bank Account No.");
            BankAccount.TESTFIELD(IBAN);
            DirectDebitCollection.LOCKTABLE;
            DirectDebitCollection.DeletePaymentFileErrors;
            COMMIT;
            if not Export(Rec,BankAccount.GetDDExportXMLPortID,DirectDebitCollection.Identifier) then
              ERROR('');

            DirectDebitCollectionEntry.SETRANGE("Direct Debit Collection No.",DirectDebitCollection."No.");
            DirectDebitCollectionEntry.MODIFYALL(Status,DirectDebitCollectionEntry.Status::"File Created");
            DirectDebitCollection.Status := DirectDebitCollection.Status::"File Created";
            DirectDebitCollection.MODIFY;
          END;

  }
  CODE
  {
    VAR
      ExportToServerFile@1000 : Boolean;

    LOCAL PROCEDURE Export@2(VAR DirectDebitCollectionEntry@1006 : Record "Direct Debit Collection Entry";XMLPortID@1004 : Integer;FileName@1003 : Text) : Boolean;
    VAR
      TempBlob@1002 : Record TempBlob;
      FileManagement@1001 : Codeunit "File Management";
      OutStr@1000 : OutStream;
    BEGIN
      TempBlob.INIT;
      TempBlob.Blob.CREATEOUTSTREAM(OutStr);
      XMLPORT.EXPORT(XMLPortID,OutStr,DirectDebitCollectionEntry);
      exit(FileManagement.BLOBExport(TempBlob,STRSUBSTNO('%1.XML',FileName),not ExportToServerFile) <> '');
    END;

    [External]
    PROCEDURE EnableExportToServerFile@1();
    BEGIN
      ExportToServerFile := true;
    END;

    BEGIN
    END.
  }
}

