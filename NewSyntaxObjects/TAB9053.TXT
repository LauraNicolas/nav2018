OBJECT Table 9053 Sales Cue
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Cue;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Sales Quotes - Open ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE ("Document Type"=FILTER(Quote),
                                                                                           Status=FILTER(Open),
                                                                                           "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   CaptionML=ENU=Sales Quotes - Open;
                                                   Editable=false }
    { 3   ;   ;Sales Orders - Open ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE ("Document Type"=FILTER(Order),
                                                                                           Status=FILTER(Open),
                                                                                           "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   AccessByPermission=TableData "Sales Shipment Header"=R;
                                                   CaptionML=ENU=Sales Orders - Open;
                                                   Editable=false }
    { 4   ;   ;Ready to Ship       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE ("Document Type"=FILTER(Order),
                                                                                           Status=FILTER(Released),
                                                                                           "Completely Shipped"=CONST(false),
                                                                                           "Shipment Date"=FIELD("Date Filter2"),
                                                                                           "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   AccessByPermission=TableData "Sales Shipment Header"=R;
                                                   CaptionML=ENU=Ready to Ship;
                                                   Editable=false }
    { 5   ;   ;Delayed             ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE ("Document Type"=FILTER(Order),
                                                                                           Status=FILTER(Released),
                                                                                           "Completely Shipped"=CONST(false),
                                                                                           "Shipment Date"=FIELD("Date Filter"),
                                                                                           "Responsibility Center"=FIELD("Responsibility Center Filter"),
                                                                                           "Late Order Shipping"=FILTER(true)));
                                                   AccessByPermission=TableData "Sales Shipment Header"=R;
                                                   CaptionML=ENU=Delayed;
                                                   Editable=false }
    { 6   ;   ;Sales Return Orders - Open;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE ("Document Type"=FILTER("Return Order"),
                                                                                           Status=FILTER(Open),
                                                                                           "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   AccessByPermission=TableData "Return Receipt Header"=R;
                                                   CaptionML=ENU=Sales Return Orders - Open;
                                                   Editable=false }
    { 7   ;   ;Sales Credit Memos - Open;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE ("Document Type"=FILTER("Credit Memo"),
                                                                                           Status=FILTER(Open),
                                                                                           "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   CaptionML=ENU=Sales Credit Memos - Open;
                                                   Editable=false }
    { 8   ;   ;Partially Shipped   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE ("Document Type"=FILTER(Order),
                                                                                           Status=FILTER(Released),
                                                                                           Shipped=FILTER(true),
                                                                                           "Completely Shipped"=FILTER(false),
                                                                                           "Shipment Date"=FIELD("Date Filter2"),
                                                                                           "Responsibility Center"=FIELD("Responsibility Center Filter")));
                                                   AccessByPermission=TableData "Sales Shipment Header"=R;
                                                   CaptionML=ENU=Partially Shipped;
                                                   Editable=false }
    { 9   ;   ;Average Days Delayed;Decimal       ;AccessByPermission=TableData "Sales Shipment Header"=R;
                                                   CaptionML=ENU=Average Days Delayed;
                                                   DecimalPlaces=1:1;
                                                   Editable=false }
    { 10  ;   ;Sales Inv. - Pending Doc.Exch.;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Invoice Header" WHERE ("Document Exchange Status"=FILTER("Sent to Document Exchange Service"|"Delivery Failed")));
                                                   CaptionML=ENU=Sales Invoices - Pending Document Exchange;
                                                   Editable=false }
    { 12  ;   ;Sales CrM. - Pending Doc.Exch.;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Cr.Memo Header" WHERE ("Document Exchange Status"=FILTER("Sent to Document Exchange Service"|"Delivery Failed")));
                                                   CaptionML=ENU=Sales Credit Memos - Pending Document Exchange;
                                                   Editable=false }
    { 20  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter;
                                                   Editable=false }
    { 21  ;   ;Date Filter2        ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter 2;
                                                   Editable=false }
    { 22  ;   ;Responsibility Center Filter;Code10;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Responsibility Center Filter;
                                                   Editable=false }
    { 23  ;   ;User ID Filter      ;Code50        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=User ID Filter }
    { 24  ;   ;Pending Tasks       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("User Task" WHERE ("Assigned To User Name"=FIELD("User ID Filter"),
                                                                                        "Percent Complete"=FILTER(<>100)));
                                                   CaptionML=ENU=Pending Tasks }
  }
  KEYS
  {
    {    ;"Primary Key"                           ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE SetRespCenterFilter@1();
    VAR
      UserSetupMgt@1000 : Codeunit "User Setup Management";
      RespCenterCode@1001 : Code[10];
    BEGIN
      RespCenterCode := UserSetupMgt.GetSalesFilter;
      if RespCenterCode <> '' then begin
        FILTERGROUP(2);
        SETRANGE("Responsibility Center Filter",RespCenterCode);
        FILTERGROUP(0);
      end;
    END;

    [External]
    PROCEDURE CalculateAverageDaysDelayed@2() AverageDays : Decimal;
    VAR
      SalesHeader@1000 : Record "Sales Header";
      SumDelayDays@1002 : Integer;
      CountDelayedInvoices@1001 : Integer;
    BEGIN
      FilterOrders(SalesHeader,FIELDNO(Delayed));
      if SalesHeader.FINDSET then begin
        repeat
          SumDelayDays += MaximumDelayAmongLines(SalesHeader);
          CountDelayedInvoices += 1;
        until SalesHeader.NEXT = 0;
        AverageDays := SumDelayDays / CountDelayedInvoices;
      end;
    END;

    LOCAL PROCEDURE MaximumDelayAmongLines@17(SalesHeader@1000 : Record "Sales Header") MaxDelay : Integer;
    VAR
      SalesLine@1001 : Record "Sales Line";
    BEGIN
      MaxDelay := 0;
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine.SETFILTER("Shipment Date",'<%1&<>%2',WORKDATE,0D);
      if SalesLine.FINDSET then
        repeat
          if WORKDATE - SalesLine."Shipment Date" > MaxDelay then
            MaxDelay := WORKDATE - SalesLine."Shipment Date";
        until SalesLine.NEXT = 0;
    END;

    [External]
    PROCEDURE CountOrders@9(FieldNumber@1000 : Integer) : Integer;
    VAR
      CountSalesOrders@1001 : Query "Count Sales Orders";
    BEGIN
      CountSalesOrders.SETRANGE(Status,CountSalesOrders.Status::Released);
      CountSalesOrders.SETRANGE(Completely_Shipped,false);
      FILTERGROUP(2);
      CountSalesOrders.SETFILTER(Responsibility_Center,GETFILTER("Responsibility Center Filter"));
      FILTERGROUP(0);

      case FieldNumber of
        FIELDNO("Ready to Ship"):
          begin
            CountSalesOrders.SETRANGE(Ship);
            CountSalesOrders.SETFILTER(Shipment_Date,GETFILTER("Date Filter2"));
          end;
        FIELDNO("Partially Shipped"):
          begin
            CountSalesOrders.SETRANGE(Shipped,true);
            CountSalesOrders.SETFILTER(Shipment_Date,GETFILTER("Date Filter2"));
          end;
        FIELDNO(Delayed):
          begin
            CountSalesOrders.SETRANGE(Ship);
            CountSalesOrders.SETFILTER(Date_Filter,GETFILTER("Date Filter"));
            CountSalesOrders.SETRANGE(Late_Order_Shipping,true);
          end;
      end;
      CountSalesOrders.OPEN;
      CountSalesOrders.READ;
      exit(CountSalesOrders.Count_Orders);
    END;

    LOCAL PROCEDURE FilterOrders@11(VAR SalesHeader@1001 : Record "Sales Header";FieldNumber@1000 : Integer);
    BEGIN
      SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Order);
      SalesHeader.SETRANGE(Status,SalesHeader.Status::Released);
      SalesHeader.SETRANGE("Completely Shipped",false);
      case FieldNumber of
        FIELDNO("Ready to Ship"):
          begin
            SalesHeader.SETRANGE(Ship);
            SalesHeader.SETFILTER("Shipment Date",GETFILTER("Date Filter2"));
          end;
        FIELDNO("Partially Shipped"):
          begin
            SalesHeader.SETRANGE(Shipped,true);
            SalesHeader.SETFILTER("Shipment Date",GETFILTER("Date Filter2"));
          end;
        FIELDNO(Delayed):
          begin
            SalesHeader.SETRANGE(Ship);
            SalesHeader.SETFILTER("Date Filter",GETFILTER("Date Filter"));
            SalesHeader.SETRANGE("Late Order Shipping",true);
          end;
      end;
      FILTERGROUP(2);
      SalesHeader.SETFILTER("Responsibility Center",GETFILTER("Responsibility Center Filter"));
      FILTERGROUP(0);
    END;

    [External]
    PROCEDURE ShowOrders@3(FieldNumber@1001 : Integer);
    VAR
      SalesHeader@1000 : Record "Sales Header";
    BEGIN
      FilterOrders(SalesHeader,FieldNumber);
      PAGE.RUN(PAGE::"Sales Order List",SalesHeader);
    END;

    BEGIN
    END.
  }
}

