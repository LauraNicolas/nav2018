OBJECT Report 5696 Date Compress FA Ledger
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Date Compr. Register"=rimd,
                TableData "Dimension Set ID Filter Line"=imd,
                TableData "FA Ledger Entry"=rimd,
                TableData "FA Register"=rimd;
    CaptionML=ENU=Date Compress FA Ledger;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  DimSelectionBuf.CompareDimText(
                    3,REPORT::"Date Compress FA Ledger",'',RetainDimText,Text012);
                  FALedgEntryFilter := COPYSTR("FA Ledger Entry".GETFILTERS,1,MAXSTRLEN(DateComprReg.Filter));

                  with FALedgEntry2 do begin
                    COPY("FA Ledger Entry");
                    SETRANGE("FA No.");
                    SETRANGE("Depreciation Book Code");
                    if GETFILTERS <> '' then
                      ERROR(
                        Text000,
                        FIELDCAPTION("FA No."),FIELDCAPTION("Depreciation Book Code"));
                    RESET;
                    CLEAR(FALedgEntry2);
                  end;
                END;

  }
  DATASET
  {
    { 9888;    ;DataItem;FA Ledger Entry     ;
               DataItemTable="FA Ledger Entry";
               DataItemTableView=SORTING("FA No.","Depreciation Book Code","FA Posting Date");
               OnPreDataItem=VAR
                               GLSetup@1000 : Record "General Ledger Setup";
                             BEGIN
                               if not CONFIRM(Text001,false) then
                                 CurrReport.BREAK;

                               if EntrdDateComprReg."Ending Date" = 0D then
                                 ERROR(STRSUBSTNO(Text004,EntrdDateComprReg.FIELDCAPTION("Ending Date")));

                               Window.OPEN(
                                 Text005 +
                                 Text006 +
                                 Text007 +
                                 Text008 +
                                 Text009);

                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Compress FA Ledger");

                               SelectedDim.GetSelectedDim(
                                 USERID,3,REPORT::"Date Compress FA Ledger",'',TempSelectedDim);
                               FADimMgt.GetSelectedDim(TempSelectedDim);
                               GLSetup.GET;
                               Retain[4] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress FA Ledger",'',GLSetup."Global Dimension 1 Code");
                               Retain[5] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress FA Ledger",'',GLSetup."Global Dimension 2 Code");

                               NewFALedgEntry.LOCKTABLE;
                               FAReg.LOCKTABLE;
                               DateComprReg.LOCKTABLE;

                               if FALedgEntry2.FIND('+') then;
                               LastEntryNo := FALedgEntry2."Entry No.";
                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE("FA Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");

                               InitRegisters;
                             END;

               OnAfterGetRecord=BEGIN
                                  if "FA No." <> '' then begin
                                    if "FA Posting Category" <> "FA Posting Category"::" " then
                                      CurrReport.SKIP;
                                    case "FA Posting Type" of
                                      "FA Posting Type"::"Proceeds on Disposal",
                                      "FA Posting Type"::"Gain/Loss":
                                        CurrReport.SKIP;
                                    end;
                                  end;
                                  FALedgEntry2 := "FA Ledger Entry";
                                  with FALedgEntry2 do begin
                                    SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
                                    COPYFILTERS("FA Ledger Entry");

                                    SETRANGE("FA No.","FA No.");
                                    SETRANGE("Depreciation Book Code","Depreciation Book Code");
                                    SETRANGE("FA Posting Category","FA Posting Category");
                                    SETRANGE("FA Posting Type","FA Posting Type");
                                    SETRANGE("Part of Book Value","Part of Book Value");
                                    SETRANGE("Part of Depreciable Basis","Part of Depreciable Basis");
                                    SETRANGE("FA Posting Group","FA Posting Group");
                                    SETRANGE("Document Type","Document Type");

                                    SETFILTER("FA Posting Date",DateComprMgt.GetDateFilter("FA Posting Date",EntrdDateComprReg,true));

                                    LastEntryNo := LastEntryNo + 1;

                                    NewFALedgEntry.INIT;
                                    NewFALedgEntry."Entry No." := LastEntryNo;
                                    NewFALedgEntry."FA No." := "FA No.";
                                    NewFALedgEntry."Depreciation Book Code" := "Depreciation Book Code";
                                    NewFALedgEntry."FA Posting Category" := "FA Posting Category";
                                    NewFALedgEntry."FA Posting Type" := "FA Posting Type";
                                    NewFALedgEntry."Part of Book Value" := "Part of Book Value";
                                    NewFALedgEntry."Part of Depreciable Basis" := "Part of Depreciable Basis";
                                    NewFALedgEntry."FA Posting Group" := "FA Posting Group";
                                    NewFALedgEntry."Document Type" := "Document Type";
                                    NewFALedgEntry."FA Posting Date" := GETRANGEMIN("FA Posting Date");
                                    NewFALedgEntry."Posting Date" := GETRANGEMIN("FA Posting Date");
                                    NewFALedgEntry.Description := EntrdFALedgEntry.Description;
                                    NewFALedgEntry."Source Code" := SourceCodeSetup."Compress FA Ledger";
                                    NewFALedgEntry."User ID" := USERID;
                                    Window.UPDATE(1,NewFALedgEntry."FA No.");
                                    Window.UPDATE(2,NewFALedgEntry."FA Posting Date");
                                    DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
                                    Window.UPDATE(3,DateComprReg."No. of New Records");

                                    if RetainNo(FIELDNO("Document No.")) then begin
                                      SETRANGE("Document No.","Document No.");
                                      NewFALedgEntry."Document No." := "Document No.";
                                    end;
                                    if RetainNo(FIELDNO("Reclassification Entry")) then begin
                                      SETRANGE("Reclassification Entry","Reclassification Entry");
                                      NewFALedgEntry."Reclassification Entry" := "Reclassification Entry";
                                    end;
                                    if RetainNo(FIELDNO("Index Entry")) then begin
                                      SETRANGE("Index Entry","Index Entry");
                                      NewFALedgEntry."Index Entry" := "Index Entry";
                                    end;
                                    if RetainNo(FIELDNO("Global Dimension 1 Code")) then begin
                                      SETRANGE("Global Dimension 1 Code","Global Dimension 1 Code");
                                      NewFALedgEntry."Global Dimension 1 Code" := "Global Dimension 1 Code";
                                    end;
                                    if RetainNo(FIELDNO("Global Dimension 2 Code")) then begin
                                      SETRANGE("Global Dimension 2 Code","Global Dimension 2 Code");
                                      NewFALedgEntry."Global Dimension 2 Code" := "Global Dimension 2 Code";
                                    end;
                                    if Quantity >= 0 then
                                      SETFILTER(Quantity,'>=0')
                                    else
                                      SETFILTER(Quantity,'<0');
                                    FADimMgt.GetFALedgEntryDimID(0,"Dimension Set ID");

                                    if "FA No." <> '' then
                                      FindEqualPostingType(FALedgEntry2);
                                    repeat
                                      if FALedgEntryTmp.GET("Entry No.") or ("FA No." = '') then begin
                                        EqualDim := FADimMgt.TestEqualFALedgEntryDimID("Dimension Set ID");
                                        if EqualDim then
                                          SummarizeEntry(NewFALedgEntry,FALedgEntry2);
                                      end else
                                        EqualDim := false;
                                    until (NEXT = 0) or not EqualDim;
                                    InsertNewEntry(NewFALedgEntry);
                                  end;

                                  if DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 then begin
                                    NoOfDeleted := DateComprReg."No. Records Deleted";
                                    InsertRegisters(FAReg,DateComprReg);
                                  end;
                                END;

               OnPostDataItem=BEGIN
                                if DateComprReg."No. Records Deleted" > NoOfDeleted then
                                  InsertRegisters(FAReg,DateComprReg);
                              END;

               ReqFilterFields="FA No.","Depreciation Book Code" }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   if EntrdDateComprReg."Ending Date" = 0D then
                     EntrdDateComprReg."Ending Date" := TODAY;
                   if EntrdFALedgEntry.Description = '' then
                     EntrdFALedgEntry.Description := Text010;

                   with "FA Ledger Entry" do begin
                     InsertField(FIELDNO("Document No."),FIELDCAPTION("Document No."));
                     InsertField(FIELDNO("Reclassification Entry"),FIELDCAPTION("Reclassification Entry"));
                     InsertField(FIELDNO("Index Entry"),FIELDCAPTION("Index Entry"));
                     InsertField(FIELDNO("Global Dimension 1 Code"),FIELDCAPTION("Global Dimension 1 Code"));
                     InsertField(FIELDNO("Global Dimension 2 Code"),FIELDCAPTION("Global Dimension 2 Code"));
                   end;

                   RetainDimText := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Date Compress FA Ledger",'');
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  Name=Starting Date;
                  CaptionML=ENU=Starting Date;
                  ToolTipML=ENU=Specifies the first date to be included in the date compression. The compression affects all fixed asset ledger entries from this date to the Ending Date.;
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  Name=Ending Date;
                  CaptionML=ENU=Ending Date;
                  ToolTipML=ENU=Specifies the date to which the report or batch job processes information.;
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 3   ;2   ;Field     ;
                  Name=Period Length;
                  CaptionML=ENU=Period Length;
                  ToolTipML=ENU=Specifies the length of the period for which combined entries will be created. If you selected the period length Quarter, Month or Week, then only entries with a common accounting period are compressed.;
                  OptionCaptionML=ENU=Day,Week,Month,Quarter,Year,Period;
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 4   ;2   ;Field     ;
                  Name=Posting Description;
                  CaptionML=ENU=Posting Description;
                  ToolTipML=ENU=Specifies the posting date to be used by the batch job as a filter.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=EntrdFALedgEntry.Description }

      { 1907651001;2;Group  ;
                  Name=Retain Field Contents;
                  CaptionML=ENU=Retain Field Contents }

      { 5   ;3   ;Field     ;
                  Name=Document No.;
                  CaptionML=ENU=Document No.;
                  ToolTipML=ENU=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=Retain[1] }

      { 6   ;3   ;Field     ;
                  Name=Reclassification Entry;
                  CaptionML=ENU=Reclassification Entry;
                  ToolTipML=ENU=Specifies which reclassification entry to date compress.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=Retain[2] }

      { 7   ;3   ;Field     ;
                  Name=Index Entry;
                  CaptionML=ENU=Index Entry;
                  ToolTipML=ENU=Specifies the index entry to be data compressed. With the Index Fixed Assets batch job, you can index fixed assets that are linked to a specific depreciation book. The batch job creates entries in a journal based on the conditions that you specify. You can then post the journal or adjust the entries before posting, if necessary.;
                  ApplicationArea=#FixedAssets;
                  SourceExpr=Retain[3] }

      { 8   ;2   ;Field     ;
                  Name=Retain Dimensions;
                  CaptionML=ENU=Retain Dimensions;
                  ToolTipML=ENU=Specifies which dimension information you want to retain when the entries are compressed. The more dimension information that you choose to retain, the more detailed the compressed entries are.;
                  ApplicationArea=#Suite;
                  SourceExpr=RetainDimText;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Date Compress FA Ledger",RetainDimText);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You may set filters only on %1 and %2.';
      Text001@1001 : TextConst 'ENU=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to date compress the entries?';
      Text004@1004 : TextConst 'ENU=%1 must be specified.';
      Text005@1005 : TextConst 'ENU=Date compressing FA ledger entries...\\';
      Text006@1006 : TextConst 'ENU=FA No.               #1##########\';
      Text007@1007 : TextConst 'ENU=Date                 #2######\\';
      Text008@1008 : TextConst 'ENU=No. of new entries   #3######\';
      Text009@1009 : TextConst 'ENU=No. of entries del.  #4######';
      Text010@1010 : TextConst 'ENU=Date Compressed';
      Text011@1011 : TextConst 'ENU=The date compression has been interrupted. Another user changed the table %1.';
      Text012@1012 : TextConst 'ENU=Retain Dimensions';
      SourceCodeSetup@1013 : Record "Source Code Setup";
      DateComprReg@1014 : Record "Date Compr. Register";
      EntrdDateComprReg@1015 : Record "Date Compr. Register";
      FAReg@1016 : Record "FA Register";
      EntrdFALedgEntry@1017 : Record "FA Ledger Entry";
      NewFALedgEntry@1018 : Record "FA Ledger Entry";
      FALedgEntry2@1019 : Record "FA Ledger Entry";
      FALedgEntryTmp@1020 : TEMPORARY Record "FA Ledger Entry";
      SelectedDim@1021 : Record "Selected Dimension";
      TempSelectedDim@1022 : TEMPORARY Record "Selected Dimension";
      DimSelectionBuf@1023 : Record "Dimension Selection Buffer";
      DateComprMgt@1024 : Codeunit DateComprMgt;
      DimMgt@1025 : Codeunit DimensionManagement;
      FACheckConsistency@1026 : Codeunit "FA Check Consistency";
      FADimMgt@1027 : Codeunit FADimensionManagement;
      Window@1028 : Dialog;
      FALedgEntryFilter@1029 : Text[250];
      NoOfFields@1030 : Integer;
      Retain@1031 : ARRAY [10] OF Boolean;
      FieldNumber@1032 : ARRAY [10] OF Integer;
      FieldNameArray@1033 : ARRAY [10] OF Text[100];
      LastEntryNo@1034 : Integer;
      NoOfDeleted@1035 : Integer;
      FARegExists@1036 : Boolean;
      i@1037 : Integer;
      RetainDimText@1038 : Text[250];
      EqualDim@1039 : Boolean;

    LOCAL PROCEDURE InitRegisters@4();
    BEGIN
      if FAReg.FIND('+') then;
      FAReg.INIT;
      FAReg."No." := FAReg."No." + 1;
      FAReg."Creation Date" := TODAY;
      FAReg."Journal Type" := FAReg."Journal Type"::"Fixed Asset";
      FAReg."Source Code" := SourceCodeSetup."Compress FA Ledger";
      FAReg."User ID" := USERID;
      FAReg."From Entry No." := LastEntryNo + 1;

      if DateComprReg.FINDLAST then;
      DateComprReg.INIT;
      DateComprReg."No." := DateComprReg."No." + 1;
      DateComprReg."Table ID" := DATABASE::"FA Ledger Entry";
      DateComprReg."Creation Date" := TODAY;
      DateComprReg."Starting Date" := EntrdDateComprReg."Starting Date";
      DateComprReg."Ending Date" := EntrdDateComprReg."Ending Date";
      DateComprReg."Period Length" := EntrdDateComprReg."Period Length";
      for i := 1 to NoOfFields do
        if Retain[i] then
          DateComprReg."Retain Field Contents" :=
            COPYSTR(
              DateComprReg."Retain Field Contents" + ',' + FieldNameArray[i],1,
              MAXSTRLEN(DateComprReg."Retain Field Contents"));
      DateComprReg."Retain Field Contents" := COPYSTR(DateComprReg."Retain Field Contents",2);
      DateComprReg.Filter := FALedgEntryFilter;
      DateComprReg."Register No." := FAReg."No.";
      DateComprReg."Source Code" := SourceCodeSetup."Compress FA Ledger";
      DateComprReg."User ID" := USERID;

      FARegExists := false;
      NoOfDeleted := 0;
    END;

    LOCAL PROCEDURE InsertRegisters@1(VAR FAReg@1000 : Record "FA Register";VAR DateComprReg@1001 : Record "Date Compr. Register");
    VAR
      FAReg2@1002 : Record "FA Register";
    BEGIN
      FAReg."To Entry No." := NewFALedgEntry."Entry No.";

      if FARegExists then begin
        FAReg.MODIFY;
        DateComprReg.MODIFY;
      end else begin
        FAReg.INSERT;
        DateComprReg.INSERT;
        FARegExists := true;
      end;
      FALedgEntry2.RESET;
      if FALedgEntry2.FIND('+') then;
      LastEntryNo := FALedgEntry2."Entry No.";
      COMMIT;

      NewFALedgEntry.LOCKTABLE;
      FAReg.LOCKTABLE;
      DateComprReg.LOCKTABLE;

      if FALedgEntry2.FIND('+') then;
      if LastEntryNo <> FALedgEntry2."Entry No." then
        ERROR(
          Text011,FALedgEntry2.TABLECAPTION);

      if FAReg2.FINDLAST then ;
      if FAReg."No." <> FAReg2."No." then
        ERROR(
          Text011,FAReg.TABLECAPTION);
    END;

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      exit(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      for i := 1 to NoOfFields do
        if Number = FieldNumber[i] then
          exit(i);
    END;

    LOCAL PROCEDURE FindEqualPostingType@5(VAR FALedgEntry2@1000 : Record "FA Ledger Entry");
    VAR
      FALedgEntry@1001 : Record "FA Ledger Entry";
      EqualPostingType@1002 : Boolean;
    BEGIN
      FALedgEntryTmp.DELETEALL;
      with FALedgEntry do begin
        SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
        SETRANGE("FA No.",FALedgEntry2."FA No.");
        SETRANGE("Depreciation Book Code",FALedgEntry2."Depreciation Book Code");
        SETRANGE("Entry No.",0,"FA Ledger Entry".GETRANGEMAX("Entry No."));
        GET(FALedgEntry2."Entry No.");
        repeat
          EqualPostingType :=
            (FALedgEntry2."FA Posting Category" = "FA Posting Category") and
            (FALedgEntry2."FA Posting Type" = "FA Posting Type") and
            (FALedgEntry2."Part of Book Value" = "Part of Book Value") and
            (FALedgEntry2."Part of Depreciable Basis" = "Part of Depreciable Basis");
          if EqualPostingType then begin
            FALedgEntryTmp."Entry No." := "Entry No.";
            FALedgEntryTmp.INSERT;
          end;
        until (NEXT = 0) or not EqualPostingType;
      end;
    END;

    LOCAL PROCEDURE SummarizeEntry@7(VAR NewFALedgEntry@1000 : Record "FA Ledger Entry";FALedgEntry@1001 : Record "FA Ledger Entry");
    BEGIN
      with FALedgEntry do begin
        NewFALedgEntry.Quantity := NewFALedgEntry.Quantity + Quantity;
        NewFALedgEntry.Amount := NewFALedgEntry.Amount + Amount;
        DELETE;
        DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
        Window.UPDATE(4,DateComprReg."No. Records Deleted");
      end;
    END;

    LOCAL PROCEDURE InsertNewEntry@23(VAR NewFALedgEntry@1000 : Record "FA Ledger Entry");
    VAR
      TempDimBuf@1001 : TEMPORARY Record "Dimension Buffer";
      TempDimSetEntry@1004 : TEMPORARY Record "Dimension Set Entry";
    BEGIN
      TempDimBuf.DELETEALL;
      FADimMgt.GetDimensions(TempDimBuf);
      DimMgt.CopyDimBufToDimSetEntry(TempDimBuf,TempDimSetEntry);
      NewFALedgEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
      NewFALedgEntry.INSERT;
      if NewFALedgEntry."FA No." <> '' then
        FACheckConsistency.SetFAPostingDate(NewFALedgEntry,false);
    END;

    PROCEDURE InitializeRequest@8(StartingDateFrom@1000 : Date;EndingDateFrom@1001 : Date;PeriodLengthFrom@1002 : Option;DescriptionFrom@1003 : Text[50];RetainDimTextFrom@1004 : Text[250]);
    BEGIN
      EntrdDateComprReg."Starting Date" := StartingDateFrom;
      EntrdDateComprReg."Ending Date" := EndingDateFrom;
      EntrdDateComprReg."Period Length" := PeriodLengthFrom;
      EntrdFALedgEntry.Description := DescriptionFrom;
      RetainDimText := RetainDimTextFrom;

      InsertField("FA Ledger Entry".FIELDNO("Reclassification Entry"),"FA Ledger Entry".FIELDCAPTION("Reclassification Entry"));
      InsertField("FA Ledger Entry".FIELDNO("Global Dimension 1 Code"),"FA Ledger Entry".FIELDCAPTION("Global Dimension 1 Code"));
      InsertField("FA Ledger Entry".FIELDNO("Global Dimension 2 Code"),"FA Ledger Entry".FIELDCAPTION("Global Dimension 2 Code"));
    END;

    PROCEDURE SetRetainDocumentNo@9(RetainValue@1000 : Boolean);
    BEGIN
      Retain[1] := RetainValue;
      InsertField("FA Ledger Entry".FIELDNO("Document No."),"FA Ledger Entry".FIELDCAPTION("Document No."));
    END;

    PROCEDURE SetRetainIndexEntry@10(RetainValue@1000 : Boolean);
    BEGIN
      Retain[3] := RetainValue;
      InsertField("FA Ledger Entry".FIELDNO("Index Entry"),"FA Ledger Entry".FIELDCAPTION("Index Entry"));
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

