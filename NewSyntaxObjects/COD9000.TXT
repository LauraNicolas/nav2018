OBJECT Codeunit 9000 Users - Create Super User
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=VAR
            User@1002 : Record User;
            PermissionSet@1001 : Record "Permission Set";
          BEGIN
            if not User.ISEMPTY then
              exit;

            GetSuperRole(PermissionSet);
            CreateUser(User,USERID,SID);
            AssignPermissionSetToUser(User,PermissionSet);
          END;

  }
  CODE
  {
    VAR
      SuperPermissonSetDescTxt@1000 : TextConst 'ENU=This user has all permissions.';

    LOCAL PROCEDURE GetSuperRole@1(VAR PermissionSet@1000 : Record "Permission Set");
    VAR
      Permission@1001 : Record Permission;
    BEGIN
      if PermissionSet.GET('SUPER') then
        exit;
      PermissionSet."Role ID" := 'SUPER';
      PermissionSet.Name := COPYSTR(SuperPermissonSetDescTxt,1,MAXSTRLEN(PermissionSet.Name));
      PermissionSet.INSERT(true);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::"Table Data",0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Table,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Report,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Codeunit,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::XMLport,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::MenuSuite,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Page,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Query,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::System,0);
    END;

    LOCAL PROCEDURE CreateUser@4(VAR User@1003 : Record User;UserName@1000 : Code[50];WindowsSecurityID@1001 : Text[119]);
    BEGIN
      User.INIT;
      User."User Security ID" := CREATEGUID;
      User."User Name" := UserName;
      User."Windows Security ID" := WindowsSecurityID;
      User.INSERT(true);
    END;

    LOCAL PROCEDURE AddPermissionToPermissionSet@7(VAR PermissionSet@1000 : Record "Permission Set";ObjectType@1001 : Option;ObjectID@1002 : Integer);
    VAR
      Permission@1003 : Record Permission;
    BEGIN
      with Permission do begin
        INIT;
        "Role ID" := PermissionSet."Role ID";
        "Object Type" := ObjectType;
        "Object ID" := ObjectID;
        if "Object Type" = "Object Type"::"Table Data" then
          "Execute Permission" := "Execute Permission"::" "
        else begin
          "Read Permission" := "Read Permission"::" ";
          "Insert Permission" := "Insert Permission"::" ";
          "Modify Permission" := "Modify Permission"::" ";
          "Delete Permission" := "Delete Permission"::" ";
        end;
        INSERT(true);
      end;
    END;

    LOCAL PROCEDURE AssignPermissionSetToUser@5(VAR User@1000 : Record User;VAR PermissionSet@1001 : Record "Permission Set");
    VAR
      AccessControl@1002 : Record "Access Control";
    BEGIN
      AccessControl.SETRANGE("User Security ID",User."User Security ID");
      AccessControl.SETRANGE("Role ID",PermissionSet."Role ID");
      if not AccessControl.ISEMPTY then
        exit;
      AccessControl."User Security ID" := User."User Security ID";
      AccessControl."Role ID" := PermissionSet."Role ID";
      AccessControl.INSERT(true);
    END;

    [Internal]
    PROCEDURE AddUserAsSuper@2(VAR User@1000 : Record User);
    VAR
      PermissionSet@1001 : Record "Permission Set";
    BEGIN
      GetSuperRole(PermissionSet);
      AssignPermissionSetToUser(User,PermissionSet);
    END;

    BEGIN
    END.
  }
}

