OBJECT Page 197 Acc. Sched. KPI Web Service
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    Editable=false;
    CaptionML=ENU=Account Schedule KPI Web Service;
    SourceTable=Integer;
    PageType=List;
    OnOpenPage=BEGIN
                 InitSetupData;
                 FILTERGROUP(2);
                 SETRANGE(Number,0,NoOfLines - 1);
                 FILTERGROUP(0);
               END;

    OnAfterGetRecord=BEGIN
                       CalcValues;
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=Number;
                ToolTipML=ENU=Specifies the number of the account-schedule KPI web service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Number;
                Visible=FALSE;
                ImplicitType=Integer }

    { 17  ;2   ;Field     ;
                Name=Date;
                CaptionML=ENU=Date;
                ToolTipML=ENU=Specifies the date that the account-schedule KPI data is based on.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Date;
                ImplicitType=Date }

    { 16  ;2   ;Field     ;
                Name=Closed Period;
                CaptionML=ENU=Closed Period;
                ToolTipML=ENU=Specifies if the accounting period is closed or locked.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ClosedPeriod;
                ImplicitType=Boolean }

    { 13  ;2   ;Field     ;
                Name=Account Schedule Name;
                CaptionML=ENU=Account Schedule Name;
                ToolTipML=ENU=Specifies the name of the account schedule that the KPI web service is based on.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AccScheduleName;
                ImplicitType=Code10 }

    { 18  ;2   ;Field     ;
                Name=KPI Code;
                CaptionML=ENU=KPI Code;
                ToolTipML=ENU=Specifies a code for the account-schedule KPI web service.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=KPICode;
                ImplicitType=Code10 }

    { 4   ;2   ;Field     ;
                Name=KPI Name;
                CaptionML=ENU=KPI Name;
                ToolTipML=ENU=Specifies the name that will be shown on the KPI as a user-friendly name for the account schedule values.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=KPIName;
                ImplicitType=Text }

    { 5   ;2   ;Field     ;
                Name=Net Change Actual;
                CaptionML=ENU=Net Change Actual;
                ToolTipML=ENU=Specifies changes in the actual general ledger amount, for closed accounting periods, up until the date in the Date field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ColumnValue[1] }

    { 6   ;2   ;Field     ;
                Name=Balance at Date Actual;
                CaptionML=ENU=Balance at Date Actual;
                ToolTipML=ENU=Specifies the actual general ledger balance, based on closed accounting periods, on the date in the Date field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ColumnValue[2] }

    { 7   ;2   ;Field     ;
                Name=Net Change Budget;
                CaptionML=ENU=Net Change Budget;
                ToolTipML=ENU=Specifies changes in the budgeted general ledger amount, based on the general ledger budget, up until the date in the Date field.;
                ApplicationArea=#Suite;
                SourceExpr=ColumnValue[3] }

    { 8   ;2   ;Field     ;
                Name=Balance at Date Budget;
                CaptionML=ENU=Balance at Date Budget;
                ToolTipML=ENU=Specifies the budgeted general ledger balance, based on the general ledger budget, on the date in the Date field.;
                ApplicationArea=#Suite;
                SourceExpr=ColumnValue[4] }

    { 9   ;2   ;Field     ;
                Name=Net Change Actual Last Year;
                CaptionML=ENU=Net Change Actual Last Year;
                ToolTipML=ENU=Specifies actual changes in the general ledger amount, based on closed accounting periods, through the previous accounting year.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ColumnValue[5] }

    { 10  ;2   ;Field     ;
                Name=Balance at Date Actual Last Year;
                CaptionML=ENU=Balance at Date Actual Last Year;
                ToolTipML=ENU=Specifies the actual general ledger balance, based on closed accounting periods, on the date in the Date field in the previous accounting year.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ColumnValue[6] }

    { 11  ;2   ;Field     ;
                Name=Net Change Budget Last Year;
                CaptionML=ENU=Net Change Budget Last Year;
                ToolTipML=ENU=Specifies budgeted changes in the general ledger amount, up until the date in the Date field in the previous year.;
                ApplicationArea=#Suite;
                SourceExpr=ColumnValue[7] }

    { 12  ;2   ;Field     ;
                Name=Balance at Date Budget Last Year;
                CaptionML=ENU=Balance at Date Budget Last Year;
                ToolTipML=ENU=Specifies the budgeted general ledger balance, based on the general ledger budget, on the date in the Date field in the previous accounting year.;
                ApplicationArea=#Suite;
                SourceExpr=ColumnValue[8] }

    { 15  ;2   ;Field     ;
                Name=Net Change Forecast;
                CaptionML=ENU=Net Change Forecast;
                ToolTipML=ENU=Specifies forecasted changes in the general ledger amount, based on open accounting periods, up until the date in the Date field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ColumnValue[9] }

    { 14  ;2   ;Field     ;
                Name=Balance at Date Forecast;
                CaptionML=ENU=Balance at Date Forecast;
                ToolTipML=ENU=Specifies the forecasted general ledger balance, based on open accounting periods, on the date in the Date field.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ColumnValue[10] }

  }
  CODE
  {
    VAR
      AccSchedKPIWebSrvSetup@1002 : Record "Acc. Sched. KPI Web Srv. Setup";
      TempAccScheduleLine@1003 : TEMPORARY Record "Acc. Schedule Line";
      TempColumnLayout@1004 : TEMPORARY Record "Column Layout";
      AccSchedManagement@1014 : Codeunit AccSchedManagement;
      NoOfActiveAccSchedLines@1000 : Integer;
      NoOfLines@1005 : Integer;
      StartDate@1006 : Date;
      EndDate@1007 : Date;
      LastClosedDate@1009 : Date;
      AccScheduleName@1012 : Code[10];
      KPICode@1001 : Code[10];
      KPIName@1008 : Text;
      ColumnValue@1010 : ARRAY [10] OF Decimal;
      Date@1011 : Date;
      ClosedPeriod@1013 : Boolean;

    LOCAL PROCEDURE InitSetupData@1();
    VAR
      AccSchedKPIWebSrvLine@1004 : Record "Acc. Sched. KPI Web Srv. Line";
      AccScheduleLine@1000 : Record "Acc. Schedule Line";
      ColumnLayout@1001 : Record "Column Layout";
      LogInManagement@1002 : Codeunit LogInManagement;
    BEGIN
      if not GUIALLOWED then
        WORKDATE := LogInManagement.GetDefaultWorkDate;
      AccSchedKPIWebSrvSetup.GET;
      AccSchedKPIWebSrvLine.FINDSET;
      AccScheduleLine.SETRANGE(Show,AccScheduleLine.Show::Yes);
      AccScheduleLine.SETFILTER(Totaling,'<>%1','');
      repeat
        AccScheduleLine.SETRANGE("Schedule Name",AccSchedKPIWebSrvLine."Acc. Schedule Name");
        AccScheduleLine.FINDSET;
        repeat
          NoOfActiveAccSchedLines += 1;
          TempAccScheduleLine := AccScheduleLine;
          TempAccScheduleLine."Line No." := NoOfActiveAccSchedLines;
          TempAccScheduleLine.INSERT;
        until AccScheduleLine.NEXT = 0;
      until AccSchedKPIWebSrvLine.NEXT = 0;

      with ColumnLayout do begin
        // Net Change Actual
        InsertTempColumn("Column Type"::"Net Change","Ledger Entry Type"::Entries,false);
        // Balance at Date Actual
        InsertTempColumn("Column Type"::"Balance at Date","Ledger Entry Type"::Entries,false);
        // Net Change Budget
        InsertTempColumn("Column Type"::"Net Change","Ledger Entry Type"::"Budget Entries",false);
        // Balance at Date Budget
        InsertTempColumn("Column Type"::"Balance at Date","Ledger Entry Type"::"Budget Entries",false);
        // Net Change Actual Last Year
        InsertTempColumn("Column Type"::"Net Change","Ledger Entry Type"::Entries,true);
        // Balance at Date Actual Last Year
        InsertTempColumn("Column Type"::"Balance at Date","Ledger Entry Type"::Entries,true);
        // Net Change Budget Last Year
        InsertTempColumn("Column Type"::"Net Change","Ledger Entry Type"::"Budget Entries",true);
        // Balance at Date Budget Last Year
        InsertTempColumn("Column Type"::"Balance at Date","Ledger Entry Type"::"Budget Entries",true);
      end;

      AccSchedKPIWebSrvSetup.GetPeriodLength(NoOfLines,StartDate,EndDate);
      NoOfLines *= NoOfActiveAccSchedLines;
      LastClosedDate := AccSchedKPIWebSrvSetup.GetLastClosedAccDate;
    END;

    LOCAL PROCEDURE InsertTempColumn@9(ColumnType@1000 : Option;EntryType@1001 : Option;LastYear@1002 : Boolean);
    BEGIN
      with TempColumnLayout do begin
        if FINDLAST then;
        INIT;
        "Line No." += 10000;
        "Column Type" := ColumnType;
        "Ledger Entry Type" := EntryType;
        if LastYear then
          EVALUATE("Comparison Date Formula",'<-1Y>');
        INSERT;
      end;
    END;

    LOCAL PROCEDURE CalcValues@2();
    VAR
      ToDate@1000 : Date;
      ColNo@1001 : Integer;
    BEGIN
      Date := AccSchedKPIWebSrvSetup.CalcNextStartDate(StartDate,Number div NoOfActiveAccSchedLines);
      ToDate := AccSchedKPIWebSrvSetup.CalcNextStartDate(Date,1) - 1;
      TempAccScheduleLine.FINDSET;
      if Number mod NoOfActiveAccSchedLines > 0 then
        TempAccScheduleLine.NEXT(Number mod NoOfActiveAccSchedLines);
      AccScheduleName := TempAccScheduleLine."Schedule Name";
      TempAccScheduleLine.SETRANGE("Date Filter",Date,ToDate);
      TempAccScheduleLine.SETRANGE("G/L Budget Filter",AccSchedKPIWebSrvSetup."G/L Budget Name");

      KPICode := TempAccScheduleLine."Row No.";
      KPIName := TempAccScheduleLine.Description;

      ColNo := 0;
      TempColumnLayout.FINDSET;
      repeat
        ColNo += 1;
        ColumnValue[ColNo] := AccSchedManagement.CalcCell(TempAccScheduleLine,TempColumnLayout,false);
      until TempColumnLayout.NEXT = 0;

      ClosedPeriod := Date <= LastClosedDate;
      // Forecasted values
      with AccSchedKPIWebSrvSetup do
        if (("Forecasted Values Start" = "Forecasted Values Start"::"After Latest Closed Period") and not ClosedPeriod) or
           (("Forecasted Values Start" = "Forecasted Values Start"::"After Current Date") and (Date > WORKDATE))
        then begin
          ColumnValue[9] := ColumnValue[3]; // Net Change Budget
          ColumnValue[10] := ColumnValue[4]; // Balance at Date Budget
        end else begin
          ColumnValue[9] := ColumnValue[1]; // Net Change Actual
          ColumnValue[10] := ColumnValue[2]; // Balance at Date Actual
        end;
    END;

    BEGIN
    END.
  }
}

