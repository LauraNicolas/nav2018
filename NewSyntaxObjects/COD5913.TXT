OBJECT Codeunit 5913 FaultResolRelation-Calculate
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Searching Posted Service Order No.  #1###########';
      TempFaultResolutionRelation@1001 : TEMPORARY Record "Fault/Resol. Cod. Relationship";
      FaultResolutionRelation@1002 : Record "Fault/Resol. Cod. Relationship";
      ServShptHeader@1003 : Record "Service Shipment Header";
      ServShptItemLine@1004 : Record "Service Shipment Item Line";
      ServShptLine@1005 : Record "Service Shipment Line";
      ResolutionCode@1006 : Record "Resolution Code";
      ServMgtSetup@1007 : Record "Service Mgt. Setup";
      Window@1008 : Dialog;
      AreaFlag@1009 : Boolean;
      SymptomFlag@1010 : Boolean;

    [External]
    PROCEDURE CopyResolutionRelationToTable@34(FromDate@1000 : Date;ToDate@1001 : Date;ServiceItemGroupRelation@1002 : Boolean;RetainManuallyInserted@1003 : Boolean);
    BEGIN
      TempFaultResolutionRelation.RESET;
      if RetainManuallyInserted then
        FaultResolutionRelation.SETRANGE("Created Manually",false);

      FaultResolutionRelation.DELETEALL;

      CLEAR(FaultResolutionRelation);
      if FaultResolutionRelation.FIND('-') then
        repeat
          TempFaultResolutionRelation.INIT;
          TempFaultResolutionRelation."Fault Code" := FaultResolutionRelation."Fault Code";
          TempFaultResolutionRelation."Fault Area Code" := FaultResolutionRelation."Fault Area Code";
          TempFaultResolutionRelation."Symptom Code" := FaultResolutionRelation."Symptom Code";
          TempFaultResolutionRelation."Resolution Code" := FaultResolutionRelation."Resolution Code";
          TempFaultResolutionRelation."Service Item Group Code" := FaultResolutionRelation."Service Item Group Code";
          TempFaultResolutionRelation.Occurrences := FaultResolutionRelation.Occurrences;
          TempFaultResolutionRelation.Description := FaultResolutionRelation.Description;
          TempFaultResolutionRelation."Created Manually" := FaultResolutionRelation."Created Manually";
          TempFaultResolutionRelation.INSERT;
        until FaultResolutionRelation.NEXT = 0;

      CLEAR(FaultResolutionRelation);
      FaultResolutionRelation.DELETEALL;
      Window.OPEN(
        Text000);
      ServShptHeader.SETCURRENTKEY("Posting Date");
      ServShptHeader.SETRANGE("Posting Date",FromDate,ToDate);
      if ServShptHeader.FIND('-') then begin
        ServMgtSetup.GET;
        case ServMgtSetup."Fault Reporting Level" of
          ServMgtSetup."Fault Reporting Level"::Fault:
            begin
              AreaFlag := false;
              SymptomFlag := false;
            end;
          ServMgtSetup."Fault Reporting Level"::"Fault+Symptom":
            begin
              AreaFlag := false;
              SymptomFlag := true;
            end;
          ServMgtSetup."Fault Reporting Level"::"Fault+Symptom+Area (IRIS)":
            begin
              AreaFlag := true;
              SymptomFlag := true;
            end;
        end;

        repeat
          Window.UPDATE(1,ServShptHeader."No.");
          ServShptItemLine.SETRANGE("No.",ServShptHeader."No.");
          if ServShptItemLine.FIND('-') then
            repeat
              ServShptLine.SETRANGE("Document No.",ServShptHeader."No.");
              ServShptLine.SETRANGE("Service Item Line No.",ServShptItemLine."Line No.");
              ServShptLine.SETFILTER("Resolution Code",'<>%1','');
              if ServShptLine.FIND('-') then
                repeat
                  TempFaultResolutionRelation.INIT;
                  TempFaultResolutionRelation."Fault Code" := ServShptLine."Fault Code";
                  if ServiceItemGroupRelation then
                    TempFaultResolutionRelation."Service Item Group Code" := ServShptItemLine."Service Item Group Code"
                  else
                    TempFaultResolutionRelation."Service Item Group Code" := '';
                  if AreaFlag then
                    TempFaultResolutionRelation."Fault Area Code" := ServShptLine."Fault Area Code"
                  else
                    TempFaultResolutionRelation."Fault Area Code" := '';
                  if SymptomFlag then
                    TempFaultResolutionRelation."Symptom Code" := ServShptLine."Symptom Code"
                  else
                    TempFaultResolutionRelation."Symptom Code" := '';
                  TempFaultResolutionRelation."Resolution Code" := ServShptLine."Resolution Code";
                  if ResolutionCode.GET(ServShptLine."Resolution Code") then
                    TempFaultResolutionRelation.Description := ResolutionCode.Description;
                  if not TempFaultResolutionRelation.INSERT then begin
                    FaultResolutionRelation.SETRANGE("Fault Code",ServShptLine."Fault Code");
                    if AreaFlag then
                      FaultResolutionRelation.SETRANGE("Fault Area Code",ServShptLine."Fault Area Code")
                    else
                      FaultResolutionRelation.SETRANGE("Fault Area Code");
                    if ServiceItemGroupRelation then
                      FaultResolutionRelation.SETRANGE("Service Item Group Code",ServShptItemLine."Service Item Group Code")
                    else
                      FaultResolutionRelation.SETRANGE("Service Item Group Code");
                    if SymptomFlag then
                      FaultResolutionRelation.SETRANGE("Symptom Code",ServShptLine."Symptom Code")
                    else
                      FaultResolutionRelation.SETRANGE("Symptom Code");
                    FaultResolutionRelation.SETRANGE("Resolution Code",ServShptLine."Resolution Code");
                    if FaultResolutionRelation.FIND('-') then begin
                      FaultResolutionRelation.Occurrences := FaultResolutionRelation.Occurrences + 1;
                      FaultResolutionRelation.MODIFY;
                    end else begin
                      FaultResolutionRelation.INIT;
                      FaultResolutionRelation."Fault Code" := ServShptLine."Fault Code";
                      if AreaFlag then
                        FaultResolutionRelation."Fault Area Code" := ServShptLine."Fault Area Code"
                      else
                        FaultResolutionRelation."Fault Area Code" := '';
                      if SymptomFlag then
                        FaultResolutionRelation."Symptom Code" := ServShptLine."Symptom Code"
                      else
                        FaultResolutionRelation."Symptom Code" := '';
                      if ServiceItemGroupRelation then
                        FaultResolutionRelation."Service Item Group Code" := ServShptItemLine."Service Item Group Code"
                      else
                        FaultResolutionRelation."Service Item Group Code" := '';
                      FaultResolutionRelation."Resolution Code" := ServShptLine."Resolution Code";
                      if ResolutionCode.GET(ServShptLine."Resolution Code") then
                        FaultResolutionRelation.Description := ResolutionCode.Description;
                      FaultResolutionRelation.Occurrences := 1;
                      FaultResolutionRelation.INSERT;
                    end;
                  end;
                until ServShptLine.NEXT = 0;

            until ServShptItemLine.NEXT = 0;

        until ServShptHeader.NEXT = 0;
      end;
      if TempFaultResolutionRelation.FIND('-') then
        repeat
          FaultResolutionRelation.SETRANGE("Fault Code",TempFaultResolutionRelation."Fault Code");
          if ServiceItemGroupRelation then
            FaultResolutionRelation.SETRANGE("Service Item Group Code",TempFaultResolutionRelation."Service Item Group Code")
          else
            FaultResolutionRelation.SETRANGE("Service Item Group Code");
          if AreaFlag then
            FaultResolutionRelation.SETRANGE("Fault Area Code",TempFaultResolutionRelation."Fault Area Code")
          else
            FaultResolutionRelation.SETRANGE("Fault Area Code");
          if SymptomFlag then
            FaultResolutionRelation.SETRANGE("Symptom Code",TempFaultResolutionRelation."Symptom Code")
          else
            FaultResolutionRelation.SETRANGE("Symptom Code");
          FaultResolutionRelation.SETRANGE("Resolution Code",TempFaultResolutionRelation."Resolution Code");
          if FaultResolutionRelation.FIND('-') then begin
            FaultResolutionRelation.Occurrences := FaultResolutionRelation.Occurrences + 1;
            FaultResolutionRelation.MODIFY;
          end else begin
            FaultResolutionRelation.INIT;
            FaultResolutionRelation."Fault Code" := TempFaultResolutionRelation."Fault Code";
            if AreaFlag then
              FaultResolutionRelation."Fault Area Code" := TempFaultResolutionRelation."Fault Area Code"
            else
              FaultResolutionRelation."Fault Area Code" := '';
            if SymptomFlag then
              FaultResolutionRelation."Symptom Code" := TempFaultResolutionRelation."Symptom Code"
            else
              FaultResolutionRelation."Symptom Code" := '';
            FaultResolutionRelation."Resolution Code" := TempFaultResolutionRelation."Resolution Code";
            if ServiceItemGroupRelation then
              FaultResolutionRelation."Service Item Group Code" := TempFaultResolutionRelation."Service Item Group Code"
            else
              FaultResolutionRelation."Service Item Group Code" := '';
            if ResolutionCode.GET(TempFaultResolutionRelation."Resolution Code") then
              FaultResolutionRelation.Description := ResolutionCode.Description;
            FaultResolutionRelation."Created Manually" := TempFaultResolutionRelation."Created Manually";
            FaultResolutionRelation.Occurrences := 1;
            FaultResolutionRelation.INSERT;
          end;
        until TempFaultResolutionRelation.NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

