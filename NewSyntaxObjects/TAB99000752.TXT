OBJECT Table 99000752 Shop Calendar Working Days
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields="Shop Calendar Code";
    OnInsert=BEGIN
               CheckRedundancy;
             END;

    OnRename=BEGIN
               CheckRedundancy;
             END;

    CaptionML=ENU=Shop Calendar Working Days;
  }
  FIELDS
  {
    { 1   ;   ;Shop Calendar Code  ;Code10        ;TableRelation="Shop Calendar";
                                                   CaptionML=ENU=Shop Calendar Code;
                                                   NotBlank=true }
    { 2   ;   ;Day                 ;Option        ;CaptionML=ENU=Day;
                                                   OptionCaptionML=ENU=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday;
                                                   OptionString=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday }
    { 3   ;   ;Work Shift Code     ;Code10        ;TableRelation="Work Shift";
                                                   CaptionML=ENU=Work Shift Code;
                                                   NotBlank=true }
    { 4   ;   ;Starting Time       ;Time          ;OnValidate=BEGIN
                                                                if ("Ending Time" = 000000T) or
                                                                   ("Ending Time" < "Starting Time")
                                                                then begin
                                                                  ShopCalendar.SETRANGE("Shop Calendar Code","Shop Calendar Code");
                                                                  ShopCalendar.SETRANGE(Day,Day);
                                                                  ShopCalendar.SETRANGE("Starting Time","Starting Time",235959T);
                                                                  if ShopCalendar.FINDFIRST then
                                                                    "Ending Time" := ShopCalendar."Starting Time"
                                                                  else
                                                                    "Ending Time" := 235959T;
                                                                end;
                                                                CheckRedundancy;
                                                              END;

                                                   CaptionML=ENU=Starting Time }
    { 5   ;   ;Ending Time         ;Time          ;OnValidate=BEGIN
                                                                if ("Ending Time" < "Starting Time") and
                                                                   ("Ending Time" <> 000000T)
                                                                then
                                                                  ERROR(Text000,FIELDCAPTION("Ending Time"),FIELDCAPTION("Starting Time"));

                                                                CheckRedundancy;
                                                              END;

                                                   CaptionML=ENU=Ending Time }
  }
  KEYS
  {
    {    ;"Shop Calendar Code",Day,"Starting Time","Ending Time","Work Shift Code";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be higher than %2.';
      Text001@1001 : TextConst 'ENU=There is redundancy in the Shop Calendar. Actual work shift %1 from : %2 to %3. Conflicting work shift %4 from : %5 to %6.';
      ShopCalendar@1002 : Record "Shop Calendar Working Days";

    LOCAL PROCEDURE CheckRedundancy@1();
    VAR
      ShopCalendar2@1000 : Record "Shop Calendar Working Days";
      TempShopCalendar@1001 : TEMPORARY Record "Shop Calendar Working Days";
    BEGIN
      ShopCalendar2.SETRANGE("Shop Calendar Code","Shop Calendar Code");
      ShopCalendar2.SETRANGE(Day,Day);
      if ShopCalendar2.FIND('-') then
        repeat
          TempShopCalendar := ShopCalendar2;
          TempShopCalendar.INSERT;
        until ShopCalendar2.NEXT = 0;

      TempShopCalendar := xRec;
      if TempShopCalendar.DELETE then ;

      TempShopCalendar.SETRANGE("Shop Calendar Code","Shop Calendar Code");
      TempShopCalendar.SETRANGE(Day,Day);
      TempShopCalendar.SETRANGE("Starting Time",000000T,"Ending Time" - 1);
      TempShopCalendar.SETRANGE("Ending Time","Starting Time" + 1,235959T);

      if TempShopCalendar.FINDFIRST then
        ERROR(
          Text001,
          "Work Shift Code",
          "Starting Time",
          "Ending Time",
          TempShopCalendar."Work Shift Code",
          TempShopCalendar."Starting Time",
          TempShopCalendar."Ending Time");
    END;

    BEGIN
    END.
  }
}

