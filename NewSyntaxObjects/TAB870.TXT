OBJECT Table 870 Social Listening Setup
{
  OBJECT-PROPERTIES
  {
    Date=20150915D;
    Time=120000T;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               if "Signup URL" = '' then
                 "Signup URL" := SocialListeningMgt.GetSignupURL;
               if "Terms of Use URL" = '' then
                 "Terms of Use URL" := SocialListeningMgt.GetTermsOfUseURL;
             END;

    CaptionML=ENU=Social Engagement Setup;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Solution ID         ;Text250       ;OnValidate=BEGIN
                                                                if "Solution ID" = '' then begin
                                                                  VALIDATE("Show on Items",false);
                                                                  VALIDATE("Show on Customers",false);
                                                                  VALIDATE("Show on Vendors",false);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Solution ID;
                                                   Editable=false }
    { 3   ;   ;Show on Items       ;Boolean       ;OnValidate=BEGIN
                                                                if "Show on Items" then begin
                                                                  TESTFIELD("Social Listening URL");
                                                                  TESTFIELD("Accept License Agreement");
                                                                  TESTFIELD("Solution ID");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Show on Items }
    { 4   ;   ;Show on Customers   ;Boolean       ;OnValidate=BEGIN
                                                                if "Show on Customers" then begin
                                                                  TESTFIELD("Social Listening URL");
                                                                  TESTFIELD("Accept License Agreement");
                                                                  TESTFIELD("Solution ID");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Show on Customers }
    { 5   ;   ;Show on Vendors     ;Boolean       ;OnValidate=BEGIN
                                                                if "Show on Vendors" then begin
                                                                  TESTFIELD("Social Listening URL");
                                                                  TESTFIELD("Accept License Agreement");
                                                                  TESTFIELD("Solution ID");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Show on Vendors }
    { 6   ;   ;Accept License Agreement;Boolean   ;OnValidate=BEGIN
                                                                if not "Accept License Agreement" then begin
                                                                  VALIDATE("Show on Items",false);
                                                                  VALIDATE("Show on Customers",false);
                                                                  VALIDATE("Show on Vendors",false);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Accept License Agreement }
    { 7   ;   ;Terms of Use URL    ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=ENU=Terms of Use URL;
                                                   NotBlank=true }
    { 8   ;   ;Signup URL          ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=ENU=Signup URL;
                                                   NotBlank=true }
    { 9   ;   ;Social Listening URL;Text250       ;OnValidate=BEGIN
                                                                if "Social Listening URL" = '' then begin
                                                                  VALIDATE("Solution ID",'');
                                                                  exit;
                                                                end;

                                                                if STRPOS("Social Listening URL",'https://') <> 1 then
                                                                  ERROR(MustStartWithErr,FIELDCAPTION("Social Listening URL"));
                                                                if STRPOS("Social Listening URL",'/app/') = 0 then
                                                                  ERROR(MustContainSolutionIDErr,FIELDCAPTION("Social Listening URL"));

                                                                VALIDATE("Solution ID",SocialListeningMgt.ConvertURLToID("Social Listening URL",'/app/'));
                                                              END;

                                                   ExtendedDatatype=URL;
                                                   CaptionML=ENU=Social Engagement URL }
  }
  KEYS
  {
    {    ;"Primary Key"                           ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SocialListeningMgt@1000 : Codeunit "Social Listening Management";
      MustStartWithErr@1001 : TextConst 'ENU=The %1 must start with ''https://''.';
      MustContainSolutionIDErr@1002 : TextConst 'ENU=The %1 must contain the Solution ID.';

    BEGIN
    END.
  }
}

