OBJECT Page 2862 Native - Payment Methods
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=nativePaymentMethods];
    SourceTable="Payment Method";
    DelayedInsert=true;
    PageType=List;
    OnAfterGetRecord=VAR
                       O365SalesInitialSetup@1000 : Record "O365 Sales Initial Setup";
                     BEGIN
                       Default := O365SalesInitialSetup.IsDefaultPaymentMethod(Rec);
                     END;

    OnInsertRecord=VAR
                     PaymentMethod@1002 : Record "Payment Method";
                     O365SalesInitialSetup@1004 : Record "O365 Sales Initial Setup";
                     GraphMgtGeneralTools@1001 : Codeunit "Graph Mgt - General Tools";
                     RecRef@1000 : RecordRef;
                   BEGIN
                     PaymentMethod.SETRANGE(Code,Code);
                     if not PaymentMethod.ISEMPTY then
                       INSERT;

                     INSERT(true);

                     RecRef.GETTABLE(Rec);
                     GraphMgtGeneralTools.ProcessNewRecordFromAPI(RecRef,TempFieldSet,CURRENTDATETIME);
                     RecRef.SETTABLE(Rec);

                     MODIFY(true);

                     if IsDefaultSet then begin
                       O365SalesInitialSetup.UpdateDefaultPaymentMethod(Code);
                       FIND;
                     end;

                     exit(false);
                   END;

    OnModifyRecord=VAR
                     PaymentMethod@1001 : Record "Payment Method";
                     O365SalesInitialSetup@1002 : Record "O365 Sales Initial Setup";
                     GraphMgtGeneralTools@1000 : Codeunit "Graph Mgt - General Tools";
                   BEGIN
                     if xRec.Id <> Id then
                       GraphMgtGeneralTools.ErrorIdImmutable;
                     PaymentMethod.SETRANGE(Id,Id);
                     PaymentMethod.FINDFIRST;

                     if Code = PaymentMethod.Code then
                       MODIFY(true)
                     else begin
                       PaymentMethod.TRANSFERFIELDS(Rec,false);
                       PaymentMethod.RENAME(Code);
                       TRANSFERFIELDS(PaymentMethod);
                     end;

                     if IsDefaultSet then begin
                       O365SalesInitialSetup.UpdateDefaultPaymentMethod(Code);
                       FIND;
                     end;
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id];
                ApplicationArea=#All;
                SourceExpr=Id;
                ImplicitType=GUID }

    { 4   ;2   ;Field     ;
                Name=code;
                CaptionML=[@@@={Locked};
                           ENU=Code];
                ApplicationArea=#All;
                SourceExpr=Code;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Code));
                           END;

                ImplicitType=Code10 }

    { 5   ;2   ;Field     ;
                Name=displayName;
                CaptionML=[@@@={Locked};
                           ENU=Description];
                ApplicationArea=#All;
                SourceExpr=Description;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Description));
                           END;

                ImplicitType=Text50 }

    { 7   ;2   ;Field     ;
                Name=default;
                CaptionML=ENU=default;
                ToolTipML=ENU=Specifies that the payment methods are the default.;
                ApplicationArea=#All;
                SourceExpr=Default;
                OnValidate=BEGIN
                             if not Default then
                               ERROR(CannotSetDefaultToFalseErr);

                             IsDefaultSet := true;
                           END;

                ImplicitType=Boolean }

    { 6   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time";
                ImplicitType=DateTime }

  }
  CODE
  {
    VAR
      TempFieldSet@1000 : TEMPORARY Record Field;
      Default@1001 : Boolean;
      CannotSetDefaultToFalseErr@1002 : TextConst 'ENU=It is not possible to set the default to false. Select a different Payment Method as a default.';
      IsDefaultSet@1003 : Boolean;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      if TempFieldSet.GET(DATABASE::"Payment Method",FieldNo) then
        exit;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::"Payment Method";
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(true);
    END;

    BEGIN
    END.
  }
}

