OBJECT Codeunit 429 IC Dimension Value-Indent
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="IC Dimension Value";
    OnRun=BEGIN
            if not
               CONFIRM(
                 STRSUBSTNO(
                   Text000 +
                   Text001 +
                   Text002 +
                   Text003,"Dimension Code"),true)
            then
              exit;
            ICDimVal.SETRANGE("Dimension Code","Dimension Code");
            Indent;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="This function updates the indentation of all the IC dimension values for IC dimension %1. "';
      Text001@1001 : TextConst 'ENU="All IC dimension values between a Begin-Total and the matching End-Total are indented by one level. "';
      Text002@1002 : TextConst 'ENU=The Totaling field for each End-Total is also updated.\\';
      Text003@1003 : TextConst 'ENU=Do you want to indent the IC dimension values?';
      Text004@1004 : TextConst 'ENU=Indenting IC Dimension Values @1@@@@@@@@@@@@@@@@@@';
      ICDimVal@1006 : Record "IC Dimension Value";
      Window@1007 : Dialog;
      i@1009 : Integer;
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.';

    LOCAL PROCEDURE Indent@1();
    VAR
      NoOfDimVals@1000 : Integer;
      Progress@1001 : Integer;
    BEGIN
      Window.OPEN(Text004);

      NoOfDimVals := ICDimVal.COUNT;
      if NoOfDimVals = 0 then
        NoOfDimVals := 1;
      with ICDimVal do
        if FINDSET then
          repeat
            Progress := Progress + 1;
            Window.UPDATE(1,10000 * Progress div NoOfDimVals);
            if "Dimension Value Type" = "Dimension Value Type"::"End-Total" then begin
              if i < 1 then
                ERROR(
                  Text005,
                  Code);
              i := i - 1;
            end;

            Indentation := i;
            MODIFY;

            if "Dimension Value Type" = "Dimension Value Type"::"Begin-Total" then
              i += 1;
          until NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

