OBJECT Table 113 Sales Invoice Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Item Ledger Entry"=r,
                TableData "Value Entry"=r;
    OnDelete=VAR
               SalesDocLineComments@1000 : Record "Sales Comment Line";
               PostedDeferralHeader@1001 : Record "Posted Deferral Header";
             BEGIN
               SalesDocLineComments.SETRANGE("Document Type",SalesDocLineComments."Document Type"::"Posted Invoice");
               SalesDocLineComments.SETRANGE("No.","Document No.");
               SalesDocLineComments.SETRANGE("Document Line No.","Line No.");
               if not SalesDocLineComments.ISEMPTY then
                 SalesDocLineComments.DELETEALL;

               PostedDeferralHeader.DeleteHeader(DeferralUtilities.GetSalesDeferralDocType,'','',
                 SalesDocLineComments."Document Type"::"Posted Invoice","Document No.","Line No.");
             END;

    CaptionML=ENU=Sales Invoice Line;
    LookupPageID="Posted Sales Invoice Lines";
    DrillDownPageID="Posted Sales Invoice Lines";
  }
  FIELDS
  {
    { 2   ;   ;Sell-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Sell-to Customer No.;
                                                   Editable=false }
    { 3   ;   ;Document No.        ;Code20        ;TableRelation="Sales Invoice Header";
                                                   CaptionML=ENU=Document No. }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 5   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)";
                                                   OptionString=" ","G/L Account",Item,Resource,"Fixed Asset","Charge (Item)" }
    { 6   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST("Fixed Asset")) "Fixed Asset"
                                                                 ELSE IF (Type=CONST("Charge (Item)")) "Item Charge";
                                                   CaptionML=ENU=No.;
                                                   CaptionClass=GetCaptionClass(FIELDNO("No.")) }
    { 7   ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE ("Use As In-Transit"=CONST(false));
                                                   CaptionML=ENU=Location Code }
    { 8   ;   ;Posting Group       ;Code20        ;TableRelation=IF (Type=CONST(Item)) "Inventory Posting Group"
                                                                 ELSE IF (Type=CONST("Fixed Asset")) "FA Posting Group";
                                                   CaptionML=ENU=Posting Group;
                                                   Editable=false }
    { 10  ;   ;Shipment Date       ;Date          ;CaptionML=ENU=Shipment Date }
    { 11  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 12  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2 }
    { 13  ;   ;Unit of Measure     ;Text10        ;CaptionML=ENU=Unit of Measure }
    { 15  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5 }
    { 22  ;   ;Unit Price          ;Decimal       ;CaptionML=ENU=Unit Price;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=GetCurrencyCode;
                                                   CaptionClass=GetCaptionClass(FIELDNO("Unit Price")) }
    { 23  ;   ;Unit Cost (LCY)     ;Decimal       ;CaptionML=ENU=Unit Cost (LCY);
                                                   AutoFormatType=2 }
    { 25  ;   ;VAT %               ;Decimal       ;CaptionML=ENU=VAT %;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 27  ;   ;Line Discount %     ;Decimal       ;CaptionML=ENU=Line Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 28  ;   ;Line Discount Amount;Decimal       ;CaptionML=ENU=Line Discount Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 29  ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 30  ;   ;Amount Including VAT;Decimal       ;CaptionML=ENU=Amount Including VAT;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 32  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Allow Invoice Disc. }
    { 34  ;   ;Gross Weight        ;Decimal       ;CaptionML=ENU=Gross Weight;
                                                   DecimalPlaces=0:5 }
    { 35  ;   ;Net Weight          ;Decimal       ;CaptionML=ENU=Net Weight;
                                                   DecimalPlaces=0:5 }
    { 36  ;   ;Units per Parcel    ;Decimal       ;CaptionML=ENU=Units per Parcel;
                                                   DecimalPlaces=0:5 }
    { 37  ;   ;Unit Volume         ;Decimal       ;CaptionML=ENU=Unit Volume;
                                                   DecimalPlaces=0:5 }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Appl.-to Item Entry }
    { 40  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1));
                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 41  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2));
                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 42  ;   ;Customer Price Group;Code10        ;TableRelation="Customer Price Group";
                                                   CaptionML=ENU=Customer Price Group }
    { 45  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Job No. }
    { 52  ;   ;Work Type Code      ;Code10        ;TableRelation="Work Type";
                                                   CaptionML=ENU=Work Type Code }
    { 63  ;   ;Shipment No.        ;Code20        ;CaptionML=ENU=Shipment No.;
                                                   Editable=false }
    { 64  ;   ;Shipment Line No.   ;Integer       ;CaptionML=ENU=Shipment Line No.;
                                                   Editable=false }
    { 68  ;   ;Bill-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Bill-to Customer No.;
                                                   Editable=false }
    { 69  ;   ;Inv. Discount Amount;Decimal       ;CaptionML=ENU=Inv. Discount Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 73  ;   ;Drop Shipment       ;Boolean       ;AccessByPermission=TableData "Drop Shpt. Post. Buffer"=R;
                                                   CaptionML=ENU=Drop Shipment }
    { 74  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 75  ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=ENU=Gen. Prod. Posting Group }
    { 77  ;   ;VAT Calculation Type;Option        ;CaptionML=ENU=VAT Calculation Type;
                                                   OptionCaptionML=ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   OptionString="Normal VAT","Reverse Charge VAT","Full VAT","Sales Tax" }
    { 78  ;   ;Transaction Type    ;Code10        ;TableRelation="Transaction Type";
                                                   CaptionML=ENU=Transaction Type }
    { 79  ;   ;Transport Method    ;Code10        ;TableRelation="Transport Method";
                                                   CaptionML=ENU=Transport Method }
    { 80  ;   ;Attached to Line No.;Integer       ;TableRelation="Sales Invoice Line"."Line No." WHERE ("Document No."=FIELD("Document No."));
                                                   CaptionML=ENU=Attached to Line No. }
    { 81  ;   ;Exit Point          ;Code10        ;TableRelation="Entry/Exit Point";
                                                   CaptionML=ENU=Exit Point }
    { 82  ;   ;Area                ;Code10        ;TableRelation=Area;
                                                   CaptionML=ENU=Area }
    { 83  ;   ;Transaction Specification;Code10   ;TableRelation="Transaction Specification";
                                                   CaptionML=ENU=Transaction Specification }
    { 84  ;   ;Tax Category        ;Code10        ;CaptionML=ENU=Tax Category }
    { 85  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=ENU=Tax Area Code }
    { 86  ;   ;Tax Liable          ;Boolean       ;CaptionML=ENU=Tax Liable }
    { 87  ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   CaptionML=ENU=Tax Group Code }
    { 88  ;   ;VAT Clause Code     ;Code20        ;TableRelation="VAT Clause";
                                                   CaptionML=ENU=VAT Clause Code }
    { 89  ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 90  ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=ENU=VAT Prod. Posting Group }
    { 97  ;   ;Blanket Order No.   ;Code20        ;TableRelation="Sales Header"."No." WHERE ("Document Type"=CONST("Blanket Order"));
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Blanket Order No. }
    { 98  ;   ;Blanket Order Line No.;Integer     ;TableRelation="Sales Line"."Line No." WHERE ("Document Type"=CONST("Blanket Order"),
                                                                                                "Document No."=FIELD("Blanket Order No."));
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Blanket Order Line No. }
    { 99  ;   ;VAT Base Amount     ;Decimal       ;CaptionML=ENU=VAT Base Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 100 ;   ;Unit Cost           ;Decimal       ;CaptionML=ENU=Unit Cost;
                                                   Editable=false;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 101 ;   ;System-Created Entry;Boolean       ;CaptionML=ENU=System-Created Entry;
                                                   Editable=false }
    { 103 ;   ;Line Amount         ;Decimal       ;CaptionML=ENU=Line Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode;
                                                   CaptionClass=GetCaptionClass(FIELDNO("Line Amount")) }
    { 104 ;   ;VAT Difference      ;Decimal       ;CaptionML=ENU=VAT Difference;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 106 ;   ;VAT Identifier      ;Code20        ;CaptionML=ENU=VAT Identifier;
                                                   Editable=false }
    { 107 ;   ;IC Partner Ref. Type;Option        ;CaptionML=ENU=IC Partner Ref. Type;
                                                   OptionCaptionML=ENU=" ,G/L Account,Item,,,Charge (Item),Cross reference,Common Item No.";
                                                   OptionString=" ","G/L Account",Item,,,"Charge (Item)","Cross reference","Common Item No." }
    { 108 ;   ;IC Partner Reference;Code20        ;CaptionML=ENU=IC Partner Reference }
    { 123 ;   ;Prepayment Line     ;Boolean       ;CaptionML=ENU=Prepayment Line;
                                                   Editable=false }
    { 130 ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner";
                                                   CaptionML=ENU=IC Partner Code }
    { 131 ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
    { 1001;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE ("Job No."=FIELD("Job No."));
                                                   CaptionML=ENU=Job Task No.;
                                                   Editable=false }
    { 1002;   ;Job Contract Entry No.;Integer     ;CaptionML=ENU=Job Contract Entry No.;
                                                   Editable=false }
    { 1700;   ;Deferral Code       ;Code10        ;TableRelation="Deferral Template"."Deferral Code";
                                                   CaptionML=ENU=Deferral Code }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE ("Item No."=FIELD("No."));
                                                   CaptionML=ENU=Variant Code }
    { 5403;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE ("Location Code"=FIELD("Location Code"),
                                                                                 "Item Filter"=FIELD("No."),
                                                                                 "Variant Filter"=FIELD("Variant Code"));
                                                   CaptionML=ENU=Bin Code }
    { 5404;   ;Qty. per Unit of Measure;Decimal   ;CaptionML=ENU=Qty. per Unit of Measure;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5407;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE ("Item No."=FIELD("No."))
                                                                 ELSE "Unit of Measure";
                                                   CaptionML=ENU=Unit of Measure Code }
    { 5415;   ;Quantity (Base)     ;Decimal       ;CaptionML=ENU=Quantity (Base);
                                                   DecimalPlaces=0:5 }
    { 5600;   ;FA Posting Date     ;Date          ;CaptionML=ENU=FA Posting Date }
    { 5602;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   CaptionML=ENU=Depreciation Book Code }
    { 5605;   ;Depr. until FA Posting Date;Boolean;CaptionML=ENU=Depr. until FA Posting Date }
    { 5612;   ;Duplicate in Depreciation Book;Code10;
                                                   TableRelation="Depreciation Book";
                                                   CaptionML=ENU=Duplicate in Depreciation Book }
    { 5613;   ;Use Duplication List;Boolean       ;CaptionML=ENU=Use Duplication List }
    { 5700;   ;Responsibility Center;Code10       ;TableRelation="Responsibility Center";
                                                   CaptionML=ENU=Responsibility Center }
    { 5705;   ;Cross-Reference No. ;Code20        ;AccessByPermission=TableData "Item Cross Reference"=R;
                                                   CaptionML=ENU=Cross-Reference No. }
    { 5706;   ;Unit of Measure (Cross Ref.);Code10;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE ("Item No."=FIELD("No."));
                                                   CaptionML=ENU=Unit of Measure (Cross Ref.) }
    { 5707;   ;Cross-Reference Type;Option        ;CaptionML=ENU=Cross-Reference Type;
                                                   OptionCaptionML=ENU=" ,Customer,Vendor,Bar Code";
                                                   OptionString=" ",Customer,Vendor,"Bar Code" }
    { 5708;   ;Cross-Reference Type No.;Code30    ;CaptionML=ENU=Cross-Reference Type No. }
    { 5709;   ;Item Category Code  ;Code20        ;TableRelation=IF (Type=CONST(Item)) "Item Category";
                                                   CaptionML=ENU=Item Category Code }
    { 5710;   ;Nonstock            ;Boolean       ;CaptionML=ENU=Nonstock }
    { 5711;   ;Purchasing Code     ;Code10        ;TableRelation=Purchasing;
                                                   CaptionML=ENU=Purchasing Code }
    { 5712;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE ("Item Category Code"=FIELD("Item Category Code"));
                                                   ValidateTableRelation=false;
                                                   CaptionML=ENU=Product Group Code }
    { 5811;   ;Appl.-from Item Entry;Integer      ;AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Appl.-from Item Entry;
                                                   MinValue=0 }
    { 6608;   ;Return Reason Code  ;Code10        ;TableRelation="Return Reason";
                                                   CaptionML=ENU=Return Reason Code }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Allow Line Disc. }
    { 7002;   ;Customer Disc. Group;Code20        ;TableRelation="Customer Discount Group";
                                                   CaptionML=ENU=Customer Disc. Group }
    { 7004;   ;Price description   ;Text80        ;CaptionML=ENU=Price description }
  }
  KEYS
  {
    {    ;"Document No.","Line No."               ;SumIndexFields=Amount,"Amount Including VAT";
                                                   MaintainSIFTIndex=false;
                                                   Clustered=true }
    {    ;"Blanket Order No.","Blanket Order Line No." }
    {    ;"Sell-to Customer No."                   }
    { false;"Sell-to Customer No.",Type,"Document No.";
                                                   MaintainSQLIndex=false }
    {    ;"Shipment No.","Shipment Line No."       }
    {    ;"Job Contract Entry No."                 }
    {    ;"Bill-to Customer No."                   }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;"No.",Description,"Line Amount","Price description",Quantity,"Unit of Measure Code" }
  }
  CODE
  {
    VAR
      SalesInvoiceHeader@1002 : Record "Sales Invoice Header";
      Currency@1003 : Record Currency;
      DimMgt@1001 : Codeunit DimensionManagement;
      DeferralUtilities@1000 : Codeunit "Deferral Utilities";
      PriceDescriptionTxt@1004 : TextConst '@@@={Locked};ENU=x%1 (%2%3/%4)';
      PriceDescriptionWithLineDiscountTxt@1066 : TextConst '@@@={Locked};ENU=x%1 (%2%3/%4) - %5%';

    [External]
    PROCEDURE GetCurrencyCode@1() : Code[10];
    BEGIN
      GetHeader;
      exit(SalesInvoiceHeader."Currency Code");
    END;

    [External]
    PROCEDURE ShowDimensions@25();
    BEGIN
      DimMgt.ShowDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2 %3',TABLECAPTION,"Document No.","Line No."));
    END;

    [External]
    PROCEDURE ShowItemTrackingLines@3();
    VAR
      ItemTrackingDocMgt@1000 : Codeunit "Item Tracking Doc. Management";
    BEGIN
      ItemTrackingDocMgt.ShowItemTrackingForInvoiceLine(RowID1);
    END;

    [External]
    PROCEDURE CalcVATAmountLines@2(SalesInvHeader@1000 : Record "Sales Invoice Header";VAR TempVATAmountLine@1001 : TEMPORARY Record "VAT Amount Line");
    BEGIN
      TempVATAmountLine.DELETEALL;
      SETRANGE("Document No.",SalesInvHeader."No.");
      if FIND('-') then
        repeat
          TempVATAmountLine.INIT;
          TempVATAmountLine.CopyFromSalesInvLine(Rec);
          TempVATAmountLine.InsertLine;
        until NEXT = 0;
    END;

    PROCEDURE GetLineAmountExclVAT@149() : Decimal;
    BEGIN
      GetHeader;
      if not SalesInvoiceHeader."Prices Including VAT" then
        exit("Line Amount");
      exit("Line Amount" - GetVATForLineAmount);
    END;

    PROCEDURE GetLineAmountInclVAT@151() : Decimal;
    BEGIN
      GetHeader;
      if SalesInvoiceHeader."Prices Including VAT" then
        exit("Line Amount");
      exit("Line Amount" + GetVATForLineAmount);
    END;

    LOCAL PROCEDURE GetVATForLineAmount@152() : Decimal;
    VAR
      AdjustmentFactorForInvoiceDiscount@1000 : Decimal;
    BEGIN
      if "Line Amount" <= "Inv. Discount Amount" then // Avoid division by zero
        AdjustmentFactorForInvoiceDiscount := 1
      else
        AdjustmentFactorForInvoiceDiscount := "Line Amount" / ("Line Amount" - "Inv. Discount Amount");

      exit(ROUND(("Amount Including VAT" - Amount) * AdjustmentFactorForInvoiceDiscount,Currency."Amount Rounding Precision"));
    END;

    LOCAL PROCEDURE GetHeader@15();
    BEGIN
      if SalesInvoiceHeader."No." = "Document No." then
        exit;
      if not SalesInvoiceHeader.GET("Document No.") then
        SalesInvoiceHeader.INIT;

      if SalesInvoiceHeader."Currency Code" = '' then
        Currency.InitRoundingPrecision
      else
        if not Currency.GET(SalesInvoiceHeader."Currency Code") then
          Currency.InitRoundingPrecision;
    END;

    LOCAL PROCEDURE GetFieldCaption@31(FieldNumber@1000 : Integer) : Text[100];
    VAR
      Field@1001 : Record Field;
    BEGIN
      Field.GET(DATABASE::"Sales Invoice Line",FieldNumber);
      exit(Field."Field Caption");
    END;

    LOCAL PROCEDURE GetCaptionClass@34(FieldNumber@1000 : Integer) : Text[80];
    BEGIN
      GetHeader;
      case FieldNumber of
        FIELDNO("No."):
          exit(STRSUBSTNO('3,%1',GetFieldCaption(FieldNumber)));
        else begin
          if SalesInvoiceHeader."Prices Including VAT" then
            exit('2,1,' + GetFieldCaption(FieldNumber));
          exit('2,0,' + GetFieldCaption(FieldNumber));
        end
      end;
    END;

    [External]
    PROCEDURE RowID1@44() : Text[250];
    VAR
      ItemTrackingMgt@1000 : Codeunit "Item Tracking Management";
    BEGIN
      exit(ItemTrackingMgt.ComposeRowID(DATABASE::"Sales Invoice Line",
          0,"Document No.",'',0,"Line No."));
    END;

    LOCAL PROCEDURE GetSalesShptLines@4(VAR TempSalesShptLine@1000 : TEMPORARY Record "Sales Shipment Line");
    VAR
      SalesShptLine@1003 : Record "Sales Shipment Line";
      ItemLedgEntry@1002 : Record "Item Ledger Entry";
      ValueEntry@1001 : Record "Value Entry";
    BEGIN
      TempSalesShptLine.RESET;
      TempSalesShptLine.DELETEALL;

      if Type <> Type::Item then
        exit;

      FilterPstdDocLineValueEntries(ValueEntry);
      if ValueEntry.FINDSET then
        repeat
          ItemLedgEntry.GET(ValueEntry."Item Ledger Entry No.");
          if ItemLedgEntry."Document Type" = ItemLedgEntry."Document Type"::"Sales Shipment" then
            if SalesShptLine.GET(ItemLedgEntry."Document No.",ItemLedgEntry."Document Line No.") then begin
              TempSalesShptLine.INIT;
              TempSalesShptLine := SalesShptLine;
              if TempSalesShptLine.INSERT then;
            end;
        until ValueEntry.NEXT = 0;
    END;

    [External]
    PROCEDURE CalcShippedSaleNotReturned@5(VAR ShippedQtyNotReturned@1006 : Decimal;VAR RevUnitCostLCY@1004 : Decimal;ExactCostReverse@1003 : Boolean);
    VAR
      TempItemLedgEntry@1002 : TEMPORARY Record "Item Ledger Entry";
      TotalCostLCY@1009 : Decimal;
      TotalQtyBase@1007 : Decimal;
    BEGIN
      ShippedQtyNotReturned := 0;
      if (Type <> Type::Item) or (Quantity <= 0) then begin
        RevUnitCostLCY := "Unit Cost (LCY)";
        exit;
      end;

      RevUnitCostLCY := 0;
      GetItemLedgEntries(TempItemLedgEntry,false);
      if TempItemLedgEntry.FINDSET then
        repeat
          ShippedQtyNotReturned := ShippedQtyNotReturned - TempItemLedgEntry."Shipped Qty. Not Returned";
          if ExactCostReverse then begin
            TempItemLedgEntry.CALCFIELDS("Cost Amount (Expected)","Cost Amount (Actual)");
            TotalCostLCY :=
              TotalCostLCY + TempItemLedgEntry."Cost Amount (Expected)" + TempItemLedgEntry."Cost Amount (Actual)";
            TotalQtyBase := TotalQtyBase + TempItemLedgEntry.Quantity;
          end;
        until TempItemLedgEntry.NEXT = 0;

      if ExactCostReverse and (ShippedQtyNotReturned <> 0) and (TotalQtyBase <> 0) then
        RevUnitCostLCY := ABS(TotalCostLCY / TotalQtyBase) * "Qty. per Unit of Measure"
      else
        RevUnitCostLCY := "Unit Cost (LCY)";
      ShippedQtyNotReturned := CalcQty(ShippedQtyNotReturned);

      if ShippedQtyNotReturned > Quantity then
        ShippedQtyNotReturned := Quantity;
    END;

    LOCAL PROCEDURE CalcQty@11(QtyBase@1000 : Decimal) : Decimal;
    BEGIN
      if "Qty. per Unit of Measure" = 0 then
        exit(QtyBase);
      exit(ROUND(QtyBase / "Qty. per Unit of Measure",0.00001));
    END;

    [External]
    PROCEDURE GetItemLedgEntries@6(VAR TempItemLedgEntry@1000 : TEMPORARY Record "Item Ledger Entry";SetQuantity@1003 : Boolean);
    VAR
      ItemLedgEntry@1002 : Record "Item Ledger Entry";
      ValueEntry@1001 : Record "Value Entry";
    BEGIN
      if SetQuantity then begin
        TempItemLedgEntry.RESET;
        TempItemLedgEntry.DELETEALL;

        if Type <> Type::Item then
          exit;
      end;

      FilterPstdDocLineValueEntries(ValueEntry);
      ValueEntry.SETFILTER("Invoiced Quantity",'<>0');
      if ValueEntry.FINDSET then
        repeat
          ItemLedgEntry.GET(ValueEntry."Item Ledger Entry No.");
          TempItemLedgEntry := ItemLedgEntry;
          if SetQuantity then begin
            TempItemLedgEntry.Quantity := ValueEntry."Invoiced Quantity";
            if ABS(TempItemLedgEntry."Shipped Qty. Not Returned") > ABS(TempItemLedgEntry.Quantity) then
              TempItemLedgEntry."Shipped Qty. Not Returned" := TempItemLedgEntry.Quantity;
          end;
          if TempItemLedgEntry.INSERT then;
        until ValueEntry.NEXT = 0;
    END;

    [External]
    PROCEDURE FilterPstdDocLineValueEntries@7(VAR ValueEntry@1000 : Record "Value Entry");
    BEGIN
      ValueEntry.RESET;
      ValueEntry.SETCURRENTKEY("Document No.");
      ValueEntry.SETRANGE("Document No.","Document No.");
      ValueEntry.SETRANGE("Document Type",ValueEntry."Document Type"::"Sales Invoice");
      ValueEntry.SETRANGE("Document Line No.","Line No.");
    END;

    [External]
    PROCEDURE ShowItemShipmentLines@9();
    VAR
      TempSalesShptLine@1000 : TEMPORARY Record "Sales Shipment Line";
    BEGIN
      if Type = Type::Item then begin
        GetSalesShptLines(TempSalesShptLine);
        PAGE.RUNMODAL(0,TempSalesShptLine);
      end;
    END;

    [External]
    PROCEDURE ShowLineComments@8();
    VAR
      SalesCommentLine@1000 : Record "Sales Comment Line";
    BEGIN
      SalesCommentLine.ShowComments(SalesCommentLine."Document Type"::"Posted Invoice","Document No.","Line No.");
    END;

    [External]
    PROCEDURE InitFromSalesLine@12(SalesInvHeader@1002 : Record "Sales Invoice Header";SalesLine@1001 : Record "Sales Line");
    BEGIN
      INIT;
      TRANSFERFIELDS(SalesLine);
      if ("No." = '') and (Type in [Type::"G/L Account"..Type::"Charge (Item)"]) then
        Type := Type::" ";
      "Posting Date" := SalesInvHeader."Posting Date";
      "Document No." := SalesInvHeader."No.";
      Quantity := SalesLine."Qty. to Invoice";
      "Quantity (Base)" := SalesLine."Qty. to Invoice (Base)";

      OnAfterInitFromSalesLine(SalesInvHeader,SalesLine,Rec);
    END;

    [External]
    PROCEDURE ShowDeferrals@13();
    BEGIN
      DeferralUtilities.OpenLineScheduleView(
        "Deferral Code",DeferralUtilities.GetSalesDeferralDocType,'','',
        GetDocumentType,"Document No.","Line No.");
    END;

    PROCEDURE UpdatePriceDescription@143();
    VAR
      Currency@1000 : Record Currency;
    BEGIN
      "Price description" := '';
      if Type in [Type::"Charge (Item)",Type::"Fixed Asset",Type::Item,Type::Resource] then begin
        if "Line Discount %" = 0 then
          "Price description" := STRSUBSTNO(
              PriceDescriptionTxt,Quantity,Currency.ResolveGLCurrencySymbol(GetCurrencyCode),
              "Unit Price","Unit of Measure")
        else
          "Price description" := STRSUBSTNO(
              PriceDescriptionWithLineDiscountTxt,Quantity,Currency.ResolveGLCurrencySymbol(GetCurrencyCode),
              "Unit Price","Unit of Measure","Line Discount %")
      end;
    END;

    PROCEDURE FormatType@144() : Text;
    VAR
      SalesLine@1000 : Record "Sales Line";
    BEGIN
      if Type = Type::" " then
        exit(SalesLine.FormatType);

      exit(FORMAT(Type));
    END;

    [External]
    PROCEDURE GetDocumentType@14() : Integer;
    VAR
      SalesCommentLine@1000 : Record "Sales Comment Line";
    BEGIN
      exit(SalesCommentLine."Document Type"::"Posted Invoice")
    END;

    PROCEDURE IsCancellationSupported@29() : Boolean;
    BEGIN
      exit(Type in [Type::" ",Type::Item,Type::"G/L Account",Type::"Charge (Item)"]);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitFromSalesLine@10(SalesInvHeader@1000 : Record "Sales Invoice Header";SalesLine@1001 : Record "Sales Line";VAR SalesInvLine@1002 : Record "Sales Invoice Line");
    BEGIN
    END;

    BEGIN
    END.
  }
}

