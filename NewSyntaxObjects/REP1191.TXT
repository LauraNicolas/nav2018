OBJECT Report 1191 Suggest Res. Price Chg. (Res.)
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Suggest Res. Price Chg. (Res.);
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);

                  if ToCurrency.Code = '' then begin
                    ToCurrency.InitRoundingPrecision;
                  end else begin
                    ToCurrency.FIND;
                    ToCurrency.TESTFIELD("Unit-Amount Rounding Precision");
                  end;

                  with ResPriceChg do begin
                    "Currency Code" := ToCurrency.Code;
                    "Work Type Code" := ToWorkType.Code;
                  end;
                END;

  }
  DATASET
  {
    { 5508;    ;DataItem;Resource            ;
               DataItemTable=Resource;
               DataItemTableView=SORTING("No.");
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000)
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");
                                  with ResPriceChg do begin
                                    Type := Type::Resource;
                                    Code := Resource."No.";
                                    "New Unit Price" :=
                                      ROUND(
                                        CurrExchRate.ExchangeAmtLCYToFCY(
                                          WORKDATE,ToCurrency.Code,
                                          Resource."Unit Price",
                                          CurrExchRate.ExchangeRate(
                                            WORKDATE,ToCurrency.Code)),
                                        ToCurrency."Unit-Amount Rounding Precision");

                                    if "New Unit Price" > PriceLowerLimit then
                                      "New Unit Price" := "New Unit Price" * UnitPriceFactor;
                                    if RoundingMethod.Code <> '' then begin
                                      RoundingMethod."Minimum Amount" := "New Unit Price";
                                      if RoundingMethod.FIND('=<') then begin
                                        "New Unit Price" := "New Unit Price" + RoundingMethod."Amount Added Before";

                                        if RoundingMethod.Precision > 0 then
                                          "New Unit Price" :=
                                            ROUND(
                                              "New Unit Price",
                                              RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                                        "New Unit Price" := "New Unit Price" + RoundingMethod."Amount Added After";
                                      end;
                                    end;

                                    ResPrice.SETRANGE(Type,Type);
                                    ResPrice.SETRANGE(Code,Code);
                                    ResPrice.SETRANGE("Currency Code",ToCurrency.Code);
                                    ResPrice.SETRANGE("Work Type Code",ToWorkType.Code);
                                    if ResPrice.FINDLAST then begin
                                      "Current Unit Price" := ResPrice."Unit Price";
                                      PriceAlreadyExists := true
                                    end else begin
                                      "Current Unit Price" := 0;
                                      PriceAlreadyExists := false;
                                    end;

                                    if PriceAlreadyExists or CreateNewPrices then begin
                                      ResPriceChg2 := ResPriceChg;
                                      if ResPriceChg2.FIND('=') then
                                        MODIFY
                                      else
                                        INSERT;
                                    end;
                                  end;
                                END;

               ReqFilterFields="No.","Resource Group No." }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   if UnitPriceFactor = 0 then
                     UnitPriceFactor := 1;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 15  ;2   ;Group     ;
                  Name=Copy to Res. Price Change...;
                  CaptionML=ENU=Copy to Res. Price Change... }

      { 7   ;3   ;Field     ;
                  Name=Currency Code;
                  CaptionML=ENU=Currency Code;
                  ToolTipML=ENU=Specifies the code for the currency that amounts are shown in.;
                  ApplicationArea=#Jobs;
                  SourceExpr=ToCurrency.Code;
                  TableRelation=Currency }

      { 4   ;3   ;Field     ;
                  Name=Work Type;
                  CaptionML=ENU=Work Type;
                  ToolTipML=ENU=Specifies the work types to which the new prices apply. If you want the new price to be calculated for all work types, enter ALL.;
                  ApplicationArea=#Jobs;
                  SourceExpr=ToWorkType.Code;
                  TableRelation="Work Type" }

      { 16  ;2   ;Field     ;
                  Name=Only Amounts Above;
                  CaptionML=ENU=Only Amounts Above;
                  ToolTipML=ENU=Specifies an amount to determine the lowest unit price that is changed. Only prices that are higher than this are changed. If a price is lower than or equal to this amount, a line for it is created in the Resource Price Changes window, but with the same unit price as on the resource card.;
                  ApplicationArea=#Jobs;
                  DecimalPlaces=2:5;
                  SourceExpr=PriceLowerLimit }

      { 13  ;2   ;Field     ;
                  Name=Adjustment Factor;
                  CaptionML=ENU=Adjustment Factor;
                  ToolTipML=ENU=Specifies an adjustment factor to multiply the amounts that you want suggested. By entering an adjustment factor, you can increase or decrease the amounts that are suggested.;
                  ApplicationArea=#Jobs;
                  DecimalPlaces=0:5;
                  SourceExpr=UnitPriceFactor;
                  MinValue=0 }

      { 9   ;2   ;Field     ;
                  Name=Rounding Method;
                  CaptionML=ENU=Rounding Method;
                  ToolTipML=ENU=Specifies a code for the rounding method that you want to apply to costs or prices that you adjust.;
                  ApplicationArea=#Jobs;
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

      { 1   ;2   ;Field     ;
                  Name=Create New Prices;
                  CaptionML=ENU=Create New Prices;
                  ToolTipML=ENU=Specifies if you want the batch job to create new price suggestions, such as a new combination of currency, job number, or work type. If you only want to adjust existing alternative prices, do not select.;
                  ApplicationArea=#Jobs;
                  SourceExpr=CreateNewPrices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing items  #1##########';
      RoundingMethod@1001 : Record "Rounding Method";
      ToCurrency@1002 : Record Currency;
      CurrExchRate@1003 : Record "Currency Exchange Rate";
      ToWorkType@1010 : Record "Work Type";
      ResPriceChg2@1009 : Record "Resource Price Change";
      ResPriceChg@1008 : Record "Resource Price Change";
      ResPrice@1013 : Record "Resource Price";
      Window@1012 : Dialog;
      PriceAlreadyExists@1004 : Boolean;
      CreateNewPrices@1005 : Boolean;
      UnitPriceFactor@1006 : Decimal;
      PriceLowerLimit@1007 : Decimal;

    PROCEDURE InitializeCopyToResPrice@2(CurrencyCode@1000 : Code[10];WorkTypeCode@1002 : Code[10]);
    BEGIN
      ToCurrency.Code := CurrencyCode;
      ToWorkType.Code := WorkTypeCode;
    END;

    PROCEDURE InitializeRequest@1(PriceLowerLimitFrom@1003 : Decimal;UnitPriceFactorFrom@1004 : Decimal;RoundingMethodCode@1005 : Code[10];CreateNewPricesFrom@1006 : Boolean);
    BEGIN
      PriceLowerLimit := PriceLowerLimitFrom;
      UnitPriceFactor := UnitPriceFactorFrom;
      RoundingMethod.Code := RoundingMethodCode;
      CreateNewPrices := CreateNewPricesFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

