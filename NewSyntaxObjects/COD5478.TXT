OBJECT Codeunit 5478 Graph Mgt - Journal Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GraphMgtJournal@1000 : Codeunit "Graph Mgt - Journal";

    PROCEDURE SetJournalLineTemplateAndBatch@1(VAR GenJournalLine@1001 : Record "Gen. Journal Line";JournalLineBatchName@1003 : Code[10]);
    BEGIN
      GenJournalLine.VALIDATE("Journal Template Name",GraphMgtJournal.GetDefaultJournalLinesTemplateName);
      GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");

      GenJournalLine.VALIDATE("Journal Batch Name",JournalLineBatchName);
      GenJournalLine.SETRANGE("Journal Batch Name",JournalLineBatchName);
    END;

    PROCEDURE SetJournalLineFilters@3(VAR GenJournalLine@1000 : Record "Gen. Journal Line");
    BEGIN
      GenJournalLine.SETRANGE("Document Type",GenJournalLine."Document Type"::" ");
      GenJournalLine.SETRANGE("Account Type",GenJournalLine."Account Type"::"G/L Account");
      GenJournalLine.SETRANGE("Journal Template Name",GraphMgtJournal.GetDefaultJournalLinesTemplateName);
    END;

    PROCEDURE SetJournalLineValues@25(VAR GenJournalLine@1000 : Record "Gen. Journal Line";TempGenJournalLine@1001 : TEMPORARY Record "Gen. Journal Line");
    VAR
      DummyDate@1002 : Date;
    BEGIN
      GenJournalLine.VALIDATE("Account Type",GenJournalLine."Account Type"::"G/L Account");
      GenJournalLine.VALIDATE("Account No.",TempGenJournalLine."Account No.");
      if TempGenJournalLine."Posting Date" <> DummyDate then
        GenJournalLine.VALIDATE("Posting Date",TempGenJournalLine."Posting Date");
      GenJournalLine.VALIDATE("Document Type",GenJournalLine."Document Type"::" ");
      if TempGenJournalLine."Document No." <> '' then
        GenJournalLine.VALIDATE("Document No.",TempGenJournalLine."Document No.");
      GenJournalLine.VALIDATE("External Document No.",TempGenJournalLine."External Document No.");
      GenJournalLine.VALIDATE(Amount,TempGenJournalLine.Amount);
      if TempGenJournalLine.Description <> '' then
        GenJournalLine.VALIDATE(Description,TempGenJournalLine.Description);
      GenJournalLine.VALIDATE(Comment,TempGenJournalLine.Comment);
      GenJournalLine.VALIDATE("Bal. Account No.",'');
    END;

    LOCAL PROCEDURE EnableAccountODataWebService@1008();
    BEGIN
      UpdateIntegrationRecords(false);
    END;

    PROCEDURE UpdateIntegrationRecords@2(OnlyItemsWithoutId@1000 : Boolean);
    VAR
      GenJnlLine@1003 : Record "Gen. Journal Line";
      GraphMgtGeneralTools@1002 : Codeunit "Graph Mgt - General Tools";
      GenJnlLineRecordRef@1001 : RecordRef;
    BEGIN
      GenJnlLineRecordRef.OPEN(DATABASE::"Gen. Journal Line");
      GraphMgtGeneralTools.UpdateIntegrationRecords(GenJnlLineRecordRef,GenJnlLine.FIELDNO(Id),OnlyItemsWithoutId);
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnJournalLines@1016(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummyGenJnlLine@1004 : Record "Gen. Journal Line";
      GraphMgtGeneralTools@1003 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(
        RecRef,NewId,Handled,DATABASE::"Gen. Journal Line",DummyGenJnlLine.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@1017(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummyGenJnlLine@1004 : Record "Gen. Journal Line";
      GraphMgtGeneralTools@1003 : Codeunit "Graph Mgt - General Tools";
    BEGIN
      GraphMgtGeneralTools.HandleGetPredefinedIdValue(
        Id,RecRef,Handled,DATABASE::"Gen. Journal Line",DummyGenJnlLine.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@1003();
    BEGIN
      EnableAccountODataWebService;
      UpdateIds;
    END;

    PROCEDURE UpdateIds@6();
    VAR
      GenJournalLine@1000 : Record "Gen. Journal Line";
    BEGIN
      with GenJournalLine do begin
        SETRANGE("Account Type","Account Type"::"G/L Account");

        if FINDSET then
          repeat
            UpdateAccountID;
            UpdateJournalBatchID;
            MODIFY(false);
          until NEXT = 0;
      end;
    END;

    BEGIN
    END.
  }
}

