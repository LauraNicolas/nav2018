OBJECT Codeunit 5506 Graph Mgt - Sales Quote Buffer
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GraphMgtGeneralTools@1000 : Codeunit "Graph Mgt - General Tools";
      DocumentIDNotSpecifiedForLinesErr@1008 : TextConst '@@@={Locked};ENU=You must specify a document id to get the lines.';
      DocumentDoesNotExistErr@1002 : TextConst '@@@={Locked};ENU=No document with the specified ID exists.';
      MultipleDocumentsFoundForIdErr@1003 : TextConst '@@@={Locked};ENU=Multiple documents have been found for the specified criteria.';
      CannotInsertALineThatAlreadyExistsErr@1004 : TextConst '@@@={Locked};ENU=You cannot insert a line because a line already exists.';
      CannotModifyALineThatDoesntExistErr@1005 : TextConst '@@@={Locked};ENU=You cannot modify a line that does not exist.';
      SkipUpdateDiscounts@1006 : Boolean;

    [EventSubscriber(Table,36,OnAfterInsertEvent)]
    LOCAL PROCEDURE OnAfterInsertSalesHeader@2(VAR Rec@1000 : Record "Sales Header";RunTrigger@1001 : Boolean);
    BEGIN
      if not CheckValidRecord(Rec) or (not GraphMgtGeneralTools.IsApiEnabled) then
        exit;

      InsertOrModifyFromSalesHeader(Rec);
    END;

    [EventSubscriber(Table,36,OnAfterModifyEvent)]
    LOCAL PROCEDURE OnAfterModifySalesHeader@3(VAR Rec@1000 : Record "Sales Header";VAR xRec@1001 : Record "Sales Header";RunTrigger@1002 : Boolean);
    BEGIN
      if not CheckValidRecord(Rec) or (not GraphMgtGeneralTools.IsApiEnabled) then
        exit;

      InsertOrModifyFromSalesHeader(Rec);
    END;

    [EventSubscriber(Table,36,OnAfterDeleteEvent)]
    LOCAL PROCEDURE OnAfterDeleteSalesHeader@5(VAR Rec@1000 : Record "Sales Header";RunTrigger@1001 : Boolean);
    VAR
      SalesQuoteEntityBuffer@1002 : Record "Sales Quote Entity Buffer";
    BEGIN
      if not CheckValidRecord(Rec) or (not GraphMgtGeneralTools.IsApiEnabled) then
        exit;

      if not SalesQuoteEntityBuffer.GET(Rec."No.") then
        exit;
      SalesQuoteEntityBuffer.DELETE;
    END;

    [EventSubscriber(Codeunit,56,OnAfterResetRecalculateInvoiceDisc)]
    LOCAL PROCEDURE OnAfterResetRecalculateInvoiceDisc@41(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      if not CheckValidRecord(SalesHeader) or (not GraphMgtGeneralTools.IsApiEnabled) then
        exit;

      InsertOrModifyFromSalesHeader(SalesHeader);
    END;

    [EventSubscriber(Table,37,OnAfterInsertEvent)]
    LOCAL PROCEDURE OnAfterInsertSalesLine@71(VAR Rec@1000 : Record "Sales Line";RunTrigger@1001 : Boolean);
    BEGIN
      if not CheckValidLineRecord(Rec) then
        exit;

      ModifyTotalsSalesLine(Rec,true);
    END;

    [EventSubscriber(Table,37,OnAfterModifyEvent)]
    LOCAL PROCEDURE OnAfterModifySalesLine@72(VAR Rec@1000 : Record "Sales Line";VAR xRec@1001 : Record "Sales Line";RunTrigger@1002 : Boolean);
    BEGIN
      if not CheckValidLineRecord(Rec) then
        exit;

      ModifyTotalsSalesLine(Rec,Rec."Recalculate Invoice Disc.");
    END;

    [EventSubscriber(Table,37,OnAfterDeleteEvent)]
    LOCAL PROCEDURE OnAfterDeleteSalesLine@73(VAR Rec@1000 : Record "Sales Line";RunTrigger@1001 : Boolean);
    VAR
      SalesLine@1003 : Record "Sales Line";
    BEGIN
      if not CheckValidLineRecord(Rec) then
        exit;

      SalesLine.SETRANGE("Document No.",Rec."Document No.");
      SalesLine.SETRANGE("Document Type",Rec."Document Type");
      SalesLine.SETRANGE("Recalculate Invoice Disc.",true);

      if SalesLine.FINDFIRST then begin
        ModifyTotalsSalesLine(SalesLine,true);
        exit;
      end;

      SalesLine.SETRANGE("Recalculate Invoice Disc.");

      if not SalesLine.FINDFIRST then
        BlankTotals(Rec."Document No.");
    END;

    [EventSubscriber(Codeunit,60,OnAfterCalcSalesDiscount)]
    LOCAL PROCEDURE OnAfterCalculateSalesDiscountOnSalesHeader@36(VAR SalesHeader@1000 : Record "Sales Header");
    BEGIN
      if not CheckValidRecord(SalesHeader) or (not GraphMgtGeneralTools.IsApiEnabled) then
        exit;

      InsertOrModifyFromSalesHeader(SalesHeader);
    END;

    PROCEDURE PropagateOnInsert@15(VAR SalesQuoteEntityBuffer@1000 : Record "Sales Quote Entity Buffer";VAR TempFieldBuffer@1003 : TEMPORARY Record "Field Buffer");
    VAR
      SalesHeader@1001 : Record "Sales Header";
      TypeHelper@1002 : Codeunit "Type Helper";
      TargetRecordRef@1004 : RecordRef;
      DocTypeFieldRef@1006 : FieldRef;
      NoFieldRef@1007 : FieldRef;
    BEGIN
      if SalesQuoteEntityBuffer.ISTEMPORARY or (not GraphMgtGeneralTools.IsApiEnabled) then
        exit;

      TargetRecordRef.OPEN(DATABASE::"Sales Header");

      DocTypeFieldRef := TargetRecordRef.FIELD(SalesHeader.FIELDNO("Document Type"));
      DocTypeFieldRef.VALUE(SalesHeader."Document Type"::Quote);

      NoFieldRef := TargetRecordRef.FIELD(SalesHeader.FIELDNO("No."));

      TypeHelper.TransferFieldsWithValidate(TempFieldBuffer,SalesQuoteEntityBuffer,TargetRecordRef);
      SetStatusOptionToSalesHeader(TempFieldBuffer,SalesQuoteEntityBuffer,TargetRecordRef);

      TargetRecordRef.INSERT(true);

      SalesQuoteEntityBuffer."No." := NoFieldRef.VALUE;
      SalesQuoteEntityBuffer.FIND;
    END;

    PROCEDURE PropagateOnModify@16(VAR SalesQuoteEntityBuffer@1000 : Record "Sales Quote Entity Buffer";VAR TempFieldBuffer@1004 : TEMPORARY Record "Field Buffer");
    VAR
      SalesHeader@1001 : Record "Sales Header";
      TypeHelper@1002 : Codeunit "Type Helper";
      TargetRecordRef@1005 : RecordRef;
      Exists@1003 : Boolean;
    BEGIN
      if SalesQuoteEntityBuffer.ISTEMPORARY or (not GraphMgtGeneralTools.IsApiEnabled) then
        exit;

      Exists := SalesHeader.GET(SalesHeader."Document Type"::Quote,SalesQuoteEntityBuffer."No.");
      if Exists then
        TargetRecordRef.GETTABLE(SalesHeader)
      else
        TargetRecordRef.OPEN(DATABASE::"Sales Header");

      TypeHelper.TransferFieldsWithValidate(TempFieldBuffer,SalesQuoteEntityBuffer,TargetRecordRef);
      SetStatusOptionToSalesHeader(TempFieldBuffer,SalesQuoteEntityBuffer,TargetRecordRef);

      if Exists then
        TargetRecordRef.MODIFY(true)
      else
        TargetRecordRef.INSERT(true);
    END;

    PROCEDURE PropagateOnDelete@17(VAR SalesQuoteEntityBuffer@1000 : Record "Sales Quote Entity Buffer");
    VAR
      SalesHeader@1002 : Record "Sales Header";
    BEGIN
      if SalesQuoteEntityBuffer.ISTEMPORARY or (not GraphMgtGeneralTools.IsApiEnabled) then
        exit;

      SalesHeader.GET(SalesHeader."Document Type"::Quote,SalesQuoteEntityBuffer."No.");
      SalesHeader.DELETE(true);
    END;

    PROCEDURE UpdateBufferTableRecords@35();
    VAR
      SalesHeader@1000 : Record "Sales Header";
    BEGIN
      SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Quote);
      if SalesHeader.FINDSET then
        repeat
          InsertOrModifyFromSalesHeader(SalesHeader);
        until SalesHeader.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertOrModifyFromSalesHeader@39(VAR SalesHeader@1000 : Record "Sales Header");
    VAR
      SalesQuoteEntityBuffer@1002 : Record "Sales Quote Entity Buffer";
      RecordExists@1001 : Boolean;
    BEGIN
      SalesQuoteEntityBuffer.LOCKTABLE;
      RecordExists := SalesQuoteEntityBuffer.GET(SalesHeader."No.");

      SalesQuoteEntityBuffer.TRANSFERFIELDS(SalesHeader,true);
      SetStatusOptionFromSalesHeader(SalesHeader,SalesQuoteEntityBuffer);
      AssignTotalsFromSalesHeader(SalesHeader,SalesQuoteEntityBuffer);
      SalesQuoteEntityBuffer.UpdateReferencedRecordIds;

      if RecordExists then
        SalesQuoteEntityBuffer.MODIFY(true)
      else
        SalesQuoteEntityBuffer.INSERT(true);
    END;

    LOCAL PROCEDURE SetStatusOptionFromSalesHeader@4(VAR SalesHeader@1000 : Record "Sales Header";VAR SalesQuoteEntityBuffer@1001 : Record "Sales Quote Entity Buffer");
    BEGIN
      if (not GraphMgtGeneralTools.IsApiEnabled) and (not SalesHeader.ISTEMPORARY) then
        exit;

      if SalesHeader."Quote Accepted" then begin
        SalesQuoteEntityBuffer.Status := SalesQuoteEntityBuffer.Status::Accepted;
        exit;
      end;

      if ((SalesHeader."Quote Valid Until Date" = 0D) or (SalesHeader."Quote Valid Until Date" >= WORKDATE)) and
         (SalesHeader."Quote Sent to Customer" <> 0DT)
      then begin
        SalesQuoteEntityBuffer.Status := SalesQuoteEntityBuffer.Status::Sent;
        exit;
      end;

      SalesQuoteEntityBuffer.Status := SalesQuoteEntityBuffer.Status::Draft;
    END;

    LOCAL PROCEDURE SetStatusOptionToSalesHeader@9(VAR TempFieldBuffer@1001 : TEMPORARY Record "Field Buffer";SalesQuoteEntityBuffer@1000 : Record "Sales Quote Entity Buffer";VAR TargetTableRecRef@1003 : RecordRef);
    VAR
      SalesHeader@1002 : Record "Sales Header";
      TargetFieldRef@1004 : FieldRef;
    BEGIN
      TempFieldBuffer.RESET;
      TempFieldBuffer.SETRANGE("Field ID",SalesQuoteEntityBuffer.FIELDNO(Status));
      if not TempFieldBuffer.FINDFIRST then
        exit;

      case SalesQuoteEntityBuffer.Status of
        SalesQuoteEntityBuffer.Status::Accepted:
          begin
            // "Quote Accepted" = True
            TargetFieldRef := TargetTableRecRef.FIELD(SalesHeader.FIELDNO("Quote Accepted"));
            TargetFieldRef.VALIDATE(true);
          end;
        SalesQuoteEntityBuffer.Status::Sent:
          begin
            // "Quote Accepted" = False
            TargetFieldRef := TargetTableRecRef.FIELD(SalesHeader.FIELDNO("Quote Accepted"));
            TargetFieldRef.VALIDATE(false);
            // "Quote Sent to Customer" = TODAY
            TargetFieldRef := TargetTableRecRef.FIELD(SalesHeader.FIELDNO("Quote Sent to Customer"));
            TargetFieldRef.VALIDATE(CURRENTDATETIME);
          end;
        SalesQuoteEntityBuffer.Status::Draft:
          begin
            // "Quote Accepted" = False
            TargetFieldRef := TargetTableRecRef.FIELD(SalesHeader.FIELDNO("Quote Accepted"));
            TargetFieldRef.VALIDATE(false);
            // "Quote Sent to Customer" = TODAY
            TargetFieldRef := TargetTableRecRef.FIELD(SalesHeader.FIELDNO("Quote Sent to Customer"));
            TargetFieldRef.VALIDATE(0DT);
          end;
      end;
    END;

    LOCAL PROCEDURE AssignTotalsFromSalesHeader@58(VAR SalesHeader@1000 : Record "Sales Header";VAR SalesQuoteEntityBuffer@1001 : Record "Sales Quote Entity Buffer");
    VAR
      SalesLine@1006 : Record "Sales Line";
    BEGIN
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");

      if not SalesLine.FINDFIRST then begin
        BlankTotals(SalesLine."Document No.");
        exit;
      end;

      AssignTotalsFromSalesLine(SalesLine,SalesQuoteEntityBuffer,SalesHeader);
    END;

    LOCAL PROCEDURE AssignTotalsFromSalesLine@76(VAR SalesLine@1000 : Record "Sales Line";VAR SalesQuoteEntityBuffer@1001 : Record "Sales Quote Entity Buffer";VAR SalesHeader@1002 : Record "Sales Header");
    VAR
      TotalSalesLine@1003 : Record "Sales Line";
      DocumentTotals@1004 : Codeunit "Document Totals";
      VATAmount@1005 : Decimal;
    BEGIN
      if SalesLine."VAT Calculation Type" = SalesLine."VAT Calculation Type"::"Sales Tax" then begin
        SalesQuoteEntityBuffer."Discount Applied Before Tax" := true;
        SalesQuoteEntityBuffer."Prices Including VAT" := false;
      end else
        SalesQuoteEntityBuffer."Discount Applied Before Tax" := not SalesHeader."Prices Including VAT";

      DocumentTotals.CalculateSalesTotals(TotalSalesLine,VATAmount,SalesLine);

      SalesQuoteEntityBuffer."Invoice Discount Amount" := TotalSalesLine."Inv. Discount Amount";
      SalesQuoteEntityBuffer."Subtotal Amount" := TotalSalesLine."Line Amount";
      SalesQuoteEntityBuffer.Amount := TotalSalesLine.Amount;
      SalesQuoteEntityBuffer."Total Tax Amount" := VATAmount;
      SalesQuoteEntityBuffer."Amount Including VAT" := TotalSalesLine."Amount Including VAT";
    END;

    LOCAL PROCEDURE BlankTotals@25(DocumentNo@1000 : Code[20]);
    VAR
      SalesQuoteEntityBuffer@1002 : Record "Sales Quote Entity Buffer";
    BEGIN
      if not SalesQuoteEntityBuffer.GET(DocumentNo) then
        exit;

      SalesQuoteEntityBuffer."Invoice Discount Amount" := 0;
      SalesQuoteEntityBuffer."Total Tax Amount" := 0;
      SalesQuoteEntityBuffer."Subtotal Amount" := 0;

      SalesQuoteEntityBuffer.Amount := 0;
      SalesQuoteEntityBuffer."Amount Including VAT" := 0;
      SalesQuoteEntityBuffer.MODIFY;
    END;

    LOCAL PROCEDURE CheckValidRecord@8(VAR SalesHeader@1000 : Record "Sales Header") : Boolean;
    BEGIN
      if SalesHeader.ISTEMPORARY then
        exit(false);

      if SalesHeader."Document Type" <> SalesHeader."Document Type"::Quote then
        exit(false);

      exit(true);
    END;

    LOCAL PROCEDURE CheckValidLineRecord@21(VAR SalesLine@1000 : Record "Sales Line") : Boolean;
    BEGIN
      if SalesLine.ISTEMPORARY or (not GraphMgtGeneralTools.IsApiEnabled) then
        exit(false);

      if SalesLine."Document Type" <> SalesLine."Document Type"::Quote then
        exit(false);

      exit(true);
    END;

    LOCAL PROCEDURE ModifyTotalsSalesLine@12(VAR SalesLine@1000 : Record "Sales Line";RecalculateInvoiceDiscount@1002 : Boolean);
    VAR
      SalesQuoteEntityBuffer@1001 : Record "Sales Quote Entity Buffer";
      SalesHeader@1003 : Record "Sales Header";
    BEGIN
      if not SalesQuoteEntityBuffer.GET(SalesLine."Document No.") then
        exit;

      if not RecalculateInvoiceDiscount then
        exit;

      SalesHeader.GET(SalesLine."Document Type",SalesLine."Document No.");
      AssignTotalsFromSalesLine(SalesLine,SalesQuoteEntityBuffer,SalesHeader);
      SalesQuoteEntityBuffer.MODIFY(true);
    END;

    LOCAL PROCEDURE TransferSalesInvoiceLineAggregateToSalesLine@37(VAR SalesInvoiceLineAggregate@1000 : Record "Sales Invoice Line Aggregate";VAR SalesLine@1001 : Record "Sales Line";VAR TempFieldBuffer@1002 : TEMPORARY Record "Field Buffer");
    VAR
      TypeHelper@1004 : Codeunit "Type Helper";
      SalesLineRecordRef@1003 : RecordRef;
    BEGIN
      SalesLine."Document Type" := SalesLine."Document Type"::Quote;
      SalesLineRecordRef.GETTABLE(SalesLine);

      TypeHelper.TransferFieldsWithValidate(TempFieldBuffer,SalesInvoiceLineAggregate,SalesLineRecordRef);

      SalesLineRecordRef.SETTABLE(SalesLine);
    END;

    PROCEDURE RedistributeInvoiceDiscounts@31(VAR SalesQuoteEntityBuffer@1002 : Record "Sales Quote Entity Buffer");
    VAR
      SalesLine@1001 : Record "Sales Line";
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::Quote);
      SalesLine.SETRANGE("Document No.",SalesQuoteEntityBuffer."No.");
      SalesLine.SETRANGE("Recalculate Invoice Disc.",true);
      if SalesLine.FINDFIRST then
        CODEUNIT.RUN(CODEUNIT::"Sales - Calc Discount By Type",SalesLine);

      SalesQuoteEntityBuffer.FIND;
    END;

    PROCEDURE LoadLines@1(VAR SalesInvoiceLineAggregate@1000 : Record "Sales Invoice Line Aggregate";DocumentIdFilter@1007 : Text);
    VAR
      SalesQuoteEntityBuffer@1004 : Record "Sales Quote Entity Buffer";
    BEGIN
      if DocumentIdFilter = '' then
        ERROR(DocumentIDNotSpecifiedForLinesErr);

      SalesQuoteEntityBuffer.SETFILTER(Id,DocumentIdFilter);
      if not SalesQuoteEntityBuffer.FINDFIRST then
        exit;

      LoadSalesLines(SalesInvoiceLineAggregate,SalesQuoteEntityBuffer);
    END;

    LOCAL PROCEDURE LoadSalesLines@38(VAR SalesInvoiceLineAggregate@1002 : Record "Sales Invoice Line Aggregate";VAR SalesQuoteEntityBuffer@1000 : Record "Sales Quote Entity Buffer");
    VAR
      SalesLine@1001 : Record "Sales Line";
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::Quote);
      SalesLine.SETRANGE("Document No.",SalesQuoteEntityBuffer."No.");

      if SalesLine.FINDSET(false,false) then
        repeat
          TransferFromSalesLine(SalesInvoiceLineAggregate,SalesQuoteEntityBuffer,SalesLine);
          SalesInvoiceLineAggregate.INSERT(true);
        until SalesLine.NEXT = 0;
    END;

    LOCAL PROCEDURE TransferFromSalesLine@40(VAR SalesInvoiceLineAggregate@1002 : Record "Sales Invoice Line Aggregate";VAR SalesQuoteEntityBuffer@1000 : Record "Sales Quote Entity Buffer";VAR SalesLine@1001 : Record "Sales Line");
    VAR
      SalesInvoiceAggregator@1003 : Codeunit "Sales Invoice Aggregator";
    BEGIN
      CLEAR(SalesInvoiceLineAggregate);
      SalesInvoiceLineAggregate.TRANSFERFIELDS(SalesLine,true);
      SalesInvoiceLineAggregate."Document Id" := SalesQuoteEntityBuffer.Id;
      SalesInvoiceAggregator.SetTaxGroupIdAndCode(
        SalesInvoiceLineAggregate,
        SalesLine."Tax Group Code",
        SalesLine."VAT Prod. Posting Group",
        SalesLine."VAT Identifier");
      SalesInvoiceLineAggregate."VAT %" := SalesLine."VAT %";
      SalesInvoiceLineAggregate."Tax Amount" := SalesLine."Amount Including VAT" - SalesLine."VAT Base Amount";
      SalesInvoiceLineAggregate.UpdateReferencedRecordIds;
      UpdateLineAmountsFromSalesLine(SalesInvoiceLineAggregate,SalesLine);
    END;

    PROCEDURE PropagateInsertLine@7(VAR SalesInvoiceLineAggregate@1000 : Record "Sales Invoice Line Aggregate";VAR TempFieldBuffer@1005 : TEMPORARY Record "Field Buffer");
    VAR
      SalesQuoteEntityBuffer@1001 : Record "Sales Quote Entity Buffer";
      SalesLine@1002 : Record "Sales Line";
      LastUsedSalesLine@1004 : Record "Sales Line";
    BEGIN
      VerifyCRUDIsPossibleForLine(SalesInvoiceLineAggregate,SalesQuoteEntityBuffer);

      SalesLine."Document Type" := SalesLine."Document Type"::Quote;
      SalesLine."Document No." := SalesQuoteEntityBuffer."No.";

      if SalesInvoiceLineAggregate."Line No." = 0 then begin
        LastUsedSalesLine.SETRANGE("Document Type",SalesLine."Document Type"::Quote);
        LastUsedSalesLine.SETRANGE("Document No.",SalesQuoteEntityBuffer."No.");
        if LastUsedSalesLine.FINDLAST then
          SalesInvoiceLineAggregate."Line No." := LastUsedSalesLine."Line No." + 10000
        else
          SalesInvoiceLineAggregate."Line No." := 10000;

        SalesLine."Line No." := SalesInvoiceLineAggregate."Line No.";
      end else
        if SalesLine.GET(SalesLine."Document Type"::Quote,SalesQuoteEntityBuffer."No.",SalesInvoiceLineAggregate."Line No.") then
          ERROR(CannotInsertALineThatAlreadyExistsErr);

      TransferSalesInvoiceLineAggregateToSalesLine(SalesInvoiceLineAggregate,SalesLine,TempFieldBuffer);
      SalesLine.INSERT(true);

      if not SkipUpdateDiscounts then
        RedistributeInvoiceDiscounts(SalesQuoteEntityBuffer);

      SalesLine.FIND;
      TransferFromSalesLine(SalesInvoiceLineAggregate,SalesQuoteEntityBuffer,SalesLine);
    END;

    PROCEDURE PropagateModifyLine@24(VAR SalesInvoiceLineAggregate@1000 : Record "Sales Invoice Line Aggregate";VAR TempFieldBuffer@1004 : TEMPORARY Record "Field Buffer");
    VAR
      SalesQuoteEntityBuffer@1001 : Record "Sales Quote Entity Buffer";
      SalesLine@1002 : Record "Sales Line";
    BEGIN
      VerifyCRUDIsPossibleForLine(SalesInvoiceLineAggregate,SalesQuoteEntityBuffer);

      if not SalesLine.GET(SalesLine."Document Type"::Quote,SalesQuoteEntityBuffer."No.",SalesInvoiceLineAggregate."Line No.") then
        ERROR(CannotModifyALineThatDoesntExistErr);

      TransferSalesInvoiceLineAggregateToSalesLine(SalesInvoiceLineAggregate,SalesLine,TempFieldBuffer);

      SalesLine.MODIFY(true);

      if not SkipUpdateDiscounts then
        RedistributeInvoiceDiscounts(SalesQuoteEntityBuffer);

      SalesLine.FIND;
      TransferFromSalesLine(SalesInvoiceLineAggregate,SalesQuoteEntityBuffer,SalesLine);
    END;

    PROCEDURE PropagateDeleteLine@6(VAR SalesInvoiceLineAggregate@1000 : Record "Sales Invoice Line Aggregate");
    VAR
      SalesQuoteEntityBuffer@1003 : Record "Sales Quote Entity Buffer";
      SalesLine@1002 : Record "Sales Line";
    BEGIN
      VerifyCRUDIsPossibleForLine(SalesInvoiceLineAggregate,SalesQuoteEntityBuffer);

      if SalesLine.GET(SalesLine."Document Type"::Quote,SalesQuoteEntityBuffer."No.",SalesInvoiceLineAggregate."Line No.") then begin
        SalesLine.DELETE(true);
        if not SkipUpdateDiscounts then
          RedistributeInvoiceDiscounts(SalesQuoteEntityBuffer);
      end;
    END;

    PROCEDURE PropagateMultipleLinesUpdate@65(VAR TempNewSalesInvoiceLineAggregate@1000 : TEMPORARY Record "Sales Invoice Line Aggregate");
    VAR
      TempCurrentSalesInvoiceLineAggregate@1005 : TEMPORARY Record "Sales Invoice Line Aggregate";
      SalesQuoteEntityBuffer@1001 : Record "Sales Quote Entity Buffer";
      SalesLine@1002 : Record "Sales Line";
      TempAllFieldBuffer@1003 : TEMPORARY Record "Field Buffer";
      NativeEDMTypes@1004 : Codeunit "Native - EDM Types";
    BEGIN
      VerifyCRUDIsPossibleForLine(TempNewSalesInvoiceLineAggregate,SalesQuoteEntityBuffer);
      NativeEDMTypes.GetFieldSetBufferWithAllFieldsSet(TempAllFieldBuffer);

      if not TempNewSalesInvoiceLineAggregate.FINDFIRST then begin
        SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::Quote);
        SalesLine.SETRANGE("Document No.",SalesQuoteEntityBuffer."No.");
        SalesLine.DELETEALL(true);
        exit;
      end;

      LoadLines(TempCurrentSalesInvoiceLineAggregate,SalesQuoteEntityBuffer.Id);

      SkipUpdateDiscounts := true;

      // Remove deleted lines
      if TempCurrentSalesInvoiceLineAggregate.FINDSET(true,false) then
        repeat
          if not TempNewSalesInvoiceLineAggregate.GET(SalesQuoteEntityBuffer.Id,TempCurrentSalesInvoiceLineAggregate."Line No.") then
            PropagateDeleteLine(TempCurrentSalesInvoiceLineAggregate);
        until TempCurrentSalesInvoiceLineAggregate.NEXT = 0;

      // Update Lines
      TempNewSalesInvoiceLineAggregate.FINDFIRST;

      repeat
        if not TempCurrentSalesInvoiceLineAggregate.GET(
             TempNewSalesInvoiceLineAggregate."Document Id",TempNewSalesInvoiceLineAggregate."Line No.")
        then
          PropagateInsertLine(TempNewSalesInvoiceLineAggregate,TempAllFieldBuffer)
        else
          PropagateModifyLine(TempNewSalesInvoiceLineAggregate,TempAllFieldBuffer);
      until TempNewSalesInvoiceLineAggregate.NEXT = 0;

      SalesQuoteEntityBuffer.FIND;
    END;

    LOCAL PROCEDURE VerifyCRUDIsPossibleForLine@60(VAR SalesInvoiceLineAggregate@1001 : Record "Sales Invoice Line Aggregate";VAR SalesQuoteEntityBuffer@1000 : Record "Sales Quote Entity Buffer");
    VAR
      SearchSalesQuoteEntityBuffer@1002 : Record "Sales Quote Entity Buffer";
      DocumentIDFilter@1003 : Text;
    BEGIN
      if ISNULLGUID(SalesInvoiceLineAggregate."Document Id") then begin
        DocumentIDFilter := SalesInvoiceLineAggregate.GETFILTER("Document Id");
        if DocumentIDFilter = '' then
          ERROR(DocumentIDNotSpecifiedForLinesErr);
        SalesQuoteEntityBuffer.SETFILTER(Id,DocumentIDFilter);
      end else
        SalesQuoteEntityBuffer.SETRANGE(Id,SalesInvoiceLineAggregate."Document Id");

      if not SalesQuoteEntityBuffer.FINDFIRST then
        ERROR(DocumentDoesNotExistErr);

      SearchSalesQuoteEntityBuffer.COPY(SalesQuoteEntityBuffer);
      if SearchSalesQuoteEntityBuffer.NEXT <> 0 then
        ERROR(MultipleDocumentsFoundForIdErr);
    END;

    LOCAL PROCEDURE UpdateLineAmountsFromSalesLine@44(VAR SalesInvoiceLineAggregate@1000 : Record "Sales Invoice Line Aggregate";VAR SalesLine@1001 : Record "Sales Line");
    BEGIN
      SalesInvoiceLineAggregate."Line Amount Excluding Tax" := SalesLine.GetLineAmountExclVAT;
      SalesInvoiceLineAggregate."Line Amount Including Tax" := SalesLine.GetLineAmountInclVAT;
      SalesInvoiceLineAggregate."Line Tax Amount" :=
        SalesInvoiceLineAggregate."Line Amount Including Tax" - SalesInvoiceLineAggregate."Line Amount Excluding Tax";
      UpdateInvoiceDiscountAmount(SalesInvoiceLineAggregate);
    END;

    LOCAL PROCEDURE UpdateInvoiceDiscountAmount@48(VAR SalesInvoiceLineAggregate@1000 : Record "Sales Invoice Line Aggregate");
    BEGIN
      if SalesInvoiceLineAggregate."Prices Including Tax" then
        SalesInvoiceLineAggregate."Inv. Discount Amount Excl. VAT" :=
          SalesInvoiceLineAggregate."Line Amount Excluding Tax" - SalesInvoiceLineAggregate.Amount
      else
        SalesInvoiceLineAggregate."Inv. Discount Amount Excl. VAT" := SalesInvoiceLineAggregate."Inv. Discount Amount";
    END;

    BEGIN
    END.
  }
}

