OBJECT Codeunit 1233 SEPA DD-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=20150915D;
    Time=120000T;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo="Direct Debit Collection Entry";
    OnRun=BEGIN
            DeletePaymentFileErrors;
            CheckCollectionEntry(Rec);
          END;

  }
  CODE
  {
    VAR
      EuroCurrErr@1007 : TextConst 'ENU=Only transactions in euro (EUR) are allowed.';
      FieldBlankErr@1006 : TextConst '@@@="%1=field name, %2=table name. Example: Name must have a value in Customer.";ENU=%1 must have a value in %2.';
      FieldKeyBlankErr@1004 : TextConst '@@@="%1=field name, %2= table name, %3=key field value. Example: Name must have a value in Customer 10000.";ENU=%1 must have a value in %2 %3.';
      MustBeCustomerErr@1001 : TextConst 'ENU=The customer %1 does not exist.';
      MustBePositiveErr@1000 : TextConst 'ENU=The amount must be positive.';
      NotActiveMandateErr@1010 : TextConst 'ENU=The mandate %1 is not active.';
      PartnerTypeErr@1008 : TextConst '@@@="%1 = Partner Type; %2 = Company/Person; %3 = Company/Person.";ENU=The customer''s %1, %2, must be equal to the %1, %3, specified in the collection.';
      TransferDateErr@1002 : TextConst 'ENU=The earliest possible transfer date is today.';
      SelectedRecordTxt@1003 : TextConst 'ENU=the currently selected record';
      PartnerTypeBlankErr@1103 : TextConst '@@@=Partner Type must be filled.;ENU=%1 must be filled.';

    LOCAL PROCEDURE CheckCollectionEntry@2(DirectDebitCollectionEntry@1000 : Record "Direct Debit Collection Entry");
    VAR
      Customer@1004 : Record Customer;
      CustomerBankAccount@1003 : Record "Customer Bank Account";
      DirectDebitCollection@1005 : Record "Direct Debit Collection";
      GLSetup@1002 : Record "General Ledger Setup";
      SEPADirectDebitMandate@1001 : Record "SEPA Direct Debit Mandate";
    BEGIN
      GLSetup.GET;
      with DirectDebitCollectionEntry do begin
        if "Transfer Amount" <= 0 then
          InsertPaymentFileError(MustBePositiveErr);

        if "Currency Code" <> GLSetup.GetCurrencyCode('EUR') then
          InsertPaymentFileError(EuroCurrErr);

        if "Transfer Date" < TODAY then
          InsertPaymentFileError(TransferDateErr);

        if not Customer.GET("Customer No.") then begin
          InsertPaymentFileError(STRSUBSTNO(MustBeCustomerErr,"Customer No."));
          exit;
        end;

        if Customer.Name = '' then
          AddFieldEmptyError(DirectDebitCollectionEntry,Customer.TABLECAPTION,Customer.FIELDCAPTION(Name),"Customer No.");

        DirectDebitCollection.GET("Direct Debit Collection No.");
        if Customer."Partner Type" <> DirectDebitCollection."Partner Type" then
          InsertPaymentFileError(STRSUBSTNO(PartnerTypeErr,Customer.FIELDCAPTION("Partner Type"),Customer."Partner Type",
              DirectDebitCollection."Partner Type"));

        if DirectDebitCollection."Partner Type" = DirectDebitCollection."Partner Type"::" " then
          InsertPaymentFileError(STRSUBSTNO(PartnerTypeBlankErr,DirectDebitCollection.FIELDCAPTION("Partner Type")));

        if "Mandate ID" = '' then
          AddFieldEmptyError(DirectDebitCollectionEntry,SelectedRecordTxt,FIELDCAPTION("Mandate ID"),"Mandate ID")
        else begin
          SEPADirectDebitMandate.GET("Mandate ID");
          if SEPADirectDebitMandate."Date of Signature" = 0D then
            AddFieldEmptyError(
              DirectDebitCollectionEntry,SEPADirectDebitMandate.TABLECAPTION,SEPADirectDebitMandate.FIELDCAPTION("Date of Signature"),
              "Mandate ID");
          if not SEPADirectDebitMandate.IsMandateActive("Transfer Date") then
            InsertPaymentFileError(STRSUBSTNO(NotActiveMandateErr,"Mandate ID"));

          if SEPADirectDebitMandate."Customer Bank Account Code" = '' then
            AddFieldEmptyError(
              DirectDebitCollectionEntry,SEPADirectDebitMandate.TABLECAPTION,
              SEPADirectDebitMandate.FIELDCAPTION("Customer Bank Account Code"),SEPADirectDebitMandate.ID)
          else begin
            CustomerBankAccount.GET(Customer."No.",SEPADirectDebitMandate."Customer Bank Account Code");
            if CustomerBankAccount."SWIFT Code" = '' then
              AddFieldEmptyError(
                DirectDebitCollectionEntry,CustomerBankAccount.TABLECAPTION,CustomerBankAccount.FIELDCAPTION("SWIFT Code"),
                CustomerBankAccount.Code);
            if CustomerBankAccount.IBAN = '' then
              AddFieldEmptyError(
                DirectDebitCollectionEntry,CustomerBankAccount.TABLECAPTION,CustomerBankAccount.FIELDCAPTION(IBAN),
                CustomerBankAccount.Code);
          end;
        end;
      end;
    END;

    LOCAL PROCEDURE AddFieldEmptyError@1(VAR DirectDebitCollectionEntry@1002 : Record "Direct Debit Collection Entry";TableCaption@1000 : Text;FieldCaption@1001 : Text;KeyValue@1003 : Text);
    VAR
      ErrorText@1004 : Text;
    BEGIN
      if KeyValue = '' then
        ErrorText := STRSUBSTNO(FieldBlankErr,FieldCaption,TableCaption)
      else
        ErrorText := STRSUBSTNO(FieldKeyBlankErr,FieldCaption,TableCaption,KeyValue);
      DirectDebitCollectionEntry.InsertPaymentFileError(ErrorText);
    END;

    BEGIN
    END.
  }
}

