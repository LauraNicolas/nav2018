OBJECT Page 5492 Item Categories Entity
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=itemCategories];
    SourceTable="Item Category";
    DelayedInsert=true;
    PageType=API;
    OnInsertRecord=VAR
                     ItemCategory@1003 : Record "Item Category";
                     GraphMgtGeneralTools@1001 : Codeunit "Graph Mgt - General Tools";
                     RecRef@1000 : RecordRef;
                   BEGIN
                     ItemCategory.SETRANGE(Code,Code);
                     if not ItemCategory.ISEMPTY then
                       INSERT;

                     INSERT(true);

                     RecRef.GETTABLE(Rec);
                     GraphMgtGeneralTools.ProcessNewRecordFromAPI(RecRef,TempFieldSet,CURRENTDATETIME);
                     RecRef.SETTABLE(Rec);

                     MODIFY(true);
                     exit(false);
                   END;

    OnModifyRecord=VAR
                     ItemCategory@1001 : Record "Item Category";
                     GraphMgtGeneralTools@1000 : Codeunit "Graph Mgt - General Tools";
                   BEGIN
                     if xRec.Id <> Id then
                       GraphMgtGeneralTools.ErrorIdImmutable;
                     ItemCategory.SETRANGE(Id,Id);
                     ItemCategory.FINDFIRST;

                     if Code = ItemCategory.Code then
                       MODIFY(true)
                     else begin
                       ItemCategory.TRANSFERFIELDS(Rec,false);
                       ItemCategory.RENAME(Code);
                       TRANSFERFIELDS(ItemCategory);
                     end;
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id];
                ApplicationArea=#All;
                SourceExpr=Id;
                ImplicitType=GUID }

    { 4   ;2   ;Field     ;
                Name=code;
                CaptionML=[@@@={Locked};
                           ENU=Code];
                ApplicationArea=#All;
                SourceExpr=Code;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Code));
                           END;

                ImplicitType=Code20 }

    { 5   ;2   ;Field     ;
                Name=displayName;
                CaptionML=[@@@={Locked};
                           ENU=Description];
                ApplicationArea=#All;
                SourceExpr=Description;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Description));
                           END;

                ImplicitType=Text50 }

    { 6   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time";
                ImplicitType=DateTime }

  }
  CODE
  {
    VAR
      TempFieldSet@1000 : TEMPORARY Record Field;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      if TempFieldSet.GET(DATABASE::"Item Category",FieldNo) then
        exit;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::"Item Category";
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(true);
    END;

    BEGIN
    END.
  }
}

