OBJECT Table 1225 Data Exch. Field Mapping
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Column No.");

               if IsValidToUseMultiplier and (Multiplier = 0) then
                 VALIDATE(Multiplier,1);
             END;

    OnModify=BEGIN
               if IsValidToUseMultiplier and (Multiplier = 0) then
                 VALIDATE(Multiplier,1);
             END;

    CaptionML=ENU=Data Exch. Field Mapping;
  }
  FIELDS
  {
    { 1   ;   ;Data Exch. Def Code ;Code20        ;TableRelation="Data Exch. Def".Code;
                                                   CaptionML=ENU=Data Exch. Def Code;
                                                   NotBlank=true }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation="Data Exch. Mapping"."Table ID";
                                                   CaptionML=ENU=Table ID;
                                                   NotBlank=true }
    { 3   ;   ;Column No.          ;Integer       ;TableRelation="Data Exch. Column Def"."Column No." WHERE ("Data Exch. Def Code"=FIELD("Data Exch. Def Code"),
                                                                                                             "Data Exch. Line Def Code"=FIELD("Data Exch. Line Def Code"));
                                                   CaptionML=ENU=Column No.;
                                                   NotBlank=true }
    { 4   ;   ;Field ID            ;Integer       ;TableRelation=Field."No." WHERE (TableNo=FIELD("Table ID"));
                                                   CaptionML=ENU=Field ID }
    { 5   ;   ;Optional            ;Boolean       ;CaptionML=ENU=Optional }
    { 6   ;   ;Use Default Value   ;Boolean       ;OnValidate=BEGIN
                                                                if not "Use Default Value" then
                                                                  "Default Value" := '';
                                                              END;

                                                   CaptionML=ENU=Use Default Value }
    { 7   ;   ;Default Value       ;Text250       ;OnValidate=BEGIN
                                                                VALIDATE("Use Default Value",true);
                                                              END;

                                                   CaptionML=ENU=Default Value }
    { 8   ;   ;Data Exch. Line Def Code;Code20    ;TableRelation="Data Exch. Line Def".Code WHERE ("Data Exch. Def Code"=FIELD("Data Exch. Def Code"));
                                                   CaptionML=ENU=Data Exch. Line Def Code;
                                                   NotBlank=true }
    { 9   ;   ;Multiplier          ;Decimal       ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                if IsValidToUseMultiplier and (Multiplier = 0) then
                                                                  ERROR(ZeroNotAllowedErr);
                                                              END;

                                                   CaptionML=ENU=Multiplier }
    { 10  ;   ;Target Table ID     ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE ("Object Type"=CONST(Table));
                                                   CaptionML=ENU=Target Table ID }
    { 11  ;   ;Target Field ID     ;Integer       ;TableRelation=Field."No." WHERE (TableNo=FIELD("Target Table ID"));
                                                   OnLookup=VAR
                                                              Field@1001 : Record Field;
                                                              FieldsLookup@1000 : Page "Fields Lookup";
                                                            BEGIN
                                                              Field.SETRANGE(TableNo,"Target Table ID");
                                                              FieldsLookup.SETTABLEVIEW(Field);
                                                              FieldsLookup.LOOKUPMODE(true);
                                                              if FieldsLookup.RUNMODAL = ACTION::LookupOK then begin
                                                                FieldsLookup.GETRECORD(Field);
                                                                VALIDATE("Target Field ID",Field."No.");
                                                              end;
                                                            END;

                                                   CaptionML=ENU=Target Field ID }
    { 12  ;   ;Target Table Caption;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Table Metadata".Caption WHERE (ID=FIELD("Target Table ID")));
                                                   CaptionML=ENU=Target Table Caption }
    { 13  ;   ;Target Field Caption;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD("Target Table ID"),
                                                                                                   "No."=FIELD("Target Field ID")));
                                                   CaptionML=ENU=Target Field Caption }
    { 20  ;   ;Transformation Rule ;Code20        ;TableRelation="Transformation Rule";
                                                   CaptionML=ENU=Transformation Rule }
    { 21  ;   ;Overwrite Value     ;Boolean       ;CaptionML=ENU=Overwrite Value }
  }
  KEYS
  {
    {    ;"Data Exch. Def Code","Data Exch. Line Def Code","Table ID","Column No.","Field ID";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ZeroNotAllowedErr@1000 : TextConst 'ENU=All numeric values are allowed except zero.';

    [External]
    PROCEDURE InsertRec@1(DataExchDefCode@1003 : Code[20];DataExchLineDefCode@1005 : Code[20];TableId@1000 : Integer;ColumnNo@1001 : Integer;FieldId@1002 : Integer;NewOptional@1004 : Boolean;NewMultiplier@1006 : Decimal);
    BEGIN
      INIT;
      VALIDATE("Data Exch. Def Code",DataExchDefCode);
      VALIDATE("Data Exch. Line Def Code",DataExchLineDefCode);
      "Table ID" := TableId;
      "Column No." := ColumnNo;
      "Field ID" := FieldId;
      VALIDATE(Optional,NewOptional);
      VALIDATE(Multiplier,NewMultiplier);
      INSERT;
    END;

    [External]
    PROCEDURE FillSourceRecord@2(Field@1000 : Record Field);
    BEGIN
      SETRANGE("Field ID");
      INIT;

      "Table ID" := Field.TableNo;
      "Field ID" := Field."No.";
    END;

    [External]
    PROCEDURE GetColumnCaption@7() : Text;
    VAR
      DataExchColDef@1000 : Record "Data Exch. Column Def";
    BEGIN
      DataExchColDef.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
      DataExchColDef.SETRANGE("Data Exch. Line Def Code","Data Exch. Line Def Code");
      DataExchColDef.SETRANGE("Column No.","Column No.");
      if DataExchColDef.FINDFIRST then
        exit(DataExchColDef.Name);
      exit('');
    END;

    [Internal]
    PROCEDURE GetFieldCaption@12() : Text;
    VAR
      recRef@1000 : RecordRef;
      fieldRef@1001 : FieldRef;
    BEGIN
      recRef.OPEN("Table ID");
      fieldRef := recRef.FIELD("Field ID");
      exit(fieldRef.CAPTION);
    END;

    LOCAL PROCEDURE IsValidToUseMultiplier@8() : Boolean;
    VAR
      DataExchDef@1000 : Record "Data Exch. Def";
      DataExchColumnDef@1001 : Record "Data Exch. Column Def";
    BEGIN
      DataExchDef.GET("Data Exch. Def Code");
      if DataExchColumnDef.GET("Data Exch. Def Code","Data Exch. Line Def Code","Column No.") then
        exit(DataExchColumnDef."Data Type" = DataExchColumnDef."Data Type"::Decimal);
      exit(false);
    END;

    [External]
    PROCEDURE GetPath@3() : Text;
    VAR
      DataExchColDef@1000 : Record "Data Exch. Column Def";
    BEGIN
      DataExchColDef.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
      DataExchColDef.SETRANGE("Data Exch. Line Def Code","Data Exch. Line Def Code");
      DataExchColDef.SETRANGE("Column No.","Column No.");
      if DataExchColDef.FINDFIRST then
        exit(DataExchColDef.Path);
      exit('');
    END;

    BEGIN
    END.
  }
}

