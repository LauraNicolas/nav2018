OBJECT Codeunit 1261 Imp. SEPA CAMT Bank Rec. Lines
{
  OBJECT-PROPERTIES
  {
    Date=20150915D;
    Time=120000T;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    TableNo="Bank Acc. Reconciliation Line";
    OnRun=VAR
            DataExch@1002 : Record "Data Exch.";
            ProcessDataExch@1001 : Codeunit "Process Data Exch.";
            RecRef@1000 : RecordRef;
          BEGIN
            DataExch.GET("Data Exch. Entry No.");
            RecRef.GETTABLE(Rec);
            PreProcess(Rec);
            ProcessDataExch.ProcessAllLinesColumnMapping(DataExch,RecRef);
            PostProcess(Rec)
          END;

  }
  CODE
  {
    VAR
      StatementIDTxt@1000 : TextConst '@@@={Locked};ENU=/Document/BkToCstmrStmt/Stmt/Id';
      IBANTxt@1001 : TextConst '@@@={Locked};ENU=/Document/BkToCstmrStmt/Stmt/Acct/Id/IBAN';
      CurrencyTxt@1002 : TextConst '@@@={Locked};ENU=/Document/BkToCstmrStmt/Stmt/Bal/Amt[@Ccy]';
      BalTypeTxt@1006 : TextConst '@@@={Locked};ENU=/Document/BkToCstmrStmt/Stmt/Bal/Tp/CdOrPrtry/Cd';
      ClosingBalTxt@1005 : TextConst '@@@={Locked};ENU=/Document/BkToCstmrStmt/Stmt/Bal/Amt';
      StatementDateTxt@1004 : TextConst '@@@={Locked};ENU=/Document/BkToCstmrStmt/Stmt/CreDtTm';
      CrdDbtIndTxt@1003 : TextConst '@@@={Locked};ENU=/Document/BkToCstmrStmt/Stmt/Bal/CdtDbtInd';

    LOCAL PROCEDURE PreProcess@1(BankAccReconciliationLine@1000 : Record "Bank Acc. Reconciliation Line");
    VAR
      DataExch@1001 : Record "Data Exch.";
      PrePostProcessXMLImport@1002 : Codeunit "Pre & Post Process XML Import";
    BEGIN
      DataExch.GET(BankAccReconciliationLine."Data Exch. Entry No.");
      PrePostProcessXMLImport.PreProcessFile(DataExch,StatementIDTxt);
      PrePostProcessXMLImport.PreProcessBankAccount(DataExch,BankAccReconciliationLine."Bank Account No.",IBANTxt,CurrencyTxt);
    END;

    LOCAL PROCEDURE PostProcess@2(BankAccReconciliationLine@1000 : Record "Bank Acc. Reconciliation Line");
    VAR
      DataExch@1001 : Record "Data Exch.";
      BankAccReconciliation@1002 : Record "Bank Acc. Reconciliation";
      PrePostProcessXMLImport@1004 : Codeunit "Pre & Post Process XML Import";
      RecRef@1003 : RecordRef;
    BEGIN
      DataExch.GET(BankAccReconciliationLine."Data Exch. Entry No.");
      BankAccReconciliation.GET(
        BankAccReconciliationLine."Statement Type",
        BankAccReconciliationLine."Bank Account No.",
        BankAccReconciliationLine."Statement No.");

      RecRef.GETTABLE(BankAccReconciliation);
      PrePostProcessXMLImport.PostProcessStatementEndingBalance(DataExch,RecRef,
        BankAccReconciliation.FIELDNO("Statement Ending Balance"),'CLBD',BalTypeTxt,ClosingBalTxt,CrdDbtIndTxt,4);
      PrePostProcessXMLImport.PostProcessStatementDate(DataExch,RecRef,BankAccReconciliation.FIELDNO("Statement Date"),
        StatementDateTxt);
    END;

    BEGIN
    END.
  }
}

