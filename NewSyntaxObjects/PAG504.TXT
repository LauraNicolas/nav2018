OBJECT Page 504 Available - Item Ledg. Entries
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Item Ledger Entry"=rm;
    Editable=false;
    CaptionML=ENU=Available - Item Ledg. Entries;
    InsertAllowed=false;
    DeleteAllowed=false;
    ModifyAllowed=false;
    LinksAllowed=false;
    SourceTable="Item Ledger Entry";
    DataCaptionExpr=CaptionText;
    SourceTableView=SORTING("Item No.",Open);
    PageType=List;
    OnOpenPage=BEGIN
                 ReservEntry.TESTFIELD("Source Type");

                 RESET;
                 SETRANGE("Item No.",ReservEntry."Item No.");
                 SETRANGE("Variant Code",ReservEntry."Variant Code");
                 SETRANGE("Location Code",ReservEntry."Location Code");
                 SETRANGE("Drop Shipment",false);
                 SETRANGE(Open,true);
                 if ReservMgt.FieldFilterNeeded2(ReservEntry,ReservMgt.IsPositive,0) then
                   SETFILTER("Lot No.",ReservMgt.GetFieldFilter);
                 if ReservMgt.FieldFilterNeeded2(ReservEntry,ReservMgt.IsPositive,1) then
                   SETFILTER("Serial No.",ReservMgt.GetFieldFilter);
                 if ReservMgt.IsPositive then begin
                   SETRANGE(Positive,true);
                   SETFILTER("Remaining Quantity",'>0');
                 end else begin
                   SETRANGE(Positive,false);
                   SETFILTER("Remaining Quantity",'<0');
                 end;
               END;

    OnAfterGetRecord=BEGIN
                       ReservMgt.ItemLedgEntryUpdateValues(Rec,QtyToReserve,QtyReservedThisLine);
                     END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      Name=ActionContainer1900000003;
                      ActionContainerType=RelatedInformation }
      { 50      ;1   ;ActionGroup;
                      Name=Ent&ry;
                      CaptionML=ENU=Ent&ry;
                      Image=Entry }
      { 51      ;2   ;Action    ;
                      Name=Dimensions;
                      AccessByPermission=TableData Dimension=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=ENU=Dimensions;
                      ToolTipML=ENU=View or edit dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                      ApplicationArea=#Dimensions;
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      Name=ActionContainer1900000004;
                      ActionContainerType=ActionItems }
      { 22      ;1   ;ActionGroup;
                      Name=F&unctions;
                      CaptionML=ENU=F&unctions;
                      Image=Action }
      { 23      ;2   ;Action    ;
                      Name=Reserve;
                      CaptionML=ENU=&Reserve;
                      ToolTipML=ENU=Reserve the quantity that is required on the document line that you opened this window for.;
                      ApplicationArea=#Advanced;
                      Image=Reserve;
                      OnAction=VAR
                                 NewQtyReservedThisLine2@1000 : Decimal;
                               BEGIN
                                 ReservEntry.LOCKTABLE;
                                 UpdateReservMgt;
                                 ReservMgt.ItemLedgEntryUpdateValues(Rec,QtyToReserve,QtyReservedThisLine);
                                 ReservMgt.CalculateRemainingQty(NewQtyReservedThisLine2,NewQtyReservedThisLine);
                                 if MaxQtyDefined and (ABS(MaxQtyToReserve) < ABS(NewQtyReservedThisLine)) then
                                   NewQtyReservedThisLine := MaxQtyToReserve;

                                 ReservMgt.CopySign(NewQtyReservedThisLine,QtyToReserve);
                                 if NewQtyReservedThisLine <> 0 then begin
                                   if ABS(NewQtyReservedThisLine) > ABS(QtyToReserve) then begin
                                     CreateReservation(QtyToReserve);
                                     MaxQtyToReserve := MaxQtyToReserve - QtyToReserve;
                                   end else begin
                                     CreateReservation(NewQtyReservedThisLine);
                                     MaxQtyToReserve := MaxQtyToReserve - NewQtyReservedThisLine;
                                   end;
                                   if MaxQtyToReserve < 0 then
                                     MaxQtyToReserve := 0;
                                 end else
                                   ERROR(Text000);
                               END;
                                }
      { 24      ;2   ;Action    ;
                      Name=CancelReservation;
                      AccessByPermission=TableData Item=R;
                      CaptionML=ENU=&Cancel Reservation;
                      ToolTipML=ENU=Cancel the reservation that exists for the document line that you opened this window for.;
                      ApplicationArea=#Advanced;
                      Image=Cancel;
                      OnAction=BEGIN
                                 if not CONFIRM(Text001,false) then
                                   exit;

                                 ReservMgt.ItemLedgEntryUpdateValues(Rec,QtyToReserve,QtyReservedThisLine);

                                 ReservEntry2.COPY(ReservEntry);
                                 if ReservMgt.IsPositive then
                                   ReserveItemLedgEntry.FilterReservFor(ReservEntry2,Rec)
                                 else
                                   ERROR(Text99000000);
                                 ReservEntry2.SETRANGE("Expected Receipt Date");
                                 if ReservEntry2.FIND('-') then begin
                                   UpdateReservMgt;
                                   repeat
                                     ReservEngineMgt.CancelReservation(ReservEntry2);
                                   until ReservEntry2.NEXT = 0;

                                   TotalAvailQty := TotalAvailQty + QtyReservedThisLine;
                                   MaxQtyToReserve := MaxQtyToReserve + QtyReservedThisLine;
                                   UpdateReservFrom;
                                 end;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Name=Entry Type;
                ToolTipML=ENU=Specifies which type of transaction that the entry is created from.;
                ApplicationArea=#Advanced;
                SourceExpr="Entry Type";
                ImplicitType=Option }

    { 4   ;2   ;Field     ;
                Name=Document No.;
                ToolTipML=ENU=Specifies the document number on the entry. The document is the voucher that the entry was based on, for example, a receipt.;
                ApplicationArea=#Advanced;
                SourceExpr="Document No.";
                ImplicitType=Code20 }

    { 21  ;2   ;Field     ;
                Name=Serial No.;
                ToolTipML=ENU=Specifies a serial number if the posted item carries such a number.;
                ApplicationArea=#ItemTracking;
                SourceExpr="Serial No.";
                Visible=FALSE;
                ImplicitType=Code20 }

    { 26  ;2   ;Field     ;
                Name=Lot No.;
                ToolTipML=ENU=Specifies a lot number if the posted item carries such a number.;
                ApplicationArea=#ItemTracking;
                SourceExpr="Lot No.";
                Visible=FALSE;
                ImplicitType=Code20 }

    { 6   ;2   ;Field     ;
                Name=Location Code;
                ToolTipML=ENU=Specifies the code for the location that the entry is linked to.;
                ApplicationArea=#Advanced;
                SourceExpr="Location Code";
                ImplicitType=Code10 }

    { 8   ;2   ;Field     ;
                Name=Posting Date;
                ToolTipML=ENU=Specifies the entry's posting date.;
                ApplicationArea=#Advanced;
                SourceExpr="Posting Date";
                ImplicitType=Date }

    { 10  ;2   ;Field     ;
                Name=Remaining Quantity;
                ToolTipML=ENU=Specifies the quantity in the Quantity field that remains to be processed.;
                ApplicationArea=#Advanced;
                SourceExpr="Remaining Quantity";
                ImplicitType=Decimal }

    { 12  ;2   ;Field     ;
                Name=Reserved Quantity;
                ToolTipML=ENU=Specifies how many units of the item on the line have been reserved.;
                ApplicationArea=#Advanced;
                SourceExpr="Reserved Quantity";
                Editable=FALSE;
                ImplicitType=Decimal }

    { 17  ;2   ;Field     ;
                Name=QtyToReserve;
                CaptionML=ENU=Available Quantity;
                ToolTipML=ENU=Specifies the quantity of the item that is available for reservation.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=QtyToReserve;
                Editable=FALSE;
                ImplicitType=Decimal }

    { 19  ;2   ;Field     ;
                Name=ReservedThisLine;
                CaptionML=ENU=Current Reserved Quantity;
                ToolTipML=ENU=Specifies the quantity of the item that is reserved from the item ledger entry, for the current line or entry.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=ReservedThisLine;
                OnDrillDown=BEGIN
                              ReservEntry2.RESET;
                              ReserveItemLedgEntry.FilterReservFor(ReservEntry2,Rec);
                              ReservEntry2.SETRANGE("Reservation Status",ReservEntry2."Reservation Status"::Reservation);
                              ReservMgt.MarkReservConnection(ReservEntry2,ReservEntry);
                              PAGE.RUNMODAL(PAGE::"Reservation Entries",ReservEntry2);
                              UpdateReservFrom;
                              CurrPage.UPDATE;
                            END;
                             }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Fully reserved.';
      Text001@1001 : TextConst 'ENU=Do you want to cancel the reservation?';
      Text002@1031 : TextConst 'ENU=Reservation cannot be carried out because the available quantity is already allocated in a warehouse.';
      Text99000000@1002 : TextConst 'ENU=You can only cancel reservations to inventory.';
      ReservEntry@1003 : Record "Reservation Entry";
      ReservEntry2@1004 : Record "Reservation Entry";
      SalesLine@1005 : Record "Sales Line";
      PurchLine@1006 : Record "Purchase Line";
      ReqLine@1008 : Record "Requisition Line";
      ProdOrderLine@1009 : Record "Prod. Order Line";
      ProdOrderComp@1010 : Record "Prod. Order Component";
      PlanningComponent@1011 : Record "Planning Component";
      TransLine@1012 : Record "Transfer Line";
      ServiceLine@1013 : Record "Service Line";
      JobPlanningLine@1035 : Record "Job Planning Line";
      AssemblyLine@1036 : Record "Assembly Line";
      AssemblyHeader@1037 : Record "Assembly Header";
      ReservMgt@1014 : Codeunit "Reservation Management";
      ReservEngineMgt@1015 : Codeunit "Reservation Engine Mgt.";
      ReserveSalesLine@1016 : Codeunit "Sales Line-Reserve";
      ReserveReqLine@1017 : Codeunit "Req. Line-Reserve";
      ReservePurchLine@1018 : Codeunit "Purch. Line-Reserve";
      ReserveProdOrderLine@1020 : Codeunit "Prod. Order Line-Reserve";
      ReserveProdOrderComp@1021 : Codeunit "Prod. Order Comp.-Reserve";
      ReservePlanningComponent@1022 : Codeunit "Plng. Component-Reserve";
      ReserveItemLedgEntry@1023 : Codeunit "Item Ledger Entry-Reserve";
      ReserveTransLine@1024 : Codeunit "Transfer Line-Reserve";
      ReserveServiceLine@1025 : Codeunit "Service Line-Reserve";
      JobPlanningLineReserve@1034 : Codeunit "Job Planning Line-Reserve";
      AssemblyLineReserve@1038 : Codeunit "Assembly Line-Reserve";
      AssemblyHeaderReserve@1039 : Codeunit "Assembly Header-Reserve";
      QtyToReserve@1026 : Decimal;
      QtyReservedThisLine@1027 : Decimal;
      NewQtyReservedThisLine@1028 : Decimal;
      TotalAvailQty@1030 : Decimal;
      MaxQtyToReserve@1032 : Decimal;
      MaxQtyDefined@1033 : Boolean;
      CaptionText@1029 : Text[80];

    [External]
    PROCEDURE SetSalesLine@24(VAR CurrentSalesLine@1000 : Record "Sales Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentSalesLine.TESTFIELD(Type,CurrentSalesLine.Type::Item);
      SalesLine := CurrentSalesLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetSalesLine(SalesLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveSalesLine.FilterReservFor(ReservEntry,SalesLine);
      CaptionText := ReserveSalesLine.Caption(SalesLine);
    END;

    [External]
    PROCEDURE SetReqLine@23(VAR CurrentReqLine@1000 : Record "Requisition Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      ReqLine := CurrentReqLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetReqLine(ReqLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveReqLine.FilterReservFor(ReservEntry,ReqLine);
      CaptionText := ReserveReqLine.Caption(ReqLine);
    END;

    [External]
    PROCEDURE SetPurchLine@22(VAR CurrentPurchLine@1000 : Record "Purchase Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentPurchLine.TESTFIELD(Type,CurrentPurchLine.Type::Item);
      PurchLine := CurrentPurchLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetPurchLine(PurchLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReservePurchLine.FilterReservFor(ReservEntry,PurchLine);
      CaptionText := ReservePurchLine.Caption(PurchLine);
    END;

    [External]
    PROCEDURE SetProdOrderLine@19(VAR CurrentProdOrderLine@1000 : Record "Prod. Order Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      ProdOrderLine := CurrentProdOrderLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetProdOrderLine(ProdOrderLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveProdOrderLine.FilterReservFor(ReservEntry,ProdOrderLine);
      CaptionText := ReserveProdOrderLine.Caption(ProdOrderLine);
    END;

    [External]
    PROCEDURE SetProdOrderComponent@18(VAR CurrentProdOrderComp@1000 : Record "Prod. Order Component";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      ProdOrderComp := CurrentProdOrderComp;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetProdOrderComponent(ProdOrderComp);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveProdOrderComp.FilterReservFor(ReservEntry,ProdOrderComp);
      CaptionText := ReserveProdOrderComp.Caption(ProdOrderComp);
    END;

    [External]
    PROCEDURE SetPlanningComponent@15(VAR CurrentPlanningComponent@1000 : Record "Planning Component";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      PlanningComponent := CurrentPlanningComponent;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetPlanningComponent(PlanningComponent);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReservePlanningComponent.FilterReservFor(ReservEntry,PlanningComponent);
      CaptionText := ReservePlanningComponent.Caption(PlanningComponent);
    END;

    [External]
    PROCEDURE SetTransferLine@16(VAR CurrentTransLine@1000 : Record "Transfer Line";CurrentReservEntry@1001 : Record "Reservation Entry";Direction@1002 : 'Outbound,Inbound');
    BEGIN
      TransLine := CurrentTransLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetTransferLine(TransLine,Direction);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveTransLine.FilterReservFor(ReservEntry,TransLine,Direction);
      CaptionText := ReserveTransLine.Caption(TransLine);
    END;

    [External]
    PROCEDURE SetServiceLine@8(VAR CurrentServiceLine@1000 : Record "Service Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentServiceLine.TESTFIELD(Type,CurrentServiceLine.Type::Item);
      ServiceLine := CurrentServiceLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetServLine(ServiceLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      ReserveServiceLine.FilterReservFor(ReservEntry,ServiceLine);
      CaptionText := ReserveServiceLine.Caption(ServiceLine);
    END;

    [External]
    PROCEDURE SetJobPlanningLine@5(VAR CurrentJobPlanningLine@1000 : Record "Job Planning Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentJobPlanningLine.TESTFIELD(Type,CurrentJobPlanningLine.Type::Item);
      JobPlanningLine := CurrentJobPlanningLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetJobPlanningLine(JobPlanningLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      JobPlanningLineReserve.FilterReservFor(ReservEntry,JobPlanningLine);
      CaptionText := JobPlanningLineReserve.Caption(JobPlanningLine);
    END;

    [External]
    PROCEDURE SetTotalAvailQty@1(TotalAvailQty2@1000 : Decimal);
    BEGIN
      TotalAvailQty := TotalAvailQty2;
    END;

    [External]
    PROCEDURE SetMaxQtyToReserve@3(NewMaxQtyToReserve@1000 : Decimal);
    BEGIN
      MaxQtyToReserve := NewMaxQtyToReserve;
      MaxQtyDefined := true;
    END;

    [External]
    PROCEDURE SetAssemblyLine@4(VAR CurrentAsmLine@1002 : Record "Assembly Line";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      CurrentAsmLine.TESTFIELD(Type,CurrentAsmLine.Type::Item);
      AssemblyLine := CurrentAsmLine;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetAssemblyLine(AssemblyLine);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      AssemblyLineReserve.FilterReservFor(ReservEntry,AssemblyLine);
      CaptionText := AssemblyLineReserve.Caption(AssemblyLine);
    END;

    [External]
    PROCEDURE SetAssemblyHeader@7(VAR CurrentAsmHeader@1000 : Record "Assembly Header";CurrentReservEntry@1001 : Record "Reservation Entry");
    BEGIN
      AssemblyHeader := CurrentAsmHeader;
      ReservEntry := CurrentReservEntry;

      CLEAR(ReservMgt);
      ReservMgt.SetAssemblyHeader(AssemblyHeader);
      ReservEngineMgt.InitFilterAndSortingFor(ReservEntry,true);
      AssemblyHeaderReserve.FilterReservFor(ReservEntry,AssemblyHeader);
      CaptionText := AssemblyHeaderReserve.Caption(AssemblyHeader);
    END;

    LOCAL PROCEDURE CreateReservation@14(VAR ReserveQuantity@1000 : Decimal);
    VAR
      TrackingSpecification@1001 : Record "Tracking Specification";
    BEGIN
      TESTFIELD("Drop Shipment",false);
      TESTFIELD("Item No.",ReservEntry."Item No.");
      TESTFIELD("Variant Code",ReservEntry."Variant Code");
      TESTFIELD("Location Code",ReservEntry."Location Code");

      if TotalAvailQty < 0 then begin
        ReserveQuantity := 0;
        exit;
      end;

      if TotalAvailQty < ReserveQuantity then
        ReserveQuantity := TotalAvailQty;
      TotalAvailQty := TotalAvailQty - ReserveQuantity;

      if (TotalAvailQty = 0) and
         (ReserveQuantity = 0) and
         (QtyToReserve <> 0)
      then
        ERROR(Text002);

      UpdateReservMgt;
      TrackingSpecification.InitTrackingSpecification(
        DATABASE::"Item Ledger Entry",0,'','',0,"Entry No.",
        "Variant Code","Location Code","Serial No.","Lot No.","Qty. per Unit of Measure");
      ReservMgt.CreateReservation(
        ReservEntry.Description,0D,0,ReserveQuantity,TrackingSpecification);
      UpdateReservFrom;
    END;

    LOCAL PROCEDURE UpdateReservFrom@17();
    BEGIN
      case ReservEntry."Source Type" of
        DATABASE::"Sales Line":
          begin
            SalesLine.FIND;
            SetSalesLine(SalesLine,ReservEntry);
          end;
        DATABASE::"Requisition Line":
          begin
            ReqLine.FIND;
            SetReqLine(ReqLine,ReservEntry);
          end;
        DATABASE::"Purchase Line":
          begin
            PurchLine.FIND;
            SetPurchLine(PurchLine,ReservEntry);
          end;
        DATABASE::"Prod. Order Line":
          begin
            ProdOrderLine.FIND;
            SetProdOrderLine(ProdOrderLine,ReservEntry);
          end;
        DATABASE::"Prod. Order Component":
          begin
            ProdOrderComp.FIND;
            SetProdOrderComponent(ProdOrderComp,ReservEntry);
          end;
        DATABASE::"Planning Component":
          begin
            PlanningComponent.FIND;
            SetPlanningComponent(PlanningComponent,ReservEntry);
          end;
        DATABASE::"Transfer Line":
          begin
            TransLine.FIND;
            SetTransferLine(TransLine,ReservEntry,ReservEntry."Source Subtype");
          end;
        DATABASE::"Service Line":
          begin
            ServiceLine.FIND;
            SetServiceLine(ServiceLine,ReservEntry);
          end;
        DATABASE::"Job Planning Line":
          begin
            JobPlanningLine.FIND;
            SetJobPlanningLine(JobPlanningLine,ReservEntry);
          end;
        DATABASE::"Assembly Line":
          begin
            AssemblyLine.FIND;
            SetAssemblyLine(AssemblyLine,ReservEntry);
          end;
        DATABASE::"Assembly Header":
          begin
            AssemblyHeader.FIND;
            SetAssemblyHeader(AssemblyHeader,ReservEntry);
          end;
      end;
    END;

    LOCAL PROCEDURE UpdateReservMgt@13();
    BEGIN
      CLEAR(ReservMgt);
      case ReservEntry."Source Type" of
        DATABASE::"Sales Line":
          ReservMgt.SetSalesLine(SalesLine);
        DATABASE::"Requisition Line":
          ReservMgt.SetReqLine(ReqLine);
        DATABASE::"Purchase Line":
          ReservMgt.SetPurchLine(PurchLine);
        DATABASE::"Prod. Order Line":
          ReservMgt.SetProdOrderLine(ProdOrderLine);
        DATABASE::"Prod. Order Component":
          ReservMgt.SetProdOrderComponent(ProdOrderComp);
        DATABASE::"Planning Component":
          ReservMgt.SetPlanningComponent(PlanningComponent);
        DATABASE::"Transfer Line":
          ReservMgt.SetTransferLine(TransLine,ReservEntry."Source Subtype");
        DATABASE::"Service Line":
          ReservMgt.SetServLine(ServiceLine);
        DATABASE::"Job Planning Line":
          ReservMgt.SetJobPlanningLine(JobPlanningLine);
        DATABASE::"Assembly Line":
          ReservMgt.SetAssemblyLine(AssemblyLine);
        DATABASE::"Assembly Header":
          ReservMgt.SetAssemblyHeader(AssemblyHeader);
      end;
      ReservMgt.SetSerialLotNo(ReservEntry."Serial No.",ReservEntry."Lot No.");
    END;

    LOCAL PROCEDURE ReservedThisLine@2() : Decimal;
    BEGIN
      ReservEntry2.RESET;
      ReserveItemLedgEntry.FilterReservFor(ReservEntry2,Rec);
      ReservEntry2.SETRANGE("Reservation Status",ReservEntry2."Reservation Status"::Reservation);
      exit(ReservMgt.MarkReservConnection(ReservEntry2,ReservEntry));
    END;

    BEGIN
    END.
  }
}

