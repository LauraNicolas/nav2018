OBJECT Codeunit 143 ECSL Report Validate
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="VAT Report Header";
    OnRun=BEGIN
            ErrorMessage.SetContext(RECORDID);
            ErrorMessage.ClearLog;

            if not IsPeriodValid then begin
              ErrorMessage.LogMessage(Rec,FIELDNO("No."),ErrorMessage."Message Type"::Error,InvalidPeriodErr);
              exit;
            end;

            ValidateVATReportHasLine(Rec);
            CheckVATRegNoOnLines(Rec);
            CheckZeroAmountOnLines(Rec);
          END;

  }
  CODE
  {
    VAR
      NoLineFoundErr@1000 : TextConst 'ENU=You cannot release a blank VAT report.';
      ErrorMessage@1042 : Record "Error Message";
      ZeroTotalValueErr@1040 : TextConst '@@@=Placeholder 1 holds the line no that cause the error;ENU=You cannot release the report because line No. %1 has zero as the Total Value Of Supplies.';
      NoVatRegNoErr@1041 : TextConst '@@@=Placeholder 1 holds the line no that cause the error;ENU=You cannot release the report because line No. %1 is missing a VAT Registration Number. You need to correct this on the sales document and post it again.';
      InvalidPeriodErr@1043 : TextConst 'ENU=The period is not valid.';

    LOCAL PROCEDURE ValidateVATReportHasLine@7(VATReportHeader@1040 : Record "VAT Report Header");
    VAR
      ECSLVATReportLine@1041 : Record "ECSL VAT Report Line";
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      if ECSLVATReportLine.COUNT = 0 then
        ErrorMessage.LogMessage(VATReportHeader,VATReportHeader.FIELDNO("No."),ErrorMessage."Message Type"::Error,NoLineFoundErr);
    END;

    LOCAL PROCEDURE CheckVATRegNoOnLines@1040(VATReportHeader@1040 : Record "VAT Report Header");
    VAR
      ECSLVATReportLine@1041 : Record "ECSL VAT Report Line";
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      ECSLVATReportLine.SETRANGE("Customer VAT Reg. No.",'');
      if ECSLVATReportLine.FINDSET then
        repeat
          ErrorMessage.LogMessage(ECSLVATReportLine,ECSLVATReportLine.FIELDNO("Customer VAT Reg. No."),
            ErrorMessage."Message Type"::Error,STRSUBSTNO(NoVatRegNoErr,ECSLVATReportLine."Line No."));
        until ECSLVATReportLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckZeroAmountOnLines@1041(VATReportHeader@1040 : Record "VAT Report Header");
    VAR
      ECSLVATReportLine@1041 : Record "ECSL VAT Report Line";
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      ECSLVATReportLine.SETRANGE("Total Value Of Supplies",0);

      if ECSLVATReportLine.FINDSET then
        repeat
          ErrorMessage.LogMessage(ECSLVATReportLine,ECSLVATReportLine.FIELDNO("Total Value Of Supplies"),
            ErrorMessage."Message Type"::Error,STRSUBSTNO(ZeroTotalValueErr,ECSLVATReportLine."Line No."));
        until ECSLVATReportLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

