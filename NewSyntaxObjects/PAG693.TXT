OBJECT Page 693 Program Selection
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Program Selection;
    InsertAllowed=false;
    DeleteAllowed=false;
    SourceTable="Send-To Program";
    DataCaptionExpr=STRSUBSTNO(Text001,AllObjWithCaption."Object Caption",Name);
    PageType=List;
    OnAfterGetRecord=BEGIN
                       GetDefaultStylesheet;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           GetDefaultStylesheet;
                         END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Group1;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Name=Name;
                CaptionML=ENU=Name;
                ToolTipML=ENU=Specifies the name of the program to send data to from Dynamics 365.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Name;
                Editable=FALSE;
                ImplicitType=Text250 }

    { 4   ;2   ;Field     ;
                Name=Executable;
                CaptionML=ENU=Executable;
                ToolTipML=ENU=Specifies the name of the executable file that launches the program.;
                ApplicationArea=#Advanced;
                SourceExpr=Executable;
                Visible=FALSE;
                Editable=FALSE;
                ImplicitType=Text250 }

    { 9   ;2   ;Field     ;
                Name=StylesheetName;
                CaptionML=ENU=Style Sheet;
                ToolTipML=ENU=Specifies the style sheet for the program to send data to from Dynamics 365.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=StylesheetName;
                OnValidate=BEGIN
                             ValidateStylesheet;
                             StylesheetNameOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           LookupStylesheet;
                         END;

                ImplicitType=Text250 }

    { 1900000007;0;Container;
                Name=Container1900000007;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Name=Part1900383207;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Name=Part1905767507;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      AllObjWithCaption@1004 : Record AllObjWithCaption;
      StylesheetName@1000 : Text[250];
      ObjType@1001 : Integer;
      ObjectID@1002 : Integer;
      StylesheetID@1003 : GUID;
      Text001@1005 : TextConst 'ENU=Send %1 to %2';

    [External]
    PROCEDURE SetObjectID@9(NewObjectType@1000 : Integer;NewObjectID@1001 : Integer);
    BEGIN
      ObjType := NewObjectType;
      ObjectID := NewObjectID;
      if not AllObjWithCaption.GET(AllObjWithCaption."Object Type"::Page,NewObjectID) then
        AllObjWithCaption.INIT;
    END;

    [External]
    PROCEDURE GetSelectedStyleSheetID@1() : GUID;
    BEGIN
      GetDefaultStylesheet;
      exit(StylesheetID);
    END;

    LOCAL PROCEDURE LookupStylesheet@2();
    VAR
      Stylesheet@1000 : Record "Style Sheet";
      Stylesheets@1001 : Page "Style Sheets";
    BEGIN
      Stylesheet.SETRANGE("Program ID","Program ID");
      Stylesheet.SETRANGE("Object Type",ObjType);
      Stylesheet.SETFILTER("Object ID",'%1|%2',0,ObjectID);
      if StylesheetName <> '' then begin
        Stylesheet.SETRANGE(Name,StylesheetName);
        if Stylesheet.FINDFIRST then
          Stylesheets.SETRECORD(Stylesheet);
        Stylesheet.SETRANGE(Name);
      end;
      Stylesheets.SetParams(ObjectID,Name);
      Stylesheets.LOOKUPMODE := true;
      Stylesheets.SETTABLEVIEW(Stylesheet);
      if Stylesheets.RUNMODAL = ACTION::LookupOK then begin
        Stylesheets.GETRECORD(Stylesheet);
        SetDefaultStylesheet(Stylesheet);
      end;
    END;

    LOCAL PROCEDURE ValidateStylesheet@3();
    VAR
      Stylesheet@1000 : Record "Style Sheet";
    BEGIN
      Stylesheet.SETRANGE("Program ID","Program ID");
      Stylesheet.SETRANGE("Object Type",ObjType);
      Stylesheet.SETFILTER("Object ID",'%1|%2',0,ObjectID);
      Stylesheet.SETRANGE(Name,StylesheetName);
      if not Stylesheet.FINDFIRST then begin
        Stylesheet.SETFILTER(Name,'@*' + StylesheetName + '*');
        Stylesheet.FINDFIRST
      end;
      SetDefaultStylesheet(Stylesheet);
    END;

    LOCAL PROCEDURE GetDefaultStylesheet@5();
    VAR
      UserDefaultStylesheet@1000 : Record "User Default Style Sheet";
      Stylesheet@1001 : Record "Style Sheet";
      Found@1002 : Boolean;
    BEGIN
      if UserDefaultStylesheet.GET(UPPERCASE(USERID),ObjType,ObjectID,"Program ID") then
        Found := Stylesheet.GET(UserDefaultStylesheet."Style Sheet ID");

      if not Found then begin
        Stylesheet.SETRANGE("Object ID",ObjectID);
        Stylesheet.SETRANGE("Object Type",ObjType);
        Stylesheet.SETRANGE("Program ID","Program ID");
        Found := Stylesheet.FINDFIRST;
        if not Found then begin
          Stylesheet.SETRANGE("Object ID",0);
          Found := Stylesheet.FINDFIRST;
        end;
      end;
      if Found then begin
        StylesheetID := Stylesheet."Style Sheet ID";
        StylesheetName := Stylesheet.Name;
      end else begin
        CLEAR(StylesheetID);
        StylesheetName := '';
      end;
    END;

    LOCAL PROCEDURE SetDefaultStylesheet@10(VAR Stylesheet@1000 : Record "Style Sheet");
    VAR
      UserDefaultStylesheet@1001 : Record "User Default Style Sheet";
    BEGIN
      StylesheetID := Stylesheet."Style Sheet ID";
      StylesheetName := Stylesheet.Name;

      UserDefaultStylesheet.SETRANGE("User ID",UPPERCASE(USERID));
      UserDefaultStylesheet.SETRANGE("Object Type",Stylesheet."Object Type");
      UserDefaultStylesheet.SETRANGE("Object ID",ObjectID);
      UserDefaultStylesheet.SETRANGE("Program ID",Stylesheet."Program ID");
      UserDefaultStylesheet.DELETEALL;

      UserDefaultStylesheet."User ID" := UPPERCASE(USERID);
      UserDefaultStylesheet."Object Type" := Stylesheet."Object Type";
      UserDefaultStylesheet."Object ID" := ObjectID;
      UserDefaultStylesheet."Program ID" := Stylesheet."Program ID";
      UserDefaultStylesheet."Style Sheet ID" := Stylesheet."Style Sheet ID";
      UserDefaultStylesheet.INSERT;
    END;

    LOCAL PROCEDURE StylesheetNameOnAfterValidate@19058654();
    BEGIN
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

