OBJECT Table 9178 Application Area Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataPerCompany=false;
    CaptionML=ENU=Application Area Setup;
  }
  FIELDS
  {
    { 1   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company Name }
    { 2   ;   ;Profile ID          ;Code30        ;TableRelation=Profile;
                                                   CaptionML=ENU=Profile ID }
    { 3   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   CaptionML=ENU=User ID }
    { 40  ;   ;Invoicing           ;Boolean       ;CaptionML=ENU=Invoicing }
    { 100 ;   ;Basic               ;Boolean       ;CaptionML=ENU=Basic }
    { 200 ;   ;Suite               ;Boolean       ;CaptionML=ENU=Suite }
    { 300 ;   ;Relationship Mgmt   ;Boolean       ;CaptionML=ENU=Relationship Mgmt }
    { 400 ;   ;Jobs                ;Boolean       ;CaptionML=ENU=Jobs }
    { 500 ;   ;Fixed Assets        ;Boolean       ;CaptionML=ENU=Fixed Assets }
    { 600 ;   ;Location            ;Boolean       ;CaptionML=ENU=Location }
    { 700 ;   ;BasicHR             ;Boolean       ;CaptionML=ENU=BasicHR }
    { 800 ;   ;Assembly            ;Boolean       ;CaptionML=ENU=Assembly }
    { 900 ;   ;Item Charges        ;Boolean       ;CaptionML=ENU=Item Charges }
    { 1000;   ;Advanced            ;Boolean       ;CaptionML=ENU=Advanced }
    { 1100;   ;Warehouse           ;Boolean       ;CaptionML=ENU=Warehouse }
    { 1200;   ;Service             ;Boolean       ;CaptionML=ENU=Service }
    { 1300;   ;Manufacturing       ;Boolean       ;CaptionML=ENU=Manufacturing }
    { 1400;   ;Planning            ;Boolean       ;CaptionML=ENU=Planning }
    { 1500;   ;Dimensions          ;Boolean       ;CaptionML=ENU=Dimensions }
    { 1600;   ;Item Tracking       ;Boolean       ;CaptionML=ENU=Item Tracking }
    { 1700;   ;Intercompany        ;Boolean       ;CaptionML=ENU=Intercompany }
    { 1800;   ;Sales Return Order  ;Boolean       ;CaptionML=ENU=Sales Return Order }
    { 1900;   ;Purch Return Order  ;Boolean       ;CaptionML=ENU=Purch Return Order }
    { 2000;   ;Prepayments         ;Boolean       ;CaptionML=ENU=Prepayments }
    { 2100;   ;Cost Accounting     ;Boolean       ;CaptionML=ENU=Cost Accounting }
    { 2200;   ;Sales Budget        ;Boolean       ;CaptionML=ENU=Sales Budget }
    { 2300;   ;Purchase Budget     ;Boolean       ;CaptionML=ENU=Purchase Budget }
    { 2400;   ;Item Budget         ;Boolean       ;CaptionML=ENU=Item Budget }
    { 2500;   ;Sales Analysis      ;Boolean       ;CaptionML=ENU=Sales Analysis }
    { 2600;   ;Purchase Analysis   ;Boolean       ;CaptionML=ENU=Purchase Analysis }
    { 2700;   ;XBRL                ;Boolean       ;CaptionML=ENU=XBRL }
  }
  KEYS
  {
    {    ;"Company Name","Profile ID","User ID"   ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ValuesNotAllowedErr@1000 : TextConst 'ENU=" The selected experience tier is not supported.\\In the Application Area window, you define what is shown in the user interface."';

    LOCAL PROCEDURE GetApplicationAreaSetupRec@3(VAR ApplicationAreaSetup@1000 : Record "Application Area Setup") : Boolean;
    VAR
      ConfPersonalizationMgt@1001 : Codeunit "Conf./Personalization Mgt.";
    BEGIN
      if not ApplicationAreaSetup.GET('','',USERID) then
        if not ApplicationAreaSetup.GET('',ConfPersonalizationMgt.GetCurrentProfileIDNoError) then
          exit(GetApplicationAreaSetupRecFromCompany(ApplicationAreaSetup));
      exit(true);
    END;

    LOCAL PROCEDURE GetApplicationAreaSetupRecFromCompany@13(VAR ApplicationAreaSetup@1000 : Record "Application Area Setup") : Boolean;
    BEGIN
      if not ApplicationAreaSetup.GET(COMPANYNAME) then
        exit(ApplicationAreaSetup.GET);
      exit(true);
    END;

    [Internal]
    PROCEDURE GetApplicationAreaSetup@66() ApplicationAreas : Text;
    VAR
      ApplicationAreaSetup@1003 : Record "Application Area Setup";
      RecRef@1002 : RecordRef;
      FieldRef@1005 : FieldRef;
      FieldIndex@1004 : Integer;
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(ApplicationAreas);

      RecRef.GETTABLE(ApplicationAreaSetup);

      for FieldIndex := 1 to RecRef.FIELDCOUNT do begin
        FieldRef := RecRef.FIELDINDEX(FieldIndex);
        if not IsInPrimaryKey(FieldRef) then
          if FieldRef.VALUE then
            if ApplicationAreas = '' then
              ApplicationAreas := '#' + DELCHR(FieldRef.NAME)
            else
              ApplicationAreas := ApplicationAreas + ',#' + DELCHR(FieldRef.NAME);
      end;
    END;

    [Internal]
    PROCEDURE GetApplicationAreaBuffer@2(VAR TempApplicationAreaBuffer@1000 : TEMPORARY Record "Application Area Buffer");
    VAR
      ApplicationAreaSetup@1003 : Record "Application Area Setup";
      RecRef@1002 : RecordRef;
      FieldRef@1005 : FieldRef;
      FieldIndex@1004 : Integer;
    BEGIN
      GetApplicationAreaSetupRec(ApplicationAreaSetup);
      RecRef.GETTABLE(ApplicationAreaSetup);

      for FieldIndex := GetFirstPublicAppAreaFieldIndex to RecRef.FIELDCOUNT do begin
        FieldRef := RecRef.FIELDINDEX(FieldIndex);
        if not IsInPrimaryKey(FieldRef) then begin
          TempApplicationAreaBuffer.INIT;
          TempApplicationAreaBuffer."Field No." := FieldRef.NUMBER;
          TempApplicationAreaBuffer."Application Area" := FieldRef.CAPTION;
          TempApplicationAreaBuffer.Selected := FieldRef.VALUE;
          TempApplicationAreaBuffer.INSERT(true);
        end;
      end;
    END;

    [External]
    PROCEDURE IsFoundationEnabled@4() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Basic or ApplicationAreaSetup.Suite);
    END;

    [External]
    PROCEDURE IsBasicOnlyEnabled@14() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Basic and not ApplicationAreaSetup.Suite and not ApplicationAreaSetup.Advanced);
    END;

    [External]
    PROCEDURE IsAdvanced@6() : Boolean;
    BEGIN
      exit(not IsFoundationEnabled);
    END;

    [External]
    PROCEDURE IsFixedAssetEnabled@22() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup."Fixed Assets");
    END;

    [External]
    PROCEDURE IsJobsEnabled@15() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Jobs);
    END;

    [External]
    PROCEDURE IsBasicHREnabled@19() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.BasicHR);
    END;

    [External]
    PROCEDURE IsDimensionEnabled@37() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Dimensions);
    END;

    [External]
    PROCEDURE IsLocationEnabled@18() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Location);
    END;

    [External]
    PROCEDURE IsAssemblyEnabled@27() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Assembly);
    END;

    [External]
    PROCEDURE IsItemChargesEnabled@28() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup."Item Charges");
    END;

    [External]
    PROCEDURE IsItemTrackingEnabled@36() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup."Item Tracking");
    END;

    [External]
    PROCEDURE IsIntercompanyEnabled@38() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Intercompany);
    END;

    [External]
    PROCEDURE IsSalesReturnOrderEnabled@31() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup."Sales Return Order");
    END;

    [External]
    PROCEDURE IsPurchaseReturnOrderEnabled@40() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup."Purch Return Order");
    END;

    [External]
    PROCEDURE IsCostAccountingEnabled@41() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup."Cost Accounting");
    END;

    [External]
    PROCEDURE IsInvoicingOnlyEnabled@29() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(
        ApplicationAreaSetup.Invoicing and
        not ApplicationAreaSetup.Basic and
        not ApplicationAreaSetup.Suite and
        not ApplicationAreaSetup.Advanced and
        not ApplicationAreaSetup."Relationship Mgmt" and
        not ApplicationAreaSetup.Jobs and
        not ApplicationAreaSetup."Fixed Assets" and
        not ApplicationAreaSetup.Location and
        not ApplicationAreaSetup.BasicHR and
        not ApplicationAreaSetup.Assembly and
        not ApplicationAreaSetup.Dimensions and
        not ApplicationAreaSetup."Item Charges" and
        not ApplicationAreaSetup."Item Tracking" and
        not ApplicationAreaSetup."Cost Accounting" and
        not ApplicationAreaSetup.Manufacturing and
        not ApplicationAreaSetup.Planning and
        not ApplicationAreaSetup.Service and
        not ApplicationAreaSetup.Warehouse and
        not ApplicationAreaSetup.Intercompany and
        not ApplicationAreaSetup.Prepayments);
    END;

    [External]
    PROCEDURE IsManufacturingEnabled@32() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Manufacturing);
    END;

    [External]
    PROCEDURE IsPlanningEnabled@33() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Planning);
    END;

    [External]
    PROCEDURE IsRelationshipMgmtEnabled@39() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup."Relationship Mgmt");
    END;

    [External]
    PROCEDURE IsServiceEnabled@34() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Service);
    END;

    [External]
    PROCEDURE IsWarehouseEnabled@35() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Warehouse);
    END;

    [External]
    PROCEDURE IsSuiteEnabled@16() : Boolean;
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);

      exit(ApplicationAreaSetup.Suite);
    END;

    [External]
    PROCEDURE IsAllDisabled@17() : Boolean;
    BEGIN
      exit(not IsAnyEnabled);
    END;

    LOCAL PROCEDURE IsAnyEnabled@21() : Boolean;
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRec(ApplicationAreaSetup) then
        exit(false);
      exit(SelectedAppAreaCount(ApplicationAreaSetup) > 0);
    END;

    PROCEDURE IsAdvancedSaaSEnabled@30() : Boolean;
    VAR
      PermissionManager@1001 : Codeunit "Permission Manager";
    BEGIN
      exit(PermissionManager.IsSandboxConfiguration);
    END;

    LOCAL PROCEDURE SelectedAppAreaCount@25(ApplicationAreaSetup@1000 : Record "Application Area Setup") : Integer;
    VAR
      RecRef@1003 : RecordRef;
      FieldRef@1002 : FieldRef;
      FieldIndex@1001 : Integer;
      Count@1004 : Integer;
    BEGIN
      RecRef.GETTABLE(ApplicationAreaSetup);

      for FieldIndex := GetFirstPublicAppAreaFieldIndex to RecRef.FIELDCOUNT do begin
        FieldRef := RecRef.FIELDINDEX(FieldIndex);
        if not IsInPrimaryKey(FieldRef) then
          if FieldRef.VALUE then
            Count += 1;
      end;
      exit(Count);
    END;

    LOCAL PROCEDURE SaveApplicationArea@5(VAR TempApplicationAreaBuffer@1000 : TEMPORARY Record "Application Area Buffer";ApplicationAreaSetup@1006 : Record "Application Area Setup";NoApplicationAreasExist@1007 : Boolean);
    VAR
      ExistingTempApplicationAreaBuffer@1009 : TEMPORARY Record "Application Area Buffer";
      UserPreference@1010 : Record "User Preference";
      RecRef@1002 : RecordRef;
      FieldRef@1005 : FieldRef;
      ApplicationAreasChanged@1001 : Boolean;
    BEGIN
      GetApplicationAreaBuffer(ExistingTempApplicationAreaBuffer);
      RecRef.GETTABLE(ApplicationAreaSetup);

      TempApplicationAreaBuffer.FINDFIRST;
      ExistingTempApplicationAreaBuffer.FINDFIRST;
      repeat
        FieldRef := RecRef.FIELD(TempApplicationAreaBuffer."Field No.");
        FieldRef.VALUE := TempApplicationAreaBuffer.Selected;
        if TempApplicationAreaBuffer.Selected <> ExistingTempApplicationAreaBuffer.Selected then
          ApplicationAreasChanged := true;
      until (TempApplicationAreaBuffer.NEXT = 0) and (ExistingTempApplicationAreaBuffer.NEXT = 0);

      if NoApplicationAreasExist then begin
        if ApplicationAreasChanged then
          RecRef.INSERT(true);
      end else
        RecRef.MODIFY(true);

      UserPreference.SETFILTER("User ID",USERID);
      UserPreference.DELETEALL;

      SetupApplicationArea;
    END;

    LOCAL PROCEDURE TrySaveApplicationArea@8(VAR TempApplicationAreaBuffer@1000 : TEMPORARY Record "Application Area Buffer";ApplicationAreaSetup@1003 : Record "Application Area Setup";NoApplicationAreaExist@1002 : Boolean) IsApplicationAreaChanged : Boolean;
    VAR
      OldApplicationArea@1001 : Text;
    BEGIN
      OldApplicationArea := APPLICATIONAREA;
      SaveApplicationArea(TempApplicationAreaBuffer,ApplicationAreaSetup,NoApplicationAreaExist);
      IsApplicationAreaChanged := OldApplicationArea <> APPLICATIONAREA;
    END;

    [Internal]
    PROCEDURE TrySaveApplicationAreaCurrentCompany@11(VAR TempApplicationAreaBuffer@1000 : TEMPORARY Record "Application Area Buffer") IsApplicationAreaChanged : Boolean;
    VAR
      ApplicationAreaSetup@1003 : Record "Application Area Setup";
      NoCompanyApplicationAreasExist@1002 : Boolean;
    BEGIN
      if not ApplicationAreaSetup.GET(COMPANYNAME) then begin
        ApplicationAreaSetup."Company Name" := COMPANYNAME;
        NoCompanyApplicationAreasExist := true;
      end;

      IsApplicationAreaChanged :=
        TrySaveApplicationArea(TempApplicationAreaBuffer,ApplicationAreaSetup,NoCompanyApplicationAreasExist);
    END;

    [Internal]
    PROCEDURE TrySaveApplicationAreaCurrentUser@12(VAR TempApplicationAreaBuffer@1000 : TEMPORARY Record "Application Area Buffer") IsApplicationAreaChanged : Boolean;
    VAR
      ApplicationAreaSetup@1003 : Record "Application Area Setup";
      NoUserApplicationAreasExist@1002 : Boolean;
    BEGIN
      if not ApplicationAreaSetup.GET('','',USERID) then begin
        ApplicationAreaSetup."User ID" := USERID;
        NoUserApplicationAreasExist := true;
      end;

      IsApplicationAreaChanged :=
        TrySaveApplicationArea(TempApplicationAreaBuffer,ApplicationAreaSetup,NoUserApplicationAreasExist);
    END;

    [Internal]
    PROCEDURE SetupApplicationArea@1();
    BEGIN
      APPLICATIONAREA(GetApplicationAreaSetup);
    END;

    LOCAL PROCEDURE IsInPrimaryKey@7(FieldRef@1000 : FieldRef) : Boolean;
    VAR
      RecRef@1003 : RecordRef;
      KeyRef@1001 : KeyRef;
      FieldIndex@1002 : Integer;
    BEGIN
      RecRef := FieldRef.RECORD;

      KeyRef := RecRef.KEYINDEX(1);
      for FieldIndex := 1 to KeyRef.FIELDCOUNT do
        if KeyRef.FIELDINDEX(FieldIndex).NUMBER = FieldRef.NUMBER then
          exit(true);

      exit(false);
    END;

    [External]
    PROCEDURE GetExperienceTierCurrentCompany@9(VAR ExperienceTier@1000 : ',,,,,Basic,,,,,,,,,,Suite,,,,,Custom,,,,,Advanced');
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if not GetApplicationAreaSetupRecFromCompany(ApplicationAreaSetup) then
        exit;

      if GetApplicationAreaSetup = '' then begin
        if IsAdvancedSaaSEnabled then
          ExperienceTier := ExperienceTier::Advanced;
        exit;
      end;

      with ApplicationAreaSetup do
        case true of
          (SelectedAppAreaCount(ApplicationAreaSetup) = 15) and
          Basic and Suite and "Relationship Mgmt" and Jobs and "Fixed Assets" and Location and
          BasicHR and Assembly and "Item Charges" and Intercompany and "Sales Return Order" and
          "Purch Return Order" and Prepayments and "Cost Accounting" and "Item Tracking":
            ExperienceTier := ExperienceTier::Suite;
          (SelectedAppAreaCount(ApplicationAreaSetup) = 2) and Basic and "Relationship Mgmt":
            ExperienceTier := ExperienceTier::Basic;
          Basic and Suite and Advanced:
            ExperienceTier := ExperienceTier::Advanced;
          else
            ExperienceTier := ExperienceTier::Custom;
        end;
    END;

    [External]
    PROCEDURE SetExperienceTierCurrentCompany@10(ExperienceTier@1000 : ',,,,,Basic,,,,,,,,,,Suite,,,,,Custom,,,,,Advanced');
    VAR
      ApplicationAreaSetup@1001 : Record "Application Area Setup";
    BEGIN
      if ExperienceTier = ExperienceTier::Custom then
        ERROR(ValuesNotAllowedErr);

      with ApplicationAreaSetup do begin
        if not GET(COMPANYNAME,'','') then begin
          "Company Name" := COMPANYNAME;
          INSERT;
        end;

        SetExperienceTier(ExperienceTier);
        MODIFY;
      end;

      SetupApplicationArea;
    END;

    PROCEDURE SetExperienceTier@23(ExperienceTier@1000 : '" ",,,,,Basic,,,,,,,,,,Suite,,,,,Custom,,,,,Advanced');
    BEGIN
      if ExperienceTier = ExperienceTier::Advanced then begin
        Basic := false;
        Suite := false;
        Advanced := false;
      end else begin
        Basic := ExperienceTier <> ExperienceTier::" ";
        Suite := ExperienceTier = ExperienceTier::Suite;
        Advanced := false;
      end;
      SetApplicationAreasMatchingCurrentExperienceTier;
    END;

    PROCEDURE GetFirstPublicAppAreaFieldIndex@20() : Integer;
    VAR
      RecRef@1000 : RecordRef;
      FirstPublicAppAreaFieldRef@1001 : FieldRef;
      i@1003 : Integer;
    BEGIN
      RecRef.GETTABLE(Rec);
      FirstPublicAppAreaFieldRef := RecRef.FIELD(FIELDNO(Basic));
      for i := 1 to RecRef.FIELDCOUNT do
        if RecRef.FIELDINDEX(i).NUMBER = FirstPublicAppAreaFieldRef.NUMBER then
          exit(i);
    END;

    PROCEDURE SetApplicationAreasMatchingCurrentExperienceTier@26();
    BEGIN
      "Relationship Mgmt" := Basic;
      Assembly := Suite;
      Jobs := Suite;
      "Fixed Assets" := Suite;
      Location := Suite;
      BasicHR := Suite;
      "Item Charges" := Suite;
      Intercompany := Suite;
      "Sales Return Order" := Suite;
      "Purch Return Order" := Suite;
      Prepayments := Suite;
      "Cost Accounting" := Suite;
      "Item Tracking" := Suite;
      Dimensions := Advanced;
      Manufacturing := Advanced;
      Planning := Advanced;
      Service := Advanced;
      Warehouse := Advanced;
    END;

    PROCEDURE ResetNonSelectableApplicationAreas@24();
    VAR
      ApplicationAreaSetup@1000 : Record "Application Area Setup";
    BEGIN
      if ApplicationAreaSetup.FINDSET(true) then
        repeat
          ApplicationAreaSetup.SetApplicationAreasMatchingCurrentExperienceTier;
          ApplicationAreaSetup.MODIFY;
        until ApplicationAreaSetup.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

