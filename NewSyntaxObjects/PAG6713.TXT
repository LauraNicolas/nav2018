OBJECT Page 6713 OData Fields Export
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Select Fields to Export;
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 CurrPage.ODataColumnChooseSubForm.PAGE.InitColumns(
                   TenantWebService."Object Type",TenantWebService."Object ID",2,TenantWebService."Service Name",'');
                 CurrPage.ODataColumnChooseSubForm.PAGE.SetCalledForExcelExport(RecRef);
               END;

    OnQueryClosePage=VAR
                       TempTenantWebServiceColumns@1000 : TEMPORARY Record "Tenant Web Service Columns";
                       TenantWebServiceColumns@1001 : Record "Tenant Web Service Columns";
                       TenantWebServiceFilter@1002 : Record "Tenant Web Service Filter";
                       TenantWebServiceOData@1005 : Record "Tenant Web Service OData";
                       ODataUtility@1003 : Codeunit ODataUtility;
                       SelectText@1006 : Text;
                       ODataV3FilterText@1007 : Text;
                       ODataV4FilterText@1008 : Text;
                     BEGIN
                       if CloseAction = ACTION::OK then begin
                         CurrPage.ODataColumnChooseSubForm.PAGE.GetColumns(TempTenantWebServiceColumns);
                         TenantWebServiceColumns.CreateFromTemp(TempTenantWebServiceColumns,TenantWebService.RECORDID);
                         TenantWebServiceFilter.CreateFromRecordRef(RecRef,TenantWebService.RECORDID);

                         TenantWebServiceOData.SETRANGE(TenantWebServiceID,TenantWebService.RECORDID);
                         TenantWebServiceOData.FINDFIRST;
                         ODataUtility.GenerateSelectText(TenantWebService."Service Name",TenantWebService."Object Type",SelectText);
                         ODataUtility.GenerateODataV3FilterText(TenantWebService."Service Name",TenantWebService."Object Type",ODataV3FilterText);
                         ODataUtility.GenerateODataV4FilterText(TenantWebService."Service Name",TenantWebService."Object Type",ODataV4FilterText);
                         TenantWebServiceOData.SetOdataSelectClause(SelectText);
                         TenantWebServiceOData.SetOdataFilterClause(ODataV3FilterText);
                         TenantWebServiceOData.SetOdataV4FilterClause(ODataV4FilterText);
                         TenantWebServiceOData.MODIFY(true);

                         ODataUtility.GenerateExcelWorkBook(TenantWebService."Object Type",TenantWebService."Service Name",true);
                       end;
                     END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Container1;
                CaptionML=ENU="";
                ContainerType=ContentArea }

    { 2   ;1   ;Part      ;
                Name=ODataColumnChooseSubForm;
                CaptionML=ENU=Choose columns to export;
                ApplicationArea=#Basic,#Suite;
                PagePartID="OData Column Choose SubForm";
                PartType=Page }

  }
  CODE
  {
    VAR
      TenantWebService@1000 : Record "Tenant Web Service";
      RecRef@1001 : RecordRef;

    PROCEDURE SetExportData@4(VAR ExportTenantWebService@1001 : Record "Tenant Web Service";VAR ExportRecRef@1000 : RecordRef);
    BEGIN
      TenantWebService := ExportTenantWebService;
      RecRef := ExportRecRef;
    END;

    BEGIN
    END.
  }
}

