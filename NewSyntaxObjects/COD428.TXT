OBJECT Codeunit 428 IC Mapping
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE MapAccounts@1(ICGLAcc@1002 : Record "IC G/L Account");
    VAR
      GlAcc@1001 : Record "G/L Account";
    BEGIN
      GlAcc.SETRANGE("No.",ICGLAcc."No.");
      if GlAcc.FINDFIRST and (ICGLAcc."Account Type" = GlAcc."Account Type") then begin
        ICGLAcc."Map-to G/L Acc. No." := GlAcc."No.";
        ICGLAcc.MODIFY;
      end;
    END;

    [External]
    PROCEDURE MapIncomingICDimensions@2(ICDimension@1004 : Record "IC Dimension");
    VAR
      Dimension@1001 : Record Dimension;
      DimensionValue@1003 : Record "Dimension Value";
      ICDimensionValue@1000 : Record "IC Dimension Value";
    BEGIN
      Dimension.SETRANGE(Code,ICDimension.Code);
      if Dimension.FINDFIRST then begin
        ICDimension."Map-to Dimension Code" := Dimension.Code;
        ICDimension.MODIFY;
        ICDimensionValue.SETRANGE("Dimension Code",ICDimension.Code);
        if ICDimensionValue.FIND('-') then begin
          ICDimensionValue.MODIFYALL("Map-to Dimension Code",ICDimension."Map-to Dimension Code");
          if ICDimensionValue.FIND('-') then
            repeat
              if ICDimensionValue."Map-to Dimension Value Code" = '' then begin
                DimensionValue.SETRANGE(Code,ICDimensionValue.Code);
                if DimensionValue.FINDFIRST and
                   (DimensionValue."Dimension Value Type" = ICDimensionValue."Dimension Value Type")
                then begin
                  ICDimensionValue."Map-to Dimension Value Code" := DimensionValue.Code;
                  ICDimensionValue.MODIFY;
                end;
              end;
            until ICDimensionValue.NEXT = 0;
        end;
      end;
    END;

    [External]
    PROCEDURE MapOutgoingICDimensions@3(Dimension@1000 : Record Dimension);
    VAR
      ICDimension@1001 : Record "IC Dimension";
      ICDimensionValue@1003 : Record "IC Dimension Value";
      DimensionValue@1002 : Record "Dimension Value";
    BEGIN
      ICDimension.SETRANGE(Code,Dimension.Code);
      if ICDimension.FINDFIRST then begin
        Dimension."Map-to IC Dimension Code" := ICDimension.Code;
        Dimension.MODIFY;
        DimensionValue.SETRANGE("Dimension Code",Dimension.Code);
        if DimensionValue.FIND('-') then begin
          DimensionValue.MODIFYALL("Map-to IC Dimension Code",Dimension."Map-to IC Dimension Code");
          if DimensionValue.FIND('-') then
            repeat
              if DimensionValue."Map-to IC Dimension Value Code" = '' then begin
                ICDimensionValue.SETRANGE(Code,DimensionValue.Code);
                if ICDimensionValue.FINDFIRST and
                   (DimensionValue."Dimension Value Type" = ICDimensionValue."Dimension Value Type")
                then begin
                  DimensionValue."Map-to IC Dimension Value Code" := ICDimensionValue.Code;
                  DimensionValue.MODIFY;
                end;
              end;
            until DimensionValue.NEXT = 0;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

