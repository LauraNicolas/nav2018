OBJECT XMLport 1230 Export Generic CSV
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Export Generic CSV;
    Direction=Export;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    Window.OPEN(ProgressMsg);
                  END;

    OnPreXMLport=BEGIN
                   InitializeGlobals;
                 END;

    OnPostXMLport=BEGIN
                    if ErrorText <> '' then
                      ERROR(ErrorText);

                    Window.CLOSE;

                    if DataExch.GET(DataExchEntryNo) then
                      if DataExchDef.GET(DataExch."Data Exch. Def Code") then
                        currXMLport.FILENAME := DataExchDef.Name + '.csv';
                  END;

    Format=Variable Text;
    UseRequestPage=false;
  }
  ELEMENTS
  {
    { ['{C19D763B-C1F1-473D-8D3E-3640AD129D95}'];;root                ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { ['{125F3B85-5B53-4F67-9434-589A04D6DCA1}'];1;DataExchField      ;Element ;Table   ;
                                                  SourceTable="Data Exch. Field" }

    { ['{A64C65BE-4E70-436F-9680-A7C77EC03315}'];2;ColumnX            ;Element ;Text    ;
                                                  DataType=Text;
                                                  MinOccurs=Zero;
                                                  Unbound=true;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 if QuitLoop then
                                                                                   currXMLport.BREAKUNBOUND;

                                                                                 if "Data Exch. Field"."Line No." <> LastLineNo then begin
                                                                                   if "Data Exch. Field"."Line No." <> LastLineNo + 1 then
                                                                                     ErrorText += LinesNotSequentialErr
                                                                                   else begin
                                                                                     LastLineNo := "Data Exch. Field"."Line No.";
                                                                                     PrevColumnNo := 0;
                                                                                     "Data Exch. Field".NEXT(-1);
                                                                                     Window.UPDATE(1,LastLineNo);
                                                                                   end;
                                                                                   currXMLport.BREAKUNBOUND;
                                                                                 end;

                                                                                 CheckColumnSequence;
                                                                                 ColumnX := "Data Exch. Field".Value;

                                                                                 if "Data Exch. Field".NEXT = 0 then
                                                                                   QuitLoop := true;
                                                                               END;
                                                                                }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      DataExchDef@1064 : Record "Data Exch. Def";
      DataExch@1063 : Record "Data Exch.";
      Window@1000 : Dialog;
      ErrorText@1005 : Text;
      DataExchEntryNo@1062 : Integer;
      LastLineNo@1065 : Integer;
      PrevColumnNo@1003 : Integer;
      QuitLoop@1001 : Boolean;
      ColumnsNotSequentialErr@1002 : TextConst 'ENU=The data to be exported is not structured correctly. The columns in the dataset must be sequential.';
      LinesNotSequentialErr@1004 : TextConst 'ENU=The data to be exported is not structured correctly. The lines in the dataset must be sequential.';
      ProgressMsg@1060 : TextConst 'ENU=Exporting line no. #1######';

    LOCAL PROCEDURE InitializeGlobals@11();
    BEGIN
      DataExchEntryNo := "Data Exch. Field".GETRANGEMIN("Data Exch. No.");
      LastLineNo := 1;
      PrevColumnNo := 0;
      QuitLoop := false;
    END;

    [External]
    PROCEDURE CheckColumnSequence@3();
    BEGIN
      if "Data Exch. Field"."Column No." <> PrevColumnNo + 1 then begin
        ErrorText += ColumnsNotSequentialErr;
        currXMLport.BREAKUNBOUND;
      end;

      PrevColumnNo := "Data Exch. Field"."Column No.";
    END;

    BEGIN
    END.
  }
}

