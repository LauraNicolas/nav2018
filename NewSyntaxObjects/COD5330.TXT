OBJECT Codeunit 5330 CRM Integration Management
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Service Password"=rimd;
    SingleInstance=true;
    OnRun=BEGIN
            CheckOrEnableCRMConnection;
          END;

  }
  CODE
  {
    VAR
      CRMEntityUrlTemplateTxt@1001 : TextConst '@@@={Locked};ENU="%1/main.aspx?pagetype=entityrecord&etn=%2&id=%3"';
      UnableToResolveCRMEntityNameFrmTableIDErr@1002 : TextConst '@@@="%1 = table ID (numeric), %2 = CRM Product Name";ENU=The application is not designed to integrate table %1 with %2.';
      CouplingNotFoundErr@1000 : TextConst '@@@="%1 = CRM Product Name";ENU=The record is not coupled to %1.';
      NoCardPageActionDefinedForTableIdErr@1022 : TextConst '@@@="%1 = Table ID";ENU=The open page action is not supported for Table %1.';
      IntegrationTableMappingNotFoundErr@1005 : TextConst '@@@="%1 = Integration Table Mapping caption, %2 = Table caption for the table which is not mapped";ENU=No %1 was found for table %2.';
      UpdateNowDirectionQst@1006 : TextConst '@@@="String menu options separated by comma. %1 = CRM Product Name";ENU=Send data update to %1.,Get data update from %1.';
      UpdateOneNowTitleTxt@1007 : TextConst '@@@="%1 = Table caption and value for the entity we want to synchronize now.";ENU=Synchronize data for %1?';
      UpdateMultipleNowTitleTxt@1014 : TextConst 'ENU=Synchronize data for the selected records?';
      ManageCouplingQst@1003 : TextConst '@@@="%1=The record caption (type), %2 = CRM Product Name";ENU=The %1 record is not coupled to %2. Do you want to create a coupling?';
      SyncNowFailedMsg@1037 : TextConst 'ENU=The synchronization failed.';
      SyncNowScheduledMsg@1004 : TextConst 'ENU=The synchronization has been scheduled.';
      SyncNowSkippedMsg@1053 : TextConst 'ENU=The synchronization has been skipped.';
      SyncMultipleMsg@1038 : TextConst '@@@=%1,%2,%3,%4 are numbers of records;ENU=The synchronization has been scheduled for %1 of %4 records. %2 records failed. %3 records were skipped.';
      SyncSkippedMsg@1008 : TextConst 'ENU=The record will be skipped for further synchronization due to a repeatable failure.';
      SyncRestoredMsg@1010 : TextConst 'ENU=The record has been restored for synchronization.';
      SyncMultipleRestoredMsg@1012 : TextConst '@@@=%1 - an integer, a count of records.;ENU=%1 records have been restored for synchronization.';
      DetailsTxt@1009 : TextConst 'ENU=Details.';
      UpdateOneNowToCRMQst@1017 : TextConst '@@@="%1 = Table caption and value for the entity we want to synchronize now., %2 = short CRM Product Name";ENU=Send data update to %2 for %1?';
      UpdateOneNowToModifiedCRMQst@1033 : TextConst '@@@="%1 = Table caption and value for the entity we want to synchronize now. %2 - product name, %3 = short CRM product name";ENU=The %3 record coupled to %1 contains newer data than the %2 record. Do you want to overwrite the data in %3?';
      UpdateOneNowFromCRMQst@1018 : TextConst '@@@="%1 = Table caption and value for the entity we want to synchronize now., %2 = short CRM product name";ENU=Get data update from %2 for %1?';
      UpdateOneNowFromOldCRMQst@1011 : TextConst '@@@="%1 = Table caption and value for the entity we want to synchronize now. %2 - product name, %3 = short CRM product name";ENU=The %2 record %1 contains newer data than the %3 record. Get data update from %3, overwriting data in %2?';
      UpdateMultipleNowToCRMQst@1019 : TextConst '@@@="%1 = short CRM product name";ENU=Send data update to %1 for the selected records?';
      UpdateMultipleNowFromCRMQst@1020 : TextConst '@@@="%1 = short CRM product name";ENU=Get data update from %1 for the selected records?';
      AccountStatisticsUpdatedMsg@1024 : TextConst '@@@="%1 = short CRM product name";ENU=The customer statistics have been successfully updated in %1.';
      BothRecordsModifiedBiDirectionalMsg@1029 : TextConst '@@@="%1 and %2 area captions of tables such as Customer and CRM Account, %3 = short CRM product name";ENU=Both the %1 record and the %3 %2 record have been changed since the last synchronization, or synchronization has never been performed. If you continue with synchronization, data on one of the records will be lost and replaced with data from the other record.';
      BothRecordsModifiedToCRMQst@1030 : TextConst '@@@="%1 is a formatted RecordID, such as ''Customer 1234''. %2 is the caption of a CRM table. %3 - product name, %4 = short CRM product name";ENU=Both %1 and the %4 %2 record have been changed since the last synchronization, or synchronization has never been performed. If you continue with synchronization, data in %4 will be overwritten with data from %3. Are you sure you want to synchronize?';
      BothRecordsModifiedToNAVQst@1031 : TextConst '@@@="%1 is a formatted RecordID, such as ''Customer 1234''. %2 is the caption of a CRM table. %3 - product name, %4 = short CRM product name";ENU=Both %1 and the %4 %2 record have been changed since the last synchronization, or synchronization has never been performed. If you continue with synchronization, data in %3 will be overwritten with data from %4. Are you sure you want to synchronize?';
      CRMProductName@1015 : Codeunit "CRM Product Name";
      CRMIntegrationEnabledState@1021 : '" ","Not Enabled",Enabled,"Enabled But Not For Current User"';
      NotEnabledForCurrentUserMsg@1035 : TextConst '@@@="%1 = Current User Id %2 - product name, %3 = CRM product name";ENU=%3 Integration is enabled.\However, because the %2 Users Must Map to %3 Users field is set, %3 integration is not enabled for %1.';
      CRMIntegrationEnabledLastError@1036 : Text;
      ImportSolutionConnectStringTok@1039 : TextConst '@@@={Locked};ENU=%1api%2/XRMServices/2011/Organization.svc';
      UserDoesNotExistCRMTxt@1042 : TextConst '@@@="%1 = User email address, %2 = CRM product name";ENU=There is no user with email address %1 in %2. Enter a valid email address.';
      RoleIdDoesNotExistCRMTxt@1043 : TextConst '@@@="%1 = CRM Product name";ENU=The Integration role does not exist in %1. \\Make sure the relevant customization is imported or check if the name of the role has changed.';
      EmailAndServerAddressEmptyErr@1046 : TextConst 'ENU=The Integration User Email and Server Address fields must not be empty.';
      CRMSolutionFileNotFoundErr@1041 : TextConst 'ENU=A file for a CRM solution could not be found.';
      MicrosoftDynamicsNavIntegrationTxt@1040 : TextConst '@@@={Locked};ENU=MicrosoftDynamicsNavIntegration';
      AdminEmailPasswordWrongErr@1044 : TextConst '@@@="%1 = CRM product name";ENU=Enter valid %1 administrator credentials.';
      AdminUserDoesNotHavePriviligesErr@1045 : TextConst '@@@="%1 = CRM product name";ENU=The specified %1 administrator does not have sufficient privileges to import a %1 solution.';
      InvalidUriErr@1050 : TextConst 'ENU=The value entered is not a valid URL.';
      MustUseHttpsErr@1049 : TextConst '@@@="%1 = CRM product name";ENU=The application is set up to support secure connections (HTTPS) to %1 only. You cannot use HTTP.';
      MustUseHttpOrHttpsErr@1048 : TextConst '@@@="%1 is a URI scheme, such as FTP, HTTP, chrome or file, %2 = CRM product name";ENU=%1 is not a valid URI scheme for %2 connections. You can only use HTTPS or HTTP as the scheme in the URL.';
      ReplaceServerAddressQst@1047 : TextConst '@@@=%1 and %2 are URLs;ENU=The URL is not valid. Do you want to replace it with the URL suggested below?\\Entered URL: "%1".\Suggested URL: "%2".';
      CRMConnectionURLWrongErr@1051 : TextConst '@@@="%1 = CRM product name";ENU=The URL is incorrect. Enter the URL for the %1 connection.';
      NoOf@1052 : ',Scheduled,Failed,Skipped,Total';
      CRMConnSetupWizardQst@1013 : TextConst '@@@="%1 = CRM product name";ENU=Do you want to open the %1 Connection assisted setup wizard?';

    [External]
    PROCEDURE IsCRMIntegrationEnabled@5() : Boolean;
    VAR
      CRMConnectionSetup@1000 : Record "CRM Connection Setup";
    BEGIN
      if CRMIntegrationEnabledState = CRMIntegrationEnabledState::" " then begin
        CLEARLASTERROR;
        CRMIntegrationEnabledState := CRMIntegrationEnabledState::"Not Enabled";
        CLEAR(CRMIntegrationEnabledLastError);
        if CRMConnectionSetup.GET then
          if CRMConnectionSetup."Is Enabled" then begin
            if not HASTABLECONNECTION(TABLECONNECTIONTYPE::CRM,GETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::CRM)) then
              CRMConnectionSetup.RegisterUserConnection;
            if not CRMConnectionSetup."Is User Mapping Required" then
              CRMIntegrationEnabledState := CRMIntegrationEnabledState::Enabled
            else
              if CRMConnectionSetup.IsCurrentUserMappedToCrmSystemUser then
                CRMIntegrationEnabledState := CRMIntegrationEnabledState::Enabled
              else begin
                CRMIntegrationEnabledState := CRMIntegrationEnabledState::"Enabled But Not For Current User";
                CRMIntegrationEnabledLastError := GetLastErrorMessage;
              end;
          end;
      end;

      exit(CRMIntegrationEnabledState = CRMIntegrationEnabledState::Enabled);
    END;

    [Internal]
    PROCEDURE IsCRMSolutionInstalled@19() : Boolean;
    BEGIN
      if TryTouchCRMSolutionEntities then
        exit(true);

      CLEARLASTERROR;
      exit(false);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryTouchCRMSolutionEntities@27();
    VAR
      CRMNAVConnection@1001 : Record "CRM NAV Connection";
      CRMAccountStatistics@1000 : Record "CRM Account Statistics";
    BEGIN
      if CRMAccountStatistics.FINDFIRST then;
      if CRMNAVConnection.FINDFIRST then;
    END;

    [External]
    PROCEDURE SetCRMNAVConnectionUrl@38(WebClientUrl@1000 : Text[250]);
    VAR
      CRMNAVConnection@1002 : Record "CRM NAV Connection";
      NewConnection@1001 : Boolean;
    BEGIN
      if not CRMNAVConnection.FINDFIRST then begin
        CRMNAVConnection.INIT;
        NewConnection := true;
      end;

      CRMNAVConnection."Dynamics NAV URL" := WebClientUrl;

      if NewConnection then
        CRMNAVConnection.INSERT
      else
        CRMNAVConnection.MODIFY;
    END;

    [External]
    PROCEDURE SetCRMNAVODataUrlCredentials@48(ODataUrl@1000 : Text[250];Username@1003 : Text[250];Accesskey@1004 : Text[250]);
    VAR
      CRMNAVConnection@1002 : Record "CRM NAV Connection";
      NewConnection@1001 : Boolean;
    BEGIN
      if not CRMNAVConnection.FINDFIRST then begin
        CRMNAVConnection.INIT;
        NewConnection := true;
      end;

      CRMNAVConnection."Dynamics NAV OData URL" := ODataUrl;
      CRMNAVConnection."Dynamics NAV OData Username" := Username;
      CRMNAVConnection."Dynamics NAV OData Accesskey" := Accesskey;

      if NewConnection then
        CRMNAVConnection.INSERT
      else
        CRMNAVConnection.MODIFY;
    END;

    [External]
    PROCEDURE UpdateMultipleNow@14(RecVariant@1007 : Variant);
    VAR
      IntegrationTableMapping@1001 : Record "Integration Table Mapping";
      RecRef@1015 : RecordRef;
      RecordCounter@1016 : ARRAY [4] OF Integer;
      SelectedDirection@1003 : Integer;
      CRMID@1000 : GUID;
      Unused@1004 : Boolean;
    BEGIN
      RecordCounter[NoOf::Total] := GetRecordRef(RecVariant,RecRef);
      if RecordCounter[NoOf::Total] = 0 then
        exit;
      GetIntegrationTableMapping(IntegrationTableMapping,RecRef.NUMBER);

      if RecordCounter[NoOf::Total] = 1 then
        if GetCoupledCRMID(RecRef.RECORDID,CRMID) then
          SelectedDirection :=
            GetSelectedSingleSyncDirection(IntegrationTableMapping,RecRef,CRMID,Unused)
        else begin
          DefineCouplingIfNotCoupled(RecRef.RECORDID,CRMID);
          exit;
        end
      else
        SelectedDirection := GetSelectedMultipleSyncDirection(IntegrationTableMapping);
      if SelectedDirection = 0 then
        exit; // The user cancelled

      repeat
        if not GetCoupledCRMID(RecRef.RECORDID,CRMID) then
          RecordCounter[NoOf::Skipped] += 1
        else begin
          if (RecordCounter[NoOf::Total] > 1) and
             WasRecordModifiedAfterLastSynch(IntegrationTableMapping,RecRef,CRMID,SelectedDirection)
          then
            RecordCounter[NoOf::Skipped] += 1
          else
            if IsRecordSkipped(RecRef.RECORDID) then
              RecordCounter[NoOf::Skipped] += 1
            else
              if EnqueueSyncJob(IntegrationTableMapping,RecRef.RECORDID,CRMID,SelectedDirection) then
                RecordCounter[NoOf::Scheduled] += 1
              else
                RecordCounter[NoOf::Failed] += 1;
        end;
      until RecRef.NEXT = 0;

      SendSyncNotification(RecordCounter);
    END;

    [External]
    PROCEDURE UpdateOneNow@17(RecordID@1000 : RecordID);
    BEGIN
      // Extinct method. Kept for backward compatibility.
      UpdateMultipleNow(RecordID)
    END;

    [Internal]
    PROCEDURE UpdateSkippedNow@71(VAR CRMIntegrationRecord@1000 : Record "CRM Integration Record");
    VAR
      IntegrationRecord@1001 : Record "Integration Record";
      RestoredRecCounter@1003 : Integer;
    BEGIN
      if CRMIntegrationRecord.FINDSET then
        repeat
          if IntegrationRecord.GET(CRMIntegrationRecord."Integration ID") then begin
            CRMIntegrationRecord.Skipped := false;
            CRMIntegrationRecord.MODIFY;
            RestoredRecCounter += 1;
          end;
        until CRMIntegrationRecord.NEXT = 0;
      SendRestoredSyncNotification(RestoredRecCounter);
    END;

    LOCAL PROCEDURE WasRecordModifiedAfterLastSynch@75(IntegrationTableMapping@1001 : Record "Integration Table Mapping";RecRef@1003 : RecordRef;CRMID@1002 : GUID;SelectedDirection@1000 : Option) : Boolean;
    VAR
      IntegrationRecSynchInvoke@1007 : Codeunit "Integration Rec. Synch. Invoke";
      CRMRecordRef@1006 : RecordRef;
      RecordModified@1005 : Boolean;
      CRMRecordModified@1004 : Boolean;
    BEGIN
      RecordModified := IntegrationRecSynchInvoke.WasModifiedAfterLastSynch(IntegrationTableMapping,RecRef);
      IntegrationTableMapping.GetRecordRef(CRMID,CRMRecordRef);
      CRMRecordModified := IntegrationRecSynchInvoke.WasModifiedAfterLastSynch(IntegrationTableMapping,CRMRecordRef);
      exit(
        ((SelectedDirection = IntegrationTableMapping.Direction::ToIntegrationTable) and CRMRecordModified) or
        ((SelectedDirection = IntegrationTableMapping.Direction::FromIntegrationTable) and RecordModified))
    END;

    [External]
    PROCEDURE CheckOrEnableCRMConnection@23();
    BEGIN
      if IsCRMIntegrationEnabled then
        exit;

      if CRMIntegrationEnabledLastError <> '' then
        ERROR(CRMIntegrationEnabledLastError);

      if CRMIntegrationEnabledState = CRMIntegrationEnabledState::"Enabled But Not For Current User" then
        MESSAGE(NotEnabledForCurrentUserMsg,USERID,PRODUCTNAME.SHORT,CRMProductName.SHORT)
      else
        if CONFIRM(CRMConnSetupWizardQst,true,CRMProductName.SHORT) then
          PAGE.RUN(PAGE::"CRM Connection Setup Wizard");

      ERROR('');
    END;

    LOCAL PROCEDURE GetRecordRef@68(RecVariant@1000 : Variant;VAR RecordRef@1001 : RecordRef) : Integer;
    BEGIN
      case true of
        RecVariant.ISRECORD:
          RecordRef.GETTABLE(RecVariant);
        RecVariant.ISRECORDID:
          if RecordRef.GET(RecVariant) then
            RecordRef.SETRECFILTER;
        RecVariant.ISRECORDREF:
          RecordRef := RecVariant;
        else
          exit(0);
      end;
      if RecordRef.FINDSET then
        exit(RecordRef.COUNT);
      exit(0);
    END;

    [External]
    PROCEDURE CreateNewRecordInCRM@25(RecordID@1000 : RecordID;ConfirmBeforeDeletingExistingCoupling@1003 : Boolean);
    BEGIN
      // Extinct method. Kept for backward compatibility.
      ConfirmBeforeDeletingExistingCoupling := false;
      CreateNewRecordsInCRM(RecordID);
    END;

    [External]
    PROCEDURE CreateNewRecordsInCRM@22(RecVariant@1004 : Variant);
    VAR
      IntegrationTableMapping@1002 : Record "Integration Table Mapping";
      RecRef@1000 : RecordRef;
      CRMID@1001 : GUID;
      RecordCounter@1005 : ARRAY [4] OF Integer;
    BEGIN
      RecordCounter[NoOf::Total] := GetRecordRef(RecVariant,RecRef);
      if RecordCounter[NoOf::Total] = 0 then
        exit;
      GetIntegrationTableMapping(IntegrationTableMapping,RecRef.NUMBER);

      repeat
        if GetCoupledCRMID(RecRef.RECORDID,CRMID) then
          RecordCounter[NoOf::Skipped] += 1
        else
          if EnqueueSyncJob(IntegrationTableMapping,RecRef.RECORDID,CRMID,IntegrationTableMapping.Direction::ToIntegrationTable) then
            RecordCounter[NoOf::Scheduled] += 1
          else
            RecordCounter[NoOf::Failed] += 1;
      until RecRef.NEXT = 0;

      SendSyncNotification(RecordCounter);
    END;

    [Internal]
    PROCEDURE CreateNewRecordsFromCRM@15(RecVariant@1001 : Variant);
    VAR
      IntegrationTableMapping@1002 : Record "Integration Table Mapping";
      CRMIntegrationRecord@1005 : Record "CRM Integration Record";
      RecRef@1000 : RecordRef;
      RecordID@1006 : RecordID;
      CRMID@1003 : GUID;
      RecordCounter@1009 : ARRAY [4] OF Integer;
    BEGIN
      RecordCounter[NoOf::Total] := GetRecordRef(RecVariant,RecRef);
      if RecordCounter[NoOf::Total] = 0 then
        exit;
      GetIntegrationTableMapping(IntegrationTableMapping,RecRef.NUMBER);

      repeat
        CRMID := RecRef.FIELD(IntegrationTableMapping."Integration Table UID Fld. No.").VALUE;
        if CRMIntegrationRecord.FindRecordIDFromID(CRMID,IntegrationTableMapping."Table ID",RecordID) then
          RecordCounter[NoOf::Skipped] += 1
        else
          if EnqueueSyncJob(IntegrationTableMapping,RecRef.RECORDID,CRMID,IntegrationTableMapping.Direction::FromIntegrationTable) then
            RecordCounter[NoOf::Scheduled] += 1
          else
            RecordCounter[NoOf::Failed] += 1;
      until RecRef.NEXT = 0;

      SendSyncNotification(RecordCounter);
    END;

    LOCAL PROCEDURE PerformInitialSynchronization@16(RecordID@1000 : RecordID;CRMID@1002 : GUID;Direction@1001 : Option);
    VAR
      IntegrationTableMapping@1005 : Record "Integration Table Mapping";
      RecordCounter@1003 : ARRAY [4] OF Integer;
    BEGIN
      RecordCounter[NoOf::Total] := 1;
      GetIntegrationTableMapping(IntegrationTableMapping,RecordID.TABLENO);
      if EnqueueSyncJob(IntegrationTableMapping,RecordID,CRMID,Direction) then
        RecordCounter[NoOf::Scheduled] += 1
      else
        RecordCounter[NoOf::Failed] += 1;

      SendSyncNotification(RecordCounter);
    END;

    LOCAL PROCEDURE GetIntegrationTableMapping@9(VAR IntegrationTableMapping@1001 : Record "Integration Table Mapping";TableID@1000 : Integer);
    VAR
      TableMetadata@1002 : Record "Table Metadata";
    BEGIN
      IntegrationTableMapping.SETRANGE("Synch. Codeunit ID",CODEUNIT::"CRM Integration Table Synch.");
      IntegrationTableMapping.SETRANGE("Delete After Synchronization",false);
      if IsCRMTable(TableID) then
        IntegrationTableMapping.SETRANGE("Integration Table ID",TableID)
      else
        IntegrationTableMapping.SETRANGE("Table ID",TableID);
      if not IntegrationTableMapping.FINDFIRST then begin
        TableMetadata.GET(TableID);
        ERROR(IntegrationTableMappingNotFoundErr,IntegrationTableMapping.TABLECAPTION,TableMetadata.Caption);
      end;
    END;

    [External]
    PROCEDURE IsCRMTable@51(TableID@1000 : Integer) : Boolean;
    VAR
      TableMetadata@1001 : Record "Table Metadata";
    BEGIN
      if TableMetadata.GET(TableID) then
        exit(TableMetadata.TableType = TableMetadata.TableType::CRM);
    END;

    LOCAL PROCEDURE IsRecordSkipped@61(RecID@1001 : RecordID) : Boolean;
    VAR
      CRMIntegrationRecord@1000 : Record "CRM Integration Record";
    BEGIN
      if CRMIntegrationRecord.FindByRecordID(RecID) then
        exit(CRMIntegrationRecord.Skipped);
    END;

    PROCEDURE EnqueueFullSyncJob@78(Name@1001 : Code[20]) : GUID;
    VAR
      IntegrationTableMapping@1000 : Record "Integration Table Mapping";
      JobQueueEntry@1002 : Record "Job Queue Entry";
      CRMSetupDefaults@1008 : Codeunit "CRM Setup Defaults";
    BEGIN
      IntegrationTableMapping.GET(Name);
      IntegrationTableMapping."Full Sync is Running" := true;
      IntegrationTableMapping.CALCFIELDS("Table Filter","Integration Table Filter");
      AddIntegrationTableMapping(IntegrationTableMapping);
      COMMIT;
      if CRMSetupDefaults.CreateJobQueueEntry(IntegrationTableMapping) then begin
        JobQueueEntry.SETRANGE("Record ID to Process",IntegrationTableMapping.RECORDID);
        if JobQueueEntry.FINDFIRST then
          exit(JobQueueEntry.ID);
      end;
    END;

    LOCAL PROCEDURE EnqueueSyncJob@56(IntegrationTableMapping@1006 : Record "Integration Table Mapping";RecordID@1005 : RecordID;CRMID@1004 : GUID;Direction@1003 : Integer) : Boolean;
    VAR
      CRMSetupDefaults@1008 : Codeunit "CRM Setup Defaults";
    BEGIN
      IntegrationTableMapping.Direction := Direction;
      if Direction = IntegrationTableMapping.Direction::FromIntegrationTable then
        IntegrationTableMapping.SetIntegrationTableFilter(GetTableViewForGuid(IntegrationTableMapping."Integration Table ID",CRMID))
      else
        IntegrationTableMapping.SetTableFilter(GetTableViewForRecordID(RecordID));
      AddIntegrationTableMapping(IntegrationTableMapping);
      COMMIT;
      exit(CRMSetupDefaults.CreateJobQueueEntry(IntegrationTableMapping));
    END;

    LOCAL PROCEDURE AddIntegrationTableMapping@70(VAR IntegrationTableMapping@1005 : Record "Integration Table Mapping");
    VAR
      SourceMappingName@1004 : Code[20];
    BEGIN
      SourceMappingName := IntegrationTableMapping.Name;
      IntegrationTableMapping.Name := COPYSTR(DELCHR(FORMAT(CREATEGUID),'=','{}-'),1,MAXSTRLEN(IntegrationTableMapping.Name));
      IntegrationTableMapping."Synch. Only Coupled Records" := false;
      IntegrationTableMapping."Delete After Synchronization" := true;
      IntegrationTableMapping."Parent Name" := SourceMappingName;
      CLEAR(IntegrationTableMapping."Synch. Modified On Filter");
      CLEAR(IntegrationTableMapping."Synch. Int. Tbl. Mod. On Fltr.");
      CLEAR(IntegrationTableMapping."Last Full Sync Start DateTime");
      IntegrationTableMapping.INSERT;

      CloneIntegrationFieldMapping(SourceMappingName,IntegrationTableMapping.Name);
    END;

    LOCAL PROCEDURE CloneIntegrationFieldMapping@62(SourceMappingName@1000 : Code[20];DestinationMappingName@1002 : Code[20]);
    VAR
      IntegrationFieldMapping@1001 : Record "Integration Field Mapping";
      NewIntegrationFieldMapping@1003 : Record "Integration Field Mapping";
    BEGIN
      IntegrationFieldMapping.SETRANGE("Integration Table Mapping Name",SourceMappingName);
      if IntegrationFieldMapping.FINDSET then
        repeat
          NewIntegrationFieldMapping := IntegrationFieldMapping;
          NewIntegrationFieldMapping."No." := 0; // Autoincrement
          NewIntegrationFieldMapping."Integration Table Mapping Name" := DestinationMappingName;
          NewIntegrationFieldMapping.INSERT;
        until IntegrationFieldMapping.NEXT = 0;
    END;

    LOCAL PROCEDURE GetTableViewForGuid@57(TableNo@1004 : Integer;CRMId@1000 : GUID) View : Text;
    VAR
      FieldRef@1003 : FieldRef;
      KeyRef@1002 : KeyRef;
      RecordRef@1001 : RecordRef;
    BEGIN
      RecordRef.OPEN(TableNo);
      KeyRef := RecordRef.KEYINDEX(1); // Primary Key
      FieldRef := KeyRef.FIELDINDEX(1);
      FieldRef.SETRANGE(CRMId);
      View := RecordRef.GETVIEW;
      RecordRef.CLOSE;
    END;

    LOCAL PROCEDURE GetTableViewForRecordID@60(RecordID@1000 : RecordID) View : Text;
    VAR
      FieldRef@1003 : FieldRef;
      KeyRef@1002 : KeyRef;
      RecordRef@1001 : RecordRef;
      I@1005 : Integer;
    BEGIN
      RecordRef := RecordID.GETRECORD;
      KeyRef := RecordRef.KEYINDEX(1); // Primary Key
      for I := 1 to KeyRef.FIELDCOUNT do begin
        FieldRef := KeyRef.FIELDINDEX(I);
        FieldRef.SETRANGE(FieldRef.VALUE);
      end;
      View := RecordRef.GETVIEW;
      RecordRef.CLOSE;
    END;

    [External]
    PROCEDURE CreateOrUpdateCRMAccountStatistics@20(Customer@1000 : Record Customer);
    VAR
      CRMAccount@1004 : Record "CRM Account";
      CRMStatisticsJob@1005 : Codeunit "CRM Statistics Job";
      CRMID@1001 : GUID;
    BEGIN
      if not GetCoupledCRMID(Customer.RECORDID,CRMID) then
        exit;

      CRMAccount.GET(CRMID);
      CRMStatisticsJob.CreateOrUpdateCRMAccountStatistics(Customer,CRMAccount);
      CRMStatisticsJob.UpdateStatusOfPaidInvoices(Customer."No.");
      MESSAGE(STRSUBSTNO(AccountStatisticsUpdatedMsg,CRMProductName.SHORT));
    END;

    [External]
    PROCEDURE ShowCRMEntityFromRecordID@8(RecordID@1000 : RecordID);
    VAR
      CRMID@1001 : GUID;
    BEGIN
      if not DefineCouplingIfNotCoupled(RecordID,CRMID) then
        exit;

      HYPERLINK(GetCRMEntityUrlFromRecordID(RecordID));
    END;

    [External]
    PROCEDURE GetCRMEntityUrlFromRecordID@4(TargetRecordID@1000 : RecordID) : Text;
    VAR
      CRMIntegrationRecord@1001 : Record "CRM Integration Record";
      IntegrationRecord@1005 : Record "Integration Record";
      CRMId@1004 : GUID;
    BEGIN
      if not CRMIntegrationRecord.FindIDFromRecordID(TargetRecordID,CRMId) then
        ERROR(CouplingNotFoundErr,CRMProductName.FULL);

      IntegrationRecord.FindByRecordId(TargetRecordID);
      exit(GetCRMEntityUrlFromCRMID(IntegrationRecord."Table ID",CRMId));
    END;

    [External]
    PROCEDURE GetCRMEntityUrlFromCRMID@6(TableId@1001 : Integer;CRMId@1006 : GUID) : Text;
    VAR
      CRMConnectionSetup@1003 : Record "CRM Connection Setup";
    BEGIN
      CRMConnectionSetup.GET;
      exit(STRSUBSTNO(CRMEntityUrlTemplateTxt,CRMConnectionSetup."Server Address",GetCRMEntityTypeName(TableId),CRMId));
    END;

    [External]
    PROCEDURE OpenCoupledNavRecordPage@35(CRMID@1000 : GUID;CRMEntityTypeName@1012 : Text) : Boolean;
    VAR
      CRMIntegrationRecord@1001 : Record "CRM Integration Record";
      TempNameValueBuffer@1013 : TEMPORARY Record "Name/Value Buffer";
      CRMSetupDefaults@1014 : Codeunit "CRM Setup Defaults";
      RecordID@1008 : RecordID;
      TableId@1002 : Integer;
    BEGIN
      // Find the corresponding NAV record and type
      CRMSetupDefaults.GetTableIDCRMEntityNameMapping(TempNameValueBuffer);
      TempNameValueBuffer.SETRANGE(Name,LOWERCASE(CRMEntityTypeName));
      if not TempNameValueBuffer.FINDSET then
        exit(false);

      repeat
        EVALUATE(TableId,TempNameValueBuffer.Value);
        if CRMIntegrationRecord.FindRecordIDFromID(CRMID,TableId,RecordID) then
          break;
      until TempNameValueBuffer.NEXT = 0;

      if RecordID.TABLENO = 0 then
        exit(false);

      OpenRecordCardPage(RecordID);
      exit(true);
    END;

    LOCAL PROCEDURE OpenRecordCardPage@36(RecordID@1008 : RecordID);
    VAR
      Customer@1007 : Record Customer;
      Contact@1006 : Record Contact;
      Currency@1005 : Record Currency;
      SalespersonPurchaser@1004 : Record "Salesperson/Purchaser";
      UnitOfMeasure@1003 : Record "Unit of Measure";
      Item@1002 : Record Item;
      Resource@1001 : Record Resource;
      SalesInvoiceHeader@1015 : Record "Sales Invoice Header";
      CustomerPriceGroup@1009 : Record "Customer Price Group";
      RecordRef@1000 : RecordRef;
    BEGIN
      // Open the right kind of card page
      RecordRef := RecordID.GETRECORD;
      case RecordID.TABLENO of
        DATABASE::Contact:
          begin
            RecordRef.SETTABLE(Contact);
            PAGE.RUN(PAGE::"Contact Card",Contact);
          end;
        DATABASE::Currency:
          begin
            RecordRef.SETTABLE(Currency);
            PAGE.RUN(PAGE::"Currency Card",Currency);
          end;
        DATABASE::Customer:
          begin
            RecordRef.SETTABLE(Customer);
            PAGE.RUN(PAGE::"Customer Card",Customer);
          end;
        DATABASE::Item:
          begin
            RecordRef.SETTABLE(Item);
            PAGE.RUN(PAGE::"Item Card",Item);
          end;
        DATABASE::"Sales Invoice Header":
          begin
            RecordRef.SETTABLE(SalesInvoiceHeader);
            PAGE.RUN(PAGE::"Posted Sales Invoice",SalesInvoiceHeader);
          end;
        DATABASE::Resource:
          begin
            RecordRef.SETTABLE(Resource);
            PAGE.RUN(PAGE::"Resource Card",Resource);
          end;
        DATABASE::"Salesperson/Purchaser":
          begin
            RecordRef.SETTABLE(SalespersonPurchaser);
            PAGE.RUN(PAGE::"Salesperson/Purchaser Card",SalespersonPurchaser);
          end;
        DATABASE::"Unit of Measure":
          begin
            RecordRef.SETTABLE(UnitOfMeasure);
            // There is no Unit of Measure card. Open the list, filtered down to this instance.
            PAGE.RUN(PAGE::"Units of Measure",UnitOfMeasure);
          end;
        DATABASE::"Customer Price Group":
          begin
            RecordRef.SETTABLE(CustomerPriceGroup);
            // There is no Customer Price Group card. Open the list, filtered down to this instance.
            PAGE.RUN(PAGE::"Customer Price Groups",CustomerPriceGroup);
          end;
        else
          ERROR(NoCardPageActionDefinedForTableIdErr,RecordID.TABLENO);
      end;
    END;

    [External]
    PROCEDURE GetCRMEntityTypeName@7(TableId@1000 : Integer) : Text;
    VAR
      TempNameValueBuffer@1001 : TEMPORARY Record "Name/Value Buffer";
      CRMSetupDefaults@1002 : Codeunit "CRM Setup Defaults";
    BEGIN
      CRMSetupDefaults.GetTableIDCRMEntityNameMapping(TempNameValueBuffer);
      TempNameValueBuffer.SETRANGE(Value,FORMAT(TableId));
      if TempNameValueBuffer.FINDFIRST then
        exit(TempNameValueBuffer.Name);
      ERROR(UnableToResolveCRMEntityNameFrmTableIDErr,TableId,CRMProductName.SHORT);
    END;

    LOCAL PROCEDURE GetCoupledCRMID@18(RecordID@1000 : RecordID;VAR CRMID@1001 : GUID) : Boolean;
    VAR
      CRMIntegrationRecord@1002 : Record "CRM Integration Record";
    BEGIN
      exit(CRMIntegrationRecord.FindIDFromRecordID(RecordID,CRMID))
    END;

    LOCAL PROCEDURE DefineCouplingIfNotCoupled@30(RecordID@1000 : RecordID;VAR CRMID@1001 : GUID) : Boolean;
    VAR
      RecordRef@1004 : RecordRef;
    BEGIN
      if GetCoupledCRMID(RecordID,CRMID) then
        exit(true);

      RecordRef.OPEN(RecordID.TABLENO);
      if CONFIRM(STRSUBSTNO(ManageCouplingQst,RecordRef.CAPTION,CRMProductName.FULL),false) then
        if DefineCoupling(RecordID) then
          exit(GetCoupledCRMID(RecordID,CRMID));
      exit(false);
    END;

    [External]
    PROCEDURE DefineCoupling@101(RecordID@1000 : RecordID) : Boolean;
    VAR
      CRMCouplingManagement@1005 : Codeunit "CRM Coupling Management";
      CreateNew@1001 : Boolean;
      Synchronize@1002 : Boolean;
      Direction@1003 : Option;
      CRMID@1004 : GUID;
    BEGIN
      if CRMCouplingManagement.DefineCoupling(RecordID,CRMID,CreateNew,Synchronize,Direction) then begin
        if CreateNew then
          CreateNewRecordsInCRM(RecordID)
        else
          if Synchronize then
            PerformInitialSynchronization(RecordID,CRMID,Direction);
        exit(true);
      end;

      exit(false);
    END;

    [External]
    PROCEDURE ManageCreateNewRecordFromCRM@2(TableID@1000 : Integer);
    BEGIN
      // Extinct method. Kept for backward compatibility.
      case TableID of
        DATABASE::Contact:
          CreateNewContactFromCRM;
        DATABASE::Customer:
          CreateNewCustomerFromCRM;
      end;
    END;

    [External]
    PROCEDURE CreateNewContactFromCRM@69();
    VAR
      IntegrationTableMapping@1001 : Record "Integration Table Mapping";
    BEGIN
      GetIntegrationTableMapping(IntegrationTableMapping,DATABASE::Contact);
      PAGE.RUNMODAL(PAGE::"CRM Contact List");
    END;

    [External]
    PROCEDURE CreateNewCustomerFromCRM@67();
    VAR
      IntegrationTableMapping@1001 : Record "Integration Table Mapping";
    BEGIN
      GetIntegrationTableMapping(IntegrationTableMapping,DATABASE::Customer);
      PAGE.RUNMODAL(PAGE::"CRM Account List");
    END;

    [Internal]
    PROCEDURE ShowCustomerCRMOpportunities@28(Customer@1002 : Record Customer);
    VAR
      CRMOpportunity@1000 : Record "CRM Opportunity";
      CRMID@1001 : GUID;
    BEGIN
      if not IsCRMIntegrationEnabled then
        exit;

      if not DefineCouplingIfNotCoupled(Customer.RECORDID,CRMID) then
        exit;

      CRMOpportunity.FILTERGROUP := 2;
      CRMOpportunity.SETRANGE(ParentAccountId,CRMID);
      CRMOpportunity.SETRANGE(StateCode,CRMOpportunity.StateCode::Open);
      CRMOpportunity.FILTERGROUP := 0;
      PAGE.RUN(PAGE::"CRM Opportunity List",CRMOpportunity);
    END;

    [Internal]
    PROCEDURE ShowCustomerCRMQuotes@3(Customer@1002 : Record Customer);
    VAR
      CRMQuote@1000 : Record "CRM Quote";
      CRMID@1001 : GUID;
    BEGIN
      if not IsCRMIntegrationEnabled then
        exit;

      if not DefineCouplingIfNotCoupled(Customer.RECORDID,CRMID) then
        exit;

      CRMQuote.FILTERGROUP := 2;
      CRMQuote.SETRANGE(CustomerId,CRMID);
      CRMQuote.SETRANGE(StateCode,CRMQuote.StateCode::Active);
      CRMQuote.FILTERGROUP := 0;
      PAGE.RUN(PAGE::"CRM Quote List",CRMQuote);
    END;

    [Internal]
    PROCEDURE ShowCustomerCRMCases@10(Customer@1002 : Record Customer);
    VAR
      CRMIncident@1000 : Record "CRM Incident";
      CRMID@1001 : GUID;
    BEGIN
      if not IsCRMIntegrationEnabled then
        exit;

      if not DefineCouplingIfNotCoupled(Customer.RECORDID,CRMID) then
        exit;

      CRMIncident.FILTERGROUP := 2;
      CRMIncident.SETRANGE(CustomerId,CRMID);
      CRMIncident.SETRANGE(StateCode,CRMIncident.StateCode::Active);
      CRMIncident.FILTERGROUP := 2;
      PAGE.RUN(PAGE::"CRM Case List",CRMIncident);
    END;

    [External]
    PROCEDURE GetNoOfCRMOpportunities@13(Customer@1003 : Record Customer) : Integer;
    VAR
      CRMOpportunity@1002 : Record "CRM Opportunity";
      CRMID@1001 : GUID;
    BEGIN
      if not IsCRMIntegrationEnabled then
        exit(0);

      if not GetCoupledCRMID(Customer.RECORDID,CRMID) then
        exit(0);

      CRMOpportunity.SETRANGE(ParentAccountId,CRMID);
      CRMOpportunity.SETRANGE(StateCode,CRMOpportunity.StateCode::Open);
      exit(CRMOpportunity.COUNT);
    END;

    [External]
    PROCEDURE GetNoOfCRMQuotes@12(Customer@1003 : Record Customer) : Integer;
    VAR
      CRMQuote@1002 : Record "CRM Quote";
      CRMID@1001 : GUID;
    BEGIN
      if not IsCRMIntegrationEnabled then
        exit(0);

      if not GetCoupledCRMID(Customer.RECORDID,CRMID) then
        exit(0);

      CRMQuote.SETRANGE(CustomerId,CRMID);
      CRMQuote.SETRANGE(StateCode,CRMQuote.StateCode::Active);
      exit(CRMQuote.COUNT);
    END;

    [External]
    PROCEDURE GetNoOfCRMCases@31(Customer@1003 : Record Customer) : Integer;
    VAR
      CRMIncident@1002 : Record "CRM Incident";
      CRMID@1000 : GUID;
    BEGIN
      if not IsCRMIntegrationEnabled then
        exit(0);

      if not GetCoupledCRMID(Customer.RECORDID,CRMID) then
        exit(0);

      CRMIncident.SETRANGE(StateCode,CRMIncident.StateCode::Active);
      CRMIncident.SETRANGE(CustomerId,CRMID);
      exit(CRMIncident.COUNT);
    END;

    LOCAL PROCEDURE GetSelectedMultipleSyncDirection@24(IntegrationTableMapping@1003 : Record "Integration Table Mapping") : Integer;
    VAR
      SynchronizeNowQuestion@1004 : Text;
      AllowedDirection@1001 : Integer;
      RecommendedDirection@1000 : Integer;
    BEGIN
      AllowedDirection := IntegrationTableMapping.Direction;
      RecommendedDirection := AllowedDirection;
      case AllowedDirection of
        IntegrationTableMapping.Direction::Bidirectional:
          exit(
            STRMENU(STRSUBSTNO(UpdateNowDirectionQst,CRMProductName.SHORT),RecommendedDirection,UpdateMultipleNowTitleTxt));
        IntegrationTableMapping.Direction::FromIntegrationTable:
          SynchronizeNowQuestion := STRSUBSTNO(UpdateMultipleNowFromCRMQst,CRMProductName.SHORT);
        else
          SynchronizeNowQuestion := STRSUBSTNO(UpdateMultipleNowToCRMQst,CRMProductName.SHORT);
      end;

      if CONFIRM(SynchronizeNowQuestion,true) then
        exit(AllowedDirection);
      exit(0); // user canceled the process
    END;

    LOCAL PROCEDURE GetSelectedSingleSyncDirection@77(IntegrationTableMapping@1003 : Record "Integration Table Mapping";RecordRef@1005 : RecordRef;CRMID@1012 : GUID;VAR RecommendedDirectionIgnored@1006 : Boolean) : Integer;
    VAR
      IntegrationRecSynchInvoke@1007 : Codeunit "Integration Rec. Synch. Invoke";
      CRMRecordRef@1010 : RecordRef;
      RecordIDDescr@1000 : Text;
      SynchronizeNowQuestion@1004 : Text;
      AllowedDirection@1001 : Integer;
      RecommendedDirection@1002 : Integer;
      SelectedDirection@1008 : Integer;
      RecordModified@1013 : Boolean;
      CRMRecordModified@1014 : Boolean;
      DefaultAnswer@1015 : Boolean;
    BEGIN
      AllowedDirection := IntegrationTableMapping.Direction;

      // Determine which sides were modified since last synch
      IntegrationTableMapping.GetRecordRef(CRMID,CRMRecordRef);
      RecordModified := IntegrationRecSynchInvoke.WasModifiedAfterLastSynch(IntegrationTableMapping,RecordRef);
      CRMRecordModified := IntegrationRecSynchInvoke.WasModifiedAfterLastSynch(IntegrationTableMapping,CRMRecordRef);
      RecordIDDescr := FORMAT(RecordRef.RECORDID,0,1);
      if RecordModified and CRMRecordModified then
        // Changes on both sides. Bidirectional: warn user. Unidirectional: confirm and exit.
        case AllowedDirection of
          IntegrationTableMapping.Direction::Bidirectional:
            MESSAGE(BothRecordsModifiedBiDirectionalMsg,RecordRef.CAPTION,CRMRecordRef.CAPTION,CRMProductName.SHORT);
          IntegrationTableMapping.Direction::ToIntegrationTable:
            begin
              if CONFIRM(
                   BothRecordsModifiedToCRMQst,false,RecordIDDescr,CRMRecordRef.CAPTION,PRODUCTNAME.FULL,CRMProductName.SHORT)
              then
                exit(AllowedDirection);
              exit(0);
            end;
          IntegrationTableMapping.Direction::FromIntegrationTable:
            begin
              if CONFIRM(BothRecordsModifiedToNAVQst,false,RecordIDDescr,CRMRecordRef.CAPTION,PRODUCTNAME.SHORT) then
                exit(AllowedDirection);
              exit(0);
            end;
        end;

      // Zero or one side changed. Synch for zero too because dependent objects could have changed.
      case AllowedDirection of
        IntegrationTableMapping.Direction::Bidirectional:
          begin
            // Default from NAV to CRM
            RecommendedDirection := IntegrationTableMapping.Direction::ToIntegrationTable;
            if CRMRecordModified and not RecordModified then
              RecommendedDirection := IntegrationTableMapping.Direction::FromIntegrationTable;
            SelectedDirection :=
              STRMENU(
                STRSUBSTNO(UpdateNowDirectionQst,CRMProductName.SHORT),RecommendedDirection,
                STRSUBSTNO(UpdateOneNowTitleTxt,RecordIDDescr));
            RecommendedDirectionIgnored := SelectedDirection <> RecommendedDirection;
            exit(SelectedDirection);
          end;
        IntegrationTableMapping.Direction::FromIntegrationTable:
          if RecordModified then
            SynchronizeNowQuestion := STRSUBSTNO(UpdateOneNowFromOldCRMQst,RecordIDDescr,PRODUCTNAME.SHORT,CRMProductName.SHORT)
          else begin
            SynchronizeNowQuestion := STRSUBSTNO(UpdateOneNowFromCRMQst,RecordIDDescr,CRMProductName.SHORT);
            DefaultAnswer := true;
          end;
        else
          if CRMRecordModified then
            SynchronizeNowQuestion := STRSUBSTNO(UpdateOneNowToModifiedCRMQst,RecordIDDescr,PRODUCTNAME.SHORT,CRMProductName.SHORT)
          else begin
            SynchronizeNowQuestion := STRSUBSTNO(UpdateOneNowToCRMQst,RecordIDDescr,CRMProductName.SHORT);
            DefaultAnswer := true;
          end;
      end;

      if CONFIRM(SynchronizeNowQuestion,DefaultAnswer) then
        exit(AllowedDirection);

      exit(0); // user canceled the process
    END;

    [EventSubscriber(Table,1400,OnRegisterServiceConnection)]
    [External]
    PROCEDURE HandleCRMRegisterServiceConnection@32(VAR ServiceConnection@1000 : Record "Service Connection");
    VAR
      CRMConnectionSetup@1001 : Record "CRM Connection Setup";
      RecRef@1002 : RecordRef;
    BEGIN
      if not CRMConnectionSetup.GET then begin
        if not CRMConnectionSetup.WRITEPERMISSION then
          exit;
        CRMConnectionSetup.INIT;
        CRMConnectionSetup.INSERT;
      end;

      RecRef.GETTABLE(CRMConnectionSetup);
      ServiceConnection.Status := ServiceConnection.Status::Enabled;
      with CRMConnectionSetup do begin
        if not "Is Enabled" then
          ServiceConnection.Status := ServiceConnection.Status::Disabled
        else begin
          if TestConnection then
            ServiceConnection.Status := ServiceConnection.Status::Connected
          else
            ServiceConnection.Status := ServiceConnection.Status::Error;
        end;
        ServiceConnection.InsertServiceConnectionExtended(
          ServiceConnection,RecRef.RECORDID,TABLECAPTION,"Server Address",PAGE::"CRM Connection Setup",
          PAGE::"CRM Connection Setup Wizard");
      end;
    END;

    [External]
    PROCEDURE ClearState@21();
    BEGIN
      CRMIntegrationEnabledState := CRMIntegrationEnabledState::" "
    END;

    [Internal]
    PROCEDURE GetLastErrorMessage@33() : Text;
    VAR
      ErrorObject@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Exception";
    BEGIN
      ErrorObject := GETLASTERROROBJECT;
      if ISNULL(ErrorObject) then
        exit('');
      if STRPOS(ErrorObject.GetType.Name,'NavCrmException') > 0 then
        if not ISNULL(ErrorObject.InnerException) then
          exit(ErrorObject.InnerException.Message);
      exit(GETLASTERRORTEXT);
    END;

    [TryFunction]
    [Internal]
    PROCEDURE ImportCRMSolution@34(ServerAddress@1006 : Text;IntegrationUserEmail@1017 : Text;AdminUserEmail@1000 : Text;AdminUserPassword@1001 : Text);
    VAR
      ServicePassword@1007 : Record "Service Password";
      URI@1004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      HomeRealmURI@1010 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      ClientCredentials@1018 : DotNet "'System.ServiceModel, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.ServiceModel.Description.ClientCredentials";
      OrganizationServiceProxy@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy";
      CRMHelper@1003 : DotNet "'Microsoft.Dynamics.Nav.CrmCustomizationHelper, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.CrmCustomizationHelper.CrmHelper";
      UserGUID@1005 : GUID;
      IntegrationAdminRoleGUID@1013 : GUID;
      IntegrationUserRoleGUID@1014 : GUID;
    BEGIN
      CheckConnectRequiredFields(ServerAddress,IntegrationUserEmail);
      URI := URI.Uri(ConstructConnectionStringForSolutionImport(ServerAddress));
      ClientCredentials := ClientCredentials.ClientCredentials;
      ClientCredentials.UserName.UserName := AdminUserEmail;
      ServicePassword.GET(AdminUserPassword);
      ClientCredentials.UserName.Password := ServicePassword.GetPassword;
      if not InitializeCRMConnection(URI,HomeRealmURI,ClientCredentials,CRMHelper,OrganizationServiceProxy) then
        ProcessConnectionFailures;

      if ISNULL(OrganizationServiceProxy.ServiceManagement.GetIdentityProvider(AdminUserEmail)) then
        ERROR(STRSUBSTNO(AdminEmailPasswordWrongErr,CRMProductName.SHORT));

      UserGUID := GetUserGUID(OrganizationServiceProxy,IntegrationUserEmail);

      if not CheckSolutionPresence(OrganizationServiceProxy) then
        if not ImportDefaultCRMSolution(CRMHelper,OrganizationServiceProxy) then
          ProcessConnectionFailures;

      IntegrationAdminRoleGUID := GetRoleGUID(OrganizationServiceProxy,GetIntegrationAdminRoleID);
      IntegrationUserRoleGUID := GetRoleGUID(OrganizationServiceProxy,GetIntegrationUserRoleID);

      if not CheckRoleAssignedToUser(OrganizationServiceProxy,UserGUID,IntegrationAdminRoleGUID) then
        AssociateUserWithRole(UserGUID,IntegrationAdminRoleGUID,OrganizationServiceProxy);
      if not CheckRoleAssignedToUser(OrganizationServiceProxy,UserGUID,IntegrationUserRoleGUID) then
        AssociateUserWithRole(UserGUID,IntegrationUserRoleGUID,OrganizationServiceProxy);
    END;

    [TryFunction]
    LOCAL PROCEDURE ImportDefaultCRMSolution@45(CRMHelper@1001 : DotNet "'Microsoft.Dynamics.Nav.CrmCustomizationHelper, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.CrmCustomizationHelper.CrmHelper";VAR OrganizationServiceProxy@1000 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy");
    BEGIN
      CRMHelper.ImportDefaultCrmSolution(OrganizationServiceProxy);
    END;

    LOCAL PROCEDURE AssociateUserWithRole@66(UserGUID@1000 : GUID;RoleGUID@1001 : GUID;VAR OrganizationServiceProxy@1014 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy");
    VAR
      AssociateRequest@1012 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Messages.AssociateRequest";
    BEGIN
      CreateAssociateRequest(UserGUID,RoleGUID,AssociateRequest);
      OrganizationServiceProxy.Execute(AssociateRequest);
    END;

    LOCAL PROCEDURE GetQueryExpression@72(EntityName@1009 : Text;Column@1010 : Text;ConditionField@1011 : Text;ConditionFieldValue@1012 : Text;ErrorMsg@1005 : Text;VAR OrganizationServiceProxy@1006 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy") : GUID;
    VAR
      QueryExpression@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.QueryExpression";
      Entity@1007 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Entity";
      EntityCollection@1013 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.EntityCollection";
      LinkEntity@1014 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.LinkEntity";
    BEGIN
      CreateQueryExpression(EntityName,Column,ConditionField,ConditionFieldValue,LinkEntity,QueryExpression);
      if not ProcessQueryExpression(OrganizationServiceProxy,EntityCollection,QueryExpression) then begin
        OrganizationServiceProxy.Dispose;
        ProcessConnectionFailures;
      end;
      if EntityCollection.Entities.Count = 0 then
        ERROR(STRSUBSTNO(ErrorMsg,ConditionFieldValue));
      Entity := EntityCollection.Item(0);
      exit(Entity.Id);
    END;

    LOCAL PROCEDURE GetUserGUID@41(VAR OrganizationServiceProxy@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy";UserEmail@1000 : Text) : GUID;
    BEGIN
      exit(
        GetQueryExpression(
          'systemuser','systemuserid','internalemailaddress',UserEmail,
          STRSUBSTNO(UserDoesNotExistCRMTxt,UserEmail,CRMProductName.SHORT),OrganizationServiceProxy));
    END;

    LOCAL PROCEDURE GetRoleGUID@43(VAR OrganizationServiceProxy@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy";RoleName@1000 : Text) : GUID;
    BEGIN
      exit(
        GetQueryExpression(
          'role','roleid','roleid',RoleName,STRSUBSTNO(RoleIdDoesNotExistCRMTxt,CRMProductName.SHORT),OrganizationServiceProxy));
    END;

    [External]
    PROCEDURE CheckConnectRequiredFields@46(ServerAddress@1000 : Text;IntegrationUserEmail@1001 : Text);
    BEGIN
      if (IntegrationUserEmail = '') or (ServerAddress = '') then
        ERROR(EmailAndServerAddressEmptyErr);
    END;

    [External]
    PROCEDURE CreateAssociateRequest@44(UserGUID@1001 : GUID;RoleGUID@1000 : GUID;VAR AssociateRequest@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Messages.AssociateRequest");
    VAR
      EntityReferenceCollection@1005 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.EntityReferenceCollection";
      RelationShip@1004 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Relationship";
      EntityReference@1003 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.EntityReference";
    BEGIN
      EntityReferenceCollection := EntityReferenceCollection.EntityReferenceCollection;
      EntityReferenceCollection.Add(EntityReference.EntityReference('role',RoleGUID));
      RelationShip := RelationShip.Relationship('systemuserroles_association');
      AssociateRequest := AssociateRequest.AssociateRequest;
      AssociateRequest.Target(EntityReference.EntityReference('systemuser',UserGUID));
      AssociateRequest.RelatedEntities(EntityReferenceCollection);
      AssociateRequest.Relationship(RelationShip);
    END;

    [External]
    PROCEDURE CreateFilterExpression@64(AttributeName@1003 : Text;Value@1002 : Text;VAR FilterExpression@1000 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.FilterExpression");
    VAR
      ConditionExpression@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.ConditionExpression";
      ConditionOperator@1004 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.ConditionOperator";
    BEGIN
      ConditionExpression :=
        ConditionExpression.ConditionExpression(AttributeName,ConditionOperator.Equal,Value);
      FilterExpression := FilterExpression.FilterExpression;
      FilterExpression.AddCondition(ConditionExpression);
    END;

    [External]
    PROCEDURE CreateLinkEntity@55(VAR LinkEntity@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.LinkEntity";LinkFromEntityName@1000 : Text;LinkFromAttributeName@1002 : Text;LinkToEntityName@1003 : Text;LinkToAttributeName@1004 : Text);
    VAR
      JoinOperator@1005 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.JoinOperator";
    BEGIN
      LinkEntity :=
        LinkEntity.LinkEntity(
          LinkFromEntityName,LinkToEntityName,LinkFromAttributeName,LinkToAttributeName,
          JoinOperator.Inner);
    END;

    [Internal]
    PROCEDURE CreateRoleToUserIDQueryExpression@40(UserIDGUID@1003 : GUID;RoleIDGUID@1004 : GUID;VAR QueryExpression@1000 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.QueryExpression");
    VAR
      LinkEntity1@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.LinkEntity";
      LinkEntity2@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.LinkEntity";
      FilterExpression@1005 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.FilterExpression";
    BEGIN
      CreateLinkEntity(LinkEntity1,'systemuserroles','systemuserid','systemuser','systemuserid');

      CreateFilterExpression('systemuserid',UserIDGUID,FilterExpression);
      LinkEntity1.LinkCriteria(FilterExpression);

      CreateLinkEntity(LinkEntity2,'role','roleid','systemuserroles','roleid');
      LinkEntity2.LinkEntities.Add(LinkEntity1);

      CreateQueryExpression('role','roleid','roleid',RoleIDGUID,LinkEntity2,QueryExpression);
    END;

    [Internal]
    PROCEDURE CreateAnyRoleToUserIDQueryExpression@54(UserIDGUID@1003 : GUID;VAR QueryExpression@1000 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.QueryExpression");
    VAR
      LinkEntity1@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.LinkEntity";
      FilterExpression@1005 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.FilterExpression";
      ColumnSet@1007 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.ColumnSet";
    BEGIN
      CreateLinkEntity(LinkEntity1,'role','roleid','systemuserroles','roleid');

      CreateFilterExpression('systemuserid',UserIDGUID,FilterExpression);
      LinkEntity1.LinkCriteria(FilterExpression);

      QueryExpression := QueryExpression.QueryExpression('role');
      QueryExpression.ColumnSet(ColumnSet.ColumnSet);
      QueryExpression.LinkEntities.Add(LinkEntity1);
    END;

    LOCAL PROCEDURE CheckSolutionPresence@42(VAR OrganizationServiceProxy@1000 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy") : Boolean;
    VAR
      ColumnSet@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.ColumnSet";
      QueryExpression@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.QueryExpression";
      ConditionExpression@1004 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.ConditionExpression";
      ConditionOperator@1003 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.ConditionOperator";
      EntityCollection@1005 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.EntityCollection";
    BEGIN
      QueryExpression := QueryExpression.QueryExpression('solution');
      ColumnSet := ColumnSet.ColumnSet;
      QueryExpression.ColumnSet(ColumnSet);
      ConditionExpression :=
        ConditionExpression.ConditionExpression('uniquename',ConditionOperator.Equal,MicrosoftDynamicsNavIntegrationTxt);
      QueryExpression.Criteria.AddCondition(ConditionExpression);
      if not ProcessQueryExpression(OrganizationServiceProxy,EntityCollection,QueryExpression) then
        ProcessConnectionFailures;
      exit(EntityCollection.Entities.Count > 0);
    END;

    [External]
    PROCEDURE CheckModifyCRMConnectionURL@37(VAR ServerAddress@1006 : Text[250]);
    VAR
      CRMSetupDefaults@1005 : Codeunit "CRM Setup Defaults";
      UriHelper@1004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      UriHelper2@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      UriKindHelper@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.UriKind";
      UriPartialHelper@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.UriPartial";
      ProposedUri@1000 : Text[250];
    BEGIN
      if (ServerAddress = '') or (ServerAddress = '@@test@@') then
        exit;

      ServerAddress := DELCHR(ServerAddress,'<>');

      if not UriHelper.TryCreate(ServerAddress,UriKindHelper.Absolute,UriHelper2) then
        if not UriHelper.TryCreate('https://' + ServerAddress,UriKindHelper.Absolute,UriHelper2) then
          ERROR(InvalidUriErr);

      if UriHelper2.Scheme <> 'https' then begin
        if not CRMSetupDefaults.GetAllowNonSecureConnections then
          ERROR(STRSUBSTNO(MustUseHttpsErr,CRMProductName.SHORT));
        if UriHelper2.Scheme <> 'http' then
          ERROR(STRSUBSTNO(MustUseHttpOrHttpsErr,UriHelper2.Scheme,CRMProductName.SHORT));
      end;

      ProposedUri := UriHelper2.GetLeftPart(UriPartialHelper.Authority);

      // Test that a specific port number is given
      if ((UriHelper2.Port = 443) or (UriHelper2.Port = 80)) and (LOWERCASE(ServerAddress) <> LOWERCASE(ProposedUri)) then begin
        if CONFIRM(STRSUBSTNO(ReplaceServerAddressQst,ServerAddress,ProposedUri)) then
          ServerAddress := ProposedUri;
      end;
    END;

    PROCEDURE GetOrganizationFromUrl@76(ServerAddress@1006 : Text[250]) orgName : Text;
    VAR
      UriHelper@1004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      UriHelper2@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      UriKindHelper@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.UriKind";
    BEGIN
      // Return the organization name from an OnPremise URL which is in the form
      // http://crm-server:port/organization-name
      // Notice that TryCreate will fail if the port is not a number

      if (ServerAddress = '') or (ServerAddress = '@@test@@') then
        exit('');

      ServerAddress := DELCHR(ServerAddress,'<>');

      if not UriHelper.TryCreate(ServerAddress,UriKindHelper.Absolute,UriHelper2) then
        if not UriHelper.TryCreate('https://' + ServerAddress,UriKindHelper.Absolute,UriHelper2) then
          ERROR(InvalidUriErr);

      orgName := UriHelper2.AbsolutePath;
      if orgName = '/' then
        exit('');

      if (orgName <> '') and (STRLEN(orgName) > 1) then
        orgName := COPYSTR(orgName,2);
      exit(orgName);
    END;

    [Internal]
    PROCEDURE CreateQueryExpression@49(EntityName@1000 : Text;Column@1001 : Text;ConditionField@1006 : Text;ConditionFieldValue@1007 : Text;LinkEntity@1003 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.LinkEntity";VAR QueryExpression@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.QueryExpression");
    VAR
      ColumnSet@1005 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.ColumnSet";
      FilterExpression@1004 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.FilterExpression";
    BEGIN
      ColumnSet := ColumnSet.ColumnSet;
      if Column <> '' then
        ColumnSet.AddColumn(Column);
      CreateFilterExpression(ConditionField,ConditionFieldValue,FilterExpression);
      QueryExpression := QueryExpression.QueryExpression(EntityName);
      QueryExpression.ColumnSet(ColumnSet);
      if not ISNULL(LinkEntity) then
        QueryExpression.LinkEntities.Add(LinkEntity);
      QueryExpression.Criteria(FilterExpression);
    END;

    LOCAL PROCEDURE CheckRoleAssignedToUser@59(VAR OrganizationServiceProxy@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy";UserIDGUID@1000 : GUID;RoleIDGUID@1002 : GUID) : Boolean;
    VAR
      QueryExpression@1007 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.QueryExpression";
      EntityCollection@1010 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.EntityCollection";
    BEGIN
      CreateRoleToUserIDQueryExpression(UserIDGUID,RoleIDGUID,QueryExpression);
      if not ProcessQueryExpression(OrganizationServiceProxy,EntityCollection,QueryExpression) then
        ProcessConnectionFailures;
      exit(EntityCollection.Entities.Count > 0);
    END;

    [External]
    PROCEDURE ConstructConnectionStringForSolutionImport@39(ServerAddress@1005 : Text) : Text;
    VAR
      FirstPart@1000 : Text;
      SecondPart@1002 : Text;
      FirstLevel@1001 : Integer;
    BEGIN
      FirstLevel := STRPOS(ServerAddress,'.');
      if FirstLevel = 0 then
        ERROR(STRSUBSTNO(CRMConnectionURLWrongErr,CRMProductName.SHORT));
      FirstPart := COPYSTR(ServerAddress,1,FirstLevel);
      SecondPart := COPYSTR(ServerAddress,FirstLevel);
      exit(STRSUBSTNO(ImportSolutionConnectStringTok,FirstPart,SecondPart));
    END;

    [TryFunction]
    LOCAL PROCEDURE InitializeCRMConnection@47(URI@1004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";HomeRealmURI@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";ClientCredentials@1002 : DotNet "'System.ServiceModel, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.ServiceModel.Description.ClientCredentials";VAR CRMHelper@1005 : DotNet "'Microsoft.Dynamics.Nav.CrmCustomizationHelper, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.CrmCustomizationHelper.CrmHelper";VAR OrganizationServiceProxy@1000 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy");
    BEGIN
      OrganizationServiceProxy := CRMHelper.InitializeServiceProxy(URI,HomeRealmURI,ClientCredentials);
    END;

    [TryFunction]
    LOCAL PROCEDURE ProcessQueryExpression@58(VAR OrganizationServiceProxy@1000 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Client.OrganizationServiceProxy";VAR EntityCollection@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.EntityCollection";QueryExpression@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=8.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.Query.QueryExpression");
    BEGIN
      EntityCollection := OrganizationServiceProxy.RetrieveMultiple(QueryExpression);
    END;

    LOCAL PROCEDURE ProcessConnectionFailures@50();
    VAR
      DotNetExceptionHandler@1000 : Codeunit "DotNet Exception Handler";
      FaultException@1001 : DotNet "'System.ServiceModel, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.ServiceModel.FaultException";
      FileNotFoundException@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.FileNotFoundException";
      ArgumentNullException@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.ArgumentNullException";
      CRMHelper@1005 : DotNet "'Microsoft.Dynamics.Nav.CrmCustomizationHelper, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.CrmCustomizationHelper.CrmHelper";
    BEGIN
      DotNetExceptionHandler.Collect;

      if DotNetExceptionHandler.TryCastToType(GETDOTNETTYPE(FaultException)) then
        ERROR(STRSUBSTNO(AdminEmailPasswordWrongErr,CRMProductName.SHORT));
      if DotNetExceptionHandler.TryCastToType(GETDOTNETTYPE(FileNotFoundException)) then
        ERROR(CRMSolutionFileNotFoundErr);
      if DotNetExceptionHandler.TryCastToType(CRMHelper.OrganizationServiceFaultExceptionType) then
        ERROR(STRSUBSTNO(AdminUserDoesNotHavePriviligesErr,CRMProductName.SHORT));
      if DotNetExceptionHandler.TryCastToType(CRMHelper.SystemNetWebException) then
        ERROR(STRSUBSTNO(CRMConnectionURLWrongErr,CRMProductName.SHORT));
      if DotNetExceptionHandler.CastToType(ArgumentNullException,GETDOTNETTYPE(ArgumentNullException)) then
        if ArgumentNullException.ParamName = 'identityProvider' then
          ERROR(STRSUBSTNO(AdminEmailPasswordWrongErr,CRMProductName.SHORT));
      DotNetExceptionHandler.Rethrow;
    END;

    [Internal]
    PROCEDURE SetupItemAvailabilityService@100();
    VAR
      TenantWebService@1000 : Record "Tenant Web Service";
      WebServiceManagement@1001 : Codeunit "Web Service Management";
    BEGIN
      WebServiceManagement.CreateTenantWebService(
        TenantWebService."Object Type"::Page,PAGE::"Product Item Availability",'ProductItemAvailability',true);
    END;

    LOCAL PROCEDURE GetIntegrationAdminRoleID@29() : Text;
    BEGIN
      exit('8c8d4f51-a72b-e511-80d9-3863bb349780');
    END;

    LOCAL PROCEDURE GetIntegrationUserRoleID@53() : Text;
    BEGIN
      exit('6f960e32-a72b-e511-80d9-3863bb349780');
    END;

    LOCAL PROCEDURE SendRestoredSyncNotification@73(Counter@1000 : Integer);
    VAR
      Msg@1001 : Text;
    BEGIN
      if Counter = 1 then
        Msg := SyncRestoredMsg
      else
        Msg := STRSUBSTNO(SyncMultipleRestoredMsg,Counter);
      SendNotification(Msg);
    END;

    [External]
    PROCEDURE SendResultNotification@1(RecVariant@1000 : Variant) : Boolean;
    VAR
      CRMIntegrationRecord@1001 : Record "CRM Integration Record";
      IntegrationSynchJob@1004 : Record "Integration Synch. Job";
      RecordRef@1002 : RecordRef;
      NotificationMessage@1003 : Text;
    BEGIN
      RecordRef.GETTABLE(RecVariant);
      if CRMIntegrationRecord.FindByRecordID(RecordRef.RECORDID) then begin
        if CRMIntegrationRecord.Skipped then
          exit(SendSkippedSyncNotification(CRMIntegrationRecord."Integration ID"));

        case CRMIntegrationRecord."Last Synch. CRM Result" of
          CRMIntegrationRecord."Last Synch. CRM Result"::Failure:
            begin
              IntegrationSynchJob.GET(CRMIntegrationRecord."Last Synch. CRM Job ID");
              NotificationMessage := IntegrationSynchJob.GetErrorForRecordID(RecordRef.RECORDID);
            end;
        end;
        case CRMIntegrationRecord."Last Synch. Result" of
          CRMIntegrationRecord."Last Synch. Result"::Failure:
            begin
              IntegrationSynchJob.GET(CRMIntegrationRecord."Last Synch. Job ID");
              NotificationMessage := IntegrationSynchJob.GetErrorForRecordID(RecordRef.RECORDID);
            end;
        end;
      end else begin
        CLEAR(IntegrationSynchJob);
        IntegrationSynchJob."Synch. Direction" := IntegrationSynchJob."Synch. Direction"::ToIntegrationTable;
        NotificationMessage := IntegrationSynchJob.GetErrorForRecordID(RecordRef.RECORDID);
      end;
      if NotificationMessage <> '' then
        exit(SendNotification(NotificationMessage));
    END;

    LOCAL PROCEDURE SendSkippedSyncNotification@52(IntegrationID@1000 : GUID) : Boolean;
    VAR
      SyncNotification@1001 : Notification;
    BEGIN
      SyncNotification.ID := CREATEGUID;
      SyncNotification.MESSAGE(SyncSkippedMsg);
      SyncNotification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
      SyncNotification.SETDATA('IntegrationID',IntegrationID);
      SyncNotification.ADDACTION(DetailsTxt,CODEUNIT::"CRM Integration Management",'ShowSkippedRecords');
      SyncNotification.SEND;
      exit(true);
    END;

    LOCAL PROCEDURE SendSyncNotification@74(RecordCounter@1001 : ARRAY [4] OF Integer) : Boolean;
    BEGIN
      if RecordCounter[NoOf::Total] = 1 then begin
        if RecordCounter[NoOf::Scheduled] = 1 then
          exit(SendNotification(SyncNowScheduledMsg));
        if RecordCounter[NoOf::Skipped] = 1 then
          exit(SendNotification(SyncNowSkippedMsg));
        exit(SendNotification(SyncNowFailedMsg));
      end;
      exit(SendMultipleSyncNotification(RecordCounter));
    END;

    LOCAL PROCEDURE SendMultipleSyncNotification@65(RecordCounter@1001 : ARRAY [4] OF Integer) : Boolean;
    BEGIN
      exit(
        SendNotification(
          STRSUBSTNO(
            SyncMultipleMsg,
            RecordCounter[NoOf::Scheduled],RecordCounter[NoOf::Failed],
            RecordCounter[NoOf::Skipped],RecordCounter[NoOf::Total])));
    END;

    LOCAL PROCEDURE SendNotification@63(Msg@1000 : Text) : Boolean;
    VAR
      SyncNotification@1001 : Notification;
    BEGIN
      SyncNotification.ID := CREATEGUID;
      SyncNotification.MESSAGE(Msg);
      SyncNotification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
      SyncNotification.SEND;
      exit(true);
    END;

    [External]
    PROCEDURE ShowLog@11(RecId@1000 : RecordID);
    VAR
      CRMIntegrationRecord@1003 : Record "CRM Integration Record";
      IntegrationTableMapping@1001 : Record "Integration Table Mapping";
    BEGIN
      GetIntegrationTableMapping(IntegrationTableMapping,RecId.TABLENO);
      CRMIntegrationRecord.FindByRecordID(RecId);
      IntegrationTableMapping.ShowLog(CRMIntegrationRecord.GetLatestJobIDFilter);
    END;

    [External]
    PROCEDURE ShowSkippedRecords@26(SkippedSyncNotification@1000 : Notification);
    VAR
      CRMIntegrationRecord@1002 : Record "CRM Integration Record";
      IntegrationID@1001 : GUID;
    BEGIN
      if EVALUATE(IntegrationID,SkippedSyncNotification.GETDATA('IntegrationID')) then begin
        CRMIntegrationRecord.SETRANGE("Integration ID",IntegrationID);
        if CRMIntegrationRecord.FINDFIRST then;
      end;
      CRMIntegrationRecord.RESET;
      PAGE.RUN(PAGE::"CRM Skipped Records",CRMIntegrationRecord);
    END;

    [External]
    PROCEDURE CoupleCRMEntity@79(RecordID@1001 : RecordID;CRMID@1003 : GUID;VAR Synchronize@1005 : Boolean;VAR Direction@1004 : Option) : Boolean;
    VAR
      CRMIntegrationRecord@1002 : Record "CRM Integration Record";
      CouplingRecordBuffer@1007 : Record "Coupling Record Buffer";
    BEGIN
      CouplingRecordBuffer.Initialize(RecordID);
      CouplingRecordBuffer."CRM ID" := CRMID;
      if not CouplingRecordBuffer.INSERT then
        CouplingRecordBuffer.MODIFY;
      if not ISNULLGUID(CouplingRecordBuffer."CRM ID") then begin
        CRMIntegrationRecord.CoupleRecordIdToCRMID(CouplingRecordBuffer."NAV Record ID",CouplingRecordBuffer."CRM ID");
        if CouplingRecordBuffer.GetPerformInitialSynchronization then begin
          Synchronize := true;
          Direction := CouplingRecordBuffer.GetInitialSynchronizationDirection;
          PerformInitialSynchronization(CouplingRecordBuffer."NAV Record ID",CouplingRecordBuffer."CRM ID",Direction);
        end;
      end else
        exit(false);
      exit(true);
    END;

    BEGIN
    END.
  }
}

