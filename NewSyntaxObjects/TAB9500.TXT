OBJECT Table 9500 Email Item
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Email Item;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;GUID          ;CaptionML=ENU=ID }
    { 2   ;   ;From Name           ;Text100       ;CaptionML=ENU=From Name }
    { 3   ;   ;From Address        ;Text250       ;OnValidate=VAR
                                                                MailManagement@1000 : Codeunit "Mail Management";
                                                              BEGIN
                                                                if "From Address" <> '' then
                                                                  MailManagement.CheckValidEmailAddresses("From Address");
                                                              END;

                                                   CaptionML=ENU=From Address }
    { 4   ;   ;Send to             ;Text250       ;OnValidate=BEGIN
                                                                if "Send to" <> '' then
                                                                  CorrectAndValidateEmailList("Send to");
                                                              END;

                                                   CaptionML=ENU=Send to }
    { 5   ;   ;Send CC             ;Text250       ;OnValidate=BEGIN
                                                                if "Send CC" <> '' then
                                                                  CorrectAndValidateEmailList("Send CC");
                                                              END;

                                                   CaptionML=ENU=Send CC }
    { 6   ;   ;Send BCC            ;Text250       ;OnValidate=BEGIN
                                                                if "Send BCC" <> '' then
                                                                  CorrectAndValidateEmailList("Send BCC");
                                                              END;

                                                   CaptionML=ENU=Send BCC }
    { 7   ;   ;Subject             ;Text250       ;CaptionML=ENU=Subject }
    { 8   ;   ;Body                ;BLOB          ;CaptionML=ENU=Body }
    { 9   ;   ;Attachment File Path;Text250       ;CaptionML=ENU=Attachment File Path }
    { 10  ;   ;Attachment Name     ;Text250       ;CaptionML=ENU=Attachment Name }
    { 11  ;   ;Plaintext Formatted ;Boolean       ;InitValue=true;
                                                   OnValidate=BEGIN
                                                                if "Plaintext Formatted" then
                                                                  VALIDATE("Body File Path",'')
                                                                else
                                                                  SetBodyText('');
                                                              END;

                                                   CaptionML=ENU=Plaintext Formatted }
    { 12  ;   ;Body File Path      ;Text250       ;OnValidate=BEGIN
                                                                if "Body File Path" <> '' then
                                                                  TESTFIELD("Plaintext Formatted",false);
                                                              END;

                                                   CaptionML=ENU=Body File Path }
    { 13  ;   ;Message Type        ;Option        ;CaptionML=ENU=Message Type;
                                                   OptionCaptionML=ENU=Custom Message,From Email Body Template;
                                                   OptionString="Custom Message","From Email Body Template" }
    { 21  ;   ;Attachment File Path 2;Text250     ;CaptionML=ENU=Attachment File Path 2 }
    { 22  ;   ;Attachment Name 2   ;Text50        ;CaptionML=ENU=Attachment Name 2 }
    { 23  ;   ;Attachment File Path 3;Text250     ;CaptionML=ENU=Attachment File Path 3 }
    { 24  ;   ;Attachment Name 3   ;Text50        ;CaptionML=ENU=Attachment Name 3 }
    { 25  ;   ;Attachment File Path 4;Text250     ;CaptionML=ENU=Attachment File Path 4 }
    { 26  ;   ;Attachment Name 4   ;Text50        ;CaptionML=ENU=Attachment Name 4 }
    { 27  ;   ;Attachment File Path 5;Text250     ;CaptionML=ENU=Attachment File Path 5 }
    { 28  ;   ;Attachment Name 5   ;Text50        ;CaptionML=ENU=Attachment Name 5 }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      O365EmailSetup@1000 : Record "O365 Email Setup";

    [External]
    PROCEDURE Init@1();
    BEGIN
      ID := CREATEGUID;
    END;

    [Internal]
    PROCEDURE Send@2(HideMailDialog@1000 : Boolean) : Boolean;
    VAR
      MailManagement@1001 : Codeunit "Mail Management";
    BEGIN
      MailManagement.SendMailOrDownload(Rec,HideMailDialog);
      exit(MailManagement.IsSent);
    END;

    [External]
    PROCEDURE SetBodyText@5(Value@1000 : Text);
    VAR
      DataStream@1001 : OutStream;
      BodyText@1002 : BigText;
    BEGIN
      CLEAR(Body);
      BodyText.ADDTEXT(Value);
      Body.CREATEOUTSTREAM(DataStream,TEXTENCODING::UTF8);
      BodyText.WRITE(DataStream);
    END;

    [Internal]
    PROCEDURE GetBodyText@3() Value : Text;
    VAR
      TempBlob@1005 : Record TempBlob;
      FileManagement@1001 : Codeunit "File Management";
      DataStream@1004 : InStream;
      BlobInStream@1003 : InStream;
      BodyOutStream@1002 : OutStream;
      BodyText@1000 : BigText;
    BEGIN
      // Note this is intended only to get the body in memory - not from the database.
      Value := '';

      // If the body doesn't have a value, attempt to import the value from the file path, otherwise exit.
      if not Body.HASVALUE then begin
        if ("Body File Path" <> '') and FileManagement.ServerFileExists("Body File Path") then begin
          TempBlob.INIT;
          FileManagement.BLOBImportFromServerFile(TempBlob,"Body File Path");
          TempBlob.Blob.CREATEINSTREAM(BlobInStream,TEXTENCODING::UTF8);
          Body.CREATEOUTSTREAM(BodyOutStream);
          COPYSTREAM(BodyOutStream,BlobInStream);
        end else
          exit;
      end;

      if "Plaintext Formatted" then begin
        Body.CREATEINSTREAM(DataStream,TEXTENCODING::UTF8);
        BodyText.READ(DataStream);
        BodyText.GETSUBTEXT(Value,1);
      end else begin
        Body.CREATEINSTREAM(DataStream,TEXTENCODING::UTF8);
        DataStream.READ(Value);
      end;

      exit(Value);
    END;

    LOCAL PROCEDURE CorrectAndValidateEmailList@6(VAR EmailAddresses@1000 : Text[250]);
    VAR
      MailManagement@1001 : Codeunit "Mail Management";
    BEGIN
      EmailAddresses := CONVERTSTR(EmailAddresses,',',';');
      EmailAddresses := DELCHR(EmailAddresses,'<>');
      MailManagement.CheckValidEmailAddresses(EmailAddresses);
    END;

    [External]
    PROCEDURE AddCcBcc@4();
    BEGIN
      "Send CC" := O365EmailSetup.GetCCAddressesFromO365EmailSetup;
      "Send BCC" := O365EmailSetup.GetBCCAddressesFromO365EmailSetup;
    END;

    [Internal]
    PROCEDURE AttachIncomingDocuments@7(SalesInvoiceNo@1001 : Code[20]);
    VAR
      SalesHeader@1000 : Record "Sales Header";
      SalesInvoiceHeader@1002 : Record "Sales Invoice Header";
      IncomingDocumentAttachment@1003 : Record "Incoming Document Attachment";
      FileManagement@1006 : Codeunit "File Management";
      InStr@1007 : InStream;
      OutStr@1008 : OutStream;
      File@1009 : File;
      AttachmentCounter@1004 : Integer;
      FilePath@1005 : Text[250];
      AttachmentName@1010 : Text[50];
      IsPostedDocument@1011 : Boolean;
    BEGIN
      if SalesInvoiceNo = '' then
        exit;
      IsPostedDocument := true;
      if not SalesInvoiceHeader.GET(SalesInvoiceNo) then begin
        if not SalesHeader.GET(SalesHeader."Document Type"::Quote,SalesInvoiceNo) then
          exit;
        if SalesHeader."Incoming Document Entry No." = 0 then
          exit;
        IsPostedDocument := false;
      end;

      if IsPostedDocument then begin
        IncomingDocumentAttachment.SETRANGE("Document No.",SalesInvoiceNo);
        IncomingDocumentAttachment.SETRANGE("Posting Date",SalesInvoiceHeader."Posting Date");
      end else
        IncomingDocumentAttachment.SETRANGE("Incoming Document Entry No.",SalesHeader."Incoming Document Entry No.");

      IncomingDocumentAttachment.SETAUTOCALCFIELDS(Content);
      if IncomingDocumentAttachment.FINDSET then
        repeat
          if IncomingDocumentAttachment.Content.HASVALUE then begin
            AttachmentCounter += 1;
            FilePath := COPYSTR(FileManagement.ServerTempFileName(IncomingDocumentAttachment."File Extension"),1,MAXSTRLEN(FilePath));
            File.CREATE(FilePath);
            File.CREATEOUTSTREAM(OutStr);
            IncomingDocumentAttachment.Content.CREATEINSTREAM(InStr);
            COPYSTREAM(OutStr,InStr);
            File.CLOSE;
            AttachmentName :=
              COPYSTR(
                STRSUBSTNO(
                  '%1.%2',IncomingDocumentAttachment.Name,IncomingDocumentAttachment."File Extension"),1,MAXSTRLEN(AttachmentName));
            case AttachmentCounter of
              1:
                begin
                  "Attachment File Path 2" := FilePath;
                  "Attachment Name 2" := AttachmentName;
                end;
              2:
                begin
                  "Attachment File Path 3" := FilePath;
                  "Attachment Name 3" := AttachmentName;
                end;
              3:
                begin
                  "Attachment File Path 4" := FilePath;
                  "Attachment Name 4" := AttachmentName;
                end;
              4:
                begin
                  "Attachment File Path 5" := FilePath;
                  "Attachment Name 5" := AttachmentName;
                end;
            end;
          end;
        until (AttachmentCounter = 4) or (IncomingDocumentAttachment.NEXT = 0);
    END;

    BEGIN
    END.
  }
}

