OBJECT Codeunit 5655 Insurance Jnl.-B.Post
{
  OBJECT-PROPERTIES
  {
    Date=20120907D;
    Time=120000T;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo="Insurance Journal Batch";
    OnRun=BEGIN
            InsuranceJnlBatch.COPY(Rec);
            Code;
            COPY(InsuranceJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.';
      InsuranceJnlTempl@1004 : Record "Insurance Journal Template";
      InsuranceJnlBatch@1005 : Record "Insurance Journal Batch";
      InsuranceJnlLine@1006 : Record "Insurance Journal Line";
      InsuranceJnlPostBatch@1007 : Codeunit "Insurance Jnl.-Post Batch";
      JournalWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with InsuranceJnlBatch do begin
        InsuranceJnlTempl.GET("Journal Template Name");
        InsuranceJnlTempl.TESTFIELD("Force Posting Report",false);

        if not CONFIRM(Text000,false) then
          exit;

        FIND('-');
        repeat
          InsuranceJnlLine."Journal Template Name" := "Journal Template Name";
          InsuranceJnlLine."Journal Batch Name" := Name;
          InsuranceJnlLine."Line No." := 1;

          CLEAR(InsuranceJnlPostBatch);
          if InsuranceJnlPostBatch.RUN(InsuranceJnlLine) then
            MARK(false)
          else begin
            MARK(true);
            JournalWithErrors := true;
          end;
        until NEXT = 0;

        if not JournalWithErrors then
          MESSAGE(Text001)
        else
          MESSAGE(
            Text002 +
            Text003);

        if not FIND('=><') then begin
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP := 0;
          Name := '';
        end;
      end;
    END;

    BEGIN
    END.
  }
}

