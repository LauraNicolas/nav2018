OBJECT Table 5640 Main Asset Component
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               LockFixedAsset;
               if "FA No." <> '' then begin
                 FA.GET("FA No.");
                 UpdateMainAsset(FA,0);
               end;
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=ENU=Main Asset Component;
  }
  FIELDS
  {
    { 1   ;   ;Main Asset No.      ;Code20        ;TableRelation="Fixed Asset";
                                                   CaptionML=ENU=Main Asset No.;
                                                   NotBlank=true;
                                                   Editable=false }
    { 3   ;   ;FA No.              ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=BEGIN
                                                                if ("FA No." = '') or ("Main Asset No." = '') then
                                                                  exit;
                                                                LockFixedAsset;
                                                                FA.GET("FA No.");
                                                                if "FA No." = "Main Asset No." then
                                                                  CreateError("FA No.",1);
                                                                Description := FA.Description;
                                                                MainAssetComp.SETRANGE("Main Asset No.","FA No.");
                                                                if MainAssetComp.FINDFIRST then
                                                                  CreateError("FA No.",1);
                                                                MainAssetComp.SETRANGE("Main Asset No.");
                                                                MainAssetComp.SETCURRENTKEY("FA No.");
                                                                MainAssetComp.SETRANGE("FA No.","FA No.");
                                                                if MainAssetComp.FINDFIRST then
                                                                  CreateError("FA No.",2);
                                                                MainAssetComp.SETRANGE("FA No.","Main Asset No.");
                                                                if MainAssetComp.FINDFIRST then
                                                                  CreateError("Main Asset No.",1);
                                                                UpdateMainAsset(FA,2);
                                                                FA.GET("Main Asset No.");
                                                                if FA."Main Asset/Component" <> FA."Main Asset/Component"::"Main Asset" then begin
                                                                  FA."Main Asset/Component" := FA."Main Asset/Component"::"Main Asset";
                                                                  ERROR(
                                                                    Text001,
                                                                    DepreciationCalc.FAName(FA,''),FA."Main Asset/Component");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=FA No.;
                                                   NotBlank=true }
    { 4   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
  }
  KEYS
  {
    {    ;"Main Asset No.","FA No."               ;Clustered=true }
    {    ;"FA No."                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot rename a %1.';
      Text001@1001 : TextConst 'ENU=%1 is not a %2.';
      Text002@1002 : TextConst 'ENU=%1 is a %2.';
      FA@1003 : Record "Fixed Asset";
      FADeprBook@1004 : Record "FA Depreciation Book";
      MainAssetComp@1005 : Record "Main Asset Component";
      DepreciationCalc@1006 : Codeunit "Depreciation Calculation";
      Text003@1007 : TextConst 'ENU=Main Asset,Component';

    LOCAL PROCEDURE LockFixedAsset@6();
    BEGIN
      FA.LOCKTABLE;
      FADeprBook.LOCKTABLE;
    END;

    LOCAL PROCEDURE UpdateMainAsset@1(VAR FA@1000 : Record "Fixed Asset";ComponentType@1001 : '" ","Main Asset",Component');
    VAR
      FA2@1002 : Record "Fixed Asset";
    BEGIN
      if ComponentType = ComponentType::" " then begin
        FA."Main Asset/Component" := FA."Main Asset/Component"::" ";
        FA."Component of Main Asset" := '';
      end;
      if ComponentType = ComponentType::Component then begin
        FA."Component of Main Asset" := "Main Asset No.";
        FA."Main Asset/Component" := FA."Main Asset/Component"::Component;
      end;
      FA.MODIFY(true);
      UpdateFADeprBooks(FA);

      FA.RESET;
      FA.SETCURRENTKEY("Component of Main Asset");
      FA.SETRANGE("Component of Main Asset","Main Asset No.");
      FA.SETRANGE("Main Asset/Component",FA2."Main Asset/Component"::Component);
      FA2.GET("Main Asset No.");
      if FA.FIND('=><') then begin
        if FA2."Main Asset/Component" <> FA2."Main Asset/Component"::"Main Asset" then begin
          FA2."Main Asset/Component" := FA2."Main Asset/Component"::"Main Asset";
          FA2."Component of Main Asset" := FA2."No.";
          FA2.MODIFY(true);
          UpdateFADeprBooks(FA2);
        end;
      end else begin
        FA2."Main Asset/Component" := FA2."Main Asset/Component"::" ";
        FA2."Component of Main Asset" := '';
        FA2.MODIFY(true);
        UpdateFADeprBooks(FA2);
      end;
    END;

    LOCAL PROCEDURE UpdateFADeprBooks@3(VAR FA@1000 : Record "Fixed Asset");
    BEGIN
      with FADeprBook do begin
        SETCURRENTKEY("FA No.");
        SETRANGE("FA No.",FA."No.");
        if FIND('-') then
          repeat
            "Main Asset/Component" := FA."Main Asset/Component";
            "Component of Main Asset" := FA."Component of Main Asset";
            MODIFY(true);
          until NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE CreateError@2(FANo@1000 : Code[20];MainAssetComponent@1001 : '" ","Main Asset",Component');
    BEGIN
      FA."No." := FANo;
      ERROR(
        Text002,
        DepreciationCalc.FAName(FA,''),SELECTSTR(MainAssetComponent,Text003));
    END;

    BEGIN
    END.
  }
}

