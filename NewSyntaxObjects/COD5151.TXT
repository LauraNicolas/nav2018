OBJECT Codeunit 5151 Integration Service
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE GetDeletedIntegrationItems@2(VAR IntegrationRecords@1000 : XMLport "Integration Records";FromDateTime@1001 : DateTime;MaxRecords@1002 : Integer;PageID@1003 : Integer);
    VAR
      IntegrationRecord@1004 : Record "Integration Record";
    BEGIN
      IntegrationRecord.SETCURRENTKEY("Page ID","Deleted On");
      IntegrationRecord.SETRANGE("Page ID",PageID);
      if FromDateTime = 0DT then
        IntegrationRecord.SETFILTER("Deleted On",'<>%1',0DT)
      else
        IntegrationRecord.SETFILTER("Deleted On",'>=%1',FromDateTime);
      IntegrationRecords.SetMaxRecords(MaxRecords);
      IntegrationRecords.SETTABLEVIEW(IntegrationRecord);
    END;

    [External]
    PROCEDURE GetModifiedIntegrationItems@3(VAR IntegrationRecords@1000 : XMLport "Integration Records";FromDateTime@1001 : DateTime;MaxRecords@1002 : Integer;PageID@1003 : Integer);
    VAR
      IntegrationRecord@1004 : Record "Integration Record";
    BEGIN
      IntegrationRecord.SETCURRENTKEY("Page ID","Modified On");
      IntegrationRecord.SETRANGE("Page ID",PageID);
      IntegrationRecord.SETFILTER("Deleted On",'=%1',0DT);
      if FromDateTime <> 0DT then
        IntegrationRecord.SETFILTER("Modified On",'>=%1',FromDateTime);
      IntegrationRecords.SetMaxRecords(MaxRecords);
      IntegrationRecords.SETTABLEVIEW(IntegrationRecord);
    END;

    [External]
    PROCEDURE UpdateIntegrationID@4(RecIDIn@1000 : Text[1024];IntegrationID@1001 : GUID);
    VAR
      IntegrationRecord@1002 : Record "Integration Record";
      RecID@1003 : RecordID;
    BEGIN
      EVALUATE(RecID,RecIDIn);
      IntegrationRecord.SETRANGE("Record ID",RecID);
      IntegrationRecord.FINDFIRST;
      IntegrationRecord.RENAME(IntegrationID);
    END;

    [External]
    PROCEDURE GetRecIDFromIntegrationID@5(IntegrationID@1000 : GUID) : Text[1024];
    VAR
      IntegrationRecord@1001 : Record "Integration Record";
    BEGIN
      if IntegrationRecord.GET(IntegrationID) then
        exit(FORMAT(IntegrationRecord."Record ID"));
    END;

    [External]
    PROCEDURE GetIntegrationIDFromRecID@6(RecIDIn@1000 : Text[1024]) : GUID;
    VAR
      IntegrationRecord@1001 : Record "Integration Record";
      RecID@1002 : RecordID;
    BEGIN
      EVALUATE(RecID,RecIDIn);
      IntegrationRecord.SETRANGE("Record ID",RecID);
      IntegrationRecord.FINDFIRST;
      exit(IntegrationRecord."Integration ID");
    END;

    [External]
    PROCEDURE GetVersion@7() : Text[30];
    BEGIN
      exit('1.0.0.0');
    END;

    BEGIN
    END.
  }
}

