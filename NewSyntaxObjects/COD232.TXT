OBJECT Codeunit 232 Gen. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="Gen. Journal Line";
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the report(s)?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.';
      GenJnlTemplate@1005 : Record "Gen. Journal Template";
      GenJnlLine@1006 : Record "Gen. Journal Line";
      GLReg@1007 : Record "G/L Register";
      CustLedgEntry@1010 : Record "Cust. Ledger Entry";
      VendLedgEntry@1011 : Record "Vendor Ledger Entry";
      TempJnlBatchName@1009 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      with GenJnlLine do begin
        GenJnlTemplate.GET("Journal Template Name");
        if GenJnlTemplate."Force Posting Report" or
           (GenJnlTemplate."Cust. Receipt Report ID" = 0) and (GenJnlTemplate."Vendor Receipt Report ID" = 0)
        then
          GenJnlTemplate.TESTFIELD("Posting Report ID");
        if GenJnlTemplate.Recurring and (GETFILTER("Posting Date") <> '') then
          FIELDERROR("Posting Date",Text000);

        if not CONFIRM(Text001,false) then
          exit;

        TempJnlBatchName := "Journal Batch Name";

        CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post Batch",GenJnlLine);

        if GLReg.GET("Line No.") then begin
          if GenJnlTemplate."Cust. Receipt Report ID" <> 0 then begin
            CustLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
            REPORT.RUN(GenJnlTemplate."Cust. Receipt Report ID",false,false,CustLedgEntry);
          end;
          if GenJnlTemplate."Vendor Receipt Report ID" <> 0 then begin
            VendLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
            REPORT.RUN(GenJnlTemplate."Vendor Receipt Report ID",false,false,VendLedgEntry);
          end;
          if GenJnlTemplate."Posting Report ID" <> 0 then begin
            GLReg.SETRECFILTER;
            REPORT.RUN(GenJnlTemplate."Posting Report ID",false,false,GLReg);
          end;
        end;

        if "Line No." = 0 then
          MESSAGE(Text002)
        else
          if TempJnlBatchName = "Journal Batch Name" then
            MESSAGE(Text003)
          else
            MESSAGE(
              Text004,
              "Journal Batch Name");

        if not FIND('=><') or (TempJnlBatchName <> "Journal Batch Name") then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        end;
      end;
    END;

    BEGIN
    END.
  }
}

