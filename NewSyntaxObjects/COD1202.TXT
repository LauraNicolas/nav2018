OBJECT Codeunit 1202 Import Payroll Transaction
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Data Exch."=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      FileMgt@1000 : Codeunit "File Management";
      ImportPayrollTransCap@1002 : TextConst 'ENU=Select Payroll Transaction';
      FileFilterTxt@1001 : TextConst 'ENU="Text Files(*.txt;*.csv)|*.txt;*.csv"';
      FileFilterExtensionTxt@1004 : TextConst '@@@={Locked};ENU=txt,csv';
      ProcessingSetupErr@1003 : TextConst 'ENU=You must specify either a reading/writing XMLport or a reading/writing codeunit.';

    [Internal]
    PROCEDURE SelectAndImportPayrollDataToGL@2(VAR GenJournalLine@1000 : Record "Gen. Journal Line";DataExchDefCode@1001 : Code[20]);
    VAR
      TempBlob@1002 : Record TempBlob;
      FileName@1003 : Text;
    BEGIN
      FileName := FileMgt.BLOBImportWithFilter(TempBlob,ImportPayrollTransCap,'',FileFilterTxt,FileFilterExtensionTxt);
      if FileName <> '' then
        ImportPayrollDataToGL(GenJournalLine,FileName,TempBlob,DataExchDefCode);
    END;

    [Internal]
    PROCEDURE ImportPayrollDataToGL@18(GenJournalLine@1000 : Record "Gen. Journal Line";FileName@1005 : Text;TempBlob@1001 : Record TempBlob;DataExchDefCode@1002 : Code[20]);
    VAR
      GenJournalLineTemplate@1060 : Record "Gen. Journal Line";
      DataExch@1003 : Record "Data Exch.";
      DataExchDef@1004 : Record "Data Exch. Def";
    BEGIN
      DataExchDef.GET(DataExchDefCode);
      if (DataExchDef."Reading/Writing XMLport" <> 0) = (DataExchDef."Reading/Writing Codeunit" <> 0) then
        ERROR(ProcessingSetupErr);

      PrepareGenJournalLine(GenJournalLineTemplate,GenJournalLine);
      CreateDataExch(DataExch,FileName,TempBlob,DataExchDefCode);
      ImportPayrollFile(DataExch);
      MapDataToGenJournalLine(DataExch,GenJournalLineTemplate);
    END;

    LOCAL PROCEDURE CreateDataExch@9(VAR DataExch@1004 : Record "Data Exch.";FileName@1002 : Text;TempBlob@1000 : Record TempBlob;DataExchDefCode@1001 : Code[20]);
    VAR
      Source@1003 : InStream;
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(Source);
      DataExch.InsertRec(COPYSTR(FileName,1,MAXSTRLEN(DataExch."File Name")),Source,DataExchDefCode);
    END;

    LOCAL PROCEDURE ImportPayrollFile@11(DataExch@1000 : Record "Data Exch.");
    VAR
      DataExchDef@1006 : Record "Data Exch. Def";
      Source@1002 : InStream;
    BEGIN
      DataExch."File Content".CREATEINSTREAM(Source);
      DataExch.SETRANGE("Entry No.",DataExch."Entry No.");
      DataExchDef.GET(DataExch."Data Exch. Def Code");
      if DataExchDef."Reading/Writing XMLport" <> 0 then
        XMLPORT.IMPORT(DataExchDef."Reading/Writing XMLport",Source,DataExch)
      else
        if DataExchDef."Reading/Writing Codeunit" <> 0 then
          CODEUNIT.RUN(DataExchDef."Reading/Writing Codeunit",DataExch);
    END;

    LOCAL PROCEDURE MapDataToGenJournalLine@14(DataExch@1000 : Record "Data Exch.";GenJournalLineTemplate@1001 : Record "Gen. Journal Line");
    VAR
      ProcessDataExch@1003 : Codeunit "Process Data Exch.";
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(GenJournalLineTemplate);
      ProcessDataExch.ProcessAllLinesColumnMapping(DataExch,RecRef);
    END;

    LOCAL PROCEDURE PrepareGenJournalLine@1060(VAR GenJournalLineTemplate@1060 : Record "Gen. Journal Line";GenJournalLine@1061 : Record "Gen. Journal Line");
    BEGIN
      with GenJournalLineTemplate do begin
        "Journal Template Name" := GenJournalLine."Journal Template Name";
        "Journal Batch Name" := GenJournalLine."Journal Batch Name";
        SetUpNewLine(GenJournalLine,GenJournalLine."Balance (LCY)",true);
        "Account Type" := "Account Type"::"G/L Account";

        GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");
        GenJournalLine.SETRANGE("Journal Batch Name",GenJournalLine."Journal Batch Name");
        if GenJournalLine.FINDLAST then begin
          "Line No." := GenJournalLine."Line No.";
          "Document No." := INCSTR(GenJournalLine."Document No.");
        end else
          "Document No." := GenJournalLine."Document No.";
      end;
    END;

    BEGIN
    END.
  }
}

