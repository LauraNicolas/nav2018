OBJECT Codeunit 233 Gen. Jnl.-B.Post
{
  OBJECT-PROPERTIES
  {
    Date=20161025D;
    Time=120000T;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo="Gen. Journal Batch";
    OnRun=BEGIN
            GenJnlBatch.COPY(Rec);
            Code;
            COPY(GenJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.';
      GenJnlTemplate@1004 : Record "Gen. Journal Template";
      GenJnlBatch@1005 : Record "Gen. Journal Batch";
      GenJnlLine@1006 : Record "Gen. Journal Line";
      GenJnlPostBatch@1007 : Codeunit "Gen. Jnl.-Post Batch";
      JnlWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      with GenJnlBatch do begin
        GenJnlTemplate.GET("Journal Template Name");
        GenJnlTemplate.TESTFIELD("Force Posting Report",false);

        if not CONFIRM(Text000,false) then
          exit;

        FIND('-');
        repeat
          GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
          GenJnlLine.SETRANGE("Journal Batch Name",Name);
          if GenJnlLine.FINDFIRST then begin
            CLEAR(GenJnlPostBatch);
            if GenJnlPostBatch.RUN(GenJnlLine) then
              MARK(false)
            else begin
              MARK(true);
              JnlWithErrors := true;
            end;
          end;
        until NEXT = 0;

        if not JnlWithErrors then
          MESSAGE(Text001)
        else begin
          MARKEDONLY(true);
          MESSAGE(
            Text002 +
            Text003);
        end;

        if not FIND('=><') then begin
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP(0);
          Name := '';
        end;
      end;
    END;

    BEGIN
    END.
  }
}

