OBJECT Report 6661 Delete Invd Purch. Ret. Orders
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Delete Invd Purch. Ret. Orders;
    ProcessingOnly=true;
  }
  DATASET
  {
    { 4458;    ;DataItem;Purchase Header     ;
               DataItemTable="Purchase Header";
               DataItemTableView=SORTING("Document Type","No.") WHERE("Document Type"=CONST("Return Order"));
               ReqFilterHeadingML=ENU=Purchase Return Order;
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit "Approvals Mgmt.";
                                  PostPurchDelete@1001 : Codeunit "PostPurch-Delete";
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  AllLinesDeleted := true;
                                  ItemChargeAssgntPurch.RESET;
                                  ItemChargeAssgntPurch.SETRANGE("Document Type","Document Type");
                                  ItemChargeAssgntPurch.SETRANGE("Document No.","No.");
                                  PurchLine.RESET;
                                  PurchLine.SETRANGE("Document Type","Document Type");
                                  PurchLine.SETRANGE("Document No.","No.");
                                  PurchLine.SETFILTER("Quantity Invoiced",'<>0');
                                  if PurchLine.FIND('-') then begin
                                    PurchLine.SETRANGE("Quantity Invoiced");
                                    PurchLine.SETFILTER("Outstanding Quantity",'<>0');
                                    if not PurchLine.FIND('-') then begin
                                      PurchLine.SETRANGE("Outstanding Quantity");
                                      PurchLine.SETFILTER("Return Qty. Shipped Not Invd.",'<>0');
                                      if not PurchLine.FIND('-') then begin
                                        PurchLine.LOCKTABLE;
                                        if not PurchLine.FIND('-') then begin
                                          PurchLine.SETRANGE("Return Qty. Shipped Not Invd.");
                                          if PurchLine.FIND('-') then
                                            repeat
                                              PurchLine.CALCFIELDS("Qty. Assigned");
                                              if ((PurchLine."Qty. Assigned" = PurchLine."Quantity Invoiced") and
                                                  (PurchLine."Qty. Assigned" <> 0)) or
                                                 (PurchLine.Type <> PurchLine.Type::"Charge (Item)")
                                              then begin
                                                if PurchLine.Type = PurchLine.Type::"Charge (Item)" then begin
                                                  ItemChargeAssgntPurch.SETRANGE("Document Line No.",PurchLine."Line No.");
                                                  ItemChargeAssgntPurch.DELETEALL;
                                                end;
                                                if PurchLine.HASLINKS then
                                                  PurchLine.DELETELINKS;
                                                PurchLine.DELETE;
                                              end else
                                                AllLinesDeleted := false;
                                            until PurchLine.NEXT = 0;

                                          if AllLinesDeleted then begin
                                            PostPurchDelete.DeleteHeader(
                                              "Purchase Header",PurchRcptHeader,PurchInvHeader,PurchCrMemoHeader,
                                              ReturnShptHeader,PrepmtPurchInvHeader,PrepmtPurchCrMemoHeader);
                                            ReservePurchLine.DeleteInvoiceSpecFromHeader("Purchase Header");

                                            PurchCommentLine.SETRANGE("Document Type","Document Type");
                                            PurchCommentLine.SETRANGE("No.","No.");
                                            PurchCommentLine.DELETEALL;

                                            WhseRequest.SETRANGE("Source Type",DATABASE::"Purchase Line");
                                            WhseRequest.SETRANGE("Source Subtype","Document Type");
                                            WhseRequest.SETRANGE("Source No.","No.");
                                            if not WhseRequest.ISEMPTY then
                                              WhseRequest.DELETEALL(true);

                                            ApprovalsMgmt.DeleteApprovalEntries(RECORDID);

                                            DELETE;
                                          end;
                                          COMMIT;
                                        end;
                                      end;
                                    end;
                                  end;
                                END;

               ReqFilterFields="No.","Buy-from Vendor No.","Pay-to Vendor No." }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing purch. orders #1##########';
      PurchLine@1001 : Record "Purchase Line";
      PurchRcptHeader@1011 : Record "Purch. Rcpt. Header";
      PurchInvHeader@1010 : Record "Purch. Inv. Header";
      PurchCrMemoHeader@1009 : Record "Purch. Cr. Memo Hdr.";
      ReturnShptHeader@1008 : Record "Return Shipment Header";
      PrepmtPurchInvHeader@1015 : Record "Purch. Inv. Header";
      PrepmtPurchCrMemoHeader@1014 : Record "Purch. Cr. Memo Hdr.";
      PurchCommentLine@1002 : Record "Purch. Comment Line";
      ItemChargeAssgntPurch@1005 : Record "Item Charge Assignment (Purch)";
      WhseRequest@1007 : Record "Warehouse Request";
      ReservePurchLine@1013 : Codeunit "Purch. Line-Reserve";
      Window@1004 : Dialog;
      AllLinesDeleted@1006 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

