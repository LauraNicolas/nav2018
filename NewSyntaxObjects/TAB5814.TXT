OBJECT Table 5814 Inventory Period
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               if not IsValidDate("Ending Date") then
                 ERROR(Text002,TABLECAPTION,"Ending Date");
             END;

    OnDelete=BEGIN
               TESTFIELD(Closed,false);
               InvtPeriodEntry.SETRANGE("Ending Date","Ending Date");
               InvtPeriodEntry.DELETEALL;
             END;

    OnRename=BEGIN
               TESTFIELD(Closed,false);
               if InvtPeriodEntryExists(xRec."Ending Date") then
                 ERROR(Text001,TABLECAPTION,InvtPeriodEntry.TABLECAPTION);

               if not IsValidDate("Ending Date") then
                 ERROR(Text001,TABLECAPTION,"Ending Date");
             END;

    CaptionML=ENU=Inventory Period;
    LookupPageID="Inventory Periods";
  }
  FIELDS
  {
    { 1   ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                Name := FORMAT("Ending Date",0,Text000);
                                                              END;

                                                   CaptionML=ENU=Ending Date;
                                                   NotBlank=true }
    { 2   ;   ;Name                ;Text50        ;CaptionML=ENU=Name }
    { 3   ;   ;Closed              ;Boolean       ;CaptionML=ENU=Closed;
                                                   Editable=false }
  }
  KEYS
  {
    {    ;"Ending Date"                           ;Clustered=true }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;"Ending Date",Name,Closed                }
    { 2   ;Brick               ;"Ending Date",Name,Closed                }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=<Month Text> <Year4>';
      Text001@1002 : TextConst 'ENU=You cannot rename the %1 because there is at least one %2 in this period.';
      InvtPeriodEntry@1003 : Record "Inventory Period Entry";
      Text002@1004 : TextConst 'ENU=You are not allowed to insert an %1 that ends before %2.';
      Text003@1005 : TextConst 'ENU=You cannot post before %1 because the %2 is already closed. You must re-open the period first.';

    LOCAL PROCEDURE InvtPeriodEntryExists@1(EndingDate@1001 : Date) : Boolean;
    VAR
      InvtPeriodEntry@1000 : Record "Inventory Period Entry";
    BEGIN
      InvtPeriodEntry.SETRANGE("Ending Date",EndingDate);
      exit(not InvtPeriodEntry.ISEMPTY);
    END;

    [External]
    PROCEDURE IsValidDate@56(VAR EndingDate@1000 : Date) : Boolean;
    VAR
      InvtPeriod@1001 : Record "Inventory Period";
    BEGIN
      InvtPeriod.SETFILTER("Ending Date",'>=%1',EndingDate);
      InvtPeriod.SETRANGE(Closed,true);
      if InvtPeriod.FINDLAST then
        EndingDate := InvtPeriod."Ending Date"
      else
        exit(true);
    END;

    [External]
    PROCEDURE ShowError@2(PostingDate@1001 : Date);
    BEGIN
      ERROR(Text003,CALCDATE('<+1D>',PostingDate),TABLECAPTION);
    END;

    [External]
    PROCEDURE IsInvtPeriodClosed@3(EndingDate@1000 : Date) : Boolean;
    VAR
      AccPeriod@1001 : Record "Accounting Period";
    BEGIN
      AccPeriod.SETFILTER("Starting Date",'>=%1',EndingDate);
      if not AccPeriod.FIND('-') then
        exit(false);
      if AccPeriod.NEXT <> 0 then
        EndingDate := CALCDATE('<-1D>',AccPeriod."Starting Date");

      SETFILTER("Ending Date",'>=%1',EndingDate);
      SETRANGE(Closed,true);
      exit(not ISEMPTY);
    END;

    BEGIN
    END.
  }
}

