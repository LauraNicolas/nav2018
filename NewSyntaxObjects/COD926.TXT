OBJECT Codeunit 926 Assembly Line-Reserve
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Reservation Entry"=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CreateReservEntry@1013 : Codeunit "Create Reserv. Entry";
      ReservMgt@1017 : Codeunit "Reservation Management";
      ReservEngineMgt@1014 : Codeunit "Reservation Engine Mgt.";
      SetFromType@1010 : Integer;
      SetFromSubtype@1009 : Integer;
      SetFromID@1008 : Code[20];
      SetFromBatchName@1007 : Code[10];
      SetFromProdOrderLine@1006 : Integer;
      SetFromRefNo@1005 : Integer;
      SetFromVariantCode@1004 : Code[10];
      SetFromLocationCode@1003 : Code[10];
      SetFromSerialNo@1002 : Code[20];
      SetFromLotNo@1001 : Code[20];
      SetFromQtyPerUOM@1000 : Decimal;
      Text000@1012 : TextConst 'ENU=Reserved quantity cannot be greater than %1.';
      Text001@1011 : TextConst 'ENU=Codeunit is not initialized correctly.';
      DeleteItemTracking@1016 : Boolean;
      Text002@1019 : TextConst '@@@=starts with "Due Date";ENU=must be filled in when a quantity is reserved';
      Text003@1018 : TextConst '@@@=starts with some field name;ENU=must not be changed when a quantity is reserved';

    [External]
    PROCEDURE CreateReservation@1(AssemblyLine@1000 : Record "Assembly Line";Description@1001 : Text[50];ExpectedReceiptDate@1002 : Date;Quantity@1006 : Decimal;QuantityBase@1003 : Decimal;ForSerialNo@1004 : Code[20];ForLotNo@1005 : Code[20]);
    VAR
      ShipmentDate@1007 : Date;
    BEGIN
      if SetFromType = 0 then
        ERROR(Text001);

      AssemblyLine.TESTFIELD(Type,AssemblyLine.Type::Item);
      AssemblyLine.TESTFIELD("No.");
      AssemblyLine.TESTFIELD("Due Date");

      AssemblyLine.CALCFIELDS("Reserved Qty. (Base)");
      if ABS(AssemblyLine."Remaining Quantity (Base)") < ABS(AssemblyLine."Reserved Qty. (Base)") + QuantityBase then
        ERROR(
          Text000,
          ABS(AssemblyLine."Remaining Quantity (Base)") - ABS(AssemblyLine."Reserved Qty. (Base)"));

      AssemblyLine.TESTFIELD("Variant Code",SetFromVariantCode);
      AssemblyLine.TESTFIELD("Location Code",SetFromLocationCode);

      if QuantityBase * SignFactor(AssemblyLine) < 0 then
        ShipmentDate := AssemblyLine."Due Date"
      else begin
        ShipmentDate := ExpectedReceiptDate;
        ExpectedReceiptDate := AssemblyLine."Due Date";
      end;

      CreateReservEntry.CreateReservEntryFor(
        DATABASE::"Assembly Line",AssemblyLine."Document Type",
        AssemblyLine."Document No.",'',0,AssemblyLine."Line No.",AssemblyLine."Qty. per Unit of Measure",
        Quantity,QuantityBase,ForSerialNo,ForLotNo);
      CreateReservEntry.CreateReservEntryFrom(
        SetFromType,SetFromSubtype,SetFromID,SetFromBatchName,SetFromProdOrderLine,SetFromRefNo,
        SetFromQtyPerUOM,SetFromSerialNo,SetFromLotNo);
      CreateReservEntry.CreateReservEntry(
        AssemblyLine."No.",AssemblyLine."Variant Code",AssemblyLine."Location Code",
        Description,ExpectedReceiptDate,ShipmentDate);

      SetFromType := 0;
    END;

    LOCAL PROCEDURE CreateBindingReservation@18(AssemblyLine@1000 : Record "Assembly Line";Description@1001 : Text[50];ExpectedReceiptDate@1002 : Date;Quantity@1006 : Decimal;QuantityBase@1003 : Decimal);
    BEGIN
      CreateReservation(AssemblyLine,Description,ExpectedReceiptDate,Quantity,QuantityBase,'','');
    END;

    [External]
    PROCEDURE CreateReservationSetFrom@9(TrackingSpecificationFrom@1000 : Record "Tracking Specification");
    BEGIN
      with TrackingSpecificationFrom do begin
        SetFromType := "Source Type";
        SetFromSubtype := "Source Subtype";
        SetFromID := "Source ID";
        SetFromBatchName := "Source Batch Name";
        SetFromProdOrderLine := "Source Prod. Order Line";
        SetFromRefNo := "Source Ref. No.";
        SetFromVariantCode := "Variant Code";
        SetFromLocationCode := "Location Code";
        SetFromSerialNo := "Serial No.";
        SetFromLotNo := "Lot No.";
        SetFromQtyPerUOM := "Qty. per Unit of Measure";
      end;
    END;

    LOCAL PROCEDURE SignFactor@21(AssemblyLine@1000 : Record "Assembly Line") : Integer;
    BEGIN
      if AssemblyLine."Document Type" in [2,3,5] then
        ERROR(Text001);

      exit(-1);
    END;

    [External]
    PROCEDURE SetBinding@15(Binding@1000 : '" ","Order-to-Order"');
    BEGIN
      CreateReservEntry.SetBinding(Binding);
    END;

    [External]
    PROCEDURE FilterReservFor@12(VAR FilterReservEntry@1000 : Record "Reservation Entry";AssemblyLine@1001 : Record "Assembly Line");
    BEGIN
      FilterReservEntry.SETRANGE("Source Type",DATABASE::"Assembly Line");
      FilterReservEntry.SETRANGE("Source Subtype",AssemblyLine."Document Type");
      FilterReservEntry.SETRANGE("Source ID",AssemblyLine."Document No.");
      FilterReservEntry.SETRANGE("Source Batch Name",'');
      FilterReservEntry.SETRANGE("Source Prod. Order Line",0);
      FilterReservEntry.SETRANGE("Source Ref. No.",AssemblyLine."Line No.");
    END;

    [External]
    PROCEDURE FindReservEntry@4(AssemblyLine@1000 : Record "Assembly Line";VAR ReservEntry@1001 : Record "Reservation Entry") : Boolean;
    BEGIN
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,false);
      FilterReservFor(ReservEntry,AssemblyLine);
      exit(ReservEntry.FINDLAST);
    END;

    LOCAL PROCEDURE AssignForPlanning@7(VAR AssemblyLine@1000 : Record "Assembly Line");
    VAR
      PlanningAssignment@1001 : Record "Planning Assignment";
    BEGIN
      with AssemblyLine do begin
        if "Document Type" <> "Document Type"::Order then
          exit;

        if Type <> Type::Item then
          exit;

        if "No." <> '' then
          PlanningAssignment.ChkAssignOne("No.","Variant Code","Location Code",WORKDATE);
      end;
    END;

    [External]
    PROCEDURE ReservEntryExist@16(AssemblyLine@1000 : Record "Assembly Line") : Boolean;
    VAR
      ReservEntry@1002 : Record "Reservation Entry";
      ReservEngineMgt@1001 : Codeunit "Reservation Engine Mgt.";
    BEGIN
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,false);
      FilterReservFor(ReservEntry,AssemblyLine);
      exit(not ReservEntry.ISEMPTY);
    END;

    [External]
    PROCEDURE DeleteLine@3(VAR AssemblyLine@1000 : Record "Assembly Line");
    BEGIN
      with AssemblyLine do begin
        ReservMgt.SetAssemblyLine(AssemblyLine);
        if DeleteItemTracking then
          ReservMgt.SetItemTrackingHandling(1); // Allow Deletion
        ReservMgt.DeleteReservEntries(true,0);
        ReservMgt.ClearActionMessageReferences;
        CALCFIELDS("Reserved Qty. (Base)");
        AssignForPlanning(AssemblyLine);
      end;
    END;

    [External]
    PROCEDURE SetDeleteItemTracking@13(AllowDirectDeletion@1000 : Boolean);
    BEGIN
      DeleteItemTracking := AllowDirectDeletion;
    END;

    [External]
    PROCEDURE VerifyChange@59(VAR NewAssemblyLine@1000 : Record "Assembly Line";VAR OldAssemblyLine@1001 : Record "Assembly Line");
    VAR
      AssemblyLine@1005 : Record "Assembly Line";
      ReservEntry@1002 : Record "Reservation Entry";
      ShowError@1003 : Boolean;
      HasError@1004 : Boolean;
    BEGIN
      if (NewAssemblyLine.Type <> NewAssemblyLine.Type::Item) and (OldAssemblyLine.Type <> OldAssemblyLine.Type::Item) then
        exit;

      if NewAssemblyLine."Line No." = 0 then
        if not AssemblyLine.GET(NewAssemblyLine."Document Type",NewAssemblyLine."Document No.",NewAssemblyLine."Line No.") then
          exit;

      NewAssemblyLine.CALCFIELDS("Reserved Qty. (Base)");
      ShowError := NewAssemblyLine."Reserved Qty. (Base)" <> 0;

      if NewAssemblyLine."Due Date" = 0D then begin
        if ShowError then
          NewAssemblyLine.FIELDERROR("Due Date",Text002);
        HasError := true;
      end;

      if NewAssemblyLine.Type <> OldAssemblyLine.Type then begin
        if ShowError then
          NewAssemblyLine.FIELDERROR(Type,Text003);
        HasError := true;
      end;

      if NewAssemblyLine."No." <> OldAssemblyLine."No." then begin
        if ShowError then
          NewAssemblyLine.FIELDERROR("No.",Text003);
        HasError := true;
      end;

      if NewAssemblyLine."Location Code" <> OldAssemblyLine."Location Code" then begin
        if ShowError then
          NewAssemblyLine.FIELDERROR("Location Code",Text003);
        HasError := true;
      end;

      if (NewAssemblyLine.Type = NewAssemblyLine.Type::Item) and (OldAssemblyLine.Type = OldAssemblyLine.Type::Item) and
         (NewAssemblyLine."Bin Code" <> OldAssemblyLine."Bin Code")
      then
        if not ReservMgt.CalcIsAvailTrackedQtyInBin(
             NewAssemblyLine."No.",NewAssemblyLine."Bin Code",
             NewAssemblyLine."Location Code",NewAssemblyLine."Variant Code",
             DATABASE::"Assembly Line",NewAssemblyLine."Document Type",
             NewAssemblyLine."Document No.",'',0,NewAssemblyLine."Line No.")
        then begin
          if ShowError then
            NewAssemblyLine.FIELDERROR("Bin Code",Text003);
          HasError := true;
        end;

      if NewAssemblyLine."Variant Code" <> OldAssemblyLine."Variant Code" then begin
        if ShowError then
          NewAssemblyLine.FIELDERROR("Variant Code",Text003);
        HasError := true;
      end;

      if NewAssemblyLine."Line No." <> OldAssemblyLine."Line No." then
        HasError := true;

      if HasError then
        if (NewAssemblyLine."No." <> OldAssemblyLine."No.") or
           FindReservEntry(NewAssemblyLine,ReservEntry)
        then begin
          if NewAssemblyLine."No." <> OldAssemblyLine."No." then begin
            ReservMgt.SetAssemblyLine(OldAssemblyLine);
            ReservMgt.DeleteReservEntries(true,0);
            ReservMgt.SetAssemblyLine(NewAssemblyLine);
          end else begin
            ReservMgt.SetAssemblyLine(NewAssemblyLine);
            ReservMgt.DeleteReservEntries(true,0);
          end;
          ReservMgt.AutoTrack(NewAssemblyLine."Remaining Quantity (Base)");
        end;

      if HasError or (NewAssemblyLine."Due Date" <> OldAssemblyLine."Due Date") then begin
        AssignForPlanning(NewAssemblyLine);
        if (NewAssemblyLine."No." <> OldAssemblyLine."No.") or
           (NewAssemblyLine."Variant Code" <> OldAssemblyLine."Variant Code") or
           (NewAssemblyLine."Location Code" <> OldAssemblyLine."Location Code")
        then
          AssignForPlanning(OldAssemblyLine);
      end;
    END;

    [External]
    PROCEDURE VerifyQuantity@10(VAR NewAssemblyLine@1000 : Record "Assembly Line";VAR OldAssemblyLine@1001 : Record "Assembly Line");
    VAR
      AssemblyLine@1002 : Record "Assembly Line";
    BEGIN
      with NewAssemblyLine do begin
        if Type <> Type::Item then
          exit;
        if "Line No." = OldAssemblyLine."Line No." then
          if "Remaining Quantity (Base)" = OldAssemblyLine."Remaining Quantity (Base)" then
            exit;
        if "Line No." = 0 then
          if not AssemblyLine.GET("Document Type","Document No.","Line No.") then
            exit;

        ReservMgt.SetAssemblyLine(NewAssemblyLine);
        if "Qty. per Unit of Measure" <> OldAssemblyLine."Qty. per Unit of Measure" then
          ReservMgt.ModifyUnitOfMeasure;
        ReservMgt.DeleteReservEntries(false,"Remaining Quantity (Base)");
        ReservMgt.ClearSurplus;
        ReservMgt.AutoTrack("Remaining Quantity (Base)");
        AssignForPlanning(NewAssemblyLine);
      end;
    END;

    [External]
    PROCEDURE Caption@30(AssemblyLine@1001 : Record "Assembly Line") CaptionText@1000 : Text[80];
    BEGIN
      CaptionText :=
        STRSUBSTNO('%1 %2 %3',AssemblyLine."Document Type",AssemblyLine."Document No.",AssemblyLine."Line No.");
    END;

    [External]
    PROCEDURE CallItemTracking@2(VAR AssemblyLine@1000 : Record "Assembly Line");
    VAR
      TrackingSpecification@1001 : Record "Tracking Specification";
      ItemTrackingLines@1002 : Page "Item Tracking Lines";
    BEGIN
      TrackingSpecification.InitFromAsmLine(AssemblyLine);
      ItemTrackingLines.SetSourceSpec(TrackingSpecification,AssemblyLine."Due Date");
      ItemTrackingLines.SetInbound(AssemblyLine.IsInbound);
      ItemTrackingLines.RUNMODAL;
    END;

    [External]
    PROCEDURE DeleteLineConfirm@5(VAR AssemblyLine@1000 : Record "Assembly Line") : Boolean;
    BEGIN
      with AssemblyLine do begin
        if not ReservEntryExist(AssemblyLine) then
          exit(true);

        ReservMgt.SetAssemblyLine(AssemblyLine);
        if ReservMgt.DeleteItemTrackingConfirm then
          DeleteItemTracking := true;
      end;

      exit(DeleteItemTracking);
    END;

    [External]
    PROCEDURE UpdateItemTrackingAfterPosting@11(AssemblyLine@1000 : Record "Assembly Line");
    VAR
      ReservEntry@1003 : Record "Reservation Entry";
      CreateReservEntry@1001 : Codeunit "Create Reserv. Entry";
    BEGIN
      // Used for updating Quantity to Handle and Quantity to Invoice after posting
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,false);
      ReservEntry.SETRANGE("Source Type",DATABASE::"Assembly Line");
      ReservEntry.SETRANGE("Source Subtype",AssemblyLine."Document Type");
      ReservEntry.SETRANGE("Source ID",AssemblyLine."Document No.");
      ReservEntry.SETRANGE("Source Batch Name",'');
      ReservEntry.SETRANGE("Source Prod. Order Line",0);
      CreateReservEntry.UpdateItemTrackingAfterPosting(ReservEntry);
    END;

    [External]
    PROCEDURE TransferAsmLineToItemJnlLine@35(VAR AssemblyLine@1000 : Record "Assembly Line";VAR ItemJnlLine@1001 : Record "Item Journal Line";TransferQty@1002 : Decimal;CheckApplFromItemEntry@1004 : Boolean) : Decimal;
    VAR
      OldReservEntry@1003 : Record "Reservation Entry";
    BEGIN
      if TransferQty = 0 then
        exit;
      if not FindReservEntry(AssemblyLine,OldReservEntry) then
        exit(TransferQty);

      ItemJnlLine.TESTFIELD("Item No.",AssemblyLine."No.");
      ItemJnlLine.TESTFIELD("Variant Code",AssemblyLine."Variant Code");
      ItemJnlLine.TESTFIELD("Location Code",AssemblyLine."Location Code");

      OldReservEntry.Lock;

      if ReservEngineMgt.InitRecordSet(OldReservEntry) then begin
        repeat
          OldReservEntry.TESTFIELD("Item No.",AssemblyLine."No.");
          OldReservEntry.TESTFIELD("Variant Code",AssemblyLine."Variant Code");
          OldReservEntry.TESTFIELD("Location Code",AssemblyLine."Location Code");

          if CheckApplFromItemEntry then begin
            OldReservEntry.TESTFIELD("Appl.-from Item Entry");
            CreateReservEntry.SetApplyFromEntryNo(OldReservEntry."Appl.-from Item Entry");
          end;

          TransferQty := CreateReservEntry.TransferReservEntry(DATABASE::"Item Journal Line",
              ItemJnlLine."Entry Type",ItemJnlLine."Journal Template Name",
              ItemJnlLine."Journal Batch Name",0,ItemJnlLine."Line No.",
              ItemJnlLine."Qty. per Unit of Measure",OldReservEntry,TransferQty);

        until (ReservEngineMgt.NEXTRecord(OldReservEntry) = 0) or (TransferQty = 0);
        CheckApplFromItemEntry := false;
      end;
      exit(TransferQty);
    END;

    [External]
    PROCEDURE TransferAsmLineToAsmLine@31(VAR OldAssemblyLine@1000 : Record "Assembly Line";VAR NewAssemblyLine@1001 : Record "Assembly Line";TransferQty@1002 : Decimal);
    VAR
      OldReservEntry@1003 : Record "Reservation Entry";
      Status@1004 : 'Reservation,Tracking,Surplus,Prospect';
    BEGIN
      if TransferQty = 0 then
        exit;

      if not FindReservEntry(OldAssemblyLine,OldReservEntry) then
        exit;

      OldReservEntry.Lock;

      NewAssemblyLine.TESTFIELD("No.",OldAssemblyLine."No.");
      NewAssemblyLine.TESTFIELD("Variant Code",OldAssemblyLine."Variant Code");
      NewAssemblyLine.TESTFIELD("Location Code",OldAssemblyLine."Location Code");

      for Status := Status::Reservation to Status::Prospect do begin
        OldReservEntry.SETRANGE("Reservation Status",Status);
        if OldReservEntry.FINDSET then
          repeat
            OldReservEntry.TESTFIELD("Item No.",OldAssemblyLine."No.");
            OldReservEntry.TESTFIELD("Variant Code",OldAssemblyLine."Variant Code");
            OldReservEntry.TESTFIELD("Location Code",OldAssemblyLine."Location Code");

            TransferQty := CreateReservEntry.TransferReservEntry(DATABASE::"Assembly Line",
                NewAssemblyLine."Document Type",NewAssemblyLine."Document No.",'',0,
                NewAssemblyLine."Line No.",NewAssemblyLine."Qty. per Unit of Measure",OldReservEntry,TransferQty);

          until (OldReservEntry.NEXT = 0) or (TransferQty = 0);
      end;
    END;

    [External]
    PROCEDURE BindToPurchase@6(AsmLine@1000 : Record "Assembly Line";PurchLine@1001 : Record "Purchase Line";ReservQty@1002 : Decimal;ReservQtyBase@1003 : Decimal);
    VAR
      TrackingSpecification@1004 : Record "Tracking Specification";
      ReservationEntry@1005 : Record "Reservation Entry";
    BEGIN
      SetBinding(ReservationEntry.Binding::"Order-to-Order");
      TrackingSpecification.InitTrackingSpecification2(
        DATABASE::"Purchase Line",PurchLine."Document Type",PurchLine."Document No.",'',0,PurchLine."Line No.",
        PurchLine."Variant Code",PurchLine."Location Code",PurchLine."Qty. per Unit of Measure");
      CreateReservationSetFrom(TrackingSpecification);
      CreateBindingReservation(AsmLine,PurchLine.Description,PurchLine."Expected Receipt Date",ReservQty,ReservQtyBase);
    END;

    [External]
    PROCEDURE BindToProdOrder@17(AsmLine@1003 : Record "Assembly Line";ProdOrderLine@1002 : Record "Prod. Order Line";ReservQty@1001 : Decimal;ReservQtyBase@1000 : Decimal);
    VAR
      TrackingSpecification@1005 : Record "Tracking Specification";
      ReservationEntry@1004 : Record "Reservation Entry";
    BEGIN
      SetBinding(ReservationEntry.Binding::"Order-to-Order");
      TrackingSpecification.InitTrackingSpecification2(
        DATABASE::"Prod. Order Line",ProdOrderLine.Status,ProdOrderLine."Prod. Order No.",'',ProdOrderLine."Line No.",0,
        ProdOrderLine."Variant Code",ProdOrderLine."Location Code",ProdOrderLine."Qty. per Unit of Measure");
      CreateReservationSetFrom(TrackingSpecification);
      CreateBindingReservation(AsmLine,ProdOrderLine.Description,ProdOrderLine."Ending Date",ReservQty,ReservQtyBase);
    END;

    [External]
    PROCEDURE BindToAssembly@8(AsmLine@1003 : Record "Assembly Line";AsmHeader@1002 : Record "Assembly Header";ReservQty@1001 : Decimal;ReservQtyBase@1000 : Decimal);
    VAR
      TrackingSpecification@1005 : Record "Tracking Specification";
      ReservationEntry@1004 : Record "Reservation Entry";
    BEGIN
      SetBinding(ReservationEntry.Binding::"Order-to-Order");
      TrackingSpecification.InitTrackingSpecification2(
        DATABASE::"Assembly Header",AsmHeader."Document Type",AsmHeader."No.",'',0,0,
        AsmHeader."Variant Code",AsmHeader."Location Code",AsmHeader."Qty. per Unit of Measure");
      CreateReservationSetFrom(TrackingSpecification);
      CreateBindingReservation(AsmLine,AsmHeader.Description,AsmHeader."Due Date",ReservQty,ReservQtyBase);
    END;

    [External]
    PROCEDURE BindToTransfer@14(AsmLine@1003 : Record "Assembly Line";TransLine@1002 : Record "Transfer Line";ReservQty@1001 : Decimal;ReservQtyBase@1000 : Decimal);
    VAR
      TrackingSpecification@1005 : Record "Tracking Specification";
      ReservationEntry@1004 : Record "Reservation Entry";
    BEGIN
      SetBinding(ReservationEntry.Binding::"Order-to-Order");
      TrackingSpecification.InitTrackingSpecification2(
        DATABASE::"Transfer Line",1,TransLine."Document No.",'',0,TransLine."Line No.",
        TransLine."Variant Code",TransLine."Transfer-to Code",TransLine."Qty. per Unit of Measure");
      CreateReservationSetFrom(TrackingSpecification);
      CreateBindingReservation(AsmLine,TransLine.Description,TransLine."Receipt Date",ReservQty,ReservQtyBase);
    END;

    BEGIN
    END.
  }
}

