OBJECT Table 7004 Sales Line Discount
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               if "Sales Type" = "Sales Type"::"All Customers" then
                 "Sales Code" := ''
               else
                 TESTFIELD("Sales Code");
               TESTFIELD(Code);
             END;

    OnRename=BEGIN
               if "Sales Type" <> "Sales Type"::"All Customers" then
                 TESTFIELD("Sales Code");
               TESTFIELD(Code);
             END;

    CaptionML=ENU=Sales Line Discount;
    LookupPageID="Sales Line Discounts";
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST("Item Disc. Group")) "Item Discount Group";
                                                   OnValidate=VAR
                                                                Item@1000 : Record Item;
                                                              BEGIN
                                                                if xRec.Code <> Code then begin
                                                                  "Unit of Measure Code" := '';
                                                                  "Variant Code" := '';

                                                                  if Type = Type::Item then
                                                                    if Item.GET(Code) then
                                                                      "Unit of Measure Code" := Item."Sales Unit of Measure"
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Code;
                                                   NotBlank=true }
    { 2   ;   ;Sales Code          ;Code20        ;TableRelation=IF ("Sales Type"=CONST("Customer Disc. Group")) "Customer Discount Group"
                                                                 ELSE IF ("Sales Type"=CONST(Customer)) Customer
                                                                 ELSE IF ("Sales Type"=CONST(Campaign)) Campaign;
                                                   OnValidate=BEGIN
                                                                if "Sales Code" <> '' then
                                                                  case "Sales Type" of
                                                                    "Sales Type"::"All Customers":
                                                                      ERROR(Text001,FIELDCAPTION("Sales Code"));
                                                                    "Sales Type"::Campaign:
                                                                      begin
                                                                        Campaign.GET("Sales Code");
                                                                        "Starting Date" := Campaign."Starting Date";
                                                                        "Ending Date" := Campaign."Ending Date";
                                                                      end;
                                                                  end;
                                                              END;

                                                   CaptionML=ENU=Sales Code }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code }
    { 4   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                if ("Starting Date" > "Ending Date") and ("Ending Date" <> 0D) then
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));

                                                                if CurrFieldNo = 0 then
                                                                  exit;
                                                                if "Sales Type" = "Sales Type"::Campaign then
                                                                  ERROR(Text003,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),"Sales Type");
                                                              END;

                                                   CaptionML=ENU=Starting Date }
    { 5   ;   ;Line Discount %     ;Decimal       ;CaptionML=ENU=Line Discount %;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   AutoFormatType=2 }
    { 13  ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                if "Sales Type" <> xRec."Sales Type" then
                                                                  VALIDATE("Sales Code",'');
                                                              END;

                                                   CaptionML=ENU=Sales Type;
                                                   OptionCaptionML=ENU=Customer,Customer Disc. Group,All Customers,Campaign;
                                                   OptionString=Customer,"Customer Disc. Group","All Customers",Campaign }
    { 14  ;   ;Minimum Quantity    ;Decimal       ;CaptionML=ENU=Minimum Quantity;
                                                   MinValue=0 }
    { 15  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Starting Date");

                                                                if CurrFieldNo = 0 then
                                                                  exit;
                                                                if "Sales Type" = "Sales Type"::Campaign then
                                                                  ERROR(Text003,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),"Sales Type");
                                                              END;

                                                   CaptionML=ENU=Ending Date }
    { 21  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                if xRec.Type <> Type then
                                                                  VALIDATE(Code,'');
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Item,Item Disc. Group;
                                                   OptionString=Item,"Item Disc. Group" }
    { 5400;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE ("Item No."=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Item);
                                                              END;

                                                   CaptionML=ENU=Unit of Measure Code }
    { 5700;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE ("Item No."=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Item);
                                                              END;

                                                   CaptionML=ENU=Variant Code }
  }
  KEYS
  {
    {    ;Type,Code,"Sales Type","Sales Code","Starting Date","Currency Code","Variant Code","Unit of Measure Code","Minimum Quantity";
                                                   Clustered=true }
    {    ;"Sales Type","Sales Code",Type,Code,"Starting Date","Currency Code","Variant Code","Unit of Measure Code","Minimum Quantity" }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;"Sales Type","Sales Code","Line Discount %",Type,Code,"Starting Date","Ending Date" }
  }
  CODE
  {
    VAR
      Text000@1001 : TextConst 'ENU=%1 cannot be after %2';
      Text001@1003 : TextConst 'ENU=%1 must be blank.';
      Campaign@1004 : Record Campaign;
      Text003@1006 : TextConst 'ENU="You can only change the %1 and %2 from the Campaign Card when %3 = %4."';

    BEGIN
    END.
  }
}

