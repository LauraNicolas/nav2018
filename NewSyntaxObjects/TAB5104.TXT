OBJECT Table 5104 Segment Interaction Language
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Cont@1003 : Record Contact;
               SegHeader@1002 : Record "Segment Header";
               SegLine@1001 : Record "Segment Line";
               SegInteractLanguage@1000 : Record "Segment Interaction Language";
               FirstSegIntLanguage@1004 : Boolean;
             BEGIN
               if "Segment Line No." = 0 then begin
                 SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
                 SegInteractLanguage.SETRANGE("Segment Line No.",0);
                 FirstSegIntLanguage := not SegInteractLanguage.FINDFIRST;

                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegHeader.GET("Segment No.");
                 SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code");
                 if SegLine.FIND('-') then
                   repeat
                     if FirstSegIntLanguage then begin
                       if SegLine.AttachmentInherited or
                          (SegLine."Attachment No." = 0)
                       then begin
                         SegLine."Language Code" := "Language Code";
                         SegLine.Subject := Subject;
                         SegLine."Attachment No." := "Attachment No.";
                         SegLine.MODIFY;
                       end
                     end else
                       if SegLine.AttachmentInherited or (SegLine."Attachment No." = 0) then
                         if Cont.GET(SegLine."Contact No.") then
                           if Cont."Language Code" = "Language Code" then begin
                             SegLine."Language Code" := "Language Code";
                             SegLine.Subject := Subject;
                             SegLine."Attachment No." := "Attachment No.";
                             SegLine.MODIFY;
                           end;
                   until SegLine.NEXT = 0;
               end else begin
                 SegLine.GET("Segment No.","Segment Line No.");
                 SegLine."Language Code" := "Language Code";
                 SegLine.Subject := Subject;
                 SegLine."Attachment No." := "Attachment No.";
                 SegLine.MODIFY;
               end;
             END;

    OnDelete=VAR
               SegHeader@1000 : Record "Segment Header";
               SegLine@1001 : Record "Segment Line";
               Cont@1002 : Record Contact;
               SegInteractLanguage@1003 : Record "Segment Interaction Language";
             BEGIN
               SegHeader.GET("Segment No.");
               if ("Segment Line No." = 0) and
                  ("Language Code" = SegHeader."Language Code (Default)")
               then begin
                 SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
                 SegInteractLanguage.SETRANGE("Segment Line No.",0);
                 SegInteractLanguage.SETFILTER("Language Code",'<>%1',"Language Code");
                 if SegInteractLanguage.FINDFIRST then begin
                   SegHeader."Language Code (Default)" := SegInteractLanguage."Language Code";
                   SegHeader."Subject (Default)" := SegInteractLanguage.Subject;
                 end else begin
                   SegHeader."Language Code (Default)" := '';
                   SegHeader."Subject (Default)" := '';
                 end;
                 SegHeader.MODIFY;
               end;
               SegHeader.CALCFIELDS("Attachment No.");

               if "Segment Line No." = 0 then begin
                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegLine.SETRANGE("Attachment No.","Attachment No.");
                 if SegLine.FIND('-') then
                   repeat
                     if Cont.GET(SegLine."Contact No.") then begin
                       if (Cont."Language Code" <> "Language Code") and
                          SegInteractLanguage.GET("Segment No.",0,Cont."Language Code")
                       then begin
                         SegLine."Language Code" := Cont."Language Code";
                         SegLine.Subject := SegInteractLanguage.Subject;
                         SegLine."Attachment No." := SegInteractLanguage."Attachment No.";
                       end else begin
                         SegLine."Language Code" := SegHeader."Language Code (Default)";
                         SegLine.Subject := SegHeader."Subject (Default)";
                         SegLine."Attachment No." := SegHeader."Attachment No.";
                       end;
                       SegLine.MODIFY;
                     end;
                   until SegLine.NEXT = 0;
               end else begin // UNIQUE Attachment
                 SegLine.GET("Segment No.","Segment Line No.");
                 if SegLine."Attachment No." = "Attachment No." then begin
                   SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
                   SegInteractLanguage.SETRANGE("Segment Line No.","Segment Line No.");
                   SegInteractLanguage.SETFILTER("Language Code",'<>%1',"Language Code");
                   if SegInteractLanguage.FINDFIRST then begin
                     SegLine."Language Code" := SegInteractLanguage."Language Code";
                     SegLine.Subject := SegInteractLanguage.Subject;
                     SegLine."Attachment No." := SegInteractLanguage."Attachment No.";
                   end else
                     if SegLine."Interaction Template Code" = SegHeader."Interaction Template Code" then begin
                       Cont.GET(SegLine."Contact No.");
                       if SegInteractLanguage.GET("Segment No.",0,Cont."Language Code") then begin
                         SegLine."Language Code" := Cont."Language Code";
                         SegLine.Subject := SegInteractLanguage.Subject;
                         SegLine."Attachment No." := SegInteractLanguage."Attachment No.";
                       end else begin
                         SegLine."Language Code" := SegHeader."Language Code (Default)";
                         SegLine.Subject := SegHeader."Subject (Default)";
                         SegLine."Attachment No." := SegHeader."Attachment No.";
                       end;
                     end else begin
                       SegLine."Language Code" := '';
                       SegLine.Subject := '';
                       SegLine."Attachment No." := 0;
                     end;
                   SegLine.MODIFY;
                 end;
               end;

               RemoveAttachment(false);
             END;

    OnRename=VAR
               SegHeader@1001 : Record "Segment Header";
               SegLine@1000 : Record "Segment Line";
               Cont@1002 : Record Contact;
             BEGIN
               SegHeader.GET("Segment No.");
               SegHeader.CALCFIELDS("Attachment No.");
               if ("Segment Line No." = 0) and
                  (SegHeader."Language Code (Default)" = xRec."Language Code")
               then begin
                 SegHeader."Language Code (Default)" := "Language Code";
                 SegHeader.MODIFY;
               end;

               if "Segment Line No." = 0 then begin
                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegLine.SETRANGE("Attachment No.","Attachment No.");
                 if SegLine.FIND('-') then
                   repeat
                     SegLine."Language Code" := SegHeader."Language Code (Default)";
                     SegLine."Attachment No." := SegHeader."Attachment No.";
                     SegLine.MODIFY;
                   until SegLine.NEXT = 0;

                 SegLine.RESET;
                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code");
                 if SegLine.FIND('-') then
                   repeat
                     if SegLine.AttachmentInherited or (SegLine."Attachment No." = 0) then
                       if Cont.GET(SegLine."Contact No.") then
                         if Cont."Language Code" = "Language Code" then begin
                           SegLine."Language Code" := "Language Code";
                           SegLine.Subject := Subject;
                           SegLine."Attachment No." := "Attachment No.";
                           SegLine.MODIFY;
                         end;
                   until SegLine.NEXT = 0;
               end else begin
                 SegLine.GET("Segment No.","Segment Line No.");
                 if SegLine."Language Code" = xRec."Language Code" then begin
                   SegLine."Language Code" := "Language Code";
                   SegLine."Attachment No." := "Attachment No.";
                   SegLine.MODIFY;
                 end;
               end;
             END;

    CaptionML=ENU=Segment Interaction Language;
    LookupPageID="Segment Interaction Languages";
  }
  FIELDS
  {
    { 1   ;   ;Segment No.         ;Code20        ;TableRelation="Segment Header";
                                                   CaptionML=ENU=Segment No. }
    { 2   ;   ;Segment Line No.    ;Integer       ;TableRelation="Segment Line"."Line No." WHERE ("Segment No."=FIELD("Segment No."));
                                                   CaptionML=ENU=Segment Line No. }
    { 3   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=ENU=Language Code }
    { 4   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 5   ;   ;Attachment No.      ;Integer       ;CaptionML=ENU=Attachment No. }
    { 6   ;   ;Subject             ;Text50        ;OnValidate=VAR
                                                                SegHeader@1000 : Record "Segment Header";
                                                                SegLine@1001 : Record "Segment Line";
                                                                UpdateLines@1002 : Boolean;
                                                              BEGIN
                                                                SegHeader.GET("Segment No.");
                                                                if "Segment Line No." = 0 then
                                                                  if SegHeader.SegLinesExist('') then
                                                                    UpdateLines := CONFIRM(STRSUBSTNO(Text005,FIELDCAPTION(Subject)),true);

                                                                if SegHeader."Language Code (Default)" = "Language Code" then
                                                                  if SegHeader."Subject (Default)" = xRec.Subject then begin
                                                                    SegHeader."Subject (Default)" := Subject;
                                                                    SegHeader.MODIFY;
                                                                    MODIFY;
                                                                  end;

                                                                if not UpdateLines then
                                                                  exit;

                                                                SegLine.SETRANGE("Segment No.","Segment No.");
                                                                if "Segment Line No." = 0 then
                                                                  SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code")
                                                                else begin
                                                                  SegLine.GET("Segment No.","Segment Line No.");
                                                                  SegLine.SETRANGE("Interaction Template Code",SegLine."Interaction Template Code");
                                                                end;
                                                                SegLine.SETRANGE("Language Code","Language Code");
                                                                SegLine.SETRANGE(Subject,xRec.Subject);
                                                                SegLine.MODIFYALL(Subject,Subject);
                                                              END;

                                                   CaptionML=ENU=Subject }
  }
  KEYS
  {
    {    ;"Segment No.","Segment Line No.","Language Code";
                                                   Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You have canceled the create process.';
      Text001@1004 : TextConst 'ENU=Replace existing attachment?';
      Text002@1003 : TextConst 'ENU=The import was canceled or the specified file could not be accessed. The import failed.';
      Text003@1002 : TextConst 'ENU=Inherited';
      Text004@1001 : TextConst 'ENU=Unique';
      Text005@1005 : TextConst 'ENU=You have modified %1.\\Do you want to update the segment lines with the same Interaction Template Code and Language Code?';

    [Internal]
    PROCEDURE CreateAttachment@1();
    VAR
      SegInteractLanguage@1000 : Record "Segment Interaction Language";
      Attachment@1001 : Record Attachment;
      WordManagement@1002 : Codeunit WordManagement;
      ClientTypeManagement@1004 : Codeunit ClientTypeManagement;
      NewAttachmentNo@1003 : Integer;
    BEGIN
      if "Attachment No." <> 0 then begin
        if not CONFIRM(Text001,false) then
          exit;
        RemoveAttachment(false);
      end;

      if ClientTypeManagement.GetCurrentClientType in [CLIENTTYPE::Web,CLIENTTYPE::Tablet,CLIENTTYPE::Phone] then begin
        if Attachment.ImportAttachmentFromClientFile('',false,true) then
          NewAttachmentNo := Attachment."No.";
      end else
        NewAttachmentNo := WordManagement.CreateWordAttachment("Segment No." + ' ' + Description,"Language Code");

      if NewAttachmentNo <> 0 then begin
        "Attachment No." := NewAttachmentNo;
        if SegInteractLanguage.GET("Segment No.","Segment Line No.","Language Code") then
          MODIFY
        else
          INSERT(true);
        UpdateSegLineAttachment(0);
      end else
        ERROR(Text000);
    END;

    [Internal]
    PROCEDURE OpenAttachment@2();
    VAR
      Attachment@1000 : Record Attachment;
    BEGIN
      if "Attachment No." = 0 then
        exit;
      Attachment.GET("Attachment No.");
      Attachment.OpenAttachment("Segment No." + ' ' + Description,false,"Language Code");
    END;

    [Internal]
    PROCEDURE CopyFromAttachment@3();
    VAR
      SegInteractLanguage@1004 : Record "Segment Interaction Language";
      AttachmentManagement@1002 : Codeunit AttachmentManagement;
      NewAttachNo@1001 : Integer;
    BEGIN
      if "Attachment No." <> 0 then begin
        if not CONFIRM(Text001,false) then
          exit;
        RemoveAttachment(false);
        COMMIT;
      end;

      SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
      SegInteractLanguage.SETFILTER("Attachment No.",'<>%1&<>%2',0,"Attachment No.");
      if PAGE.RUNMODAL(0,SegInteractLanguage) = ACTION::LookupOK then begin
        NewAttachNo := AttachmentManagement.InsertAttachment(SegInteractLanguage."Attachment No.");
        if NewAttachNo <> 0 then begin
          "Attachment No." := NewAttachNo;
          MODIFY;
          UpdateSegLineAttachment(0);
        end;
      end;
    END;

    [Internal]
    PROCEDURE ImportAttachment@4();
    VAR
      Attachment@1003 : Record Attachment;
    BEGIN
      if "Attachment No." <> 0 then begin
        if not CONFIRM(Text001,false) then
          exit;
        RemoveAttachment(false);
      end;

      if Attachment.ImportAttachmentFromClientFile('',false,false) then begin
        "Attachment No." := Attachment."No.";
        MODIFY;
        UpdateSegLineAttachment(0);
      end else
        ERROR(Text002);
    END;

    [Internal]
    PROCEDURE ExportAttachment@5();
    VAR
      Attachment@1000 : Record Attachment;
      FileName@1001 : Text[1024];
    BEGIN
      if Attachment.GET("Attachment No.") then begin
        if Attachment."Storage Type" = Attachment."Storage Type"::Embedded then
          Attachment.CALCFIELDS("Attachment File");
        Attachment.ExportAttachmentToClientFile(FileName);
      end;
    END;

    [Internal]
    PROCEDURE RemoveAttachment@6(Prompt@1000 : Boolean);
    VAR
      Attachment@1002 : Record Attachment;
      OldAttachmentNo@1001 : Integer;
    BEGIN
      OldAttachmentNo := "Attachment No.";
      if Attachment.GET("Attachment No.") and (not Attachment."Read Only") then
        if Attachment.RemoveAttachment(Prompt) then begin
          "Attachment No." := 0;
          MODIFY;
        end;
      UpdateSegLineAttachment(OldAttachmentNo);
    END;

    LOCAL PROCEDURE UpdateSegLineAttachment@7(OldAttachmentNo@1000 : Integer);
    VAR
      SegLine@1001 : Record "Segment Line";
      SegHeader@1002 : Record "Segment Header";
    BEGIN
      if "Segment Line No." = 0 then begin
        SegHeader.GET("Segment No.");
        SegLine.SETRANGE("Segment No.","Segment No.");
        SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code");
        SegLine.SETRANGE("Language Code","Language Code");
        SegLine.SETRANGE("Attachment No.",OldAttachmentNo);
        SegLine.MODIFYALL("Attachment No.","Attachment No.");
      end else begin
        SegLine.SETRANGE("Segment No.","Segment No.");
        SegLine.SETRANGE("Line No.","Segment Line No.");
        SegLine.SETRANGE("Attachment No.",OldAttachmentNo);
        if SegLine.FINDFIRST then begin
          SegLine."Attachment No." := "Attachment No.";
          SegLine.MODIFY;
        end;
      end;
    END;

    [External]
    PROCEDURE AttachmentText@9() : Text[30];
    BEGIN
      if "Attachment No." = 0 then
        exit('');

      if "Segment Line No." = 0 then
        exit(Text003);

      exit(Text004);
    END;

    [External]
    PROCEDURE Caption@8() CaptionText@1002 : Text[260];
    VAR
      SegHeader@1003 : Record "Segment Header";
      SegLine@1000 : Record "Segment Line";
      Cont@1001 : Record Contact;
    BEGIN
      SegHeader.GET("Segment No.");
      CaptionText := FORMAT("Segment No.") + ' ' + SegHeader.Description;
      if "Segment Line No." <> 0 then begin
        SegLine.GET("Segment No.","Segment Line No.");
        if Cont.GET(SegLine."Contact No.") then;
        CaptionText := CaptionText + ' ' + FORMAT(SegLine."Contact No.") + ' ' + Cont.Name;
      end;
    END;

    BEGIN
    END.
  }
}

