OBJECT Report 85 Copy - VAT Posting Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy - VAT Posting Setup;
    ProcessingOnly=true;
  }
  DATASET
  {
    { 1756;    ;DataItem;VAT Posting Setup   ;
               DataItemTable="VAT Posting Setup";
               DataItemTableView=SORTING("VAT Bus. Posting Group","VAT Prod. Posting Group");
               OnPreDataItem=BEGIN
                               SETRANGE("VAT Bus. Posting Group",UseVATPostingSetup."VAT Bus. Posting Group");
                               SETRANGE("VAT Prod. Posting Group",UseVATPostingSetup."VAT Prod. Posting Group");
                             END;

               OnAfterGetRecord=BEGIN
                                  VATPostingSetup.FIND;
                                  if VATSetup then begin
                                    "VAT Calculation Type" := VATPostingSetup."VAT Calculation Type";
                                    "VAT %" := VATPostingSetup."VAT %";
                                    "Unrealized VAT Type" := VATPostingSetup."Unrealized VAT Type";
                                    "Adjust for Payment Discount" := VATPostingSetup."Adjust for Payment Discount";
                                    "VAT Identifier" := VATPostingSetup."VAT Identifier";
                                  end;

                                  if Sales then begin
                                    "Sales VAT Account" := VATPostingSetup."Sales VAT Account";
                                    "Sales VAT Unreal. Account" := VATPostingSetup."Sales VAT Unreal. Account";
                                  end;

                                  if Purch then begin
                                    "Purchase VAT Account" := VATPostingSetup."Purchase VAT Account";
                                    "Purch. VAT Unreal. Account" := VATPostingSetup."Purch. VAT Unreal. Account";
                                    "Reverse Chrg. VAT Acc." := VATPostingSetup."Reverse Chrg. VAT Acc.";
                                    "Reverse Chrg. VAT Unreal. Acc." := VATPostingSetup."Reverse Chrg. VAT Unreal. Acc.";
                                  end;

                                  if CONFIRM(Text000,false) then
                                    MODIFY;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   if Selection = Selection::"All fields" then begin
                     VATSetup := true;
                     Sales := true;
                     Purch := true;
                   end;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 2   ;2   ;Field     ;
                  Name=VATBusPostingGroup;
                  CaptionML=ENU=VAT Bus. Posting Group;
                  ToolTipML=ENU=Specifies the VAT business posting group to copy from.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=VATPostingSetup."VAT Bus. Posting Group";
                  TableRelation="VAT Business Posting Group" }

      { 4   ;2   ;Field     ;
                  Name=VATProdPostingGroup;
                  CaptionML=ENU=VAT Prod. Posting Group;
                  ToolTipML=ENU=Specifies the VAT product posting group to copy from.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=VATPostingSetup."VAT Prod. Posting Group";
                  TableRelation="VAT Product Posting Group" }

      { 7   ;2   ;Field     ;
                  Name=Copy;
                  CaptionML=ENU=Copy;
                  ToolTipML=ENU=Specifies if all fields or only selected fields are copied.;
                  OptionCaptionML=ENU=All fields,Selected fields;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Selection;
                  OnValidate=BEGIN
                               if Selection = Selection::"All fields" then
                                 AllfieldsSelectionOnValidate;
                             END;
                              }

      { 10  ;2   ;Field     ;
                  Name=VATetc;
                  CaptionML=ENU=VAT % etc.;
                  ToolTipML=ENU=Specifies if you want to copy the VAT rate.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=VATSetup;
                  OnValidate=BEGIN
                               Selection := Selection::"Selected fields";
                             END;
                              }

      { 1   ;2   ;Field     ;
                  Name=SalesAccounts;
                  CaptionML=ENU=Sales Accounts;
                  ToolTipML=ENU=Specifies if you want to copy the sales VAT accounts.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Sales;
                  OnValidate=BEGIN
                               Selection := Selection::"Selected fields";
                             END;
                              }

      { 17  ;2   ;Field     ;
                  Name=PurchaseAccounts;
                  CaptionML=ENU=Purchase Accounts;
                  ToolTipML=ENU=Specifies if you want to copy the purchase VAT accounts.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=Purch;
                  OnValidate=BEGIN
                               Selection := Selection::"Selected fields";
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Copy VAT Posting Setup?';
      UseVATPostingSetup@1001 : Record "VAT Posting Setup";
      VATPostingSetup@1002 : Record "VAT Posting Setup";
      VATSetup@1003 : Boolean;
      Sales@1004 : Boolean;
      Purch@1005 : Boolean;
      Selection@1006 : '"All fields","Selected fields"';

    [External]
    PROCEDURE SetVATSetup@1(VATPostingSetup2@1000 : Record "VAT Posting Setup");
    BEGIN
      UseVATPostingSetup := VATPostingSetup2;
    END;

    LOCAL PROCEDURE AllfieldsSelectionOnPush@19052427();
    BEGIN
      VATSetup := true;
      Sales := true;
      Purch := true;
    END;

    LOCAL PROCEDURE AllfieldsSelectionOnValidate@19034152();
    BEGIN
      AllfieldsSelectionOnPush;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

