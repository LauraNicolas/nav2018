OBJECT Table 38 Purchase Header
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataCaptionFields="No.","Buy-from Vendor Name";
    OnInsert=BEGIN
               if not SkipInitialization then
                 InitInsert;

               if GETFILTER("Buy-from Vendor No.") <> '' then
                 if GETRANGEMIN("Buy-from Vendor No.") = GETRANGEMAX("Buy-from Vendor No.") then
                   VALIDATE("Buy-from Vendor No.",GETRANGEMIN("Buy-from Vendor No."));

               if "Purchaser Code" = '' then
                 SetDefaultPurchaser;
             END;

    OnDelete=VAR
               PostPurchDelete@1000 : Codeunit "PostPurch-Delete";
               ArchiveManagement@1001 : Codeunit ArchiveManagement;
             BEGIN
               if not UserSetupMgt.CheckRespCenter(1,"Responsibility Center") then
                 ERROR(
                   Text023,
                   RespCenter.TABLECAPTION,UserSetupMgt.GetPurchasesFilter);

               ArchiveManagement.AutoArchivePurchDocument(Rec);
               PostPurchDelete.DeleteHeader(
                 Rec,PurchRcptHeader,PurchInvHeader,PurchCrMemoHeader,
                 ReturnShptHeader,PurchInvHeaderPrepmt,PurchCrMemoHeaderPrepmt);
               VALIDATE("Applies-to ID",'');
               VALIDATE("Incoming Document Entry No.",0);

               ApprovalsMgmt.DeleteApprovalEntries(RECORDID);
               PurchLine.LOCKTABLE;

               WhseRequest.SETRANGE("Source Type",DATABASE::"Purchase Line");
               WhseRequest.SETRANGE("Source Subtype","Document Type");
               WhseRequest.SETRANGE("Source No.","No.");
               WhseRequest.DELETEALL(true);

               PurchLine.SETRANGE("Document Type","Document Type");
               PurchLine.SETRANGE("Document No.","No.");
               PurchLine.SETRANGE(Type,PurchLine.Type::"Charge (Item)");
               DeletePurchaseLines;
               PurchLine.SETRANGE(Type);
               DeletePurchaseLines;

               PurchCommentLine.SETRANGE("Document Type","Document Type");
               PurchCommentLine.SETRANGE("No.","No.");
               PurchCommentLine.DELETEALL;

               if (PurchRcptHeader."No." <> '') or
                  (PurchInvHeader."No." <> '') or
                  (PurchCrMemoHeader."No." <> '') or
                  (ReturnShptHeader."No." <> '') or
                  (PurchInvHeaderPrepmt."No." <> '') or
                  (PurchCrMemoHeaderPrepmt."No." <> '')
               then
                 MESSAGE(PostedDocsToPrintCreatedMsg);
             END;

    OnRename=BEGIN
               ERROR(Text003,TABLECAPTION);
             END;

    CaptionML=ENU=Purchase Header;
    LookupPageID="Purchase List";
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   OptionString=Quote,Order,Invoice,"Credit Memo","Blanket Order","Return Order" }
    { 2   ;   ;Buy-from Vendor No. ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                if "No." = '' then
                                                                  InitRecord;
                                                                TESTFIELD(Status,Status::Open);
                                                                if ("Buy-from Vendor No." <> xRec."Buy-from Vendor No.") and
                                                                   (xRec."Buy-from Vendor No." <> '')
                                                                then begin
                                                                  CheckDropShipmentLineExists;
                                                                  if HideValidationDialog then
                                                                    Confirmed := true
                                                                  else
                                                                    Confirmed := CONFIRM(ConfirmChangeQst,false,BuyFromVendorTxt);
                                                                  if Confirmed then begin
                                                                    if InitFromVendor("Buy-from Vendor No.",FIELDCAPTION("Buy-from Vendor No.")) then
                                                                      exit;

                                                                    CheckReceiptInfo(PurchLine,false);
                                                                    CheckPrepmtInfo(PurchLine);
                                                                    CheckReturnInfo(PurchLine,false);

                                                                    PurchLine.RESET;
                                                                  end else begin
                                                                    Rec := xRec;
                                                                    exit;
                                                                  end;
                                                                end;

                                                                GetVend("Buy-from Vendor No.");
                                                                Vend.CheckBlockedVendOnDocs(Vend,false);
                                                                Vend.TESTFIELD("Gen. Bus. Posting Group");
                                                                "Buy-from Vendor Name" := Vend.Name;
                                                                "Buy-from Vendor Name 2" := Vend."Name 2";
                                                                CopyBuyFromVendorAddressFieldsFromVendor(Vend);
                                                                if not SkipBuyFromContact then
                                                                  "Buy-from Contact" := Vend.Contact;
                                                                "Gen. Bus. Posting Group" := Vend."Gen. Bus. Posting Group";
                                                                "VAT Bus. Posting Group" := Vend."VAT Bus. Posting Group";
                                                                "Tax Area Code" := Vend."Tax Area Code";
                                                                "Tax Liable" := Vend."Tax Liable";
                                                                "VAT Country/Region Code" := Vend."Country/Region Code";
                                                                "VAT Registration No." := Vend."VAT Registration No.";
                                                                VALIDATE("Lead Time Calculation",Vend."Lead Time Calculation");
                                                                "Responsibility Center" := UserSetupMgt.GetRespCenter(1,Vend."Responsibility Center");
                                                                VALIDATE("Sell-to Customer No.",'');
                                                                VALIDATE("Location Code",UserSetupMgt.GetLocation(1,Vend."Location Code","Responsibility Center"));

                                                                if "Buy-from Vendor No." = xRec."Pay-to Vendor No." then
                                                                  if ReceivedPurchLinesExist or ReturnShipmentExist then begin
                                                                    TESTFIELD("VAT Bus. Posting Group",xRec."VAT Bus. Posting Group");
                                                                    TESTFIELD("Gen. Bus. Posting Group",xRec."Gen. Bus. Posting Group");
                                                                  end;

                                                                "Buy-from IC Partner Code" := Vend."IC Partner Code";
                                                                "Send IC Document" := ("Buy-from IC Partner Code" <> '') and ("IC Direction" = "IC Direction"::Outgoing);

                                                                if Vend."Pay-to Vendor No." <> '' then
                                                                  VALIDATE("Pay-to Vendor No.",Vend."Pay-to Vendor No.")
                                                                else begin
                                                                  if "Buy-from Vendor No." = "Pay-to Vendor No." then
                                                                    SkipPayToContact := true;
                                                                  VALIDATE("Pay-to Vendor No.","Buy-from Vendor No.");
                                                                  SkipPayToContact := false;
                                                                end;
                                                                "Order Address Code" := '';

                                                                VALIDATE("Order Address Code");

                                                                if (xRec."Buy-from Vendor No." <> "Buy-from Vendor No.") or
                                                                   (xRec."Currency Code" <> "Currency Code") or
                                                                   (xRec."Gen. Bus. Posting Group" <> "Gen. Bus. Posting Group") or
                                                                   (xRec."VAT Bus. Posting Group" <> "VAT Bus. Posting Group")
                                                                then
                                                                  RecreatePurchLines(BuyFromVendorTxt);

                                                                if not SkipBuyFromContact then
                                                                  UpdateBuyFromCont("Buy-from Vendor No.");

                                                                if (xRec."Buy-from Vendor No." <> '') and (xRec."Buy-from Vendor No." <> "Buy-from Vendor No.") then
                                                                  RecallModifyAddressNotification(GetModifyVendorAddressNotificationId);
                                                              END;

                                                   CaptionML=ENU=Buy-from Vendor No. }
    { 3   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                if "No." <> xRec."No." then begin
                                                                  PurchSetup.GET;
                                                                  NoSeriesMgt.TestManual(GetNoSeriesCode);
                                                                  "No. Series" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 4   ;   ;Pay-to Vendor No.   ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                if (xRec."Pay-to Vendor No." <> "Pay-to Vendor No.") and
                                                                   (xRec."Pay-to Vendor No." <> '')
                                                                then begin
                                                                  if HideValidationDialog then
                                                                    Confirmed := true
                                                                  else
                                                                    Confirmed := CONFIRM(ConfirmChangeQst,false,PayToVendorTxt);
                                                                  if Confirmed then begin
                                                                    PurchLine.SETRANGE("Document Type","Document Type");
                                                                    PurchLine.SETRANGE("Document No.","No.");

                                                                    CheckReceiptInfo(PurchLine,true);
                                                                    CheckPrepmtInfo(PurchLine);
                                                                    CheckReturnInfo(PurchLine,true);

                                                                    PurchLine.RESET;
                                                                  end else
                                                                    "Pay-to Vendor No." := xRec."Pay-to Vendor No.";
                                                                end;

                                                                GetVend("Pay-to Vendor No.");
                                                                Vend.CheckBlockedVendOnDocs(Vend,false);
                                                                Vend.TESTFIELD("Vendor Posting Group");
                                                                PostingSetupMgt.CheckVendPostingGroupPayablesAccount("Vendor Posting Group");

                                                                "Pay-to Name" := Vend.Name;
                                                                "Pay-to Name 2" := Vend."Name 2";
                                                                CopyPayToVendorAddressFieldsFromVendor(Vend);
                                                                if not SkipPayToContact then
                                                                  "Pay-to Contact" := Vend.Contact;
                                                                "Payment Terms Code" := Vend."Payment Terms Code";
                                                                "Prepmt. Payment Terms Code" := Vend."Payment Terms Code";

                                                                if "Document Type" = "Document Type"::"Credit Memo" then begin
                                                                  "Payment Method Code" := '';
                                                                  if PaymentTerms.GET("Payment Terms Code") then
                                                                    if PaymentTerms."Calc. Pmt. Disc. on Cr. Memos" then
                                                                      "Payment Method Code" := Vend."Payment Method Code"
                                                                end else
                                                                  "Payment Method Code" := Vend."Payment Method Code";

                                                                "Shipment Method Code" := Vend."Shipment Method Code";
                                                                "Vendor Posting Group" := Vend."Vendor Posting Group";
                                                                GLSetup.GET;
                                                                if GLSetup."Bill-to/Sell-to VAT Calc." = GLSetup."Bill-to/Sell-to VAT Calc."::"Bill-to/Pay-to No." then begin
                                                                  "VAT Bus. Posting Group" := Vend."VAT Bus. Posting Group";
                                                                  "VAT Country/Region Code" := Vend."Country/Region Code";
                                                                  "VAT Registration No." := Vend."VAT Registration No.";
                                                                  "Gen. Bus. Posting Group" := Vend."Gen. Bus. Posting Group";
                                                                end;
                                                                "Prices Including VAT" := Vend."Prices Including VAT";
                                                                "Currency Code" := Vend."Currency Code";
                                                                "Invoice Disc. Code" := Vend."Invoice Disc. Code";
                                                                "Language Code" := Vend."Language Code";
                                                                "Purchaser Code" := Vend."Purchaser Code";
                                                                VALIDATE("Payment Terms Code");
                                                                VALIDATE("Prepmt. Payment Terms Code");
                                                                VALIDATE("Payment Method Code");
                                                                VALIDATE("Currency Code");
                                                                VALIDATE("Creditor No.",Vend."Creditor No.");

                                                                OnValidatePurchaseHeaderPayToVendorNo(Vend);

                                                                if "Document Type" = "Document Type"::Order then
                                                                  VALIDATE("Prepayment %",Vend."Prepayment %");

                                                                if "Pay-to Vendor No." = xRec."Pay-to Vendor No." then begin
                                                                  if ReceivedPurchLinesExist then
                                                                    TESTFIELD("Currency Code",xRec."Currency Code");
                                                                end;

                                                                CreateDim(
                                                                  DATABASE::Vendor,"Pay-to Vendor No.",
                                                                  DATABASE::"Salesperson/Purchaser","Purchaser Code",
                                                                  DATABASE::Campaign,"Campaign No.",
                                                                  DATABASE::"Responsibility Center","Responsibility Center");

                                                                if (xRec."Buy-from Vendor No." = "Buy-from Vendor No.") and
                                                                   (xRec."Pay-to Vendor No." <> "Pay-to Vendor No.")
                                                                then
                                                                  RecreatePurchLines(PayToVendorTxt);

                                                                if not SkipPayToContact then
                                                                  UpdatePayToCont("Pay-to Vendor No.");

                                                                "Pay-to IC Partner Code" := Vend."IC Partner Code";

                                                                if (xRec."Pay-to Vendor No." <> '') and (xRec."Pay-to Vendor No." <> "Pay-to Vendor No.") then
                                                                  RecallModifyAddressNotification(GetModifyPayToVendorAddressNotificationId);
                                                              END;

                                                   CaptionML=ENU=Pay-to Vendor No.;
                                                   NotBlank=true }
    { 5   ;   ;Pay-to Name         ;Text50        ;TableRelation=Vendor;
                                                   OnValidate=VAR
                                                                Vendor@1000 : Record Vendor;
                                                              BEGIN
                                                                VALIDATE("Pay-to Vendor No.",Vendor.GetVendorNo("Pay-to Name"));
                                                              END;

                                                   ValidateTableRelation=false;
                                                   CaptionML=ENU=Pay-to Name }
    { 6   ;   ;Pay-to Name 2       ;Text50        ;CaptionML=ENU=Pay-to Name 2 }
    { 7   ;   ;Pay-to Address      ;Text50        ;OnValidate=BEGIN
                                                                ModifyPayToVendorAddress;
                                                              END;

                                                   CaptionML=ENU=Pay-to Address }
    { 8   ;   ;Pay-to Address 2    ;Text50        ;OnValidate=BEGIN
                                                                ModifyPayToVendorAddress;
                                                              END;

                                                   CaptionML=ENU=Pay-to Address 2 }
    { 9   ;   ;Pay-to City         ;Text30        ;TableRelation=IF ("Pay-to Country/Region Code"=CONST('')) "Post Code".City
                                                                 ELSE IF ("Pay-to Country/Region Code"=FILTER(<>'')) "Post Code".City WHERE ("Country/Region Code"=FIELD("Pay-to Country/Region Code"));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(
                                                                  "Pay-to City","Pay-to Post Code","Pay-to County","Pay-to Country/Region Code",(CurrFieldNo <> 0) and GUIALLOWED);
                                                                ModifyPayToVendorAddress;
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Pay-to City }
    { 10  ;   ;Pay-to Contact      ;Text50        ;OnValidate=BEGIN
                                                                ModifyPayToVendorAddress;
                                                              END;

                                                   OnLookup=VAR
                                                              Contact@1000 : Record Contact;
                                                            BEGIN
                                                              LookupContact("Pay-to Vendor No.","Pay-to Contact No.",Contact);
                                                              if PAGE.RUNMODAL(0,Contact) = ACTION::LookupOK then
                                                                VALIDATE("Pay-to Contact No.",Contact."No.");
                                                            END;

                                                   CaptionML=ENU=Pay-to Contact }
    { 11  ;   ;Your Reference      ;Text35        ;CaptionML=ENU=Your Reference }
    { 12  ;   ;Ship-to Code        ;Code10        ;TableRelation="Ship-to Address".Code WHERE ("Customer No."=FIELD("Sell-to Customer No."));
                                                   OnValidate=VAR
                                                                ShipToAddr@1000 : Record "Ship-to Address";
                                                              BEGIN
                                                                if ("Document Type" = "Document Type"::Order) and
                                                                   (xRec."Ship-to Code" <> "Ship-to Code")
                                                                then begin
                                                                  PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
                                                                  PurchLine.SETRANGE("Document No.","No.");
                                                                  PurchLine.SETFILTER("Sales Order Line No.",'<>0');
                                                                  if not PurchLine.ISEMPTY then
                                                                    ERROR(
                                                                      YouCannotChangeFieldErr,
                                                                      FIELDCAPTION("Ship-to Code"));
                                                                end;

                                                                if "Ship-to Code" <> '' then begin
                                                                  ShipToAddr.GET("Sell-to Customer No.","Ship-to Code");
                                                                  SetShipToAddress(
                                                                    ShipToAddr.Name,ShipToAddr."Name 2",ShipToAddr.Address,ShipToAddr."Address 2",
                                                                    ShipToAddr.City,ShipToAddr."Post Code",ShipToAddr.County,ShipToAddr."Country/Region Code");
                                                                  "Ship-to Contact" := ShipToAddr.Contact;
                                                                  "Shipment Method Code" := ShipToAddr."Shipment Method Code";
                                                                  if ShipToAddr."Location Code" <> '' then
                                                                    VALIDATE("Location Code",ShipToAddr."Location Code");
                                                                end else begin
                                                                  TESTFIELD("Sell-to Customer No.");
                                                                  Cust.GET("Sell-to Customer No.");
                                                                  SetShipToAddress(
                                                                    Cust.Name,Cust."Name 2",Cust.Address,Cust."Address 2",
                                                                    Cust.City,Cust."Post Code",Cust.County,Cust."Country/Region Code");
                                                                  "Ship-to Contact" := Cust.Contact;
                                                                  "Shipment Method Code" := Cust."Shipment Method Code";
                                                                  if Cust."Location Code" <> '' then
                                                                    VALIDATE("Location Code",Cust."Location Code");
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Ship-to Code }
    { 13  ;   ;Ship-to Name        ;Text50        ;CaptionML=ENU=Ship-to Name }
    { 14  ;   ;Ship-to Name 2      ;Text50        ;CaptionML=ENU=Ship-to Name 2 }
    { 15  ;   ;Ship-to Address     ;Text50        ;CaptionML=ENU=Ship-to Address }
    { 16  ;   ;Ship-to Address 2   ;Text50        ;CaptionML=ENU=Ship-to Address 2 }
    { 17  ;   ;Ship-to City        ;Text30        ;TableRelation=IF ("Ship-to Country/Region Code"=CONST('')) "Post Code".City
                                                                 ELSE IF ("Ship-to Country/Region Code"=FILTER(<>'')) "Post Code".City WHERE ("Country/Region Code"=FIELD("Ship-to Country/Region Code"));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(
                                                                  "Ship-to City","Ship-to Post Code","Ship-to County","Ship-to Country/Region Code",(CurrFieldNo <> 0) and GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Ship-to City }
    { 18  ;   ;Ship-to Contact     ;Text50        ;CaptionML=ENU=Ship-to Contact }
    { 19  ;   ;Order Date          ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                if ("Document Type" in ["Document Type"::Quote,"Document Type"::Order]) and
                                                                   not ("Order Date" = xRec."Order Date")
                                                                then
                                                                  PriceMessageIfPurchLinesExist(FIELDCAPTION("Order Date"));
                                                              END;

                                                   AccessByPermission=TableData "Purch. Rcpt. Header"=R;
                                                   CaptionML=ENU=Order Date }
    { 20  ;   ;Posting Date        ;Date          ;OnValidate=VAR
                                                                SkipJobCurrFactorUpdate@1000 : Boolean;
                                                              BEGIN
                                                                TestNoSeriesDate(
                                                                  "Posting No.","Posting No. Series",
                                                                  FIELDCAPTION("Posting No."),FIELDCAPTION("Posting No. Series"));
                                                                TestNoSeriesDate(
                                                                  "Prepayment No.","Prepayment No. Series",
                                                                  FIELDCAPTION("Prepayment No."),FIELDCAPTION("Prepayment No. Series"));
                                                                TestNoSeriesDate(
                                                                  "Prepmt. Cr. Memo No.","Prepmt. Cr. Memo No. Series",
                                                                  FIELDCAPTION("Prepmt. Cr. Memo No."),FIELDCAPTION("Prepmt. Cr. Memo No. Series"));

                                                                if "Incoming Document Entry No." = 0 then
                                                                  VALIDATE("Document Date","Posting Date");

                                                                if ("Document Type" in ["Document Type"::Invoice,"Document Type"::"Credit Memo"]) and
                                                                   not ("Posting Date" = xRec."Posting Date")
                                                                then
                                                                  PriceMessageIfPurchLinesExist(FIELDCAPTION("Posting Date"));

                                                                if "Currency Code" <> '' then begin
                                                                  UpdateCurrencyFactor;
                                                                  if "Currency Factor" <> xRec."Currency Factor" then
                                                                    SkipJobCurrFactorUpdate := not ConfirmUpdateCurrencyFactor;
                                                                end;

                                                                if "Posting Date" <> xRec."Posting Date" then
                                                                  if DeferralHeadersExist then
                                                                    ConfirmUpdateDeferralDate;

                                                                if PurchLinesExist then
                                                                  JobUpdatePurchLines(SkipJobCurrFactorUpdate);
                                                              END;

                                                   CaptionML=ENU=Posting Date }
    { 21  ;   ;Expected Receipt Date;Date         ;OnValidate=BEGIN
                                                                if "Expected Receipt Date" <> 0D then
                                                                  UpdatePurchLines(FIELDCAPTION("Expected Receipt Date"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=ENU=Expected Receipt Date }
    { 22  ;   ;Posting Description ;Text50        ;CaptionML=ENU=Posting Description }
    { 23  ;   ;Payment Terms Code  ;Code10        ;TableRelation="Payment Terms";
                                                   OnValidate=BEGIN
                                                                if ("Payment Terms Code" <> '') and ("Document Date" <> 0D) then begin
                                                                  PaymentTerms.GET("Payment Terms Code");
                                                                  if IsCreditDocType and not PaymentTerms."Calc. Pmt. Disc. on Cr. Memos" then begin
                                                                    VALIDATE("Due Date","Document Date");
                                                                    VALIDATE("Pmt. Discount Date",0D);
                                                                    VALIDATE("Payment Discount %",0);
                                                                  end else begin
                                                                    "Due Date" := CALCDATE(PaymentTerms."Due Date Calculation","Document Date");
                                                                    "Pmt. Discount Date" := CALCDATE(PaymentTerms."Discount Date Calculation","Document Date");
                                                                    if not UpdateDocumentDate then
                                                                      VALIDATE("Payment Discount %",PaymentTerms."Discount %")
                                                                  end;
                                                                end else begin
                                                                  VALIDATE("Due Date","Document Date");
                                                                  if not UpdateDocumentDate then begin
                                                                    VALIDATE("Pmt. Discount Date",0D);
                                                                    VALIDATE("Payment Discount %",0);
                                                                  end;
                                                                end;
                                                                if xRec."Payment Terms Code" = "Prepmt. Payment Terms Code" then
                                                                  VALIDATE("Prepmt. Payment Terms Code","Payment Terms Code");
                                                              END;

                                                   CaptionML=ENU=Payment Terms Code }
    { 24  ;   ;Due Date            ;Date          ;CaptionML=ENU=Due Date }
    { 25  ;   ;Payment Discount %  ;Decimal       ;OnValidate=BEGIN
                                                                if not (CurrFieldNo in [0,FIELDNO("Posting Date"),FIELDNO("Document Date")]) then
                                                                  TESTFIELD(Status,Status::Open);
                                                                GLSetup.GET;
                                                                if "Payment Discount %" < GLSetup."VAT Tolerance %" then
                                                                  "VAT Base Discount %" := "Payment Discount %"
                                                                else
                                                                  "VAT Base Discount %" := GLSetup."VAT Tolerance %";
                                                                VALIDATE("VAT Base Discount %");
                                                              END;

                                                   CaptionML=ENU=Payment Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 26  ;   ;Pmt. Discount Date  ;Date          ;CaptionML=ENU=Pmt. Discount Date }
    { 27  ;   ;Shipment Method Code;Code10        ;TableRelation="Shipment Method";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                              END;

                                                   CaptionML=ENU=Shipment Method Code }
    { 28  ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE ("Use As In-Transit"=CONST(false));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                if ("Location Code" <> xRec."Location Code") and
                                                                   (xRec."Buy-from Vendor No." = "Buy-from Vendor No.")
                                                                then
                                                                  MessageIfPurchLinesExist(FIELDCAPTION("Location Code"));

                                                                UpdateShipToAddress;

                                                                if "Location Code" = '' then begin
                                                                  if InvtSetup.GET then
                                                                    "Inbound Whse. Handling Time" := InvtSetup."Inbound Whse. Handling Time";
                                                                end else begin
                                                                  if Location.GET("Location Code") then;
                                                                  "Inbound Whse. Handling Time" := Location."Inbound Whse. Handling Time";
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Location Code }
    { 29  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1),
                                                                                               Blocked=CONST(false));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 30  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2),
                                                                                               Blocked=CONST(false));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 31  ;   ;Vendor Posting Group;Code20        ;TableRelation="Vendor Posting Group";
                                                   CaptionML=ENU=Vendor Posting Group;
                                                   Editable=false }
    { 32  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                if not (CurrFieldNo in [0,FIELDNO("Posting Date")]) or ("Currency Code" <> xRec."Currency Code") then
                                                                  TESTFIELD(Status,Status::Open);
                                                                if (CurrFieldNo <> FIELDNO("Currency Code")) and ("Currency Code" = xRec."Currency Code") then
                                                                  UpdateCurrencyFactor
                                                                else
                                                                  if "Currency Code" <> xRec."Currency Code" then begin
                                                                    UpdateCurrencyFactor;
                                                                    if PurchLinesExist then
                                                                      if CONFIRM(ChangeCurrencyQst,false,FIELDCAPTION("Currency Code")) then begin
                                                                        SetHideValidationDialog(true);
                                                                        RecreatePurchLines(FIELDCAPTION("Currency Code"));
                                                                        SetHideValidationDialog(false);
                                                                      end else
                                                                        ERROR(Text018,FIELDCAPTION("Currency Code"));
                                                                  end else
                                                                    if "Currency Code" <> '' then begin
                                                                      UpdateCurrencyFactor;
                                                                      if "Currency Factor" <> xRec."Currency Factor" then
                                                                        ConfirmUpdateCurrencyFactor;
                                                                    end;
                                                              END;

                                                   CaptionML=ENU=Currency Code }
    { 33  ;   ;Currency Factor     ;Decimal       ;OnValidate=BEGIN
                                                                if "Currency Factor" <> xRec."Currency Factor" then
                                                                  UpdatePurchLines(FIELDCAPTION("Currency Factor"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=ENU=Currency Factor;
                                                   DecimalPlaces=0:15;
                                                   MinValue=0;
                                                   Editable=false }
    { 35  ;   ;Prices Including VAT;Boolean       ;OnValidate=VAR
                                                                PurchLine@1000 : Record "Purchase Line";
                                                                Currency@1001 : Record Currency;
                                                                RecalculatePrice@1002 : Boolean;
                                                              BEGIN
                                                                TESTFIELD(Status,Status::Open);

                                                                if "Prices Including VAT" <> xRec."Prices Including VAT" then begin
                                                                  PurchLine.SETRANGE("Document Type","Document Type");
                                                                  PurchLine.SETRANGE("Document No.","No.");
                                                                  PurchLine.SETFILTER("Direct Unit Cost",'<>%1',0);
                                                                  PurchLine.SETFILTER("VAT %",'<>%1',0);
                                                                  if PurchLine.FIND('-') then begin
                                                                    RecalculatePrice :=
                                                                      CONFIRM(
                                                                        STRSUBSTNO(
                                                                          Text025 +
                                                                          Text027,
                                                                          FIELDCAPTION("Prices Including VAT"),PurchLine.FIELDCAPTION("Direct Unit Cost")),
                                                                        true);
                                                                    PurchLine.SetPurchHeader(Rec);

                                                                    if RecalculatePrice and "Prices Including VAT" then
                                                                      PurchLine.MODIFYALL(Amount,0,true);

                                                                    if "Currency Code" = '' then
                                                                      Currency.InitRoundingPrecision
                                                                    else
                                                                      Currency.GET("Currency Code");

                                                                    PurchLine.FINDSET;
                                                                    repeat
                                                                      PurchLine.TESTFIELD("Quantity Invoiced",0);
                                                                      PurchLine.TESTFIELD("Prepmt. Amt. Inv.",0);
                                                                      if not RecalculatePrice then begin
                                                                        PurchLine."VAT Difference" := 0;
                                                                        PurchLine.UpdateAmounts;
                                                                      end else
                                                                        if "Prices Including VAT" then begin
                                                                          PurchLine."Direct Unit Cost" :=
                                                                            ROUND(
                                                                              PurchLine."Direct Unit Cost" * (1 + PurchLine."VAT %" / 100),
                                                                              Currency."Unit-Amount Rounding Precision");
                                                                          if PurchLine.Quantity <> 0 then begin
                                                                            PurchLine."Line Discount Amount" :=
                                                                              ROUND(
                                                                                PurchLine.Quantity * PurchLine."Direct Unit Cost" * PurchLine."Line Discount %" / 100,
                                                                                Currency."Amount Rounding Precision");
                                                                            PurchLine.VALIDATE("Inv. Discount Amount",
                                                                              ROUND(
                                                                                PurchLine."Inv. Discount Amount" * (1 + PurchLine."VAT %" / 100),
                                                                                Currency."Amount Rounding Precision"));
                                                                          end;
                                                                        end else begin
                                                                          PurchLine."Direct Unit Cost" :=
                                                                            ROUND(
                                                                              PurchLine."Direct Unit Cost" / (1 + PurchLine."VAT %" / 100),
                                                                              Currency."Unit-Amount Rounding Precision");
                                                                          if PurchLine.Quantity <> 0 then begin
                                                                            PurchLine."Line Discount Amount" :=
                                                                              ROUND(
                                                                                PurchLine.Quantity * PurchLine."Direct Unit Cost" * PurchLine."Line Discount %" / 100,
                                                                                Currency."Amount Rounding Precision");
                                                                            PurchLine.VALIDATE("Inv. Discount Amount",
                                                                              ROUND(
                                                                                PurchLine."Inv. Discount Amount" / (1 + PurchLine."VAT %" / 100),
                                                                                Currency."Amount Rounding Precision"));
                                                                          end;
                                                                        end;
                                                                      PurchLine.MODIFY;
                                                                    until PurchLine.NEXT = 0;
                                                                  end;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Prices Including VAT }
    { 37  ;   ;Invoice Disc. Code  ;Code20        ;OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                MessageIfPurchLinesExist(FIELDCAPTION("Invoice Disc. Code"));
                                                              END;

                                                   CaptionML=ENU=Invoice Disc. Code }
    { 41  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   OnValidate=BEGIN
                                                                MessageIfPurchLinesExist(FIELDCAPTION("Language Code"));
                                                              END;

                                                   CaptionML=ENU=Language Code }
    { 43  ;   ;Purchaser Code      ;Code20        ;TableRelation="Salesperson/Purchaser";
                                                   OnValidate=VAR
                                                                ApprovalEntry@1001 : Record "Approval Entry";
                                                              BEGIN
                                                                ApprovalEntry.SETRANGE("Table ID",DATABASE::"Purchase Header");
                                                                ApprovalEntry.SETRANGE("Document Type","Document Type");
                                                                ApprovalEntry.SETRANGE("Document No.","No.");
                                                                ApprovalEntry.SETFILTER(Status,'%1|%2',ApprovalEntry.Status::Created,ApprovalEntry.Status::Open);
                                                                if not ApprovalEntry.ISEMPTY then
                                                                  ERROR(Text042,FIELDCAPTION("Purchaser Code"));

                                                                CreateDim(
                                                                  DATABASE::"Salesperson/Purchaser","Purchaser Code",
                                                                  DATABASE::Vendor,"Pay-to Vendor No.",
                                                                  DATABASE::Campaign,"Campaign No.",
                                                                  DATABASE::"Responsibility Center","Responsibility Center");
                                                              END;

                                                   CaptionML=ENU=Purchaser Code }
    { 45  ;   ;Order Class         ;Code10        ;CaptionML=ENU=Order Class }
    { 46  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Purch. Comment Line" WHERE ("Document Type"=FIELD("Document Type"),
                                                                                                  "No."=FIELD("No."),
                                                                                                  "Document Line No."=CONST(0)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=false }
    { 47  ;   ;No. Printed         ;Integer       ;CaptionML=ENU=No. Printed;
                                                   Editable=false }
    { 51  ;   ;On Hold             ;Code3         ;CaptionML=ENU=On Hold }
    { 52  ;   ;Applies-to Doc. Type;Option        ;CaptionML=ENU=Applies-to Doc. Type;
                                                   OptionCaptionML=ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                   OptionString=" ",Payment,Invoice,"Credit Memo","Finance Charge Memo",Reminder,Refund }
    { 53  ;   ;Applies-to Doc. No. ;Code20        ;OnValidate=BEGIN
                                                                if "Applies-to Doc. No." <> '' then
                                                                  TESTFIELD("Bal. Account No.",'');

                                                                if ("Applies-to Doc. No." <> xRec."Applies-to Doc. No.") and (xRec."Applies-to Doc. No." <> '') and
                                                                   ("Applies-to Doc. No." <> '')
                                                                then begin
                                                                  SetAmountToApply("Applies-to Doc. No.","Buy-from Vendor No.");
                                                                  SetAmountToApply(xRec."Applies-to Doc. No.","Buy-from Vendor No.");
                                                                end else
                                                                  if ("Applies-to Doc. No." <> xRec."Applies-to Doc. No.") and (xRec."Applies-to Doc. No." = '') then
                                                                    SetAmountToApply("Applies-to Doc. No.","Buy-from Vendor No.")
                                                                  else
                                                                    if ("Applies-to Doc. No." <> xRec."Applies-to Doc. No.") and ("Applies-to Doc. No." = '') then
                                                                      SetAmountToApply(xRec."Applies-to Doc. No.","Buy-from Vendor No.");
                                                              END;

                                                   OnLookup=VAR
                                                              GenJnlLine@1001 : Record "Gen. Journal Line";
                                                              GenJnlApply@1000 : Codeunit "Gen. Jnl.-Apply";
                                                              ApplyVendEntries@1002 : Page "Apply Vendor Entries";
                                                            BEGIN
                                                              TESTFIELD("Bal. Account No.",'');
                                                              VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive,"Due Date");
                                                              VendLedgEntry.SETRANGE("Vendor No.","Pay-to Vendor No.");
                                                              VendLedgEntry.SETRANGE(Open,true);
                                                              if "Applies-to Doc. No." <> '' then begin
                                                                VendLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                                                                VendLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
                                                                if VendLedgEntry.FINDFIRST then;
                                                                VendLedgEntry.SETRANGE("Document Type");
                                                                VendLedgEntry.SETRANGE("Document No.");
                                                              end else
                                                                if "Applies-to Doc. Type" <> 0 then begin
                                                                  VendLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                                                                  if VendLedgEntry.FINDFIRST then;
                                                                  VendLedgEntry.SETRANGE("Document Type");
                                                                end else
                                                                  if Amount <> 0 then begin
                                                                    VendLedgEntry.SETRANGE(Positive,Amount < 0);
                                                                    if VendLedgEntry.FINDFIRST then;
                                                                    VendLedgEntry.SETRANGE(Positive);
                                                                  end;
                                                              ApplyVendEntries.SetPurch(Rec,VendLedgEntry,PurchHeader.FIELDNO("Applies-to Doc. No."));
                                                              ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
                                                              ApplyVendEntries.SETRECORD(VendLedgEntry);
                                                              ApplyVendEntries.LOOKUPMODE(true);
                                                              if ApplyVendEntries.RUNMODAL = ACTION::LookupOK then begin
                                                                ApplyVendEntries.GetVendLedgEntry(VendLedgEntry);
                                                                GenJnlApply.CheckAgainstApplnCurrency(
                                                                  "Currency Code",VendLedgEntry."Currency Code",GenJnlLine."Account Type"::Vendor,true);
                                                                "Applies-to Doc. Type" := VendLedgEntry."Document Type";
                                                                "Applies-to Doc. No." := VendLedgEntry."Document No.";
                                                              end;
                                                              CLEAR(ApplyVendEntries);
                                                            END;

                                                   CaptionML=ENU=Applies-to Doc. No. }
    { 55  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF ("Bal. Account Type"=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF ("Bal. Account Type"=CONST("Bank Account")) "Bank Account";
                                                   OnValidate=BEGIN
                                                                if "Bal. Account No." <> '' then
                                                                  case "Bal. Account Type" of
                                                                    "Bal. Account Type"::"G/L Account":
                                                                      begin
                                                                        GLAcc.GET("Bal. Account No.");
                                                                        GLAcc.CheckGLAcc;
                                                                        GLAcc.TESTFIELD("Direct Posting",true);
                                                                      end;
                                                                    "Bal. Account Type"::"Bank Account":
                                                                      begin
                                                                        BankAcc.GET("Bal. Account No.");
                                                                        BankAcc.TESTFIELD(Blocked,false);
                                                                        BankAcc.TESTFIELD("Currency Code","Currency Code");
                                                                      end;
                                                                  end;
                                                              END;

                                                   CaptionML=ENU=Bal. Account No. }
    { 56  ;   ;Recalculate Invoice Disc.;Boolean  ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Purchase Line" WHERE ("Document Type"=FIELD("Document Type"),
                                                                                            "Document No."=FIELD("No."),
                                                                                            "Recalculate Invoice Disc."=CONST(true)));
                                                   CaptionML=ENU=Recalculate Invoice Disc.;
                                                   Editable=false }
    { 57  ;   ;Receive             ;Boolean       ;CaptionML=ENU=Receive }
    { 58  ;   ;Invoice             ;Boolean       ;CaptionML=ENU=Invoice }
    { 59  ;   ;Print Posted Documents;Boolean     ;CaptionML=ENU=Print Posted Documents }
    { 60  ;   ;Amount              ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line".Amount WHERE ("Document Type"=FIELD("Document Type"),
                                                                                                 "Document No."=FIELD("No.")));
                                                   CaptionML=ENU=Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 61  ;   ;Amount Including VAT;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line"."Amount Including VAT" WHERE ("Document Type"=FIELD("Document Type"),
                                                                                                                 "Document No."=FIELD("No.")));
                                                   CaptionML=ENU=Amount Including VAT;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 62  ;   ;Receiving No.       ;Code20        ;CaptionML=ENU=Receiving No. }
    { 63  ;   ;Posting No.         ;Code20        ;CaptionML=ENU=Posting No. }
    { 64  ;   ;Last Receiving No.  ;Code20        ;TableRelation="Purch. Rcpt. Header";
                                                   CaptionML=ENU=Last Receiving No.;
                                                   Editable=false }
    { 65  ;   ;Last Posting No.    ;Code20        ;TableRelation="Purch. Inv. Header";
                                                   CaptionML=ENU=Last Posting No.;
                                                   Editable=false }
    { 66  ;   ;Vendor Order No.    ;Code35        ;CaptionML=ENU=Vendor Order No. }
    { 67  ;   ;Vendor Shipment No. ;Code35        ;CaptionML=ENU=Vendor Shipment No. }
    { 68  ;   ;Vendor Invoice No.  ;Code35        ;OnValidate=VAR
                                                                VendorLedgerEntry@1000 : Record "Vendor Ledger Entry";
                                                              BEGIN
                                                                if "Vendor Invoice No." <> '' then
                                                                  if FindPostedDocumentWithSameExternalDocNo(VendorLedgerEntry,"Vendor Invoice No.") then
                                                                    ShowExternalDocAlreadyExistNotification(VendorLedgerEntry)
                                                                  else
                                                                    RecallExternalDocAlreadyExistsNotification;
                                                              END;

                                                   CaptionML=ENU=Vendor Invoice No. }
    { 69  ;   ;Vendor Cr. Memo No. ;Code35        ;OnValidate=VAR
                                                                VendorLedgerEntry@1000 : Record "Vendor Ledger Entry";
                                                              BEGIN
                                                                if "Vendor Cr. Memo No." <> '' then
                                                                  if FindPostedDocumentWithSameExternalDocNo(VendorLedgerEntry,"Vendor Cr. Memo No.") then
                                                                    ShowExternalDocAlreadyExistNotification(VendorLedgerEntry)
                                                                  else
                                                                    RecallExternalDocAlreadyExistsNotification;
                                                              END;

                                                   CaptionML=ENU=Vendor Cr. Memo No. }
    { 70  ;   ;VAT Registration No.;Text20        ;CaptionML=ENU=VAT Registration No. }
    { 72  ;   ;Sell-to Customer No.;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                if ("Document Type" = "Document Type"::Order) and
                                                                   (xRec."Sell-to Customer No." <> "Sell-to Customer No.")
                                                                then begin
                                                                  PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
                                                                  PurchLine.SETRANGE("Document No.","No.");
                                                                  PurchLine.SETFILTER("Sales Order Line No.",'<>0');
                                                                  if not PurchLine.ISEMPTY then
                                                                    ERROR(
                                                                      YouCannotChangeFieldErr,
                                                                      FIELDCAPTION("Sell-to Customer No."));

                                                                  PurchLine.SETRANGE("Sales Order Line No.");
                                                                  PurchLine.SETFILTER("Special Order Sales Line No.",'<>0');
                                                                  if not PurchLine.ISEMPTY then
                                                                    ERROR(
                                                                      YouCannotChangeFieldErr,
                                                                      FIELDCAPTION("Sell-to Customer No."));
                                                                end;

                                                                if "Sell-to Customer No." = '' then
                                                                  VALIDATE("Location Code",UserSetupMgt.GetLocation(1,'',"Responsibility Center"))
                                                                else
                                                                  VALIDATE("Ship-to Code",'');
                                                              END;

                                                   CaptionML=ENU=Sell-to Customer No. }
    { 73  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=ENU=Reason Code }
    { 74  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                if (xRec."Buy-from Vendor No." = "Buy-from Vendor No.") and
                                                                   (xRec."Gen. Bus. Posting Group" <> "Gen. Bus. Posting Group")
                                                                then begin
                                                                  if GenBusPostingGrp.ValidateVatBusPostingGroup(GenBusPostingGrp,"Gen. Bus. Posting Group") then
                                                                    "VAT Bus. Posting Group" := GenBusPostingGrp."Def. VAT Bus. Posting Group";
                                                                  RecreatePurchLines(FIELDCAPTION("Gen. Bus. Posting Group"));
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 76  ;   ;Transaction Type    ;Code10        ;TableRelation="Transaction Type";
                                                   OnValidate=BEGIN
                                                                UpdatePurchLines(FIELDCAPTION("Transaction Type"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=ENU=Transaction Type }
    { 77  ;   ;Transport Method    ;Code10        ;TableRelation="Transport Method";
                                                   OnValidate=BEGIN
                                                                UpdatePurchLines(FIELDCAPTION("Transport Method"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=ENU=Transport Method }
    { 78  ;   ;VAT Country/Region Code;Code10     ;TableRelation="Country/Region";
                                                   CaptionML=ENU=VAT Country/Region Code }
    { 79  ;   ;Buy-from Vendor Name;Text50        ;TableRelation=Vendor;
                                                   OnValidate=VAR
                                                                Vendor@1000 : Record Vendor;
                                                              BEGIN
                                                                VALIDATE("Buy-from Vendor No.",Vendor.GetVendorNo("Buy-from Vendor Name"));
                                                              END;

                                                   ValidateTableRelation=false;
                                                   CaptionML=ENU=Buy-from Vendor Name }
    { 80  ;   ;Buy-from Vendor Name 2;Text50      ;CaptionML=ENU=Buy-from Vendor Name 2 }
    { 81  ;   ;Buy-from Address    ;Text50        ;OnValidate=BEGIN
                                                                UpdatePayToAddressFromBuyFromAddress(FIELDNO("Pay-to Address"));
                                                                ModifyVendorAddress;
                                                              END;

                                                   CaptionML=ENU=Buy-from Address }
    { 82  ;   ;Buy-from Address 2  ;Text50        ;OnValidate=BEGIN
                                                                UpdatePayToAddressFromBuyFromAddress(FIELDNO("Pay-to Address 2"));
                                                                ModifyVendorAddress;
                                                              END;

                                                   CaptionML=ENU=Buy-from Address 2 }
    { 83  ;   ;Buy-from City       ;Text30        ;TableRelation=IF ("Buy-from Country/Region Code"=CONST('')) "Post Code".City
                                                                 ELSE IF ("Buy-from Country/Region Code"=FILTER(<>'')) "Post Code".City WHERE ("Country/Region Code"=FIELD("Buy-from Country/Region Code"));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(
                                                                  "Buy-from City","Buy-from Post Code","Buy-from County","Buy-from Country/Region Code",(CurrFieldNo <> 0) and GUIALLOWED);
                                                                UpdatePayToAddressFromBuyFromAddress(FIELDNO("Pay-to City"));
                                                                ModifyVendorAddress;
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Buy-from City }
    { 84  ;   ;Buy-from Contact    ;Text50        ;OnValidate=BEGIN
                                                                ModifyVendorAddress;
                                                              END;

                                                   OnLookup=VAR
                                                              Contact@1001 : Record Contact;
                                                            BEGIN
                                                              if "Buy-from Vendor No." = '' then
                                                                exit;

                                                              LookupContact("Buy-from Vendor No.","Buy-from Contact No.",Contact);
                                                              if PAGE.RUNMODAL(0,Contact) = ACTION::LookupOK then
                                                                VALIDATE("Buy-from Contact No.",Contact."No.");
                                                            END;

                                                   CaptionML=ENU=Buy-from Contact }
    { 85  ;   ;Pay-to Post Code    ;Code20        ;TableRelation=IF ("Pay-to Country/Region Code"=CONST('')) "Post Code"
                                                                 ELSE IF ("Pay-to Country/Region Code"=FILTER(<>'')) "Post Code" WHERE ("Country/Region Code"=FIELD("Pay-to Country/Region Code"));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(
                                                                  "Pay-to City","Pay-to Post Code","Pay-to County","Pay-to Country/Region Code",(CurrFieldNo <> 0) and GUIALLOWED);
                                                                ModifyPayToVendorAddress;
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Pay-to Post Code }
    { 86  ;   ;Pay-to County       ;Text30        ;OnValidate=BEGIN
                                                                ModifyPayToVendorAddress;
                                                              END;

                                                   CaptionML=ENU=Pay-to County }
    { 87  ;   ;Pay-to Country/Region Code;Code10  ;TableRelation="Country/Region";
                                                   OnValidate=BEGIN
                                                                ModifyPayToVendorAddress;
                                                              END;

                                                   CaptionML=ENU=Pay-to Country/Region Code }
    { 88  ;   ;Buy-from Post Code  ;Code20        ;TableRelation=IF ("Buy-from Country/Region Code"=CONST('')) "Post Code"
                                                                 ELSE IF ("Buy-from Country/Region Code"=FILTER(<>'')) "Post Code" WHERE ("Country/Region Code"=FIELD("Buy-from Country/Region Code"));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(
                                                                  "Buy-from City","Buy-from Post Code","Buy-from County","Buy-from Country/Region Code",(CurrFieldNo <> 0) and GUIALLOWED);
                                                                UpdatePayToAddressFromBuyFromAddress(FIELDNO("Pay-to Post Code"));
                                                                ModifyVendorAddress;
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Buy-from Post Code }
    { 89  ;   ;Buy-from County     ;Text30        ;OnValidate=BEGIN
                                                                UpdatePayToAddressFromBuyFromAddress(FIELDNO("Pay-to County"));
                                                                ModifyVendorAddress;
                                                              END;

                                                   CaptionML=ENU=Buy-from County }
    { 90  ;   ;Buy-from Country/Region Code;Code10;TableRelation="Country/Region";
                                                   OnValidate=BEGIN
                                                                UpdatePayToAddressFromBuyFromAddress(FIELDNO("Pay-to Country/Region Code"));
                                                                ModifyVendorAddress;
                                                              END;

                                                   CaptionML=ENU=Buy-from Country/Region Code }
    { 91  ;   ;Ship-to Post Code   ;Code20        ;TableRelation=IF ("Ship-to Country/Region Code"=CONST('')) "Post Code"
                                                                 ELSE IF ("Ship-to Country/Region Code"=FILTER(<>'')) "Post Code" WHERE ("Country/Region Code"=FIELD("Ship-to Country/Region Code"));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(
                                                                  "Ship-to City","Ship-to Post Code","Ship-to County","Ship-to Country/Region Code",(CurrFieldNo <> 0) and GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=false;
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Ship-to Post Code }
    { 92  ;   ;Ship-to County      ;Text30        ;CaptionML=ENU=Ship-to County }
    { 93  ;   ;Ship-to Country/Region Code;Code10 ;TableRelation="Country/Region";
                                                   CaptionML=ENU=Ship-to Country/Region Code }
    { 94  ;   ;Bal. Account Type   ;Option        ;CaptionML=ENU=Bal. Account Type;
                                                   OptionCaptionML=ENU=G/L Account,Bank Account;
                                                   OptionString="G/L Account","Bank Account" }
    { 95  ;   ;Order Address Code  ;Code10        ;TableRelation="Order Address".Code WHERE ("Vendor No."=FIELD("Buy-from Vendor No."));
                                                   OnValidate=BEGIN
                                                                if "Order Address Code" <> '' then begin
                                                                  OrderAddr.GET("Buy-from Vendor No.","Order Address Code");
                                                                  "Buy-from Vendor Name" := OrderAddr.Name;
                                                                  "Buy-from Vendor Name 2" := OrderAddr."Name 2";
                                                                  "Buy-from Address" := OrderAddr.Address;
                                                                  "Buy-from Address 2" := OrderAddr."Address 2";
                                                                  "Buy-from City" := OrderAddr.City;
                                                                  "Buy-from Contact" := OrderAddr.Contact;
                                                                  "Buy-from Post Code" := OrderAddr."Post Code";
                                                                  "Buy-from County" := OrderAddr.County;
                                                                  "Buy-from Country/Region Code" := OrderAddr."Country/Region Code";

                                                                  if IsCreditDocType then begin
                                                                    SetShipToAddress(
                                                                      OrderAddr.Name,OrderAddr."Name 2",OrderAddr.Address,OrderAddr."Address 2",
                                                                      OrderAddr.City,OrderAddr."Post Code",OrderAddr.County,OrderAddr."Country/Region Code");
                                                                    "Ship-to Contact" := OrderAddr.Contact;
                                                                  end
                                                                end else begin
                                                                  GetVend("Buy-from Vendor No.");
                                                                  "Buy-from Vendor Name" := Vend.Name;
                                                                  "Buy-from Vendor Name 2" := Vend."Name 2";
                                                                  CopyPayToVendorAddressFieldsFromVendor(Vend);

                                                                  if IsCreditDocType then begin
                                                                    "Ship-to Name" := Vend.Name;
                                                                    "Ship-to Name 2" := Vend."Name 2";
                                                                    CopyShipToVendorAddressFieldsFromVendor(Vend);
                                                                    "Ship-to Contact" := Vend.Contact;
                                                                    "Shipment Method Code" := Vend."Shipment Method Code";
                                                                    if Vend."Location Code" <> '' then
                                                                      VALIDATE("Location Code",Vend."Location Code");
                                                                  end
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Order Address Code }
    { 97  ;   ;Entry Point         ;Code10        ;TableRelation="Entry/Exit Point";
                                                   OnValidate=BEGIN
                                                                UpdatePurchLines(FIELDCAPTION("Entry Point"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=ENU=Entry Point }
    { 98  ;   ;Correction          ;Boolean       ;CaptionML=ENU=Correction }
    { 99  ;   ;Document Date       ;Date          ;OnValidate=BEGIN
                                                                if xRec."Document Date" <> "Document Date" then
                                                                  UpdateDocumentDate := true;
                                                                VALIDATE("Payment Terms Code");
                                                                VALIDATE("Prepmt. Payment Terms Code");
                                                              END;

                                                   CaptionML=ENU=Document Date }
    { 101 ;   ;Area                ;Code10        ;TableRelation=Area;
                                                   OnValidate=BEGIN
                                                                UpdatePurchLines(FIELDCAPTION(Area),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=ENU=Area }
    { 102 ;   ;Transaction Specification;Code10   ;TableRelation="Transaction Specification";
                                                   OnValidate=BEGIN
                                                                UpdatePurchLines(FIELDCAPTION("Transaction Specification"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=ENU=Transaction Specification }
    { 104 ;   ;Payment Method Code ;Code10        ;TableRelation="Payment Method";
                                                   OnValidate=BEGIN
                                                                PaymentMethod.INIT;
                                                                if "Payment Method Code" <> '' then
                                                                  PaymentMethod.GET("Payment Method Code");
                                                                "Bal. Account Type" := PaymentMethod."Bal. Account Type";
                                                                "Bal. Account No." := PaymentMethod."Bal. Account No.";
                                                                if "Bal. Account No." <> '' then begin
                                                                  TESTFIELD("Applies-to Doc. No.",'');
                                                                  TESTFIELD("Applies-to ID",'');
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Payment Method Code }
    { 107 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=false }
    { 108 ;   ;Posting No. Series  ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if "Posting No. Series" <> '' then begin
                                                                  PurchSetup.GET;
                                                                  TestNoSeries;
                                                                  NoSeriesMgt.TestSeries(GetPostingNoSeriesCode,"Posting No. Series");
                                                                end;
                                                                TESTFIELD("Posting No.",'');
                                                              END;

                                                   OnLookup=BEGIN
                                                              with PurchHeader do begin
                                                                PurchHeader := Rec;
                                                                PurchSetup.GET;
                                                                TestNoSeries;
                                                                if NoSeriesMgt.LookupSeries(GetPostingNoSeriesCode,"Posting No. Series") then
                                                                  VALIDATE("Posting No. Series");
                                                                Rec := PurchHeader;
                                                              end;
                                                            END;

                                                   CaptionML=ENU=Posting No. Series }
    { 109 ;   ;Receiving No. Series;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if "Receiving No. Series" <> '' then begin
                                                                  PurchSetup.GET;
                                                                  PurchSetup.TESTFIELD("Posted Receipt Nos.");
                                                                  NoSeriesMgt.TestSeries(PurchSetup."Posted Receipt Nos.","Receiving No. Series");
                                                                end;
                                                                TESTFIELD("Receiving No.",'');
                                                              END;

                                                   OnLookup=BEGIN
                                                              with PurchHeader do begin
                                                                PurchHeader := Rec;
                                                                PurchSetup.GET;
                                                                PurchSetup.TESTFIELD("Posted Receipt Nos.");
                                                                if NoSeriesMgt.LookupSeries(PurchSetup."Posted Receipt Nos.","Receiving No. Series") then
                                                                  VALIDATE("Receiving No. Series");
                                                                Rec := PurchHeader;
                                                              end;
                                                            END;

                                                   CaptionML=ENU=Receiving No. Series }
    { 114 ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                MessageIfPurchLinesExist(FIELDCAPTION("Tax Area Code"));
                                                              END;

                                                   CaptionML=ENU=Tax Area Code }
    { 115 ;   ;Tax Liable          ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                MessageIfPurchLinesExist(FIELDCAPTION("Tax Liable"));
                                                              END;

                                                   CaptionML=ENU=Tax Liable }
    { 116 ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                if (xRec."Buy-from Vendor No." = "Buy-from Vendor No.") and
                                                                   (xRec."VAT Bus. Posting Group" <> "VAT Bus. Posting Group")
                                                                then
                                                                  RecreatePurchLines(FIELDCAPTION("VAT Bus. Posting Group"));
                                                              END;

                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 118 ;   ;Applies-to ID       ;Code50        ;OnValidate=VAR
                                                                TempVendLedgEntry@1000 : TEMPORARY Record "Vendor Ledger Entry";
                                                                VendEntrySetApplID@1001 : Codeunit "Vend. Entry-SetAppl.ID";
                                                              BEGIN
                                                                if "Applies-to ID" <> '' then
                                                                  TESTFIELD("Bal. Account No.",'');
                                                                if ("Applies-to ID" <> xRec."Applies-to ID") and (xRec."Applies-to ID" <> '') then begin
                                                                  VendLedgEntry.SETCURRENTKEY("Vendor No.",Open);
                                                                  VendLedgEntry.SETRANGE("Vendor No.","Pay-to Vendor No.");
                                                                  VendLedgEntry.SETRANGE(Open,true);
                                                                  VendLedgEntry.SETRANGE("Applies-to ID",xRec."Applies-to ID");
                                                                  if VendLedgEntry.FINDFIRST then
                                                                    VendEntrySetApplID.SetApplId(VendLedgEntry,TempVendLedgEntry,'');
                                                                  VendLedgEntry.RESET;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Applies-to ID }
    { 119 ;   ;VAT Base Discount % ;Decimal       ;OnValidate=BEGIN
                                                                GLSetup.GET;
                                                                if "VAT Base Discount %" > GLSetup."VAT Tolerance %" then begin
                                                                  if HideValidationDialog then
                                                                    Confirmed := true
                                                                  else
                                                                    Confirmed :=
                                                                      CONFIRM(
                                                                        Text007 +
                                                                        Text008,false,
                                                                        FIELDCAPTION("VAT Base Discount %"),
                                                                        GLSetup.FIELDCAPTION("VAT Tolerance %"),
                                                                        GLSetup.TABLECAPTION);
                                                                  if not Confirmed then
                                                                    "VAT Base Discount %" := xRec."VAT Base Discount %";
                                                                end;

                                                                if ("VAT Base Discount %" = xRec."VAT Base Discount %") and
                                                                   (CurrFieldNo <> 0)
                                                                then
                                                                  exit;

                                                                PurchLine.SETRANGE("Document Type","Document Type");
                                                                PurchLine.SETRANGE("Document No.","No.");
                                                                PurchLine.SETFILTER(Type,'<>%1',PurchLine.Type::" ");
                                                                PurchLine.SETFILTER(Quantity,'<>0');
                                                                PurchLine.LOCKTABLE;
                                                                if PurchLine.FINDSET then begin
                                                                  MODIFY;
                                                                  repeat
                                                                    PurchLine.UpdateAmounts;
                                                                    PurchLine.MODIFY;
                                                                  until PurchLine.NEXT = 0;
                                                                end;
                                                                PurchLine.RESET;
                                                              END;

                                                   CaptionML=ENU=VAT Base Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 120 ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Open,Released,Pending Approval,Pending Prepayment;
                                                   OptionString=Open,Released,"Pending Approval","Pending Prepayment";
                                                   Editable=false }
    { 121 ;   ;Invoice Discount Calculation;Option;CaptionML=ENU=Invoice Discount Calculation;
                                                   OptionCaptionML=ENU=None,%,Amount;
                                                   OptionString=None,"%",Amount;
                                                   Editable=false }
    { 122 ;   ;Invoice Discount Value;Decimal     ;CaptionML=ENU=Invoice Discount Value;
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 123 ;   ;Send IC Document    ;Boolean       ;OnValidate=BEGIN
                                                                if "Send IC Document" then begin
                                                                  TESTFIELD("Buy-from IC Partner Code");
                                                                  TESTFIELD("IC Direction","IC Direction"::Outgoing);
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Send IC Document }
    { 124 ;   ;IC Status           ;Option        ;CaptionML=ENU=IC Status;
                                                   OptionCaptionML=ENU=New,Pending,Sent;
                                                   OptionString=New,Pending,Sent }
    { 125 ;   ;Buy-from IC Partner Code;Code20    ;TableRelation="IC Partner";
                                                   CaptionML=ENU=Buy-from IC Partner Code;
                                                   Editable=false }
    { 126 ;   ;Pay-to IC Partner Code;Code20      ;TableRelation="IC Partner";
                                                   CaptionML=ENU=Pay-to IC Partner Code;
                                                   Editable=false }
    { 129 ;   ;IC Direction        ;Option        ;OnValidate=BEGIN
                                                                if "IC Direction" = "IC Direction"::Incoming then
                                                                  "Send IC Document" := false;
                                                              END;

                                                   CaptionML=ENU=IC Direction;
                                                   OptionCaptionML=ENU=Outgoing,Incoming;
                                                   OptionString=Outgoing,Incoming }
    { 130 ;   ;Prepayment No.      ;Code20        ;CaptionML=ENU=Prepayment No. }
    { 131 ;   ;Last Prepayment No. ;Code20        ;TableRelation="Purch. Inv. Header";
                                                   CaptionML=ENU=Last Prepayment No. }
    { 132 ;   ;Prepmt. Cr. Memo No.;Code20        ;CaptionML=ENU=Prepmt. Cr. Memo No. }
    { 133 ;   ;Last Prepmt. Cr. Memo No.;Code20   ;TableRelation="Purch. Cr. Memo Hdr.";
                                                   CaptionML=ENU=Last Prepmt. Cr. Memo No. }
    { 134 ;   ;Prepayment %        ;Decimal       ;OnValidate=BEGIN
                                                                if xRec."Prepayment %" <> "Prepayment %" then
                                                                  UpdatePurchLines(FIELDCAPTION("Prepayment %"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=ENU=Prepayment %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 135 ;   ;Prepayment No. Series;Code20       ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if "Prepayment No. Series" <> '' then begin
                                                                  PurchSetup.GET;
                                                                  PurchSetup.TESTFIELD("Posted Prepmt. Inv. Nos.");
                                                                  NoSeriesMgt.TestSeries(GetPostingPrepaymentNoSeriesCode,"Prepayment No. Series");
                                                                end;
                                                                TESTFIELD("Prepayment No.",'');
                                                              END;

                                                   OnLookup=BEGIN
                                                              with PurchHeader do begin
                                                                PurchHeader := Rec;
                                                                PurchSetup.GET;
                                                                PurchSetup.TESTFIELD("Posted Prepmt. Inv. Nos.");
                                                                if NoSeriesMgt.LookupSeries(GetPostingPrepaymentNoSeriesCode,"Prepayment No. Series") then
                                                                  VALIDATE("Prepayment No. Series");
                                                                Rec := PurchHeader;
                                                              end;
                                                            END;

                                                   CaptionML=ENU=Prepayment No. Series }
    { 136 ;   ;Compress Prepayment ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Compress Prepayment }
    { 137 ;   ;Prepayment Due Date ;Date          ;CaptionML=ENU=Prepayment Due Date }
    { 138 ;   ;Prepmt. Cr. Memo No. Series;Code20 ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if "Prepmt. Cr. Memo No. Series" <> '' then begin
                                                                  PurchSetup.GET;
                                                                  PurchSetup.TESTFIELD("Posted Prepmt. Cr. Memo Nos.");
                                                                  NoSeriesMgt.TestSeries(GetPostingPrepaymentNoSeriesCode,"Prepmt. Cr. Memo No. Series");
                                                                end;
                                                                TESTFIELD("Prepmt. Cr. Memo No.",'');
                                                              END;

                                                   OnLookup=BEGIN
                                                              with PurchHeader do begin
                                                                PurchHeader := Rec;
                                                                PurchSetup.GET;
                                                                PurchSetup.TESTFIELD("Posted Prepmt. Cr. Memo Nos.");
                                                                if NoSeriesMgt.LookupSeries(GetPostingPrepaymentNoSeriesCode,"Prepmt. Cr. Memo No. Series") then
                                                                  VALIDATE("Prepmt. Cr. Memo No. Series");
                                                                Rec := PurchHeader;
                                                              end;
                                                            END;

                                                   CaptionML=ENU=Prepmt. Cr. Memo No. Series }
    { 139 ;   ;Prepmt. Posting Description;Text50 ;CaptionML=ENU=Prepmt. Posting Description }
    { 142 ;   ;Prepmt. Pmt. Discount Date;Date    ;CaptionML=ENU=Prepmt. Pmt. Discount Date }
    { 143 ;   ;Prepmt. Payment Terms Code;Code10  ;TableRelation="Payment Terms";
                                                   OnValidate=VAR
                                                                PaymentTerms@1000 : Record "Payment Terms";
                                                              BEGIN
                                                                if ("Prepmt. Payment Terms Code" <> '') and ("Document Date" <> 0D) then begin
                                                                  PaymentTerms.GET("Prepmt. Payment Terms Code");
                                                                  if IsCreditDocType and not PaymentTerms."Calc. Pmt. Disc. on Cr. Memos" then begin
                                                                    VALIDATE("Prepayment Due Date","Document Date");
                                                                    VALIDATE("Prepmt. Pmt. Discount Date",0D);
                                                                    VALIDATE("Prepmt. Payment Discount %",0);
                                                                  end else begin
                                                                    "Prepayment Due Date" := CALCDATE(PaymentTerms."Due Date Calculation","Document Date");
                                                                    "Prepmt. Pmt. Discount Date" := CALCDATE(PaymentTerms."Discount Date Calculation","Document Date");
                                                                    if not UpdateDocumentDate then
                                                                      VALIDATE("Prepmt. Payment Discount %",PaymentTerms."Discount %")
                                                                  end;
                                                                end else begin
                                                                  VALIDATE("Prepayment Due Date","Document Date");
                                                                  if not UpdateDocumentDate then begin
                                                                    VALIDATE("Prepmt. Pmt. Discount Date",0D);
                                                                    VALIDATE("Prepmt. Payment Discount %",0);
                                                                  end;
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Prepmt. Payment Terms Code }
    { 144 ;   ;Prepmt. Payment Discount %;Decimal ;OnValidate=BEGIN
                                                                if not (CurrFieldNo in [0,FIELDNO("Posting Date"),FIELDNO("Document Date")]) then
                                                                  TESTFIELD(Status,Status::Open);
                                                                GLSetup.GET;
                                                                if "Payment Discount %" < GLSetup."VAT Tolerance %" then
                                                                  "VAT Base Discount %" := "Payment Discount %"
                                                                else
                                                                  "VAT Base Discount %" := GLSetup."VAT Tolerance %";
                                                                VALIDATE("VAT Base Discount %");
                                                              END;

                                                   CaptionML=ENU=Prepmt. Payment Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 151 ;   ;Quote No.           ;Code20        ;CaptionML=ENU=Quote No.;
                                                   Editable=false }
    { 160 ;   ;Job Queue Status    ;Option        ;OnLookup=VAR
                                                              JobQueueEntry@1000 : Record "Job Queue Entry";
                                                            BEGIN
                                                              if "Job Queue Status" = "Job Queue Status"::" " then
                                                                exit;
                                                              JobQueueEntry.ShowStatusMsg("Job Queue Entry ID");
                                                            END;

                                                   CaptionML=ENU=Job Queue Status;
                                                   OptionCaptionML=ENU=" ,Scheduled for Posting,Error,Posting";
                                                   OptionString=" ","Scheduled for Posting",Error,Posting;
                                                   Editable=false }
    { 161 ;   ;Job Queue Entry ID  ;GUID          ;CaptionML=ENU=Job Queue Entry ID;
                                                   Editable=false }
    { 165 ;   ;Incoming Document Entry No.;Integer;TableRelation="Incoming Document";
                                                   OnValidate=VAR
                                                                IncomingDocument@1000 : Record "Incoming Document";
                                                              BEGIN
                                                                if "Incoming Document Entry No." = xRec."Incoming Document Entry No." then
                                                                  exit;
                                                                if "Incoming Document Entry No." = 0 then
                                                                  IncomingDocument.RemoveReferenceToWorkingDocument(xRec."Incoming Document Entry No.")
                                                                else
                                                                  IncomingDocument.SetPurchDoc(Rec);
                                                              END;

                                                   CaptionML=ENU=Incoming Document Entry No. }
    { 170 ;   ;Creditor No.        ;Code20        ;CaptionML=ENU=Creditor No.;
                                                   Numeric=true }
    { 171 ;   ;Payment Reference   ;Code50        ;CaptionML=ENU=Payment Reference;
                                                   Numeric=true }
    { 300 ;   ;A. Rcd. Not Inv. Ex. VAT (LCY);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line"."A. Rcd. Not Inv. Ex. VAT (LCY)" WHERE ("Document No."=FIELD("No.")));
                                                   CaptionML=ENU=Amount Received Not Invoiced (LCY) }
    { 301 ;   ;Amt. Rcd. Not Invoiced (LCY);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line"."Amt. Rcd. Not Invoiced (LCY)" WHERE ("Document No."=FIELD("No.")));
                                                   CaptionML=ENU=Amount Received Not Invoiced (LCY) Incl. VAT }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDocDim;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
    { 1305;   ;Invoice Discount Amount;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line"."Inv. Discount Amount" WHERE ("Document No."=FIELD("No."),
                                                                                                                 "Document Type"=FIELD("Document Type")));
                                                   CaptionML=ENU=Invoice Discount Amount;
                                                   Editable=false;
                                                   AutoFormatType=1 }
    { 5043;   ;No. of Archived Versions;Integer   ;FieldClass=FlowField;
                                                   CalcFormula=Max("Purchase Header Archive"."Version No." WHERE ("Document Type"=FIELD("Document Type"),
                                                                                                                  "No."=FIELD("No."),
                                                                                                                  "Doc. No. Occurrence"=FIELD("Doc. No. Occurrence")));
                                                   CaptionML=ENU=No. of Archived Versions;
                                                   Editable=false }
    { 5048;   ;Doc. No. Occurrence ;Integer       ;CaptionML=ENU=Doc. No. Occurrence }
    { 5050;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   OnValidate=BEGIN
                                                                CreateDim(
                                                                  DATABASE::Campaign,"Campaign No.",
                                                                  DATABASE::Vendor,"Pay-to Vendor No.",
                                                                  DATABASE::"Salesperson/Purchaser","Purchaser Code",
                                                                  DATABASE::"Responsibility Center","Responsibility Center");
                                                              END;

                                                   CaptionML=ENU=Campaign No. }
    { 5052;   ;Buy-from Contact No.;Code20        ;TableRelation=Contact;
                                                   OnValidate=VAR
                                                                ContBusinessRelation@1000 : Record "Contact Business Relation";
                                                                Cont@1002 : Record Contact;
                                                              BEGIN
                                                                TESTFIELD(Status,Status::Open);

                                                                if ("Buy-from Contact No." <> xRec."Buy-from Contact No.") and
                                                                   (xRec."Buy-from Contact No." <> '')
                                                                then begin
                                                                  if HideValidationDialog then
                                                                    Confirmed := true
                                                                  else
                                                                    Confirmed := CONFIRM(ConfirmChangeQst,false,FIELDCAPTION("Buy-from Contact No."));
                                                                  if Confirmed then begin
                                                                    if InitFromContact("Buy-from Contact No.","Buy-from Vendor No.",FIELDCAPTION("Buy-from Contact No.")) then
                                                                      exit
                                                                  end else begin
                                                                    Rec := xRec;
                                                                    exit;
                                                                  end;
                                                                end;

                                                                if ("Buy-from Vendor No." <> '') and ("Buy-from Contact No." <> '') then begin
                                                                  Cont.GET("Buy-from Contact No.");
                                                                  if ContBusinessRelation.FindByRelation(ContBusinessRelation."Link to Table"::Vendor,"Buy-from Vendor No.") then
                                                                    if ContBusinessRelation."Contact No." <> Cont."Company No." then
                                                                      ERROR(Text038,Cont."No.",Cont.Name,"Buy-from Vendor No.");
                                                                end;

                                                                UpdateBuyFromVend("Buy-from Contact No.");
                                                              END;

                                                   OnLookup=VAR
                                                              Cont@1001 : Record Contact;
                                                              ContBusinessRelation@1000 : Record "Contact Business Relation";
                                                            BEGIN
                                                              if "Buy-from Vendor No." <> '' then
                                                                if Cont.GET("Buy-from Contact No.") then
                                                                  Cont.SETRANGE("Company No.",Cont."Company No.")
                                                                else
                                                                  if ContBusinessRelation.FindByRelation(ContBusinessRelation."Link to Table"::Vendor,"Buy-from Vendor No.") then
                                                                    Cont.SETRANGE("Company No.",ContBusinessRelation."Contact No.")
                                                                  else
                                                                    Cont.SETRANGE("No.",'');

                                                              if "Buy-from Contact No." <> '' then
                                                                if Cont.GET("Buy-from Contact No.") then ;
                                                              if PAGE.RUNMODAL(0,Cont) = ACTION::LookupOK then begin
                                                                xRec := Rec;
                                                                VALIDATE("Buy-from Contact No.",Cont."No.");
                                                              end;
                                                            END;

                                                   CaptionML=ENU=Buy-from Contact No. }
    { 5053;   ;Pay-to Contact No.  ;Code20        ;TableRelation=Contact;
                                                   OnValidate=VAR
                                                                ContBusinessRelation@1004 : Record "Contact Business Relation";
                                                                Cont@1002 : Record Contact;
                                                              BEGIN
                                                                TESTFIELD(Status,Status::Open);

                                                                if ("Pay-to Contact No." <> xRec."Pay-to Contact No.") and
                                                                   (xRec."Pay-to Contact No." <> '')
                                                                then begin
                                                                  if HideValidationDialog then
                                                                    Confirmed := true
                                                                  else
                                                                    Confirmed := CONFIRM(ConfirmChangeQst,false,FIELDCAPTION("Pay-to Contact No."));
                                                                  if Confirmed then begin
                                                                    if InitFromContact("Pay-to Contact No.","Pay-to Vendor No.",FIELDCAPTION("Pay-to Contact No.")) then
                                                                      exit
                                                                  end else begin
                                                                    "Pay-to Contact No." := xRec."Pay-to Contact No.";
                                                                    exit;
                                                                  end;
                                                                end;

                                                                if ("Pay-to Vendor No." <> '') and ("Pay-to Contact No." <> '') then begin
                                                                  Cont.GET("Pay-to Contact No.");
                                                                  if ContBusinessRelation.FindByRelation(ContBusinessRelation."Link to Table"::Vendor,"Pay-to Vendor No.") then
                                                                    if ContBusinessRelation."Contact No." <> Cont."Company No." then
                                                                      ERROR(Text038,Cont."No.",Cont.Name,"Pay-to Vendor No.");
                                                                end;

                                                                UpdatePayToVend("Pay-to Contact No.");
                                                              END;

                                                   OnLookup=VAR
                                                              Cont@1000 : Record Contact;
                                                              ContBusinessRelation@1001 : Record "Contact Business Relation";
                                                            BEGIN
                                                              if "Pay-to Vendor No." <> '' then
                                                                if Cont.GET("Pay-to Contact No.") then
                                                                  Cont.SETRANGE("Company No.",Cont."Company No.")
                                                                else
                                                                  if ContBusinessRelation.FindByRelation(ContBusinessRelation."Link to Table"::Vendor,"Pay-to Vendor No.") then
                                                                    Cont.SETRANGE("Company No.",ContBusinessRelation."Contact No.")
                                                                  else
                                                                    Cont.SETRANGE("No.",'');

                                                              if "Pay-to Contact No." <> '' then
                                                                if Cont.GET("Pay-to Contact No.") then ;
                                                              if PAGE.RUNMODAL(0,Cont) = ACTION::LookupOK then begin
                                                                xRec := Rec;
                                                                VALIDATE("Pay-to Contact No.",Cont."No.");
                                                              end;
                                                            END;

                                                   CaptionML=ENU=Pay-to Contact No. }
    { 5700;   ;Responsibility Center;Code10       ;TableRelation="Responsibility Center";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                if not UserSetupMgt.CheckRespCenter(1,"Responsibility Center") then
                                                                  ERROR(
                                                                    Text028,
                                                                    RespCenter.TABLECAPTION,UserSetupMgt.GetPurchasesFilter);

                                                                "Location Code" := UserSetupMgt.GetLocation(1,'',"Responsibility Center");
                                                                if "Location Code" = '' then begin
                                                                  if InvtSetup.GET then
                                                                    "Inbound Whse. Handling Time" := InvtSetup."Inbound Whse. Handling Time";
                                                                end else begin
                                                                  if Location.GET("Location Code") then;
                                                                  "Inbound Whse. Handling Time" := Location."Inbound Whse. Handling Time";
                                                                end;

                                                                UpdateShipToAddress;

                                                                CreateDim(
                                                                  DATABASE::"Responsibility Center","Responsibility Center",
                                                                  DATABASE::Vendor,"Pay-to Vendor No.",
                                                                  DATABASE::"Salesperson/Purchaser","Purchaser Code",
                                                                  DATABASE::Campaign,"Campaign No.");

                                                                if xRec."Responsibility Center" <> "Responsibility Center" then begin
                                                                  RecreatePurchLines(FIELDCAPTION("Responsibility Center"));
                                                                  "Assigned User ID" := '';
                                                                end;
                                                              END;

                                                   CaptionML=ENU=Responsibility Center }
    { 5752;   ;Completely Received ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Min("Purchase Line"."Completely Received" WHERE ("Document Type"=FIELD("Document Type"),
                                                                                                                "Document No."=FIELD("No."),
                                                                                                                Type=FILTER(<>" "),
                                                                                                                "Location Code"=FIELD("Location Filter")));
                                                   CaptionML=ENU=Completely Received;
                                                   Editable=false }
    { 5753;   ;Posting from Whse. Ref.;Integer    ;AccessByPermission=TableData Location=R;
                                                   CaptionML=ENU=Posting from Whse. Ref. }
    { 5754;   ;Location Filter     ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Location;
                                                   CaptionML=ENU=Location Filter }
    { 5790;   ;Requested Receipt Date;Date        ;OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                if "Promised Receipt Date" <> 0D then
                                                                  ERROR(
                                                                    Text034,
                                                                    FIELDCAPTION("Requested Receipt Date"),
                                                                    FIELDCAPTION("Promised Receipt Date"));

                                                                if "Requested Receipt Date" <> xRec."Requested Receipt Date" then
                                                                  UpdatePurchLines(FIELDCAPTION("Requested Receipt Date"),CurrFieldNo <> 0);
                                                              END;

                                                   AccessByPermission=TableData "Order Promising Line"=R;
                                                   CaptionML=ENU=Requested Receipt Date }
    { 5791;   ;Promised Receipt Date;Date         ;OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                if "Promised Receipt Date" <> xRec."Promised Receipt Date" then
                                                                  UpdatePurchLines(FIELDCAPTION("Promised Receipt Date"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=ENU=Promised Receipt Date }
    { 5792;   ;Lead Time Calculation;DateFormula  ;OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                LeadTimeMgt.CheckLeadTimeIsNotNegative("Lead Time Calculation");

                                                                if "Lead Time Calculation" <> xRec."Lead Time Calculation" then
                                                                  UpdatePurchLines(FIELDCAPTION("Lead Time Calculation"),CurrFieldNo <> 0);
                                                              END;

                                                   AccessByPermission=TableData "Purch. Rcpt. Header"=R;
                                                   CaptionML=ENU=Lead Time Calculation }
    { 5793;   ;Inbound Whse. Handling Time;DateFormula;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                if "Inbound Whse. Handling Time" <> xRec."Inbound Whse. Handling Time" then
                                                                  UpdatePurchLines(FIELDCAPTION("Inbound Whse. Handling Time"),CurrFieldNo <> 0);
                                                              END;

                                                   AccessByPermission=TableData Location=R;
                                                   CaptionML=ENU=Inbound Whse. Handling Time }
    { 5796;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
    { 5800;   ;Vendor Authorization No.;Code35    ;CaptionML=ENU=Vendor Authorization No. }
    { 5801;   ;Return Shipment No. ;Code20        ;CaptionML=ENU=Return Shipment No. }
    { 5802;   ;Return Shipment No. Series;Code20  ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                if "Return Shipment No. Series" <> '' then begin
                                                                  PurchSetup.GET;
                                                                  PurchSetup.TESTFIELD("Posted Return Shpt. Nos.");
                                                                  NoSeriesMgt.TestSeries(PurchSetup."Posted Return Shpt. Nos.","Return Shipment No. Series");
                                                                end;
                                                                TESTFIELD("Return Shipment No.",'');
                                                              END;

                                                   OnLookup=BEGIN
                                                              with PurchHeader do begin
                                                                PurchHeader := Rec;
                                                                PurchSetup.GET;
                                                                PurchSetup.TESTFIELD("Posted Return Shpt. Nos.");
                                                                if NoSeriesMgt.LookupSeries(PurchSetup."Posted Return Shpt. Nos.","Return Shipment No. Series") then
                                                                  VALIDATE("Return Shipment No. Series");
                                                                Rec := PurchHeader;
                                                              end;
                                                            END;

                                                   CaptionML=ENU=Return Shipment No. Series }
    { 5803;   ;Ship                ;Boolean       ;CaptionML=ENU=Ship }
    { 5804;   ;Last Return Shipment No.;Code20    ;TableRelation="Return Shipment Header";
                                                   CaptionML=ENU=Last Return Shipment No.;
                                                   Editable=false }
    { 8000;   ;Id                  ;GUID          ;CaptionML=ENU=Id }
    { 9000;   ;Assigned User ID    ;Code50        ;TableRelation="User Setup";
                                                   OnValidate=BEGIN
                                                                if not UserSetupMgt.CheckRespCenter2(1,"Responsibility Center","Assigned User ID") then
                                                                  ERROR(
                                                                    Text049,"Assigned User ID",
                                                                    RespCenter.TABLECAPTION,UserSetupMgt.GetPurchasesFilter2("Assigned User ID"));
                                                              END;

                                                   CaptionML=ENU=Assigned User ID }
    { 9001;   ;Pending Approvals   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Entry" WHERE ("Table ID"=CONST(38),
                                                                                             "Document Type"=FIELD("Document Type"),
                                                                                             "Document No."=FIELD("No."),
                                                                                             Status=FILTER(Open|Created)));
                                                   CaptionML=ENU=Pending Approvals }
  }
  KEYS
  {
    {    ;"Document Type","No."                   ;Clustered=true }
    {    ;"No.","Document Type"                    }
    {    ;"Document Type","Buy-from Vendor No."    }
    {    ;"Document Type","Pay-to Vendor No."      }
    {    ;"Buy-from Vendor No."                    }
    {    ;"Incoming Document Entry No."            }
    {    ;"Document Date"                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text003@1003 : TextConst 'ENU=You cannot rename a %1.';
      ConfirmChangeQst@1004 : TextConst '@@@="%1 = a Field Caption like Currency Code";ENU=Do you want to change %1?';
      Text005@1005 : TextConst 'ENU=You cannot reset %1 because the document still has one or more lines.';
      YouCannotChangeFieldErr@1006 : TextConst '@@@=%1 - fieldcaption;ENU=You cannot change %1 because the order is associated with one or more sales orders.';
      Text007@1007 : TextConst 'ENU=%1 is greater than %2 in the %3 table.\';
      Text008@1008 : TextConst 'ENU=Confirm change?';
      Text009@1009 : TextConst '@@@="%1 = Document No.";ENU=Deleting this document will cause a gap in the number series for receipts. An empty receipt %1 will be created to fill this gap in the number series.\\Do you want to continue?';
      Text012@1012 : TextConst '@@@="%1 = Document No.";ENU=Deleting this document will cause a gap in the number series for posted invoices. An empty posted invoice %1 will be created to fill this gap in the number series.\\Do you want to continue?';
      Text014@1014 : TextConst '@@@="%1 = Document No.";ENU=Deleting this document will cause a gap in the number series for posted credit memos. An empty posted credit memo %1 will be created to fill this gap in the number series.\\Do you want to continue?';
      Text016@1016 : TextConst 'ENU=If you change %1, the existing purchase lines will be deleted and new purchase lines based on the new information in the header will be created.\\';
      Text018@1017 : TextConst 'ENU=You must delete the existing purchase lines before you can change %1.';
      Text019@1018 : TextConst 'ENU=You have changed %1 on the purchase header, but it has not been changed on the existing purchase lines.\';
      Text020@1019 : TextConst 'ENU=You must update the existing purchase lines manually.';
      Text021@1020 : TextConst 'ENU=The change may affect the exchange rate used on the price calculation of the purchase lines.';
      Text022@1021 : TextConst 'ENU=Do you want to update the exchange rate?';
      Text023@1022 : TextConst 'ENU=You cannot delete this document. Your identification is set up to process from %1 %2 only.';
      Text025@1024 : TextConst 'ENU="You have modified the %1 field. Note that the recalculation of VAT may cause penny differences, so you must check the amounts afterwards. "';
      Text027@1026 : TextConst 'ENU=Do you want to update the %2 field on the lines to reflect the new value of %1?';
      Text028@1027 : TextConst 'ENU=Your identification is set up to process from %1 %2 only.';
      Text029@1028 : TextConst '@@@="%1 = Document No.";ENU=Deleting this document will cause a gap in the number series for return shipments. An empty return shipment %1 will be created to fill this gap in the number series.\\Do you want to continue?';
      Text032@1031 : TextConst 'ENU=You have modified %1.\\';
      Text033@1032 : TextConst 'ENU=Do you want to update the lines?';
      PurchSetup@1033 : Record "Purchases & Payables Setup";
      GLSetup@1034 : Record "General Ledger Setup";
      GLAcc@1035 : Record "G/L Account";
      PurchLine@1036 : Record "Purchase Line";
      xPurchLine@1080 : Record "Purchase Line";
      VendLedgEntry@1037 : Record "Vendor Ledger Entry";
      Vend@1038 : Record Vendor;
      PaymentTerms@1039 : Record "Payment Terms";
      PaymentMethod@1040 : Record "Payment Method";
      CurrExchRate@1041 : Record "Currency Exchange Rate";
      PurchHeader@1042 : Record "Purchase Header";
      PurchCommentLine@1043 : Record "Purch. Comment Line";
      Cust@1045 : Record Customer;
      CompanyInfo@1046 : Record "Company Information";
      PostCode@1047 : Record "Post Code";
      OrderAddr@1048 : Record "Order Address";
      BankAcc@1049 : Record "Bank Account";
      PurchRcptHeader@1050 : Record "Purch. Rcpt. Header";
      PurchInvHeader@1051 : Record "Purch. Inv. Header";
      PurchCrMemoHeader@1052 : Record "Purch. Cr. Memo Hdr.";
      ReturnShptHeader@1053 : Record "Return Shipment Header";
      PurchInvHeaderPrepmt@1090 : Record "Purch. Inv. Header";
      PurchCrMemoHeaderPrepmt@1089 : Record "Purch. Cr. Memo Hdr.";
      GenBusPostingGrp@1054 : Record "Gen. Business Posting Group";
      RespCenter@1056 : Record "Responsibility Center";
      Location@1057 : Record Location;
      WhseRequest@1058 : Record "Warehouse Request";
      InvtSetup@1059 : Record "Inventory Setup";
      NoSeriesMgt@1060 : Codeunit NoSeriesManagement;
      DimMgt@1065 : Codeunit DimensionManagement;
      ApprovalsMgmt@1082 : Codeunit "Approvals Mgmt.";
      UserSetupMgt@1066 : Codeunit "User Setup Management";
      LeadTimeMgt@1002 : Codeunit "Lead-Time Management";
      PostingSetupMgt@1023 : Codeunit PostingSetupManagement;
      CurrencyDate@1069 : Date;
      HideValidationDialog@1070 : Boolean;
      Confirmed@1071 : Boolean;
      Text034@1072 : TextConst 'ENU=You cannot change the %1 when the %2 has been filled in.';
      Text037@1076 : TextConst 'ENU=Contact %1 %2 is not related to vendor %3.';
      Text038@1075 : TextConst 'ENU=Contact %1 %2 is related to a different company than vendor %3.';
      Text039@1077 : TextConst 'ENU=Contact %1 %2 is not related to a vendor.';
      SkipBuyFromContact@1030 : Boolean;
      SkipPayToContact@1078 : Boolean;
      Text040@1079 : TextConst 'ENU="You can not change the %1 field because %2 %3 has %4 = %5 and the %6 has already been assigned %7 %8."';
      Text042@1084 : TextConst 'ENU=You must cancel the approval process if you wish to change the %1.';
      Text045@1086 : TextConst 'ENU=Deleting this document will cause a gap in the number series for prepayment invoices. An empty prepayment invoice %1 will be created to fill this gap in the number series.\\Do you want to continue?';
      Text046@1087 : TextConst 'ENU=Deleting this document will cause a gap in the number series for prepayment credit memos. An empty prepayment credit memo %1 will be created to fill this gap in the number series.\\Do you want to continue?';
      Text049@1092 : TextConst 'ENU=%1 is set up to process from %2 %3 only.';
      Text050@1067 : TextConst 'ENU=Reservations exist for this order. These reservations will be canceled if a date conflict is caused by this change.\\Do you want to continue?';
      Text051@1025 : TextConst 'ENU=You may have changed a dimension.\\Do you want to update the lines?';
      Text052@1091 : TextConst 'ENU=The %1 field on the purchase order %2 must be the same as on sales order %3.';
      UpdateDocumentDate@1120 : Boolean;
      Text053@1095 : TextConst 'ENU=There are unposted prepayment amounts on the document of type %1 with the number %2.';
      Text054@1096 : TextConst 'ENU=There are unpaid prepayment invoices that are related to the document of type %1 with the number %2.';
      DeferralLineQst@1055 : TextConst '@@@="%1=The posting date on the document.";ENU=You have changed the %1 on the purchase header, do you want to update the deferral schedules for the lines with this date?';
      ChangeCurrencyQst@1073 : TextConst 'ENU=If you change %1, the existing purchase lines will be deleted and new purchase lines based on the new information in the header will be created. You may need to update the price information manually.\\Do you want to change %1?';
      PostedDocsToPrintCreatedMsg@1083 : TextConst 'ENU=One or more related posted documents have been generated during deletion to fill gaps in the posting number series. You can view or print the documents from the respective document archive.';
      BuyFromVendorTxt@1010 : TextConst 'ENU=Buy-from Vendor';
      PayToVendorTxt@1011 : TextConst 'ENU=Pay-to Vendor';
      DocumentNotPostedClosePageQst@1013 : TextConst 'ENU=The document has not been posted.\Are you sure you want to exit?';
      DocTxt@1000 : TextConst 'ENU=Purchase Order';
      SelectNoSeriesAllowed@1015 : Boolean;
      MixedDropshipmentErr@1001 : TextConst 'ENU=You cannot print the purchase order because it contains one or more lines for drop shipment in addition to regular purchase lines.';
      ModifyVendorAddressNotificationLbl@1062 : TextConst 'ENU=Update the address';
      DontShowAgainActionLbl@1064 : TextConst 'ENU=Don''t show again';
      DontShowAgainFunctionTok@1104 : TextConst 'ENU=HidePurchaseNotificationForCurrentUser';
      UpdateAddressWithBuyFromAddressFunctionTok@1105 : TextConst 'ENU=CopyBuyFromVendorAddressFieldsFromSalesDocument';
      UpdateAddressWithPayToAddressTok@1100 : TextConst 'ENU=CopyPayToVendorAddressFieldsFromSalesDocument';
      ModifyVendorAddressNotificationMsg@1063 : TextConst '@@@="%1=Vendor name";ENU=The address you entered for %1 is different from the Vendor''s existing address.';
      ModifyBuyFromVendorAddressNotificationNameTxt@1106 : TextConst 'ENU=Update Buy-from Vendor Address';
      ModifyBuyFromVendorAddressNotificationDescriptionTxt@1098 : TextConst 'ENU=Warn if the Buy-from address on sales documents is different from the Vendor''s existing address.';
      ModifyPayToVendorAddressNotificationNameTxt@1102 : TextConst 'ENU=Update Pay-to Vendor Address';
      ModifyPayToVendorAddressNotificationDescriptionTxt@1099 : TextConst 'ENU=Warn if the Pay-to address on sales documents is different from the Vendor''s existing address.';
      PurchaseAlreadyExistsTxt@1029 : TextConst '@@@="%1 = Document Type; %2 = Document No.";ENU=Purchase %1 %2 already exists for this vendor.';
      ShowVendLedgEntryTxt@1044 : TextConst 'ENU=Show the vendor ledger entry.';
      ShowDocAlreadyExistNotificationNameTxt@1068 : TextConst 'ENU=Purchase document with same external document number already exists.';
      ShowDocAlreadyExistNotificationDescriptionTxt@1061 : TextConst 'ENU=Warn if purchase document with same external document number already exists.';

    LOCAL PROCEDURE InitInsert@41();
    BEGIN
      if "No." = '' then begin
        TestNoSeries;
        NoSeriesMgt.InitSeries(GetNoSeriesCode,xRec."No. Series","Posting Date","No.","No. Series");
      end;

      InitRecord;
    END;

    LOCAL PROCEDURE SkipInitialization@42() : Boolean;
    BEGIN
      if "No." = '' then
        exit(false);

      if "Buy-from Vendor No." = '' then
        exit(false);

      if xRec."Document Type" <> "Document Type" then
        exit(false);

      if GETFILTER("Buy-from Vendor No.") <> '' then
        if GETRANGEMIN("Buy-from Vendor No.") = GETRANGEMAX("Buy-from Vendor No.") then
          if "Buy-from Vendor No." = GETRANGEMIN("Buy-from Vendor No.") then
            exit(false);

      exit(true);
    END;

    [External]
    PROCEDURE InitRecord@10();
    VAR
      ArchiveManagement@1000 : Codeunit ArchiveManagement;
    BEGIN
      PurchSetup.GET;

      case "Document Type" of
        "Document Type"::Quote,"Document Type"::Order:
          begin
            NoSeriesMgt.SetDefaultSeries("Posting No. Series",PurchSetup."Posted Invoice Nos.");
            NoSeriesMgt.SetDefaultSeries("Receiving No. Series",PurchSetup."Posted Receipt Nos.");
            if "Document Type" = "Document Type"::Order then begin
              NoSeriesMgt.SetDefaultSeries("Prepayment No. Series",PurchSetup."Posted Prepmt. Inv. Nos.");
              NoSeriesMgt.SetDefaultSeries("Prepmt. Cr. Memo No. Series",PurchSetup."Posted Prepmt. Cr. Memo Nos.");
            end;
          end;
        "Document Type"::Invoice:
          begin
            if ("No. Series" <> '') and
               (PurchSetup."Invoice Nos." = PurchSetup."Posted Invoice Nos.")
            then
              "Posting No. Series" := "No. Series"
            else
              NoSeriesMgt.SetDefaultSeries("Posting No. Series",PurchSetup."Posted Invoice Nos.");
            if PurchSetup."Receipt on Invoice" then
              NoSeriesMgt.SetDefaultSeries("Receiving No. Series",PurchSetup."Posted Receipt Nos.");
          end;
        "Document Type"::"Return Order":
          begin
            NoSeriesMgt.SetDefaultSeries("Posting No. Series",PurchSetup."Posted Credit Memo Nos.");
            NoSeriesMgt.SetDefaultSeries("Return Shipment No. Series",PurchSetup."Posted Return Shpt. Nos.");
          end;
        "Document Type"::"Credit Memo":
          begin
            if ("No. Series" <> '') and
               (PurchSetup."Credit Memo Nos." = PurchSetup."Posted Credit Memo Nos.")
            then
              "Posting No. Series" := "No. Series"
            else
              NoSeriesMgt.SetDefaultSeries("Posting No. Series",PurchSetup."Posted Credit Memo Nos.");
            if PurchSetup."Return Shipment on Credit Memo" then
              NoSeriesMgt.SetDefaultSeries("Return Shipment No. Series",PurchSetup."Posted Return Shpt. Nos.");
          end;
      end;

      if "Document Type" in
         ["Document Type"::Order,"Document Type"::Invoice,"Document Type"::"Return Order","Document Type"::Quote]
      then
        "Order Date" := WORKDATE;

      if "Document Type" = "Document Type"::Invoice then
        "Expected Receipt Date" := WORKDATE;

      if not ("Document Type" in ["Document Type"::"Blanket Order","Document Type"::Quote]) and
         ("Posting Date" = 0D)
      then
        "Posting Date" := WORKDATE;

      if PurchSetup."Default Posting Date" = PurchSetup."Default Posting Date"::"No Date" then
        "Posting Date" := 0D;

      "Document Date" := WORKDATE;

      VALIDATE("Sell-to Customer No.",'');

      if IsCreditDocType then begin
        GLSetup.GET;
        Correction := GLSetup."Mark Cr. Memos as Corrections";
      end;

      "Posting Description" := FORMAT("Document Type") + ' ' + "No.";

      if InvtSetup.GET then
        "Inbound Whse. Handling Time" := InvtSetup."Inbound Whse. Handling Time";

      "Responsibility Center" := UserSetupMgt.GetRespCenter(1,"Responsibility Center");
      "Doc. No. Occurrence" := ArchiveManagement.GetNextOccurrenceNo(DATABASE::"Purchase Header","Document Type","No.");

      OnAfterInitRecord(Rec);
    END;

    LOCAL PROCEDURE InitNoSeries@52();
    BEGIN
      if xRec."Receiving No." <> '' then begin
        "Receiving No. Series" := xRec."Receiving No. Series";
        "Receiving No." := xRec."Receiving No.";
      end;
      if xRec."Posting No." <> '' then begin
        "Posting No. Series" := xRec."Posting No. Series";
        "Posting No." := xRec."Posting No.";
      end;
      if xRec."Return Shipment No." <> '' then begin
        "Return Shipment No. Series" := xRec."Return Shipment No. Series";
        "Return Shipment No." := xRec."Return Shipment No.";
      end;
      if xRec."Prepayment No." <> '' then begin
        "Prepayment No. Series" := xRec."Prepayment No. Series";
        "Prepayment No." := xRec."Prepayment No.";
      end;
      if xRec."Prepmt. Cr. Memo No." <> '' then begin
        "Prepmt. Cr. Memo No. Series" := xRec."Prepmt. Cr. Memo No. Series";
        "Prepmt. Cr. Memo No." := xRec."Prepmt. Cr. Memo No.";
      end;

      OnAfterInitNoSeries(Rec);
    END;

    [External]
    PROCEDURE AssistEdit@2(OldPurchHeader@1000 : Record "Purchase Header") : Boolean;
    BEGIN
      PurchSetup.GET;
      TestNoSeries;
      if NoSeriesMgt.SelectSeries(GetNoSeriesCode,OldPurchHeader."No. Series","No. Series") then begin
        PurchSetup.GET;
        TestNoSeries;
        NoSeriesMgt.SetSeries("No.");
        exit(true);
      end;
    END;

    LOCAL PROCEDURE TestNoSeries@6();
    BEGIN
      PurchSetup.GET;
      case "Document Type" of
        "Document Type"::Quote:
          PurchSetup.TESTFIELD("Quote Nos.");
        "Document Type"::Order:
          PurchSetup.TESTFIELD("Order Nos.");
        "Document Type"::Invoice:
          begin
            PurchSetup.TESTFIELD("Invoice Nos.");
            PurchSetup.TESTFIELD("Posted Invoice Nos.");
          end;
        "Document Type"::"Return Order":
          PurchSetup.TESTFIELD("Return Order Nos.");
        "Document Type"::"Credit Memo":
          begin
            PurchSetup.TESTFIELD("Credit Memo Nos.");
            PurchSetup.TESTFIELD("Posted Credit Memo Nos.");
          end;
        "Document Type"::"Blanket Order":
          PurchSetup.TESTFIELD("Blanket Order Nos.");
      end;

      OnAfterTestNoSeries(Rec);
    END;

    LOCAL PROCEDURE GetNoSeriesCode@9() : Code[20];
    VAR
      NoSeriesCode@1001 : Code[20];
    BEGIN
      case "Document Type" of
        "Document Type"::Quote:
          NoSeriesCode := PurchSetup."Quote Nos.";
        "Document Type"::Order:
          NoSeriesCode := PurchSetup."Order Nos.";
        "Document Type"::Invoice:
          NoSeriesCode := PurchSetup."Invoice Nos.";
        "Document Type"::"Return Order":
          NoSeriesCode := PurchSetup."Return Order Nos.";
        "Document Type"::"Credit Memo":
          NoSeriesCode := PurchSetup."Credit Memo Nos.";
        "Document Type"::"Blanket Order":
          NoSeriesCode := PurchSetup."Blanket Order Nos.";
      end;
      exit(NoSeriesMgt.GetNoSeriesWithCheck(NoSeriesCode,SelectNoSeriesAllowed,"No. Series"));
    END;

    LOCAL PROCEDURE GetPostingNoSeriesCode@8() : Code[10];
    BEGIN
      if IsCreditDocType then
        exit(PurchSetup."Posted Credit Memo Nos.");
      exit(PurchSetup."Posted Invoice Nos.");
    END;

    LOCAL PROCEDURE GetPostingPrepaymentNoSeriesCode@37() : Code[10];
    BEGIN
      if IsCreditDocType then
        exit(PurchSetup."Posted Prepmt. Cr. Memo Nos.");
      exit(PurchSetup."Posted Prepmt. Inv. Nos.");
    END;

    LOCAL PROCEDURE TestNoSeriesDate@40(No@1000 : Code[20];NoSeriesCode@1001 : Code[20];NoCapt@1002 : Text[1024];NoSeriesCapt@1004 : Text[1024]);
    VAR
      NoSeries@1005 : Record "No. Series";
    BEGIN
      if (No <> '') and (NoSeriesCode <> '') then begin
        NoSeries.GET(NoSeriesCode);
        if NoSeries."Date Order" then
          ERROR(
            Text040,
            FIELDCAPTION("Posting Date"),NoSeriesCapt,NoSeriesCode,
            NoSeries.FIELDCAPTION("Date Order"),NoSeries."Date Order","Document Type",
            NoCapt,No);
      end;
    END;

    [External]
    PROCEDURE ConfirmDeletion@11() : Boolean;
    BEGIN
      if PurchRcptHeader."No." <> '' then
        if not CONFIRM(Text009,true,PurchRcptHeader."No.")
        then
          exit;
      if PurchInvHeader."No." <> '' then
        if not CONFIRM(Text012,true,PurchInvHeader."No.")
        then
          exit;
      if PurchCrMemoHeader."No." <> '' then
        if not CONFIRM(Text014,true,PurchCrMemoHeader."No.")
        then
          exit;
      if ReturnShptHeader."No." <> '' then
        if not CONFIRM(Text029,true,ReturnShptHeader."No.")
        then
          exit;
      if "Prepayment No." <> '' then
        if not CONFIRM(Text045,true,PurchInvHeaderPrepmt."No.")
        then
          exit;
      if "Prepmt. Cr. Memo No." <> '' then
        if not CONFIRM(Text046,true,PurchCrMemoHeaderPrepmt."No.")
        then
          exit;
      exit(true);
    END;

    LOCAL PROCEDURE GetVend@1(VendNo@1000 : Code[20]);
    BEGIN
      if VendNo <> Vend."No." then
        Vend.GET(VendNo);
    END;

    [External]
    PROCEDURE PurchLinesExist@3() : Boolean;
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Document Type");
      PurchLine.SETRANGE("Document No.","No.");
      exit(PurchLine.FINDFIRST);
    END;

    LOCAL PROCEDURE RecreatePurchLines@4(ChangedFieldName@1000 : Text[100]);
    VAR
      TempPurchLine@1001 : TEMPORARY Record "Purchase Line";
      ItemChargeAssgntPurch@1005 : Record "Item Charge Assignment (Purch)";
      TempItemChargeAssgntPurch@1004 : TEMPORARY Record "Item Charge Assignment (Purch)";
      TempInteger@1003 : TEMPORARY Record Integer;
      SalesHeader@1006 : Record "Sales Header";
      TransferExtendedText@1009 : Codeunit "Transfer Extended Text";
      ExtendedTextAdded@1002 : Boolean;
    BEGIN
      if PurchLinesExist then begin
        if HideValidationDialog then
          Confirmed := true
        else
          Confirmed :=
            CONFIRM(
              Text016 +
              ConfirmChangeQst,false,ChangedFieldName);
        if Confirmed then begin
          PurchLine.LOCKTABLE;
          ItemChargeAssgntPurch.LOCKTABLE;
          MODIFY;

          PurchLine.RESET;
          PurchLine.SETRANGE("Document Type","Document Type");
          PurchLine.SETRANGE("Document No.","No.");
          if PurchLine.FINDSET then begin
            repeat
              PurchLine.TESTFIELD("Quantity Received",0);
              PurchLine.TESTFIELD("Quantity Invoiced",0);
              PurchLine.TESTFIELD("Return Qty. Shipped",0);
              PurchLine.CALCFIELDS("Reserved Qty. (Base)");
              PurchLine.TESTFIELD("Reserved Qty. (Base)",0);
              PurchLine.TESTFIELD("Receipt No.",'');
              PurchLine.TESTFIELD("Return Shipment No.",'');
              PurchLine.TESTFIELD("Blanket Order No.",'');
              if PurchLine."Drop Shipment" or PurchLine."Special Order" then begin
                case true of
                  PurchLine."Drop Shipment":
                    SalesHeader.GET(SalesHeader."Document Type"::Order,PurchLine."Sales Order No.");
                  PurchLine."Special Order":
                    SalesHeader.GET(SalesHeader."Document Type"::Order,PurchLine."Special Order Sales No.");
                end;
                TESTFIELD("Sell-to Customer No.",SalesHeader."Sell-to Customer No.");
                TESTFIELD("Ship-to Code",SalesHeader."Ship-to Code");
              end;

              PurchLine.TESTFIELD("Prepmt. Amt. Inv.",0);
              TempPurchLine := PurchLine;
              if PurchLine.Nonstock then begin
                PurchLine.Nonstock := false;
                PurchLine.MODIFY;
              end;
              TempPurchLine.INSERT;
            until PurchLine.NEXT = 0;

            ItemChargeAssgntPurch.SETRANGE("Document Type","Document Type");
            ItemChargeAssgntPurch.SETRANGE("Document No.","No.");
            if ItemChargeAssgntPurch.FINDSET then begin
              repeat
                TempItemChargeAssgntPurch.INIT;
                TempItemChargeAssgntPurch := ItemChargeAssgntPurch;
                TempItemChargeAssgntPurch.INSERT;
              until ItemChargeAssgntPurch.NEXT = 0;
              ItemChargeAssgntPurch.DELETEALL;
            end;

            PurchLine.DELETEALL(true);

            PurchLine.INIT;
            PurchLine."Line No." := 0;
            TempPurchLine.FINDSET;
            ExtendedTextAdded := false;
            repeat
              if TempPurchLine."Attached to Line No." = 0 then begin
                PurchLine.INIT;
                PurchLine."Line No." := PurchLine."Line No." + 10000;
                PurchLine.VALIDATE(Type,TempPurchLine.Type);
                if TempPurchLine."No." = '' then begin
                  PurchLine.VALIDATE(Description,TempPurchLine.Description);
                  PurchLine.VALIDATE("Description 2",TempPurchLine."Description 2");
                end else begin
                  PurchLine.VALIDATE("No.",TempPurchLine."No.");
                  if PurchLine.Type <> PurchLine.Type::" " then
                    case true of
                      TempPurchLine."Drop Shipment":
                        TransferSavedFieldsDropShipment(PurchLine,TempPurchLine);
                      TempPurchLine."Special Order":
                        TransferSavedFieldsSpecialOrder(PurchLine,TempPurchLine);
                      else
                        TransferSavedFields(PurchLine,TempPurchLine);
                    end;
                end;

                PurchLine.INSERT;
                ExtendedTextAdded := false;

                if PurchLine.Type = PurchLine.Type::Item then begin
                  ClearItemAssgntPurchFilter(TempItemChargeAssgntPurch);
                  TempItemChargeAssgntPurch.SETRANGE("Applies-to Doc. Type",TempPurchLine."Document Type");
                  TempItemChargeAssgntPurch.SETRANGE("Applies-to Doc. No.",TempPurchLine."Document No.");
                  TempItemChargeAssgntPurch.SETRANGE("Applies-to Doc. Line No.",TempPurchLine."Line No.");
                  if TempItemChargeAssgntPurch.FINDSET then
                    repeat
                      if not TempItemChargeAssgntPurch.MARK then begin
                        TempItemChargeAssgntPurch."Applies-to Doc. Line No." := PurchLine."Line No.";
                        TempItemChargeAssgntPurch.Description := PurchLine.Description;
                        TempItemChargeAssgntPurch.MODIFY;
                        TempItemChargeAssgntPurch.MARK(true);
                      end;
                    until TempItemChargeAssgntPurch.NEXT = 0;
                end;
                if PurchLine.Type = PurchLine.Type::"Charge (Item)" then begin
                  TempInteger.INIT;
                  TempInteger.Number := PurchLine."Line No.";
                  TempInteger.INSERT;
                end;
              end else
                if not ExtendedTextAdded then begin
                  TransferExtendedText.PurchCheckIfAnyExtText(PurchLine,true);
                  TransferExtendedText.InsertPurchExtText(PurchLine);
                  PurchLine.FINDLAST;
                  ExtendedTextAdded := true;
                end;
            until TempPurchLine.NEXT = 0;

            ClearItemAssgntPurchFilter(TempItemChargeAssgntPurch);
            TempPurchLine.SETRANGE(Type,PurchLine.Type::"Charge (Item)");
            if TempPurchLine.FINDSET then
              repeat
                TempItemChargeAssgntPurch.SETRANGE("Document Line No.",TempPurchLine."Line No.");
                if TempItemChargeAssgntPurch.FINDSET then begin
                  repeat
                    TempInteger.FINDFIRST;
                    ItemChargeAssgntPurch.INIT;
                    ItemChargeAssgntPurch := TempItemChargeAssgntPurch;
                    ItemChargeAssgntPurch."Document Line No." := TempInteger.Number;
                    ItemChargeAssgntPurch.VALIDATE("Unit Cost",0);
                    ItemChargeAssgntPurch.INSERT;
                  until TempItemChargeAssgntPurch.NEXT = 0;
                  TempInteger.DELETE;
                end;
              until TempPurchLine.NEXT = 0;

            TempPurchLine.SETRANGE(Type);
            TempPurchLine.DELETEALL;
            ClearItemAssgntPurchFilter(TempItemChargeAssgntPurch);
            TempItemChargeAssgntPurch.DELETEALL;
          end;
        end else
          ERROR(
            Text018,ChangedFieldName);
      end;
    END;

    LOCAL PROCEDURE TransferSavedFields@72(VAR DestinationPurchaseLine@1000 : Record "Purchase Line";VAR SourcePurchaseLine@1001 : Record "Purchase Line");
    BEGIN
      DestinationPurchaseLine.VALIDATE("Unit of Measure Code",SourcePurchaseLine."Unit of Measure Code");
      DestinationPurchaseLine.VALIDATE("Variant Code",SourcePurchaseLine."Variant Code");
      DestinationPurchaseLine."Prod. Order No." := SourcePurchaseLine."Prod. Order No.";
      if DestinationPurchaseLine."Prod. Order No." <> '' then begin
        DestinationPurchaseLine.Description := SourcePurchaseLine.Description;
        DestinationPurchaseLine.VALIDATE("VAT Prod. Posting Group",SourcePurchaseLine."VAT Prod. Posting Group");
        DestinationPurchaseLine.VALIDATE("Gen. Prod. Posting Group",SourcePurchaseLine."Gen. Prod. Posting Group");
        DestinationPurchaseLine.VALIDATE("Expected Receipt Date",SourcePurchaseLine."Expected Receipt Date");
        DestinationPurchaseLine.VALIDATE("Requested Receipt Date",SourcePurchaseLine."Requested Receipt Date");
        DestinationPurchaseLine.VALIDATE("Qty. per Unit of Measure",SourcePurchaseLine."Qty. per Unit of Measure");
      end;
      if (SourcePurchaseLine."Job No." <> '') and (SourcePurchaseLine."Job Task No." <> '') then begin
        DestinationPurchaseLine.VALIDATE("Job No.",SourcePurchaseLine."Job No.");
        DestinationPurchaseLine.VALIDATE("Job Task No.",SourcePurchaseLine."Job Task No.");
        DestinationPurchaseLine."Job Line Type" := SourcePurchaseLine."Job Line Type";
      end;
      if SourcePurchaseLine.Quantity <> 0 then
        DestinationPurchaseLine.VALIDATE(Quantity,SourcePurchaseLine.Quantity);
      if ("Currency Code" = xRec."Currency Code") and (PurchLine."Direct Unit Cost" = 0) then
        DestinationPurchaseLine.VALIDATE("Direct Unit Cost",SourcePurchaseLine."Direct Unit Cost");
      DestinationPurchaseLine."Routing No." := SourcePurchaseLine."Routing No.";
      DestinationPurchaseLine."Routing Reference No." := SourcePurchaseLine."Routing Reference No.";
      DestinationPurchaseLine."Operation No." := SourcePurchaseLine."Operation No.";
      DestinationPurchaseLine."Work Center No." := SourcePurchaseLine."Work Center No.";
      DestinationPurchaseLine."Prod. Order Line No." := SourcePurchaseLine."Prod. Order Line No.";
      DestinationPurchaseLine."Overhead Rate" := SourcePurchaseLine."Overhead Rate";
    END;

    LOCAL PROCEDURE TransferSavedFieldsDropShipment@79(VAR DestinationPurchaseLine@1001 : Record "Purchase Line";VAR SourcePurchaseLine@1000 : Record "Purchase Line");
    VAR
      SalesLine@1003 : Record "Sales Line";
      CopyDocMgt@1002 : Codeunit "Copy Document Mgt.";
    BEGIN
      SalesLine.GET(SalesLine."Document Type"::Order,
        SourcePurchaseLine."Sales Order No.",
        SourcePurchaseLine."Sales Order Line No.");
      CopyDocMgt.TransfldsFromSalesToPurchLine(SalesLine,DestinationPurchaseLine);
      DestinationPurchaseLine."Drop Shipment" := SourcePurchaseLine."Drop Shipment";
      DestinationPurchaseLine."Purchasing Code" := SalesLine."Purchasing Code";
      DestinationPurchaseLine."Sales Order No." := SourcePurchaseLine."Sales Order No.";
      DestinationPurchaseLine."Sales Order Line No." := SourcePurchaseLine."Sales Order Line No.";
      EVALUATE(DestinationPurchaseLine."Inbound Whse. Handling Time",'<0D>');
      DestinationPurchaseLine.VALIDATE("Inbound Whse. Handling Time");
      SalesLine.VALIDATE("Unit Cost (LCY)",DestinationPurchaseLine."Unit Cost (LCY)");
      SalesLine."Purchase Order No." := DestinationPurchaseLine."Document No.";
      SalesLine."Purch. Order Line No." := DestinationPurchaseLine."Line No.";
      SalesLine.MODIFY;
    END;

    LOCAL PROCEDURE TransferSavedFieldsSpecialOrder@82(VAR DestinationPurchaseLine@1003 : Record "Purchase Line";VAR SourcePurchaseLine@1002 : Record "Purchase Line");
    VAR
      SalesLine@1004 : Record "Sales Line";
      CopyDocMgt@1000 : Codeunit "Copy Document Mgt.";
    BEGIN
      SalesLine.GET(SalesLine."Document Type"::Order,
        SourcePurchaseLine."Special Order Sales No.",
        SourcePurchaseLine."Special Order Sales Line No.");
      CopyDocMgt.TransfldsFromSalesToPurchLine(SalesLine,DestinationPurchaseLine);
      DestinationPurchaseLine."Special Order" := SourcePurchaseLine."Special Order";
      DestinationPurchaseLine."Purchasing Code" := SalesLine."Purchasing Code";
      DestinationPurchaseLine."Special Order Sales No." := SourcePurchaseLine."Special Order Sales No.";
      DestinationPurchaseLine."Special Order Sales Line No." := SourcePurchaseLine."Special Order Sales Line No.";
      DestinationPurchaseLine.VALIDATE("Unit of Measure Code",SourcePurchaseLine."Unit of Measure Code");
      if SourcePurchaseLine.Quantity <> 0 then
        DestinationPurchaseLine.VALIDATE(Quantity,SourcePurchaseLine.Quantity);

      SalesLine.VALIDATE("Unit Cost (LCY)",DestinationPurchaseLine."Unit Cost (LCY)");
      SalesLine."Special Order Purchase No." := DestinationPurchaseLine."Document No.";
      SalesLine."Special Order Purch. Line No." := DestinationPurchaseLine."Line No.";
      SalesLine.MODIFY;
    END;

    LOCAL PROCEDURE MessageIfPurchLinesExist@5(ChangedFieldName@1000 : Text[100]);
    BEGIN
      if PurchLinesExist and not HideValidationDialog then
        MESSAGE(
          Text019 +
          Text020,
          ChangedFieldName);
    END;

    LOCAL PROCEDURE PriceMessageIfPurchLinesExist@7(ChangedFieldName@1000 : Text[100]);
    BEGIN
      if PurchLinesExist and not HideValidationDialog then
        MESSAGE(
          Text019 +
          Text021,ChangedFieldName);
    END;

    LOCAL PROCEDURE UpdateCurrencyFactor@12();
    BEGIN
      if "Currency Code" <> '' then begin
        if "Posting Date" <> 0D then
          CurrencyDate := "Posting Date"
        else
          CurrencyDate := WORKDATE;

        "Currency Factor" := CurrExchRate.ExchangeRate(CurrencyDate,"Currency Code");
      end else
        "Currency Factor" := 0;
    END;

    LOCAL PROCEDURE ConfirmUpdateCurrencyFactor@13() : Boolean;
    BEGIN
      if HideValidationDialog then
        Confirmed := true
      else
        Confirmed := CONFIRM(Text022,false);
      if Confirmed then
        VALIDATE("Currency Factor")
      else
        "Currency Factor" := xRec."Currency Factor";
      exit(Confirmed);
    END;

    [External]
    PROCEDURE SetHideValidationDialog@14(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    LOCAL PROCEDURE UpdatePurchLines@15(ChangedFieldName@1000 : Text[100];AskQuestion@1001 : Boolean);
    VAR
      PurchLineReserve@1003 : Codeunit "Purch. Line-Reserve";
      Question@1002 : Text[250];
    BEGIN
      if not PurchLinesExist then
        exit;

      if AskQuestion then begin
        Question := STRSUBSTNO(
            Text032 +
            Text033,ChangedFieldName);
        if GUIALLOWED then
          if DIALOG.CONFIRM(Question,true) then
            case ChangedFieldName of
              FIELDCAPTION("Expected Receipt Date"),
              FIELDCAPTION("Requested Receipt Date"),
              FIELDCAPTION("Promised Receipt Date"),
              FIELDCAPTION("Lead Time Calculation"),
              FIELDCAPTION("Inbound Whse. Handling Time"):
                ConfirmResvDateConflict;
            end
          else
            exit;
      end;

      PurchLine.LOCKTABLE;
      MODIFY;

      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Document Type");
      PurchLine.SETRANGE("Document No.","No.");
      if PurchLine.FINDSET then
        repeat
          xPurchLine := PurchLine;
          case ChangedFieldName of
            FIELDCAPTION("Expected Receipt Date"):
              if PurchLine."No." <> '' then
                PurchLine.VALIDATE("Expected Receipt Date","Expected Receipt Date");
            FIELDCAPTION("Currency Factor"):
              if PurchLine.Type <> PurchLine.Type::" " then
                PurchLine.VALIDATE("Direct Unit Cost");
            FIELDCAPTION("Transaction Type"):
              PurchLine.VALIDATE("Transaction Type","Transaction Type");
            FIELDCAPTION("Transport Method"):
              PurchLine.VALIDATE("Transport Method","Transport Method");
            FIELDCAPTION("Entry Point"):
              PurchLine.VALIDATE("Entry Point","Entry Point");
            FIELDCAPTION(Area):
              PurchLine.VALIDATE(Area,Area);
            FIELDCAPTION("Transaction Specification"):
              PurchLine.VALIDATE("Transaction Specification","Transaction Specification");
            FIELDCAPTION("Requested Receipt Date"):
              if PurchLine."No." <> '' then
                PurchLine.VALIDATE("Requested Receipt Date","Requested Receipt Date");
            FIELDCAPTION("Prepayment %"):
              if PurchLine."No." <> '' then
                PurchLine.VALIDATE("Prepayment %","Prepayment %");
            FIELDCAPTION("Promised Receipt Date"):
              if PurchLine."No." <> '' then
                PurchLine.VALIDATE("Promised Receipt Date","Promised Receipt Date");
            FIELDCAPTION("Lead Time Calculation"):
              if PurchLine."No." <> '' then
                PurchLine.VALIDATE("Lead Time Calculation","Lead Time Calculation");
            FIELDCAPTION("Inbound Whse. Handling Time"):
              if PurchLine."No." <> '' then
                PurchLine.VALIDATE("Inbound Whse. Handling Time","Inbound Whse. Handling Time");
            PurchLine.FIELDCAPTION("Deferral Code"):
              if PurchLine."No." <> '' then
                PurchLine.VALIDATE("Deferral Code");
            else
              OnUpdatePurchLinesByChangedFieldName(Rec,PurchLine,ChangedFieldName);
          end;
          PurchLine.MODIFY(true);
          PurchLineReserve.VerifyChange(PurchLine,xPurchLine);
        until PurchLine.NEXT = 0;
    END;

    LOCAL PROCEDURE ConfirmResvDateConflict@31();
    VAR
      ResvEngMgt@1000 : Codeunit "Reservation Engine Mgt.";
    BEGIN
      if ResvEngMgt.ResvExistsForPurchHeader(Rec) then
        if not CONFIRM(Text050,false) then
          ERROR('');
    END;

    [External]
    PROCEDURE CreateDim@16(Type1@1000 : Integer;No1@1001 : Code[20];Type2@1002 : Integer;No2@1003 : Code[20];Type3@1004 : Integer;No3@1005 : Code[20];Type4@1006 : Integer;No4@1007 : Code[20]);
    VAR
      SourceCodeSetup@1010 : Record "Source Code Setup";
      TableID@1011 : ARRAY [10] OF Integer;
      No@1012 : ARRAY [10] OF Code[20];
      OldDimSetID@1008 : Integer;
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := Type1;
      No[1] := No1;
      TableID[2] := Type2;
      No[2] := No2;
      TableID[3] := Type3;
      No[3] := No3;
      TableID[4] := Type4;
      No[4] := No4;
      OnAfterCreateDimTableIDs(Rec,CurrFieldNo,TableID,No);

      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      OldDimSetID := "Dimension Set ID";
      "Dimension Set ID" :=
        DimMgt.GetRecDefaultDimID(
          Rec,CurrFieldNo,TableID,No,SourceCodeSetup.Purchases,"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0);

      if (OldDimSetID <> "Dimension Set ID") and PurchLinesExist then begin
        MODIFY;
        UpdateAllLineDim("Dimension Set ID",OldDimSetID);
      end;
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@19(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      OldDimSetID@1005 : Integer;
    BEGIN
      OldDimSetID := "Dimension Set ID";
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
      if "No." <> '' then
        MODIFY;

      if OldDimSetID <> "Dimension Set ID" then begin
        MODIFY;
        if PurchLinesExist then
          UpdateAllLineDim("Dimension Set ID",OldDimSetID);
      end;
    END;

    LOCAL PROCEDURE ReceivedPurchLinesExist@20() : Boolean;
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Document Type");
      PurchLine.SETRANGE("Document No.","No.");
      PurchLine.SETFILTER("Quantity Received",'<>0');
      exit(PurchLine.FINDFIRST);
    END;

    LOCAL PROCEDURE ReturnShipmentExist@5800() : Boolean;
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Document Type");
      PurchLine.SETRANGE("Document No.","No.");
      PurchLine.SETFILTER("Return Qty. Shipped",'<>0');
      exit(PurchLine.FINDFIRST);
    END;

    LOCAL PROCEDURE UpdateShipToAddress@21();
    BEGIN
      if IsCreditDocType then
        exit;

      if ("Location Code" <> '') and Location.GET("Location Code") and ("Sell-to Customer No." = '') then begin
        SetShipToAddress(
          Location.Name,Location."Name 2",Location.Address,Location."Address 2",
          Location.City,Location."Post Code",Location.County,Location."Country/Region Code");
        "Ship-to Contact" := Location.Contact;
      end;

      if ("Location Code" = '') and ("Sell-to Customer No." = '') then begin
        CompanyInfo.GET;
        "Ship-to Code" := '';
        SetShipToAddress(
          CompanyInfo."Ship-to Name",CompanyInfo."Ship-to Name 2",CompanyInfo."Ship-to Address",CompanyInfo."Ship-to Address 2",
          CompanyInfo."Ship-to City",CompanyInfo."Ship-to Post Code",CompanyInfo."Ship-to County",
          CompanyInfo."Ship-to Country/Region Code");
        "Ship-to Contact" := CompanyInfo."Ship-to Contact";
      end;

      OnAfterUpdateShipToAddress(Rec);
    END;

    LOCAL PROCEDURE DeletePurchaseLines@17();
    VAR
      ReservMgt@1000 : Codeunit "Reservation Management";
    BEGIN
      if PurchLine.FINDSET then begin
        ReservMgt.DeleteDocumentReservation(DATABASE::"Purchase Line","Document Type","No.",HideValidationDialog);
        repeat
          PurchLine.SuspendStatusCheck(true);
          PurchLine.DELETE(true);
        until PurchLine.NEXT = 0;
      end;
    END;

    LOCAL PROCEDURE ClearItemAssgntPurchFilter@22(VAR TempItemChargeAssgntPurch@1000 : TEMPORARY Record "Item Charge Assignment (Purch)");
    BEGIN
      TempItemChargeAssgntPurch.SETRANGE("Document Line No.");
      TempItemChargeAssgntPurch.SETRANGE("Applies-to Doc. Type");
      TempItemChargeAssgntPurch.SETRANGE("Applies-to Doc. No.");
      TempItemChargeAssgntPurch.SETRANGE("Applies-to Doc. Line No.");
    END;

    LOCAL PROCEDURE CheckReceiptInfo@70(VAR PurchLine@1000 : Record "Purchase Line";PayTo@1001 : Boolean);
    BEGIN
      if "Document Type" = "Document Type"::Order then
        PurchLine.SETFILTER("Quantity Received",'<>0')
      else
        if "Document Type" = "Document Type"::Invoice then begin
          if not PayTo then
            PurchLine.SETRANGE("Buy-from Vendor No.",xRec."Buy-from Vendor No.");
          PurchLine.SETFILTER("Receipt No.",'<>%1','');
        end;

      if PurchLine.FINDFIRST then
        if "Document Type" = "Document Type"::Order then
          PurchLine.TESTFIELD("Quantity Received",0)
        else
          PurchLine.TESTFIELD("Receipt No.",'');
      PurchLine.SETRANGE("Receipt No.");
      PurchLine.SETRANGE("Quantity Received");
      if not PayTo then
        PurchLine.SETRANGE("Buy-from Vendor No.");
    END;

    LOCAL PROCEDURE CheckPrepmtInfo@119(VAR PurchLine@1000 : Record "Purchase Line");
    BEGIN
      if "Document Type" = "Document Type"::Order then begin
        PurchLine.SETFILTER("Prepmt. Amt. Inv.",'<>0');
        if PurchLine.FIND('-') then
          PurchLine.TESTFIELD("Prepmt. Amt. Inv.",0);
        PurchLine.SETRANGE("Prepmt. Amt. Inv.");
      end;
    END;

    LOCAL PROCEDURE CheckReturnInfo@121(VAR PurchLine@1000 : Record "Purchase Line";PayTo@1001 : Boolean);
    BEGIN
      if "Document Type" = "Document Type"::"Return Order" then
        PurchLine.SETFILTER("Return Qty. Shipped",'<>0')
      else
        if "Document Type" = "Document Type"::"Credit Memo" then begin
          if not PayTo then
            PurchLine.SETRANGE("Buy-from Vendor No.",xRec."Buy-from Vendor No.");
          PurchLine.SETFILTER("Return Shipment No.",'<>%1','');
        end;

      if PurchLine.FINDFIRST then
        if "Document Type" = "Document Type"::"Return Order" then
          PurchLine.TESTFIELD("Return Qty. Shipped",0)
        else
          PurchLine.TESTFIELD("Return Shipment No.",'');
    END;

    LOCAL PROCEDURE UpdateBuyFromCont@24(VendorNo@1000 : Code[20]);
    VAR
      ContBusRel@1003 : Record "Contact Business Relation";
      Vend@1004 : Record Vendor;
      OfficeContact@1001 : Record Contact;
      OfficeMgt@1002 : Codeunit "Office Management";
    BEGIN
      if OfficeMgt.GetContact(OfficeContact,VendorNo) then begin
        SetHideValidationDialog(true);
        UpdateBuyFromVend(OfficeContact."No.");
        SetHideValidationDialog(false);
      end else
        if Vend.GET(VendorNo) then begin
          if Vend."Primary Contact No." <> '' then
            "Buy-from Contact No." := Vend."Primary Contact No."
          else
            "Buy-from Contact No." := ContBusRel.GetContactNo(ContBusRel."Link to Table"::Vendor,"Buy-from Vendor No.");
          "Buy-from Contact" := Vend.Contact;
        end;
    END;

    LOCAL PROCEDURE UpdatePayToCont@27(VendorNo@1000 : Code[20]);
    VAR
      ContBusRel@1003 : Record "Contact Business Relation";
      Vend@1001 : Record Vendor;
    BEGIN
      if Vend.GET(VendorNo) then begin
        if Vend."Primary Contact No." <> '' then
          "Pay-to Contact No." := Vend."Primary Contact No."
        else
          "Pay-to Contact No." := ContBusRel.GetContactNo(ContBusRel."Link to Table"::Vendor,"Pay-to Vendor No.");
        "Pay-to Contact" := Vend.Contact;
      end;
    END;

    LOCAL PROCEDURE UpdateBuyFromVend@25(ContactNo@1002 : Code[20]);
    VAR
      ContBusinessRelation@1007 : Record "Contact Business Relation";
      Vend@1006 : Record Vendor;
      Cont@1005 : Record Contact;
    BEGIN
      if Cont.GET(ContactNo) then begin
        "Buy-from Contact No." := Cont."No.";
        if Cont.Type = Cont.Type::Person then
          "Buy-from Contact" := Cont.Name
        else
          if Vend.GET("Buy-from Vendor No.") then
            "Buy-from Contact" := Vend.Contact
          else
            "Buy-from Contact" := ''
      end else begin
        "Buy-from Contact" := '';
        exit;
      end;

      if ContBusinessRelation.FindByContact(ContBusinessRelation."Link to Table"::Vendor,Cont."Company No.") then begin
        if ("Buy-from Vendor No." <> '') and
           ("Buy-from Vendor No." <> ContBusinessRelation."No.")
        then
          ERROR(Text037,Cont."No.",Cont.Name,"Buy-from Vendor No.");
        if "Buy-from Vendor No." = '' then begin
          SkipBuyFromContact := true;
          VALIDATE("Buy-from Vendor No.",ContBusinessRelation."No.");
          SkipBuyFromContact := false;
        end;
      end else
        ERROR(Text039,Cont."No.",Cont.Name);

      if ("Buy-from Vendor No." = "Pay-to Vendor No.") or
         ("Pay-to Vendor No." = '')
      then
        VALIDATE("Pay-to Contact No.","Buy-from Contact No.");
    END;

    LOCAL PROCEDURE UpdatePayToVend@26(ContactNo@1000 : Code[20]);
    VAR
      ContBusinessRelation@1005 : Record "Contact Business Relation";
      Vend@1004 : Record Vendor;
      Cont@1003 : Record Contact;
    BEGIN
      if Cont.GET(ContactNo) then begin
        "Pay-to Contact No." := Cont."No.";
        if Cont.Type = Cont.Type::Person then
          "Pay-to Contact" := Cont.Name
        else
          if Vend.GET("Pay-to Vendor No.") then
            "Pay-to Contact" := Vend.Contact
          else
            "Pay-to Contact" := '';
      end else begin
        "Pay-to Contact" := '';
        exit;
      end;

      if ContBusinessRelation.FindByContact(ContBusinessRelation."Link to Table"::Vendor,Cont."Company No.") then begin
        if "Pay-to Vendor No." = '' then begin
          SkipPayToContact := true;
          VALIDATE("Pay-to Vendor No.",ContBusinessRelation."No.");
          SkipPayToContact := false;
        end else
          if "Pay-to Vendor No." <> ContBusinessRelation."No." then
            ERROR(Text037,Cont."No.",Cont.Name,"Pay-to Vendor No.");
      end else
        ERROR(Text039,Cont."No.",Cont.Name);
    END;

    [External]
    PROCEDURE CreateInvtPutAwayPick@29();
    VAR
      WhseRequest@1000 : Record "Warehouse Request";
    BEGIN
      TESTFIELD(Status,Status::Released);

      WhseRequest.RESET;
      WhseRequest.SETCURRENTKEY("Source Document","Source No.");
      case "Document Type" of
        "Document Type"::Order:
          WhseRequest.SETRANGE("Source Document",WhseRequest."Source Document"::"Purchase Order");
        "Document Type"::"Return Order":
          WhseRequest.SETRANGE("Source Document",WhseRequest."Source Document"::"Purchase Return Order");
      end;
      WhseRequest.SETRANGE("Source No.","No.");
      REPORT.RUNMODAL(REPORT::"Create Invt Put-away/Pick/Mvmt",true,false,WhseRequest);
    END;

    [External]
    PROCEDURE ShowDocDim@32();
    VAR
      OldDimSetID@1000 : Integer;
    BEGIN
      OldDimSetID := "Dimension Set ID";
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet2(
          "Dimension Set ID",STRSUBSTNO('%1 %2',"Document Type","No."),
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");

      if OldDimSetID <> "Dimension Set ID" then begin
        MODIFY;
        if PurchLinesExist then
          UpdateAllLineDim("Dimension Set ID",OldDimSetID);
      end;
    END;

    LOCAL PROCEDURE UpdateAllLineDim@34(NewParentDimSetID@1000 : Integer;OldParentDimSetID@1001 : Integer);
    VAR
      NewDimSetID@1002 : Integer;
      ReceivedShippedItemLineDimChangeConfirmed@1003 : Boolean;
    BEGIN
      // Update all lines with changed dimensions.

      if NewParentDimSetID = OldParentDimSetID then
        exit;
      if not CONFIRM(Text051) then
        exit;

      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Document Type");
      PurchLine.SETRANGE("Document No.","No.");
      PurchLine.LOCKTABLE;
      if PurchLine.FIND('-') then
        repeat
          NewDimSetID := DimMgt.GetDeltaDimSetID(PurchLine."Dimension Set ID",NewParentDimSetID,OldParentDimSetID);
          if PurchLine."Dimension Set ID" <> NewDimSetID then begin
            PurchLine."Dimension Set ID" := NewDimSetID;

            if not HideValidationDialog and GUIALLOWED then
              VerifyReceivedShippedItemLineDimChange(ReceivedShippedItemLineDimChangeConfirmed);

            DimMgt.UpdateGlobalDimFromDimSetID(
              PurchLine."Dimension Set ID",PurchLine."Shortcut Dimension 1 Code",PurchLine."Shortcut Dimension 2 Code");
            PurchLine.MODIFY;
          end;
        until PurchLine.NEXT = 0;
    END;

    LOCAL PROCEDURE VerifyReceivedShippedItemLineDimChange@71(VAR ReceivedShippedItemLineDimChangeConfirmed@1000 : Boolean);
    BEGIN
      if PurchLine.IsReceivedShippedItemDimChanged then
        if not ReceivedShippedItemLineDimChangeConfirmed then
          ReceivedShippedItemLineDimChangeConfirmed := PurchLine.ConfirmReceivedShippedItemDimChange;
    END;

    [External]
    PROCEDURE SetAmountToApply@18(AppliesToDocNo@1000 : Code[20];VendorNo@1001 : Code[20]);
    VAR
      VendLedgEntry@1002 : Record "Vendor Ledger Entry";
    BEGIN
      VendLedgEntry.SETCURRENTKEY("Document No.");
      VendLedgEntry.SETRANGE("Document No.",AppliesToDocNo);
      VendLedgEntry.SETRANGE("Vendor No.",VendorNo);
      VendLedgEntry.SETRANGE(Open,true);
      if VendLedgEntry.FINDFIRST then begin
        if VendLedgEntry."Amount to Apply" = 0 then  begin
          VendLedgEntry.CALCFIELDS("Remaining Amount");
          VendLedgEntry."Amount to Apply" := VendLedgEntry."Remaining Amount";
        end else
          VendLedgEntry."Amount to Apply" := 0;
        VendLedgEntry."Accepted Payment Tolerance" := 0;
        VendLedgEntry."Accepted Pmt. Disc. Tolerance" := false;
        CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",VendLedgEntry);
      end;
    END;

    [External]
    PROCEDURE SetShipToForSpecOrder@23();
    BEGIN
      if Location.GET("Location Code") then begin
        "Ship-to Code" := '';
        SetShipToAddress(
          Location.Name,Location."Name 2",Location.Address,Location."Address 2",
          Location.City,Location."Post Code",Location.County,Location."Country/Region Code");
        "Ship-to Contact" := Location.Contact;
        "Location Code" := Location.Code;
      end else begin
        CompanyInfo.GET;
        "Ship-to Code" := '';
        SetShipToAddress(
          CompanyInfo."Ship-to Name",CompanyInfo."Ship-to Name 2",CompanyInfo."Ship-to Address",CompanyInfo."Ship-to Address 2",
          CompanyInfo."Ship-to City",CompanyInfo."Ship-to Post Code",CompanyInfo."Ship-to County",
          CompanyInfo."Ship-to Country/Region Code");
        "Ship-to Contact" := CompanyInfo."Ship-to Contact";
        "Location Code" := '';
      end;
    END;

    LOCAL PROCEDURE JobUpdatePurchLines@28(SkipJobCurrFactorUpdate@1000 : Boolean);
    BEGIN
      with PurchLine do begin
        SETFILTER("Job No.",'<>%1','');
        SETFILTER("Job Task No.",'<>%1','');
        LOCKTABLE;
        if FINDSET(true,false) then begin
          SetPurchHeader(Rec);
          repeat
            if not SkipJobCurrFactorUpdate then
              JobSetCurrencyFactor;
            CreateTempJobJnlLine(false);
            UpdateJobPrices;
            MODIFY;
          until NEXT = 0;
        end;
      end
    END;

    [Internal]
    PROCEDURE GetPstdDocLinesToRevere@47();
    VAR
      PurchPostedDocLines@1002 : Page "Posted Purchase Document Lines";
    BEGIN
      GetVend("Buy-from Vendor No.");
      PurchPostedDocLines.SetToPurchHeader(Rec);
      PurchPostedDocLines.SETRECORD(Vend);
      PurchPostedDocLines.LOOKUPMODE := true;
      if PurchPostedDocLines.RUNMODAL = ACTION::LookupOK then
        PurchPostedDocLines.CopyLineToDoc;

      CLEAR(PurchPostedDocLines);
    END;

    [External]
    PROCEDURE SetSecurityFilterOnRespCenter@43();
    BEGIN
      if UserSetupMgt.GetPurchasesFilter <> '' then begin
        FILTERGROUP(2);
        SETRANGE("Responsibility Center",UserSetupMgt.GetPurchasesFilter);
        FILTERGROUP(0);
      end;

      SETRANGE("Date Filter",0D,WORKDATE - 1);
    END;

    [External]
    PROCEDURE CalcInvDiscForHeader@45();
    VAR
      PurchaseInvDisc@1000 : Codeunit "Purch.-Calc.Discount";
    BEGIN
      PurchSetup.GET;
      if PurchSetup."Calc. Inv. Discount" then
        PurchaseInvDisc.CalculateIncDiscForHeader(Rec);
    END;

    [External]
    PROCEDURE AddShipToAddress@46(SalesHeader@1000 : Record "Sales Header";ShowError@1001 : Boolean);
    VAR
      PurchLine2@1002 : Record "Purchase Line";
    BEGIN
      if ShowError then begin
        PurchLine2.RESET;
        PurchLine2.SETRANGE("Document Type","Document Type"::Order);
        PurchLine2.SETRANGE("Document No.","No.");
        if not PurchLine2.ISEMPTY then begin
          if "Ship-to Name" <> SalesHeader."Ship-to Name" then
            ERROR(Text052,FIELDCAPTION("Ship-to Name"),"No.",SalesHeader."No.");
          if "Ship-to Name 2" <> SalesHeader."Ship-to Name 2" then
            ERROR(Text052,FIELDCAPTION("Ship-to Name 2"),"No.",SalesHeader."No.");
          if "Ship-to Address" <> SalesHeader."Ship-to Address" then
            ERROR(Text052,FIELDCAPTION("Ship-to Address"),"No.",SalesHeader."No.");
          if "Ship-to Address 2" <> SalesHeader."Ship-to Address 2" then
            ERROR(Text052,FIELDCAPTION("Ship-to Address 2"),"No.",SalesHeader."No.");
          if "Ship-to Post Code" <> SalesHeader."Ship-to Post Code" then
            ERROR(Text052,FIELDCAPTION("Ship-to Post Code"),"No.",SalesHeader."No.");
          if "Ship-to City" <> SalesHeader."Ship-to City" then
            ERROR(Text052,FIELDCAPTION("Ship-to City"),"No.",SalesHeader."No.");
          if "Ship-to Contact" <> SalesHeader."Ship-to Contact" then
            ERROR(Text052,FIELDCAPTION("Ship-to Contact"),"No.",SalesHeader."No.");
        end else begin
          // no purchase line exists
          "Ship-to Name" := SalesHeader."Ship-to Name";
          "Ship-to Name 2" := SalesHeader."Ship-to Name 2";
          "Ship-to Address" := SalesHeader."Ship-to Address";
          "Ship-to Address 2" := SalesHeader."Ship-to Address 2";
          "Ship-to Post Code" := SalesHeader."Ship-to Post Code";
          "Ship-to City" := SalesHeader."Ship-to City";
          "Ship-to Contact" := SalesHeader."Ship-to Contact";
        end;
      end;
    END;

    [External]
    PROCEDURE DropShptOrderExists@48(SalesHeader@1000 : Record "Sales Header") : Boolean;
    VAR
      SalesLine2@1001 : Record "Sales Line";
    BEGIN
      // returns TRUE if sales is either Drop Shipment of Special Order
      SalesLine2.RESET;
      SalesLine2.SETRANGE("Document Type",SalesLine2."Document Type"::Order);
      SalesLine2.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine2.SETRANGE("Drop Shipment",true);
      exit(not SalesLine2.ISEMPTY);
    END;

    [External]
    PROCEDURE SpecialOrderExists@81(SalesHeader@1000 : Record "Sales Header") : Boolean;
    VAR
      SalesLine3@1001 : Record "Sales Line";
    BEGIN
      SalesLine3.RESET;
      SalesLine3.SETRANGE("Document Type",SalesLine3."Document Type"::Order);
      SalesLine3.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine3.SETRANGE("Special Order",true);
      exit(not SalesLine3.ISEMPTY);
    END;

    LOCAL PROCEDURE CheckDropShipmentLineExists@153();
    VAR
      SalesShipmentLine@1000 : Record "Sales Shipment Line";
    BEGIN
      SalesShipmentLine.SETRANGE("Purchase Order No.","No.");
      SalesShipmentLine.SETRANGE("Drop Shipment",true);
      if not SalesShipmentLine.ISEMPTY then
        ERROR(YouCannotChangeFieldErr,FIELDCAPTION("Buy-from Vendor No."));
    END;

    [External]
    PROCEDURE QtyToReceiveIsZero@30() : Boolean;
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Document Type");
      PurchLine.SETRANGE("Document No.","No.");
      PurchLine.SETFILTER("Qty. to Receive",'<>0');
      exit(PurchLine.ISEMPTY);
    END;

    LOCAL PROCEDURE IsApprovedForPosting@50() : Boolean;
    VAR
      PrepaymentMgt@1000 : Codeunit "Prepayment Mgt.";
    BEGIN
      if ApprovalsMgmt.PrePostApprovalCheckPurch(Rec) then begin
        if PrepaymentMgt.TestPurchasePrepayment(Rec) then
          ERROR(STRSUBSTNO(Text053,"Document Type","No."));
        if PrepaymentMgt.TestPurchasePayment(Rec) then
          if not CONFIRM(STRSUBSTNO(Text054,"Document Type","No."),true) then
            exit(false);
        exit(true);
      end;
    END;

    [Internal]
    PROCEDURE IsApprovedForPostingBatch@51() : Boolean;
    VAR
      PrepaymentMgt@1000 : Codeunit "Prepayment Mgt.";
    BEGIN
      if ApprovalsMgmt.PrePostApprovalCheckPurch(Rec) then begin
        if PrepaymentMgt.TestPurchasePrepayment(Rec) then
          exit(false);
        if PrepaymentMgt.TestPurchasePayment(Rec) then
          exit(false);
        exit(true);
      end;
    END;

    [Internal]
    PROCEDURE IsTotalValid@36() : Boolean;
    VAR
      IncomingDocument@1002 : Record "Incoming Document";
      PurchaseLine@1001 : Record "Purchase Line";
      TempTotalPurchaseLine@1000 : TEMPORARY Record "Purchase Line";
      DocumentTotals@1003 : Codeunit "Document Totals";
      VATAmount@1004 : Decimal;
    BEGIN
      if not IncomingDocument.GET("Incoming Document Entry No.") then
        exit(true);

      if IncomingDocument."Amount Incl. VAT" = 0 then
        exit(true);

      PurchaseLine.SETRANGE("Document Type","Document Type");
      PurchaseLine.SETRANGE("Document No.","No.");
      if not PurchaseLine.FINDFIRST then
        exit(true);

      if IncomingDocument."Currency Code" <> PurchaseLine."Currency Code" then
        exit(true);

      TempTotalPurchaseLine.INIT;
      DocumentTotals.PurchaseCalculateTotalsWithInvoiceRounding(PurchaseLine,VATAmount,TempTotalPurchaseLine);

      exit(IncomingDocument."Amount Incl. VAT" = TempTotalPurchaseLine."Amount Including VAT");
    END;

    [External]
    PROCEDURE SendToPosting@57(PostingCodeunitID@1000 : Integer);
    BEGIN
      if not IsApprovedForPosting then
        exit;
      CODEUNIT.RUN(PostingCodeunitID,Rec);
    END;

    [External]
    PROCEDURE CancelBackgroundPosting@33();
    VAR
      PurchasePostViaJobQueue@1000 : Codeunit "Purchase Post via Job Queue";
    BEGIN
      PurchasePostViaJobQueue.CancelQueueEntry(Rec);
    END;

    [External]
    PROCEDURE AddSpecialOrderToAddress@80(SalesHeader@1000 : Record "Sales Header";ShowError@1001 : Boolean);
    VAR
      PurchLine3@1003 : Record "Purchase Line";
      LocationCode@1004 : Record Location;
    BEGIN
      if ShowError then begin
        PurchLine3.RESET;
        PurchLine3.SETRANGE("Document Type","Document Type"::Order);
        PurchLine3.SETRANGE("Document No.","No.");
        if not PurchLine3.ISEMPTY then begin
          LocationCode.GET("Location Code");
          if "Ship-to Name" <> LocationCode.Name then
            ERROR(Text052,FIELDCAPTION("Ship-to Name"),"No.",SalesHeader."No.");
          if "Ship-to Name 2" <> LocationCode."Name 2" then
            ERROR(Text052,FIELDCAPTION("Ship-to Name 2"),"No.",SalesHeader."No.");
          if "Ship-to Address" <> LocationCode.Address then
            ERROR(Text052,FIELDCAPTION("Ship-to Address"),"No.",SalesHeader."No.");
          if "Ship-to Address 2" <> LocationCode."Address 2" then
            ERROR(Text052,FIELDCAPTION("Ship-to Address 2"),"No.",SalesHeader."No.");
          if "Ship-to Post Code" <> LocationCode."Post Code" then
            ERROR(Text052,FIELDCAPTION("Ship-to Post Code"),"No.",SalesHeader."No.");
          if "Ship-to City" <> LocationCode.City then
            ERROR(Text052,FIELDCAPTION("Ship-to City"),"No.",SalesHeader."No.");
          if "Ship-to Contact" <> LocationCode.Contact then
            ERROR(Text052,FIELDCAPTION("Ship-to Contact"),"No.",SalesHeader."No.");
        end else
          SetShipToForSpecOrder;
      end;
    END;

    [External]
    PROCEDURE InvoicedLineExists@56() : Boolean;
    VAR
      PurchLine@1000 : Record "Purchase Line";
    BEGIN
      PurchLine.SETRANGE("Document Type","Document Type");
      PurchLine.SETRANGE("Document No.","No.");
      PurchLine.SETFILTER(Type,'<>%1',PurchLine.Type::" ");
      PurchLine.SETFILTER("Quantity Invoiced",'<>%1',0);
      exit(not PurchLine.ISEMPTY);
    END;

    [External]
    PROCEDURE CreateDimSetForPrepmtAccDefaultDim@44();
    VAR
      PurchaseLine@1001 : Record "Purchase Line";
      TempPurchaseLine@1002 : TEMPORARY Record "Purchase Line";
    BEGIN
      PurchaseLine.SETRANGE("Document Type","Document Type");
      PurchaseLine.SETRANGE("Document No.","No.");
      PurchaseLine.SETFILTER("Prepmt. Amt. Inv.",'<>%1',0);
      if PurchaseLine.FINDSET then
        repeat
          CollectParamsInBufferForCreateDimSet(TempPurchaseLine,PurchaseLine);
        until PurchaseLine.NEXT = 0;
      TempPurchaseLine.RESET;
      TempPurchaseLine.MARKEDONLY(false);
      if TempPurchaseLine.FINDSET then
        repeat
          PurchaseLine.CreateDim(DATABASE::"G/L Account",TempPurchaseLine."No.",
            DATABASE::Job,TempPurchaseLine."Job No.",
            DATABASE::"Responsibility Center",TempPurchaseLine."Responsibility Center",
            DATABASE::"Work Center",TempPurchaseLine."Work Center No.");
        until TempPurchaseLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CollectParamsInBufferForCreateDimSet@49(VAR TempPurchaseLine@1000 : TEMPORARY Record "Purchase Line";PurchaseLine@1001 : Record "Purchase Line");
    VAR
      GenPostingSetup@1002 : Record "General Posting Setup";
      DefaultDimension@1003 : Record "Default Dimension";
    BEGIN
      TempPurchaseLine.SETRANGE("Gen. Bus. Posting Group",PurchaseLine."Gen. Bus. Posting Group");
      TempPurchaseLine.SETRANGE("Gen. Prod. Posting Group",PurchaseLine."Gen. Prod. Posting Group");
      if not TempPurchaseLine.FINDFIRST then begin
        GenPostingSetup.GET(PurchaseLine."Gen. Bus. Posting Group",PurchaseLine."Gen. Prod. Posting Group");
        GenPostingSetup.TESTFIELD("Purch. Prepayments Account");
        DefaultDimension.SETRANGE("Table ID",DATABASE::"G/L Account");
        DefaultDimension.SETRANGE("No.",GenPostingSetup."Purch. Prepayments Account");
        InsertTempPurchaseLineInBuffer(TempPurchaseLine,PurchaseLine,
          GenPostingSetup."Purch. Prepayments Account",DefaultDimension.ISEMPTY);
      end else
        if not TempPurchaseLine.MARK then begin
          TempPurchaseLine.SETRANGE("Job No.",PurchaseLine."Job No.");
          TempPurchaseLine.SETRANGE("Responsibility Center",PurchaseLine."Responsibility Center");
          TempPurchaseLine.SETRANGE("Work Center No.",PurchaseLine."Work Center No.");
          if TempPurchaseLine.ISEMPTY then
            InsertTempPurchaseLineInBuffer(TempPurchaseLine,PurchaseLine,TempPurchaseLine."No.",false)
        end;
    END;

    LOCAL PROCEDURE InsertTempPurchaseLineInBuffer@35(VAR TempPurchaseLine@1000 : TEMPORARY Record "Purchase Line";PurchaseLine@1001 : Record "Purchase Line";AccountNo@1002 : Code[20];DefaultDimenstionsNotExist@1003 : Boolean);
    BEGIN
      TempPurchaseLine.INIT;
      TempPurchaseLine."Line No." := PurchaseLine."Line No.";
      TempPurchaseLine."No." := AccountNo;
      TempPurchaseLine."Job No." := PurchaseLine."Job No.";
      TempPurchaseLine."Responsibility Center" := PurchaseLine."Responsibility Center";
      TempPurchaseLine."Work Center No." := PurchaseLine."Work Center No.";
      TempPurchaseLine."Gen. Bus. Posting Group" := PurchaseLine."Gen. Bus. Posting Group";
      TempPurchaseLine."Gen. Prod. Posting Group" := PurchaseLine."Gen. Prod. Posting Group";
      TempPurchaseLine.MARK := DefaultDimenstionsNotExist;
      TempPurchaseLine.INSERT;
    END;

    [Internal]
    PROCEDURE OpenPurchaseOrderStatistics@60();
    BEGIN
      CalcInvDiscForHeader;
      CreateDimSetForPrepmtAccDefaultDim;
      COMMIT;
      PAGE.RUNMODAL(PAGE::"Purchase Order Statistics",Rec);
    END;

    [External]
    PROCEDURE GetCardpageID@58() : Integer;
    BEGIN
      case "Document Type" of
        "Document Type"::Quote:
          exit(PAGE::"Purchase Quote");
        "Document Type"::Order:
          exit(PAGE::"Purchase Order");
        "Document Type"::Invoice:
          exit(PAGE::"Purchase Invoice");
        "Document Type"::"Credit Memo":
          exit(PAGE::"Purchase Credit Memo");
        "Document Type"::"Blanket Order":
          exit(PAGE::"Blanket Purchase Order");
        "Document Type"::"Return Order":
          exit(PAGE::"Purchase Return Order");
      end;
    END;

    [Integration(TRUE)]
    [External]
    PROCEDURE OnCheckPurchasePostRestrictions@54();
    BEGIN
    END;

    [Integration(TRUE)]
    [External]
    PROCEDURE OnCheckPurchaseReleaseRestrictions@55();
    BEGIN
    END;

    [External]
    PROCEDURE SetStatus@53(NewStatus@1000 : Option);
    BEGIN
      Status := NewStatus;
      MODIFY;
    END;

    [External]
    PROCEDURE TriggerOnAfterPostPurchaseDoc@116(VAR GenJnlPostLine@1001 : Codeunit "Gen. Jnl.-Post Line";PurchRcpHdrNo@1002 : Code[20];RetShptHdrNo@1003 : Code[20];PurchInvHdrNo@1004 : Code[20];PurchCrMemoHdrNo@1005 : Code[20]);
    VAR
      PurchPost@1000 : Codeunit "Purch.-Post";
    BEGIN
      PurchPost.OnAfterPostPurchaseDoc(Rec,GenJnlPostLine,PurchRcpHdrNo,RetShptHdrNo,PurchInvHdrNo,PurchCrMemoHdrNo);
    END;

    [External]
    PROCEDURE DeferralHeadersExist@38() : Boolean;
    VAR
      DeferralHeader@1000 : Record "Deferral Header";
      DeferralUtilities@1001 : Codeunit "Deferral Utilities";
    BEGIN
      DeferralHeader.SETRANGE("Deferral Doc. Type",DeferralUtilities.GetPurchDeferralDocType);
      DeferralHeader.SETRANGE("Gen. Jnl. Template Name",'');
      DeferralHeader.SETRANGE("Gen. Jnl. Batch Name",'');
      DeferralHeader.SETRANGE("Document Type","Document Type");
      DeferralHeader.SETRANGE("Document No.","No.");
      exit(not DeferralHeader.ISEMPTY);
    END;

    LOCAL PROCEDURE ConfirmUpdateDeferralDate@85();
    BEGIN
      if HideValidationDialog then
        Confirmed := true
      else
        Confirmed := CONFIRM(DeferralLineQst,false,FIELDCAPTION("Posting Date"));
      if Confirmed then
        UpdatePurchLines(PurchLine.FIELDCAPTION("Deferral Code"),false);
    END;

    [External]
    PROCEDURE IsCreditDocType@110() : Boolean;
    BEGIN
      exit("Document Type" in ["Document Type"::"Return Order","Document Type"::"Credit Memo"]);
    END;

    [External]
    PROCEDURE SetBuyFromVendorFromFilter@186();
    VAR
      BuyFromVendorNo@1000 : Code[20];
    BEGIN
      BuyFromVendorNo := GetFilterVendNo;
      if BuyFromVendorNo = '' then begin
        FILTERGROUP(2);
        BuyFromVendorNo := GetFilterVendNo;
        FILTERGROUP(0);
      end;
      if BuyFromVendorNo <> '' then
        VALIDATE("Buy-from Vendor No.",BuyFromVendorNo);
    END;

    [External]
    PROCEDURE CopyBuyFromVendorFilter@59();
    VAR
      BuyFromVendorFilter@1000 : Text;
    BEGIN
      BuyFromVendorFilter := GETFILTER("Buy-from Vendor No.");
      if BuyFromVendorFilter <> '' then begin
        FILTERGROUP(2);
        SETFILTER("Buy-from Vendor No.",BuyFromVendorFilter);
        FILTERGROUP(0)
      end;
    END;

    LOCAL PROCEDURE GetFilterVendNo@64() : Code[20];
    BEGIN
      if GETFILTER("Buy-from Vendor No.") <> '' then
        if GETRANGEMIN("Buy-from Vendor No.") = GETRANGEMAX("Buy-from Vendor No.") then
          exit(GETRANGEMAX("Buy-from Vendor No."));
    END;

    [External]
    PROCEDURE HasBuyFromAddress@65() : Boolean;
    BEGIN
      case true of
        "Buy-from Address" <> '':
          exit(true);
        "Buy-from Address 2" <> '':
          exit(true);
        "Buy-from City" <> '':
          exit(true);
        "Buy-from Country/Region Code" <> '':
          exit(true);
        "Buy-from County" <> '':
          exit(true);
        "Buy-from Post Code" <> '':
          exit(true);
        "Buy-from Contact" <> '':
          exit(true);
      end;

      exit(false);
    END;

    [External]
    PROCEDURE HasShipToAddress@103() : Boolean;
    BEGIN
      case true of
        "Ship-to Address" <> '':
          exit(true);
        "Ship-to Address 2" <> '':
          exit(true);
        "Ship-to City" <> '':
          exit(true);
        "Ship-to Country/Region Code" <> '':
          exit(true);
        "Ship-to County" <> '':
          exit(true);
        "Ship-to Post Code" <> '':
          exit(true);
        "Ship-to Contact" <> '':
          exit(true);
      end;

      exit(false);
    END;

    [External]
    PROCEDURE HasPayToAddress@66() : Boolean;
    BEGIN
      case true of
        "Pay-to Address" <> '':
          exit(true);
        "Pay-to Address 2" <> '':
          exit(true);
        "Pay-to City" <> '':
          exit(true);
        "Pay-to Country/Region Code" <> '':
          exit(true);
        "Pay-to County" <> '':
          exit(true);
        "Pay-to Post Code" <> '':
          exit(true);
        "Pay-to Contact" <> '':
          exit(true);
      end;

      exit(false);
    END;

    LOCAL PROCEDURE CopyBuyFromVendorAddressFieldsFromVendor@62(VAR BuyFromVendor@1000 : Record Vendor);
    BEGIN
      if BuyFromVendorIsReplaced or ShouldCopyAddressFromBuyFromVendor(BuyFromVendor) then begin
        "Buy-from Address" := BuyFromVendor.Address;
        "Buy-from Address 2" := BuyFromVendor."Address 2";
        "Buy-from City" := BuyFromVendor.City;
        "Buy-from Post Code" := BuyFromVendor."Post Code";
        "Buy-from County" := BuyFromVendor.County;
        "Buy-from Country/Region Code" := BuyFromVendor."Country/Region Code";
      end;
    END;

    LOCAL PROCEDURE CopyShipToVendorAddressFieldsFromVendor@98(VAR BuyFromVendor@1000 : Record Vendor);
    BEGIN
      if BuyFromVendorIsReplaced or (not HasShipToAddress) then begin
        "Ship-to Address" := BuyFromVendor.Address;
        "Ship-to Address 2" := BuyFromVendor."Address 2";
        "Ship-to City" := BuyFromVendor.City;
        "Ship-to Post Code" := BuyFromVendor."Post Code";
        "Ship-to County" := BuyFromVendor.County;
        VALIDATE("Ship-to Country/Region Code",BuyFromVendor."Country/Region Code");
      end;
    END;

    LOCAL PROCEDURE CopyPayToVendorAddressFieldsFromVendor@63(VAR PayToVendor@1000 : Record Vendor);
    BEGIN
      if PayToVendorIsReplaced or ShouldCopyAddressFromPayToVendor(PayToVendor) then begin
        "Pay-to Address" := PayToVendor.Address;
        "Pay-to Address 2" := PayToVendor."Address 2";
        "Pay-to City" := PayToVendor.City;
        "Pay-to Post Code" := PayToVendor."Post Code";
        "Pay-to County" := PayToVendor.County;
        "Pay-to Country/Region Code" := PayToVendor."Country/Region Code";
      end;
    END;

    [External]
    PROCEDURE SetShipToAddress@117(ShipToName@1000 : Text[50];ShipToName2@1001 : Text[50];ShipToAddress@1002 : Text[50];ShipToAddress2@1003 : Text[50];ShipToCity@1004 : Text[30];ShipToPostCode@1005 : Code[20];ShipToCounty@1006 : Text[30];ShipToCountryRegionCode@1007 : Code[10]);
    BEGIN
      "Ship-to Name" := ShipToName;
      "Ship-to Name 2" := ShipToName2;
      "Ship-to Address" := ShipToAddress;
      "Ship-to Address 2" := ShipToAddress2;
      "Ship-to City" := ShipToCity;
      "Ship-to Post Code" := ShipToPostCode;
      "Ship-to County" := ShipToCounty;
      "Ship-to Country/Region Code" := ShipToCountryRegionCode;
    END;

    LOCAL PROCEDURE ShouldCopyAddressFromBuyFromVendor@101(BuyFromVendor@1000 : Record Vendor) : Boolean;
    BEGIN
      exit((not HasBuyFromAddress) and BuyFromVendor.HasAddress);
    END;

    LOCAL PROCEDURE ShouldCopyAddressFromPayToVendor@102(PayToVendor@1000 : Record Vendor) : Boolean;
    BEGIN
      exit((not HasPayToAddress) and PayToVendor.HasAddress);
    END;

    LOCAL PROCEDURE BuyFromVendorIsReplaced@96() : Boolean;
    BEGIN
      exit((xRec."Buy-from Vendor No." <> '') and (xRec."Buy-from Vendor No." <> "Buy-from Vendor No."));
    END;

    LOCAL PROCEDURE PayToVendorIsReplaced@97() : Boolean;
    BEGIN
      exit((xRec."Pay-to Vendor No." <> '') and (xRec."Pay-to Vendor No." <> "Pay-to Vendor No."));
    END;

    LOCAL PROCEDURE UpdatePayToAddressFromBuyFromAddress@61(FieldNumber@1000 : Integer);
    BEGIN
      if ("Order Address Code" = '') and PayToAddressEqualsOldBuyFromAddress then
        case FieldNumber of
          FIELDNO("Pay-to Address"):
            if xRec."Buy-from Address" = "Pay-to Address" then
              "Pay-to Address" := "Buy-from Address";
          FIELDNO("Pay-to Address 2"):
            if xRec."Buy-from Address 2" = "Pay-to Address 2" then
              "Pay-to Address 2" := "Buy-from Address 2";
          FIELDNO("Pay-to City"), FIELDNO("Pay-to Post Code"):
            begin
              if xRec."Buy-from City" = "Pay-to City" then
                "Pay-to City" := "Buy-from City";
              if xRec."Buy-from Post Code" = "Pay-to Post Code" then
                "Pay-to Post Code" := "Buy-from Post Code";
              if xRec."Buy-from County" = "Pay-to County" then
                "Pay-to County" := "Buy-from County";
              if xRec."Buy-from Country/Region Code" = "Pay-to Country/Region Code" then
                "Pay-to Country/Region Code" := "Buy-from Country/Region Code";
            end;
          FIELDNO("Pay-to County"):
            if xRec."Buy-from County" = "Pay-to County" then
              "Pay-to County" := "Buy-from County";
          FIELDNO("Pay-to Country/Region Code"):
            if  xRec."Buy-from Country/Region Code" = "Pay-to Country/Region Code" then
              "Pay-to Country/Region Code" := "Buy-from Country/Region Code";
        end;
    END;

    LOCAL PROCEDURE PayToAddressEqualsOldBuyFromAddress@67() : Boolean;
    BEGIN
      if (xRec."Buy-from Address" = "Pay-to Address") and
         (xRec."Buy-from Address 2" = "Pay-to Address 2") and
         (xRec."Buy-from City" = "Pay-to City") and
         (xRec."Buy-from County" = "Pay-to County") and
         (xRec."Buy-from Post Code" = "Pay-to Post Code") and
         (xRec."Buy-from Country/Region Code" = "Pay-to Country/Region Code")
      then
        exit(true);
    END;

    [External]
    PROCEDURE ConfirmCloseUnposted@104() : Boolean;
    VAR
      InstructionMgt@1000 : Codeunit "Instruction Mgt.";
    BEGIN
      if PurchLinesExist then
        exit(InstructionMgt.ShowConfirm(DocumentNotPostedClosePageQst,InstructionMgt.QueryPostOnCloseCode));
      exit(true)
    END;

    [External]
    PROCEDURE InitFromPurchHeader@109(SourcePurchHeader@1000 : Record "Purchase Header");
    BEGIN
      "Document Date" := SourcePurchHeader."Document Date";
      "Expected Receipt Date" := SourcePurchHeader."Expected Receipt Date";
      "Shortcut Dimension 1 Code" := SourcePurchHeader."Shortcut Dimension 1 Code";
      "Shortcut Dimension 2 Code" := SourcePurchHeader."Shortcut Dimension 2 Code";
      "Dimension Set ID" := SourcePurchHeader."Dimension Set ID";
      "Location Code" := SourcePurchHeader."Location Code";
      SetShipToAddress(
        SourcePurchHeader."Ship-to Name",SourcePurchHeader."Ship-to Name 2",SourcePurchHeader."Ship-to Address",
        SourcePurchHeader."Ship-to Address 2",SourcePurchHeader."Ship-to City",SourcePurchHeader."Ship-to Post Code",
        SourcePurchHeader."Ship-to County",SourcePurchHeader."Ship-to Country/Region Code");
      "Ship-to Contact" := SourcePurchHeader."Ship-to Contact";
    END;

    LOCAL PROCEDURE InitFromVendor@68(VendorNo@1000 : Code[20];VendorCaption@1001 : Text) : Boolean;
    BEGIN
      PurchLine.SETRANGE("Document Type","Document Type");
      PurchLine.SETRANGE("Document No.","No.");
      if VendorNo = '' then begin
        if not PurchLine.ISEMPTY then
          ERROR(Text005,VendorCaption);
        INIT;
        PurchSetup.GET;
        "No. Series" := xRec."No. Series";
        InitRecord;
        InitNoSeries;
        exit(true);
      end;
    END;

    LOCAL PROCEDURE InitFromContact@69(ContactNo@1000 : Code[20];VendorNo@1001 : Code[20];ContactCaption@1002 : Text) : Boolean;
    BEGIN
      PurchLine.SETRANGE("Document Type","Document Type");
      PurchLine.SETRANGE("Document No.","No.");
      if (ContactNo = '') and (VendorNo = '') then begin
        if not PurchLine.ISEMPTY then
          ERROR(Text005,ContactCaption);
        INIT;
        PurchSetup.GET;
        "No. Series" := xRec."No. Series";
        InitRecord;
        InitNoSeries;
        exit(true);
      end;
    END;

    LOCAL PROCEDURE LookupContact@122(VendorNo@1000 : Code[20];ContactNo@1003 : Code[20];VAR Contact@1001 : Record Contact);
    VAR
      ContactBusinessRelation@1002 : Record "Contact Business Relation";
    BEGIN
      if ContactBusinessRelation.FindByRelation(ContactBusinessRelation."Link to Table"::Vendor,VendorNo) then
        Contact.SETRANGE("Company No.",ContactBusinessRelation."Contact No.")
      else
        Contact.SETRANGE("Company No.",'');
      if ContactNo <> '' then
        if Contact.GET(ContactNo) then ;
    END;

    [Internal]
    PROCEDURE SendRecords@75();
    VAR
      DocumentSendingProfile@1000 : Record "Document Sending Profile";
      DummyReportSelections@1001 : Record "Report Selections";
    BEGIN
      CheckMixedDropShipment;

      DocumentSendingProfile.SendVendorRecords(
        DummyReportSelections.Usage::"P.Order",Rec,DocTxt,"Buy-from Vendor No.","No.",
        FIELDNO("Buy-from Vendor No."),FIELDNO("No."));
    END;

    [External]
    PROCEDURE PrintRecords@74(ShowRequestForm@1002 : Boolean);
    VAR
      DocumentSendingProfile@1001 : Record "Document Sending Profile";
      DummyReportSelections@1000 : Record "Report Selections";
    BEGIN
      CheckMixedDropShipment;

      DocumentSendingProfile.TrySendToPrinterVendor(
        DummyReportSelections.Usage::"P.Order",Rec,"Buy-from Vendor No.",ShowRequestForm);
    END;

    [Internal]
    PROCEDURE SendProfile@73(VAR DocumentSendingProfile@1000 : Record "Document Sending Profile");
    VAR
      DummyReportSelections@1001 : Record "Report Selections";
    BEGIN
      CheckMixedDropShipment;

      DocumentSendingProfile.SendVendor(
        DummyReportSelections.Usage::"P.Order",Rec,"No.","Buy-from Vendor No.",
        DocTxt,FIELDNO("Buy-from Vendor No."),FIELDNO("No."));
    END;

    LOCAL PROCEDURE CheckMixedDropShipment@84();
    BEGIN
      if HasMixedDropShipment then
        ERROR(MixedDropshipmentErr);
    END;

    LOCAL PROCEDURE HasMixedDropShipment@83() : Boolean;
    VAR
      PurchaseLine@1000 : Record "Purchase Line";
      HasDropShipmentLines@1001 : Boolean;
    BEGIN
      PurchaseLine.SETRANGE("Document Type","Document Type");
      PurchaseLine.SETRANGE("Document No.","No.");
      PurchaseLine.SETFILTER("No.",'<>%1','');
      PurchaseLine.SETRANGE("Drop Shipment",true);

      HasDropShipmentLines := not PurchaseLine.ISEMPTY;

      PurchaseLine.SETRANGE("Drop Shipment",false);

      exit(HasDropShipmentLines and not PurchaseLine.ISEMPTY);
    END;

    LOCAL PROCEDURE SetDefaultPurchaser@76();
    VAR
      UserSetup@1000 : Record "User Setup";
    BEGIN
      if not UserSetup.GET(USERID) then
        exit;

      if UserSetup."Salespers./Purch. Code" <> '' then
        VALIDATE("Purchaser Code",UserSetup."Salespers./Purch. Code");
    END;

    PROCEDURE OnAfterValidateBuyFromVendorNo@77(VAR PurchaseHeader@1000 : Record "Purchase Header";VAR xPurchaseHeader@1001 : Record "Purchase Header");
    BEGIN
      if PurchaseHeader.GETFILTER("Buy-from Vendor No.") = xPurchaseHeader."Buy-from Vendor No." then
        if PurchaseHeader."Buy-from Vendor No." <> xPurchaseHeader."Buy-from Vendor No." then
          PurchaseHeader.SETRANGE("Buy-from Vendor No.");
    END;

    PROCEDURE BatchConfirmUpdateDeferralDate@78(VAR BatchConfirm@1000 : '" ",Skip,Update';ReplacePostingDate@1001 : Boolean;PostingDateReq@1002 : Date);
    BEGIN
      if (not ReplacePostingDate) or (PostingDateReq = "Posting Date") or (BatchConfirm = BatchConfirm::Skip) then
        exit;

      if not DeferralHeadersExist then
        exit;

      "Posting Date" := PostingDateReq;
      case BatchConfirm of
        BatchConfirm::" ":
          begin
            ConfirmUpdateDeferralDate;
            if Confirmed then
              BatchConfirm := BatchConfirm::Update
            else
              BatchConfirm := BatchConfirm::Skip;
          end;
        BatchConfirm::Update:
          UpdatePurchLines(PurchLine.FIELDCAPTION("Deferral Code"),false);
      end;
      COMMIT;
    END;

    PROCEDURE SetAllowSelectNoSeries@86();
    BEGIN
      SelectNoSeriesAllowed := true;
    END;

    LOCAL PROCEDURE ModifyPayToVendorAddress@194();
    VAR
      Vendor@1000 : Record Vendor;
    BEGIN
      if IsCreditDocType then
        exit;
      if ("Pay-to Vendor No." <> "Buy-from Vendor No.") and Vendor.GET("Pay-to Vendor No.") then
        if HasPayToAddress and HasDifferentPayToAddress(Vendor) then
          ShowModifyAddressNotification(GetModifyPayToVendorAddressNotificationId,
            ModifyVendorAddressNotificationLbl,ModifyVendorAddressNotificationMsg,
            UpdateAddressWithPayToAddressTok,"Pay-to Vendor No.","Pay-to Name",FIELDNAME("Pay-to Vendor No."));
    END;

    LOCAL PROCEDURE ModifyVendorAddress@150();
    VAR
      Vendor@1000 : Record Vendor;
    BEGIN
      if IsCreditDocType then
        exit;
      if Vendor.GET("Buy-from Vendor No.") and HasBuyFromAddress and HasDifferentBuyFromAddress(Vendor) then
        ShowModifyAddressNotification(GetModifyVendorAddressNotificationId,
          ModifyVendorAddressNotificationLbl,ModifyVendorAddressNotificationMsg,
          UpdateAddressWithBuyFromAddressFunctionTok,"Buy-from Vendor No.","Buy-from Vendor Name",FIELDNAME("Buy-from Vendor No."));
    END;

    LOCAL PROCEDURE ShowModifyAddressNotification@157(NotificationID@1001 : GUID;NotificationLbl@1004 : Text;NotificationMsg@1005 : Text;NotificationFunctionTok@1006 : Text;VendorNumber@1002 : Code[20];VendorName@1003 : Text[50];VendorNumberFieldName@1008 : Text);
    VAR
      MyNotifications@1009 : Record "My Notifications";
      NotificationLifecycleMgt@1007 : Codeunit "Notification Lifecycle Mgt.";
      ModifyVendorAddressNotification@1000 : Notification;
    BEGIN
      if not MyNotifications.IsEnabled(NotificationID) then
        exit;

      ModifyVendorAddressNotification.ID := NotificationID;
      ModifyVendorAddressNotification.MESSAGE := STRSUBSTNO(NotificationMsg,VendorName);
      ModifyVendorAddressNotification.ADDACTION(NotificationLbl,CODEUNIT::"Document Notifications",NotificationFunctionTok);
      ModifyVendorAddressNotification.ADDACTION(DontShowAgainActionLbl,CODEUNIT::"Document Notifications",DontShowAgainFunctionTok);
      ModifyVendorAddressNotification.SCOPE := NOTIFICATIONSCOPE::LocalScope;
      ModifyVendorAddressNotification.SETDATA(FIELDNAME("Document Type"),FORMAT("Document Type"));
      ModifyVendorAddressNotification.SETDATA(FIELDNAME("No."),"No.");
      ModifyVendorAddressNotification.SETDATA(VendorNumberFieldName,VendorNumber);
      NotificationLifecycleMgt.SendNotification(ModifyVendorAddressNotification,RECORDID);
    END;

    PROCEDURE RecallModifyAddressNotification@148(NotificationID@1001 : GUID);
    VAR
      MyNotifications@1002 : Record "My Notifications";
      ModifyVendorAddressNotification@1000 : Notification;
    BEGIN
      if IsCreditDocType or (not MyNotifications.IsEnabled(NotificationID)) then
        exit;
      ModifyVendorAddressNotification.ID := NotificationID;
      ModifyVendorAddressNotification.RECALL;
    END;

    PROCEDURE GetModifyVendorAddressNotificationId@193() : GUID;
    BEGIN
      exit('CF3D0CD3-C54A-47D1-8A3F-57A6CCBA8DDE');
    END;

    PROCEDURE GetModifyPayToVendorAddressNotificationId@191() : GUID;
    BEGIN
      exit('16E45B3A-CB9F-4B2C-9F08-2BCE39E9E980');
    END;

    PROCEDURE GetShowExternalDocAlreadyExistNotificationId@92() : GUID;
    BEGIN
      exit('D87F624C-D3BE-4E6B-A369-D18AE269181A');
    END;

    PROCEDURE SetModifyVendorAddressNotificationDefaultState@196();
    VAR
      MyNotifications@1000 : Record "My Notifications";
    BEGIN
      MyNotifications.InsertDefault(GetModifyVendorAddressNotificationId,
        ModifyBuyFromVendorAddressNotificationNameTxt,ModifyBuyFromVendorAddressNotificationDescriptionTxt,true);
    END;

    PROCEDURE SetModifyPayToVendorAddressNotificationDefaultState@197();
    VAR
      MyNotifications@1000 : Record "My Notifications";
    BEGIN
      MyNotifications.InsertDefault(GetModifyPayToVendorAddressNotificationId,
        ModifyPayToVendorAddressNotificationNameTxt,ModifyPayToVendorAddressNotificationDescriptionTxt,true);
    END;

    PROCEDURE SetShowExternalDocAlreadyExistNotificationDefaultState@87(DefaultState@1001 : Boolean);
    VAR
      MyNotifications@1000 : Record "My Notifications";
    BEGIN
      MyNotifications.InsertDefault(GetShowExternalDocAlreadyExistNotificationId,
        ShowDocAlreadyExistNotificationNameTxt,ShowDocAlreadyExistNotificationDescriptionTxt,DefaultState);
    END;

    PROCEDURE DontNotifyCurrentUserAgain@141(NotificationID@1001 : GUID);
    VAR
      MyNotifications@1000 : Record "My Notifications";
    BEGIN
      if not MyNotifications.Disable(NotificationID) then
        case NotificationID of
          GetModifyVendorAddressNotificationId:
            MyNotifications.InsertDefault(NotificationID,ModifyBuyFromVendorAddressNotificationNameTxt,
              ModifyBuyFromVendorAddressNotificationDescriptionTxt,false);
          GetModifyPayToVendorAddressNotificationId:
            MyNotifications.InsertDefault(NotificationID,ModifyPayToVendorAddressNotificationNameTxt,
              ModifyPayToVendorAddressNotificationDescriptionTxt,false);
        end;
    END;

    LOCAL PROCEDURE HasDifferentBuyFromAddress@195(Vendor@1000 : Record Vendor) : Boolean;
    BEGIN
      exit(("Buy-from Address" <> Vendor.Address) or
        ("Buy-from Address 2" <> Vendor."Address 2") or
        ("Buy-from City" <> Vendor.City) or
        ("Buy-from Country/Region Code" <> Vendor."Country/Region Code") or
        ("Buy-from County" <> Vendor.County) or
        ("Buy-from Post Code" <> Vendor."Post Code") or
        ("Buy-from Contact" <> Vendor.Contact));
    END;

    LOCAL PROCEDURE HasDifferentPayToAddress@192(Vendor@1000 : Record Vendor) : Boolean;
    BEGIN
      exit(("Pay-to Address" <> Vendor.Address) or
        ("Pay-to Address 2" <> Vendor."Address 2") or
        ("Pay-to City" <> Vendor.City) or
        ("Pay-to Country/Region Code" <> Vendor."Country/Region Code") or
        ("Pay-to County" <> Vendor.County) or
        ("Pay-to Post Code" <> Vendor."Post Code") or
        ("Pay-to Contact" <> Vendor.Contact));
    END;

    LOCAL PROCEDURE FindPostedDocumentWithSameExternalDocNo@88(VAR VendorLedgerEntry@1000 : Record "Vendor Ledger Entry";ExternalDocumentNo@1001 : Code[35]) : Boolean;
    BEGIN
      VendorLedgerEntry.SETRANGE("External Document No.",ExternalDocumentNo);
      VendorLedgerEntry.SETRANGE("Vendor No.","Pay-to Vendor No.");
      VendorLedgerEntry.SETRANGE("Document Type",GetGenJnlDocumentType);
      VendorLedgerEntry.SETRANGE(Reversed,false);
      exit(VendorLedgerEntry.FINDFIRST);
    END;

    LOCAL PROCEDURE ShowExternalDocAlreadyExistNotification@89(VendorLedgerEntry@1000 : Record "Vendor Ledger Entry");
    VAR
      NotificationLifecycleMgt@1002 : Codeunit "Notification Lifecycle Mgt.";
      DocAlreadyExistNotification@1001 : Notification;
    BEGIN
      if not IsDocAlreadyExistNotificationEnabled then
        exit;

      DocAlreadyExistNotification.ID := GetShowExternalDocAlreadyExistNotificationId;
      DocAlreadyExistNotification.MESSAGE :=
        STRSUBSTNO(PurchaseAlreadyExistsTxt,VendorLedgerEntry."Document Type",VendorLedgerEntry."External Document No.");
      DocAlreadyExistNotification.ADDACTION(ShowVendLedgEntryTxt,CODEUNIT::"Document Notifications",'ShowVendorLedgerEntry');
      DocAlreadyExistNotification.SCOPE := NOTIFICATIONSCOPE::LocalScope;
      DocAlreadyExistNotification.SETDATA(FIELDNAME("Document Type"),FORMAT("Document Type"));
      DocAlreadyExistNotification.SETDATA(FIELDNAME("No."),"No.");
      DocAlreadyExistNotification.SETDATA(VendorLedgerEntry.FIELDNAME("Entry No."),FORMAT(VendorLedgerEntry."Entry No."));
      NotificationLifecycleMgt.SendNotificationWithAdditionalContext(
        DocAlreadyExistNotification,RECORDID,GetShowExternalDocAlreadyExistNotificationId);
    END;

    LOCAL PROCEDURE GetGenJnlDocumentType@90() : Integer;
    VAR
      RefGenJournalLine@1000 : Record "Gen. Journal Line";
    BEGIN
      case "Document Type" of
        "Document Type"::"Blanket Order",
        "Document Type"::Quote,
        "Document Type"::Invoice,
        "Document Type"::Order:
          exit(RefGenJournalLine."Document Type"::Invoice);
        else
          exit(RefGenJournalLine."Document Type"::"Credit Memo");
      end;
    END;

    LOCAL PROCEDURE RecallExternalDocAlreadyExistsNotification@39();
    VAR
      NotificationLifecycleMgt@1000 : Codeunit "Notification Lifecycle Mgt.";
    BEGIN
      if not IsDocAlreadyExistNotificationEnabled then
        exit;

      NotificationLifecycleMgt.RecallNotificationsForRecordWithAdditionalContext(
        RECORDID,GetShowExternalDocAlreadyExistNotificationId,true);
    END;

    LOCAL PROCEDURE IsDocAlreadyExistNotificationEnabled@91() : Boolean;
    VAR
      MyNotifications@1000 : Record "My Notifications";
    BEGIN
      if not MyNotifications.GET(USERID,GetShowExternalDocAlreadyExistNotificationId) then
        exit(false);

      exit(MyNotifications.Enabled);
    END;

    [External]
    PROCEDURE ShipToAddressEqualsCompanyShipToAddress@111() : Boolean;
    VAR
      CompanyInformation@1000 : Record "Company Information";
    BEGIN
      CompanyInformation.GET;
      exit(IsShipToAddressEqualToCompanyShipToAddress(Rec,CompanyInformation));
    END;

    LOCAL PROCEDURE IsShipToAddressEqualToCompanyShipToAddress@113(PurchaseHeader@1000 : Record "Purchase Header";CompanyInformation@1001 : Record "Company Information") : Boolean;
    BEGIN
      exit(
        (PurchaseHeader."Ship-to Address" = CompanyInformation."Ship-to Address") and
        (PurchaseHeader."Ship-to Address 2" = CompanyInformation."Ship-to Address 2") and
        (PurchaseHeader."Ship-to City" = CompanyInformation."Ship-to City") and
        (PurchaseHeader."Ship-to County" = CompanyInformation."Ship-to County") and
        (PurchaseHeader."Ship-to Post Code" = CompanyInformation."Ship-to Post Code") and
        (PurchaseHeader."Ship-to Country/Region Code" = CompanyInformation."Ship-to Country/Region Code") and
        (PurchaseHeader."Ship-to Name" = CompanyInformation."Ship-to Name"));
    END;

    [External]
    PROCEDURE BuyFromAddressEqualsShipToAddress@94() : Boolean;
    BEGIN
      exit(
        ("Ship-to Address" = "Buy-from Address") and
        ("Ship-to Address 2" = "Buy-from Address 2") and
        ("Ship-to City" = "Buy-from City") and
        ("Ship-to County" = "Buy-from County") and
        ("Ship-to Post Code" = "Buy-from Post Code") and
        ("Ship-to Country/Region Code" = "Buy-from Country/Region Code") and
        ("Ship-to Name" = "Buy-from Vendor Name"));
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitRecord@138(VAR PurchHeader@1000 : Record "Purchase Header");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitNoSeries@140(VAR PurchHeader@1000 : Record "Purchase Header");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTestNoSeries@136(VAR PurchHeader@1000 : Record "Purchase Header");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateShipToAddress@137(VAR PurchHeader@1000 : Record "Purchase Header");
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnUpdatePurchLinesByChangedFieldName@139(PurchHeader@1000 : Record "Purchase Header";VAR PurchLine@1001 : Record "Purchase Line";ChangedFieldName@1002 : Text[100]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateDimTableIDs@164(VAR PurchaseHeader@1000 : Record "Purchase Header";FieldNo@1001 : Integer;TableID@1003 : ARRAY [10] OF Integer;No@1002 : ARRAY [10] OF Code[20]);
    BEGIN
    END;

    [Integration(TRUE)]
    PROCEDURE OnValidatePurchaseHeaderPayToVendorNo@1215(Vendor@1214 : Record Vendor);
    BEGIN
    END;

    BEGIN
    END.
  }
}

