OBJECT Table 111 Sales Shipment Line
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Item Ledger Entry"=r,
                TableData "Value Entry"=r;
    OnDelete=VAR
               ServItem@1000 : Record "Service Item";
               SalesDocLineComments@1001 : Record "Sales Comment Line";
             BEGIN
               ServItem.RESET;
               ServItem.SETCURRENTKEY("Sales/Serv. Shpt. Document No.","Sales/Serv. Shpt. Line No.");
               ServItem.SETRANGE("Sales/Serv. Shpt. Document No.","Document No.");
               ServItem.SETRANGE("Sales/Serv. Shpt. Line No.","Line No.");
               ServItem.SETRANGE("Shipment Type",ServItem."Shipment Type"::Sales);
               if ServItem.FIND('-') then
                 repeat
                   ServItem.VALIDATE("Sales/Serv. Shpt. Document No.",'');
                   ServItem.VALIDATE("Sales/Serv. Shpt. Line No.",0);
                   ServItem.MODIFY(true);
                 until ServItem.NEXT = 0;

               SalesDocLineComments.SETRANGE("Document Type",SalesDocLineComments."Document Type"::Shipment);
               SalesDocLineComments.SETRANGE("No.","Document No.");
               SalesDocLineComments.SETRANGE("Document Line No.","Line No.");
               if not SalesDocLineComments.ISEMPTY then
                 SalesDocLineComments.DELETEALL;

               PostedATOLink.DeleteAsmFromSalesShptLine(Rec);
             END;

    CaptionML=ENU=Sales Shipment Line;
    LookupPageID="Posted Sales Shipment Lines";
    DrillDownPageID="Posted Sales Shipment Lines";
  }
  FIELDS
  {
    { 2   ;   ;Sell-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Sell-to Customer No.;
                                                   Editable=false }
    { 3   ;   ;Document No.        ;Code20        ;TableRelation="Sales Shipment Header";
                                                   CaptionML=ENU=Document No. }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 5   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,G/L Account,Item,Resource,Fixed Asset,Charge (Item)";
                                                   OptionString=" ","G/L Account",Item,Resource,"Fixed Asset","Charge (Item)" }
    { 6   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST("G/L Account")) "G/L Account"
                                                                 ELSE IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST("Fixed Asset")) "Fixed Asset"
                                                                 ELSE IF (Type=CONST("Charge (Item)")) "Item Charge";
                                                   CaptionML=ENU=No.;
                                                   CaptionClass=GetCaptionClass(FIELDNO("No.")) }
    { 7   ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE ("Use As In-Transit"=CONST(false));
                                                   CaptionML=ENU=Location Code }
    { 8   ;   ;Posting Group       ;Code20        ;TableRelation=IF (Type=CONST(Item)) "Inventory Posting Group"
                                                                 ELSE IF (Type=CONST("Fixed Asset")) "FA Posting Group";
                                                   CaptionML=ENU=Posting Group;
                                                   Editable=false }
    { 10  ;   ;Shipment Date       ;Date          ;CaptionML=ENU=Shipment Date }
    { 11  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 12  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2 }
    { 13  ;   ;Unit of Measure     ;Text10        ;CaptionML=ENU=Unit of Measure }
    { 15  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5 }
    { 22  ;   ;Unit Price          ;Decimal       ;CaptionML=ENU=Unit Price;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 23  ;   ;Unit Cost (LCY)     ;Decimal       ;CaptionML=ENU=Unit Cost (LCY);
                                                   AutoFormatType=2 }
    { 25  ;   ;VAT %               ;Decimal       ;CaptionML=ENU=VAT %;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 27  ;   ;Line Discount %     ;Decimal       ;CaptionML=ENU=Line Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 32  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Allow Invoice Disc. }
    { 34  ;   ;Gross Weight        ;Decimal       ;CaptionML=ENU=Gross Weight;
                                                   DecimalPlaces=0:5 }
    { 35  ;   ;Net Weight          ;Decimal       ;CaptionML=ENU=Net Weight;
                                                   DecimalPlaces=0:5 }
    { 36  ;   ;Units per Parcel    ;Decimal       ;CaptionML=ENU=Units per Parcel;
                                                   DecimalPlaces=0:5 }
    { 37  ;   ;Unit Volume         ;Decimal       ;CaptionML=ENU=Unit Volume;
                                                   DecimalPlaces=0:5 }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Appl.-to Item Entry }
    { 39  ;   ;Item Shpt. Entry No.;Integer       ;CaptionML=ENU=Item Shpt. Entry No. }
    { 40  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(1));
                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 41  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE ("Global Dimension No."=CONST(2));
                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 42  ;   ;Customer Price Group;Code10        ;TableRelation="Customer Price Group";
                                                   CaptionML=ENU=Customer Price Group }
    { 45  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Job No. }
    { 52  ;   ;Work Type Code      ;Code10        ;TableRelation="Work Type";
                                                   CaptionML=ENU=Work Type Code }
    { 58  ;   ;Qty. Shipped Not Invoiced;Decimal  ;AccessByPermission=TableData "Sales Shipment Header"=R;
                                                   CaptionML=ENU=Qty. Shipped Not Invoiced;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 61  ;   ;Quantity Invoiced   ;Decimal       ;CaptionML=ENU=Quantity Invoiced;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 65  ;   ;Order No.           ;Code20        ;CaptionML=ENU=Order No. }
    { 66  ;   ;Order Line No.      ;Integer       ;CaptionML=ENU=Order Line No. }
    { 68  ;   ;Bill-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Bill-to Customer No.;
                                                   Editable=false }
    { 71  ;   ;Purchase Order No.  ;Code20        ;CaptionML=ENU=Purchase Order No. }
    { 72  ;   ;Purch. Order Line No.;Integer      ;CaptionML=ENU=Purch. Order Line No. }
    { 73  ;   ;Drop Shipment       ;Boolean       ;AccessByPermission=TableData "Drop Shpt. Post. Buffer"=R;
                                                   CaptionML=ENU=Drop Shipment }
    { 74  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 75  ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=ENU=Gen. Prod. Posting Group }
    { 77  ;   ;VAT Calculation Type;Option        ;CaptionML=ENU=VAT Calculation Type;
                                                   OptionCaptionML=ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   OptionString="Normal VAT","Reverse Charge VAT","Full VAT","Sales Tax" }
    { 78  ;   ;Transaction Type    ;Code10        ;TableRelation="Transaction Type";
                                                   CaptionML=ENU=Transaction Type }
    { 79  ;   ;Transport Method    ;Code10        ;TableRelation="Transport Method";
                                                   CaptionML=ENU=Transport Method }
    { 80  ;   ;Attached to Line No.;Integer       ;TableRelation="Sales Shipment Line"."Line No." WHERE ("Document No."=FIELD("Document No."));
                                                   CaptionML=ENU=Attached to Line No. }
    { 81  ;   ;Exit Point          ;Code10        ;TableRelation="Entry/Exit Point";
                                                   CaptionML=ENU=Exit Point }
    { 82  ;   ;Area                ;Code10        ;TableRelation=Area;
                                                   CaptionML=ENU=Area }
    { 83  ;   ;Transaction Specification;Code10   ;TableRelation="Transaction Specification";
                                                   CaptionML=ENU=Transaction Specification }
    { 85  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=ENU=Tax Area Code }
    { 86  ;   ;Tax Liable          ;Boolean       ;CaptionML=ENU=Tax Liable }
    { 87  ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   CaptionML=ENU=Tax Group Code }
    { 89  ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 90  ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=ENU=VAT Prod. Posting Group }
    { 91  ;   ;Currency Code       ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Shipment Header"."Currency Code" WHERE ("No."=FIELD("Document No.")));
                                                   CaptionML=ENU=Currency Code;
                                                   Editable=false }
    { 97  ;   ;Blanket Order No.   ;Code20        ;TableRelation="Sales Header"."No." WHERE ("Document Type"=CONST("Blanket Order"));
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Blanket Order No. }
    { 98  ;   ;Blanket Order Line No.;Integer     ;TableRelation="Sales Line"."Line No." WHERE ("Document Type"=CONST("Blanket Order"),
                                                                                                "Document No."=FIELD("Blanket Order No."));
                                                   TestTableRelation=false;
                                                   CaptionML=ENU=Blanket Order Line No. }
    { 99  ;   ;VAT Base Amount     ;Decimal       ;CaptionML=ENU=VAT Base Amount;
                                                   Editable=false;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 100 ;   ;Unit Cost           ;Decimal       ;CaptionML=ENU=Unit Cost;
                                                   Editable=false;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 131 ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=false }
    { 826 ;   ;Authorized for Credit Card;Boolean ;CaptionML=ENU=Authorized for Credit Card }
    { 1001;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE ("Job No."=FIELD("Job No."));
                                                   CaptionML=ENU=Job Task No.;
                                                   Editable=false }
    { 1002;   ;Job Contract Entry No.;Integer     ;CaptionML=ENU=Job Contract Entry No.;
                                                   Editable=false }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE ("Item No."=FIELD("No."));
                                                   CaptionML=ENU=Variant Code }
    { 5403;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE ("Location Code"=FIELD("Location Code"),
                                                                                 "Item Filter"=FIELD("No."),
                                                                                 "Variant Filter"=FIELD("Variant Code"));
                                                   CaptionML=ENU=Bin Code }
    { 5404;   ;Qty. per Unit of Measure;Decimal   ;CaptionML=ENU=Qty. per Unit of Measure;
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5407;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE ("Item No."=FIELD("No."))
                                                                 ELSE "Unit of Measure";
                                                   CaptionML=ENU=Unit of Measure Code }
    { 5415;   ;Quantity (Base)     ;Decimal       ;CaptionML=ENU=Quantity (Base);
                                                   DecimalPlaces=0:5 }
    { 5461;   ;Qty. Invoiced (Base);Decimal       ;CaptionML=ENU=Qty. Invoiced (Base);
                                                   DecimalPlaces=0:5;
                                                   Editable=false }
    { 5600;   ;FA Posting Date     ;Date          ;CaptionML=ENU=FA Posting Date }
    { 5602;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   CaptionML=ENU=Depreciation Book Code }
    { 5605;   ;Depr. until FA Posting Date;Boolean;CaptionML=ENU=Depr. until FA Posting Date }
    { 5612;   ;Duplicate in Depreciation Book;Code10;
                                                   TableRelation="Depreciation Book";
                                                   CaptionML=ENU=Duplicate in Depreciation Book }
    { 5613;   ;Use Duplication List;Boolean       ;CaptionML=ENU=Use Duplication List }
    { 5700;   ;Responsibility Center;Code10       ;TableRelation="Responsibility Center";
                                                   CaptionML=ENU=Responsibility Center }
    { 5705;   ;Cross-Reference No. ;Code20        ;AccessByPermission=TableData "Item Cross Reference"=R;
                                                   CaptionML=ENU=Cross-Reference No. }
    { 5706;   ;Unit of Measure (Cross Ref.);Code10;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE ("Item No."=FIELD("No."));
                                                   CaptionML=ENU=Unit of Measure (Cross Ref.) }
    { 5707;   ;Cross-Reference Type;Option        ;CaptionML=ENU=Cross-Reference Type;
                                                   OptionCaptionML=ENU=" ,Customer,Vendor,Bar Code";
                                                   OptionString=" ",Customer,Vendor,"Bar Code" }
    { 5708;   ;Cross-Reference Type No.;Code30    ;CaptionML=ENU=Cross-Reference Type No. }
    { 5709;   ;Item Category Code  ;Code20        ;TableRelation=IF (Type=CONST(Item)) "Item Category";
                                                   CaptionML=ENU=Item Category Code }
    { 5710;   ;Nonstock            ;Boolean       ;CaptionML=ENU=Nonstock }
    { 5711;   ;Purchasing Code     ;Code10        ;TableRelation=Purchasing;
                                                   CaptionML=ENU=Purchasing Code }
    { 5712;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE ("Item Category Code"=FIELD("Item Category Code"));
                                                   ValidateTableRelation=false;
                                                   CaptionML=ENU=Product Group Code }
    { 5790;   ;Requested Delivery Date;Date       ;AccessByPermission=TableData "Order Promising Line"=R;
                                                   CaptionML=ENU=Requested Delivery Date;
                                                   Editable=false }
    { 5791;   ;Promised Delivery Date;Date        ;CaptionML=ENU=Promised Delivery Date;
                                                   Editable=false }
    { 5792;   ;Shipping Time       ;DateFormula   ;AccessByPermission=TableData "Shipping Agent Services"=R;
                                                   CaptionML=ENU=Shipping Time }
    { 5793;   ;Outbound Whse. Handling Time;DateFormula;
                                                   AccessByPermission=TableData Location=R;
                                                   CaptionML=ENU=Outbound Whse. Handling Time }
    { 5794;   ;Planned Delivery Date;Date         ;CaptionML=ENU=Planned Delivery Date;
                                                   Editable=false }
    { 5795;   ;Planned Shipment Date;Date         ;CaptionML=ENU=Planned Shipment Date;
                                                   Editable=false }
    { 5811;   ;Appl.-from Item Entry;Integer      ;AccessByPermission=TableData Item=R;
                                                   CaptionML=ENU=Appl.-from Item Entry;
                                                   MinValue=0 }
    { 5812;   ;Item Charge Base Amount;Decimal    ;CaptionML=ENU=Item Charge Base Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 5817;   ;Correction          ;Boolean       ;CaptionML=ENU=Correction;
                                                   Editable=false }
    { 6608;   ;Return Reason Code  ;Code10        ;TableRelation="Return Reason";
                                                   CaptionML=ENU=Return Reason Code }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;InitValue=true;
                                                   CaptionML=ENU=Allow Line Disc. }
    { 7002;   ;Customer Disc. Group;Code20        ;TableRelation="Customer Discount Group";
                                                   CaptionML=ENU=Customer Disc. Group }
  }
  KEYS
  {
    {    ;"Document No.","Line No."               ;Clustered=true }
    {    ;"Order No.","Order Line No."             }
    {    ;"Blanket Order No.","Blanket Order Line No." }
    {    ;"Item Shpt. Entry No."                   }
    {    ;"Sell-to Customer No."                   }
    {    ;"Bill-to Customer No."                   }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;"Document No.","Line No.","Sell-to Customer No.",Type,"No.","Shipment Date" }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Shipment No. %1:';
      Text001@1001 : TextConst 'ENU=The program cannot find this Sales line.';
      Currency@1002 : Record Currency;
      SalesShptHeader@1005 : Record "Sales Shipment Header";
      PostedATOLink@1006 : Record "Posted Assemble-to-Order Link";
      DimMgt@1003 : Codeunit DimensionManagement;
      CurrencyRead@1004 : Boolean;

    [External]
    PROCEDURE GetCurrencyCode@1() : Code[10];
    BEGIN
      if "Document No." = SalesShptHeader."No." then
        exit(SalesShptHeader."Currency Code");
      if SalesShptHeader.GET("Document No.") then
        exit(SalesShptHeader."Currency Code");
      exit('');
    END;

    [External]
    PROCEDURE ShowDimensions@25();
    BEGIN
      DimMgt.ShowDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2 %3',TABLECAPTION,"Document No.","Line No."));
    END;

    [External]
    PROCEDURE ShowItemTrackingLines@6500();
    VAR
      ItemTrackingDocMgt@1000 : Codeunit "Item Tracking Doc. Management";
    BEGIN
      ItemTrackingDocMgt.ShowItemTrackingForShptRcptLine(DATABASE::"Sales Shipment Line",0,"Document No.",'',0,"Line No.");
    END;

    [Internal]
    PROCEDURE InsertInvLineFromShptLine@2(VAR SalesLine@1000 : Record "Sales Line");
    VAR
      SalesInvHeader@1011 : Record "Sales Header";
      SalesOrderHeader@1008 : Record "Sales Header";
      SalesOrderLine@1005 : Record "Sales Line";
      TempSalesLine@1002 : TEMPORARY Record "Sales Line";
      TransferOldExtLines@1007 : Codeunit "Transfer Old Ext. Text Lines";
      ItemTrackingMgt@1009 : Codeunit "Item Tracking Management";
      LanguageManagement@1010 : Codeunit LanguageManagement;
      ExtTextLine@1006 : Boolean;
      NextLineNo@1001 : Integer;
    BEGIN
      SETRANGE("Document No.","Document No.");

      TempSalesLine := SalesLine;
      if SalesLine.FIND('+') then
        NextLineNo := SalesLine."Line No." + 10000
      else
        NextLineNo := 10000;

      if SalesInvHeader."No." <> TempSalesLine."Document No." then
        SalesInvHeader.GET(TempSalesLine."Document Type",TempSalesLine."Document No.");

      if SalesLine."Shipment No." <> "Document No." then begin
        SalesLine.INIT;
        SalesLine."Line No." := NextLineNo;
        SalesLine."Document Type" := TempSalesLine."Document Type";
        SalesLine."Document No." := TempSalesLine."Document No.";
        LanguageManagement.SetGlobalLanguageByCode(SalesInvHeader."Language Code");
        SalesLine.Description := STRSUBSTNO(Text000,"Document No.");
        LanguageManagement.RestoreGlobalLanguage;
        SalesLine.INSERT;
        NextLineNo := NextLineNo + 10000;
      end;

      TransferOldExtLines.ClearLineNumbers;

      repeat
        ExtTextLine := (TransferOldExtLines.GetNewLineNumber("Attached to Line No.") <> 0);

        if (Type <> Type::" ") and SalesOrderLine.GET(SalesOrderLine."Document Type"::Order,"Order No.","Order Line No.")
        then begin
          if (SalesOrderHeader."Document Type" <> SalesOrderLine."Document Type"::Order) or
             (SalesOrderHeader."No." <> SalesOrderLine."Document No.")
          then
            SalesOrderHeader.GET(SalesOrderLine."Document Type"::Order,"Order No.");

          InitCurrency("Currency Code");

          if SalesInvHeader."Prices Including VAT" then begin
            if not SalesOrderHeader."Prices Including VAT" then
              SalesOrderLine."Unit Price" :=
                ROUND(
                  SalesOrderLine."Unit Price" * (1 + SalesOrderLine."VAT %" / 100),
                  Currency."Unit-Amount Rounding Precision");
          end else begin
            if SalesOrderHeader."Prices Including VAT" then
              SalesOrderLine."Unit Price" :=
                ROUND(
                  SalesOrderLine."Unit Price" / (1 + SalesOrderLine."VAT %" / 100),
                  Currency."Unit-Amount Rounding Precision");
          end;
        end else begin
          SalesOrderHeader.INIT;
          if ExtTextLine or (Type = Type::" ") then begin
            SalesOrderLine.INIT;
            SalesOrderLine."Line No." := "Order Line No.";
            SalesOrderLine.Description := Description;
            SalesOrderLine."Description 2" := "Description 2";
          end else
            ERROR(Text001);
        end;

        SalesLine := SalesOrderLine;
        SalesLine."Line No." := NextLineNo;
        SalesLine."Document Type" := TempSalesLine."Document Type";
        SalesLine."Document No." := TempSalesLine."Document No.";
        SalesLine."Variant Code" := "Variant Code";
        SalesLine."Location Code" := "Location Code";
        SalesLine."Quantity (Base)" := 0;
        SalesLine.Quantity := 0;
        SalesLine."Outstanding Qty. (Base)" := 0;
        SalesLine."Outstanding Quantity" := 0;
        SalesLine."Quantity Shipped" := 0;
        SalesLine."Qty. Shipped (Base)" := 0;
        SalesLine."Quantity Invoiced" := 0;
        SalesLine."Qty. Invoiced (Base)" := 0;
        SalesLine.Amount := 0;
        SalesLine."Amount Including VAT" := 0;
        SalesLine."Purchase Order No." := '';
        SalesLine."Purch. Order Line No." := 0;
        SalesLine."Drop Shipment" := "Drop Shipment";
        SalesLine."Special Order Purchase No." := '';
        SalesLine."Special Order Purch. Line No." := 0;
        SalesLine."Special Order" := false;
        SalesLine."Shipment No." := "Document No.";
        SalesLine."Shipment Line No." := "Line No.";
        SalesLine."Appl.-to Item Entry" := 0;
        SalesLine."Appl.-from Item Entry" := 0;
        if not ExtTextLine and (SalesLine.Type <> 0) then begin
          SalesLine.VALIDATE(Quantity,Quantity - "Quantity Invoiced");
          CalcBaseQuantities(SalesLine,"Quantity (Base)" / Quantity);
          SalesLine.VALIDATE("Unit Price",SalesOrderLine."Unit Price");
          SalesLine."Allow Line Disc." := SalesOrderLine."Allow Line Disc.";
          SalesLine."Allow Invoice Disc." := SalesOrderLine."Allow Invoice Disc.";
          SalesOrderLine."Line Discount Amount" :=
            ROUND(
              SalesOrderLine."Line Discount Amount" * SalesLine.Quantity / SalesOrderLine.Quantity,
              Currency."Amount Rounding Precision");
          if SalesInvHeader."Prices Including VAT" then begin
            if not SalesOrderHeader."Prices Including VAT" then
              SalesOrderLine."Line Discount Amount" :=
                ROUND(
                  SalesOrderLine."Line Discount Amount" *
                  (1 + SalesOrderLine."VAT %" / 100),Currency."Amount Rounding Precision");
          end else begin
            if SalesOrderHeader."Prices Including VAT" then
              SalesOrderLine."Line Discount Amount" :=
                ROUND(
                  SalesOrderLine."Line Discount Amount" /
                  (1 + SalesOrderLine."VAT %" / 100),Currency."Amount Rounding Precision");
          end;
          SalesLine.VALIDATE("Line Discount Amount",SalesOrderLine."Line Discount Amount");
          SalesLine."Line Discount %" := SalesOrderLine."Line Discount %";
          SalesLine.UpdatePrePaymentAmounts;
          if SalesOrderLine.Quantity = 0 then
            SalesLine.VALIDATE("Inv. Discount Amount",0)
          else
            SalesLine.VALIDATE(
              "Inv. Discount Amount",
              ROUND(
                SalesOrderLine."Inv. Discount Amount" * SalesLine.Quantity / SalesOrderLine.Quantity,
                Currency."Amount Rounding Precision"));
        end;

        SalesLine."Attached to Line No." :=
          TransferOldExtLines.TransferExtendedText(
            SalesOrderLine."Line No.",
            NextLineNo,
            "Attached to Line No.");
        SalesLine."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
        SalesLine."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
        SalesLine."Dimension Set ID" := "Dimension Set ID";
        SalesLine.INSERT;

        ItemTrackingMgt.CopyHandledItemTrkgToInvLine(SalesOrderLine,SalesLine);

        NextLineNo := NextLineNo + 10000;
        if "Attached to Line No." = 0 then
          SETRANGE("Attached to Line No.","Line No.");
      until (NEXT = 0) or ("Attached to Line No." = 0);

      if SalesOrderHeader.GET(SalesOrderHeader."Document Type"::Order,"Order No.") then begin
        SalesOrderHeader."Get Shipment Used" := true;
        SalesOrderHeader.MODIFY;
      end;
    END;

    LOCAL PROCEDURE GetSalesInvLines@4(VAR TempSalesInvLine@1000 : TEMPORARY Record "Sales Invoice Line");
    VAR
      SalesInvLine@1003 : Record "Sales Invoice Line";
      ItemLedgEntry@1002 : Record "Item Ledger Entry";
      ValueEntry@1001 : Record "Value Entry";
    BEGIN
      TempSalesInvLine.RESET;
      TempSalesInvLine.DELETEALL;

      if Type <> Type::Item then
        exit;

      FilterPstdDocLnItemLedgEntries(ItemLedgEntry);
      ItemLedgEntry.SETFILTER("Invoiced Quantity",'<>0');
      if ItemLedgEntry.FINDSET then begin
        ValueEntry.SETCURRENTKEY("Item Ledger Entry No.","Entry Type");
        ValueEntry.SETRANGE("Entry Type",ValueEntry."Entry Type"::"Direct Cost");
        ValueEntry.SETFILTER("Invoiced Quantity",'<>0');
        repeat
          ValueEntry.SETRANGE("Item Ledger Entry No.",ItemLedgEntry."Entry No.");
          if ValueEntry.FINDSET then
            repeat
              if ValueEntry."Document Type" = ValueEntry."Document Type"::"Sales Invoice" then
                if SalesInvLine.GET(ValueEntry."Document No.",ValueEntry."Document Line No.") then begin
                  TempSalesInvLine.INIT;
                  TempSalesInvLine := SalesInvLine;
                  if TempSalesInvLine.INSERT then;
                end;
            until ValueEntry.NEXT = 0;
        until ItemLedgEntry.NEXT = 0;
      end;
    END;

    [External]
    PROCEDURE CalcShippedSaleNotReturned@3(VAR ShippedQtyNotReturned@1003 : Decimal;VAR RevUnitCostLCY@1005 : Decimal;ExactCostReverse@1006 : Boolean);
    VAR
      ItemLedgEntry@1000 : Record "Item Ledger Entry";
      TotalCostLCY@1007 : Decimal;
      TotalQtyBase@1002 : Decimal;
    BEGIN
      ShippedQtyNotReturned := 0;
      if (Type <> Type::Item) or (Quantity <= 0) then begin
        RevUnitCostLCY := "Unit Cost (LCY)";
        exit;
      end;

      RevUnitCostLCY := 0;
      FilterPstdDocLnItemLedgEntries(ItemLedgEntry);
      if ItemLedgEntry.FINDSET then
        repeat
          ShippedQtyNotReturned := ShippedQtyNotReturned - ItemLedgEntry."Shipped Qty. Not Returned";
          if ExactCostReverse then begin
            ItemLedgEntry.CALCFIELDS("Cost Amount (Expected)","Cost Amount (Actual)");
            TotalCostLCY :=
              TotalCostLCY + ItemLedgEntry."Cost Amount (Expected)" + ItemLedgEntry."Cost Amount (Actual)";
            TotalQtyBase := TotalQtyBase + ItemLedgEntry.Quantity;
          end;
        until ItemLedgEntry.NEXT = 0;

      if ExactCostReverse and (ShippedQtyNotReturned <> 0) and (TotalQtyBase <> 0) then
        RevUnitCostLCY := ABS(TotalCostLCY / TotalQtyBase) * "Qty. per Unit of Measure"
      else
        RevUnitCostLCY := "Unit Cost (LCY)";

      ShippedQtyNotReturned := CalcQty(ShippedQtyNotReturned);
    END;

    LOCAL PROCEDURE CalcQty@9(QtyBase@1000 : Decimal) : Decimal;
    BEGIN
      if "Qty. per Unit of Measure" = 0 then
        exit(QtyBase);
      exit(ROUND(QtyBase / "Qty. per Unit of Measure",0.00001));
    END;

    [External]
    PROCEDURE FilterPstdDocLnItemLedgEntries@5(VAR ItemLedgEntry@1000 : Record "Item Ledger Entry");
    BEGIN
      ItemLedgEntry.RESET;
      ItemLedgEntry.SETCURRENTKEY("Document No.");
      ItemLedgEntry.SETRANGE("Document No.","Document No.");
      ItemLedgEntry.SETRANGE("Document Type",ItemLedgEntry."Document Type"::"Sales Shipment");
      ItemLedgEntry.SETRANGE("Document Line No.","Line No.");
    END;

    [External]
    PROCEDURE ShowItemSalesInvLines@8();
    VAR
      TempSalesInvLine@1001 : TEMPORARY Record "Sales Invoice Line";
    BEGIN
      if Type = Type::Item then begin
        GetSalesInvLines(TempSalesInvLine);
        PAGE.RUNMODAL(PAGE::"Posted Sales Invoice Lines",TempSalesInvLine);
      end;
    END;

    LOCAL PROCEDURE InitCurrency@6(CurrencyCode@1001 : Code[10]);
    BEGIN
      if (Currency.Code = CurrencyCode) and CurrencyRead then
        exit;

      if CurrencyCode <> '' then
        Currency.GET(CurrencyCode)
      else
        Currency.InitRoundingPrecision;
      CurrencyRead := true;
    END;

    [External]
    PROCEDURE ShowLineComments@10();
    VAR
      SalesCommentLine@1000 : Record "Sales Comment Line";
    BEGIN
      SalesCommentLine.ShowComments(SalesCommentLine."Document Type"::Shipment,"Document No.","Line No.");
    END;

    [External]
    PROCEDURE ShowAsmToOrder@11();
    BEGIN
      PostedATOLink.ShowPostedAsm(Rec);
    END;

    [External]
    PROCEDURE AsmToShipmentExists@72(VAR PostedAsmHeader@1000 : Record "Posted Assembly Header") : Boolean;
    VAR
      PostedAssembleToOrderLink@1001 : Record "Posted Assemble-to-Order Link";
    BEGIN
      if not PostedAssembleToOrderLink.AsmExistsForPostedShipmentLine(Rec) then
        exit(false);
      exit(PostedAsmHeader.GET(PostedAssembleToOrderLink."Assembly Document No."));
    END;

    [External]
    PROCEDURE InitFromSalesLine@12(SalesShptHeader@1001 : Record "Sales Shipment Header";SalesLine@1002 : Record "Sales Line");
    BEGIN
      INIT;
      TRANSFERFIELDS(SalesLine);
      if ("No." = '') and (Type in [Type::"G/L Account"..Type::"Charge (Item)"]) then
        Type := Type::" ";
      "Posting Date" := SalesShptHeader."Posting Date";
      "Document No." := SalesShptHeader."No.";
      Quantity := SalesLine."Qty. to Ship";
      "Quantity (Base)" := SalesLine."Qty. to Ship (Base)";
      if ABS(SalesLine."Qty. to Invoice") > ABS(SalesLine."Qty. to Ship") then begin
        "Quantity Invoiced" := SalesLine."Qty. to Ship";
        "Qty. Invoiced (Base)" := SalesLine."Qty. to Ship (Base)";
      end else begin
        "Quantity Invoiced" := SalesLine."Qty. to Invoice";
        "Qty. Invoiced (Base)" := SalesLine."Qty. to Invoice (Base)";
      end;
      "Qty. Shipped Not Invoiced" := Quantity - "Quantity Invoiced";
      if SalesLine."Document Type" = SalesLine."Document Type"::Order then begin
        "Order No." := SalesLine."Document No.";
        "Order Line No." := SalesLine."Line No.";
      end;

      OnAfterInitFromSalesLine(SalesShptHeader,SalesLine,Rec);
    END;

    PROCEDURE FormatType@144() : Text;
    VAR
      SalesLine@1000 : Record "Sales Line";
    BEGIN
      if Type = Type::" " then
        exit(SalesLine.FormatType);

      exit(FORMAT(Type));
    END;

    LOCAL PROCEDURE CalcBaseQuantities@13(VAR SalesLine@1000 : Record "Sales Line";QtyFactor@1001 : Decimal);
    BEGIN
      SalesLine."Quantity (Base)" := ROUND(SalesLine.Quantity * QtyFactor,0.00001);
      SalesLine."Qty. to Asm. to Order (Base)" := ROUND(SalesLine."Qty. to Assemble to Order" * QtyFactor,0.00001);
      SalesLine."Outstanding Qty. (Base)" := ROUND(SalesLine."Outstanding Quantity" * QtyFactor,0.00001);
      SalesLine."Qty. to Ship (Base)" := ROUND(SalesLine."Qty. to Ship" * QtyFactor,0.00001);
      SalesLine."Qty. Shipped (Base)" := ROUND(SalesLine."Quantity Shipped" * QtyFactor,0.00001);
      SalesLine."Qty. Shipped Not Invd. (Base)" := ROUND(SalesLine."Qty. Shipped Not Invoiced" * QtyFactor,0.00001);
      SalesLine."Qty. to Invoice (Base)" := ROUND(SalesLine."Qty. to Invoice" * QtyFactor,0.00001);
      SalesLine."Qty. Invoiced (Base)" := ROUND(SalesLine."Quantity Invoiced" * QtyFactor,0.00001);
      SalesLine."Return Qty. to Receive (Base)" := ROUND(SalesLine."Return Qty. to Receive" * QtyFactor,0.00001);
      SalesLine."Return Qty. Received (Base)" := ROUND(SalesLine."Return Qty. Received" * QtyFactor,0.00001);
      SalesLine."Ret. Qty. Rcd. Not Invd.(Base)" := ROUND(SalesLine."Return Qty. Rcd. Not Invd." * QtyFactor,0.00001);
    END;

    LOCAL PROCEDURE GetFieldCaption@31(FieldNumber@1000 : Integer) : Text[100];
    VAR
      Field@1001 : Record Field;
    BEGIN
      Field.GET(DATABASE::"Sales Shipment Line",FieldNumber);
      exit(Field."Field Caption");
    END;

    LOCAL PROCEDURE GetCaptionClass@34(FieldNumber@1000 : Integer) : Text[80];
    BEGIN
      case FieldNumber of
        FIELDNO("No."):
          exit(STRSUBSTNO('3,%1',GetFieldCaption(FieldNumber)));
      end;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInitFromSalesLine@7(SalesShptHeader@1000 : Record "Sales Shipment Header";SalesLine@1001 : Record "Sales Line";VAR SalesShptLine@1002 : Record "Sales Shipment Line");
    BEGIN
    END;

    BEGIN
    END.
  }
}

