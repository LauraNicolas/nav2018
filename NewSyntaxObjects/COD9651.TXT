OBJECT Codeunit 9651 Document Report Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NotImplementedErr@1002 : TextConst 'ENU=This option is not available.';
      TemplateValidationQst@1000 : TextConst 'ENU=The Word layout does not comply with the current report design (for example, fields are missing or the report ID is wrong).\The following errors were detected during the layout validation:\%1\Do you want to continue?';
      TemplateValidationErr@1003 : TextConst 'ENU=The Word layout does not comply with the current report design (for example, fields are missing or the report ID is wrong).\The following errors were detected during the document validation:\%1\You must update the layout to match the current report design.';
      AbortWithValidationErr@1001 : TextConst 'ENU=The Word layout action has been canceled because of validation errors.';
      TemplateValidationUpdateQst@1004 : TextConst 'ENU=The Word layout does not comply with the current report design (for example, fields are missing or the report ID is wrong).\The following errors were detected during the layout validation:\%1\Do you want to run an automatic update?';
      TemplateAfterUpdateValidationErr@1006 : TextConst 'ENU=The automatic update could not resolve all the conflicts in the current Word layout. For example, the layout uses fields that are missing in the report design or the report ID is wrong.\The following errors were detected:\%1\You must manually update the layout to match the current report design.';
      UpgradeMessageMsg@1005 : TextConst 'ENU=The report upgrade process returned the following log messages:\%1.';
      NoReportLayoutUpgradeRequiredMsg@1007 : TextConst 'ENU=The layout upgrade process completed without detecting any required changes in the current application.';
      CompanyInformationPicErr@1011 : TextConst 'ENU=The document contains elements that cannot be converted to PDF. This may be caused by missing image data in the document.';
      FileTypeWordTxt@1008 : TextConst '@@@={Locked};ENU=docx';
      FileTypePdfTxt@1009 : TextConst '@@@={Locked};ENU=pdf';
      FileTypeHtmlTxt@1010 : TextConst '@@@={Locked};ENU=html';

    [Internal]
    PROCEDURE MergeWordLayout@5(ReportID@1000 : Integer;ReportAction@1007 : 'SaveAsPdf,SaveAsWord,SaveAsExcel,Preview,Print,SaveAsHtml';InStrXmlData@1002 : InStream;FileName@1001 : Text);
    VAR
      ReportLayoutSelection@1011 : Record "Report Layout Selection";
      CustomReportLayout@1003 : Record "Custom Report Layout";
      InTempBlob@1010 : Record TempBlob;
      OutTempBlob@1009 : Record TempBlob;
      FileMgt@1008 : Codeunit "File Management";
      InStrWordDoc@1006 : InStream;
      OutStrWordDoc@1005 : OutStream;
      NAVWordXMLMerger@1004 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
      CustomLayoutCode@1012 : Code[20];
      CurrentFileType@1013 : Text;
    BEGIN
      // FileName contains printername for ReportAction::Print.
      // Temporarily selected layout for Design-time report execution?
      if ReportLayoutSelection.GetTempLayoutSelected <> '' then
        CustomLayoutCode := ReportLayoutSelection.GetTempLayoutSelected
      else  // Normal selection
        if ReportLayoutSelection.GET(ReportID,COMPANYNAME) and
           (ReportLayoutSelection.Type = ReportLayoutSelection.Type::"Custom Layout")
        then
          CustomLayoutCode := ReportLayoutSelection."Custom Report Layout Code";

      if CustomLayoutCode <> '' then
        if not CustomReportLayout.GET(CustomLayoutCode) then
          CustomLayoutCode := '';

      if CustomLayoutCode = '' then
        REPORT.WORDLAYOUT(ReportID,InStrWordDoc)
      else begin
        ValidateAndUpdateWordLayoutOnRecord(CustomReportLayout);
        CustomReportLayout.GetLayoutBlob(InTempBlob);
        InTempBlob.Blob.CREATEINSTREAM(InStrWordDoc);
        ValidateWordLayoutCheckOnly(ReportID,InStrWordDoc);
      end;
      OutTempBlob.Blob.CREATEOUTSTREAM(OutStrWordDoc);

      OutStrWordDoc := NAVWordXMLMerger.MergeWordDocument(InStrWordDoc,InStrXmlData,OutStrWordDoc) ;
      COMMIT;

      CurrentFileType := '';
      case ReportAction of
        ReportAction::SaveAsWord:
          CurrentFileType := FileTypeWordTxt;
        ReportAction::SaveAsPdf:
          begin
            CurrentFileType := FileTypePdfTxt;
            ConvertToPdf(OutTempBlob);
          end;
        ReportAction::SaveAsHtml:
          begin
            CurrentFileType := FileTypeHtmlTxt;
            ConvertToHtml(OutTempBlob);
          end;
        ReportAction::SaveAsExcel:
          ERROR(NotImplementedErr);
        ReportAction::Print:
          PrintWordDoc(ReportID,OutTempBlob,FileName,true);
        ReportAction::Preview:
          FileMgt.BLOBExport(OutTempBlob,UserFileName(ReportID,CurrentFileType),true);
      end;

      // Export the file to the client of the action generates an output object in which case currentFileType is non-empty.
      if CurrentFileType <> '' then
        if FileName = '' then
          FileMgt.BLOBExport(OutTempBlob,UserFileName(ReportID,CurrentFileType),true)
        else
          // Dont' use FileMgt.BLOBExportToServerFile. It will fail if run through
          // CodeUnit 8800, as the filename will exist in a temp folder.
          OutTempBlob.Blob.EXPORT(FileName);
    END;

    [External]
    PROCEDURE ValidateWordLayout@2(ReportID@1001 : Integer;DocumentStream@1000 : InStream;useConfirm@1004 : Boolean;updateContext@1005 : Boolean) : Boolean;
    VAR
      NAVWordXMLMerger@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
      ValidationErrors@1003 : Text;
      ValidationErrorFormat@1006 : Text;
    BEGIN
      ValidationErrors := NAVWordXMLMerger.ValidateWordDocumentTemplate(DocumentStream,REPORT.WORDXMLPART(ReportID,true));
      if ValidationErrors <> '' then begin
        if useConfirm then begin
          if not CONFIRM(TemplateValidationQst,false,ValidationErrors) then
            ERROR(AbortWithValidationErr);
        end else begin
          if updateContext then
            ValidationErrorFormat := TemplateAfterUpdateValidationErr
          else
            ValidationErrorFormat := TemplateValidationErr;

          ERROR(ValidationErrorFormat,ValidationErrors);
        end;

        exit(false);
      end;
      exit(true);
    END;

    LOCAL PROCEDURE ValidateWordLayoutCheckOnly@4(ReportID@1001 : Integer;DocumentStream@1000 : InStream);
    VAR
      NAVWordXMLMerger@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
      ValidationErrors@1003 : Text;
      ValidationErrorFormat@1006 : Text;
    BEGIN
      ValidationErrors := NAVWordXMLMerger.ValidateWordDocumentTemplate(DocumentStream,REPORT.WORDXMLPART(ReportID,true));
      if ValidationErrors <> '' then begin
        ValidationErrorFormat := TemplateAfterUpdateValidationErr;
        MESSAGE(ValidationErrorFormat,ValidationErrors);
      end;
    END;

    LOCAL PROCEDURE ValidateAndUpdateWordLayoutOnRecord@7(CustomReportLayout@1001 : Record "Custom Report Layout") : Boolean;
    VAR
      TempBlob@1000 : Record TempBlob;
      NAVWordXMLMerger@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
      DocumentStream@1005 : InStream;
      ValidationErrors@1003 : Text;
    BEGIN
      CustomReportLayout.TESTFIELD(Type,CustomReportLayout.Type::Word);
      CustomReportLayout.GetLayoutBlob(TempBlob);
      TempBlob.Blob.CREATEINSTREAM(DocumentStream);
      NAVWordXMLMerger := NAVWordXMLMerger.WordReportManager;

      ValidationErrors :=
        NAVWordXMLMerger.ValidateWordDocumentTemplate(DocumentStream,REPORT.WORDXMLPART(CustomReportLayout."Report ID",true));
      if ValidationErrors <> '' then begin
        if CONFIRM(TemplateValidationUpdateQst,false,ValidationErrors) then begin
          ValidationErrors := CustomReportLayout.TryUpdateLayout(false);
          COMMIT;
          exit(true);
        end;
        ERROR(TemplateValidationErr,ValidationErrors);
      end;
      exit(false);
    END;

    [External]
    PROCEDURE TryUpdateWordLayout@12(DocumentStream@1001 : InStream;VAR UpdateStream@1002 : OutStream;CachedCustomPart@1003 : Text;CurrentCustomPart@1004 : Text) : Text;
    VAR
      NAVWordXMLMerger@1007 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
    BEGIN
      NAVWordXMLMerger := NAVWordXMLMerger.WordReportManager;
      NAVWordXMLMerger.UpdateWordDocumentLayout(DocumentStream,UpdateStream,CachedCustomPart,CurrentCustomPart,true);
      exit(NAVWordXMLMerger.LastUpdateError);
    END;

    [External]
    PROCEDURE TryUpdateRdlcLayout@13(reportId@1001 : Integer;RdlcStream@1002 : InStream;RdlcUpdatedStream@1000 : OutStream;CachedCustomPart@1003 : Text;CurrentCustomPart@1004 : Text;IgnoreDelete@1005 : Boolean) : Text;
    VAR
      NAVWordXMLMerger@1007 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.RdlcReportManager";
    BEGIN
      exit(NAVWordXMLMerger.TryUpdateRdlcLayout(reportId,RdlcStream,RdlcUpdatedStream,
          CachedCustomPart,CurrentCustomPart,IgnoreDelete));
    END;

    [External]
    PROCEDURE NewWordLayout@11(ReportId@1000 : Integer;VAR DocumentStream@1001 : OutStream);
    VAR
      NAVWordXmlMerger@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
    BEGIN
      NAVWordXmlMerger.NewWordDocumentLayout(DocumentStream,REPORT.WORDXMLPART(ReportId));
    END;

    LOCAL PROCEDURE ConvertToPdf@1(VAR TempBlob@1015 : Record TempBlob);
    VAR
      TempBlobPdf@1006 : Record TempBlob;
      InStreamWordDoc@1007 : InStream;
      OutStreamPdfDoc@1008 : OutStream;
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(InStreamWordDoc);
      TempBlobPdf.Blob.CREATEOUTSTREAM(OutStreamPdfDoc);

      if TryConvertToPdf(InStreamWordDoc,OutStreamPdfDoc) then
        TempBlob.Blob := TempBlobPdf.Blob
      else
        ERROR(CompanyInformationPicErr);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryConvertToPdf@23(InStreamWordDoc@1001 : InStream;OutStreamPdfDoc@1000 : OutStream);
    VAR
      PdfWriter@1002 : DotNet "'Microsoft.Dynamics.Nav.PdfWriter, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.PdfWriter.WordToPdf";
    BEGIN
      PdfWriter.ConvertToPdf(InStreamWordDoc,OutStreamPdfDoc);
    END;

    LOCAL PROCEDURE ConvertToHtml@22(VAR TempBlob@1015 : Record TempBlob);
    VAR
      TempBlobHtml@1006 : Record TempBlob;
      InStreamWordDoc@1007 : InStream;
      OutStreamHtmlDoc@1008 : OutStream;
      PdfWriter@1002 : DotNet "'Microsoft.Dynamics.Nav.PdfWriter, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.PdfWriter.WordToPdf";
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(InStreamWordDoc);
      TempBlobHtml.Blob.CREATEOUTSTREAM(OutStreamHtmlDoc);
      PdfWriter.ConvertToHtml(InStreamWordDoc,OutStreamHtmlDoc);
      TempBlob.Blob := TempBlobHtml.Blob;
    END;

    LOCAL PROCEDURE PrintWordDoc@15(ReportID@1003 : Integer;VAR TempBlob@1015 : Record TempBlob;PrinterName@1000 : Text;Collate@1002 : Boolean);
    VAR
      FileMgt@1001 : Codeunit "File Management";
    BEGIN
      if FileMgt.IsWindowsClient then
        PrintWordDocInWord(ReportID,TempBlob,PrinterName,Collate,1)
      else
        if FileMgt.IsWebOrDeviceClient then begin
          ConvertToPdf(TempBlob);
          FileMgt.BLOBExport(TempBlob,UserFileName(ReportID,FileTypePdfTxt),true);
        end else
          PrintWordDocOnServer(TempBlob,PrinterName,Collate);
    END;

    LOCAL PROCEDURE PrintWordDocInWord@17(ReportID@1006 : Integer;TempBlob@1015 : Record TempBlob;PrinterName@1004 : Text;Collate@1007 : Boolean;Copies@1008 : Integer);
    VAR
      FileMgt@1005 : Codeunit "File Management";
      WordApplication@1001 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.ApplicationClass" RUNONCLIENT;
      WordDocument@1000 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.Document" RUNONCLIENT;
      WordHelper@1016 : DotNet "'Microsoft.Dynamics.Nav.Integration.Office, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Integration.Office.Word.WordHelper" RUNONCLIENT;
      FileName@1003 : Text;
      T0@1002 : DateTime;
    BEGIN
      if GetWordApplication(WordApplication) and not ISNULL(WordApplication) then begin
        FileName := STRSUBSTNO('%1.docx',CREATEGUID);
        FileName := FileMgt.BLOBExport(TempBlob,FileName,false);

        WordDocument := WordHelper.CallOpen(WordApplication,FileName,false,false);
        WordHelper.CallPrintOut(WordDocument,PrinterName,Collate,Copies);

        T0 := CURRENTDATETIME;
        while (WordApplication.BackgroundPrintingStatus > 0) and (CURRENTDATETIME < T0 + 180000) do
          SLEEP(250);
        WordHelper.CallQuit(WordApplication,false);
        if DeleteClientFile(FileName) then;
      end else begin
        if (PrinterName <> '') and IsValidPrinter(PrinterName) then
          PrintWordDocOnServer(TempBlob,PrinterName,Collate) // Don't print on server if the printer has not been setup.
        else
          FileMgt.BLOBExport(TempBlob,UserFileName(ReportID,FileTypeWordTxt),true);
      end;
    END;

    [TryFunction]
    LOCAL PROCEDURE DeleteClientFile@20(FileName@1000 : Text);
    VAR
      FileMgt@1001 : Codeunit "File Management";
    BEGIN
      FileMgt.DeleteClientFile(FileName);
    END;

    LOCAL PROCEDURE IsValidPrinter@19(PrinterName@1000 : Text) : Boolean;
    VAR
      Printer@1001 : Record Printer;
    BEGIN
      Printer.SETFILTER(Name,PrinterName);
      Printer.FINDFIRST;
      exit(not Printer.ISEMPTY);
    END;

    [TryFunction]
    LOCAL PROCEDURE GetWordApplication@18(VAR WordApplication@1000 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.ApplicationClass" RUNONCLIENT);
    BEGIN
      WordApplication := WordApplication.ApplicationClass;
    END;

    LOCAL PROCEDURE PrintWordDocOnServer@16(TempBlob@1001 : Record TempBlob;PrinterName@1000 : Text;Collate@1004 : Boolean);
    VAR
      PdfWriter@1003 : DotNet "'Microsoft.Dynamics.Nav.PdfWriter, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.PdfWriter.WordToPdf";
      InStreamWordDoc@1002 : InStream;
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(InStreamWordDoc);
      PdfWriter.PrintWordDoc(InStreamWordDoc,PrinterName,Collate);
    END;

    LOCAL PROCEDURE UserFileName@6(ReportID@1000 : Integer;fileExtension@1003 : Text) : Text;
    VAR
      ReportMetadata@1001 : Record "Report Metadata";
      FileManagement@1004 : Codeunit "File Management";
    BEGIN
      ReportMetadata.GET(ReportID);
      if fileExtension = '' then
        fileExtension := FileTypeWordTxt;

      exit(FileManagement.GetSafeFileName(ReportMetadata.Caption) + '.' + fileExtension);
    END;

    [Internal]
    PROCEDURE ApplyUpgradeToReports@10(VAR ReportUpgradeCollection@1007 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.ReportUpgradeCollection";testOnly@1000 : Boolean) : Boolean;
    VAR
      CustomReportLayout@1005 : Record "Custom Report Layout";
      ReportUpgrade@1006 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.ReportUpgradeSet";
      ReportChangeLogCollection@1008 : DotNet "'Microsoft.Dynamics.Nav.Types.Report, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Types.Report.IReportChangeLogCollection";
    BEGIN
      foreach ReportUpgrade in ReportUpgradeCollection do begin
        CustomReportLayout.SETFILTER("Report ID",FORMAT(ReportUpgrade.ReportId));
        if CustomReportLayout.FIND('-') then
          repeat
            CustomReportLayout.ApplyUpgrade(ReportUpgrade,ReportChangeLogCollection,testOnly);
          until CustomReportLayout.NEXT = 0;
      end;

      if ISNULL(ReportChangeLogCollection) then begin // Don't break upgrade process with user information
        if (CURRENTCLIENTTYPE <> CLIENTTYPE::Background) and (CURRENTCLIENTTYPE <> CLIENTTYPE::Management) then
          MESSAGE(NoReportLayoutUpgradeRequiredMsg);

        exit(false);
      end;

      ProcessUpgradeLog(ReportChangeLogCollection);
      exit(ReportChangeLogCollection.Count > 0);
    END;

    [External]
    PROCEDURE CalculateUpgradeChangeSet@21(VAR ReportUpgradeCollection@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.ReportUpgradeCollection");
    VAR
      CustomReportLayout@1000 : Record "Custom Report Layout";
      ReportUpgradeSet@1001 : DotNet "'Microsoft.Dynamics.Nav.Types.Report, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Types.Report.IReportUpgradeSet";
    BEGIN
      if CustomReportLayout.FIND('-') then
        repeat
          ReportUpgradeSet := ReportUpgradeCollection.AddReport(CustomReportLayout."Report ID"); // runtime will load the current XmlPart from metadata
          if not ISNULL(ReportUpgradeSet) then
            ReportUpgradeSet.CalculateAutoChangeSet(CustomReportLayout.GetCustomXmlPart);
        until CustomReportLayout.NEXT <> 1;
    END;

    LOCAL PROCEDURE ProcessUpgradeLog@9(VAR ReportChangeLogCollection@1000 : DotNet "'Microsoft.Dynamics.Nav.Types.Report, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Types.Report.IReportChangeLogCollection");
    VAR
      ReportLayoutUpdateLog@1001 : Codeunit "Report Layout Update Log";
    BEGIN
      if ISNULL(ReportChangeLogCollection) then
        exit;

      if (CURRENTCLIENTTYPE <> CLIENTTYPE::Background) and (CURRENTCLIENTTYPE <> CLIENTTYPE::Management) then
        ReportLayoutUpdateLog.ViewLog(ReportChangeLogCollection)
      else
        MESSAGE(UpgradeMessageMsg,FORMAT(ReportChangeLogCollection));
    END;

    [Internal]
    PROCEDURE BulkUpgrade@14(testMode@1002 : Boolean);
    VAR
      ReportUpgradeCollection@1000 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.ReportUpgradeCollection";
    BEGIN
      ReportUpgradeCollection := ReportUpgradeCollection.ReportUpgradeCollection;
      CalculateUpgradeChangeSet(ReportUpgradeCollection);
      ApplyUpgradeToReports(ReportUpgradeCollection,testMode);
    END;

    BEGIN
    END.
  }
}

