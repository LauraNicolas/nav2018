OBJECT Page 397 Sales Invoice Statistics
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Editable=false;
    CaptionML=ENU=Sales Invoice Statistics;
    LinksAllowed=false;
    SourceTable="Sales Invoice Header";
    PageType=ListPlus;
    OnAfterGetRecord=VAR
                       CustLedgEntry@1000 : Record "Cust. Ledger Entry";
                       CostCalcMgt@1001 : Codeunit "Cost Calculation Management";
                     BEGIN
                       CLEARALL;

                       if "Currency Code" = '' then
                         currency.InitRoundingPrecision
                       else
                         currency.GET("Currency Code");

                       SalesInvLine.SETRANGE("Document No.","No.");
                       if SalesInvLine.FIND('-') then
                         repeat
                           CustAmount := CustAmount + SalesInvLine.Amount;
                           AmountInclVAT := AmountInclVAT + SalesInvLine."Amount Including VAT";
                           if "Prices Including VAT" then
                             InvDiscAmount := InvDiscAmount + SalesInvLine."Inv. Discount Amount" / (1 + SalesInvLine."VAT %" / 100)
                           else
                             InvDiscAmount := InvDiscAmount + SalesInvLine."Inv. Discount Amount";
                           CostLCY := CostLCY + (SalesInvLine.Quantity * SalesInvLine."Unit Cost (LCY)");
                           LineQty := LineQty + SalesInvLine.Quantity;
                           TotalNetWeight := TotalNetWeight + (SalesInvLine.Quantity * SalesInvLine."Net Weight");
                           TotalGrossWeight := TotalGrossWeight + (SalesInvLine.Quantity * SalesInvLine."Gross Weight");
                           TotalVolume := TotalVolume + (SalesInvLine.Quantity * SalesInvLine."Unit Volume");
                           if SalesInvLine."Units per Parcel" > 0 then
                             TotalParcels := TotalParcels + ROUND(SalesInvLine.Quantity / SalesInvLine."Units per Parcel",1,'>');
                           if SalesInvLine."VAT %" <> VATPercentage then
                             if VATPercentage = 0 then
                               VATPercentage := SalesInvLine."VAT %"
                             else
                               VATPercentage := -1;
                           TotalAdjCostLCY := TotalAdjCostLCY + CostCalcMgt.CalcSalesInvLineCostLCY(SalesInvLine);
                         until SalesInvLine.NEXT = 0;
                       VATAmount := AmountInclVAT - CustAmount;
                       InvDiscAmount := ROUND(InvDiscAmount,currency."Amount Rounding Precision");

                       if VATPercentage <= 0 then
                         VATAmountText := Text000
                       else
                         VATAmountText := STRSUBSTNO(Text001,VATPercentage);

                       if "Currency Code" = '' then
                         AmountLCY := CustAmount
                       else
                         AmountLCY :=
                           CurrExchRate.ExchangeAmtFCYToLCY(
                             WORKDATE,"Currency Code",CustAmount,"Currency Factor");

                       CustLedgEntry.SETCURRENTKEY("Document No.");
                       CustLedgEntry.SETRANGE("Document No.","No.");
                       CustLedgEntry.SETRANGE("Document Type",CustLedgEntry."Document Type"::Invoice);
                       CustLedgEntry.SETRANGE("Customer No.","Bill-to Customer No.");
                       if CustLedgEntry.FINDFIRST then
                         AmountLCY := CustLedgEntry."Sales (LCY)";

                       ProfitLCY := AmountLCY - CostLCY;
                       if AmountLCY <> 0 then
                         ProfitPct := ROUND(100 * ProfitLCY / AmountLCY,0.1);

                       AdjProfitLCY := AmountLCY - TotalAdjCostLCY;
                       if AmountLCY <> 0 then
                         AdjProfitPct := ROUND(100 * AdjProfitLCY / AmountLCY,0.1);

                       if Cust.GET("Bill-to Customer No.") then
                         Cust.CALCFIELDS("Balance (LCY)")
                       else
                         CLEAR(Cust);

                       case true of
                         Cust."Credit Limit (LCY)" = 0:
                           CreditLimitLCYExpendedPct := 0;
                         Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" < 0:
                           CreditLimitLCYExpendedPct := 0;
                         Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" > 1:
                           CreditLimitLCYExpendedPct := 10000;
                         else
                           CreditLimitLCYExpendedPct := ROUND(Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" * 10000,1);
                       end;

                       SalesInvLine.CalcVATAmountLines(Rec,TempVATAmountLine);
                       CurrPage.Subform.PAGE.SetTempVATAmountLine(TempVATAmountLine);
                       CurrPage.Subform.PAGE.InitGlobals("Currency Code",false,false,false,false,"VAT Base Discount %");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 36  ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General }

    { 2   ;2   ;Field     ;
                Name=Amount;
                CaptionML=ENU=Amount;
                ToolTipML=ENU=Specifies the net amount of all the lines in the sales document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CustAmount + InvDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 6   ;2   ;Field     ;
                Name=InvDiscAmount;
                CaptionML=ENU=Inv. Discount Amount;
                ToolTipML=ENU=Specifies the invoice discount amount for the sales document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=InvDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                ImplicitType=Decimal }

    { 12  ;2   ;Field     ;
                Name=CustAmount;
                CaptionML=ENU=Total;
                ToolTipML=ENU=Specifies the total amount, less any invoice discount amount, and excluding VAT for the sales document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CustAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                ImplicitType=Decimal }

    { 16  ;2   ;Field     ;
                Name=VATAmount;
                CaptionML=ENU=VAT Amount;
                ToolTipML=ENU=Specifies the total VAT amount that has been calculated for all the lines in the sales document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=VATAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT(VATAmountText);
                ImplicitType=Decimal }

    { 20  ;2   ;Field     ;
                Name=AmountInclVAT;
                CaptionML=ENU=Total Incl. VAT;
                ToolTipML=ENU=Specifies the total amount, including VAT, that will be posted to the customer's account for all the lines in the sales document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AmountInclVAT;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                ImplicitType=Decimal }

    { 26  ;2   ;Field     ;
                Name=AmountLCY;
                CaptionML=ENU=Sales (LCY);
                ToolTipML=ENU=Specifies your total sales turnover in the fiscal year.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AmountLCY;
                AutoFormatType=1;
                ImplicitType=Decimal }

    { 30  ;2   ;Field     ;
                Name=ProfitLCY;
                CaptionML=ENU=Original Profit (LCY);
                ToolTipML=ENU=Specifies the original profit that was associated with the sales when they were originally posted.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ProfitLCY;
                AutoFormatType=1;
                ImplicitType=Decimal }

    { 43  ;2   ;Field     ;
                Name=AdjProfitLCY;
                CaptionML=ENU=Adjusted Profit (LCY);
                ToolTipML=ENU=Specifies the profit, taking into consideration changes in the purchase prices of the goods.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=AdjProfitLCY;
                AutoFormatType=1;
                ImplicitType=Decimal }

    { 32  ;2   ;Field     ;
                Name=ProfitPct;
                CaptionML=ENU=Original Profit %;
                ToolTipML=ENU=Specifies the original percentage of profit that was associated with the sales when they were originally posted.;
                ApplicationArea=#Basic,#Suite;
                DecimalPlaces=1:1;
                SourceExpr=ProfitPct;
                ImplicitType=Decimal }

    { 41  ;2   ;Field     ;
                Name=AdjProfitPct;
                CaptionML=ENU=Adjusted Profit %;
                ToolTipML=ENU=Specifies the percentage of profit for all sales, including changes that occurred in the purchase prices of the goods.;
                ApplicationArea=#Basic,#Suite;
                DecimalPlaces=1:1;
                SourceExpr=AdjProfitPct;
                ImplicitType=Decimal }

    { 3   ;2   ;Field     ;
                Name=LineQty;
                CaptionML=ENU=Quantity;
                ToolTipML=ENU=Specifies the total quantity of G/L account entries, items and/or resources in the sales document.;
                ApplicationArea=#Basic,#Suite;
                DecimalPlaces=0:5;
                SourceExpr=LineQty;
                ImplicitType=Decimal }

    { 13  ;2   ;Field     ;
                Name=TotalParcels;
                CaptionML=ENU=Parcels;
                ToolTipML=ENU=Specifies the total number of parcels in the sales document.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalParcels;
                ImplicitType=Decimal }

    { 7   ;2   ;Field     ;
                Name=TotalNetWeight;
                CaptionML=ENU=Net Weight;
                ToolTipML=ENU=Specifies the total net weight of the items in the sales document.;
                ApplicationArea=#Basic,#Suite;
                DecimalPlaces=0:5;
                SourceExpr=TotalNetWeight;
                ImplicitType=Decimal }

    { 10  ;2   ;Field     ;
                Name=TotalGrossWeight;
                CaptionML=ENU=Gross Weight;
                ToolTipML=ENU=Specifies the total gross weight of the items in the sales document.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalGrossWeight;
                ImplicitType=Decimal }

    { 17  ;2   ;Field     ;
                Name=TotalVolume;
                CaptionML=ENU=Volume;
                ToolTipML=ENU=Specifies the total volume of the items in the sales document.;
                ApplicationArea=#Advanced;
                DecimalPlaces=0:5;
                SourceExpr=TotalVolume;
                ImplicitType=Decimal }

    { 28  ;2   ;Field     ;
                Name=CostLCY;
                CaptionML=ENU=Original Cost (LCY);
                ToolTipML=ENU=Specifies the total cost, in LCY, of the G/L account entries, items and/or resources in the sales document.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CostLCY;
                AutoFormatType=1;
                ImplicitType=Decimal }

    { 35  ;2   ;Field     ;
                Name=TotalAdjCostLCY;
                CaptionML=ENU=Adjusted Cost (LCY);
                ToolTipML=ENU=Specifies the total cost, in LCY, of the items in the posted sales invoice, adjusted for any changes in the original costs of these items.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TotalAdjCostLCY;
                AutoFormatType=1;
                ImplicitType=Decimal }

    { 39  ;2   ;Field     ;
                Name=Cost Adjmt. Amount (LCY);
                CaptionML=ENU=Cost Adjmt. Amount (LCY);
                ToolTipML=ENU=Specifies the difference between the original cost and the total adjusted cost of the items in the posted sales invoice.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TotalAdjCostLCY - CostLCY;
                AutoFormatType=1;
                OnLookup=BEGIN
                           LookupAdjmtValueEntries;
                         END;
                          }

    { 18  ;1   ;Part      ;
                Name=Subform;
                ApplicationArea=#Basic,#Suite;
                PagePartID="VAT Specification Subform";
                Editable=FALSE;
                PartType=Page }

    { 1903289601;1;Group  ;
                Name=Customer;
                CaptionML=ENU=Customer }

    { 24  ;2   ;Field     ;
                Name=Balance (LCY);
                CaptionML=ENU=Balance (LCY);
                ToolTipML=ENU=Specifies the balance in LCY on the customer's account.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Cust."Balance (LCY)";
                AutoFormatType=1 }

    { 21  ;2   ;Field     ;
                Name=Credit Limit (LCY);
                CaptionML=ENU=Credit Limit (LCY);
                ToolTipML=ENU="Specifies information about the credit limit in LCY, for the customer who you created and posted this sales invoice for. ";
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Cust."Credit Limit (LCY)";
                AutoFormatType=1 }

    { 37  ;2   ;Field     ;
                Name=CreditLimitLCYExpendedPct;
                ExtendedDatatype=Ratio;
                CaptionML=ENU=Expended % of Credit Limit (LCY);
                ToolTipML=ENU=Specifies the expended percentage of the credit limit in (LCY).;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CreditLimitLCYExpendedPct;
                ImplicitType=Decimal }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=VAT Amount';
      Text001@1001 : TextConst 'ENU=%1% VAT';
      CurrExchRate@1002 : Record "Currency Exchange Rate";
      SalesInvLine@1003 : Record "Sales Invoice Line";
      Cust@1004 : Record Customer;
      TempVATAmountLine@1005 : TEMPORARY Record "VAT Amount Line";
      currency@1022 : Record Currency;
      TotalAdjCostLCY@1023 : Decimal;
      CustAmount@1006 : Decimal;
      AmountInclVAT@1007 : Decimal;
      InvDiscAmount@1008 : Decimal;
      VATAmount@1009 : Decimal;
      CostLCY@1010 : Decimal;
      ProfitLCY@1011 : Decimal;
      ProfitPct@1012 : Decimal;
      AdjProfitLCY@1027 : Decimal;
      AdjProfitPct@1026 : Decimal;
      LineQty@1013 : Decimal;
      TotalNetWeight@1014 : Decimal;
      TotalGrossWeight@1015 : Decimal;
      TotalVolume@1016 : Decimal;
      TotalParcels@1017 : Decimal;
      AmountLCY@1018 : Decimal;
      CreditLimitLCYExpendedPct@1019 : Decimal;
      VATPercentage@1020 : Decimal;
      VATAmountText@1021 : Text[30];

    BEGIN
    END.
  }
}

