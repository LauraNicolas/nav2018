OBJECT Report 93 Create Fiscal Year
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Fiscal Year;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  AccountingPeriod."Starting Date" := FiscalYearStartDate;
                  AccountingPeriod.TESTFIELD("Starting Date");

                  if AccountingPeriod.FIND('-') then begin
                    FirstPeriodStartDate := AccountingPeriod."Starting Date";
                    FirstPeriodLocked := AccountingPeriod."Date Locked";
                    if (not HideDialog) and (FiscalYearStartDate < FirstPeriodStartDate) and FirstPeriodLocked then
                      if not CONFIRM(CreateAndCloseQst) then
                        exit;
                    if AccountingPeriod.FIND('+') then
                      LastPeriodStartDate := AccountingPeriod."Starting Date";
                  end else
                    if not HideDialog then
                      if not CONFIRM(CreateQst) then
                        exit;

                  FiscalYearStartDate2 := FiscalYearStartDate;

                  for i := 1 to NoOfPeriods + 1 do begin
                    if (FiscalYearStartDate <= FirstPeriodStartDate) and (i = NoOfPeriods + 1) then
                      exit;

                    if FirstPeriodStartDate <> 0D then
                      if (FiscalYearStartDate >= FirstPeriodStartDate) and (FiscalYearStartDate < LastPeriodStartDate) then
                        ERROR(OnlyBeforeOrAfterErr);
                    AccountingPeriod.INIT;
                    AccountingPeriod."Starting Date" := FiscalYearStartDate;
                    AccountingPeriod.VALIDATE("Starting Date");
                    if (i = 1) or (i = NoOfPeriods + 1) then begin
                      AccountingPeriod."New Fiscal Year" := true;
                      InvtSetup.GET;
                      AccountingPeriod."Average Cost Calc. Type" := InvtSetup."Average Cost Calc. Type";
                      AccountingPeriod."Average Cost Period" := InvtSetup."Average Cost Period";
                    end;
                    if (FirstPeriodStartDate = 0D) and (i = 1) then
                      AccountingPeriod."Date Locked" := true;
                    if (AccountingPeriod."Starting Date" < FirstPeriodStartDate) and FirstPeriodLocked then begin
                      AccountingPeriod.Closed := true;
                      AccountingPeriod."Date Locked" := true;
                    end;
                    if not AccountingPeriod.FIND('=') then
                      AccountingPeriod.INSERT;
                    FiscalYearStartDate := CALCDATE(PeriodLength,FiscalYearStartDate);
                  end;

                  AccountingPeriod.GET(FiscalYearStartDate2);
                  AccountingPeriod.UpdateAvgItems(0);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnOpenPage=BEGIN
                   if NoOfPeriods = 0 then begin
                     NoOfPeriods := 12;
                     EVALUATE(PeriodLength,'<1M>');
                   end;
                   if AccountingPeriod.FIND('+') then
                     FiscalYearStartDate := AccountingPeriod."Starting Date";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=ENU=Starting Date;
                  ToolTipML=ENU=Specifies the date from which the report or batch job processes information.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=FiscalYearStartDate }

      { 2   ;2   ;Field     ;
                  Name=NoOfPeriods;
                  CaptionML=ENU=No. of Periods;
                  ToolTipML=ENU=Specifies how many accounting periods to include.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=NoOfPeriods }

      { 3   ;2   ;Field     ;
                  Name=PeriodLength;
                  CaptionML=ENU=Period Length;
                  ToolTipML=ENU=Specifies the period for which data is shown in the report. For example, enter "1M" for one month, "30D" for thirty days, "3Q" for three quarters, or "5Y" for five years.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PeriodLength }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CreateAndCloseQst@1000 : TextConst 'ENU=The new fiscal year begins before an existing fiscal year, so the new year will be closed automatically.\\Do you want to create and close the fiscal year?';
      CreateQst@1002 : TextConst 'ENU=After you create the new fiscal year, you cannot change its starting date.\\Do you want to create the fiscal year?';
      OnlyBeforeOrAfterErr@1004 : TextConst 'ENU=It is only possible to create new fiscal years before or after the existing ones.';
      AccountingPeriod@1005 : Record "Accounting Period";
      InvtSetup@1013 : Record "Inventory Setup";
      NoOfPeriods@1006 : Integer;
      PeriodLength@1007 : DateFormula;
      FiscalYearStartDate@1008 : Date;
      FiscalYearStartDate2@1015 : Date;
      FirstPeriodStartDate@1009 : Date;
      LastPeriodStartDate@1010 : Date;
      FirstPeriodLocked@1011 : Boolean;
      i@1012 : Integer;
      HideDialog@1014 : Boolean;

    [External]
    PROCEDURE InitializeRequest@1(NewNoOfPeriods@1000 : Integer;NewPeriodLength@1001 : DateFormula;StartingDate@1002 : Date);
    BEGIN
      NoOfPeriods := NewNoOfPeriods;
      PeriodLength := NewPeriodLength;
      if AccountingPeriod.FINDLAST then
        FiscalYearStartDate := AccountingPeriod."Starting Date"
      else
        FiscalYearStartDate := StartingDate;
    END;

    [External]
    PROCEDURE HideConfirmationDialog@2(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

