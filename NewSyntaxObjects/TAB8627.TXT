OBJECT Table 8627 Config. Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Config. Setup;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Name                ;Text50        ;CaptionML=ENU=Name }
    { 3   ;   ;Name 2              ;Text50        ;CaptionML=ENU=Name 2 }
    { 4   ;   ;Address             ;Text50        ;CaptionML=ENU=Address }
    { 5   ;   ;Address 2           ;Text50        ;CaptionML=ENU=Address 2 }
    { 6   ;   ;City                ;Text30        ;CaptionML=ENU=City }
    { 7   ;   ;Phone No.           ;Text30        ;CaptionML=ENU=Phone No. }
    { 8   ;   ;Phone No. 2         ;Text30        ;CaptionML=ENU=Phone No. 2 }
    { 9   ;   ;Telex No.           ;Text30        ;CaptionML=ENU=Telex No. }
    { 10  ;   ;Fax No.             ;Text30        ;CaptionML=ENU=Fax No. }
    { 11  ;   ;Giro No.            ;Text20        ;CaptionML=ENU=Giro No. }
    { 12  ;   ;Bank Name           ;Text50        ;AccessByPermission=TableData "Bank Account"=R;
                                                   CaptionML=ENU=Bank Name }
    { 13  ;   ;Bank Branch No.     ;Text20        ;AccessByPermission=TableData "Bank Account"=R;
                                                   CaptionML=ENU=Bank Branch No. }
    { 14  ;   ;Bank Account No.    ;Text30        ;AccessByPermission=TableData "Bank Account"=R;
                                                   CaptionML=ENU=Bank Account No. }
    { 15  ;   ;Payment Routing No. ;Text20        ;CaptionML=ENU=Payment Routing No. }
    { 17  ;   ;Customs Permit No.  ;Text10        ;CaptionML=ENU=Customs Permit No. }
    { 18  ;   ;Customs Permit Date ;Date          ;CaptionML=ENU=Customs Permit Date }
    { 19  ;   ;VAT Registration No.;Text20        ;CaptionML=ENU=VAT Registration No. }
    { 20  ;   ;Registration No.    ;Text20        ;CaptionML=ENU=Registration No. }
    { 21  ;   ;Telex Answer Back   ;Text20        ;CaptionML=ENU=Telex Answer Back }
    { 22  ;   ;Ship-to Name        ;Text50        ;CaptionML=ENU=Ship-to Name }
    { 23  ;   ;Ship-to Name 2      ;Text50        ;CaptionML=ENU=Ship-to Name 2 }
    { 24  ;   ;Ship-to Address     ;Text50        ;CaptionML=ENU=Ship-to Address }
    { 25  ;   ;Ship-to Address 2   ;Text50        ;CaptionML=ENU=Ship-to Address 2 }
    { 26  ;   ;Ship-to City        ;Text30        ;CaptionML=ENU=Ship-to City }
    { 27  ;   ;Ship-to Contact     ;Text50        ;CaptionML=ENU=Ship-to Contact }
    { 28  ;   ;Location Code       ;Code10        ;CaptionML=ENU=Location Code }
    { 29  ;   ;Picture             ;BLOB          ;CaptionML=ENU=Picture;
                                                   SubType=Bitmap }
    { 30  ;   ;Post Code           ;Code20        ;CaptionML=ENU=Post Code }
    { 31  ;   ;County              ;Text30        ;CaptionML=ENU=County }
    { 32  ;   ;Ship-to Post Code   ;Code20        ;CaptionML=ENU=Ship-to Post Code }
    { 33  ;   ;Ship-to County      ;Text30        ;CaptionML=ENU=Ship-to County }
    { 34  ;   ;E-Mail              ;Text80        ;CaptionML=ENU=Email }
    { 35  ;   ;Home Page           ;Text80        ;CaptionML=ENU=Home Page }
    { 36  ;   ;Country/Region Code ;Code10        ;CaptionML=ENU=Country/Region Code }
    { 37  ;   ;Ship-to Country/Region Code;Code10 ;CaptionML=ENU=Ship-to Country/Region Code }
    { 38  ;   ;IBAN                ;Code50        ;OnValidate=VAR
                                                                CompanyInfo@1000 : Record "Company Information";
                                                              BEGIN
                                                                CompanyInfo.CheckIBAN(IBAN);
                                                              END;

                                                   CaptionML=ENU=IBAN }
    { 39  ;   ;SWIFT Code          ;Code20        ;CaptionML=ENU=SWIFT Code }
    { 40  ;   ;Industrial Classification;Text30   ;CaptionML=ENU=Industrial Classification }
    { 500 ;   ;Logo Position on Documents;Option  ;CaptionML=ENU=Logo Position on Documents;
                                                   OptionCaptionML=ENU=No Logo,Left,Center,Right;
                                                   OptionString="No Logo",Left,Center,Right }
    { 5700;   ;Responsibility Center;Code10       ;CaptionML=ENU=Responsibility Center }
    { 5791;   ;Check-Avail. Period Calc.;DateFormula;
                                                   CaptionML=ENU=Check-Avail. Period Calc. }
    { 5792;   ;Check-Avail. Time Bucket;Option    ;CaptionML=ENU=Check-Avail. Time Bucket;
                                                   OptionCaptionML=ENU=Day,Week,Month,Quarter,Year;
                                                   OptionString=Day,Week,Month,Quarter,Year }
    { 7600;   ;Base Calendar Code  ;Code10        ;CaptionML=ENU=Base Calendar Code }
    { 7601;   ;Cal. Convergence Time Frame;DateFormula;
                                                   InitValue=1Y;
                                                   CaptionML=ENU=Cal. Convergence Time Frame }
    { 8600;   ;Package File Name   ;Text250       ;OnValidate=BEGIN
                                                                ReadPackageHeader(DecompressPackage(true));
                                                              END;

                                                   CaptionML=ENU=Package File Name }
    { 8601;   ;Package Code        ;Code20        ;CaptionML=ENU=Package Code;
                                                   Editable=false }
    { 8602;   ;Language ID         ;Integer       ;TableRelation="Windows Language";
                                                   CaptionML=ENU=Language ID;
                                                   Editable=false }
    { 8603;   ;Product Version     ;Text80        ;CaptionML=ENU=Product Version;
                                                   Editable=false }
    { 8604;   ;Package Name        ;Text50        ;CaptionML=ENU=Package Name }
    { 8605;   ;Your Profile Code   ;Code30        ;TableRelation="All Profile"."Profile ID";
                                                   CaptionML=ENU=Your Profile Code }
    { 8606;   ;Your Profile App ID ;GUID          ;CaptionML=ENU=Your Profile App ID }
    { 8607;   ;Your Profile Scope  ;Option        ;CaptionML=ENU=Your Profile Scope;
                                                   OptionCaptionML=ENU=System,Tenant;
                                                   OptionString=System,Tenant }
  }
  KEYS
  {
    {    ;"Primary Key"                           ;Clustered=true }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PackageAlreadyExistsQst@1003 : TextConst 'ENU=The imported package already exists. Do you want to import another package?';
      HideDialog@1000 : Boolean;
      PackageDataNotDefinedErr@1001 : TextConst '@@@="%1 = ""Package Code""";ENU=%1 should be defined in the imported package.';

    [Internal]
    PROCEDURE CompleteWizard@1() : Boolean;
    VAR
      Scope@1000 : 'System,Tenant';
      AppID@1001 : GUID;
    BEGIN
      TESTFIELD("Package File Name");
      TESTFIELD("Package Code");
      TESTFIELD("Package Name");

      ImportPackage(DecompressPackage(true));
      ApplyPackages;
      ApplyAnswers;
      CopyCompInfo;
      SelectDefaultRoleCenter("Your Profile Code",AppID,Scope::System);
      exit(true);
    END;

    [External]
    PROCEDURE SelectDefaultRoleCenter@8(ProfileID@1000 : Code[30];AppID@1003 : GUID;Scope@1004 : 'System,Tenant');
    VAR
      Profile@1001 : Record "All Profile";
      ConfPersonalizationMgt@1002 : Codeunit "Conf./Personalization Mgt.";
    BEGIN
      if Profile.GET(Scope,AppID,ProfileID) then begin
        Profile.VALIDATE("Default Role Center",true);
        Profile.MODIFY;
        ConfPersonalizationMgt.ChangeDefaultRoleCenter(Profile);
      end;
    END;

    [Internal]
    PROCEDURE ReadPackageHeader@7(DecompressedFileName@1005 : Text);
    VAR
      ConfigPackage@1003 : Record "Config. Package";
      ConfigXMLExchange@1002 : Codeunit "Config. XML Exchange";
      XMLDOMManagement@1004 : Codeunit "XML DOM Management";
      PackageXML@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      DocumentElement@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      LanguageID@1006 : Text;
    BEGIN
      if "Package File Name" <> '' then begin
        XMLDOMManagement.LoadXMLDocumentFromFile(DecompressedFileName,PackageXML);
        DocumentElement := PackageXML.DocumentElement;
        "Package Code" :=
          COPYSTR(
            ConfigXMLExchange.GetAttribute(
              ConfigXMLExchange.GetElementName(ConfigPackage.FIELDNAME(Code)),DocumentElement),
            1,MAXSTRLEN("Package Code"));
        if "Package Code" = '' then
          ERROR(PackageDataNotDefinedErr,FIELDCAPTION("Package Code"));
        "Package Name" :=
          COPYSTR(
            ConfigXMLExchange.GetAttribute(
              ConfigXMLExchange.GetElementName(ConfigPackage.FIELDNAME("Package Name")),DocumentElement),
            1,MAXSTRLEN("Package Name"));
        if "Package Name" = '' then
          ERROR(PackageDataNotDefinedErr,FIELDCAPTION("Package Name"));
        "Product Version" :=
          COPYSTR(
            ConfigXMLExchange.GetAttribute(
              ConfigXMLExchange.GetElementName(ConfigPackage.FIELDNAME("Product Version")),DocumentElement),
            1,MAXSTRLEN("Product Version"));
        LanguageID := ConfigXMLExchange.GetAttribute(
            ConfigXMLExchange.GetElementName(ConfigPackage.FIELDNAME("Language ID")),DocumentElement);
        if LanguageID <> '' then
          EVALUATE("Language ID",LanguageID);
        MODIFY;
      end else begin
        "Package Code" := '';
        "Package Name" := '';
        "Product Version" := '';
        "Language ID" := 0;
      end;
    END;

    [Internal]
    PROCEDURE ImportPackage@4(DecompressedFileName@1005 : Text);
    VAR
      ConfigPackage@1000 : Record "Config. Package";
      ConfigXMLExchange@1001 : Codeunit "Config. XML Exchange";
    BEGIN
      if ConfigPackage.GET("Package Code") then
        if not HideDialog then
          if CONFIRM(PackageAlreadyExistsQst,true) then begin
            ConfigPackage.DELETE(true);
            COMMIT;
          end else
            ERROR('');

      ConfigXMLExchange.SetHideDialog(HideDialog);
      ConfigXMLExchange.ImportPackageXML(DecompressedFileName);
      COMMIT;
    END;

    [External]
    PROCEDURE ApplyPackages@6() ErrorCount : Integer;
    VAR
      ConfigPackage@1003 : Record "Config. Package";
      ConfigPackageTable@1002 : Record "Config. Package Table";
      ConfigPackageMgt@1001 : Codeunit "Config. Package Management";
    BEGIN
      ConfigPackage.GET("Package Code");
      ConfigPackageTable.SETRANGE("Package Code",ConfigPackage.Code);
      ConfigPackageMgt.SetHideDialog(HideDialog);
      ErrorCount := ConfigPackageMgt.ApplyPackage(ConfigPackage,ConfigPackageTable,true);
    END;

    [Internal]
    PROCEDURE ApplyAnswers@9();
    VAR
      ConfigQuestionnaire@1000 : Record "Config. Questionnaire";
      ConfigQuestionnaireMgt@1001 : Codeunit "Questionnaire Management";
    BEGIN
      if ConfigQuestionnaire.FINDSET then
        repeat
          ConfigQuestionnaireMgt.ApplyAnswers(ConfigQuestionnaire);
        until ConfigQuestionnaire.NEXT = 0;
    END;

    [External]
    PROCEDURE CopyCompInfo@5();
    VAR
      CompanyInfo@1000 : Record "Company Information";
      SalesReceivablesSetup@1001 : Record "Sales & Receivables Setup";
    BEGIN
      if not CompanyInfo.GET then begin
        CompanyInfo.INIT;
        CompanyInfo.INSERT;
      end;
      CompanyInfo.TRANSFERFIELDS(Rec);
      CompanyInfo.MODIFY;

      if not SalesReceivablesSetup.GET then begin
        SalesReceivablesSetup.INIT;
        SalesReceivablesSetup.INSERT;
      end;
      SalesReceivablesSetup."Logo Position on Documents" := "Logo Position on Documents";
      SalesReceivablesSetup.MODIFY;

      COMMIT;
    END;

    [External]
    PROCEDURE SetHideDialog@48(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    [Internal]
    PROCEDURE DecompressPackage@2(UploadToServer@1003 : Boolean) DecompressedFileName : Text;
    VAR
      ConfigXMLExchange@1002 : Codeunit "Config. XML Exchange";
      FileMgt@1001 : Codeunit "File Management";
    BEGIN
      if UploadToServer then
        DecompressedFileName := ConfigXMLExchange.DecompressPackage(FileMgt.UploadFileSilent("Package File Name"))
      else
        DecompressedFileName := ConfigXMLExchange.DecompressPackage("Package File Name");
    END;

    BEGIN
    END.
  }
}

