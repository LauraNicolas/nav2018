OBJECT Page 1808 Data Migration Wizard
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Data Migration;
    InsertAllowed=false;
    DeleteAllowed=false;
    LinksAllowed=false;
    SourceTable="Data Migrator Registration";
    PageType=NavigatePage;
    SourceTableTemporary=true;
    ShowFilter=false;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=VAR
                 DataMigrationMgt@1000 : Codeunit "Data Migration Mgt.";
               BEGIN
                 OnRegisterDataMigrator;
                 if FINDFIRST then;
                 ResetWizardControls;
                 ShowIntroStep;
                 DataMigrationMgt.CheckMigrationInProgress;
               END;

    OnQueryClosePage=VAR
                       AssistedSetup@1000 : Record "Assisted Setup";
                     BEGIN
                       if CloseAction = ACTION::OK then
                         if AssistedSetup.GetStatus(PAGE::"Data Migration Wizard") = AssistedSetup.Status::"Not Completed" then
                           if not CONFIRM(DataImportNotCompletedQst,false) then
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 4       ;    ;ActionContainer;
                      Name=ActionContainer4;
                      ActionContainerType=ActionItems }
      { 26      ;1   ;Action    ;
                      Name=ActionOpenExtensionManagement;
                      CaptionML=ENU=Open Extension Management;
                      ApplicationArea=#Basic,#Suite;
                      RunObject=Page "Extension Management";
                      Visible=Step = Step::Intro;
                      InFooterBar=true;
                      Image=Setup }
      { 21      ;1   ;Action    ;
                      Name=ActionDownloadTemplate;
                      CaptionML=ENU=Download Template;
                      ApplicationArea=#Basic,#Suite;
                      Visible=DownloadTemplateVisible AND (Step = Step::Import);
                      InFooterBar=true;
                      Image=Table;
                      OnAction=VAR
                                 Handled@1000 : Boolean;
                               BEGIN
                                 OnDownloadTemplate(Handled);
                                 if not Handled then
                                   ERROR('');
                               END;
                                }
      { 18      ;1   ;Action    ;
                      Name=ActionDataMigrationSettings;
                      CaptionML=ENU=Settings;
                      ApplicationArea=#Basic,#Suite;
                      Visible=DataMigrationSettingsVisible AND (Step = Step::Import);
                      InFooterBar=true;
                      Image=Setup;
                      OnAction=VAR
                                 Handled@1000 : Boolean;
                               BEGIN
                                 OnOpenSettings(Handled);
                                 if not Handled then
                                   ERROR('');
                               END;
                                }
      { 23      ;1   ;Action    ;
                      Name=ActionOpenAdvancedApply;
                      CaptionML=ENU=Advanced;
                      ApplicationArea=#Basic,#Suite;
                      Visible=OpenAdvancedApplyVisible AND (Step = Step::Apply);
                      InFooterBar=true;
                      Image=Apply;
                      OnAction=VAR
                                 Handled@1000 : Boolean;
                               BEGIN
                                 OnOpenAdvancedApply(TempDataMigrationEntity,Handled);
                                 CurrPage.DataMigrationEntities.PAGE.CopyToSourceTable(TempDataMigrationEntity);
                                 if not Handled then
                                   ERROR('');
                               END;
                                }
      { 24      ;1   ;Action    ;
                      Name=ActionShowErrors;
                      CaptionML=ENU=Show Errors;
                      ApplicationArea=#Basic,#Suite;
                      Visible=ShowErrorsVisible AND ((Step = Step::Done) OR (Step = Step::ShowPostingGroupDoneStep));
                      InFooterBar=true;
                      Image=ErrorLog;
                      OnAction=VAR
                                 Handled@1000 : Boolean;
                               BEGIN
                                 OnShowErrors(Handled);
                                 if not Handled then
                                   ERROR('');
                               END;
                                }
      { 22      ;1   ;Separator ;
                      Name=Separator22 }
      { 3       ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=ENU=Back;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=true;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 case Step of
                                   Step::Apply:
                                     TempDataMigrationEntity.DELETEALL;
                                 end;
                                 NextStep(true);
                               END;
                                }
      { 2       ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=ENU=Next;
                      ApplicationArea=#Basic,#Suite;
                      Visible=NOT ApplyButtonVisible;
                      Enabled=NextEnabled;
                      InFooterBar=true;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextAction;
                               END;
                                }
      { 61      ;1   ;Action    ;
                      Name=ActionApply;
                      CaptionML=ENU=Migrate;
                      ApplicationArea=#Basic,#Suite;
                      Visible=ApplyButtonVisible;
                      Enabled=ApplyButtonEnabled;
                      InFooterBar=true;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextAction;
                               END;
                                }
      { 1       ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=ENU=Finish;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=true;
                      Image=Approve;
                      OnAction=VAR
                                 AssistedSetup@1001 : Record "Assisted Setup";
                               BEGIN
                                 AssistedSetup.SetStatus(PAGE::"Data Migration Wizard",AssistedSetup.Status::Completed);
                                 CurrPage.CLOSE;
                                 if ShowOverviewPage then
                                   PAGE.RUN(PAGE::"Data Migration Overview");
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                Name=Container1900000001;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Name=Group96;
                Visible=TopBannerVisible AND NOT DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                Name=MediaResourcesStandard;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=false }

    { 98  ;1   ;Group     ;
                Name=Group98;
                Visible=TopBannerVisible AND DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                Name=MediaResourcesDone;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=false }

    { 8   ;1   ;Group     ;
                Name=Group8;
                Visible=IntroVisible;
                GroupType=Group }

    { 5   ;2   ;Group     ;
                Name=Welcome to Data Migration assisted setup guide;
                CaptionML=ENU=Welcome to Data Migration assisted setup guide;
                GroupType=Group;
                InstructionalTextML=ENU=You can import data from other finance solutions and other data sources, provided that the corresponding extension is available to handle the conversion. To see a list of available extensions, choose the Open Extension Management button. }

    { 11  ;2   ;Group     ;
                Name=Let's go!;
                CaptionML=ENU=Let's go!;
                GroupType=Group;
                InstructionalTextML=ENU=Choose Next to choose your data source. }

    { 56  ;1   ;Group     ;
                Name=Group56;
                Visible=ChooseSourceVisible;
                GroupType=Group }

    { 6   ;2   ;Group     ;
                Name=Choose your data source;
                CaptionML=ENU=Choose your data source;
                GroupType=Group;
                InstructionalTextML=ENU=Which finance app do you want to migrate data from? }

    { 7   ;3   ;Field     ;
                Name=Description;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                TableRelation="Data Migrator Registration".Description;
                OnLookup=BEGIN
                           if PAGE.RUNMODAL(PAGE::"Data Migrators",Rec) = ACTION::LookupOK then begin
                             Text := Description;
                             CLEAR(DataMigrationSettingsVisible);
                             OnHasSettings(DataMigrationSettingsVisible);
                             exit;
                           end;
                         END;

                ImplicitType=Text250;
                ShowCaption=false }

    { 12  ;1   ;Group     ;
                Name=Group12;
                Visible=ImportVisible;
                GroupType=Group }

    { 10  ;2   ;Group     ;
                Name=Instructions;
                CaptionML=ENU=Instructions;
                Visible=Instructions <> '';
                GroupType=Group;
                InstructionalTextML=ENU=To prepare the data for migration, follow these steps: }

    { 15  ;3   ;Field     ;
                Name=Field15;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Instructions;
                Editable=FALSE;
                MultiLine=true;
                ImplicitType=Text;
                ShowCaption=false }

    { 20  ;2   ;Group     ;
                Name=Settings;
                CaptionML=ENU=Settings;
                Visible=DataMigrationSettingsVisible;
                GroupType=Group;
                InstructionalTextML=ENU=You can change the import settings for this data source by choosing Settings in the actions below. }

    { 14  ;1   ;Group     ;
                Name=Group14;
                Visible=ApplyVisible;
                GroupType=Group }

    { 19  ;2   ;Part      ;
                Name=DataMigrationEntities;
                CaptionML=ENU=Choose the data to migrate;
                ApplicationArea=#Basic,#Suite;
                PagePartID="Data Migration Entities";
                PartType=Page }

    { 13  ;2   ;Group     ;
                Name=Group13;
                Visible=ShowPostingOptions;
                GroupType=Group }

    { 57  ;3   ;Group     ;
                Name=Group57;
                GroupType=GridLayout;
                Layout=Columns }

    { 17  ;4   ;Field     ;
                Name=BallancesPostingOption;
                CaptionML=ENU=Opening balances;
                ToolTipML=ENU=Specifies what to do with opening balances. We can post them for you, or you can review balances in journals and post them yourself.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=BallancesPostingOption;
                OnValidate=BEGIN
                             SetPosting;
                           END;

                ImplicitType=Option;
                ShowMandatory=TRUE }

    { 60  ;4   ;Group     ;
                Name=Group60;
                Visible=BallancesPostingOption = BallancesPostingOption::"Post balances for me";
                GroupType=Group }

    { 55  ;5   ;Field     ;
                Name=PostingDate;
                CaptionML=ENU=Post to ledger on;
                ToolTipML=ENU=Specifies the date to post the journal on.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PostingDate;
                OnValidate=BEGIN
                             SetPosting;
                           END;

                ImplicitType=Date }

    { 49  ;1   ;Group     ;
                Name=POSTING GROUP SETUP;
                CaptionML=ENU=POSTING GROUP SETUP;
                Visible=PostingGroupIntroVisible;
                GroupType=Group }

    { 48  ;2   ;Group     ;
                Name=Welcome to Posting Group Setup;
                CaptionML=ENU=Welcome to Posting Group Setup;
                GroupType=Group;
                InstructionalTextML=ENU=For posting accounts, you can specify the general ledger accounts that you want to post sales and purchase transactions to. }

    { 47  ;2   ;Group     ;
                Name=Group47;
                CaptionML=ENU=Let's go!;
                GroupType=Group;
                InstructionalTextML=ENU=Choose Next to create posting accounts for purchasing and sales transactions. }

    { 46  ;1   ;Group     ;
                Name=Group46;
                Visible=FirstAccountSetupVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Select the accounts to use when posting. }

    { 45  ;2   ;Field     ;
                Name=Sales Account;
                CaptionML=ENU=Sales Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 44  ;2   ;Field     ;
                Name=Sales Credit Memo Account;
                CaptionML=ENU=Sales Credit Memo Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesCreditMemoAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 43  ;2   ;Field     ;
                Name=Sales Line Disc. Account;
                CaptionML=ENU=Sales Line Disc. Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesLineDiscAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 42  ;2   ;Field     ;
                Name=Sales Inv. Disc. Account;
                CaptionML=ENU=Sales Inv. Disc. Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesInvDiscAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 41  ;2   ;Field     ;
                Name=.;
                ApplicationArea=#Basic,#Suite;
                Editable=FALSE;
                HideValue=TRUE;
                ShowCaption=false }

    { 40  ;2   ;Field     ;
                Name=Purch. Account;
                CaptionML=ENU=Purch. Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PurchAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 39  ;2   ;Field     ;
                Name=Purch. Credit Memo Account;
                CaptionML=ENU=Purch. Credit Memo Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PurchCreditMemoAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 38  ;2   ;Field     ;
                Name=Purch. Line Disc. Account;
                CaptionML=ENU=Purch. Line Disc. Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PurchLineDiscAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 37  ;2   ;Field     ;
                Name=Purch. Inv. Disc. Account;
                CaptionML=ENU=Purch. Inv. Disc. Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PurchInvDiscAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 36  ;2   ;Field     ;
                Name=..;
                ApplicationArea=#Basic,#Suite;
                Editable=FALSE;
                HideValue=TRUE;
                ShowCaption=false }

    { 50  ;2   ;Group     ;
                Name=Group50;
                Visible=FirstAccountSetupVisible;
                GroupType=Group;
                InstructionalTextML=ENU=When importing items, the following accounts need to be entered }

    { 35  ;2   ;Field     ;
                Name=COGS Account;
                CaptionML=ENU=COGS Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=COGSAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 34  ;2   ;Field     ;
                Name=Inventory Adjmt. Account;
                CaptionML=ENU=Inventory Adjmt. Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=InventoryAdjmtAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 1808;2   ;Field     ;
                Name=Inventory Account;
                CaptionML=ENU=Inventory Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=InventoryAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 33  ;1   ;Group     ;
                Name=Group33;
                Visible=SecondAccountSetupVisible;
                GroupType=Group;
                InstructionalTextML=ENU=Select the accounts to use when posting. }

    { 32  ;2   ;Group     ;
                Name=Group32;
                GroupType=Group;
                InstructionalTextML=ENU=Customers }

    { 31  ;2   ;Field     ;
                Name=Receivables Account;
                CaptionML=ENU=Receivables Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ReceivablesAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 30  ;2   ;Field     ;
                Name=Service Charge Acc.;
                CaptionML=ENU=Service Charge Acc.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ServiceChargeAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 29  ;2   ;Group     ;
                Name=Group29;
                GroupType=Group;
                InstructionalTextML=ENU=Vendors }

    { 28  ;2   ;Field     ;
                Name=Payables Account;
                CaptionML=ENU=Payables Account;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PayablesAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 27  ;2   ;Field     ;
                Name=Purch. Service Charge Acc.;
                CaptionML=ENU=Purch. Service Charge Acc.;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=PurchServiceChargeAccount;
                TableRelation="G/L Account"."No.";
                ImplicitType=Code20 }

    { 9   ;1   ;Group     ;
                Name=Group9;
                Visible=DoneVisible;
                GroupType=Group }

    { 16  ;2   ;Group     ;
                Name=That's it!;
                CaptionML=ENU=That's it!;
                Visible=NOT ShowErrorsVisible;
                GroupType=Group;
                InstructionalTextML=ENU=You have completed the Data Migration assisted setup guide. }

    { 58  ;2   ;Group     ;
                Name=Group58;
                Visible=ThatsItText <> '';
                GroupType=Group }

    { 59  ;3   ;Field     ;
                Name=ThatsItText;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ThatsItText;
                Editable=FALSE;
                MultiLine=true;
                ImplicitType=Text;
                ShowCaption=false }

    { 62  ;3   ;Group     ;
                Name=Open the Data Migration Overview page when finished:;
                CaptionML=ENU=Open the Data Migration Overview page when finished:;
                Visible=EnableTogglingOverviewPage;
                GroupType=Group }

    { 51  ;4   ;Field     ;
                Name=ShowOverviewPage;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ShowOverviewPage;
                ImplicitType=Boolean;
                ShowCaption=false }

    { 53  ;2   ;Group     ;
                Name=Duplicate contacts?;
                CaptionML=ENU=Duplicate contacts?;
                Visible=ShowDuplicateContactsText;
                GroupType=Group;
                InstructionalTextML=ENU=We found some contacts that were duplicated. You can review the list, and decide what to do with them. }

    { 54  ;3   ;Field     ;
                Name=DuplicateContacts;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DuplicateContactsLbl;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              PAGE.RUN(PAGE::"Contact Duplicates");
                            END;

                ImplicitType=TextConst;
                ShowCaption=false }

    { 25  ;2   ;Group     ;
                Name=Import completed with errors;
                CaptionML=ENU=Import completed with errors;
                Visible=ShowErrorsVisible;
                GroupType=Group;
                InstructionalTextML=ENU=There were errors during import of your data. For more details, choose Show Errors in the actions below. }

  }
  CODE
  {
    VAR
      TempDataMigrationEntity@1010 : TEMPORARY Record "Data Migration Entity";
      MediaRepositoryStandard@1040 : Record "Media Repository";
      MediaRepositoryDone@1041 : Record "Media Repository";
      MediaResourcesStandard@1048 : Record "Media Resources";
      MediaResourcesDone@1049 : Record "Media Resources";
      Step@1012 : 'Intro,ChooseSource,Import,Apply,Done,PostingGroupIntro,AccountSetup1,AccountSetup2,ShowPostingGroupDoneStep';
      BallancesPostingOption@1051 : '" ","Post balances for me","Review balances first"';
      BackEnabled@1003 : Boolean;
      NextEnabled@1004 : Boolean;
      ApplyButtonVisible@1053 : Boolean;
      ApplyButtonEnabled@1046 : Boolean;
      FinishEnabled@1005 : Boolean;
      TopBannerVisible@1042 : Boolean;
      IntroVisible@1006 : Boolean;
      ChooseSourceVisible@1000 : Boolean;
      ImportVisible@1007 : Boolean;
      ApplyVisible@1008 : Boolean;
      DoneVisible@1011 : Boolean;
      DataImportNotCompletedQst@1001 : TextConst 'ENU=Data Migration has not been completed.\\Are you sure that you want to exit?';
      DownloadTemplateVisible@1002 : Boolean;
      DataMigrationSettingsVisible@1013 : Boolean;
      OpenAdvancedApplyVisible@1014 : Boolean;
      ShowErrorsVisible@1015 : Boolean;
      PostingGroupIntroVisible@1019 : Boolean;
      FirstAccountSetupVisible@1018 : Boolean;
      SecondAccountSetupVisible@1017 : Boolean;
      AccountSetupVisible@1034 : Boolean;
      ShowPostingOptions@1044 : Boolean;
      ShowDuplicateContactsText@1047 : Boolean;
      Instructions@1009 : Text;
      ThatsItText@1050 : Text;
      TotalNoOfMigrationRecords@1016 : Integer;
      SalesAccount@1033 : Code[20];
      SalesCreditMemoAccount@1032 : Code[20];
      SalesLineDiscAccount@1031 : Code[20];
      SalesInvDiscAccount@1030 : Code[20];
      PurchAccount@1029 : Code[20];
      PurchCreditMemoAccount@1028 : Code[20];
      PurchLineDiscAccount@1027 : Code[20];
      PurchInvDiscAccount@1026 : Code[20];
      COGSAccount@1025 : Code[20];
      InventoryAdjmtAccount@1024 : Code[20];
      InventoryAccount@1035 : Code[20];
      ReceivablesAccount@1023 : Code[20];
      ServiceChargeAccount@1022 : Code[20];
      PayablesAccount@1021 : Code[20];
      PurchServiceChargeAccount@1020 : Code[20];
      PostingDate@1045 : Date;
      DuplicateContactsLbl@1039 : TextConst 'ENU=Review duplicate contacts';
      BallancesPostingErr@1052 : TextConst 'ENU=We need to know what to do with opening balances. You can:\\Let us post opening balances to the general ledger and item ledger for you, on a date you choose\\Review opening balances in journals first, and then post them yourself.';
      MissingAccountingPeriodeErr@1054 : TextConst '@@@="% = Posting Date";ENU=Posting date %1 is not within an open accounting period. To see open periods, go to the Accounting Periods page.';
      EnableTogglingOverviewPage@1036 : Boolean;
      ShowOverviewPage@1037 : Boolean;

    LOCAL PROCEDURE NextAction@15();
    VAR
      Handled@1002 : Boolean;
      ShowBalance@1001 : Boolean;
      HideSelected@1003 : Boolean;
      ListOfAccounts@1000 : ARRAY [11] OF Code[20];
    BEGIN
      case Step of
        Step::ChooseSource:
          begin
            OnGetInstructions(Instructions,Handled);
            if not Handled then
              ERROR('');
          end;
        Step::Import:
          begin
            OnShowBalance(ShowBalance);
            OnHideSelected(HideSelected);
            CurrPage.DataMigrationEntities.PAGE.SetShowBalance(ShowBalance);
            CurrPage.DataMigrationEntities.PAGE.SetHideSelected(HideSelected);
            OnValidateSettings;
            OnDataImport(Handled);
            if not Handled then
              ERROR('');
            OnSelectDataToApply(TempDataMigrationEntity,Handled);
            CurrPage.DataMigrationEntities.PAGE.CopyToSourceTable(TempDataMigrationEntity);
            TotalNoOfMigrationRecords := GetTotalNoOfMigrationRecords(TempDataMigrationEntity);
            if not Handled then
              ERROR('');
          end;
        Step::Apply:
          begin
            if ShowPostingOptions then
              if BallancesPostingOption = BallancesPostingOption::" " then
                ERROR(BallancesPostingErr);
            CurrPage.DataMigrationEntities.PAGE.CopyFromSourceTable(TempDataMigrationEntity);
            OnApplySelectedData(TempDataMigrationEntity,Handled);
            if not Handled then
              ERROR('');
          end;
        Step::AccountSetup1:
          begin
            ListOfAccounts[1] := SalesAccount;
            ListOfAccounts[2] := SalesCreditMemoAccount;
            ListOfAccounts[3] := SalesLineDiscAccount;
            ListOfAccounts[4] := SalesInvDiscAccount;
            ListOfAccounts[5] := PurchAccount;
            ListOfAccounts[6] := PurchCreditMemoAccount;
            ListOfAccounts[7] := PurchLineDiscAccount;
            ListOfAccounts[8] := PurchInvDiscAccount;
            ListOfAccounts[9] := COGSAccount;
            ListOfAccounts[10] := InventoryAdjmtAccount;
            ListOfAccounts[11] := InventoryAccount;
            OnGLPostingSetup(ListOfAccounts);
          end;
        Step::AccountSetup2:
          begin
            ListOfAccounts[1] := ReceivablesAccount;
            ListOfAccounts[2] := ServiceChargeAccount;
            ListOfAccounts[3] := PayablesAccount;
            ListOfAccounts[4] := PurchServiceChargeAccount;
            OnCustomerVendorPostingSetup(ListOfAccounts);
          end;
      end;
      NextStep(false);
    END;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      ResetWizardControls;

      if Backwards then
        Step := Step - 1
      else
        Step := Step + 1;

      case Step of
        Step::Intro:
          ShowIntroStep;
        Step::ChooseSource:
          ShowChooseSourceStep;
        Step::Import:
          ShowImportStep;
        Step::Apply:
          ShowApplyStep;
        Step::Done:
          ShowDoneStep;
        Step::PostingGroupIntro:
          ShowPostingGroupIntroStep;
        Step::AccountSetup1:
          ShowFirstAccountStep;
        Step::AccountSetup2:
          ShowSecondAccountStep;
        Step::ShowPostingGroupDoneStep:
          ShowPostingGroupDoneStep;
      end;
      CurrPage.UPDATE(true);
    END;

    LOCAL PROCEDURE ShowIntroStep@1();
    BEGIN
      IntroVisible := true;
      BackEnabled := false;
      PostingGroupIntroVisible := false;
    END;

    LOCAL PROCEDURE ShowChooseSourceStep@2();
    BEGIN
      ChooseSourceVisible := true;
    END;

    LOCAL PROCEDURE ShowImportStep@4();
    BEGIN
      ImportVisible := true;
      OnHasTemplate(DownloadTemplateVisible);
      OnHasSettings(DataMigrationSettingsVisible);
    END;

    LOCAL PROCEDURE ShowApplyStep@5();
    BEGIN
      ApplyVisible := true;
      ShowPostingOptions := false;
      NextEnabled := false;
      ApplyButtonVisible := true;
      ApplyButtonEnabled := TotalNoOfMigrationRecords > 0;
      OnHasAdvancedApply(OpenAdvancedApplyVisible);
      OnShowPostingOptions(ShowPostingOptions);
      if ShowPostingOptions then begin
        PostingDate := WORKDATE;
        CurrPage.DataMigrationEntities.PAGE.SetPostingInfromation(
          BallancesPostingOption = BallancesPostingOption::"Post balances for me",PostingDate);
      end;
    END;

    LOCAL PROCEDURE ShowDoneStep@6();
    BEGIN
      DoneVisible := true;
      NextEnabled := false;
      FinishEnabled := true;
      BackEnabled := false;
      OnPostingGroupSetup(AccountSetupVisible);
      if AccountSetupVisible then begin
        TempDataMigrationEntity.RESET;
        TempDataMigrationEntity.SETRANGE("Table ID",15);
        TempDataMigrationEntity.SETRANGE(Selected,true);
        if TempDataMigrationEntity.FINDFIRST then begin
          DoneVisible := false;
          NextEnabled := true;
          FinishEnabled := false;
          NextStep(false);
        end;
      end;
      OnHasErrors(ShowErrorsVisible);
      OnShowDuplicateContactsText(ShowDuplicateContactsText);
      OnShowThatsItMessage(ThatsItText);
      OnEnableTogglingDataMigrationOverviewPage(EnableTogglingOverviewPage);
    END;

    LOCAL PROCEDURE ShowPostingGroupIntroStep@8();
    BEGIN
      DoneVisible := false;
      BackEnabled := false;
      NextEnabled := true;
      PostingGroupIntroVisible := true;
      FirstAccountSetupVisible := false;
      SecondAccountSetupVisible := false;
      FinishEnabled := false;
    END;

    LOCAL PROCEDURE ShowFirstAccountStep@9();
    BEGIN
      DoneVisible := false;
      BackEnabled := false;
      NextEnabled := true;
      FirstAccountSetupVisible := true;
      SecondAccountSetupVisible := false;
      PostingGroupIntroVisible := false;
      FinishEnabled := false;
    END;

    LOCAL PROCEDURE ShowSecondAccountStep@11();
    BEGIN
      DoneVisible := false;
      BackEnabled := true;
      NextEnabled := true;
      PostingGroupIntroVisible := false;
      FirstAccountSetupVisible := false;
      SecondAccountSetupVisible := true;
      FinishEnabled := false;
    END;

    LOCAL PROCEDURE ResetWizardControls@10();
    BEGIN
      // Buttons
      BackEnabled := true;
      NextEnabled := true;
      ApplyButtonVisible := false;
      ApplyButtonEnabled := false;
      FinishEnabled := false;
      DownloadTemplateVisible := false;
      DataMigrationSettingsVisible := false;
      OpenAdvancedApplyVisible := false;
      ShowErrorsVisible := false;
      PostingGroupIntroVisible := false;
      FirstAccountSetupVisible := false;
      SecondAccountSetupVisible := false;

      // Tabs
      IntroVisible := false;
      ChooseSourceVisible := false;
      ImportVisible := false;
      ApplyVisible := false;
      DoneVisible := false;
    END;

    LOCAL PROCEDURE GetTotalNoOfMigrationRecords@7(VAR DataMigrationEntity@1000 : Record "Data Migration Entity") TotalCount : Integer;
    BEGIN
      if DataMigrationEntity.FINDSET then
        repeat
          TotalCount += DataMigrationEntity."No. of Records";
        until DataMigrationEntity.NEXT = 0;
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      if MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE)) and
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(CURRENTCLIENTTYPE))
      then
        if MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") and
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        then
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    LOCAL PROCEDURE ShowPostingGroupDoneStep@14();
    BEGIN
      DoneVisible := true;
      BackEnabled := false;
      NextEnabled := false;
      OnHasErrors(ShowErrorsVisible);
      FinishEnabled := true;
    END;

    LOCAL PROCEDURE SetPosting@12();
    VAR
      AccountingPeriod@1000 : Record "Accounting Period";
    BEGIN
      if BallancesPostingOption = BallancesPostingOption::"Post balances for me" then
        if AccountingPeriod.GetFiscalYearStartDate(PostingDate) = 0D then
          ERROR(MissingAccountingPeriodeErr,PostingDate);

      CurrPage.DataMigrationEntities.PAGE.SetPostingInfromation(
        BallancesPostingOption = BallancesPostingOption::"Post balances for me",PostingDate);
    END;

    BEGIN
    END.
  }
}

