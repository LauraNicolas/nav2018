OBJECT Report 1140 Update Cost Acctg. Dimensions
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Update Cost Acctg. Dimensions;
    ProcessingOnly=true;
    OnPostReport=BEGIN
                   if UpdateDimension then begin
                     if not CONFIRM(Text001,true) then
                       ERROR('');

                     CostAccSetup.VALIDATE("Cost Center Dimension",NewCCDimension);
                     CostAccSetup.VALIDATE("Cost Object Dimension",NewCODimension);
                     CostAccSetup.MODIFY(true);
                   end;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   CostAccSetup.GET;
                   GetInitialDimensions;
                 END;

    }
    CONTROLS
    {
      { 1   ;    ;Container ;
                  Name=Container1;
                  ContainerType=ContentArea }

      { 2   ;1   ;Group     ;
                  Name=Options;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 3   ;2   ;Field     ;
                  Name=CostCenterDimension;
                  CaptionML=ENU=Cost Center Dimension;
                  ToolTipML=ENU=Specifies the new cost object dimension.;
                  ApplicationArea=#CostAccounting;
                  SourceExpr=NewCCDimension;
                  TableRelation=Dimension;
                  OnValidate=BEGIN
                               if NewCCDimension = '' then begin
                                 GetInitialDimensions;
                                 UpdateDimension := false;
                                 ERROR(Text003,CostAccSetup.FIELDCAPTION("Cost Center Dimension"));
                               end;
                               if NewCCDimension = NewCODimension then begin
                                 GetInitialDimensions;
                                 UpdateDimension := false;
                                 ERROR(Text002);
                               end;

                               if CostAccSetup."Cost Center Dimension" <> NewCCDimension then
                                 UpdateDimension := true;
                             END;
                              }

      { 4   ;2   ;Field     ;
                  Name=CostObjectDimension;
                  CaptionML=ENU=Cost Object Dimension;
                  ToolTipML=ENU=Specifies where you should assign costs.;
                  ApplicationArea=#CostAccounting;
                  SourceExpr=NewCODimension;
                  TableRelation=Dimension;
                  OnValidate=BEGIN
                               if NewCODimension = '' then begin
                                 GetInitialDimensions;
                                 UpdateDimension := false;
                                 ERROR(Text003,CostAccSetup.FIELDCAPTION("Cost Object Dimension"));
                               end;
                               if NewCCDimension = NewCODimension then begin
                                 GetInitialDimensions;
                                 UpdateDimension := false;
                                 ERROR(Text002);
                               end;

                               if CostAccSetup."Cost Object Dimension" <> NewCODimension then
                                 UpdateDimension := true;
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CostAccSetup@1000 : Record "Cost Accounting Setup";
      NewCCDimension@1001 : Code[20];
      NewCODimension@1002 : Code[20];
      UpdateDimension@1003 : Boolean;
      Text001@1004 : TextConst 'ENU=Before you change the corresponding dimension on G/L entries, make sure all G/L entries using the previously defined dimension have been transferred to Cost Accounting. \\Do you want to proceed?';
      Text002@1005 : TextConst 'ENU=The dimension values for cost center and cost object cannot be same.';
      Text003@1006 : TextConst 'ENU=%1 must be filled in. Enter a value.';

    LOCAL PROCEDURE GetInitialDimensions@1();
    BEGIN
      NewCCDimension := CostAccSetup."Cost Center Dimension";
      NewCODimension := CostAccSetup."Cost Object Dimension";
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

