OBJECT Report 1134 Copy Cost Budget
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Cost Budget;
    ProcessingOnly=true;
    OnInitReport=BEGIN
                   if NoOfCopies = 0 then
                     NoOfCopies := 1;
                   if Factor = 0 then
                     Factor := 1;
                 END;

    OnPreReport=BEGIN
                  EVALUATE(DateFormula,DateChangeFormula);
                END;

  }
  DATASET
  {
    { 3233;    ;DataItem;Cost Budget Entry   ;
               DataItemTable="Cost Budget Entry";
               DataItemTableView=SORTING("Budget Name","Cost Type No.",Date);
               OnPreDataItem=BEGIN
                               if Factor = 0 then
                                 ERROR(Text000);

                               if NoOfCopies < 1 then
                                 ERROR(Text001);

                               if (NoOfCopies > 1) and (DateChangeFormula = '') then
                                 ERROR(Text002);

                               if GETFILTER("Budget Name") = '' then
                                 ERROR(Text008);

                               if CostBudgetEntryReqForm."Budget Name" = '' then
                                 ERROR(Text009);

                               if CostBudgetEntryReqForm."Budget Name" <> '' then
                                 CostBudgetNameTarget.GET(CostBudgetEntryReqForm."Budget Name")
                               else
                                 CostBudgetNameTarget.GET(GETFILTER("Budget Name"));

                               if not CONFIRM(
                                    Text004,false,GETFILTER("Budget Name"),CostBudgetNameTarget.Name,Factor,NoOfCopies,GETFILTER(Date),DateChangeFormula)
                               then
                                 ERROR('');

                               LOCKTABLE;

                               if CostBudgetEntryTarget.FINDLAST then
                                 LastEntryNo := CostBudgetEntryTarget."Entry No.";

                               NextEntryNo := LastEntryNo + 1;

                               Window.OPEN(Text005);

                               Window.UPDATE(1,COUNT);
                               Window.UPDATE(2,NoOfCopies);
                             END;

               OnAfterGetRecord=VAR
                                  CostAccMgt@1000 : Codeunit "Cost Account Mgt";
                                BEGIN
                                  if "Entry No." > LastEntryNo then
                                    CurrReport.BREAK;

                                  CostBudgetEntryTarget := "Cost Budget Entry";

                                  CostBudgetEntryTarget.Description := STRSUBSTNO(Text006,GETFILTER("Budget Name"));
                                  CostBudgetEntryTarget."Budget Name" := CostBudgetEntryReqForm."Budget Name";

                                  if CostBudgetEntryReqForm."Cost Type No." <> '' then
                                    CostBudgetEntryTarget."Cost Type No." := CostBudgetEntryReqForm."Cost Type No.";
                                  if CostBudgetEntryReqForm."Cost Center Code" <> '' then
                                    CostBudgetEntryTarget."Cost Center Code" := CostBudgetEntryReqForm."Cost Center Code";
                                  if CostBudgetEntryReqForm."Cost Object Code" <> '' then
                                    CostBudgetEntryTarget."Cost Object Code" := CostBudgetEntryReqForm."Cost Object Code";

                                  CostBudgetEntryTarget.Amount := ROUND(Amount * Factor,0.01);
                                  CostBudgetEntryTarget.Allocated := false;

                                  for i := 1 to NoOfCopies do begin
                                    CostBudgetEntryTarget."Entry No." := NextEntryNo;
                                    NextEntryNo := NextEntryNo + 1;
                                    if DateChangeFormula <> '' then
                                      CostBudgetEntryTarget.Date := CALCDATE(DateFormula,CostBudgetEntryTarget.Date);
                                    CostBudgetEntryTarget.INSERT;
                                    NoInserted := NoInserted + 1;

                                    if CostBudgetRegNo = 0 then
                                      CostBudgetRegNo :=
                                        CostAccMgt.InsertCostBudgetRegister(
                                          CostBudgetEntryTarget."Entry No.",CostBudgetEntryTarget."Budget Name",CostBudgetEntryTarget.Amount)
                                    else
                                      CostAccMgt.UpdateCostBudgetRegister(
                                        CostBudgetRegNo,CostBudgetEntryTarget."Entry No.",CostBudgetEntryTarget.Amount);

                                    if (NoInserted mod 100) = 0 then
                                      Window.UPDATE(3,NoInserted);
                                  end;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                if NoInserted = 0 then begin
                                  MESSAGE(Text010);
                                  ERROR('');
                                end;

                                if not CONFIRM(Text007,true,NoInserted,CostBudgetNameTarget.Name) then
                                  ERROR('');
                              END;

               ReqFilterFields="Budget Name","Cost Type No.","Cost Center Code","Cost Object Code",Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=true;
      OnInit=BEGIN
               CostBudgetEntryReqForm.INIT;
             END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 21  ;2   ;Group     ;
                  Name=Copy to...;
                  CaptionML=ENU=Copy to... }

      { 1   ;3   ;Field     ;
                  Name=Budget Name;
                  Lookup=true;
                  CaptionML=ENU=Budget Name;
                  ToolTipML=ENU=Specifies the name of the budget.;
                  ApplicationArea=#CostAccounting;
                  SourceExpr=CostBudgetEntryReqForm."Budget Name";
                  TableRelation="Cost Budget Name" }

      { 2   ;3   ;Field     ;
                  Name=Cost Type No.;
                  Lookup=true;
                  CaptionML=ENU=Cost Type No.;
                  ToolTipML=ENU=Specifies the cost type number.;
                  ApplicationArea=#CostAccounting;
                  SourceExpr=CostBudgetEntryReqForm."Cost Type No.";
                  TableRelation="Cost Type" }

      { 4   ;3   ;Field     ;
                  Name=Cost Center Code;
                  Lookup=true;
                  CaptionML=ENU=Cost Center Code;
                  ToolTipML=ENU=Specifies the cost center code that applies.;
                  ApplicationArea=#CostAccounting;
                  SourceExpr=CostBudgetEntryReqForm."Cost Center Code";
                  TableRelation="Cost Center" }

      { 5   ;3   ;Field     ;
                  Name=Cost Object Code;
                  Lookup=true;
                  CaptionML=ENU=Cost Object Code;
                  ToolTipML=ENU=Specifies the code of the cost element.;
                  ApplicationArea=#CostAccounting;
                  SourceExpr=CostBudgetEntryReqForm."Cost Object Code";
                  TableRelation="Cost Object" }

      { 3   ;2   ;Field     ;
                  Name=Amount multiplication factor;
                  CaptionML=ENU=Amount multiplication factor;
                  ToolTipML=ENU=Specifies an adjustment factor that multiplies the amounts you want to copy. By entering an adjustment factor, you can increase or decrease the amounts that are to be copied to the new budget.;
                  ApplicationArea=#CostAccounting;
                  SourceExpr=Factor }

      { 8   ;2   ;Field     ;
                  Name=No. of Copies;
                  CaptionML=ENU=No. of Copies;
                  ToolTipML=ENU=Specifies how many times the cost budget is copied.;
                  ApplicationArea=#CostAccounting;
                  SourceExpr=NoOfCopies }

      { 6   ;2   ;Field     ;
                  Name=Date Change Formula;
                  CaptionML=ENU=Date Change Formula;
                  ToolTipML=[ENU="Specifies how the dates on the entries that are copied will be changed. Use a date formula; for example, to copy last week's budget to this week, use the formula 1W (one week)."];
                  ApplicationArea=#CostAccounting;
                  DateFormula=true;
                  SourceExpr=DateChangeFormula }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CostBudgetEntryReqForm@1000 : Record "Cost Budget Entry";
      CostBudgetEntryTarget@1001 : Record "Cost Budget Entry";
      CostBudgetNameTarget@1002 : Record "Cost Budget Name";
      Window@1003 : Dialog;
      DateChangeFormula@1004 : Code[10];
      DateFormula@1005 : DateFormula;
      LastEntryNo@1006 : Integer;
      NextEntryNo@1007 : Integer;
      NoOfCopies@1008 : Integer;
      Factor@1009 : Decimal;
      i@1010 : Integer;
      NoInserted@1011 : Integer;
      CostBudgetRegNo@1012 : Integer;
      Text000@1013 : TextConst 'ENU=The multiplication factor must not be 0.';
      Text001@1014 : TextConst 'ENU=Number of copies must be at least 1.';
      Text002@1015 : TextConst 'ENU=If more than one copy is created, a formula for date change must be defined.';
      Text004@1017 : TextConst '@@@="%3=multiplication factor (decimal);%4=No of copies (integer)";ENU=Budget %1 will be copied to Budget %2. The budget amounts will be multiplied by a factor of %3. \%4 copies will be created and the date from range %5 will be incremented by %6.\\Do you want do copy the budget?';
      Text005@1018 : TextConst 'ENU=Copying budget entries\No of entries #1#####\No of copies  #2#####\Copied        #3#####';
      Text006@1019 : TextConst '@@@=%1 - Budget Name.;ENU=Copy of cost budget %1';
      Text007@1020 : TextConst 'ENU=%1 entries generated in budget %2.\\Do you want to copy entries?';
      Text008@1021 : TextConst 'ENU=Define name of source budget.';
      Text009@1022 : TextConst 'ENU=Define name of target budget.';
      Text010@1023 : TextConst 'ENU=No entries were copied.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

