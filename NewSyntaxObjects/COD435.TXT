OBJECT Codeunit 435 IC Inbox Import
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="IC Inbox Transaction";
    OnRun=VAR
            CompanyInfo@1004 : Record "Company Information";
            ICPartner@1027 : Record "IC Partner";
            TempICOutboxTrans@1014 : TEMPORARY Record "IC Outbox Transaction";
            TempICOutBoxJnlLine@1013 : TEMPORARY Record "IC Outbox Jnl. Line";
            TempICIOBoxJnlDim@1012 : TEMPORARY Record "IC Inbox/Outbox Jnl. Line Dim.";
            TempICOutBoxSalesHdr@1011 : TEMPORARY Record "IC Outbox Sales Header";
            TempICOutBoxSalesLine@1010 : TEMPORARY Record "IC Outbox Sales Line";
            TempICOutBoxPurchHdr@1009 : TEMPORARY Record "IC Outbox Purchase Header";
            TempICOutBoxPurchLine@1008 : TEMPORARY Record "IC Outbox Purchase Line";
            TempICDocDim@1007 : TEMPORARY Record "IC Document Dimension";
            ICInboxJnlLine@1017 : Record "IC Inbox Jnl. Line";
            ICInboxSalesHdr@1021 : Record "IC Inbox Sales Header";
            ICInboxSalesLine@1020 : Record "IC Inbox Sales Line";
            ICInboxPurchHdr@1018 : Record "IC Inbox Purchase Header";
            ICInboxPurchLine@1019 : Record "IC Inbox Purchase Line";
            ICInboxJnlLineDim@1022 : Record "IC Inbox/Outbox Jnl. Line Dim.";
            ICInboxDocDim@1023 : Record "IC Document Dimension";
            ICInboxOutboxMgt@1015 : Codeunit ICInboxOutboxMgt;
            FileMgt@1028 : Codeunit "File Management";
            PermissionManager@1005 : Codeunit "Permission Manager";
            ICOutboxImpExpXML@1003 : XMLport "IC Outbox Imp/Exp";
            IStr@1000 : InStream;
            IFile@1002 : File;
            FileName@1001 : Text;
            FromICPartnerCode@1016 : Code[20];
            ToICPartnerCode@1026 : Code[20];
            NewTableID@1024 : Integer;
          BEGIN
            CompanyInfo.GET;
            CompanyInfo.TESTFIELD("IC Partner Code");
            if ClientFileName = '' then begin
              if CompanyInfo."IC Inbox Type" = CompanyInfo."IC Inbox Type"::"File Location" then
                ClientFileName := FileMgt.CombinePath(CompanyInfo."IC Inbox Details",'*.xml');
              FileName := FileMgt.UploadFile(STRSUBSTNO(SelectFileMsg,TABLECAPTION),ClientFileName);
            end else
              FileName := FileMgt.UploadFileSilent(ClientFileName);

            if FileName = '' then
              ERROR(EnterFileNameErr);

            IFile.OPEN(FileName);
            IFile.CREATEINSTREAM(IStr);
            ICOutboxImpExpXML.SETSOURCE(IStr);
            ICOutboxImpExpXML.IMPORT;
            IFile.CLOSE;
            FromICPartnerCode := ICOutboxImpExpXML.GetFromICPartnerCode;
            ToICPartnerCode := ICOutboxImpExpXML.GetToICPartnerCode;
            if ToICPartnerCode <> CompanyInfo."IC Partner Code" then
              ERROR(
                WrongCompanyErr,ICPartner.TABLECAPTION,ToICPartnerCode,
                CompanyInfo.FIELDCAPTION("IC Partner Code"),CompanyInfo."IC Partner Code");

            ICOutboxImpExpXML.GetICOutboxTrans(TempICOutboxTrans);
            ICOutboxImpExpXML.GetICOutBoxJnlLine(TempICOutBoxJnlLine);
            ICOutboxImpExpXML.GetICIOBoxJnlDim(TempICIOBoxJnlDim);
            ICOutboxImpExpXML.GetICOutBoxSalesHdr(TempICOutBoxSalesHdr);
            ICOutboxImpExpXML.GetICOutBoxSalesLine(TempICOutBoxSalesLine);
            ICOutboxImpExpXML.GetICOutBoxPurchHdr(TempICOutBoxPurchHdr);
            ICOutboxImpExpXML.GetICOutBoxPurchLine(TempICOutBoxPurchLine);
            ICOutboxImpExpXML.GetICSalesDocDim(TempICDocDim);
            ICOutboxImpExpXML.GetICSalesDocLineDim(TempICDocDim);
            ICOutboxImpExpXML.GetICPurchDocDim(TempICDocDim);
            ICOutboxImpExpXML.GetICPurchDocLineDim(TempICDocDim);
            FromICPartnerCode := ICOutboxImpExpXML.GetFromICPartnerCode;

            if TempICOutboxTrans.FIND('-') then
              repeat
                ICInboxOutboxMgt.OutboxTransToInbox(TempICOutboxTrans,Rec,FromICPartnerCode);

                TempICOutBoxJnlLine.SETRANGE("Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxJnlLine.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxJnlLine.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                if TempICOutBoxJnlLine.FIND('-') then
                  repeat
                    ICInboxOutboxMgt.OutboxJnlLineToInbox(Rec,TempICOutBoxJnlLine,ICInboxJnlLine);
                    TempICIOBoxJnlDim.SETRANGE("Transaction No.",TempICOutboxTrans."Transaction No.");
                    TempICIOBoxJnlDim.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                    TempICIOBoxJnlDim.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                    TempICIOBoxJnlDim.SETRANGE("Line No.",ICInboxJnlLine."Line No.");
                    if TempICIOBoxJnlDim.FIND('-') then
                      repeat
                        ICInboxOutboxMgt.OutboxJnlLineDimToInbox(
                          ICInboxJnlLine,TempICIOBoxJnlDim,ICInboxJnlLineDim,DATABASE::"IC Inbox Jnl. Line");
                      until TempICIOBoxJnlDim.NEXT = 0;
                  until TempICOutBoxJnlLine.NEXT = 0;

                TempICOutBoxSalesHdr.SETRANGE("IC Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxSalesHdr.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxSalesHdr.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                if TempICOutBoxSalesHdr.FIND('-') then
                  repeat
                    ICInboxOutboxMgt.OutboxSalesHdrToInbox(Rec,TempICOutBoxSalesHdr,ICInboxPurchHdr);
                  until TempICOutBoxSalesHdr.NEXT = 0;

                TempICOutBoxSalesLine.SETRANGE("IC Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxSalesLine.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxSalesLine.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                if TempICOutBoxSalesLine.FIND('-') then
                  repeat
                    ICInboxOutboxMgt.OutboxSalesLineToInbox(Rec,TempICOutBoxSalesLine,ICInboxPurchLine);
                  until TempICOutBoxSalesLine.NEXT = 0;

                TempICOutBoxPurchHdr.SETRANGE("IC Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxPurchHdr.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxPurchHdr.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                if TempICOutBoxPurchHdr.FIND('-') then
                  repeat
                    ICInboxOutboxMgt.OutboxPurchHdrToInbox(Rec,TempICOutBoxPurchHdr,ICInboxSalesHdr);
                  until TempICOutBoxPurchHdr.NEXT = 0;

                TempICOutBoxPurchLine.SETRANGE("IC Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxPurchLine.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxPurchLine.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                if TempICOutBoxPurchLine.FIND('-') then
                  repeat
                    ICInboxOutboxMgt.OutboxPurchLineToInbox(Rec,TempICOutBoxPurchLine,ICInboxSalesLine);
                  until TempICOutBoxPurchLine.NEXT = 0;

                TempICDocDim.SETRANGE("Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICDocDim.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICDocDim.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                if TempICDocDim.FIND('-') then
                  repeat
                    case TempICDocDim."Table ID" of
                      DATABASE::"IC Outbox Sales Header":
                        NewTableID := DATABASE::"IC Inbox Purchase Header";
                      DATABASE::"IC Outbox Sales Line":
                        NewTableID := DATABASE::"IC Inbox Purchase Line";
                      DATABASE::"IC Outbox Purchase Header":
                        NewTableID := DATABASE::"IC Inbox Sales Header";
                      DATABASE::"IC Outbox Purchase Line":
                        NewTableID := DATABASE::"IC Inbox Sales Line";
                    end;
                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                      TempICDocDim,ICInboxDocDim,NewTableID,FromICPartnerCode,"Transaction Source");
                  until TempICDocDim.NEXT = 0;
              until TempICOutboxTrans.NEXT = 0;

            if not PermissionManager.SoftwareAsAService then
              FileMgt.MoveAndRenameClientFile(ClientFileName,FileMgt.GetFileName(ClientFileName),ArchiveTok);
          END;

  }
  CODE
  {
    VAR
      SelectFileMsg@1000 : TextConst '@@@="%1 = IC Inbox Import";ENU=Select file to import into %1';
      ArchiveTok@1001 : TextConst 'ENU=Archive';
      WrongCompanyErr@1002 : TextConst '@@@=The selected xml file contains data sent to IC Partner 001. Current company''s IC Partner Code is 002.;ENU=The selected xml file contains data sent to %1 %2. Current company''s %3 is %4.';
      EnterFileNameErr@1004 : TextConst 'ENU=Enter the file name.';
      ClientFileName@1003 : Text;

    [External]
    PROCEDURE SetFileName@1(NewFileName@1000 : Text);
    BEGIN
      ClientFileName := NewFileName;
    END;

    BEGIN
    END.
  }
}

