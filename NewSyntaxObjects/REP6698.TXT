OBJECT Report 6698 Move Negative Purchase Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Move Negative Purchase Lines;
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  CopyDocMgt.SetProperties(true,false,true,true,true,false,false);
                  if (FromDocType = FromDocType::"Return Order") or (FromDocType = FromDocType::"Credit Memo") then
                    ToDocType := ToDocType2;
                  ToPurchHeader."Document Type" := CopyDocMgt.PurchHeaderDocType(ToDocType);
                  CopyDocMgt.CopyPurchDoc(FromDocType,FromPurchHeader."No.",ToPurchHeader);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               DropDownForOrderAndInvoiceEdit := true;
               DropDownForRetOrderAndCrMemoEd := true;
             END;

      OnOpenPage=BEGIN
                   with FromPurchHeader do
                     case "Document Type" of
                       "Document Type"::Order:
                         begin
                           ToDocType := ToDocType::"Return Order";
                           ToDocType2 := ToDocType2::Order;
                           FromDocType := FromDocType::Order;
                           DropDownForRetOrderAndCrMemoEd := false;
                         end;
                       "Document Type"::Invoice:
                         begin
                           ToDocType := ToDocType::"Credit Memo";
                           ToDocType2 := ToDocType2::Invoice;
                           FromDocType := FromDocType::Invoice;
                           DropDownForRetOrderAndCrMemoEd := false;
                         end;
                       "Document Type"::"Return Order":
                         begin
                           ToDocType2 := ToDocType2::Order;
                           ToDocType := ToDocType::"Return Order";
                           FromDocType := FromDocType::"Return Order";
                           DropDownForOrderAndInvoiceEdit := false;
                         end;
                       "Document Type"::"Credit Memo":
                         begin
                           ToDocType2 := ToDocType2::Invoice;
                           ToDocType := ToDocType::"Credit Memo";
                           FromDocType := FromDocType::"Credit Memo";
                           DropDownForOrderAndInvoiceEdit := false;
                         end;
                     end;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  Name=Container1900000001;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  Name=Options;
                  CaptionML=ENU=Options }

      { 4   ;2   ;Group     ;
                  Name=Order && Invoice;
                  CaptionML=ENU=Order && Invoice }

      { 1   ;3   ;Field     ;
                  Name=DropDownForOrderAndInvoice;
                  CaptionML=ENU=To Document Type;
                  ToolTipML=ENU=Specifies which document type you want to move the negative purchase lines to.;
                  OptionCaptionML=ENU=,,,,Return Order,Credit Memo;
                  ApplicationArea=#Advanced;
                  SourceExpr=ToDocType;
                  Editable=DropDownForOrderAndInvoiceEdit }

      { 6   ;2   ;Group     ;
                  Name=Return Order && Credit Memo;
                  CaptionML=ENU=Return Order && Credit Memo }

      { 3   ;3   ;Field     ;
                  Name=DropDownForRetOrderAndCrMemo;
                  CaptionML=ENU=To Document Type;
                  ToolTipML=ENU=Specifies which document type you want to move the negative purchase lines to.;
                  OptionCaptionML=ENU=,,Order,Invoice;
                  ApplicationArea=#Advanced;
                  SourceExpr=ToDocType2;
                  Editable=DropDownForRetOrderAndCrMemoEd }

      { 5   ;2   ;Field     ;
                  Name=Field5;
                  ApplicationArea=#Advanced;
                  CaptionClass=Text19012737;
                  MultiLine=true }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      FromPurchHeader@1002 : Record "Purchase Header";
      ToPurchHeader@1004 : Record "Purchase Header";
      CopyDocMgt@1003 : Codeunit "Copy Document Mgt.";
      ToDocType@1000 : ',,Order,Invoice,"Return Order","Credit Memo"';
      ToDocType2@1001 : ',,Order,Invoice,"Return Order","Credit Memo"';
      FromDocType@1005 : 'Quote,"Blanket Order",Order,Invoice,"Return Order","Credit Memo"';
      Text001@1006 : TextConst 'ENU=%1 %2 has been created. Do you want to view the created document?';
      DropDownForRetOrderAndCrMemoEd@19065392 : Boolean INDATASET;
      DropDownForOrderAndInvoiceEdit@19005123 : Boolean INDATASET;
      Text19012737@19067191 : TextConst 'ENU=When you move a negative purchase line to your selected document type, the quantity of the line on the selected document will become positive.';

    [External]
    PROCEDURE SetPurchHeader@1(VAR NewFromPurchHeader@1000 : Record "Purchase Header");
    BEGIN
      FromPurchHeader := NewFromPurchHeader;
    END;

    [External]
    PROCEDURE ShowDocument@2();
    BEGIN
      COMMIT;
      if ToPurchHeader.FIND then
        if CONFIRM(Text001,true,ToPurchHeader."Document Type",ToPurchHeader."No.") then
          CopyDocMgt.ShowPurchDoc(ToPurchHeader);
    END;

    PROCEDURE InitializeRequest@3(NewFromDocType@1002 : Option;NewToDocType@1000 : Option;NewToDocType2@1001 : Option);
    BEGIN
      FromDocType := NewFromDocType;
      ToDocType := NewToDocType;
      ToDocType2 := NewToDocType2;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

