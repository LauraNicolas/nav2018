OBJECT Page 1612 Office Admin. Credentials
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    Permissions=TableData "Office Admin. Credentials"=rimd;
    CaptionML=ENU=Office Admin. Credentials;
    SaveValues=true;
    MultipleNewLines=false;
    InsertAllowed=true;
    DeleteAllowed=false;
    ModifyAllowed=true;
    SourceTable="Office Admin. Credentials";
    PageType=NavigatePage;
    OnOpenPage=BEGIN
                 ShowQuestion;
                 EmailHostedInO365 := true;
               END;

    OnQueryClosePage=BEGIN
                       if (CloseAction = ACTION::OK) or (CloseAction = ACTION::LookupOK) then begin
                         if not GET then
                           INSERT;
                         SavePassword(PasswordText);
                       end;
                     END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      Name=ActionContainer2;
                      ActionContainerType=ActionItems }
      { 9       ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=ENU=Back;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=true;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(true);
                               END;
                                }
      { 8       ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=ENU=Next;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=true;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(false);
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=ENU=Finish;
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=true;
                      Image=Approve;
                      OnAction=BEGIN
                                 if (Email = '') or (PasswordText = '') or (EmailHostedInO365 and (Endpoint = '')) then
                                   ERROR(MissingCredentialErr);

                                 if not INSERT(true) then
                                   MODIFY(true);

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                Name=Container1;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                Name=Question;
                CaptionML=ENU="";
                Visible=QuestionVisible;
                GroupType=Group }

    { 12  ;2   ;Field     ;
                Name=UseO365;
                DrillDown=true;
                CaptionML=ENU=Are you using an Office 365 mailbox?;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=EmailHostedInO365;
                ImplicitType=Boolean }

    { 11  ;1   ;Group     ;
                Name=O365Credential;
                CaptionML=ENU="";
                Visible=O365CredentialVisible;
                GroupType=Group }

    { 14  ;2   ;Field     ;
                Name=O365Email;
                ExtendedDatatype=E-Mail;
                CaptionML=ENU=Office 365 admin email address;
                ApplicationArea=#Basic,#Suite;
                NotBlank=true;
                SourceExpr=Email;
                ImplicitType=Text80 }

    { 13  ;2   ;Field     ;
                Name=O365Password;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Office 365 admin password;
                ApplicationArea=#Basic,#Suite;
                NotBlank=true;
                SourceExpr=PasswordText;
                OnValidate=BEGIN
                             if (PasswordText <> '') and (not ENCRYPTIONENABLED) then
                               if CONFIRM(EncryptionManagement.GetEncryptionIsNotActivatedQst) then
                                 PAGE.RUNMODAL(PAGE::"Data Encryption Management");
                           END;

                ImplicitType=Text }

    { 6   ;1   ;Group     ;
                Name=OnPremCredential;
                CaptionML=ENU="";
                Visible=OnPremCredentialVisible;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                Name=OnPremUsername;
                ExtendedDatatype=E-Mail;
                CaptionML=ENU=Exchange admin username;
                ApplicationArea=#Basic,#Suite;
                NotBlank=true;
                SourceExpr=Email;
                ImplicitType=Text80 }

    { 4   ;2   ;Field     ;
                Name=OnPremPassword;
                ExtendedDatatype=Masked;
                CaptionML=ENU=Exchange admin password;
                ApplicationArea=#Basic,#Suite;
                NotBlank=true;
                SourceExpr=PasswordText;
                OnValidate=BEGIN
                             if (PasswordText <> '') and (not ENCRYPTIONENABLED) then
                               if CONFIRM(EncryptionManagement.GetEncryptionIsNotActivatedQst) then
                                 PAGE.RUNMODAL(PAGE::"Data Encryption Management");
                           END;

                ImplicitType=Text }

    { 5   ;2   ;Field     ;
                Name=Endpoint;
                CaptionML=ENU=Exchange PowerShell Endpoint;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Endpoint;
                ImplicitType=Text250 }

  }
  CODE
  {
    VAR
      EncryptionManagement@1011 : Codeunit "Encryption Management";
      Step@1005 : 'Question,O365Credential,OnPremCredential';
      EmailHostedInO365@1009 : Boolean;
      QuestionVisible@1006 : Boolean;
      O365CredentialVisible@1007 : Boolean;
      OnPremCredentialVisible@1008 : Boolean;
      BackEnabled@1002 : Boolean;
      NextEnabled@1003 : Boolean;
      FinishEnabled@1004 : Boolean;
      MissingCredentialErr@1000 : TextConst 'ENU=You must specify both an email address and a password.';
      PasswordText@1001 : Text;

    LOCAL PROCEDURE NextStep@1(Backwards@1000 : Boolean);
    BEGIN
      if Backwards then
        Step := Step - 1
      else
        Step := Step + 1;

      case Step of
        Step::Question:
          ShowQuestion;
        Step::O365Credential:
          ShowO365Credential(Backwards);
        Step::OnPremCredential:
          ShowOnPremCredential;
      end;

      CurrPage.UPDATE(true);
    END;

    LOCAL PROCEDURE ShowQuestion@2();
    BEGIN
      ResetControls;

      BackEnabled := false;
      QuestionVisible := true;
    END;

    LOCAL PROCEDURE ShowO365Credential@3(Backwards@1000 : Boolean);
    BEGIN
      ResetControls;

      // Skip to the next window if we're not using O365.
      if not EmailHostedInO365 then begin
        NextStep(Backwards);
        exit;
      end;

      FinishEnabled := true;
      NextEnabled := false;
      O365CredentialVisible := true;
    END;

    LOCAL PROCEDURE ShowOnPremCredential@4();
    BEGIN
      ResetControls;

      FinishEnabled := true;
      NextEnabled := false;
      OnPremCredentialVisible := true;
    END;

    LOCAL PROCEDURE ResetControls@9();
    BEGIN
      NextEnabled := true;
      BackEnabled := true;
      FinishEnabled := false;

      QuestionVisible := false;
      O365CredentialVisible := false;
      OnPremCredentialVisible := false;
    END;

    BEGIN
    END.
  }
}

