OBJECT Codeunit 220 Resource-Find Cost
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="Resource Cost";
    OnRun=BEGIN
            ResCost.COPY(Rec);
            with ResCost do begin
              ResCost2 := ResCost;

              FindResUnitCost(ResCost);

              case "Cost Type" of
                "Cost Type"::Fixed:
                  ;
                "Cost Type"::"% Extra":
                  begin
                    ResCost2."Work Type Code" := '';
                    FindResUnitCost(ResCost2);
                    "Direct Unit Cost" := ResCost2."Direct Unit Cost" * (1 + "Direct Unit Cost" / 100);
                    "Unit Cost" := ResCost2."Unit Cost" * (1 + "Unit Cost" / 100);
                  end;
                "Cost Type"::"LCY Extra":
                  begin
                    ResCost2."Work Type Code" := '';
                    FindResUnitCost(ResCost2);
                    "Direct Unit Cost" := ResCost2."Direct Unit Cost" + "Direct Unit Cost";
                    "Unit Cost" := ResCost2."Unit Cost" + "Unit Cost";
                  end;
              end;
            end;
            Rec := ResCost;
          END;

  }
  CODE
  {
    VAR
      ResCost@1000 : Record "Resource Cost";
      ResCost2@1001 : Record "Resource Cost";
      Res@1002 : Record Resource;

    LOCAL PROCEDURE FindResUnitCost@1(VAR NearestResCost@1000 : Record "Resource Cost");
    BEGIN
      with NearestResCost do begin
        if GET(Type::Resource,Code,"Work Type Code") then
          exit;
        Res.GET(Code);
        if GET(Type::"Group(Resource)",Res."Resource Group No.","Work Type Code") then
          exit;
        if GET(Type::All,'',"Work Type Code") then
          exit;
        INIT;
        Code := Res."No.";
        "Direct Unit Cost" := Res."Direct Unit Cost";
        "Unit Cost" := Res."Unit Cost";
      end;

      OnAfterFindResUnitCost(NearestResCost,Res);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterFindResUnitCost@2(VAR ResourceCost@1001 : Record "Resource Cost";Resource@1000 : Record Resource);
    BEGIN
    END;

    BEGIN
    END.
  }
}

