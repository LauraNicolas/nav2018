OBJECT Codeunit 140 EC Sales List Suggest Lines
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    TableNo="VAT Report Header";
    OnRun=VAR
            ECSLVATReportLine@1040 : Record "ECSL VAT Report Line";
          BEGIN
            ErrorMessage.SetContext(Rec);
            ErrorMessage.ClearLog;

            if not IsPeriodValid then begin
              ErrorMessage.LogMessage(Rec,FIELDNO("No."),ErrorMessage."Message Type"::Error,InvalidPeriodErr);
              exit;
            end;

            VATReportHeader := Rec;
            ECSLVATReportLine.ClearLines(Rec);
            PopulateVatEntryLines;
          END;

  }
  CODE
  {
    VAR
      VATReportHeader@1000 : Record "VAT Report Header";
      ErrorMessage@1040 : Record "Error Message";
      InvalidPeriodErr@1041 : TextConst 'ENU=The period is not valid.';

    LOCAL PROCEDURE PopulateVatEntryLines@1();
    VAR
      CompanyInformation@1001 : Record "Company Information";
      EUVATEntries@1000 : Query "EU VAT Entries";
    BEGIN
      CompanyInformation.GET;
      EUVATEntries.SETRANGE(PostingDate,VATReportHeader."Start Date",VATReportHeader."End Date");
      EUVATEntries.SETFILTER(CountryCode,'<>%1',CompanyInformation."Country/Region Code");

      EUVATEntries.OPEN;
      while EUVATEntries.READ do
        AddOrUpdateECLLine(EUVATEntries);
      RowsTotalCorrection;
      DeleteZeroAmountLines;
    END;

    LOCAL PROCEDURE GetECLLine@2(VAR ECSLVATReportLine@1001 : Record "ECSL VAT Report Line";EUVATEntries@1000 : Query "EU VAT Entries");
    VAR
      LastId@1002 : Integer;
      TrnIndicator@1003 : Integer;
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      ECSLVATReportLine.SETRANGE("Country Code",EUVATEntries.CountryCode);
      ECSLVATReportLine.SETRANGE("Customer VAT Reg. No.",EUVATEntries.VAT_Registration_No);
      TrnIndicator := GetIndicatorCode(EUVATEntries.EU_3_Party_Trade,EUVATEntries.EU_Service);
      ECSLVATReportLine.SETRANGE("Transaction Indicator",TrnIndicator);

      if not ECSLVATReportLine.FINDFIRST then begin
        ECSLVATReportLine.RESET;
        ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
        if ECSLVATReportLine.FINDLAST then
          LastId := ECSLVATReportLine."Line No.";

        ECSLVATReportLine.INIT;
        ECSLVATReportLine."Line No." := LastId + 1;
        ECSLVATReportLine.VALIDATE("Report No.",VATReportHeader."No.");
        ECSLVATReportLine.VALIDATE("Country Code",EUVATEntries.CountryCode);
        ECSLVATReportLine.VALIDATE("Customer VAT Reg. No.",EUVATEntries.VAT_Registration_No);
        ECSLVATReportLine.VALIDATE("Transaction Indicator",TrnIndicator);
        ECSLVATReportLine.INSERT;
      end;
    END;

    LOCAL PROCEDURE AddOrUpdateECLLine@7(EUVATEntries@1000 : Query "EU VAT Entries");
    VAR
      ECSLVATReportLine@1001 : Record "ECSL VAT Report Line";
    BEGIN
      GetECLLine(ECSLVATReportLine,EUVATEntries);
      ECSLVATReportLine."Total Value Of Supplies" += EUVATEntries.Base;
      AddToRepLineRelation(EUVATEntries,ECSLVATReportLine);
      ECSLVATReportLine.MODIFY(true);
    END;

    LOCAL PROCEDURE GetIndicatorCode@89(EU3rdPartyTrade@1000 : Boolean;EUService@1002 : Boolean) : Integer;
    BEGIN
      if EUService then
        exit(3);

      if EU3rdPartyTrade then
        exit(2);

      exit(0);
    END;

    LOCAL PROCEDURE AddToRepLineRelation@6(EUVATEntries@1000 : Query "EU VAT Entries";ECSLVATReportLine@1002 : Record "ECSL VAT Report Line");
    VAR
      ECSLVATReportLineRelation@1001 : Record "ECSL VAT Report Line Relation";
    BEGIN
      ECSLVATReportLineRelation.VALIDATE("ECSL Report No.",VATReportHeader."No.");
      ECSLVATReportLineRelation.VALIDATE("ECSL Line No.",ECSLVATReportLine."Line No.");
      ECSLVATReportLineRelation.VALIDATE("VAT Entry No.",EUVATEntries.Entry_No);
      ECSLVATReportLineRelation.INSERT(true);
    END;

    LOCAL PROCEDURE RowsTotalCorrection@90();
    VAR
      ECSLVATReportLine@1010 : Record "ECSL VAT Report Line";
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");

      if not ECSLVATReportLine.FINDSET then
        exit;
      repeat
        ECSLVATReportLine."Total Value Of Supplies" := -ROUND(ECSLVATReportLine."Total Value Of Supplies",1);
        ECSLVATReportLine.MODIFY(true);
      until ECSLVATReportLine.NEXT = 0;
    END;

    LOCAL PROCEDURE DeleteZeroAmountLines@4();
    VAR
      ECSLVATReportLine@1000 : Record "ECSL VAT Report Line";
      ECSLVATReportLineRelation@1001 : Record "ECSL VAT Report Line Relation";
    BEGIN
      ECSLVATReportLineRelation.SETRANGE("ECSL Report No.",ECSLVATReportLine."Report No.");

      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      ECSLVATReportLine.SETRANGE("Total Value Of Supplies",0);
      if not ECSLVATReportLine.FINDSET then
        exit;

      repeat
        ECSLVATReportLineRelation.SETRANGE("ECSL Line No.",ECSLVATReportLine."Line No.");
        ECSLVATReportLineRelation.DELETEALL;
      until ECSLVATReportLine.NEXT = 0;

      ECSLVATReportLine.DELETEALL;
    END;

    BEGIN
    END.
  }
}

