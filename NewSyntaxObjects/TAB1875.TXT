OBJECT Table 1875 Business Setup
{
  OBJECT-PROPERTIES
  {
    Date=20171006D;
    Time=120000T;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    DataPerCompany=false;
    CaptionML=ENU=Business Setup;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Text50        ;CaptionML=ENU=Name }
    { 2   ;   ;Description         ;Text250       ;CaptionML=ENU=Description }
    { 3   ;   ;Keywords            ;Text250       ;CaptionML=ENU=Keywords }
    { 4   ;   ;Setup Page ID       ;Integer       ;CaptionML=ENU=Setup Page ID }
    { 5   ;   ;Area                ;Option        ;CaptionML=ENU=Area;
                                                   OptionCaptionML=ENU=,General,Finance,Sales,Jobs,Fixed Assets,Purchasing,Reference Data,HR,Inventory,Service,System,Relationship Mngt,Intercompany;
                                                   OptionString=,General,Finance,Sales,Jobs,"Fixed Assets",Purchasing,"Reference Data",HR,Inventory,Service,System,"Relationship Mngt",Intercompany }
    { 7   ;   ;Icon                ;Media         ;CaptionML=ENU=Icon }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=true }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Description,Name,Icon                    }
  }
  CODE
  {

    [Integration]
    [External]
    PROCEDURE OnRegisterBusinessSetup@1(VAR TempBusinessSetup@1000 : TEMPORARY Record "Business Setup");
    BEGIN
    END;

    [Integration]
    [External]
    PROCEDURE OnOpenBusinessSetupPage@2(VAR TempBusinessSetup@1000 : TEMPORARY Record "Business Setup";VAR Handled@1001 : Boolean);
    BEGIN
    END;

    [External]
    PROCEDURE InsertBusinessSetup@4(VAR TempBusinessSetup@1003 : TEMPORARY Record "Business Setup";BusinessSetupName@1001 : Text[50];BusinessSetupDescription@1000 : Text[250];BusinessSetupKeywords@1002 : Text[250];BusinessSetupArea@1004 : Option;BusinessSetupRunPage@1006 : Integer;BusinessSetupIconFileName@1005 : Text[50]);
    VAR
      BusinessSetupIcon@1007 : Record "Business Setup Icon";
    BEGIN
      if TempBusinessSetup.GET(BusinessSetupName) then
        exit;

      TempBusinessSetup.INIT;
      TempBusinessSetup.Name := BusinessSetupName;
      TempBusinessSetup.Description := BusinessSetupDescription;
      TempBusinessSetup.Keywords := BusinessSetupKeywords;
      TempBusinessSetup."Setup Page ID" := BusinessSetupRunPage;
      TempBusinessSetup.Area := BusinessSetupArea;
      TempBusinessSetup.INSERT(true);

      if BusinessSetupIcon.GET(BusinessSetupIconFileName) then
        BusinessSetupIcon.GetIcon(TempBusinessSetup);
    END;

    [External]
    PROCEDURE InsertExtensionBusinessSetup@6(VAR TempBusinessSetup@1003 : TEMPORARY Record "Business Setup";BusinessSetupName@1001 : Text[50];BusinessSetupDescription@1000 : Text[250];BusinessSetupKeywords@1002 : Text[250];BusinessSetupArea@1004 : Option;BusinessSetupRunPage@1006 : Integer;ExtensionName@1005 : Text[250]);
    BEGIN
      if TempBusinessSetup.GET(BusinessSetupName) then
        exit;

      TempBusinessSetup.INIT;
      TempBusinessSetup.Name := BusinessSetupName;
      TempBusinessSetup.Description := BusinessSetupDescription;
      TempBusinessSetup.Keywords := BusinessSetupKeywords;
      TempBusinessSetup."Setup Page ID" := BusinessSetupRunPage;
      TempBusinessSetup.Area := BusinessSetupArea;
      TempBusinessSetup.INSERT(true);

      AddExtensionIconToBusinessSetup(TempBusinessSetup,ExtensionName);
    END;

    [Internal]
    PROCEDURE SetBusinessSetupIcon@3(VAR TempBusinessSetup@1001 : TEMPORARY Record "Business Setup";IconInStream@1000 : InStream);
    VAR
      BusinessSetupIcon@1002 : Record "Business Setup Icon";
    BEGIN
      if not BusinessSetupIcon.GET(TempBusinessSetup.Name) then begin
        BusinessSetupIcon.INIT;
        BusinessSetupIcon."Business Setup Name" := TempBusinessSetup.Name;
        BusinessSetupIcon.INSERT(true);
        BusinessSetupIcon.SetIconFromInstream(TempBusinessSetup.Name,IconInStream);
      end;

      BusinessSetupIcon.GetIcon(TempBusinessSetup);
    END;

    LOCAL PROCEDURE AddExtensionIconToBusinessSetup@5(VAR TempBusinessSetup@1001 : TEMPORARY Record "Business Setup";ExtensionName@1002 : Text);
    VAR
      BusinessSetupIcon@1005 : Record "Business Setup Icon";
      NAVApp@1003 : Record "NAV App";
      Media@1006 : Record Media;
      IconInStream@1000 : InStream;
    BEGIN
      if not BusinessSetupIcon.GET(TempBusinessSetup.Name) then begin
        NAVApp.SETRANGE(Name,ExtensionName);
        if not NAVApp.FINDFIRST then
          exit;
        Media.SETRANGE(ID,NAVApp.Logo.MEDIAID);
        if not Media.FINDFIRST then
          exit;
        Media.CALCFIELDS(Content);
        Media.Content.CREATEINSTREAM(IconInStream);

        BusinessSetupIcon.INIT;
        BusinessSetupIcon."Business Setup Name" := TempBusinessSetup.Name;
        BusinessSetupIcon.Icon.IMPORTSTREAM(IconInStream,TempBusinessSetup.Name);
        if BusinessSetupIcon.INSERT(true) then;
      end;

      TempBusinessSetup.Icon := BusinessSetupIcon.Icon;
      if TempBusinessSetup.MODIFY(true) then;
    END;

    BEGIN
    END.
  }
}

