OBJECT Codeunit 1400 DocumentNoVisibility
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE SalesDocumentNoIsVisible@2(DocType@1000 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Reminder,FinChMemo';DocNo@1003 : Code[20]) : Boolean;
    VAR
      NoSeries@1002 : Record 308;
      SalesNoSeriesSetup@1001 : Page 1401;
      DocNoSeries@1004 : Code[20];
    BEGIN
      IF DocNo <> '' THEN
        EXIT(FALSE);

      DocNoSeries := DetermineSalesSeriesNo(DocType);

      IF NOT NoSeries.GET(DocNoSeries) THEN BEGIN
        SalesNoSeriesSetup.SetFieldsVisibility(DocType);
        SalesNoSeriesSetup.RUNMODAL;
        DocNoSeries := DetermineSalesSeriesNo(DocType);
      END;

      EXIT(ForceShowNoSeriesForDocNo(DocNoSeries));
    END;

    [External]
    PROCEDURE PurchaseDocumentNoIsVisible@6(DocType@1000 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';DocNo@1003 : Code[20]) : Boolean;
    VAR
      NoSeries@1002 : Record 308;
      PurchaseNoSeriesSetup@1001 : Page 1402;
      DocNoSeries@1004 : Code[20];
    BEGIN
      IF DocNo <> '' THEN
        EXIT(FALSE);

      DocNoSeries := DeterminePurchaseSeriesNo(DocType);

      IF NOT NoSeries.GET(DocNoSeries) THEN BEGIN
        PurchaseNoSeriesSetup.SetFieldsVisibility(DocType);
        PurchaseNoSeriesSetup.RUNMODAL;
        DocNoSeries := DeterminePurchaseSeriesNo(DocType);
      END;

      EXIT(ForceShowNoSeriesForDocNo(DocNoSeries));
    END;

    [External]
    PROCEDURE TransferOrderNoIsVisible@24() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineTransferOrderSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE CustomerNoIsVisible@4() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineCustomerSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE VendorNoIsVisible@21() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineVendorSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE ItemNoIsVisible@22() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineItemSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE FixedAssetNoIsVisible@8() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineFixedAssetSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE EmployeeNoIsVisible@18() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineEmployeeSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE BankAccountNoIsVisible@23() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineBankAccountSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE ResourceNoIsVisible@26() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineResourceSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE JobNoIsVisible@27() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineJobSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE ContactNoIsVisible@28() : Boolean;
    VAR
      NoSeriesCode@1004 : Code[20];
    BEGIN
      NoSeriesCode := DetermineContactSeriesNo;
      EXIT(ForceShowNoSeriesForDocNo(NoSeriesCode));
    END;

    [External]
    PROCEDURE CustomerNoSeriesIsDefault@10() : Boolean;
    VAR
      NoSeries@1000 : Record 308;
    BEGIN
      IF NoSeries.GET(DetermineCustomerSeriesNo) THEN
        EXIT(NoSeries."Default Nos.");

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE VendorNoSeriesIsDefault@15() : Boolean;
    VAR
      NoSeries@1000 : Record 308;
    BEGIN
      IF NoSeries.GET(DetermineVendorSeriesNo) THEN
        EXIT(NoSeries."Default Nos.");

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE ItemNoSeriesIsDefault@16() : Boolean;
    VAR
      NoSeries@1000 : Record 308;
    BEGIN
      IF NoSeries.GET(DetermineItemSeriesNo) THEN
        EXIT(NoSeries."Default Nos.");
    END;

    [External]
    PROCEDURE TransferOrderNoSeriesIsDefault@20() : Boolean;
    VAR
      NoSeries@1000 : Record 308;
    BEGIN
      IF NoSeries.GET(DetermineTransferOrderSeriesNo) THEN
        EXIT(NoSeries."Default Nos.");
    END;

    [External]
    PROCEDURE FixedAssetNoSeriesIsDefault@11() : Boolean;
    VAR
      NoSeries@1000 : Record 308;
    BEGIN
      IF NoSeries.GET(DetermineFixedAssetSeriesNo) THEN
        EXIT(NoSeries."Default Nos.");
    END;

    [External]
    PROCEDURE EmployeeNoSeriesIsDefault@19() : Boolean;
    VAR
      NoSeries@1000 : Record 308;
    BEGIN
      IF NoSeries.GET(DetermineEmployeeSeriesNo) THEN
        EXIT(NoSeries."Default Nos.");
    END;

    LOCAL PROCEDURE DetermineSalesSeriesNo@5(DocType@1000 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Reminder,FinChMemo') : Code[20];
    VAR
      SalesReceivablesSetup@1001 : Record 311;
    BEGIN
      SalesReceivablesSetup.GET;
      CASE DocType OF
        DocType::Quote:
          EXIT(SalesReceivablesSetup."Quote Nos.");
        DocType::Order:
          EXIT(SalesReceivablesSetup."Order Nos.");
        DocType::Invoice:
          EXIT(SalesReceivablesSetup."Invoice Nos.");
        DocType::"Credit Memo":
          EXIT(SalesReceivablesSetup."Credit Memo Nos.");
        DocType::"Blanket Order":
          EXIT(SalesReceivablesSetup."Blanket Order Nos.");
        DocType::"Return Order":
          EXIT(SalesReceivablesSetup."Return Order Nos.");
        DocType::Reminder:
          EXIT(SalesReceivablesSetup."Reminder Nos.");
        DocType::FinChMemo:
          EXIT(SalesReceivablesSetup."Fin. Chrg. Memo Nos.");
      END;
    END;

    LOCAL PROCEDURE DeterminePurchaseSeriesNo@1(DocType@1000 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order') : Code[20];
    VAR
      PurchasesPayablesSetup@1001 : Record 312;
    BEGIN
      PurchasesPayablesSetup.GET;
      CASE DocType OF
        DocType::Quote:
          EXIT(PurchasesPayablesSetup."Quote Nos.");
        DocType::Order:
          EXIT(PurchasesPayablesSetup."Order Nos.");
        DocType::Invoice:
          EXIT(PurchasesPayablesSetup."Invoice Nos.");
        DocType::"Credit Memo":
          EXIT(PurchasesPayablesSetup."Credit Memo Nos.");
        DocType::"Blanket Order":
          EXIT(PurchasesPayablesSetup."Blanket Order Nos.");
        DocType::"Return Order":
          EXIT(PurchasesPayablesSetup."Return Order Nos.");
      END;
    END;

    LOCAL PROCEDURE DetermineTransferOrderSeriesNo@14() : Code[20];
    VAR
      InventorySetup@1000 : Record 313;
    BEGIN
      InventorySetup.GET;
      EXIT(InventorySetup."Transfer Order Nos.");
    END;

    LOCAL PROCEDURE DetermineCustomerSeriesNo@7() : Code[20];
    VAR
      SalesReceivablesSetup@1001 : Record 311;
    BEGIN
      SalesReceivablesSetup.GET;
      EXIT(SalesReceivablesSetup."Customer Nos.");
    END;

    LOCAL PROCEDURE DetermineVendorSeriesNo@12() : Code[20];
    VAR
      PurchasesPayablesSetup@1001 : Record 312;
    BEGIN
      PurchasesPayablesSetup.GET;
      EXIT(PurchasesPayablesSetup."Vendor Nos.");
    END;

    LOCAL PROCEDURE DetermineItemSeriesNo@17() : Code[20];
    VAR
      InventorySetup@1001 : Record 313;
    BEGIN
      InventorySetup.GET;
      EXIT(InventorySetup."Item Nos.");
    END;

    LOCAL PROCEDURE DetermineFixedAssetSeriesNo@9() : Code[20];
    VAR
      FASetup@1001 : Record 5603;
    BEGIN
      FASetup.GET;
      EXIT(FASetup."Fixed Asset Nos.");
    END;

    LOCAL PROCEDURE DetermineEmployeeSeriesNo@13() : Code[20];
    VAR
      HumanResourcesSetup@1001 : Record 5218;
    BEGIN
      HumanResourcesSetup.GET;
      EXIT(HumanResourcesSetup."Employee Nos.");
    END;

    LOCAL PROCEDURE DetermineBankAccountSeriesNo@25() : Code[20];
    VAR
      GeneralLedgerSetup@1001 : Record 98;
    BEGIN
      GeneralLedgerSetup.GET;
      EXIT(GeneralLedgerSetup."Bank Account Nos.");
    END;

    LOCAL PROCEDURE DetermineResourceSeriesNo@29() : Code[20];
    VAR
      ResourcesSetup@1001 : Record 314;
    BEGIN
      ResourcesSetup.GET;
      EXIT(ResourcesSetup."Resource Nos.");
    END;

    LOCAL PROCEDURE DetermineJobSeriesNo@30() : Code[20];
    VAR
      JobsSetup@1001 : Record 315;
    BEGIN
      JobsSetup.GET;
      EXIT(JobsSetup."Job Nos.");
    END;

    LOCAL PROCEDURE DetermineContactSeriesNo@31() : Code[20];
    VAR
      MarketingSetup@1001 : Record 5079;
    BEGIN
      MarketingSetup.GET;
      EXIT(MarketingSetup."Contact Nos.");
    END;

    LOCAL PROCEDURE ForceShowNoSeriesForDocNo@3(NoSeriesCode@1004 : Code[20]) : Boolean;
    VAR
      NoSeries@1005 : Record 308;
      NoSeriesRelationship@1001 : Record 310;
      NoSeriesMgt@1000 : Codeunit 396;
      SeriesDate@1002 : Date;
    BEGIN
      IF NOT NoSeries.GET(NoSeriesCode) THEN
        EXIT(TRUE);

      SeriesDate := WORKDATE;
      NoSeriesRelationship.SETRANGE(Code,NoSeriesCode);
      IF NOT NoSeriesRelationship.ISEMPTY THEN
        EXIT(TRUE);

      IF NoSeries."Manual Nos." OR (NoSeries."Default Nos." = FALSE) THEN
        EXIT(TRUE);

      EXIT(NoSeriesMgt.GetNextNo3(NoSeriesCode,SeriesDate,FALSE,TRUE) = '');
    END;

    BEGIN
    END.
  }
}

