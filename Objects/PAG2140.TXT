OBJECT Page 2140 O365 Sync with Microsoft Apps
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sync with Microsoft Apps;
    OnInit=BEGIN
             UsernameVisible := CURRENTCLIENTTYPE <> CLIENTTYPE::Phone;
           END;

    OnOpenPage=VAR
                 MarketingSetup@1000 : Record 5079;
               BEGIN
                 IF MarketingSetup.GET THEN
                   EnableSync := MarketingSetup."Sync with Microsoft Graph";
                 SetUserName;
               END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                CaptionML=ENU=Microsoft Sync;
                ContainerType=ContentArea }

    { 7   ;1   ;Group     ;
                GroupType=Group }

    { 2   ;2   ;Field     ;
                CaptionML=ENU=Enable Synchronization;
                ToolTipML=ENU=Specifies whether Microsoft synchronization is enabled.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=EnableSync;
                OnValidate=VAR
                             MarketingSetup@1004 : Record 5079;
                             CompanyInformation@1003 : Record 79;
                             AzureADUserManagement@1002 : Codeunit 9010;
                             WebhookManagement@1005 : Codeunit 5377;
                             GraphSyncRunner@1001 : Codeunit 5452;
                             TenantDetail@1000 : DotNet "'Microsoft.Azure.ActiveDirectory.GraphClient, Version=1.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Azure.ActiveDirectory.GraphClient.TenantDetail";
                           BEGIN
                             WITH MarketingSetup DO BEGIN
                               IF NOT GET THEN
                                 INSERT(TRUE);
                               VALIDATE("Sync with Microsoft Graph",EnableSync);
                               MODIFY(TRUE);
                             END;

                             SetUserName;

                             IF NOT EnableSync THEN
                               EXIT;

                             IF NOT WebhookManagement.IsCurrentClientTypeAllowed THEN
                               EXIT;

                             IF NOT WebhookManagement.IsSyncAllowed THEN
                               EXIT;

                             IF NOT GraphSyncRunner.IsGraphSyncEnabled THEN
                               EXIT;

                             CompanyInformation.GET;
                             IF CompanyInformation."Sync with O365 Bus. profile" THEN
                               TASKSCHEDULER.CREATETASK(CODEUNIT::"Business Profile Sync. Runner",0,TRUE,COMPANYNAME,0DT)
                             ELSE
                               IF AzureADUserManagement.GetTenantDetail(TenantDetail) THEN BEGIN
                                 CompanyInformation.LOCKTABLE;
                                 CompanyInformation.Name := COPYSTR(TenantDetail.DisplayName,1,MAXSTRLEN(CompanyInformation.Name));
                                 CompanyInformation.Address := COPYSTR(TenantDetail.Street,1,MAXSTRLEN(CompanyInformation.Address));
                                 CompanyInformation.City := COPYSTR(TenantDetail.City,1,MAXSTRLEN(CompanyInformation.City));
                                 CompanyInformation."Post Code" := COPYSTR(TenantDetail.PostalCode,1,MAXSTRLEN(CompanyInformation."Post Code"));
                                 CompanyInformation.County := COPYSTR(TenantDetail.State,1,MAXSTRLEN(CompanyInformation.County));
                                 CompanyInformation."Country/Region Code" :=
                                   COPYSTR(TenantDetail.CountryLetterCode,1,MAXSTRLEN(CompanyInformation."Country/Region Code"));
                                 CompanyInformation.MODIFY(FALSE);
                               END;
                           END;
                            }

    { 6   ;2   ;Field     ;
                Name=SyncUser;
                CaptionML=ENU=User;
                ToolTipML=ENU=Specifies the user who owns the synchronization jobs.;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=UserName;
                Visible=UsernameVisible;
                Editable=FALSE;
                OnAssistEdit=VAR
                               SelectedUser@1001 : Record 2000000120;
                               MarketingSetup@1000 : Record 5079;
                             BEGIN
                               IF NOT EnableSync THEN
                                 EXIT;

                               SelectedUser.SETFILTER("License Type",'<> %1',SelectedUser."License Type"::"External User");
                               IF NOT (PAGE.RUNMODAL(PAGE::Users,SelectedUser) = ACTION::LookupOK) THEN
                                 EXIT;

                               MarketingSetup.GET;
                               IF MarketingSetup.TrySetWebhookSubscriptionUser(SelectedUser."User Security ID") THEN BEGIN
                                 MarketingSetup.MODIFY(TRUE);
                                 UserName := SelectedUser."User Name";
                               END
                             END;
                              }

  }
  CODE
  {
    VAR
      EnableSync@1000 : Boolean;
      UserName@1003 : Text;
      UsernameVisible@1001 : Boolean;

    LOCAL PROCEDURE SetUserName@15();
    VAR
      MarketingSetup@1002 : Record 5079;
      User@1001 : Record 2000000120;
    BEGIN
      IF EnableSync THEN BEGIN
        IF User.GET(MarketingSetup.GetWebhookSubscriptionUser) THEN
          UserName := User."User Name";
      END ELSE
        UserName := '';
    END;

    BEGIN
    END.
  }
}

