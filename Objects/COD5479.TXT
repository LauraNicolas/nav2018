OBJECT Codeunit 5479 Graph Mgt - Customer Payments
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetDefaultTemplateName@8() : Text[10];
    VAR
      GenJnlTemplate@1000 : Record 80;
    BEGIN
      GenJnlTemplate.RESET;
      GenJnlTemplate.SETRANGE("Page ID",PAGE::"Cash Receipt Journal");
      GenJnlTemplate.SETRANGE(Recurring,FALSE);
      GenJnlTemplate.SETRANGE(Type,3);
      GenJnlTemplate.FINDFIRST;
      EXIT(GenJnlTemplate.Name);
    END;

    PROCEDURE SetCustomerPaymentsBatch@1(VAR GenJournalLine@1001 : Record 81);
    VAR
      APIEntitiesSetup@1002 : Record 5466;
      GenJnlBatch@1000 : Record 232;
    BEGIN
      APIEntitiesSetup.SafeGet;
      GenJnlBatch.GET(GetDefaultTemplateName,APIEntitiesSetup."Customer Payments Batch Name");

      GenJournalLine.VALIDATE("Journal Template Name",GetDefaultTemplateName);
      GenJournalLine.VALIDATE("Journal Batch Name",APIEntitiesSetup."Customer Payments Batch Name");
      GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");
      GenJournalLine.SETRANGE("Journal Batch Name",GenJournalLine."Journal Batch Name");
    END;

    PROCEDURE SetCustomerPaymentsFilters@9(VAR GenJournalLine@1000 : Record 81);
    VAR
      APIEntitiesSetup@1001 : Record 5466;
    BEGIN
      APIEntitiesSetup.SafeGet;
      GenJournalLine.SETRANGE("Document Type",GenJournalLine."Document Type"::Payment);
      GenJournalLine.SETRANGE("Account Type",GenJournalLine."Account Type"::Customer);
      GenJournalLine.SETRANGE("Applies-to Doc. Type",GenJournalLine."Applies-to Doc. Type"::Invoice);
      GenJournalLine.SETRANGE("Journal Template Name",GetDefaultTemplateName);
      GenJournalLine.SETRANGE("Journal Batch Name",APIEntitiesSetup."Customer Payments Batch Name");
    END;

    PROCEDURE SetCustomerPaymentsValues@10(VAR GenJournalLine@1000 : Record 81;TempGenJournalLine@1001 : TEMPORARY Record 81);
    VAR
      DummyDate@1002 : Date;
    BEGIN
      GenJournalLine.VALIDATE("Account Type",GenJournalLine."Account Type"::Customer);
      IF NOT ISNULLGUID(TempGenJournalLine."Account Id") THEN
        GenJournalLine.VALIDATE("Account Id",TempGenJournalLine."Account Id");
      GenJournalLine.VALIDATE("Account No.",TempGenJournalLine."Account No.");
      IF TempGenJournalLine."Contact Graph Id" <> '' THEN
        GenJournalLine.VALIDATE("Contact Graph Id",TempGenJournalLine."Contact Graph Id");
      IF TempGenJournalLine."Posting Date" <> DummyDate THEN
        GenJournalLine.VALIDATE("Posting Date",TempGenJournalLine."Posting Date");
      GenJournalLine.VALIDATE("Document Type",GenJournalLine."Document Type"::Payment);
      GenJournalLine.VALIDATE("Document No.",TempGenJournalLine."Document No.");
      GenJournalLine.VALIDATE("External Document No.",TempGenJournalLine."External Document No.");
      GenJournalLine.VALIDATE(Amount,TempGenJournalLine.Amount);
      GenJournalLine.VALIDATE("Applies-to Doc. Type",GenJournalLine."Applies-to Doc. Type"::Invoice);
      GenJournalLine."Applies-to Doc. No." := TempGenJournalLine."Applies-to Doc. No.";
      IF NOT ISNULLGUID(TempGenJournalLine."Applies-to Invoice Id") THEN
        GenJournalLine.VALIDATE("Applies-to Invoice Id",TempGenJournalLine."Applies-to Invoice Id");
      IF TempGenJournalLine.Description <> '' THEN
        GenJournalLine.VALIDATE(Description,TempGenJournalLine.Description);
      GenJournalLine.VALIDATE(Comment,TempGenJournalLine.Comment);
      GenJournalLine.VALIDATE("Bal. Account No.",'');
    END;

    PROCEDURE UpdateIntegrationRecords@2(OnlyItemsWithoutId@1000 : Boolean);
    VAR
      GenJnlLine@1003 : Record 81;
      GraphMgtGeneralTools@1002 : Codeunit 5465;
      GenJnlLineRecordRef@1001 : RecordRef;
    BEGIN
      GenJnlLineRecordRef.OPEN(DATABASE::"Gen. Journal Line");
      GraphMgtGeneralTools.UpdateIntegrationRecords(GenJnlLineRecordRef,GenJnlLine.FIELDNO(Id),OnlyItemsWithoutId);
    END;

    LOCAL PROCEDURE EnableAccountODataWebService@1008();
    BEGIN
      UpdateIntegrationRecords(FALSE);
    END;

    PROCEDURE UpdateCustomerPaymentsCustomerId@11();
    VAR
      GenJournalLine@1003 : Record 81;
    BEGIN
      GenJournalLine.SETRANGE("Account Type",GenJournalLine."Account Type"::Customer);

      IF GenJournalLine.FINDSET THEN
        REPEAT
          GenJournalLine.UpdateCustomerID;
          GenJournalLine.MODIFY(FALSE);
        UNTIL GenJournalLine.NEXT = 0;
    END;

    PROCEDURE UpdateCustomerPaymentsAppliesToDocId@3();
    VAR
      GenJournalLine@1003 : Record 81;
    BEGIN
      GenJournalLine.SETRANGE("Applies-to Doc. Type",GenJournalLine."Applies-to Doc. Type"::Invoice);

      IF GenJournalLine.FINDSET THEN
        REPEAT
          GenJournalLine.UpdateAppliesToInvoiceID;
          GenJournalLine.MODIFY(FALSE);
        UNTIL GenJournalLine.NEXT = 0;
    END;

    [EventSubscriber(Codeunit,5150,OnUpdateReferencedIdField)]
    LOCAL PROCEDURE HandleUpdateReferencedIdFieldOnCustomerPayments@1016(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean);
    VAR
      DummyGenJnlLine@1004 : Record 81;
      GraphMgtGeneralTools@1003 : Codeunit 5465;
    BEGIN
      GraphMgtGeneralTools.HandleUpdateReferencedIdFieldOnItem(
        RecRef,NewId,Handled,DATABASE::"Gen. Journal Line",DummyGenJnlLine.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5150,OnGetPredefinedIdValue)]
    LOCAL PROCEDURE HandleGetPredefinedIdValue@1017(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean);
    VAR
      DummyGenJnlLine@1004 : Record 81;
      GraphMgtGeneralTools@1003 : Codeunit 5465;
    BEGIN
      GraphMgtGeneralTools.HandleGetPredefinedIdValue(
        Id,RecRef,Handled,DATABASE::"Gen. Journal Line",DummyGenJnlLine.FIELDNO(Id));
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@1003();
    BEGIN
      EnableAccountODataWebService;
      UpdateCustomerPaymentsCustomerId;
      UpdateCustomerPaymentsAppliesToDocId;
    END;

    BEGIN
    END.
  }
}

