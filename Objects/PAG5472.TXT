OBJECT Page 5472 Vendor Entity
{
  OBJECT-PROPERTIES
  {
    Date=06.10.17;
    Time=12:00:00;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=vendors];
    SourceTable=Table23;
    DelayedInsert=Yes;
    PageType=API;
    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     Vendor@1002 : Record 23;
                     GraphMgtVendor@1000 : Codeunit 5472;
                     GraphMgtGeneralTools@1004 : Codeunit 5465;
                     RecRef@1003 : RecordRef;
                   BEGIN
                     Vendor.SETRANGE("No.","No.");
                     IF NOT Vendor.ISEMPTY THEN
                       INSERT;

                     INSERT(TRUE);

                     GraphMgtVendor.ProcessComplexTypes(Rec,PaymentTermsJSON,PaymentMethodJSON,PostalAddressJSON);

                     RecRef.GETTABLE(Rec);
                     GraphMgtGeneralTools.ProcessNewRecordFromAPI(RecRef,TempFieldSet,CURRENTDATETIME);
                     RecRef.SETTABLE(Rec);

                     MODIFY(TRUE);
                     SetCalculatedFields;
                     EXIT(FALSE);
                   END;

    OnModifyRecord=VAR
                     Vendor@1002 : Record 23;
                     GraphMgtGeneralTools@1001 : Codeunit 5465;
                     GraphMgtVendor@1000 : Codeunit 5472;
                   BEGIN
                     IF xRec.Id <> Id THEN
                       GraphMgtGeneralTools.ErrorIdImmutable;
                     Vendor.SETRANGE(Id,Id);
                     Vendor.FINDFIRST;

                     GraphMgtVendor.ProcessComplexTypes(Rec,PaymentTermsJSON,PaymentMethodJSON,PostalAddressJSON);

                     IF "No." = Vendor."No." THEN
                       MODIFY(TRUE)
                     ELSE BEGIN
                       Vendor.TRANSFERFIELDS(Rec,FALSE);
                       Vendor.RENAME("No.");
                       TRANSFERFIELDS(Vendor);
                     END;

                     SetCalculatedFields;
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 14  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id];
                ApplicationArea=#All;
                SourceExpr=Id }

    { 13  ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number];
                ApplicationArea=#All;
                SourceExpr="No." }

    { 12  ;2   ;Field     ;
                Name=displayName;
                CaptionML=[@@@={Locked};
                           ENU=DisplayName];
                ApplicationArea=#All;
                SourceExpr=Name;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Name));
                           END;
                            }

    { 7   ;2   ;Field     ;
                Name=address;
                CaptionML=[@@@={Locked};
                           ENU=Address];
                ToolTipML=ENU=Specifies the address for the vendor.;
                ApplicationArea=#All;
                SourceExpr=PostalAddressJSON;
                ODataEDMType=POSTALADDRESS }

    { 5   ;2   ;Field     ;
                Name=phoneNumber;
                CaptionML=[@@@={Locked};
                           ENU=PhoneNumber];
                ApplicationArea=#All;
                SourceExpr="Phone No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Phone No."));
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=email;
                CaptionML=[@@@={Locked};
                           ENU=Email];
                ApplicationArea=#All;
                SourceExpr="E-Mail";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("E-Mail"));
                           END;
                            }

    { 3   ;2   ;Field     ;
                Name=website;
                CaptionML=[@@@={Locked};
                           ENU=Website];
                ApplicationArea=#All;
                SourceExpr="Home Page";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Home Page"));
                           END;
                            }

    { 15  ;2   ;Field     ;
                Name=taxRegistrationNumber;
                CaptionML=[@@@={Locked};
                           ENU=TaxRegistrationNumber];
                ApplicationArea=#All;
                SourceExpr="VAT Registration No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("VAT Registration No."));
                           END;
                            }

    { 16  ;2   ;Field     ;
                Name=currencyId;
                CaptionML=[@@@={Locked};
                           ENU=CurrencyId];
                ApplicationArea=#All;
                SourceExpr="Currency Id";
                OnValidate=BEGIN
                             IF "Currency Id" = BlankGUID THEN
                               "Currency Code" := ''
                             ELSE BEGIN
                               Currency.SETRANGE(Id,"Currency Id");
                               IF NOT Currency.FINDFIRST THEN
                                 ERROR(CurrencyIdDoesNotMatchACurrencyErr);

                               "Currency Code" := Currency.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;
                            }

    { 11  ;2   ;Field     ;
                Name=currencyCode;
                CaptionML=[@@@={Locked};
                           ENU=CurrencyCode];
                ApplicationArea=#All;
                SourceExpr=CurrencyCodeTxt;
                OnValidate=BEGIN
                             "Currency Code" :=
                               GraphMgtGeneralTools.TranslateCurrencyCodeToNAVCurrencyCode(
                                 LCYCurrencyCode,COPYSTR(CurrencyCodeTxt,1,MAXSTRLEN(LCYCurrencyCode)));

                             IF Currency.Code <> '' THEN BEGIN
                               IF Currency.Code <> "Currency Code" THEN
                                 ERROR(CurrencyValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Currency Code" = '' THEN
                               "Currency Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT Currency.GET("Currency Code") THEN
                                 ERROR(CurrencyCodeDoesNotMatchACurrencyErr);

                               "Currency Id" := Currency.Id;
                             END;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;
                            }

    { 17  ;2   ;Field     ;
                Name=irs1099Code;
                CaptionML=[@@@={Locked};
                           ENU=IRS1099Code];
                ApplicationArea=#All;
                SourceExpr=IRS1099Code }

    { 9   ;2   ;Field     ;
                Name=paymentTermsId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentTermsId];
                ApplicationArea=#All;
                SourceExpr="Payment Terms Id";
                OnValidate=BEGIN
                             IF "Payment Terms Id" = BlankGUID THEN
                               "Payment Terms Code" := ''
                             ELSE BEGIN
                               PaymentTerms.SETRANGE(Id,"Payment Terms Id");
                               IF NOT PaymentTerms.FINDFIRST THEN
                                 ERROR(PaymentTermsIdDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Code" := PaymentTerms.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;
                            }

    { 8   ;2   ;Field     ;
                Name=paymentTerms;
                CaptionML=[@@@={Locked};
                           ENU=PaymentTerms];
                ToolTipML=ENU=Specifies the Payment Terms for the Customer.;
                ApplicationArea=#All;
                SourceExpr=PaymentTermsJSON;
                OnValidate=BEGIN
                             GraphMgtVendor.UpdatePaymentTerms(PaymentTermsJSON,Rec);

                             IF PaymentTerms.Code <> '' THEN BEGIN
                               IF PaymentTerms.Code <> "Payment Terms Code" THEN
                                 ERROR(PaymentTermsValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Payment Terms Code" = '' THEN
                               "Payment Terms Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT PaymentTerms.GET("Payment Terms Code") THEN
                                 ERROR(PaymentTermsCodeDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Id" := PaymentTerms.Id;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;

                ODataEDMType=PAYMENTTERMSTYPE }

    { 10  ;2   ;Field     ;
                Name=paymentMethodId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentMethodId];
                ApplicationArea=#All;
                SourceExpr="Payment Method Id";
                OnValidate=BEGIN
                             IF "Payment Method Id" = BlankGUID THEN
                               "Payment Method Code" := ''
                             ELSE BEGIN
                               PaymentMethod.SETRANGE(Id,"Payment Method Id");
                               IF NOT PaymentMethod.FINDFIRST THEN
                                 ERROR(PaymentMethodIdDoesNotMatchAPaymentMethodErr);

                               "Payment Method Code" := PaymentMethod.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Method Id"));
                             RegisterFieldSet(FIELDNO("Payment Method Code"));
                           END;
                            }

    { 18  ;2   ;Field     ;
                Name=paymentMethod;
                CaptionML=[@@@={Locked};
                           ENU=PaymentMethod];
                ToolTipML=ENU=Specifies the Payment Method for the Vendor.;
                ApplicationArea=#All;
                SourceExpr=PaymentMethodJSON;
                OnValidate=BEGIN
                             GraphMgtVendor.UpdatePaymentMethod(PaymentMethodJSON,Rec);

                             IF PaymentMethod.Code <> '' THEN BEGIN
                               IF PaymentMethod.Code <> "Payment Method Code" THEN
                                 ERROR(PaymentMethodValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Payment Method Code" = '' THEN
                               "Payment Method Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT PaymentMethod.GET("Payment Method Code") THEN
                                 ERROR(PaymentMethodCodeDoesNotMatchAPaymentMethodErr);

                               "Payment Method Id" := PaymentMethod.Id;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Method Id"));
                             RegisterFieldSet(FIELDNO("Payment Method Code"));
                           END;

                ODataEDMType=PAYMENTMETHOD }

    { 22  ;2   ;Field     ;
                Name=taxLiable;
                CaptionML=[@@@={Locked};
                           ENU=TaxLiable];
                ApplicationArea=#All;
                SourceExpr="Tax Liable";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Tax Liable"));
                           END;
                            }

    { 23  ;2   ;Field     ;
                Name=blocked;
                CaptionML=[@@@={Locked};
                           ENU=Blocked];
                ApplicationArea=#All;
                SourceExpr=Blocked;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Blocked));
                           END;
                            }

    { 24  ;2   ;Field     ;
                Name=balance;
                CaptionML=[@@@={Locked};
                           ENU=Balance];
                ApplicationArea=#All;
                SourceExpr="Balance (LCY)" }

    { 6   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time" }

  }
  CODE
  {
    VAR
      Currency@1018 : Record 4;
      PaymentTerms@1011 : Record 3;
      PaymentMethod@1016 : Record 289;
      TempFieldSet@1003 : TEMPORARY Record 2000000041;
      GraphMgtGeneralTools@1006 : Codeunit 5465;
      GraphMgtVendor@1012 : Codeunit 5472;
      LCYCurrencyCode@1007 : Code[10];
      CurrencyCodeTxt@1008 : Text;
      PaymentTermsJSON@1002 : Text;
      PaymentMethodJSON@1000 : Text;
      PostalAddressJSON@1004 : Text;
      IRS1099Code@1001 : Code[10];
      CurrencyValuesDontMatchErr@1021 : TextConst '@@@={Locked};ENU=The currency values do not match to a specific Currency.';
      CurrencyIdDoesNotMatchACurrencyErr@1020 : TextConst '@@@={Locked};ENU=The "currencyId" does not match to a Currency.';
      CurrencyCodeDoesNotMatchACurrencyErr@1019 : TextConst '@@@={Locked};ENU=The "currencyCode" does not match to a Currency.';
      PaymentTermsValuesDontMatchErr@1010 : TextConst '@@@={Locked};ENU=The payment terms values do not match to a specific Payment Terms.';
      PaymentTermsIdDoesNotMatchAPaymentTermsErr@1009 : TextConst '@@@={Locked};ENU=The "paymentTermsId" does not match to a Payment Terms.';
      PaymentTermsCodeDoesNotMatchAPaymentTermsErr@1005 : TextConst '@@@={Locked};ENU=The "paymentTermsCode" does not match to a Payment Terms.';
      PaymentMethodValuesDontMatchErr@1015 : TextConst '@@@={Locked};ENU=The payment method values do not match to a specific Payment Method.';
      PaymentMethodIdDoesNotMatchAPaymentMethodErr@1014 : TextConst '@@@={Locked};ENU=The "paymentMethodId" does not match to a Payment Method.';
      PaymentMethodCodeDoesNotMatchAPaymentMethodErr@1013 : TextConst '@@@={Locked};ENU=The "paymentMethodCode" does not match to a Payment Method.';
      BlankGUID@1017 : GUID;

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      GraphMgtVendor@1000 : Codeunit 5472;
    BEGIN
      CALCFIELDS("Balance (LCY)");
      PaymentTermsJSON := GraphMgtVendor.PaymentTermsToJSON("Payment Terms Code");
      PaymentMethodJSON := GraphMgtVendor.PaymentMethodToJSON("Payment Method Code");
      PostalAddressJSON := GraphMgtVendor.PostalAddressToJSON(Rec);
      CurrencyCodeTxt := GraphMgtGeneralTools.TranslateNAVCurrencyCodeToCurrencyCode(LCYCurrencyCode,"Currency Code");
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(Id);
      CLEAR(PaymentTermsJSON);
      CLEAR(PaymentMethodJSON);
      CLEAR(PostalAddressJSON);
      CLEAR(IRS1099Code);
      TempFieldSet.DELETEALL;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      IF TempFieldSet.GET(DATABASE::Vendor,FieldNo) THEN
        EXIT;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::Vendor;
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

