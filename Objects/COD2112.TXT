OBJECT Codeunit 2112 O365 Sales Attachment Mgt
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE GetNoOfAttachments@124(RecordVariant@1002 : Variant) : Integer;
    VAR
      IncomingDocumentAttachment@1000 : Record 133;
      SalesHeader@1001 : Record 36;
      SalesInvoiceHeader@1003 : Record 112;
      RecordRef@1004 : RecordRef;
    BEGIN
      RecordRef.GETTABLE(RecordVariant);
      IF RecordRef.NUMBER = DATABASE::"Sales Header" THEN BEGIN
        SalesHeader := RecordVariant;
        IF SalesHeader."Incoming Document Entry No." = 0 THEN
          EXIT(0);
        IncomingDocumentAttachment.SETRANGE("Incoming Document Entry No.",SalesHeader."Incoming Document Entry No.");
        EXIT(IncomingDocumentAttachment.COUNT);
      END;
      IF RecordRef.NUMBER = DATABASE::"Sales Invoice Header" THEN BEGIN
        SalesInvoiceHeader := RecordVariant;
        IncomingDocumentAttachment.SETRANGE("Document No.",SalesInvoiceHeader."No.");
        IncomingDocumentAttachment.SETRANGE("Posting Date",SalesInvoiceHeader."Posting Date");
        EXIT(IncomingDocumentAttachment.COUNT);
      END;
    END;

    [External]
    PROCEDURE EditAttachments@123(RecordVariant@1000 : Variant) NoOfAttachments : Integer;
    VAR
      IncomingDocument@1001 : Record 130;
      SalesHeader@1003 : Record 36;
      RecordRef@1004 : RecordRef;
    BEGIN
      RecordRef.GETTABLE(RecordVariant);
      IF NOT RecordRef.FIND THEN
        EXIT;
      RecordVariant := RecordRef;
      IF RecordRef.NUMBER = DATABASE::"Sales Header" THEN BEGIN
        SalesHeader := RecordVariant;
        IF SalesHeader."Incoming Document Entry No." = 0 THEN BEGIN
          SalesHeader."Incoming Document Entry No." := IncomingDocument.CreateIncomingDocument(SalesHeader."Sell-to Customer Name",'');
          SalesHeader.MODIFY;
          COMMIT;
          RecordVariant := SalesHeader;
        END;
        PAGE.RUN(PAGE::"O365 Sales Doc. Attachments",SalesHeader);
      END;
      IF RecordRef.NUMBER = DATABASE::"Sales Invoice Header" THEN
        PAGE.RUN(PAGE::"O365 Posted Sales Inv. Att.",RecordVariant);
      NoOfAttachments := GetNoOfAttachments(RecordVariant);
    END;

    BEGIN
    END.
  }
}

