OBJECT Codeunit 5461 Graph Int. - Contact
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      BusinessTypeMissingErr@1000 : TextConst 'ENU=BusinessType is not present on Graph Contact.';
      BusinessTypeUnknownValueErr@1001 : TextConst '@@@=%1 can be anything;ENU=BusinessType has an invalid value: %1.';
      BusinessCategoryMissingErr@1002 : TextConst 'ENU=The Graph Contact must have 1 or more business category fields.';
      BusinessCategoryWrongValueErr@1003 : TextConst '@@@=%1 will be the value. %2 is the PropertyId string including the value;ENU=The business category has an invalid value: %1.\%2.';
      NoBusinessCategoryErr@1004 : TextConst 'ENU=At least one business category must be set to True.';
      CannotGetGraphXrmIdErr@1005 : TextConst '@@@={Locked};ENU=Cannot find the graph XrmId for graph record id %1.';

    [EventSubscriber(Codeunit,5345,OnAfterTransferRecordFields)]
    LOCAL PROCEDURE OnAfterTransferRecordFields@35(VAR SourceRecordRef@1002 : RecordRef;VAR DestinationRecordRef@1001 : RecordRef;VAR AdditionalFieldsWereModified@1000 : Boolean);
    VAR
      Contact@1003 : Record 5050;
      GraphContact@1004 : Record 5450;
    BEGIN
      CASE GetSourceDestCode(SourceRecordRef,DestinationRecordRef) OF
        'Contact-Graph Contact':
          BEGIN
            DestinationRecordRef.SETTABLE(GraphContact);
            SourceRecordRef.SETTABLE(Contact);
            SetContactFieldsOnGraph(Contact,GraphContact);
            SetIntegrationIdFieldOnGraph(Contact,GraphContact);
            AdditionalFieldsWereModified := TRUE;
            DestinationRecordRef.GETTABLE(GraphContact);
          END;
      END;
    END;

    [EventSubscriber(Codeunit,5345,OnBeforeInsertRecord)]
    LOCAL PROCEDURE OnBeforeInsertRecord@1(SourceRecordRef@1001 : RecordRef;VAR DestinationRecordRef@1000 : RecordRef);
    VAR
      Contact@1002 : Record 5050;
      GraphContact@1004 : Record 5450;
      GraphCollectionMgtContact@1005 : Codeunit 5458;
      ContactType@1006 : Option;
    BEGIN
      CASE GetSourceDestCode(SourceRecordRef,DestinationRecordRef) OF
        'Graph Contact-Contact':
          BEGIN
            DestinationRecordRef.SETTABLE(Contact);
            SourceRecordRef.SETTABLE(GraphContact);
            VerifyGraphContactRequiredData(GraphContact);
            ContactType := Contact.Type;
            GraphCollectionMgtContact.GetBusinessType(GraphContact.GetBusinessTypeString,ContactType);
            Contact.Type := ContactType;
            SetGraphFieldsOnContact(GraphContact,Contact);
            DestinationRecordRef.GETTABLE(Contact);
          END;
      END;
    END;

    [EventSubscriber(Codeunit,5345,OnAfterInsertRecord)]
    LOCAL PROCEDURE OnAfterInsertRecord@45(VAR SourceRecordRef@1001 : RecordRef;VAR DestinationRecordRef@1002 : RecordRef);
    VAR
      Contact@1003 : Record 5050;
      GraphContact@1004 : Record 5450;
      ModifySourceRecord@1005 : Boolean;
    BEGIN
      CASE GetSourceDestCode(SourceRecordRef,DestinationRecordRef) OF
        'Graph Contact-Contact':
          BEGIN
            DestinationRecordRef.SETTABLE(Contact);
            SourceRecordRef.SETTABLE(GraphContact);
            SetContactBusinessRelations(GraphContact,Contact);
            CheckAndFixDisplayName(GraphContact,Contact,ModifySourceRecord);
            ModifySourceGraphContact(GraphContact,ModifySourceRecord);
            DestinationRecordRef.GETTABLE(Contact);
            SourceRecordRef.GETTABLE(GraphContact);
          END;
      END;
    END;

    [EventSubscriber(Codeunit,5345,OnBeforeModifyRecord)]
    LOCAL PROCEDURE OnBeforeModifyRecord@12(IntegrationTableMapping@1000 : Record 5335;SourceRecordRef@1001 : RecordRef;VAR DestinationRecordRef@1002 : RecordRef);
    VAR
      GraphContact@1003 : Record 5450;
      Contact@1004 : Record 5050;
    BEGIN
      CASE GetSourceDestCode(SourceRecordRef,DestinationRecordRef) OF
        'Graph Contact-Contact':
          BEGIN
            DestinationRecordRef.SETTABLE(Contact);
            SourceRecordRef.SETTABLE(GraphContact);
            VerifyGraphContactRequiredData(GraphContact);
            SetGraphFieldsOnContact(GraphContact,Contact);
            SetContactBusinessRelations(GraphContact,Contact);
            DestinationRecordRef.GETTABLE(Contact);
          END;
      END;
    END;

    [EventSubscriber(Codeunit,5345,OnAfterModifyRecord)]
    LOCAL PROCEDURE OnAfterModifyRecord@51(VAR SourceRecordRef@1001 : RecordRef;VAR DestinationRecordRef@1002 : RecordRef);
    VAR
      GraphContact@1000 : Record 5450;
      Contact@1003 : Record 5050;
      ModifySourceRecord@1004 : Boolean;
    BEGIN
      CASE GetSourceDestCode(SourceRecordRef,DestinationRecordRef) OF
        'Graph Contact-Contact':
          BEGIN
            DestinationRecordRef.SETTABLE(Contact);
            SourceRecordRef.SETTABLE(GraphContact);
            CheckAndFixContactBusinessRelations(GraphContact,Contact,ModifySourceRecord);
            CheckAndFixDisplayName(GraphContact,Contact,ModifySourceRecord);
            ModifySourceGraphContact(GraphContact,ModifySourceRecord);
            SourceRecordRef.GETTABLE(GraphContact);
          END;
      END;
    END;

    [EventSubscriber(Codeunit,5347,OnBeforeDeleteRecord)]
    [Internal]
    LOCAL PROCEDURE OnBeforeDeleteRecord@13(IntegrationTableMapping@1000 : Record 5335;VAR DestinationRecordRef@1001 : RecordRef);
    VAR
      Contact@1003 : Record 5050;
    BEGIN
      CASE GetDestCode(DestinationRecordRef) OF
        'Contact':
          BEGIN
            DestinationRecordRef.SETTABLE(Contact);
            BlockCustVendBank(Contact);
          END;
      END;
    END;

    PROCEDURE FindCustomerFromGraphContact@16(GraphContactId@1000 : Text[250];VAR Customer@1001 : Record 18;VAR Contact@1006 : Record 5050) : Boolean;
    VAR
      DummyGraphIntegrationRecord@1002 : Record 5451;
      SearchGraphIntegrationRecord@1007 : Record 5451;
      ContactBusinessRelation@1003 : Record 5054;
      ContactRecordID@1005 : RecordID;
      ContactXrmId@1004 : GUID;
    BEGIN
      IF GraphContactId = '' THEN
        EXIT(FALSE);

      IF EVALUATE(ContactXrmId,GraphContactId) THEN BEGIN
        SearchGraphIntegrationRecord.SETRANGE(XRMId,ContactXrmId);
        IF NOT SearchGraphIntegrationRecord.FINDFIRST THEN
          EXIT;

        GraphContactId := SearchGraphIntegrationRecord."Graph ID";
      END;

      IF NOT DummyGraphIntegrationRecord.FindRecordIDFromID(GraphContactId,DATABASE::Contact,ContactRecordID) THEN
        EXIT(FALSE);

      IF NOT Contact.GET(ContactRecordID) THEN
        EXIT(FALSE);

      // Check primary contact first
      Customer.SETRANGE("Primary Contact No.",Contact."No.");
      IF Customer.FINDFIRST THEN
        EXIT(TRUE);

      Customer.SETRANGE("Primary Contact No.");
      ContactBusinessRelation.SETCURRENTKEY("Link to Table","No.");
      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Customer);
      ContactBusinessRelation.SETRANGE("Contact No.",Contact."No.");

      IF NOT ContactBusinessRelation.FINDFIRST THEN
        EXIT(FALSE);

      EXIT(Customer.GET(ContactBusinessRelation."No."));
    END;

    PROCEDURE FindGraphContactIdFromCustomer@18(VAR GraphContactId@1000 : Text[250];VAR Customer@1001 : Record 18;VAR Contact@1006 : Record 5050) : Boolean;
    VAR
      GraphIntegrationRecord@1002 : Record 5451;
      ContactBusinessRelation@1003 : Record 5054;
      MarketingSetup@1007 : Record 5079;
      ContactNo@1004 : Code[20];
    BEGIN
      // Use primary contact if specified
      IF Customer."Primary Contact No." <> '' THEN
        ContactNo := Customer."Primary Contact No."
      ELSE BEGIN
        ContactBusinessRelation.SETCURRENTKEY("Link to Table","No.");
        ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Customer);
        ContactBusinessRelation.SETRANGE("No.",Customer."No.");
        IF MarketingSetup.GET THEN
          ContactBusinessRelation.SETRANGE("Business Relation Code",MarketingSetup."Bus. Rel. Code for Customers");

        IF NOT ContactBusinessRelation.FINDFIRST THEN
          EXIT(FALSE);

        ContactNo := ContactBusinessRelation."Contact No.";
      END;

      IF NOT Contact.GET(ContactNo) THEN
        EXIT(FALSE);

      EXIT(GraphIntegrationRecord.FindIDFromRecordID(Contact.RECORDID,GraphContactId));
    END;

    PROCEDURE FindGraphContactIdFromCustomerNo@19(VAR GraphContactID@1002 : Text[250];CustomerNo@1001 : Code[20]) : Boolean;
    VAR
      Customer@1000 : Record 18;
      DummyContact@1003 : Record 5050;
    BEGIN
      IF NOT Customer.GET(CustomerNo) THEN
        EXIT;

      EXIT(FindGraphContactIdFromCustomer(GraphContactID,Customer,DummyContact));
    END;

    LOCAL PROCEDURE GetSourceDestCode@38(SourceRecordRef@1001 : RecordRef;DestinationRecordRef@1000 : RecordRef) : Text;
    BEGIN
      IF (SourceRecordRef.NUMBER <> 0) AND (DestinationRecordRef.NUMBER <> 0) THEN
        EXIT(STRSUBSTNO('%1-%2',SourceRecordRef.NAME,DestinationRecordRef.NAME));
      EXIT('');
    END;

    LOCAL PROCEDURE GetDestCode@14(DestinationRecordRef@1000 : RecordRef) : Text;
    BEGIN
      IF DestinationRecordRef.NUMBER <> 0 THEN
        EXIT(STRSUBSTNO('%1',DestinationRecordRef.NAME));
      EXIT('');
    END;

    LOCAL PROCEDURE SetContactFieldsOnGraph@2(Contact@1001 : Record 5050;VAR GraphContact@1000 : Record 5450);
    VAR
      GraphCollectionMgtContact@1002 : Codeunit 5458;
      EmailAddressesString@1005 : Text;
      BusinessTypeString@1006 : Text;
      IsCustomerString@1007 : Text;
      IsVendorString@1008 : Text;
      IsBankString@1009 : Text;
      IsNavCreatedString@1013 : Text;
      IsLead@1010 : Boolean;
      IsPartner@1011 : Boolean;
      IsContact@1012 : Boolean;
    BEGIN
      // Avoid syncing contacts without a name to Graph, which may be created because of creating a Customer without a name.
      Contact.TESTFIELD(Name);

      IF Contact.Type = Contact.Type::Company THEN
        GraphContact.DisplayName := Contact."Company Name"
      ELSE
        IF (Contact."First Name" IN ['',' ']) AND (Contact."Middle Name" = '') AND (Contact.Surname = '') THEN
          GraphContact.DisplayName := Contact.Name;

      // E-MailAddresses:
      EmailAddressesString := GraphContact.GetEmailAddressesString;
      EmailAddressesString := GraphCollectionMgtContact.UpdateEmailAddress(EmailAddressesString,0,Contact."E-Mail");
      EmailAddressesString := GraphCollectionMgtContact.UpdateEmailAddress(EmailAddressesString,1,Contact."E-Mail 2");
      GraphContact.SetEmailAddressesString(EmailAddressesString);

      // Extensions
      BusinessTypeString := GraphCollectionMgtContact.AddBusinessType(Contact.Type);
      GraphContact.SetBusinessTypeString(BusinessTypeString);
      // If the property exists OR IsCustomer = TRUE then set the property, else leave empty.
      IF (NOT GraphCollectionMgtContact.IsBlankOrEmptyJsonObject(GraphContact.GetIsCustomerString)) OR CheckIsCustomer(Contact)
      THEN BEGIN
        IsCustomerString := GraphCollectionMgtContact.AddIsCustomer(CheckIsCustomer(Contact));
        GraphContact.SetIsCustomerString(IsCustomerString);
      END;
      IF (NOT GraphCollectionMgtContact.IsBlankOrEmptyJsonObject(GraphContact.GetIsVendorString)) OR CheckIsVendor(Contact)
      THEN BEGIN
        IsVendorString := GraphCollectionMgtContact.AddIsVendor(CheckIsVendor(Contact));
        GraphContact.SetIsVendorString(IsVendorString);
      END;
      IF (NOT GraphCollectionMgtContact.IsBlankOrEmptyJsonObject(GraphContact.GetIsBankString) OR CheckIsBank(Contact))
      THEN BEGIN
        IsBankString := GraphCollectionMgtContact.AddIsBank(CheckIsBank(Contact));
        GraphContact.SetIsBankString(IsBankString);
      END;
      IF GraphContact.Id = '' THEN BEGIN // new graph contact
        IsNavCreatedString := GraphCollectionMgtContact.AddIsNavCreated(TRUE);
        GraphContact.SetIsNavCreatedString(IsNavCreatedString);
      END;
      IsLead := GraphCollectionMgtContact.GetIsLead(GraphContact.GetIsLeadString);
      IsPartner := GraphCollectionMgtContact.GetIsPartner(GraphContact.GetIsPartnerString);
      IsContact := GraphCollectionMgtContact.GetIsContact(GraphContact.GetIsContactString);
      // at least 1 must be true. If none are true, set IsContact = TRUE
      IF NOT (CheckIsBank(Contact) OR CheckIsCustomer(Contact) OR CheckIsVendor(Contact) OR IsLead OR IsPartner OR IsContact) THEN
        GraphContact.SetIsContactString(GraphCollectionMgtContact.AddIsContact(TRUE));

      // Comments
      GraphContact.SetPersonalNotesString(GraphCollectionMgtContact.GetContactComments(Contact));
    END;

    LOCAL PROCEDURE SetGraphFieldsOnContact@8(GraphContact@1003 : Record 5450;VAR Contact@1002 : Record 5050);
    VAR
      GraphCollectionMgtContact@1004 : Codeunit 5458;
      DummyName@1005 : Text;
    BEGIN
      IF Contact.Type = Contact.Type::Company THEN BEGIN
        IF (GraphContact.DisplayName = '') AND (GraphContact.CompanyName <> '') THEN
          Contact.VALIDATE(Name,COPYSTR(GraphContact.CompanyName,1,MAXSTRLEN(Contact.Name)))
        ELSE
          Contact.VALIDATE(Name,COPYSTR(GraphContact.DisplayName,1,MAXSTRLEN(Contact.Name)));
      END ELSE
        Contact.VALIDATE("First Name");

      // E-MailAddresses:
      GraphCollectionMgtContact.InitializeCollection(GraphContact.GetEmailAddressesString);
      GraphCollectionMgtContact.GetEmailAddress(0,DummyName,Contact."E-Mail");
      GraphCollectionMgtContact.GetEmailAddress(1,DummyName,Contact."E-Mail 2");

      // Comments
      GraphCollectionMgtContact.SetContactComments(Contact,GraphContact.GetPersonalNotesString);
    END;

    LOCAL PROCEDURE SetContactBusinessRelations@9(GraphContact@1001 : Record 5450;VAR Contact@1000 : Record 5050);
    VAR
      MarketingSetup@1005 : Record 5079;
      GraphCollectionMgtContact@1002 : Codeunit 5458;
    BEGIN
      IF MarketingSetup.GET THEN BEGIN
        // IsCustomer
        Contact.SetHideValidationDialog(TRUE);
        IF MarketingSetup."Bus. Rel. Code for Customers" <> '' THEN
          IF GraphCollectionMgtContact.GetIsCustomer(GraphContact.GetIsCustomerString) AND
             NOT CheckIsCustomer(Contact)
          THEN
            Contact.CreateCustomer(MarketingSetup.GetCustomerTemplate(Contact.Type));

        IF Contact.Type = Contact.Type::Company THEN BEGIN
          // IsVendor
          IF MarketingSetup."Bus. Rel. Code for Vendors" <> '' THEN
            IF GraphCollectionMgtContact.GetIsVendor(GraphContact.GetIsVendorString) AND
               NOT CheckIsVendor(Contact)
            THEN
              Contact.CreateVendor;

          // IsBank
          IF MarketingSetup."Bus. Rel. Code for Bank Accs." <> '' THEN
            IF GraphCollectionMgtContact.GetIsBank(GraphContact.GetIsBankString) AND
               NOT CheckIsBank(Contact)
            THEN
              Contact.CreateBankAccount;
        END;
      END;
    END;

    LOCAL PROCEDURE CheckIsCustomer@5(Contact@1001 : Record 5050) : Boolean;
    VAR
      ContactBusinessRelation@1000 : Record 5054;
    BEGIN
      ContactBusinessRelation.SETRANGE("Contact No.",Contact."No.");
      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Customer);
      EXIT(NOT ContactBusinessRelation.ISEMPTY);
    END;

    LOCAL PROCEDURE CheckIsVendor@6(Contact@1000 : Record 5050) : Boolean;
    VAR
      ContactBusinessRelation@1001 : Record 5054;
    BEGIN
      ContactBusinessRelation.SETRANGE("Contact No.",Contact."No.");
      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Vendor);
      EXIT(NOT ContactBusinessRelation.ISEMPTY);
    END;

    LOCAL PROCEDURE CheckIsBank@7(Contact@1001 : Record 5050) : Boolean;
    VAR
      ContactBusinessRelation@1000 : Record 5054;
    BEGIN
      ContactBusinessRelation.SETRANGE("Contact No.",Contact."No.");
      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::"Bank Account");
      EXIT(NOT ContactBusinessRelation.ISEMPTY);
    END;

    LOCAL PROCEDURE CheckAndFixContactBusinessRelations@3(VAR GraphContact@1001 : Record 5450;Contact@1000 : Record 5050;VAR ModifySourceRecord@1002 : Boolean);
    VAR
      GraphCollectionMgtContact@1004 : Codeunit 5458;
    BEGIN
      IF CheckIsCustomer(Contact) AND
         NOT GraphCollectionMgtContact.GetIsCustomer(GraphContact.GetIsCustomerString)
      THEN BEGIN
        GraphContact.SetIsCustomerString(GraphCollectionMgtContact.AddIsCustomer(TRUE));
        ModifySourceRecord := TRUE;
      END;

      IF CheckIsVendor(Contact) AND
         NOT GraphCollectionMgtContact.GetIsVendor(GraphContact.GetIsVendorString)
      THEN BEGIN
        GraphContact.SetIsVendorString(GraphCollectionMgtContact.AddIsVendor(TRUE));
        ModifySourceRecord := TRUE;
      END;

      IF CheckIsBank(Contact) AND
         NOT GraphCollectionMgtContact.GetIsBank(GraphContact.GetIsBankString)
      THEN BEGIN
        GraphContact.SetIsBankString(GraphCollectionMgtContact.AddIsBank(TRUE));
        ModifySourceRecord := TRUE;
      END;
    END;

    LOCAL PROCEDURE CheckAndFixDisplayName@4(VAR GraphContact@1000 : Record 5450;VAR Contact@1001 : Record 5050;VAR ModifySourceRecord@1002 : Boolean);
    BEGIN
      IF (GraphContact.DisplayName = '') AND (Contact.Name <> '') THEN BEGIN
        GraphContact.DisplayName := Contact.Name;
        ModifySourceRecord := TRUE;
      END;
    END;

    LOCAL PROCEDURE ModifySourceGraphContact@11(VAR GraphContact@1000 : Record 5450;ModifySourceRecord@1001 : Boolean);
    BEGIN
      IF ModifySourceRecord THEN
        GraphContact.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE VerifyGraphContactRequiredData@10(GraphContact@1000 : Record 5450);
    VAR
      GraphCollectionMgtContact@1001 : Codeunit 5458;
      Value@1002 : Text;
      BusinessTypeString@1009 : Text;
      IsCustomerString@1003 : Text;
      IsVendorString@1004 : Text;
      IsBankString@1005 : Text;
      IsContactString@1006 : Text;
      IsLeadString@1007 : Text;
      IsPartnerString@1008 : Text;
      HasBusinessCategory@1010 : Boolean;
    BEGIN
      BusinessTypeString := GraphContact.GetBusinessTypeString;

      // GraphContact MUST contain BusinessType
      IF NOT GraphCollectionMgtContact.HasBusinessType(BusinessTypeString) THEN
        ERROR(BusinessTypeMissingErr);

      // BusinessType MUST contain a valid value
      IF NOT GraphCollectionMgtContact.TryGetBusinessTypeValue(BusinessTypeString,Value) THEN
        ERROR(BusinessTypeUnknownValueErr,Value);

      IsCustomerString := GraphContact.GetIsCustomerString;
      IsVendorString := GraphContact.GetIsVendorString;
      IsBankString := GraphContact.GetIsBankString;
      IsContactString := GraphContact.GetIsContactString;
      IsLeadString := GraphContact.GetIsLeadString;
      IsPartnerString := GraphContact.GetIsPartnerString;

      // At least one of the Business Category fields must be present
      IF NOT (GraphCollectionMgtContact.HasIsCustomer(IsCustomerString) OR
              GraphCollectionMgtContact.HasIsVendor(IsVendorString) OR
              GraphCollectionMgtContact.HasIsBank(IsBankString) OR
              GraphCollectionMgtContact.HasIsContact(IsContactString) OR
              GraphCollectionMgtContact.HasIsLead(IsLeadString) OR
              GraphCollectionMgtContact.HasIsPartner(IsPartnerString))
      THEN
        ERROR(BusinessCategoryMissingErr);

      // Business Category fields MUST contain a valid value
      // At least 1 Business Category must be set to TRUE
      IF GraphCollectionMgtContact.HasIsCustomer(IsCustomerString) THEN BEGIN
        IF NOT GraphCollectionMgtContact.TryGetIsCustomerValue(IsCustomerString,Value) THEN
          ERROR(BusinessCategoryWrongValueErr,GETLASTERRORTEXT,IsCustomerString);
        IF GraphCollectionMgtContact.GetIsCustomer(IsCustomerString) THEN
          HasBusinessCategory := TRUE;
      END;

      IF GraphCollectionMgtContact.HasIsVendor(IsVendorString) THEN BEGIN
        IF NOT GraphCollectionMgtContact.TryGetIsVendorValue(IsVendorString,Value) THEN
          ERROR(BusinessCategoryWrongValueErr,GETLASTERRORTEXT,IsVendorString);
        IF GraphCollectionMgtContact.GetIsVendor(IsVendorString) THEN
          HasBusinessCategory := TRUE;
      END;

      IF GraphCollectionMgtContact.HasIsBank(IsBankString) THEN BEGIN
        IF NOT GraphCollectionMgtContact.TryGetIsBankValue(IsBankString,Value) THEN
          ERROR(BusinessCategoryWrongValueErr,GETLASTERRORTEXT,IsBankString);
        IF GraphCollectionMgtContact.GetIsBank(IsBankString) THEN
          HasBusinessCategory := TRUE;
      END;

      IF GraphCollectionMgtContact.HasIsContact(IsContactString) THEN BEGIN
        IF NOT GraphCollectionMgtContact.TryGetIsContactValue(IsContactString,Value) THEN
          ERROR(BusinessCategoryWrongValueErr,GETLASTERRORTEXT,IsContactString);
        IF GraphCollectionMgtContact.GetIsContact(IsContactString) THEN
          HasBusinessCategory := TRUE;
      END;

      IF GraphCollectionMgtContact.HasIsLead(IsLeadString) THEN BEGIN
        IF NOT GraphCollectionMgtContact.TryGetIsLeadValue(IsLeadString,Value) THEN
          ERROR(BusinessCategoryWrongValueErr,GETLASTERRORTEXT,IsLeadString);
        IF GraphCollectionMgtContact.GetIsLead(IsLeadString) THEN
          HasBusinessCategory := TRUE;
      END;

      IF GraphCollectionMgtContact.HasIsPartner(IsPartnerString) THEN BEGIN
        IF NOT GraphCollectionMgtContact.TryGetIsPartnerValue(IsPartnerString,Value) THEN
          ERROR(BusinessCategoryWrongValueErr,GETLASTERRORTEXT,IsPartnerString);
        IF GraphCollectionMgtContact.GetIsPartner(IsPartnerString) THEN
          HasBusinessCategory := TRUE;
      END;

      IF NOT HasBusinessCategory THEN
        ERROR(NoBusinessCategoryErr);
    END;

    LOCAL PROCEDURE BlockCustVendBank@15(DeletedContact@1000 : Record 5050);
    VAR
      ContactBusinessRelation@1001 : Record 5054;
      Customer@1002 : Record 18;
      Vendor@1003 : Record 23;
      BankAccount@1004 : Record 270;
    BEGIN
      ContactBusinessRelation.SETRANGE("Contact No.",DeletedContact."No.");
      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Customer);
      IF ContactBusinessRelation.FINDFIRST THEN
        IF Customer.GET(ContactBusinessRelation."No.") THEN BEGIN
          Customer.Blocked := Customer.Blocked::All;
          Customer.MODIFY;
        END;

      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Vendor);
      IF ContactBusinessRelation.FINDFIRST THEN
        IF Vendor.GET(ContactBusinessRelation."No.") THEN BEGIN
          Vendor.Blocked := Vendor.Blocked::All;
          Vendor.MODIFY;
        END;

      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::"Bank Account");
      IF ContactBusinessRelation.FINDFIRST THEN
        IF BankAccount.GET(ContactBusinessRelation."No.") THEN BEGIN
          BankAccount.Blocked := TRUE;
          BankAccount.MODIFY;
        END;
    END;

    LOCAL PROCEDURE SetIntegrationIdFieldOnGraph@17(VAR Contact@1000 : Record 5050;VAR GraphContact@1001 : Record 5450);
    VAR
      IntegrationRecord@1002 : Record 5151;
      GraphCollectionMgtContact@1003 : Codeunit 5458;
      IntegrationIdString@1004 : Text;
    BEGIN
      IF NOT IntegrationRecord.FindByRecordId(Contact.RECORDID) THEN
        EXIT;

      IntegrationIdString := GraphCollectionMgtContact.AddNavIntegrationId(IntegrationRecord."Integration ID");
      GraphContact.SetNavIntegrationIdString(IntegrationIdString);
    END;

    PROCEDURE SetXRMId@21(VAR GraphIntegrationRecord@1002 : Record 5451);
    VAR
      GraphContact@1004 : Record 5450;
      XrmID@1000 : GUID;
    BEGIN
      IF NOT (GraphIntegrationRecord.RECORDID.TABLENO = DATABASE::Contact) THEN
        EXIT;

      IF NOT GraphContact.GET(GraphIntegrationRecord."Graph ID") THEN
        EXIT;

      IF GraphContact.GetXrmId(XrmID) THEN
        GraphIntegrationRecord.XRMId := XrmID;
    END;

    PROCEDURE UpdateBlankXrmIds@20();
    VAR
      GraphIntegrationRecord@1007 : Record 5451;
      GraphContact@1006 : Record 5450;
      GraphConnectionSetup@1005 : Codeunit 5456;
      GraphSyncRunner@1004 : Codeunit 5452;
      BlankGuid@1003 : GUID;
      XrmId@1002 : GUID;
      InboundConnectionName@1001 : Text;
    BEGIN
      IF NOT GraphSyncRunner.IsGraphSyncEnabled THEN
        EXIT;

      GraphIntegrationRecord.SETRANGE(XRMId,BlankGuid);
      GraphIntegrationRecord.SETRANGE("Table ID",DATABASE::Contact);
      IF NOT GraphIntegrationRecord.FINDSET(TRUE,FALSE) THEN
        EXIT;

      InboundConnectionName := GraphConnectionSetup.GetInboundConnectionName(DATABASE::Contact);

      IF NOT HASTABLECONNECTION(TABLECONNECTIONTYPE::MicrosoftGraph,InboundConnectionName) THEN
        REGISTERTABLECONNECTION(
          TABLECONNECTIONTYPE::MicrosoftGraph,InboundConnectionName,
          GraphConnectionSetup.GetInboundConnectionString(DATABASE::Contact));

      SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::MicrosoftGraph,InboundConnectionName,TRUE);

      REPEAT
        IF GetGraphContact(GraphContact,GraphIntegrationRecord) THEN BEGIN
          IF NOT GraphContact.GetXrmId(XrmId) THEN
            ERROR(CannotGetGraphXrmIdErr,GraphIntegrationRecord."Graph ID");
          GraphIntegrationRecord.XRMId := XrmId;
          GraphIntegrationRecord.MODIFY;
        END;
      UNTIL GraphIntegrationRecord.NEXT = 0;
    END;

    [TryFunction]
    LOCAL PROCEDURE GetGraphContact@24(VAR GraphContact@1000 : Record 5450;GraphIntegrationRecord@1001 : Record 5451);
    BEGIN
      GraphContact.GET(GraphIntegrationRecord."Graph ID");
    END;

    BEGIN
    END.
  }
}

