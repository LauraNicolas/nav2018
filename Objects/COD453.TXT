OBJECT Codeunit 453 Job Queue - Enqueue
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    TableNo=472;
    Permissions=TableData 472=rimd;
    OnRun=BEGIN
            EnqueueJobQueueEntry(Rec);
            RemoveFailedJobs(Rec);
          END;

  }
  CODE
  {

    LOCAL PROCEDURE EnqueueJobQueueEntry@4(VAR JobQueueEntry@1000 : Record 472);
    BEGIN
      WITH JobQueueEntry DO BEGIN
        InitEntryForSchedulerWithDelayInSec(JobQueueEntry,1);
        IF ISNULLGUID(ID) THEN
          INSERT(TRUE)
        ELSE BEGIN
          IF CanScheduleTask(JobQueueEntry) THEN
            CancelTask; // clears "System Task ID"
          MODIFY;
        END;

        IF CanScheduleTask(JobQueueEntry) THEN
          "System Task ID" := ScheduleTask;

        IF NOT ISNULLGUID("System Task ID") THEN BEGIN
          Status := Status::Ready;
          MODIFY;
          COMMIT;
        END;
      END;
    END;

    LOCAL PROCEDURE InitEntryForSchedulerWithDelayInSec@3(VAR JobQueueEntry@1001 : Record 472;DelayInSec@1000 : Integer);
    BEGIN
      WITH JobQueueEntry DO BEGIN
        Status := Status::"On Hold";
        "User Session Started" := 0DT;
        IF "Earliest Start Date/Time" < (CURRENTDATETIME + 1000) THEN
          "Earliest Start Date/Time" := CURRENTDATETIME + DelayInSec * 1000;
      END;
    END;

    LOCAL PROCEDURE RemoveFailedJobs@1(CurrJobQueueEntry@1000 : Record 472);
    VAR
      JobQueueEntry@1001 : Record 472;
    BEGIN
      WITH JobQueueEntry DO BEGIN
        SETRANGE("Object Type to Run",CurrJobQueueEntry."Object Type to Run");
        SETRANGE("Object ID to Run",CurrJobQueueEntry."Object ID to Run");
        SETRANGE("Record ID to Process",CurrJobQueueEntry."Record ID to Process");
        SETRANGE(Status,Status::Error);
        SETFILTER(ID,'<>%1',CurrJobQueueEntry.ID);
        IF NOT ISEMPTY THEN
          DELETEALL;
      END;
    END;

    LOCAL PROCEDURE CanScheduleTask@5(VAR JobQueueEntry@1001 : Record 472) : Boolean;
    VAR
      DoNotScheduleTask@1000 : Boolean;
    BEGIN
      OnBeforeJobQueueScheduleTask(JobQueueEntry,DoNotScheduleTask);
      EXIT(NOT DoNotScheduleTask);
    END;

    [Integration]
    [External]
    LOCAL PROCEDURE OnBeforeJobQueueScheduleTask@2(VAR JobQueueEntry@1001 : Record 472;VAR DoNotScheduleTask@1000 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

