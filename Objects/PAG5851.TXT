OBJECT Page 5851 Get Post.Doc - S.ShptLn Sbfrm
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Lines;
    SaveValues=Yes;
    SourceTable=Table111;
    PageType=ListPart;
    OnFindRecord=BEGIN
                   IF NOT Visible THEN
                     EXIT(FALSE);

                   IF FIND(Which) THEN BEGIN
                     SalesShptLine := Rec;
                     WHILE TRUE DO BEGIN
                       ShowRec := IsShowRec(Rec);
                       IF ShowRec THEN
                         EXIT(TRUE);
                       IF NEXT(1) = 0 THEN BEGIN
                         Rec := SalesShptLine;
                         IF FIND(Which) THEN
                           WHILE TRUE DO BEGIN
                             ShowRec := IsShowRec(Rec);
                             IF ShowRec THEN
                               EXIT(TRUE);
                             IF NEXT(-1) = 0 THEN
                               EXIT(FALSE);
                           END;
                       END;
                     END;
                   END;
                   EXIT(FALSE);
                 END;

    OnNextRecord=VAR
                   RealSteps@1001 : Integer;
                   NextSteps@1000 : Integer;
                 BEGIN
                   IF Steps = 0 THEN
                     EXIT;

                   SalesShptLine := Rec;
                   REPEAT
                     NextSteps := NEXT(Steps / ABS(Steps));
                     ShowRec := IsShowRec(Rec);
                     IF ShowRec THEN BEGIN
                       RealSteps := RealSteps + NextSteps;
                       SalesShptLine := Rec;
                     END;
                   UNTIL (NextSteps = 0) OR (RealSteps = Steps);
                   Rec := SalesShptLine;
                   FIND;
                   EXIT(RealSteps);
                 END;

    OnAfterGetRecord=BEGIN
                       DocumentNoHideValue := FALSE;
                       DocumentNoOnFormat;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1907935204;1 ;ActionGroup;
                      CaptionML=ENU=&Line;
                      Image=Line }
      { 1902759804;2 ;Action    ;
                      Name=ShowDocument;
                      ShortCutKey=Shift+F7;
                      CaptionML=ENU=Show Document;
                      Image=View;
                      OnAction=BEGIN
                                 ShowPostedShipment;
                               END;
                                }
      { 1900206204;2 ;Action    ;
                      Name=Dimensions;
                      AccessByPermission=TableData 348=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=ENU=Dimensions;
                      ToolTipML=ENU=View or edit dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                               END;
                                }
      { 1901652604;2 ;Action    ;
                      Name=ItemTrackingLines;
                      ShortCutKey=Shift+Ctrl+I;
                      CaptionML=ENU=Item &Tracking Lines;
                      Image=ItemTrackingLines;
                      OnAction=BEGIN
                                 ItemTrackingLines;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Lookup=No;
                ToolTipML=ENU=Specifies the number of the related document.;
                SourceExpr="Document No.";
                HideValue=DocumentNoHideValue;
                StyleExpr='Strong' }

    { 78  ;2   ;Field     ;
                ToolTipML=ENU=Specifies when items on the document are shipped or were shipped. A shipment date is usually calculated from a requested delivery date plus lead time.;
                SourceExpr="Shipment Date" }

    { 50  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the customer that you send or sent the invoice or credit memo to.;
                SourceExpr="Bill-to Customer No.";
                Visible=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr="Sell-to Customer No.";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr=Type }

    { 8   ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the record.;
                SourceExpr="No." }

    { 28  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the cross-referenced item number. If you enter a cross reference between yours and your vendor's or customer's item number, then this number will override the standard item number when you enter the cross-reference number on a sales or purchase document.;
                SourceExpr="Cross-Reference No.";
                Visible=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 42  ;2   ;Field     ;
                SourceExpr=Nonstock;
                Visible=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr=Description }

    { 44  ;2   ;Field     ;
                SourceExpr="Return Reason Code";
                Visible=FALSE }

    { 48  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the location in which the invoice line was registered.;
                SourceExpr="Location Code";
                Visible=FALSE }

    { 54  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the bin where the items are picked or put away.;
                SourceExpr="Bin Code";
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr="Shortcut Dimension 1 Code";
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr="Shortcut Dimension 2 Code";
                Visible=FALSE }

    { 58  ;2   ;Field     ;
                SourceExpr="Unit of Measure Code" }

    { 60  ;2   ;Field     ;
                SourceExpr=Quantity }

    { 34  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the quantity of the shipped item that has been posted as shipped but that has not yet been posted as invoiced.;
                SourceExpr="Qty. Shipped Not Invoiced" }

    { 18  ;2   ;Field     ;
                CaptionML=ENU=Qty. Not Returned;
                DecimalPlaces=0:5;
                SourceExpr=QtyNotReturned }

    { 22  ;2   ;Field     ;
                CaptionML=ENU=Qty. Returned;
                DecimalPlaces=0:5;
                SourceExpr=CalcQtyReturned }

    { 64  ;2   ;Field     ;
                SourceExpr="Unit of Measure";
                Visible=FALSE }

    { 66  ;2   ;Field     ;
                SourceExpr="Unit Cost (LCY)";
                Visible=FALSE }

    { 20  ;2   ;Field     ;
                CaptionML=ENU=Reverse Unit Cost (LCY);
                SourceExpr=RevUnitCostLCY;
                AutoFormatType=2;
                Visible=FALSE }

    { 72  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the related job.;
                SourceExpr="Job No.";
                Visible=FALSE }

    { 80  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the blanket order that the record originates from.;
                SourceExpr="Blanket Order No.";
                Visible=FALSE }

    { 82  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the blanket order line that the record originates from.;
                SourceExpr="Blanket Order Line No.";
                Visible=FALSE }

    { 84  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the item ledger entry that the document or journal line is applied from.;
                SourceExpr="Appl.-from Item Entry";
                Visible=FALSE }

    { 32  ;2   ;Field     ;
                ToolTipML=ENU=Specifies the number of the item ledger entry that the document or journal line is applied -to.;
                SourceExpr="Appl.-to Item Entry" }

  }
  CODE
  {
    VAR
      SalesShptLine@1003 : Record 111;
      TempSalesShptLine@1002 : TEMPORARY Record 111;
      QtyNotReturned@1004 : Decimal;
      RevUnitCostLCY@1000 : Decimal;
      RevQtyFilter@1005 : Boolean;
      FillExactCostReverse@1007 : Boolean;
      Visible@1009 : Boolean;
      ShowRec@1001 : Boolean;
      DocumentNoHideValue@19020538 : Boolean INDATASET;

    LOCAL PROCEDURE IsFirstDocLine@2() : Boolean;
    VAR
      SalesShptLine2@1000 : Record 111;
      QtyNotReturned2@1002 : Decimal;
      RevUnitCostLCY2@1001 : Decimal;
    BEGIN
      TempSalesShptLine.RESET;
      TempSalesShptLine.COPYFILTERS(Rec);
      TempSalesShptLine.SETRANGE("Document No.","Document No.");
      IF NOT TempSalesShptLine.FINDFIRST THEN BEGIN
        QtyNotReturned2 := QtyNotReturned;
        RevUnitCostLCY2 := RevUnitCostLCY;
        SalesShptLine2.COPYFILTERS(Rec);
        SalesShptLine2.SETRANGE("Document No.","Document No.");
        IF NOT SalesShptLine2.FINDSET THEN
          EXIT(FALSE);
        REPEAT
          ShowRec := IsShowRec(SalesShptLine2);
          IF ShowRec THEN BEGIN
            TempSalesShptLine := SalesShptLine2;
            TempSalesShptLine.INSERT;
          END;
        UNTIL (SalesShptLine2.NEXT = 0) OR ShowRec;
        QtyNotReturned := QtyNotReturned2;
        RevUnitCostLCY := RevUnitCostLCY2;
      END;

      EXIT("Line No." = TempSalesShptLine."Line No.");
    END;

    LOCAL PROCEDURE IsShowRec@3(SalesShptLine2@1000 : Record 111) : Boolean;
    BEGIN
      WITH SalesShptLine2 DO BEGIN
        QtyNotReturned := 0;
        IF RevQtyFilter AND (Type = Type::" ") THEN
          EXIT("Attached to Line No." = 0);
        IF Type <> Type::Item THEN
          EXIT(TRUE);
        CalcShippedSaleNotReturned(QtyNotReturned,RevUnitCostLCY,FillExactCostReverse);
        IF NOT RevQtyFilter THEN
          EXIT(TRUE);
        EXIT(QtyNotReturned > 0);
      END;
    END;

    LOCAL PROCEDURE CalcQtyReturned@5() : Decimal;
    BEGIN
      IF (Type = Type::Item) AND (Quantity - QtyNotReturned > 0) THEN
        EXIT(Quantity - QtyNotReturned);
      EXIT(0);
    END;

    [External]
    PROCEDURE Initialize@4(NewRevQtyFilter@1000 : Boolean;NewFillExactCostReverse@1001 : Boolean;NewVisible@1002 : Boolean);
    BEGIN
      RevQtyFilter := NewRevQtyFilter;
      FillExactCostReverse := NewFillExactCostReverse;
      Visible := NewVisible;

      IF Visible THEN BEGIN
        TempSalesShptLine.RESET;
        TempSalesShptLine.DELETEALL;
      END;
    END;

    [External]
    PROCEDURE GetSelectedLine@1(VAR FromSalesShptLine@1000 : Record 111);
    BEGIN
      FromSalesShptLine.COPY(Rec);
      CurrPage.SETSELECTIONFILTER(FromSalesShptLine);
    END;

    LOCAL PROCEDURE ShowPostedShipment@6();
    VAR
      SalesShptHeader@1001 : Record 110;
    BEGIN
      IF NOT SalesShptHeader.GET("Document No.") THEN
        EXIT;
      PAGE.RUN(PAGE::"Posted Sales Shipment",SalesShptHeader);
    END;

    LOCAL PROCEDURE ItemTrackingLines@12();
    VAR
      FromSalesShptLine@1000 : Record 111;
    BEGIN
      GetSelectedLine(FromSalesShptLine);
      FromSalesShptLine.ShowItemTrackingLines;
    END;

    LOCAL PROCEDURE DocumentNoOnFormat@19001080();
    BEGIN
      IF NOT IsFirstDocLine THEN
        DocumentNoHideValue := TRUE;
    END;

    BEGIN
    END.
  }
}

