OBJECT Codeunit 2111 O365 Sales Background Setup
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    Permissions=TableData 5079=rimd;
    OnRun=BEGIN
            InitializeGraphSync;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE InitializeGraphSync@31();
    VAR
      MarketingSetup@1000 : Record 5079;
      CompanyInformation@1002 : Record 79;
      AzureADUserManagement@1003 : Codeunit 9010;
      WebhookManagement@1001 : Codeunit 5377;
      GraphSyncRunner@1005 : Codeunit 5452;
      TenantDetail@1004 : DotNet "'Microsoft.Azure.ActiveDirectory.GraphClient, Version=1.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Azure.ActiveDirectory.GraphClient.TenantDetail";
    BEGIN
      MarketingSetup.LOCKTABLE;
      IF NOT MarketingSetup.GET THEN
        MarketingSetup.INSERT(TRUE);

      IF NOT MarketingSetup."Sync with Microsoft Graph" THEN
        MarketingSetup.VALIDATE("Sync with Microsoft Graph",TRUE);
      MarketingSetup.MODIFY(TRUE);

      IF NOT WebhookManagement.IsCurrentClientTypeAllowed THEN
        EXIT;

      IF NOT WebhookManagement.IsSyncAllowed THEN
        EXIT;

      IF NOT GraphSyncRunner.IsGraphSyncEnabled THEN
        EXIT;

      CompanyInformation.GET;
      IF CompanyInformation."Sync with O365 Bus. profile" THEN
        TASKSCHEDULER.CREATETASK(CODEUNIT::"Business Profile Sync. Runner",0,TRUE,COMPANYNAME,0DT)
      ELSE
        IF AzureADUserManagement.GetTenantDetail(TenantDetail) THEN BEGIN
          CompanyInformation.LOCKTABLE;

          CompanyInformation.Name := COPYSTR(TenantDetail.DisplayName,1,MAXSTRLEN(CompanyInformation.Name));
          CompanyInformation.Address := COPYSTR(TenantDetail.Street,1,MAXSTRLEN(CompanyInformation.Address));
          CompanyInformation.City := COPYSTR(TenantDetail.City,1,MAXSTRLEN(CompanyInformation.City));
          CompanyInformation."Post Code" := COPYSTR(TenantDetail.PostalCode,1,MAXSTRLEN(CompanyInformation."Post Code"));
          CompanyInformation.County := COPYSTR(TenantDetail.State,1,MAXSTRLEN(CompanyInformation.County));
          CompanyInformation."Country/Region Code" :=
            COPYSTR(TenantDetail.CountryLetterCode,1,MAXSTRLEN(CompanyInformation."Country/Region Code"));
          CompanyInformation.MODIFY(FALSE);
        END;
    END;

    BEGIN
    END.
  }
}

