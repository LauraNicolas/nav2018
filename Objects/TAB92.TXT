OBJECT Table 92 Customer Posting Group
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               CheckCustEntries;
             END;

    CaptionML=ENU=Customer Posting Group;
    LookupPageID=Page110;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Receivables Account ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Receivables Account",FALSE,FALSE);
                                                              END;

                                                   OnLookup=BEGIN
                                                              GLAccountCategoryMgt.LookupGLAccount(
                                                                "Receivables Account",GLAccountCategory."Account Category"::Assets,GLAccountCategoryMgt.GetAR);
                                                            END;

                                                   CaptionML=ENU=Receivables Account }
    { 7   ;   ;Service Charge Acc. ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Service Charge Acc.",TRUE,TRUE);
                                                              END;

                                                   CaptionML=ENU=Service Charge Acc. }
    { 8   ;   ;Payment Disc. Debit Acc.;Code20    ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Payment Disc. Debit Acc.",FALSE,FALSE);
                                                              END;

                                                   CaptionML=ENU=Payment Disc. Debit Acc. }
    { 9   ;   ;Invoice Rounding Account;Code20    ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Invoice Rounding Account",TRUE,FALSE);
                                                              END;

                                                   CaptionML=ENU=Invoice Rounding Account }
    { 10  ;   ;Additional Fee Account;Code20      ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Additional Fee Account",TRUE,TRUE);
                                                              END;

                                                   CaptionML=ENU=Additional Fee Account }
    { 11  ;   ;Interest Account    ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Interest Account",TRUE,FALSE);
                                                              END;

                                                   CaptionML=ENU=Interest Account }
    { 12  ;   ;Debit Curr. Appln. Rndg. Acc.;Code20;
                                                   TableRelation="G/L Account";
                                                   CaptionML=ENU=Debit Curr. Appln. Rndg. Acc. }
    { 13  ;   ;Credit Curr. Appln. Rndg. Acc.;Code20;
                                                   TableRelation="G/L Account";
                                                   CaptionML=ENU=Credit Curr. Appln. Rndg. Acc. }
    { 14  ;   ;Debit Rounding Account;Code20      ;TableRelation="G/L Account";
                                                   CaptionML=ENU=Debit Rounding Account }
    { 15  ;   ;Credit Rounding Account;Code20     ;TableRelation="G/L Account";
                                                   CaptionML=ENU=Credit Rounding Account }
    { 16  ;   ;Payment Disc. Credit Acc.;Code20   ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Payment Disc. Credit Acc.",FALSE,FALSE);
                                                              END;

                                                   CaptionML=ENU=Payment Disc. Credit Acc. }
    { 17  ;   ;Payment Tolerance Debit Acc.;Code20;TableRelation="G/L Account";
                                                   CaptionML=ENU=Payment Tolerance Debit Acc. }
    { 18  ;   ;Payment Tolerance Credit Acc.;Code20;
                                                   TableRelation="G/L Account";
                                                   CaptionML=ENU=Payment Tolerance Credit Acc. }
    { 19  ;   ;Add. Fee per Line Account;Code20   ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Add. Fee per Line Account",TRUE,TRUE);
                                                              END;

                                                   CaptionML=ENU=Add. Fee per Line Account }
    { 20  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 40  ;   ;Used in Customers   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Customer WHERE (Customer Posting Group=FIELD(Code)));
                                                   CaptionML=ENU=Used in Customers;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 41  ;   ;Used in Ledger Entries;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count("Cust. Ledger Entry" WHERE (Customer Posting Group=FIELD(Code)));
                                                   CaptionML=ENU=Used in Ledger Entries;
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Code                                     }
  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      GLAccountCategory@1002 : Record 570;
      GLAccountCategoryMgt@1001 : Codeunit 570;
      YouCannotDeleteErr@1004 : TextConst '@@@="%1 = Code";ENU=You cannot delete %1.';

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20];CheckProdPostingGroup@1001 : Boolean;CheckDirectPosting@1002 : Boolean);
    VAR
      GLAcc@1003 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        IF CheckProdPostingGroup THEN
          GLAcc.TESTFIELD("Gen. Prod. Posting Group");
        IF CheckDirectPosting THEN
          GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    LOCAL PROCEDURE CheckCustEntries@13();
    BEGIN
      CALCFIELDS("Used in Customers","Used in Ledger Entries");
      IF ("Used in Customers" > 0) OR ("Used in Ledger Entries" > 0) THEN
        ERROR(YouCannotDeleteErr,Code);
    END;

    [External]
    PROCEDURE GetReceivablesAccount@6() : Code[20];
    BEGIN
      TESTFIELD("Receivables Account");
      EXIT("Receivables Account");
    END;

    [External]
    PROCEDURE GetPmtDiscountAccount@1(Debit@1000 : Boolean) : Code[20];
    BEGIN
      IF Debit THEN BEGIN
        TESTFIELD("Payment Disc. Debit Acc.");
        EXIT("Payment Disc. Debit Acc.");
      END;
      TESTFIELD("Payment Disc. Credit Acc.");
      EXIT("Payment Disc. Credit Acc.");
    END;

    [External]
    PROCEDURE GetPmtToleranceAccount@3(Debit@1000 : Boolean) : Code[20];
    BEGIN
      IF Debit THEN BEGIN
        TESTFIELD("Payment Tolerance Debit Acc.");
        EXIT("Payment Tolerance Debit Acc.");
      END;
      TESTFIELD("Payment Tolerance Credit Acc.");
      EXIT("Payment Tolerance Credit Acc.");
    END;

    [External]
    PROCEDURE GetRoundingAccount@4(Debit@1000 : Boolean) : Code[20];
    BEGIN
      IF Debit THEN BEGIN
        TESTFIELD("Debit Rounding Account");
        EXIT("Debit Rounding Account");
      END;
      TESTFIELD("Credit Rounding Account");
      EXIT("Credit Rounding Account");
    END;

    [External]
    PROCEDURE GetApplRoundingAccount@5(Debit@1000 : Boolean) : Code[20];
    BEGIN
      IF Debit THEN BEGIN
        TESTFIELD("Debit Curr. Appln. Rndg. Acc.");
        EXIT("Debit Curr. Appln. Rndg. Acc.");
      END;
      TESTFIELD("Credit Curr. Appln. Rndg. Acc.");
      EXIT("Credit Curr. Appln. Rndg. Acc.");
    END;

    [External]
    PROCEDURE GetInvRoundingAccount@9() : Code[20];
    BEGIN
      TESTFIELD("Invoice Rounding Account");
      EXIT("Invoice Rounding Account");
    END;

    [External]
    PROCEDURE GetServiceChargeAccount@7() : Code[20];
    BEGIN
      TESTFIELD("Service Charge Acc.");
      EXIT("Service Charge Acc.");
    END;

    [External]
    PROCEDURE GetAdditionalFeeAccount@8() : Code[20];
    BEGIN
      TESTFIELD("Additional Fee Account");
      EXIT("Additional Fee Account");
    END;

    [External]
    PROCEDURE GetAddFeePerLineAccount@11() : Code[20];
    BEGIN
      TESTFIELD("Add. Fee per Line Account");
      EXIT("Add. Fee per Line Account");
    END;

    [External]
    PROCEDURE GetInterestAccount@10() : Code[20];
    BEGIN
      TESTFIELD("Interest Account");
      EXIT("Interest Account");
    END;

    PROCEDURE SetAccountVisibility@12(VAR PmtToleranceVisible@1000 : Boolean;VAR PmtDiscountVisible@1002 : Boolean;VAR InvRoundingVisible@1001 : Boolean;VAR ApplnRoundingVisible@1005 : Boolean);
    VAR
      SalesSetup@1003 : Record 311;
      PaymentTerms@1004 : Record 3;
    BEGIN
      GLSetup.GET;
      PmtToleranceVisible := GLSetup."Payment Tolerance %" > 0;
      PmtDiscountVisible := PaymentTerms.UsePaymentDiscount;

      SalesSetup.GET;
      InvRoundingVisible := SalesSetup."Invoice Rounding";
      ApplnRoundingVisible := SalesSetup."Appln. between Currencies" <> SalesSetup."Appln. between Currencies"::None;
    END;

    BEGIN
    END.
  }
}

