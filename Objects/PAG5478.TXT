OBJECT Page 5478 Journal Lines Entity
{
  OBJECT-PROPERTIES
  {
    Date=28.05.17;
    Time=12:00:00;
    Version List=NAVW110.0;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=journalLines];
    SourceTable=Table81;
    DelayedInsert=Yes;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 GraphMgtJournalLines.SetJournalLineFilters(Rec);
               END;

    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  CheckFilters;

                  ClearCalculatedFields;

                  "Document Type" := "Document Type"::" ";
                  "Account Type" := "Account Type"::"G/L Account";
                END;

    OnInsertRecord=VAR
                     TempGenJournalLine@1002 : TEMPORARY Record 81;
                   BEGIN
                     TempGenJournalLine.RESET;
                     TempGenJournalLine.COPY(Rec);

                     CLEAR(Rec);
                     GraphMgtJournalLines.SetJournalLineTemplateAndBatch(
                       Rec,GetBatchNameFromId(TempGenJournalLine.GETFILTER("Journal Batch Id")));
                     LibraryAPIGeneralJournal.InitializeLine(
                       Rec,TempGenJournalLine."Line No.",TempGenJournalLine."Document No.",TempGenJournalLine."External Document No.");

                     GraphMgtJournalLines.SetJournalLineValues(Rec,TempGenJournalLine);

                     SetCalculatedFields;
                   END;

    OnModifyRecord=VAR
                     GenJournalLine@1000 : Record 81;
                   BEGIN
                     GenJournalLine.SETRANGE(Id,Id);
                     GenJournalLine.FINDFIRST;

                     IF "Line No." = GenJournalLine."Line No." THEN
                       MODIFY(TRUE)
                     ELSE BEGIN
                       GenJournalLine.TRANSFERFIELDS(Rec,FALSE);
                       GenJournalLine.RENAME("Journal Template Name","Journal Batch Name","Line No.");
                       TRANSFERFIELDS(GenJournalLine,TRUE);
                     END;

                     SetCalculatedFields;

                     EXIT(FALSE);
                   END;

    OnAfterGetCurrRecord=BEGIN
                           IF NOT FiltersChecked THEN BEGIN
                             CheckFilters;
                             FiltersChecked := TRUE;
                           END;
                         END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=<Control1>;
                CaptionML=ENU=<Control1>;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                GroupType=Repeater }

    { 8   ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id];
                ApplicationArea=#All;
                SourceExpr=Id;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                Name=journalBatchDisplayName;
                CaptionML=[@@@={Locked};
                           ENU=JournalBatchDisplayName];
                ToolTipML=ENU=Specifies the Journal Batch Name of the Journal Line;
                ApplicationArea=#All;
                SourceExpr=GlobalJournalBatchDisplayNameTxt;
                OnValidate=BEGIN
                             ERROR(CannotEditBatchNameErr);
                           END;
                            }

    { 11  ;2   ;Field     ;
                Name=lineNumber;
                CaptionML=[@@@={Locked};
                           ENU=LineNumber];
                ApplicationArea=#All;
                SourceExpr="Line No." }

    { 10  ;2   ;Field     ;
                Name=accountId;
                CaptionML=[@@@={Locked};
                           ENU=AccountId];
                ApplicationArea=#All;
                SourceExpr="Account Id";
                OnValidate=BEGIN
                             GLAccount.SETRANGE(Id,"Account Id");
                             IF NOT GLAccount.FINDFIRST THEN
                               ERROR(AccountIdDoesNotMatchAnAccountErr);

                             "Account No." := GLAccount."No.";
                           END;
                            }

    { 3   ;2   ;Field     ;
                Name=accountNumber;
                CaptionML=[@@@={Locked};
                           ENU=AccountNumber];
                ApplicationArea=#All;
                SourceExpr="Account No.";
                OnValidate=BEGIN
                             IF GLAccount."No." <> '' THEN BEGIN
                               IF GLAccount."No." <> "Account No." THEN
                                 ERROR(AccountValuesDontMatchErr);
                               EXIT;
                             END;

                             IF NOT GLAccount.GET("Account No.") THEN
                               ERROR(AccountNumberDoesNotMatchAnAccountErr);

                             "Account Id" := GLAccount.Id;
                           END;
                            }

    { 12  ;2   ;Field     ;
                Name=postingDate;
                CaptionML=[@@@={Locked};
                           ENU=PostingDate];
                ApplicationArea=#All;
                SourceExpr="Posting Date" }

    { 6   ;2   ;Field     ;
                Name=documentNumber;
                CaptionML=[@@@={Locked};
                           ENU=DocumentNumber];
                ApplicationArea=#All;
                SourceExpr="Document No." }

    { 4   ;2   ;Field     ;
                Name=externalDocumentNumber;
                CaptionML=[@@@={Locked};
                           ENU=ExternalDocumentNumber];
                ApplicationArea=#All;
                SourceExpr="External Document No." }

    { 5   ;2   ;Field     ;
                Name=amount;
                CaptionML=[@@@={Locked};
                           ENU=Amount];
                ApplicationArea=#All;
                SourceExpr=Amount }

    { 7   ;2   ;Field     ;
                Name=description;
                CaptionML=[@@@={Locked};
                           ENU=Description];
                ApplicationArea=#All;
                SourceExpr=Description }

    { 13  ;2   ;Field     ;
                Name=comment;
                CaptionML=[@@@={Locked};
                           ENU=Comment];
                ApplicationArea=#All;
                SourceExpr=Comment }

    { 9   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified DateTime";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      GLAccount@1002 : Record 15;
      GraphMgtJournalLines@1000 : Codeunit 5478;
      LibraryAPIGeneralJournal@1001 : Codeunit 5469;
      FiltersNotSpecifiedErr@1007 : TextConst 'ENU=You must specify a journal batch ID or a journal ID to get a journal line.';
      CannotEditBatchNameErr@1009 : TextConst 'ENU=The Journal Batch Display Name isn''t editable.';
      AccountValuesDontMatchErr@1005 : TextConst 'ENU=The account values do not match to a specific Account.';
      AccountIdDoesNotMatchAnAccountErr@1004 : TextConst 'ENU=The "accountId" does not match to an Account.';
      AccountNumberDoesNotMatchAnAccountErr@1003 : TextConst 'ENU=The "accountNumber" does not match to an Account.';
      GlobalJournalBatchDisplayNameTxt@1006 : Code[10];
      FiltersChecked@1008 : Boolean;

    LOCAL PROCEDURE SetCalculatedFields@6();
    BEGIN
      GlobalJournalBatchDisplayNameTxt := "Journal Batch Name";
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(GlobalJournalBatchDisplayNameTxt);
    END;

    LOCAL PROCEDURE GetBatchNameFromId@1(JournalBatchId@1001 : GUID) : Code[10];
    VAR
      GenJournalBatch@1000 : Record 232;
    BEGIN
      GenJournalBatch.SETRANGE(Id,JournalBatchId);
      GenJournalBatch.FINDFIRST;

      EXIT(GenJournalBatch.Name);
    END;

    LOCAL PROCEDURE CheckFilters@3();
    BEGIN
      IF (GETFILTER("Journal Batch Id") = '') AND
         (GETFILTER(Id) = '')
      THEN
        ERROR(FiltersNotSpecifiedErr);
    END;

    BEGIN
    END.
  }
}

