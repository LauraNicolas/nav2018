OBJECT Page 2801 Native - Customer Entity
{
  OBJECT-PROPERTIES
  {
    Date=06.10.17;
    Time=12:00:00;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=invoicingCustomers];
    SourceTable=Table18;
    DelayedInsert=Yes;
    PageType=List;
    OnOpenPage=BEGIN
                 TranslateContactIdFilterToCustomerNoFilter;
               END;

    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     Customer@1002 : Record 18;
                     MiniCustomerTemplate@1003 : Record 1300;
                     GraphMgtCustomer@1000 : Codeunit 5471;
                     GraphMgtGeneralTools@1004 : Codeunit 5465;
                     RecRef@1005 : RecordRef;
                   BEGIN
                     IF NOT CustomerCreatedFromGraph THEN BEGIN
                       Customer.SETRANGE("No.","No.");
                       IF NOT Customer.ISEMPTY THEN
                         INSERT;

                       IF NOT MiniCustomerTemplate.NewCustomerFromTemplate(Rec) THEN
                         INSERT(TRUE);
                     END;

                     GraphMgtCustomer.ProcessComplexTypes(Rec,PaymentTermsJSON,ShipmentMethodJSON,PaymentMethodJSON,PostalAddressJSON);

                     RecRef.GETTABLE(Rec);
                     GraphMgtGeneralTools.ProcessNewRecordFromAPI(RecRef,TempFieldSet,CURRENTDATETIME);
                     RecRef.SETTABLE(Rec);

                     MODIFY(TRUE);

                     SetCalculatedFields;
                     EXIT(FALSE);
                   END;

    OnModifyRecord=VAR
                     Customer@1001 : Record 18;
                     GraphMgtGeneralTools@1000 : Codeunit 5465;
                     GraphMgtCustomer@1002 : Codeunit 5471;
                   BEGIN
                     IF NOT CustomerCreatedFromGraph AND (xRec.Id <> Id) THEN
                       GraphMgtGeneralTools.ErrorIdImmutable;

                     Customer.SETRANGE(Id,Id);
                     Customer.FINDFIRST;

                     GraphMgtCustomer.ProcessComplexTypes(Rec,PaymentTermsJSON,ShipmentMethodJSON,PaymentMethodJSON,PostalAddressJSON);

                     IF "No." = Customer."No." THEN
                       MODIFY(TRUE)
                     ELSE BEGIN
                       Customer.TRANSFERFIELDS(Rec,FALSE);
                       Customer.RENAME("No.");
                       TRANSFERFIELDS(Customer);
                     END;

                     SetCalculatedFields;
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 23  ;0   ;Container ;
                ContainerType=ContentArea }

    { 22  ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 21  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id];
                ApplicationArea=#All;
                SourceExpr=Id }

    { 20  ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number];
                ApplicationArea=#All;
                SourceExpr="No." }

    { 10  ;2   ;Field     ;
                Name=graphContactId;
                CaptionML=ENU=graphContactId;
                ApplicationArea=#All;
                SourceExpr="Contact Graph Id";
                OnValidate=BEGIN
                             FindOrCreateCustomer("Contact Graph Id");
                           END;
                            }

    { 27  ;2   ;Field     ;
                Name=contactId;
                CaptionML=[@@@={Locked};
                           ENU=contactId];
                ToolTipML=ENU=Specifies the contact Id from exchange.;
                ApplicationArea=#All;
                SourceExpr="Contact ID";
                Editable=FALSE;
                OnValidate=BEGIN
                             FindOrCreateCustomer("Contact ID");
                           END;
                            }

    { 19  ;2   ;Field     ;
                Name=displayName;
                CaptionML=[@@@={Locked};
                           ENU=DisplayName];
                ApplicationArea=#All;
                SourceExpr=Name;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Name));
                           END;
                            }

    { 18  ;2   ;Field     ;
                Name=type;
                CaptionML=[@@@={Locked};
                           ENU=type];
                ApplicationArea=#All;
                SourceExpr="Contact Type" }

    { 17  ;2   ;Field     ;
                Name=address;
                CaptionML=[@@@={Locked};
                           ENU=address];
                ToolTipML=ENU=Specifies the address for the customer.;
                ApplicationArea=#All;
                SourceExpr=PostalAddressJSON;
                ODataEDMType=POSTALADDRESS }

    { 16  ;2   ;Field     ;
                Name=phoneNumber;
                CaptionML=[@@@={Locked};
                           ENU=PhoneNumber];
                ApplicationArea=#All;
                SourceExpr="Phone No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Phone No."));
                           END;
                            }

    { 15  ;2   ;Field     ;
                Name=email;
                CaptionML=[@@@={Locked};
                           ENU=Email];
                ApplicationArea=#All;
                SourceExpr="E-Mail";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("E-Mail"));
                           END;
                            }

    { 14  ;2   ;Field     ;
                Name=website;
                CaptionML=[@@@={Locked};
                           ENU=Website];
                ApplicationArea=#All;
                SourceExpr="Home Page";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Home Page"));
                           END;
                            }

    { 13  ;2   ;Field     ;
                Name=taxLiable;
                CaptionML=[@@@={Locked};
                           ENU=TaxLiable];
                ApplicationArea=#All;
                SourceExpr="Tax Liable";
                OnValidate=BEGIN
                             IF IsUsingVAT THEN
                               ERROR(CannotUseWithVATErr);

                             RegisterFieldSet(FIELDNO("Tax Liable"));
                           END;
                            }

    { 12  ;2   ;Field     ;
                Name=taxAreaId;
                CaptionML=[@@@={Locked};
                           ENU=taxAreaId];
                ApplicationArea=#All;
                SourceExpr="Tax Area ID";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Tax Area ID"));
                             IF IsUsingVAT THEN
                               RegisterFieldSet(FIELDNO("Tax Area Code"))
                             ELSE
                               RegisterFieldSet(FIELDNO("VAT Bus. Posting Group"));
                           END;
                            }

    { 11  ;2   ;Field     ;
                Name=taxAreaDisplayName;
                CaptionML=[@@@={Locked};
                           ENU=taxAreaDisplayName];
                ToolTipML=ENU=Specifies the display name of the tax area.;
                ApplicationArea=#All;
                SourceExpr=TaxAreaDisplayName;
                Editable=FALSE }

    { 26  ;2   ;Field     ;
                Name=taxRegistrationNumber;
                CaptionML=[@@@={Locked};
                           ENU=taxRegistrationNumber];
                ApplicationArea=#All;
                SourceExpr="VAT Registration No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("VAT Registration No."));
                           END;
                            }

    { 25  ;2   ;Field     ;
                Name=paymentTermsId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentTermsId];
                ApplicationArea=#All;
                SourceExpr="Payment Terms Id";
                OnValidate=BEGIN
                             IF "Payment Terms Id" = BlankGUID THEN
                               "Payment Terms Code" := ''
                             ELSE BEGIN
                               PaymentTerms.SETRANGE(Id,"Payment Terms Id");
                               IF NOT PaymentTerms.FINDFIRST THEN
                                 ERROR(PaymentTermsIdDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Code" := PaymentTerms.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;
                            }

    { 24  ;2   ;Field     ;
                Name=paymentTerms;
                CaptionML=[@@@={Locked};
                           ENU=PaymentTerms];
                ToolTipML=ENU=Specifies the Payment Terms for the Customer.;
                ApplicationArea=#All;
                SourceExpr=PaymentTermsJSON;
                OnValidate=BEGIN
                             GraphMgtCustomer.UpdatePaymentTerms(PaymentTermsJSON,Rec);

                             IF PaymentTerms.Code <> '' THEN BEGIN
                               IF PaymentTerms.Code <> "Payment Terms Code" THEN
                                 ERROR(PaymentTermsValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Payment Terms Code" = '' THEN
                               "Payment Terms Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT PaymentTerms.GET("Payment Terms Code") THEN
                                 ERROR(PaymentTermsCodeDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Id" := PaymentTerms.Id;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;

                ODataEDMType=PAYMENTTERMSTYPE }

    { 9   ;2   ;Field     ;
                Name=shipmentMethodId;
                CaptionML=[@@@={Locked};
                           ENU=ShipmentMethodId];
                ApplicationArea=#All;
                SourceExpr="Shipment Method Id";
                OnValidate=BEGIN
                             IF "Shipment Method Id" = BlankGUID THEN
                               "Shipment Method Code" := ''
                             ELSE BEGIN
                               ShipmentMethod.SETRANGE(Id,"Shipment Method Id");
                               IF NOT ShipmentMethod.FINDFIRST THEN
                                 ERROR(ShipmentMethodIdDoesNotMatchAShipmentMethodErr);

                               "Shipment Method Code" := ShipmentMethod.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Shipment Method Id"));
                             RegisterFieldSet(FIELDNO("Shipment Method Code"));
                           END;
                            }

    { 8   ;2   ;Field     ;
                Name=shipmentMethod;
                CaptionML=[@@@={Locked};
                           ENU=ShipmentMethod];
                ToolTipML=ENU=Specifies the Shipment Method for the Customer.;
                ApplicationArea=#All;
                SourceExpr=ShipmentMethodJSON;
                OnValidate=BEGIN
                             GraphMgtCustomer.UpdateShipmentMethod(ShipmentMethodJSON,Rec);

                             IF ShipmentMethod.Code <> '' THEN BEGIN
                               IF ShipmentMethod.Code <> "Shipment Method Code" THEN
                                 ERROR(ShipmentMethodValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Shipment Method Code" = '' THEN
                               "Shipment Method Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT ShipmentMethod.GET("Shipment Method Code") THEN
                                 ERROR(ShipmentMethodCodeDoesNotMatchAShipmentMethodErr);

                               "Shipment Method Id" := ShipmentMethod.Id;
                             END;

                             RegisterFieldSet(FIELDNO("Shipment Method Id"));
                             RegisterFieldSet(FIELDNO("Shipment Method Code"));
                           END;

                ODataEDMType=SHIPMENTMETHOD }

    { 7   ;2   ;Field     ;
                Name=paymentMethodId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentMethodId];
                ApplicationArea=#All;
                SourceExpr="Payment Method Id";
                OnValidate=BEGIN
                             IF "Payment Method Id" = BlankGUID THEN
                               "Payment Method Code" := ''
                             ELSE BEGIN
                               PaymentMethod.SETRANGE(Id,"Payment Method Id");
                               IF NOT PaymentMethod.FINDFIRST THEN
                                 ERROR(PaymentMethodIdDoesNotMatchAPaymentMethodErr);

                               "Payment Method Code" := PaymentMethod.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Method Id"));
                             RegisterFieldSet(FIELDNO("Payment Method Code"));
                           END;
                            }

    { 6   ;2   ;Field     ;
                Name=paymentMethod;
                CaptionML=[@@@={Locked};
                           ENU=PaymentMethod];
                ToolTipML=ENU=Specifies the Payment Method for the Vendor.;
                ApplicationArea=#All;
                SourceExpr=PaymentMethodJSON;
                OnValidate=BEGIN
                             GraphMgtCustomer.UpdatePaymentMethod(PaymentMethodJSON,Rec);

                             IF PaymentMethod.Code <> '' THEN BEGIN
                               IF PaymentMethod.Code <> "Payment Method Code" THEN
                                 ERROR(PaymentMethodValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Payment Method Code" = '' THEN
                               "Payment Method Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT PaymentMethod.GET("Payment Method Code") THEN
                                 ERROR(PaymentMethodCodeDoesNotMatchAPaymentMethodErr);

                               "Payment Method Id" := PaymentMethod.Id;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Method Id"));
                             RegisterFieldSet(FIELDNO("Payment Method Code"));
                           END;

                ODataEDMType=PAYMENTMETHOD }

    { 5   ;2   ;Field     ;
                Name=balance;
                CaptionML=[@@@={Locked};
                           ENU=Balance];
                ToolTipML=ENU=Specifies the customer's balance.;
                ApplicationArea=#All;
                SourceExpr=BalanceVar;
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                Name=overdueAmount;
                CaptionML=[@@@={Locked};
                           ENU=overdueAmount];
                ToolTipML=ENU=Specifies the amount that is overdue.;
                ApplicationArea=#All;
                SourceExpr=OverdueAmount;
                Editable=FALSE }

    { 3   ;2   ;Field     ;
                Name=totalSalesExcludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalSalesExcludingTax];
                ToolTipML=ENU=Specifies the total sales excluding tax.;
                ApplicationArea=#All;
                SourceExpr=SalesVar;
                Editable=FALSE }

    { 1   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time" }

    { 4   ;2   ;Part      ;
                Name=Coupons;
                CaptionML=[@@@={Locked};
                           ENU=Coupons];
                ApplicationArea=#All;
                SubPageLink=Customer Id=FIELD(Id);
                PagePartID=Page2815;
                PartType=Page }

  }
  CODE
  {
    VAR
      TempFieldSet@1003 : TEMPORARY Record 2000000041;
      PaymentTerms@1009 : Record 3;
      ShipmentMethod@1008 : Record 10;
      PaymentMethod@1007 : Record 289;
      GraphMgtCustomer@1019 : Codeunit 5471;
      PaymentMethodJSON@1002 : Text;
      PaymentTermsJSON@1000 : Text;
      PostalAddressJSON@1004 : Text;
      ShipmentMethodJSON@1001 : Text;
      TaxAreaDisplayName@1026 : Text;
      OverdueAmount@1005 : Decimal;
      BalanceVar@1020 : Decimal;
      SalesVar@1021 : Decimal;
      BlankGUID@1006 : GUID;
      PaymentTermsValuesDontMatchErr@1018 : TextConst '@@@={Locked};ENU=The payment terms values do not match to a specific Payment Terms.';
      PaymentTermsIdDoesNotMatchAPaymentTermsErr@1017 : TextConst '@@@={Locked};ENU=The "paymentTermsId" does not match to a Payment Terms.';
      PaymentTermsCodeDoesNotMatchAPaymentTermsErr@1016 : TextConst '@@@={Locked};ENU=The "paymentTermsCode" does not match to a Payment Terms.';
      ShipmentMethodValuesDontMatchErr@1015 : TextConst '@@@={Locked};ENU=The shipment method values do not match to a specific Shipment Method.';
      ShipmentMethodIdDoesNotMatchAShipmentMethodErr@1014 : TextConst '@@@={Locked};ENU=The "shipmentMethodId" does not match to a Shipment Method.';
      ShipmentMethodCodeDoesNotMatchAShipmentMethodErr@1013 : TextConst '@@@={Locked};ENU=The "shipmentMethodCode" does not match to a Shipment Method.';
      PaymentMethodValuesDontMatchErr@1012 : TextConst '@@@={Locked};ENU=The payment method values do not match to a specific Payment Method.';
      PaymentMethodIdDoesNotMatchAPaymentMethodErr@1011 : TextConst '@@@={Locked};ENU=The "paymentMethodId" does not match to a Payment Method.';
      PaymentMethodCodeDoesNotMatchAPaymentMethodErr@1010 : TextConst '@@@={Locked};ENU=The "paymentMethodCode" does not match to a Payment Method.';
      CannotUseWithVATErr@1022 : TextConst 'ENU=This field cannot be used with VAT.';
      CannotFindContactErr@1023 : TextConst 'ENU=Cannot find the contact for the given ID.';
      CustomerCreatedFromGraph@1024 : Boolean;
      CannotFindCustomerErr@1025 : TextConst 'ENU=Cannot find the customer for the given ID.';

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      TaxAreaBuffer@1001 : Record 5504;
      GraphMgtCustomer@1000 : Codeunit 5471;
    BEGIN
      PaymentTermsJSON := GraphMgtCustomer.PaymentTermsToJSON("Payment Terms Code");
      ShipmentMethodJSON := GraphMgtCustomer.ShipmentMethodToJSON("Shipment Method Code");
      PaymentMethodJSON := GraphMgtCustomer.PaymentMethodToJSON("Payment Method Code");
      PostalAddressJSON := GraphMgtCustomer.PostalAddressToJSON(Rec);

      SETRANGE("Date Filter",0D,WORKDATE - 1);
      CALCFIELDS("Balance Due (LCY)");
      OverdueAmount := "Balance Due (LCY)";

      SETRANGE("Date Filter",0D,WORKDATE);
      CALCFIELDS("Sales (LCY)","Balance (LCY)");
      BalanceVar := "Balance (LCY)";
      SalesVar := "Sales (LCY)";

      SetContactId;
      TaxAreaDisplayName := TaxAreaBuffer.GetTaxAreaDisplayName("Tax Area ID");
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(Id);
      CLEAR(PaymentTermsJSON);
      CLEAR(TaxAreaDisplayName);
      CLEAR(ShipmentMethodJSON);
      CLEAR(PaymentMethodJSON);
      CLEAR(PostalAddressJSON);
      CLEAR(OverdueAmount);
      CLEAR("Balance (LCY)");
      CLEAR("Sales (LCY)");
      CLEAR("Contact ID");
      TempFieldSet.DELETEALL;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    BEGIN
      IF TempFieldSet.GET(DATABASE::Customer,FieldNo) THEN
        EXIT;

      TempFieldSet.INIT;
      TempFieldSet.TableNo := DATABASE::Customer;
      TempFieldSet.VALIDATE("No.",FieldNo);
      TempFieldSet.INSERT(TRUE);
    END;

    LOCAL PROCEDURE SetContactId@1() : Boolean;
    VAR
      NewContact@1003 : Record 5050;
      GraphIntegrationRecord@1004 : Record 5451;
      IntegrationRecord@1005 : Record 5151;
      GraphIntContact@1000 : Codeunit 5461;
      GraphContactId@1001 : Text[250];
    BEGIN
      IF NOT GraphIntContact.FindGraphContactIdFromCustomer(GraphContactId,Rec,NewContact) THEN
        EXIT(FALSE);

      IntegrationRecord.SETRANGE("Record ID",NewContact.RECORDID);
      IF NOT IntegrationRecord.FINDFIRST THEN
        EXIT;

      IF NOT GraphIntegrationRecord.GET(GraphContactId,IntegrationRecord."Integration ID") THEN
        EXIT(FALSE);

      "Contact Graph Id" := GraphIntegrationRecord."Graph ID";
      EXIT(EVALUATE("Contact ID",GraphIntegrationRecord.XRMId));
    END;

    LOCAL PROCEDURE TranslateContactIdFilterToCustomerNoFilter@13();
    VAR
      GraphIntegrationRecord@1001 : Record 5451;
      NewContact@1002 : Record 5050;
      IntegrationRecord@1003 : Record 5151;
      ContactBusinessRelation@1005 : Record 5054;
      MarketingSetup@1006 : Record 5079;
      ContactIDFilter@1000 : Text;
    BEGIN
      ContactIDFilter := GETFILTER("Contact ID");
      IF ContactIDFilter <> '' THEN BEGIN
        SETFILTER("Contact ID",'');
        GraphIntegrationRecord.SETFILTER(XRMId,ContactIDFilter);
        GraphIntegrationRecord.FINDFIRST;
      END ELSE BEGIN
        ContactIDFilter := GETFILTER("Contact Graph Id");
        IF ContactIDFilter = '' THEN
          EXIT;

        SETFILTER("Contact Graph Id",'');
        GraphIntegrationRecord.SETFILTER("Graph ID",ContactIDFilter);
        GraphIntegrationRecord.FINDFIRST;
      END;

      IF NOT IntegrationRecord.GET(GraphIntegrationRecord."Integration ID") THEN
        EXIT;

      IF NOT NewContact.GET(IntegrationRecord."Record ID") THEN
        EXIT;

      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Customer);
      ContactBusinessRelation.SETRANGE("Contact No.",NewContact."No.");
      IF MarketingSetup.GET THEN
        ContactBusinessRelation.SETRANGE("Business Relation Code",MarketingSetup."Bus. Rel. Code for Customers");

      IF ContactBusinessRelation.FINDFIRST THEN
        SETRANGE("No.",ContactBusinessRelation."No.")
      ELSE
        ERROR(CannotFindCustomerErr);
    END;

    LOCAL PROCEDURE IsUsingVAT@3() : Boolean;
    VAR
      GeneralLedgerSetup@1000 : Record 98;
    BEGIN
      EXIT(GeneralLedgerSetup.UseVat);
    END;

    LOCAL PROCEDURE FindOrCreateCustomer@39(GraphContactID@1003 : Text[250]);
    VAR
      Customer@1001 : Record 18;
      NewContact@1002 : Record 5050;
      GraphIntContact@1000 : Codeunit 5461;
    BEGIN
      IF NOT GraphIntContact.FindOrCreateCustomerFromGraphContactSafe(GraphContactID,Customer,NewContact) THEN
        ERROR(CannotFindContactErr);

      TRANSFERFIELDS(Customer,TRUE);
      CustomerCreatedFromGraph := TRUE;
    END;

    BEGIN
    END.
  }
}

