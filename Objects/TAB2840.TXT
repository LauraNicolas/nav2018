OBJECT Table 2840 Native - Gen. Settings Buffer
{
  OBJECT-PROPERTIES
  {
    Date=06.10.17;
    Time=12:00:00;
    Version List=NAVW111.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Native - Gen. Settings Buffer;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Currency Symbol     ;Text10        ;CaptionML=ENU=Currency Symbol }
    { 3   ;   ;Paypal Email Address;Text250       ;CaptionML=ENU=Paypal Email Address }
    { 4   ;   ;Country/Region Code ;Code10        ;CaptionML=ENU=Country/Region Code }
    { 5   ;   ;Language Locale ID  ;Integer       ;CaptionML=ENU=Language Locale ID }
    { 50  ;   ;Default Tax ID      ;GUID          ;CaptionML=ENU=Default Tax ID;
                                                   Editable=No }
    { 51  ;   ;Defauilt Tax Description;Text50    ;CaptionML=ENU=Defauilt Tax Description;
                                                   Editable=No }
    { 52  ;   ;Default Payment Terms ID;GUID      ;CaptionML=ENU=Default Payment Terms ID;
                                                   Editable=No }
    { 53  ;   ;Def. Pmt. Term Description;Text50  ;CaptionML=ENU=Def. Pmt. Term Description;
                                                   Editable=No }
    { 54  ;   ;Default Payment Method ID;GUID     ;CaptionML=ENU=Default Payment Method ID;
                                                   Editable=No }
    { 55  ;   ;Def. Pmt. Method Description;Text50;CaptionML=ENU=Def. Pmt. Method Description;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RecordMustBeTemporaryErr@1000 : TextConst 'ENU=General Settings Buffer must be used as a temporary record.';

    PROCEDURE LoadRecord@2();
    VAR
      CompanyInformation@1001 : Record 79;
      O365SalesInitialSetup@1003 : Record 2110;
      GeneralLedgerSetup@1004 : Record 98;
      PaymentMethod@1005 : Record 289;
      PaymentTerms@1006 : Record 3;
      TempNativeAPITaxSetup@1007 : TEMPORARY Record 2850;
      UserPersonalization@1008 : Record 2000000073;
      PaypalAccountProxy@1002 : Codeunit 1060;
    BEGIN
      IF NOT ISTEMPORARY THEN
        ERROR(RecordMustBeTemporaryErr);

      DELETEALL;
      CLEAR(Rec);

      CompanyInformation.GET;
      "Country/Region Code" := CompanyInformation."Country/Region Code";

      GeneralLedgerSetup.GET;
      "Currency Symbol" := GeneralLedgerSetup."Local Currency Symbol";

      PaypalAccountProxy.GetPaypalAccount("Paypal Email Address");

      O365SalesInitialSetup.GET;
      IF PaymentTerms.GET(O365SalesInitialSetup."Default Payment Terms Code") THEN BEGIN
        "Default Payment Terms ID" := PaymentTerms.Id;
        "Def. Pmt. Term Description" := PaymentTerms.Description;
      END;

      IF PaymentMethod.GET(O365SalesInitialSetup."Default Payment Method Code") THEN BEGIN
        "Default Payment Method ID" := PaymentMethod.Id;
        "Def. Pmt. Method Description" := PaymentMethod.Description;
      END;

      TempNativeAPITaxSetup.LoadSetupRecords;
      TempNativeAPITaxSetup.SETRANGE(Default,TRUE);
      IF TempNativeAPITaxSetup.FINDFIRST THEN BEGIN
        "Default Tax ID" := TempNativeAPITaxSetup.Id;
        "Defauilt Tax Description" := TempNativeAPITaxSetup.Description;
      END;

      UserPersonalization.GET(USERSECURITYID);
      "Language Locale ID" := UserPersonalization."Language ID";

      INSERT(TRUE);
    END;

    PROCEDURE SaveRecord@1();
    VAR
      PaypalAccountProxy@1000 : Codeunit 1060;
    BEGIN
      IF xRec."Currency Symbol" <> "Currency Symbol" THEN
        UpdateCurrencySymbol;

      IF xRec."Paypal Email Address" <> "Paypal Email Address" THEN
        PaypalAccountProxy.SetPaypalAccount("Paypal Email Address",TRUE);

      IF xRec."Country/Region Code" <> "Country/Region Code" THEN
        UpdateCountryRegionCode;

      IF xRec."Language Locale ID" <> "Language Locale ID" THEN
        UpdateLanguageId;
    END;

    LOCAL PROCEDURE UpdateCurrencySymbol@3();
    VAR
      GeneralLedgerSetup@1000 : Record 98;
    BEGIN
      GeneralLedgerSetup.GET;
      IF "Currency Symbol" <> GeneralLedgerSetup."Local Currency Symbol" THEN BEGIN
        GeneralLedgerSetup.VALIDATE("Local Currency Symbol","Currency Symbol");
        GeneralLedgerSetup.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateCountryRegionCode@6();
    VAR
      CompanyInformation@1000 : Record 79;
    BEGIN
      CompanyInformation.GET;
      IF CompanyInformation."Country/Region Code" <> "Country/Region Code" THEN BEGIN
        CompanyInformation.VALIDATE("Country/Region Code","Country/Region Code");
        CompanyInformation.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateLanguageId@9();
    VAR
      UserPersonalization@1000 : Record 2000000073;
    BEGIN
      UserPersonalization.GET(USERSECURITYID);
      IF "Language Locale ID" <> UserPersonalization."Language ID" THEN BEGIN
        UserPersonalization.VALIDATE("Language ID","Language Locale ID");
        UserPersonalization.MODIFY(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

